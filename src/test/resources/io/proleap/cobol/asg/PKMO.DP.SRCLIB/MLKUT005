       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              MLKUT005.                                       
       AUTHOR.                  LEE YANG.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            10/22/2003.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  INVENTORY STORE-TO-STORE TRANSFER FROM MANIFESTED TRAILER     *        
      *                                                                *        
      *  THIS PROGRAM WILL EXTRACT DATA TO FEED THE INVENTORY          *        
      *  STORE-TO-STORE TRANSFER PROCESS FROM DATA THAT ARE EXTRACTED  *        
      *  FROM MANIFESTED TRAILER.  THE INPUT DATA IS EXTRACTED FROM    *        
      *  THE RECEIVING SYSTEM BY THAT SAME GROUP.                      *        
      *                                                                *        
      *CHANGE HISTORY:                                                 *        
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES          *        
      * ------- ---------- ----------- --------------------------------*        
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION    *        
W26006* W26006  12-22-2004 R. MCDONALD MANIFESTING TRAILERS ARE NOW    *        
W26006*                    SENDING THE QTY AS A NEGATIVE NUMBER        *        
W26006*                    NOW IF THE QUANTITY COMING IN IS POSITIVE   *        
W26006*                    THEN IT IS INVALID.  THIS PROGRAM NEEDS     *        
W26006*                    TO STILL SEND A POSITIVE QTY TO MERCHANDISE *        
W26006*                    LEDGER, SO MULTIPLY IT BY -1.               *        
      * W29145  10-18-2005 V.L. YANG USE PCRD010 COPYBOOK INSTEAD OF   *        
      *                    HARD-CODE IN PROGRAM.  USE UPC-NBR IF THE   *        
      *                    INTERNAL-UPC-ID IS NOT AVAILABLE.           *        
      *                    NEGATE QUANTITY FOR NEGATIVE QUANTITY, IF   *        
      *                    REQUESTED OPTION TO PROCESS NEGATIVE QTY.   *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT STR-REQUEST-CNTL-FILE                                         
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT INTRANSIT-DATA-FILE                                           
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT INVENTORY-TRANSFER-ENTRY                                      
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  STR-REQUEST-CNTL-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STR-REQUEST-CNTL-REC            PIC X(80).                           
                                                                                
       FD  INTRANSIT-DATA-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INTRANSIT-DATA-RECORD           PIC X(100).                          
                                                                                
       FD  INVENTORY-TRANSFER-ENTRY                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 080 CHARACTERS                                       
           DATA RECORD IS INVENTORY-TRANSFER-RECORD.                            
       01  INVENTORY-TRANSFER-RECORD       PIC X(080).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-PRINT-LINE-MAX           PIC S9(04)      VALUE +57            
                               COMP.                                            
           05  PC-MAX-RETRIES              PIC S9(04)      VALUE +2             
                               COMP SYNC.                                       
           05  PC-OPERCO-NBR               PIC X(02)       VALUE '03'.          
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-STR-REQUEST-CNTL  PIC X           VALUE 'N'.           
               88  END-OF-STR-REQUEST-CNTL                 VALUE 'Y'.           
           05  PS-END-OF-INTRANSIT-DATA    PIC X           VALUE 'N'.           
               88  END-OF-INTRANSIT-DATA                   VALUE 'Y'.           
           05  PS-PRICE-FOUND-STATUS       PIC X           VALUE 'N'.           
               88  PS-PRICE-NOT-FOUND                      VALUE 'N'.           
               88  PS-PRICE-FOUND                          VALUE 'Y'.           
           05  PS-UPC-NBR-FOUND-STATUS     PIC X           VALUE 'N'.           
               88  PS-UPC-NBR-NOT-FOUND                    VALUE 'N'.           
               88  PS-UPC-NBR-FOUND                        VALUE 'Y'.           
           05  PS-STORE-FOUND-STATUS       PIC X           VALUE 'N'.           
               88  PS-STORE-NOT-FOUND                      VALUE 'N'.           
               88  PS-STORE-FOUND                          VALUE 'Y'.           
           05  PS-VALIDATE-STORE-STATUS    PIC X(02)       VALUE 'NN'.          
               88  PS-INVALID-STORE-CNTL                   VALUE 'NN'.          
               88  PS-INVALID-FROM-STORE                   VALUE 'NY'.          
               88  PS-INVALID-TO-STORE                     VALUE 'YN'.          
               88  PS-VALID-STORE-CNTL                     VALUE 'YY'.          
           05  FILLER REDEFINES PS-VALIDATE-STORE-STATUS.                       
               10  PS-VALID-FROM-STORE-STATUS                                   
                                           PIC X(01).                           
                   88  PS-VALID-FROM-STORE                 VALUE 'Y'.   .       
               10  PS-VALID-TO-STORE-STATUS                                     
                                           PIC X(01).                           
                   88  PS-VALID-TO-STORE                   VALUE 'Y'.   .       
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** REPORT REQUEST CONTROL REC ***'.                                
                                                                                
       01  RQ-STR-CNTL-AREA.                                                    
           05  RQ-STR-CNTL-TEXT.                                                
               10  RQ-COMMENT-IND              PIC X(01)   VALUE SPACES.        
                   88  RQ-COMMENT                          VALUE '*'.           
               10  RQ-FROM-IND                 PIC X(04)   VALUE SPACES.        
                   88  RQ-FROM                             VALUE 'FROM'.        
               10  FILLER                      PIC X(01)   VALUE SPACES.        
               10  RQ-FROM-STORE               PIC 9(04)   VALUE ZEROES.        
               10  FILLER                      PIC X(01)   VALUE SPACES.        
               10  RQ-TO-IND                   PIC X(02)   VALUE SPACES.        
                   88  RQ-TO                               VALUE 'TO'.          
               10  FILLER                      PIC X(01)   VALUE SPACES.        
               10  RQ-TO-STORE                 PIC 9(04)   VALUE ZEROES.        
               10  FILLER                      PIC X(01)   VALUE SPACES.        
W29145         10  RQ-PROCESS-OPTION           PIC X(08)   VALUE ZEROES.        
                   88  RQ-DEBUG-MODE                       VALUE                
                                                           'DEBUG   '.          
W29145             88  RQ-REVERSE-NEGATIVE                 VALUE                
W29145                                                     'NEGATIVE'.          
           05  FILLER                      PIC X(54)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-RETURN-CODE              PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
               88  PV-RETURN-CODE-INFO                     VALUE +2.            
               88  PV-RETURN-CODE-WARN-0                   VALUE +4.            
               88  PV-RETURN-CODE-WARN-1                   VALUE +8.            
               88  PV-RETURN-CODE-ERR-0                    VALUE +12.           
               88  PV-RETURN-CODE-ERR-1                    VALUE +16.           
           05  PV-RETRY-COUNT              PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-CC        PIC X(02).                           
               10  PV-PROCESSING-YY        PIC X(02).                           
               10  PV-PROC-DSH1            PIC X(01).                           
               10  PV-PROCESSING-MM        PIC X(02).                           
               10  PV-PROC-DSH2            PIC X(01).                           
               10  PV-PROCESSING-DD        PIC X(02).                           
           05  PV-PROCESSING-TIME          PIC X(08)       VALUE SPACES.        
           05  PV-PROCESSING-TIME-RDF REDEFINES PV-PROCESSING-TIME.             
               10  PV-PROCESSING-HR        PIC X(02).                           
               10  PV-PROC-DOT1            PIC X(01).                           
               10  PV-PROCESSING-MIN       PIC X(02).                           
               10  PV-PROC-DOT2            PIC X(01).                           
               10  PV-PROCESSING-SEC       PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
           05  PV-TODAYS-TIME.                                                  
               10  PV-TODAYS-TIME-HR       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-MIN      PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-SEC      PIC 9(02)       VALUE ZEROES.        
               10  FILLER                  PIC 9(02)       VALUE ZEROES.        
           05  PV-TIMESTAMP.                                                    
               10  PV-TIMESTAMP-DATE       PIC X(10)       VALUE SPACES.        
               10  FILLER REDEFINES PV-TIMESTAMP-DATE.                          
                   15  PV-TIMESTAMP-DATE-CCYY                                   
                                           PIC X(04).                           
                   15  FILLER              PIC X(01).                           
                   15  PV-TIMESTAMP-DATE-MM                                     
                                           PIC X(02).                           
                   15  FILLER              PIC X(01).                           
                   15  PV-TIMESTAMP-DATE-DD                                     
                                           PIC X(02).                           
               10  FILLER                  PIC X(01)       VALUE '-'.           
               10  PV-TIMESTAMP-TIME       PIC X(08)       VALUE SPACES.        
               10  FILLER REDEFINES PV-TIMESTAMP-TIME.                          
                   15  PV-TIMESTAMP-TIME-HR                                     
                                           PIC X(02).                           
                   15  FILLER              PIC X(01).                           
                   15  PV-TIMESTAMP-TIME-MIN                                    
                                           PIC X(02).                           
                   15  FILLER              PIC X(01).                           
                   15  PV-TIMESTAMP-TIME-SEC                                    
                                           PIC X(02).                           
               10  FILLER                  PIC X(01)       VALUE '.'.           
               10  FILLER                  PIC X(06)       VALUE                
                   '999999'.                                                    
           05  PV-RECORDS-READ             PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-STORE-ISSUE-COUNT        PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-NO-UNIT-COUNT            PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-NO-PRICE-COUNT           PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-NO-UPC-NBR-COUNT         PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-CLEARANCE-COUNT          PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-GROUP-PRICE-COUNT        PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-RECORDS-ACCEPTED         PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-RECORDS-READ         PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-STORE-ISSUE-COUNT    PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-NO-UNIT-COUNT        PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-NO-PRICE-COUNT       PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-NO-UPC-NBR-COUNT     PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-CLEARANCE-COUNT      PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-GROUP-PRICE-COUNT    PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-RECORDS-ACCEPTED     PIC 9(08)       VALUE ZEROES         
                               COMP-3.                                          
           05  PV-UNT-RTL-AMT-HOLD         PIC S9(7)V9(2)  VALUE ZEROES         
                               COMP-3.                                          
           05  PV-FROM-STORE-NBR-9         PIC 9(04)       VALUE ZEROES.        
           05  PV-FROM-STORE-NBR-X REDEFINES PV-FROM-STORE-NBR-9                
                                           PIC X(04).                           
           05  FILLER REDEFINES PV-FROM-STORE-NBR-9.                            
W26603*******  10  FILLER                  PIC X(01).                           
W26603         10  PV-FROM-STORE-NBR-3X    PIC X(04).                           
           05  PV-TO-STORE-NBR-9           PIC 9(04)       VALUE ZEROES.        
           05  PV-TO-STORE-NBR-X REDEFINES PV-TO-STORE-NBR-9                    
                                           PIC X(04).                           
               88  PV-TO-STORE-NBR-NOT-FOUND               VALUE SPACES.        
           05  FILLER REDEFINES PV-TO-STORE-NBR-9.                              
W26603*****    10  FILLER                  PIC X(01).                           
W26603         10  PV-TO-STORE-NBR-3X      PIC X(04).                           
W29145     05  PV-IN-TRNST-QTY             PIC S9(9)       VALUE ZEROES         
W29145                         COMP.                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING TABLES BEGIN ***'.                                  
                                                                                
       01  PT-STORE-TBL-AREA.                                                   
           05  PT-STORE-TBL-MAX            PIC S9(04)      VALUE +9999          
                               COMP.                                            
           05  PT-STORE-TBL-COUNT          PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PT-STORE-TBL-INDX           PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PT-STORE-TBL OCCURS  9999 TIMES.                                 
               10  PT-FROM-STORE-NBR       PIC 9(04)       VALUE ZEROES.        
               10  PT-TO-STORE-NBR         PIC 9(04)       VALUE ZEROES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** COPYBOOKS AREA BEGIN ***'.                                      
                                                                                
      *01  ML003-TRANSFER-RECORD.                                               
           COPY MLWS003.                                                        
                                                                                
      *01  PC010-PCT-INTRANSIT-REC.                                             
W29145     COPY PCRD010.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** SQL WORK AREA BEGIN ***'.                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  ABEND-4004                                  VALUE +4004.         
           88  ABEND-4013                                  VALUE +4013.         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------* INKCS046
      *    XMT_LOC - MERCHANDISE DOMAIN'S LOCATION TABLE              * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
               INCLUDE XMT00001                                         INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TUPC    - UPC FOUNDATION TABLE                             * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
               INCLUDE TUPC                                             INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TUPCPLS - PRICE LISTING FOR STORE TABLE                    * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
               INCLUDE TUPCPLS                                          INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TMITGPL - GROUP PRICE LISTING TABLE                        * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
               INCLUDE TMITGPL                                          INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      *  MAIN RPOCESSING.                                                       
      ******************************************************************        
       A100-MAIN-MODULE.                                                        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM Z100-READ-STR-REQUEST-CNTL.                                  
           DISPLAY '    CONTROL CARDS FORMAT ARE AS FOLLOW:     '.              
           DISPLAY '         01-01: COMMENT INDICATOR           '.              
           DISPLAY '         02-05: LITERAL "FROM"              '.              
           DISPLAY '         06-06: FILLER                      '.              
           DISPLAY '         07-10: 4 DIGIT FROM STORE NUMBER   '.              
           DISPLAY '         11-11: FILLER                      '.              
           DISPLAY '         12-13: LITERAL "TO"                '.              
           DISPLAY '         14-14: FILLER                      '.              
           DISPLAY '         15-18: 4 DIGIT TO STORE NUMBER     '.              
W29145     DISPLAY '         19-19: FILLER                      '.              
           DISPLAY '         20-27: "DEBUG" FOR SHOWING MORE EXEC INFO'.        
W29145     DISPLAY '                "NEGATIVE" FOR NEGATING QTY < ZERO'.        
           DISPLAY '    EXAMPLE:                                '.              
           DISPLAY '      ----+----1----+----2----+----3----+'.                 
           DISPLAY '      *FROM XXXX TO XXXX DEBUG              '.              
           DISPLAY 'CONTROL CARDS:'.                                            
           DISPLAY '  ----+----1----+----2----+----3----+----4'.                
           PERFORM B200-PROCESS-CNTL                                            
               UNTIL END-OF-STR-REQUEST-CNTL.                                   
           DISPLAY ' '.                                                         
           MOVE PT-STORE-TBL-INDX TO PT-STORE-TBL-COUNT.                        
                                                                                
           IF PT-STORE-TBL-COUNT > 0                                            
               PERFORM Z200-READ-INTRANSIT-DATA                                 
               PERFORM B300-PROCESS-INTRANSIT-STORE                             
                   UNTIL END-OF-INTRANSIT-DATA                                  
           ELSE                                                                 
               SET PV-RETURN-CODE-INFO TO TRUE                                  
           END-IF.                                                              
                                                                                
           PERFORM B400-FINALIZE.                                               
                                                                                
           IF RQ-DEBUG-MODE                                                     
               MOVE PV-RETURN-CODE TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       B100-HOUSEKEEPING.                                                       
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
                                                                                
           MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
           MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
           MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
                                                                                
           MOVE '-' TO PV-PROC-DSH1.                                            
           MOVE '-' TO PV-PROC-DSH2.                                            
                                                                                
           IF PV-TODAYS-DATE-YY > '80'                                          
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC.                                   
                                                                                
           MOVE PV-TODAYS-TIME-HR  TO PV-PROCESSING-HR.                         
           MOVE PV-TODAYS-TIME-MIN TO PV-PROCESSING-MIN.                        
           MOVE PV-TODAYS-TIME-SEC TO PV-PROCESSING-SEC.                        
                                                                                
           MOVE '.' TO PV-PROC-DOT1.                                            
           MOVE '.' TO PV-PROC-DOT2.                                            
                                                                                
           MOVE PV-PROCESSING-DATE TO PV-TIMESTAMP-DATE.                        
           MOVE PV-PROCESSING-TIME TO PV-TIMESTAMP-TIME.                        
                                                                                
           OPEN INPUT  STR-REQUEST-CNTL-FILE                                    
                       INTRANSIT-DATA-FILE                                      
                OUTPUT INVENTORY-TRANSFER-ENTRY.                                
                                                                                
           DISPLAY 'PROCESS DATE ' PV-PROCESSING-DATE.                          
                                                                                
                                                                                
       B200-PROCESS-CNTL.                                                       
                                                                                
           IF RQ-COMMENT                                                        
               DISPLAY '  ' RQ-STR-CNTL-AREA                                    
           ELSE                                                                 
               PERFORM C100-VALIDATE-STORE-CNTL                                 
               IF PS-VALID-STORE-CNTL                                           
                   PERFORM C200-LOAD-STORE-CNTL                                 
               ELSE                                                             
                   SET PV-RETURN-CODE-INFO TO TRUE                              
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM Z100-READ-STR-REQUEST-CNTL.                                  
                                                                                
                                                                                
       B300-PROCESS-INTRANSIT-STORE.                                            
                                                                                
           MOVE ZEROES        TO PV-RECORDS-READ                                
                                 PV-STORE-ISSUE-COUNT                           
                                 PV-NO-UNIT-COUNT                               
                                 PV-NO-PRICE-COUNT                              
                                 PV-NO-UPC-NBR-COUNT                            
                                 PV-CLEARANCE-COUNT                             
                                 PV-GROUP-PRICE-COUNT                           
                                 PV-RECORDS-ACCEPTED.                           
                                                                                
           MOVE PC010-STR-NBR TO PV-FROM-STORE-NBR-9.                           
           PERFORM C500-SEARCH-REQUEST-CNTL.                                    
           IF PV-TO-STORE-NBR-NOT-FOUND                                         
               DISPLAY ' STORE ' PV-FROM-STORE-NBR-X                            
                       ' IS NOT FOUND ON THE REQUESTED INPUT CONTROL.'          
               DISPLAY '  DATA FOR THE STORE WILL BE IGNORED.'                  
               SET PV-RETURN-CODE-WARN-0 TO TRUE                                
           ELSE                                                                 
               MOVE PC010-STR-NBR       TO XMT00001-LOC-NBR                     
               PERFORM Z600-GET-STORE-INFO                                      
               IF PS-STORE-FOUND                                                
                   DISPLAY '  PROCESSING: ' PC010-STR-NBR                       
                           ' - ' XMT00001-LOC-NM                                
                           ' ...TO... ' PV-TO-STORE-NBR-9                       
               ELSE                                                             
                   SET PV-TO-STORE-NBR-NOT-FOUND TO TRUE                        
                   SET PV-RETURN-CODE-WARN-1 TO TRUE                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
      * PERFORM THIS PARAGRAPH REGARDLESS OF WHETHER STORE FOUND.               
      * THIS PARAGRAPH WILL READ AND BYPASS RECORDS THAT ARE NOT                
      * IN THE CONTROL CARD.                                                    
           PERFORM C600-PROCESS-INTRANSIT-REC                                   
               UNTIL END-OF-INTRANSIT-DATA                                      
                   OR PC010-STR-NBR NOT = PV-FROM-STORE-NBR-9.                  
                                                                                
           PERFORM C700-SHOW-STORE-STATS.                                       
                                                                                
           ADD PV-RECORDS-READ       TO PV-TOT-RECORDS-READ.                    
           ADD PV-STORE-ISSUE-COUNT  TO PV-TOT-STORE-ISSUE-COUNT.               
           ADD PV-NO-UNIT-COUNT      TO PV-TOT-NO-UNIT-COUNT.                   
           ADD PV-NO-PRICE-COUNT     TO PV-TOT-NO-PRICE-COUNT.                  
           ADD PV-NO-UPC-NBR-COUNT   TO PV-TOT-NO-UPC-NBR-COUNT.                
           ADD PV-CLEARANCE-COUNT    TO PV-TOT-CLEARANCE-COUNT.                 
           ADD PV-GROUP-PRICE-COUNT  TO PV-TOT-GROUP-PRICE-COUNT.               
           ADD PV-RECORDS-ACCEPTED   TO PV-TOT-RECORDS-ACCEPTED.                
                                                                                
                                                                                
       B400-FINALIZE.                                                           
                                                                                
           MOVE PV-TOT-RECORDS-READ       TO PV-RECORDS-READ.                   
           MOVE PV-TOT-STORE-ISSUE-COUNT  TO PV-STORE-ISSUE-COUNT.              
           MOVE PV-TOT-NO-UNIT-COUNT      TO PV-NO-UNIT-COUNT.                  
           MOVE PV-TOT-NO-PRICE-COUNT     TO PV-NO-PRICE-COUNT.                 
           MOVE PV-TOT-NO-UPC-NBR-COUNT   TO PV-NO-UPC-NBR-COUNT.               
           MOVE PV-TOT-CLEARANCE-COUNT    TO PV-CLEARANCE-COUNT.                
           MOVE PV-TOT-GROUP-PRICE-COUNT  TO PV-GROUP-PRICE-COUNT.              
           MOVE PV-TOT-RECORDS-ACCEPTED   TO PV-RECORDS-ACCEPTED.               
                                                                                
           DISPLAY ' GRAND TOTALS:'.                                            
           PERFORM C700-SHOW-STORE-STATS.                                       
                                                                                
           CLOSE   STR-REQUEST-CNTL-FILE                                        
                   INTRANSIT-DATA-FILE                                          
                   INVENTORY-TRANSFER-ENTRY.                                    
                                                                                
                                                                                
       C100-VALIDATE-STORE-CNTL.                                                
                                                                                
           SET PS-INVALID-STORE-CNTL TO TRUE.                                   
                                                                                
           IF RQ-FROM-STORE NUMERIC                                             
               AND RQ-FROM                                                      
               MOVE RQ-FROM-STORE TO PV-FROM-STORE-NBR-X                        
               MOVE PV-FROM-STORE-NBR-9 TO XMT00001-LOC-NBR                     
               PERFORM Z600-GET-STORE-INFO                                      
               IF PS-STORE-FOUND                                                
                   SET PS-VALID-FROM-STORE TO TRUE                              
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF RQ-TO-STORE NUMERIC                                               
               AND RQ-TO                                                        
               MOVE RQ-TO-STORE TO PV-TO-STORE-NBR-X                            
               MOVE PV-TO-STORE-NBR-9 TO XMT00001-LOC-NBR                       
               PERFORM Z600-GET-STORE-INFO                                      
               IF PS-STORE-FOUND                                                
                   SET PS-VALID-TO-STORE   TO TRUE                              
               END-IF                                                           
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
               WHEN PS-INVALID-STORE-CNTL                                       
                   DISPLAY '  ' RQ-STR-CNTL-TEXT                                
                           ' *** INVALID FROM AND TO ***'                       
               WHEN PS-INVALID-FROM-STORE                                       
                   DISPLAY '  ' RQ-STR-CNTL-TEXT                                
                           ' *** INVALID FROM ***'                              
               WHEN PS-INVALID-TO-STORE                                         
                   DISPLAY '  ' RQ-STR-CNTL-TEXT                                
                           ' *** INVALID TO ***'                                
               WHEN PS-VALID-STORE-CNTL                                         
                   IF RQ-FROM-STORE = RQ-TO-STORE                               
                       SET PS-INVALID-STORE-CNTL TO TRUE                        
                       DISPLAY '  ' RQ-STR-CNTL-TEXT                            
                               ' *** INVALID SAME STORE ***'                    
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       C200-LOAD-STORE-CNTL.                                                    
                                                                                
           DISPLAY '  ' RQ-STR-CNTL-TEXT                                        
                   ' *** ACCEPTED ***'.                                         
           ADD 1 TO PT-STORE-TBL-INDX.                                          
                                                                                
           IF PT-STORE-TBL-INDX > PT-STORE-TBL-MAX                              
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PGM = MLKUT005 **'                                   
               DISPLAY '** STORE TABLE IS TOO SMALL'                            
               DISPLAY '** INCREASE STORE TABLE SIZE'                           
               SET ABEND-4004         TO TRUE                                   
               PERFORM Z999-ABEND                                               
           ELSE                                                                 
               MOVE RQ-FROM-STORE                                               
                   TO PT-FROM-STORE-NBR     (PT-STORE-TBL-INDX)                 
               MOVE RQ-TO-STORE                                                 
                   TO PT-TO-STORE-NBR       (PT-STORE-TBL-INDX)                 
           END-IF.                                                              
                                                                                
                                                                                
       C500-SEARCH-REQUEST-CNTL.                                                
                                                                                
           SET PV-TO-STORE-NBR-NOT-FOUND TO TRUE.                               
           MOVE 1 TO PT-STORE-TBL-INDX.                                         
           PERFORM                                                              
               UNTIL PT-STORE-TBL-INDX > PT-STORE-TBL-COUNT                     
                   OR PT-FROM-STORE-NBR (PT-STORE-TBL-INDX)                     
                       = PV-FROM-STORE-NBR-9                                    
               ADD 1 TO PT-STORE-TBL-INDX                                       
           END-PERFORM.                                                         
           IF PT-STORE-TBL-INDX <= PT-STORE-TBL-COUNT                           
               MOVE PT-TO-STORE-NBR (PT-STORE-TBL-INDX)                         
                   TO PV-TO-STORE-NBR-9                                         
           END-IF.                                                              
                                                                                
                                                                                
       C600-PROCESS-INTRANSIT-REC.                                              
                                                                                
           IF PV-TO-STORE-NBR-NOT-FOUND                                         
               ADD 1 TO PV-STORE-ISSUE-COUNT                                    
           ELSE                                                                 
W29145         IF PC010-INTR-UPC-ID = ZEROES                                    
W29145             AND PC010-UPC-NBR NOT = ZEROES                               
W29145             MOVE PC010-UPC-NBR     TO UPC-UPC-NBR                        
W29145             PERFORM Z750-GET-UPC-ID                                      
W29145             MOVE UPC-UPC-ID        TO UPCPLS-UPC-ID                      
W29145         ELSE                                                             
                   MOVE PC010-INTR-UPC-ID TO UPCPLS-UPC-ID                      
                                             UPC-UPC-ID                         
W29145             PERFORM Z700-GET-UPC-NBR                                     
W29145         END-IF                                                           
               IF PS-UPC-NBR-FOUND                                              
                   MOVE ZEROES        TO UPCPLS-STR-NBR                         
                   PERFORM Z800-GET-PRICE                                       
                   IF UPCPLS-UPC-STAT-CDE = '25'                                
                       OR UPCPLS-UPC-STAT-CDE = '30'                            
DEBUG                  IF RQ-DEBUG-MODE                                         
DEBUG                      DISPLAY ' * CLEARANCE UPC-ID: ' UPCPLS-UPC-ID        
DEBUG                              ' STATUS: ' UPCPLS-UPC-STAT-CDE              
DEBUG                              ' FROM: ' PV-FROM-STORE-NBR-9                
DEBUG                              ' TO: ' PV-TO-STORE-NBR-9                    
DEBUG                  END-IF                                                   
                       ADD 1 TO PV-CLEARANCE-COUNT                              
                       MOVE PC010-STR-NBR     TO UPCPLS-STR-NBR                 
                       PERFORM Z800-GET-PRICE                                   
                       IF PS-PRICE-NOT-FOUND                                    
      *  USE CORPORATE PRICE                                                    
                           SET PS-PRICE-FOUND TO TRUE                           
                       END-IF                                                   
                   END-IF                                                       
                   IF PS-PRICE-FOUND                                            
W29145                 IF PC010-IN-TRNST-QTY > 0                                
W29145                     OR RQ-REVERSE-NEGATIVE                               
                           PERFORM D100-BUILD-TRANSFER                          
                       ELSE                                                     
                           SET PV-RETURN-CODE-ERR-0 TO TRUE                     
                           DISPLAY ' *** UNIT/QTY IS ZERO OR LESS'              
                                   ' FOR UPC-ID: '                              
                                   UPC-UPC-ID                                   
                                   ' INTERNAL UPC: ' PC010-INTR-UPC-ID          
W29145                             ' UPC NBR: ' PC010-UPC-NBR                   
                           ADD 1 TO PV-NO-UNIT-COUNT                            
                       END-IF                                                   
                   ELSE                                                         
                       SET PV-RETURN-CODE-ERR-0 TO TRUE                         
                       ADD 1 TO PV-NO-PRICE-COUNT                               
                   END-IF                                                       
               ELSE                                                             
                   DISPLAY ' *** NO UPC NBR FOUND FOR UPC-ID: '                 
                           UPC-UPC-ID                                           
                           ' INTERNAL UPC: ' PC010-INTR-UPC-ID                  
W29145                     ' UPC NBR: ' PC010-UPC-NBR                           
                   SET PV-RETURN-CODE-ERR-0 TO TRUE                             
                   ADD 1 TO PV-NO-UPC-NBR-COUNT                                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM Z200-READ-INTRANSIT-DATA.                                    
                                                                                
                                                                                
       C700-SHOW-STORE-STATS.                                                   
                                                                                
           DISPLAY '   RECORDS READ:       ' PV-RECORDS-READ.                   
           DISPLAY '   STORE ISSUE COUNT:  ' PV-STORE-ISSUE-COUNT.              
           DISPLAY '   NO UNIT COUNT:      ' PV-NO-UNIT-COUNT.                  
           DISPLAY '   NO PRICE COUNT:     ' PV-NO-PRICE-COUNT.                 
           DISPLAY '   NO UPC NBR COUNT:   ' PV-NO-UPC-NBR-COUNT.               
           DISPLAY '   RECORDS ACCEPTED:   ' PV-RECORDS-ACCEPTED.               
           DISPLAY '     CLEARANCE COUNT:    ' PV-CLEARANCE-COUNT.              
           DISPLAY '     GROUP PRICE COUNT:  ' PV-GROUP-PRICE-COUNT.            
           DISPLAY ' '.                                                         
                                                                                
                                                                                
       D100-BUILD-TRANSFER.                                                     
                                                                                
           MOVE SPACES                TO ML003-TRANSFER-RECORD.                 
                                                                                
           MOVE PV-PROCESSING-MM      TO ML003-MM.                              
           MOVE PV-PROCESSING-DD      TO ML003-DD.                              
           MOVE PV-PROCESSING-YY      TO ML003-YY.                              
                                                                                
           MOVE PV-FROM-STORE-NBR-3X  TO ML003-FROM-STORE.                      
           MOVE PV-TO-STORE-NBR-3X    TO ML003-TO-STORE.                        
                                                                                
           MOVE UPC-UPC-NBR           TO ML003-UPC.                             
                                                                                
           MOVE PV-UNT-RTL-AMT-HOLD   TO ML003-RETAIL-PRICE.                    
           IF RQ-REVERSE-NEGATIVE                                               
               AND PC010-IN-TRNST-QTY < ZEROES                                  
W29145         COMPUTE PV-IN-TRNST-QTY = PC010-IN-TRNST-QTY * -1                
W29145         MOVE PV-IN-TRNST-QTY       TO ML003-UNITS                        
W29145     ELSE                                                                 
W29145         MOVE PC010-IN-TRNST-QTY    TO ML003-UNITS                        
W29145     END-IF.                                                              
                                                                                
           PERFORM Z300-WRITE-TRANSFER-REC.                                     
                                                                                
                                                                                
       Z100-READ-STR-REQUEST-CNTL.                                              
                                                                                
           READ STR-REQUEST-CNTL-FILE                                           
               INTO RQ-STR-CNTL-AREA                                            
               AT END SET END-OF-STR-REQUEST-CNTL TO TRUE.                      
                                                                                
                                                                                
       Z200-READ-INTRANSIT-DATA.                                                
                                                                                
           ADD 1 TO PV-RECORDS-READ.                                            
           READ INTRANSIT-DATA-FILE                                             
               INTO PC010-PCT-INTRANSIT-REC                                     
               AT END SET END-OF-INTRANSIT-DATA   TO TRUE.                      
                                                                                
                                                                                
       Z300-WRITE-TRANSFER-REC.                                                 
                                                                                
           ADD 1 TO PV-RECORDS-ACCEPTED.                                        
           WRITE INVENTORY-TRANSFER-RECORD FROM                                 
               ML003-TRANSFER-RECORD.                                           
                                                                                
                                                                                
       Z600-GET-STORE-INFO.                                                     
                                                                                
           SET PS-STORE-NOT-FOUND TO TRUE.                                      
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
               UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                          
                   OR (SQLCODE = 0)                                             
                   OR (SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   SELECT  LOC_NM                                               
                   INTO   :XMT00001-LOC-NM                                      
                   FROM                                                         
                         XMT_LOC                                                
                   WHERE                                                        
                         LOC_NBR            = :XMT00001-LOC-NBR                 
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                        SET PS-STORE-FOUND  TO TRUE                             
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       DISPLAY '***PARAGRAPH - Z600-GET-STORE-INFO ***'         
                       DISPLAY ' STORE: ' XMT00001-LOC-NBR                      
                       PERFORM Z900-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               DISPLAY '*** MAX RETRIES - Z600-GET-STORE-INFO ***'              
               DISPLAY ' STORE: ' XMT00001-LOC-NBR                              
               PERFORM Z900-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
       Z700-GET-UPC-NBR.                                                        
                                                                                
           MOVE PV-TIMESTAMP-DATE TO UPC-EFF-BEG-DTE.                           
           SET PS-UPC-NBR-NOT-FOUND TO TRUE.                                    
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
               UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                          
                   OR (SQLCODE = 0)                                             
                   OR (SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   SELECT  UPC_NBR                                              
                   INTO   :UPC-UPC-NBR                                          
                   FROM                                                         
                         TUPC                                                   
                   WHERE                                                        
                         UPC_ID             = :UPC-UPC-ID                       
                     AND EFF_BEG_DTE       <= :UPC-EFF-BEG-DTE                  
                     AND (EFF_END_DTE      >= :UPC-EFF-BEG-DTE                  
                          OR EFF_END_DTE IS NULL)                               
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       SET PS-UPC-NBR-FOUND  TO TRUE                            
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       DISPLAY '***PARAGRAPH - Z700-GET-UPC-NBR ***'            
                       DISPLAY ' UPC-ID: ' UPC-UPC-ID                           
                               ' EFF DATE: ' UPC-EFF-BEG-DTE                    
                       PERFORM Z900-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               DISPLAY '*** MAX RETRIES - Z700-GET-UPC-NBR ***'                 
               DISPLAY ' UPC-ID: ' UPC-UPC-ID                                   
                       ' EFF DATE: ' UPC-EFF-BEG-DTE                            
               PERFORM Z900-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
W29145 Z750-GET-UPC-ID.                                                         
W29145                                                                          
W29145     MOVE PV-TIMESTAMP-DATE TO UPC-EFF-BEG-DTE.                           
W29145     SET PS-UPC-NBR-NOT-FOUND TO TRUE.                                    
W29145                                                                          
W29145     PERFORM                                                              
W29145         WITH TEST AFTER                                                  
W29145         VARYING PV-RETRY-COUNT FROM 1 BY 1                               
W29145         UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                          
W29145             OR (SQLCODE = 0)                                             
W29145             OR (SQLCODE = +100)                                          
W29145                                                                          
W29145         EXEC SQL                                                         
W29145             SELECT  INTR_UPC_ID                                          
W29145             INTO   :UPC-INTR-UPC-ID                                      
W29145             FROM                                                         
W29145                   TUPC                                                   
W29145             WHERE                                                        
W29145                   UPC_NBR            = :UPC-UPC-NBR                      
W29145               AND EFF_BEG_DTE       <= :UPC-EFF-BEG-DTE                  
W29145               AND (EFF_END_DTE      >= :UPC-EFF-BEG-DTE                  
W29145                    OR EFF_END_DTE IS NULL)                               
W29145         END-EXEC                                                         
W29145                                                                          
W29145         EVALUATE TRUE                                                    
W29145             WHEN SQLCODE = 0                                             
W29145                 SET PS-UPC-NBR-FOUND  TO TRUE                            
W29145                 MOVE UPC-INTR-UPC-ID   TO UPC-UPC-ID                     
W29145             WHEN SQLCODE = -904                                          
W29145             WHEN SQLCODE = -911                                          
W29145             WHEN SQLCODE = +100                                          
W29145                 MOVE ZEROES            TO UPC-UPC-ID                     
W29145             WHEN OTHER                                                   
W29145                 DISPLAY '***PARAGRAPH - Z750-GET-UPC-ID ***'             
W29145                 DISPLAY ' UPC-NBR: ' UPC-UPC-NBR                         
W29145                         ' EFF DATE: ' UPC-EFF-BEG-DTE                    
W29145                 PERFORM Z900-SQL-ERROR                                   
W29145         END-EVALUATE                                                     
W29145     END-PERFORM.                                                         
W29145                                                                          
W29145     IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
W29145         DISPLAY '*** MAX RETRIES - Z750-GET-UPC-ID ***'                  
W29145         DISPLAY ' UPC-NBR: ' UPC-UPC-NBR                                 
W29145                 ' EFF DATE: ' UPC-EFF-BEG-DTE                            
W29145         PERFORM Z900-SQL-ERROR                                           
W29145     END-IF.                                                              
                                                                                
                                                                                
       Z800-GET-PRICE.                                                          
                                                                                
           MOVE PV-TIMESTAMP      TO UPCPLS-EFF-BEG-TMST.                       
           SET PS-PRICE-NOT-FOUND TO TRUE.                                      
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
               UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                          
                   OR (SQLCODE = 0)                                             
                   OR (SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   SELECT  UNT_RTL_AMT                                          
                          ,UPC_STAT_CDE                                         
                          ,MEITGP_NBR                                           
                          ,UPC_STAT_CHG_DTE                                     
                          ,UNT_RTL_CHG_DTE                                      
                   INTO   :UPCPLS-UNT-RTL-AMT                                   
                         ,:UPCPLS-UPC-STAT-CDE                                  
                         ,:UPCPLS-MEITGP-NBR                                    
                         ,:UPCPLS-UPC-STAT-CHG-DTE                              
                         ,:UPCPLS-UNT-RTL-CHG-DTE                               
                   FROM                                                         
                         TUPCPLS                                                
                   WHERE                                                        
                         UPC_ID             = :UPCPLS-UPC-ID                    
                     AND OPERCO_NBR         = :PC-OPERCO-NBR                    
                     AND STR_NBR            = :UPCPLS-STR-NBR                   
                     AND EFF_BEG_TMST      <= :UPCPLS-EFF-BEG-TMST              
                     AND (EFF_END_TMST     >= :UPCPLS-EFF-BEG-TMST              
                          OR EFF_END_TMST IS NULL)                              
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       SET PS-PRICE-FOUND    TO TRUE                            
                       IF UPCPLS-MEITGP-NBR NOT = ZEROES                        
                           PERFORM Z850-GET-GROUP-PRICE                         
                           ADD 1 TO PV-GROUP-PRICE-COUNT                        
                       END-IF                                                   
                       MOVE UPCPLS-UNT-RTL-AMT TO PV-UNT-RTL-AMT-HOLD           
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       DISPLAY '***PARAGRAPH - Z800-GET-PRICE ***'              
                       DISPLAY ' UPC-ID: ' UPCPLS-UPC-ID                        
                               ' STORE:  ' UPCPLS-STR-NBR                       
                               ' EFF TIMESTAMP: ' UPCPLS-EFF-BEG-TMST           
                       PERFORM Z900-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               DISPLAY '*** MAX RETRIES - Z800-GET-PRICE ***'                   
               DISPLAY ' UPC-ID: ' UPCPLS-UPC-ID                                
                       ' STORE:  ' UPCPLS-STR-NBR                               
                       ' EFF TIMESTAMP: ' UPCPLS-EFF-BEG-TMST                   
               PERFORM Z900-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
       Z850-GET-GROUP-PRICE.                                                    
                                                                                
DEBUG      IF RQ-DEBUG-MODE                                                     
DEBUG          DISPLAY ' *** LOOKING UP GROUP PRICING ***'                      
DEBUG                  ' FROM STORE: ' PV-FROM-STORE-NBR-X                      
DEBUG                  ' TO STORE: ' PV-TO-STORE-NBR-X                          
DEBUG                  ' UPC:   ' UPC-UPC-NBR                                   
DEBUG      END-IF.                                                              
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
               UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                          
                   OR (SQLCODE = 0)                                             
                   OR (SQLCODE = +100)                                          
                                                                                
               EXEC SQL                                                         
                   SELECT  MITGPL_QTY                                           
                          ,GP_AMT                                               
                   INTO    :MITGPL-MITGPL-QTY                                   
                          ,:MITGPL-GP-AMT                                       
                   FROM   TMITGPL                                               
                   WHERE  OPERCO_NBR    = :PC-OPERCO-NBR                        
                     AND  MEITGP_NBR    = :UPCPLS-MEITGP-NBR                    
                     AND  ADLOC_ID      = '   '                                 
                     AND  (CURRENT DATE BETWEEN                                 
                           BEG_DTE AND END_DTE)                                 
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                        COMPUTE UPCPLS-UNT-RTL-AMT ROUNDED =                    
                              (MITGPL-GP-AMT /                                  
                               MITGPL-MITGPL-QTY )                              
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       DISPLAY '***PARAGRAPH - Z850-GET-GROUP-PRICE ***'        
                       DISPLAY ' UPC-ID: ' UPCPLS-UPC-ID                        
                               ' STORE:  ' UPCPLS-STR-NBR                       
                               ' GROUP PRICE ID: ' UPCPLS-MEITGP-NBR            
                       PERFORM Z900-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               DISPLAY '*** MAX RETRIES - Z850-GET-GROUP-PRICE ***'             
               DISPLAY ' UPC-ID: ' UPCPLS-UPC-ID                                
                       ' STORE:  ' UPCPLS-STR-NBR                               
                       ' GROUP PRICE ID: ' UPCPLS-MEITGP-NBR                    
               PERFORM Z900-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                                    
      ******************************************************************        
       Z900-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013            TO TRUE.                                   
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  MLKUT005'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM Z999-ABEND.                                                  
                                                                                
                                                                                
       Z999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
