      ******************************************************************00010007
       IDENTIFICATION DIVISION.                                         00020007
      ******************************************************************00030007
       PROGRAM-ID.    SUKUT042.                                         00050007
       AUTHOR.        DEANNA BRANTNER.                                  00060007
       INSTALLATION.  KOHLS.                                            00070007
       DATE-WRITTEN   OCTOBER, 2000.                                    00080007
       DATE-COMPILED.                                                   00090007
      ******************************************************************00110007
      *  THIS PROGRAM CREATES AN EXTRACT FILE FROM THE CROSS REFERENCE  00120007
      *  TABLE (TSULBXF) WHICH WILL BE USED TO REMOVE ROWS IN A LATER   00130007
      *  STEP.                                                          00130007
      ******************************************************************00140007
      ******************************************************************00160007
       ENVIRONMENT DIVISION.                                            00170007
       CONFIGURATION SECTION.                                           00200007
       SOURCE-COMPUTER.        IBM-3090.                                00220007
       OBJECT-COMPUTER.        IBM-3090.                                00230007
                                                                        00240007
       INPUT-OUTPUT SECTION.                                            00250007
       FILE-CONTROL.                                                    00270007
                                                                        00290007
           SELECT SHIPMENT-LOC-FILE                                     00300007
               ASSIGN TO INP01.                                         00310007
                                                                        00320007
           SELECT XREF-OUT-FILE                                         00330007
               ASSIGN TO OUT02.                                         00340007
                                                                        00350007
      ******************************************************************00380007
       DATA DIVISION.                                                   00390007
       FILE SECTION.                                                    00420007
                                                                        00430007
       FD  SHIPMENT-LOC-FILE                                            00440007
           RECORDING MODE IS F                                          00450007
           LABEL RECORDS ARE STANDARD                                   00460007
           BLOCK CONTAINS 0 RECORDS.                                    00470007
                                                                        00480007
       01  SHIPMENT-LOC-REC          PIC  X(56).                        00490007
                                                                        00500007
       FD  XREF-OUT-FILE                                                00510007
           RECORDING MODE IS F                                          00520007
           LABEL RECORDS ARE STANDARD                                   00530007
           BLOCK CONTAINS 0 RECORDS.                                    00540007
                                                                        00550007
       01  XREF-OUT-REC             PIC  X(088).                        00560007
                                                                        00570007
      ******************************************************************00650007
       WORKING-STORAGE SECTION.                                         00660007
      ******************************************************************00670007
      ******************************************                        00790007
      *  DB2 FORMATTED MESSAGE AREA                                     00800007
      ******************************************                        00810007
           COPY DPWS004.                                                00820007
                                                                        00850007
      ******************************************                        00860007
      *  COMMUNICATIONS AREA FOR DB2                                    00870007
      ******************************************                        00880007
           EXEC SQL                                                     00890007
                INCLUDE SQLCA                                           00900007
           END-EXEC.                                                    00910007
                                                                        00920007
      ***********************************************                   01020007
      *  DCLGEN FOR THE SHIPMENT UNIT LOCATION TABLE                    01030007
      ***********************************************                   01050007
           EXEC SQL                                                     01060007
                INCLUDE TSHUNLO                                         01070007
           END-EXEC.                                                    01080007
                                                                        01090007
      ***********************************************                   01100007
      *  DCLGEN FOR THE CROSS REFERENCE TABLE                           01110007
      ***********************************************                   01130007
           EXEC SQL                                                     01140007
                INCLUDE TSULBXF                                         01150007
           END-EXEC.                                                    01160007
                                                                        01170007
       01  ABEND-CODE                      PIC S9(04)    VALUE +0       01180007
                                                         COMP SYNC.     01190007
                                                                        01200007
       01  PS-PROGRAM-SWITCHES.                                         01210007
           05  PS-EOF-XREF-CSR-SW  PIC  X(01)            VALUE 'N'.     01220007
               88  PS-EOF-XREF-CSR                       VALUE 'Y'.     01230007
               88  PS-MORE-XREF-CSR                      VALUE 'N'.     01240007
           05  PS-EOF-SHIPMENT-LOC-FILE-SW PIC  X(01)    VALUE 'N'.     01220007
               88  PS-EOF-SHIPMENT-LOC-FILE              VALUE 'Y'.     01230007
               88  PS-MORE-SHIPMENT-LOC-FILE             VALUE 'N'.     01240007
           05  PS-SHIPMENT-LOC-RECORDS-EXIST  PIC X(01)  VALUE 'N'.     01220007
               88  PS-SHIPMENT-LOC-RECORDS-YES           VALUE 'Y'.     01230007
               88  PS-SHIPMENT-LOC-RECORDS-NO            VALUE 'N'.     01240007
                                                                        01290007
       01  PC-PROGRAM-CONSTANTS.                                        01300007
           05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100     01310007
                                                         COMP SYNC.     01320007
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE          01330007
               'SUKUT042'.                                              01340007
           05  PC-CURRENT-OPERATING-COMPANY                             01350007
                                           PIC  X(02)    VALUE '03'.    01360007
                                                                        01400007
       01  PV-PROGRAM-VARIABLES.                                        01410007
           05  PV-ITEM-NBR                 PIC  9(15)    VALUE ZERO.    01420007
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.  01430007
           05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.  01440007
           05  PV-OPEN-COUNTER             PIC S9(04)    VALUE ZERO     01450007
                                                         COMP SYNC.     01460007
           05  PV-CLOSE-COUNTER            PIC S9(04)    VALUE ZERO     01470007
                                                         COMP SYNC.     01480007
           05  PV-FETCH-COUNTER            PIC S9(04)    VALUE ZERO     01490007
                                                         COMP SYNC.     01500007
           05  PV-WRITE-COUNTER            PIC S9(09)    VALUE ZERO     01510007
                                                         COMP SYNC.     01520007
           05  PV-WRITE2-COUNTER           PIC S9(09)    VALUE ZERO     01530007
                                                         COMP SYNC.     01540007
           05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.             01550007
                                                                        01560007
           05  PV-DB2-COUNT                PIC S9(09)    VALUE ZERO     01570007
                                                         COMP-3.        01580007
      *  CURSOR DECLARATION FOR THE CROSS REFERENCE TABLE               01670007
                                                                        01700007
           EXEC SQL                                                     01710007
                DECLARE XREF_CURSOR CURSOR FOR                          01720007
                 SELECT                                                 01730007
                       SHIPT_UNT_ID                                     01740007
                     , BRCDE_NBR                                        01750007
                     , CART_LBL_TYP_CDE                                 01760007
                     , DEST_OPER_UNT_ID                                 01770007
                     , STAT_CDE                                         01780007
                     , LAST_STAT_CHG_TMST                               01790007
                     , CRE_TMST                                         01800007
                   FROM TSULBXF                                         01810007
                  WHERE SHIPT_UNT_ID   = :SHUNLO-SHIPT-UNT-ID           01820007
                  ORDER BY SHIPT_UNT_ID                                 01830007
                                                                        01840007
           END-EXEC.                                                    01850007
                                                                        01860007
      ******************************************************************01880007
       PROCEDURE DIVISION.                                              01890007
013705     DISPLAY '**** START OF SUKUT042 ****'.                               
      ******************************************************************01900007
                                                                        01910007
      ******************************************************************01930007
       0000-PROGRAM-MAINLINE.                                           01920007
      ******************************************************************01960007
           PERFORM 1000-INITIALIZE THRU 1000-EXIT.                      01980007
           IF PS-MORE-SHIPMENT-LOC-FILE                                 01990007
               PERFORM 3000-PROCESS-SHIPMENT-LOC THRU 3000-EXIT         02000007
                  UNTIL PS-EOF-SHIPMENT-LOC-FILE                        02010007
           END-IF.                                                      02020007
           PERFORM 7000-EOJ-ROUTINE THRU 7000-EXIT.                     02030007
                                                                        02040007
           STOP RUN.                                                    02050007
                                                                        02040007
      ******************************************************************02080007
       1000-INITIALIZE.                                                 02070007
      ******************************************************************02080007
           OPEN OUTPUT XREF-OUT-FILE                                    02170007
                INPUT  SHIPMENT-LOC-FILE.                               02190007
                                                                        02200007
           PERFORM 2000-READ-SHIPMENT-LOC-FILE THRU 2000-EXIT.          02210007
           IF PS-MORE-SHIPMENT-LOC-FILE                                 01990007
              PERFORM 4000-OPEN-XREF-CURSOR THRU 4000-EXIT              02220007
              PERFORM 5000-FETCH-XREF-CURSOR THRU 5000-EXIT             02230007
              SET PS-SHIPMENT-LOC-RECORDS-YES  TO TRUE                          
           ELSE                                                                 
              SET PS-SHIPMENT-LOC-RECORDS-NO   TO TRUE                          
           END-IF.                                                      02020007
                                                                        02240007
       1000-EXIT.  EXIT.                                                02070007
      ******************************************************************02320007
       2000-READ-SHIPMENT-LOC-FILE.                                     02250007
      ******************************************************************02320007
            READ SHIPMENT-LOC-FILE INTO DCLTSHUNLO                      02260007
             AT END                                                     02300007
                SET PS-EOF-SHIPMENT-LOC-FILE TO TRUE.                   02300007
                                                                        02390007
       2000-EXIT.  EXIT.                                                02070007
      ******************************************************************02320007
       3000-PROCESS-SHIPMENT-LOC.                                       02310007
      ******************************************************************02320007
           IF SQLCODE = PC-ROW-NOT-FOUND                                02390007
               PERFORM 5500-CLOSE-XREF-CURSOR THRU 5500-EXIT            03520007
               PERFORM 2000-READ-SHIPMENT-LOC-FILE THRU 2000-EXIT       02210007
               PERFORM 4000-OPEN-XREF-CURSOR THRU 4000-EXIT             02510007
               PERFORM 5000-FETCH-XREF-CURSOR THRU 5000-EXIT            02490007
           ELSE                                                                 
               IF SQLCODE = ZERO                                        02390007
                   PERFORM 6000-WRITE-XREF-OUT-RECORD THRU 6000-EXIT    02460007
                   PERFORM 5000-FETCH-XREF-CURSOR THRU 5000-EXIT        02490007
               END-IF                                                           
           END-IF.                                                              
                                                                        02500007
       3000-EXIT.  EXIT.                                                02070007
      ******************************************************************02520007
       4000-OPEN-XREF-CURSOR.                                           02510007
      ******************************************************************02540007
               EXEC SQL                                                 02610007
                    OPEN XREF_CURSOR                                    02620007
               END-EXEC                                                 02630007
                                                                        02640007
               EVALUATE TRUE                                            02650007
                   WHEN SQLWARN0 NOT = SPACES                           02690007
                   WHEN SQLCODE < ZERO                                  02700007
                   WHEN SQLCODE > ZERO                                  02710007
                       MOVE '4000-OPEN-XREF-CURSOR    '                 02720007
                                       TO PV-PARAGRAPH-NAME             02730007
                       MOVE 'OPEN THE XREF_CURSOR    '                  02740007
                                       TO PV-DB2-OPERATION-ATTEMPTED    02750007
                       PERFORM 9999-SQL-ABEND THRU 9999-EXIT            02760007
                   WHEN SQLCODE = ZERO                                  02770007
                   WHEN SQLCODE = +100                                  02770007
                       CONTINUE                                         02780007
               END-EVALUATE.                                            02790007
                                                                        02810007
       4000-EXIT.  EXIT.                                                02070007
      ******************************************************************02910007
       5000-FETCH-XREF-CURSOR.                                          02900007
      ******************************************************************02930007
           EXEC SQL                                                     02940007
                FETCH XREF_CURSOR                                       02950007
                 INTO :SULBXF-SHIPT-UNT-ID                              02960007
                    , :SULBXF-BRCDE-NBR                                 02970007
                    , :SULBXF-CART-LBL-TYP-CDE                          02980007
                    , :SULBXF-DEST-OPER-UNT-ID                          02990007
                    , :SULBXF-STAT-CDE                                  03000007
                    , :SULBXF-LAST-STAT-CHG-TMST                        03000007
                    , :SULBXF-CRE-TMST                                  02990007
           END-EXEC.                                                    03030007
                                                                        03040007
           EVALUATE TRUE                                                03050007
               WHEN SQLCODE = ZERO                                      03060007
                   CONTINUE                                             03070007
               WHEN SQLCODE = +100                                      03080007
                   SET PS-EOF-XREF-CSR TO TRUE                          03090007
               WHEN SQLWARN0 NOT = SPACES                               03110007
               WHEN SQLCODE < ZERO                                      03120007
               WHEN SQLCODE > ZERO                                      03130007
                   MOVE '5000-XREF-CURSOR  '                            03140007
                                       TO PV-PARAGRAPH-NAME             03150007
                   MOVE 'FETCH THE XREF-CURSOR   '                      03160007
                                       TO PV-DB2-OPERATION-ATTEMPTED    03170007
                   PERFORM 9999-SQL-ABEND THRU 9999-EXIT                03180007
           END-EVALUATE.                                                03190007
                                                                        03200007
       5000-EXIT.  EXIT.                                                02070007
      ******************************************************************03530007
       5500-CLOSE-XREF-CURSOR.                                          03520007
      ******************************************************************03550007
               EXEC SQL                                                 03970007
                    CLOSE XREF_CURSOR                                   03980007
               END-EXEC                                                 03990007
                                                                        04000007
               EVALUATE TRUE                                            04010007
                   WHEN SQLWARN0 NOT = SPACES                           04050007
                   WHEN SQLCODE < ZERO                                  04060007
                   WHEN SQLCODE > ZERO                                  04070007
                       MOVE '5500-CLOSE-XREF-CURSOR         '           04080007
                                       TO PV-PARAGRAPH-NAME             04090007
                       MOVE 'CLOSE THE XREF_CURSOR    '                 04100007
                                       TO PV-DB2-OPERATION-ATTEMPTED    04110007
                       PERFORM 9999-SQL-ABEND THRU 9999-EXIT            04120007
                   WHEN SQLCODE = ZERO                                  04130007
                       CONTINUE                                         04140007
               END-EVALUATE.                                            04150007
                                                                        04170007
       5500-EXIT.  EXIT.                                                02070007
      ******************************************************************03530007
       6000-WRITE-XREF-OUT-RECORD.                                      03520007
      ******************************************************************03550007
           INITIALIZE XREF-OUT-REC.                                     03560007
           WRITE XREF-OUT-REC FROM DCLTSULBXF.                          03570007
           ADD +1 TO PV-WRITE-COUNTER.                                  03590007
                                                                        03600007
       6000-EXIT.  EXIT.                                                02070007
      ******************************************************************03790007
       7000-EOJ-ROUTINE.                                                03780007
      ******************************************************************03810007
           CLOSE SHIPMENT-LOC-FILE                                      03820007
                 XREF-OUT-FILE.                                         03830007
                                                                        03850007
           MOVE PV-WRITE-COUNTER TO PV-DISPLAY-COUNT.                   03860007
           DISPLAY 'TOTAL EXTRACT RECORDS WRITTEN    ' PV-DISPLAY-COUNT.03870007
                                                                                
           IF PS-SHIPMENT-LOC-RECORDS-YES                               03900007
               EXEC SQL                                                 03970007
                    CLOSE XREF_CURSOR                                   03980007
               END-EXEC                                                 03990007
                                                                        04000007
               EVALUATE TRUE                                            04010007
                   WHEN SQLWARN0 NOT = SPACES                           04050007
                   WHEN SQLCODE < ZERO                                  04060007
                   WHEN SQLCODE > ZERO                                  04070007
                       MOVE '7000-EOJ-ROUTINE               '           04080007
                                       TO PV-PARAGRAPH-NAME             04090007
                       MOVE 'END OF JOB ROUTINE       '                 04100007
                                       TO PV-DB2-OPERATION-ATTEMPTED    04110007
                       PERFORM 9999-SQL-ABEND THRU 9999-EXIT            04120007
                   WHEN SQLCODE = ZERO                                  04130007
                       CONTINUE                                         04140007
               END-EVALUATE                                             04150007
           END-IF.                                                      03900007
                                                                        04170007
       7000-EXIT.  EXIT.                                                02070007
      ******************************************************************04270007
       9999-SQL-ABEND.                                                  04260007
      *  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL    04280007
      *  ERROR OR WARNING CODE OCCURS.                                  04290007
      ******************************************************************04300007
           DISPLAY '9999-SQL-ABEND'.                                    04310007
           DISPLAY '** ABEND     **'.                                   04320007
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        04330007
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              04340007
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.     04350007
                                                                        04360007
      ******************************************************************04370007
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    04380007
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         04390007
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     04400007
      * FORMAT.                                                         04410007
      ******************************************************************04420007
           COPY DPPD004.                                                04430007
                                                                        04440007
           MOVE +4013                  TO ABEND-CODE.                   04450007
           CALL 'ILBOABN0' USING ABEND-CODE.                            04460007
       9999-EXIT.  EXIT.                                                02070007
