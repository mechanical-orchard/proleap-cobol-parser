      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.   SUKUP902.                                                  
       AUTHOR.       SUBASH T.                                                  
       INSTALLATION. KOHLS DEPARTMENT STORES.                                   
       DATE-WRITTEN. MAY, 2014.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  THIS PROGRAM IS USED TO INSERT THE DIVERTS INTO TSUCRDV TABLE *        
      *  FOR THE DIVERTS THAT ERRORED OUT TO THE TUPLDER TABLE DUE TO  *        
      *  THE CONVEYOR SYSTEM AT DC(S) NOT BEING IN SYNC WITH THE DB2   *        
      *  TABLES ON THE MAINFRAME FROM A DATA STANDPOINT.               *        
      *  PROGRAM SUKUT945 IS CREATING AN EXTRACT FROM THE TUPLDER TABLE*        
      *  THAT THIS PROGRAM WILL TAKE IN AND INSERT IT INTO TSUCRDV     *        
      *  TABLE.                                                        *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT TSUCRDV-INPUT-FILE                                            
               ASSIGN TO INP01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  TSUCRDV-INPUT-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
           COPY SURD945.                                                        
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      ******************************************************************        
      *  COMMUNICATIONS AREA FOR DB2                                   *        
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DB2 TABLE DECLARATION - TSUCRDV                               *        
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE TSUCRDV                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  WORK AREA FOR SQL ERROR ROUTINE 'DSNTIAR'                     *        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                    PIC S9(04)  VALUE +0                   
                                                         COMP SYNC.             
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-TOTALS.                                                       
               10 PA-INPUT-COUNT         PIC S9(13)  VALUE +0  COMP-3.          
               10 PA-INSERT-COUNT        PIC S9(13)  VALUE +0  COMP-3.          
               10 PA-DUP-COUNT           PIC S9(13)  VALUE +0  COMP-3.          
               10 PA-COMMIT-COUNT        PIC S9(13)  VALUE +0  COMP-3.          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-INP-SW          PIC X(01)   VALUE 'N'.                 
               88  PS-END-OF-INP                     VALUE 'Y'.                 
               88  PS-END-OF-INP-NO                  VALUE 'N'.                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES            PIC S9(03)  VALUE +3  COMP-3.          
           05  PC-PGM-NAME               PIC X(08)   VALUE 'SUKUP902'.          
           05  PC-COMMIT-HOLD            PIC 9(3)    VALUE 300.                 
                                                                                
       01 PV-PROGRAM-VARIABLES.                                                 
           05  PV-PARAGRAPH-NAME         PIC X(30)   VALUE SPACES.              
           05  PV-RETRY-COUNTER          PIC S9(03)  COMP-3.                    
           05  PV-COMMIT-COUNTER         PIC 9(03)   VALUE ZERO.                
           05  PV-DISPLAY-COUNT          PIC Z(12)9.                            
                                                                                
       LINKAGE SECTION.                                                         
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *  0000-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE PROGRAM.  *        
      ******************************************************************        
       0000-PROGRAM-MAINLINE.                                                   
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-READ-INP-FILE                                           
               UNTIL PS-END-OF-INP.                                             
                                                                                
           PERFORM 8000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *  OPENS INPUT AND PERFORMS OTHER BEGINNING OF JOB PROCESSES.    *        
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT TSUCRDV-INPUT-FILE.                                       
           SET PS-END-OF-INP-NO            TO TRUE.                             
           DISPLAY 'START OF PROGRAM : ' PC-PGM-NAME.                           
                                                                                
       EJECT                                                                    
      *                                                                         
      ******************************************************************        
      * READ EVERY RECORD FROM THE INPUT FILE FOR FURTHER PROCESSING   *        
      ******************************************************************        
       2000-READ-INP-FILE.                                                      
                                                                                
           INITIALIZE SU945-UPLDER-RECORD.                                      
                                                                                
           READ TSUCRDV-INPUT-FILE                                              
               AT END                                                           
                 SET PS-END-OF-INP         TO TRUE.                             
                                                                                
           IF PS-END-OF-INP-NO                                                  
              PERFORM 3000-INSERT-TSUCRDV                                       
              ADD 1                        TO PA-INPUT-COUNT                    
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *                                                                         
      ******************************************************************        
      * INSERTS INTO TSUCRDV                                           *        
      ******************************************************************        
       3000-INSERT-TSUCRDV.                                                     
                                                                                
           MOVE SU945-OPER-UNT-ID          TO SUCRDV-OPER-UNT-ID.               
           MOVE SU945-DIVERT-NBR           TO SUCRDV-DIVERT-NBR.                
           MOVE SU945-DIVERT-LOC-ID        TO SUCRDV-DIVERT-LOC-ID.             
           MOVE SU945-DIVERT-TMST          TO SUCRDV-DIVERT-TMST.               
           MOVE SU945-BRCDE-NBR            TO SUCRDV-BRCDE-NBR.                 
           MOVE SU945-PRC-RSN-CDE          TO SUCRDV-PRC-RSN-CDE.               
           MOVE SU945-SHIPT-UNT-ID         TO SUCRDV-SHIPT-UNT-ID.              
           MOVE SU945-BTCH-NBR             TO SUCRDV-BTCH-NBR.                  
           MOVE SU945-BTCH-RVSN-NBR        TO SUCRDV-BTCH-RVSN-NBR.             
           MOVE SU945-ACTV-CDE             TO SUCRDV-ACTV-CDE.                  
           MOVE SU945-NACTV-USR-ID         TO SUCRDV-NACTV-USR-ID.              
           MOVE SU945-DOOR-ID              TO SUCRDV-DOOR-ID.                   
           MOVE SU945-DEST-OPER-UNT-ID     TO SUCRDV-DEST-OPER-UNT-ID.          
           MOVE SU945-DIVERT-SYS-DTE       TO SUCRDV-DIVERT-SYS-DTE.            
           MOVE SU945-DIVERT-SYS-TM        TO SUCRDV-DIVERT-SYS-TM.             
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
                     UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                 
                        OR  (SQLCODE = -803)                                    
                        OR  (SQLCODE = +0))                                     
               EXEC SQL                                                 09850006
                   INSERT INTO TSUCRDV                                  09860006
                          (OPER_UNT_ID                                  09870037
                          ,DIVERT_NBR                                   09880006
                          ,DIVERT_LOC_ID                                09890006
                          ,DIVERT_TMST                                  09900006
                          ,BRCDE_NBR                                    09900006
                          ,PRC_RSN_CDE                                  09900006
                          ,SHIPT_UNT_ID                                 09900006
                          ,BTCH_NBR                                     09900006
                          ,BTCH_RVSN_NBR                                09900006
                          ,ACTV_CDE                                     09900006
                          ,NACTV_USR_ID                                 09900006
                          ,DOOR_ID                                      09900006
                          ,DEST_OPER_UNT_ID                             09900006
                          ,DIVERT_SYS_DTE                               09900006
                          ,DIVERT_SYS_TM                                09910006
                          )                                                     
                   VALUES                                               09920006
                          (:SUCRDV-OPER-UNT-ID                          09930008
                          ,:SUCRDV-DIVERT-NBR                           09940006
                          ,:SUCRDV-DIVERT-LOC-ID                        09950008
                          ,:SUCRDV-DIVERT-TMST                          09950008
                          ,:SUCRDV-BRCDE-NBR                            09950008
                          ,:SUCRDV-PRC-RSN-CDE                          09950008
                          ,:SUCRDV-SHIPT-UNT-ID                         09950008
                          ,:SUCRDV-BTCH-NBR                             09950008
                          ,:SUCRDV-BTCH-RVSN-NBR                        09960008
                          ,:SUCRDV-ACTV-CDE                             09960008
                          ,:SUCRDV-NACTV-USR-ID                         09960008
                          ,:SUCRDV-DOOR-ID                              09960008
                          ,:SUCRDV-DEST-OPER-UNT-ID                     09960008
                          ,:SUCRDV-DIVERT-SYS-DTE                       09960008
                          ,:SUCRDV-DIVERT-SYS-TM                        09960008
                          )                                             09970006
               END-EXEC                                                 09980006
                                                                        09990006
               EVALUATE TRUE                                            10000006
                 WHEN SQLCODE = ZERO                                    10010006
                     ADD 1                 TO PA-INSERT-COUNT           10050006
                                              PV-COMMIT-COUNTER                 
                                                                        10060006
                 WHEN SQLCODE = -803                                    10010006
                     ADD 1                 TO PA-DUP-COUNT                      
                     DISPLAY '*****************************************'10080006
                     DISPLAY '* INFORMATION MESSAGE                   *'10090006
                     DISPLAY '*****************************************'10080006
                     DISPLAY '*FOLLOWING DIVERT NOT INSERTED IN       *'10100006
                     DISPLAY '*TSUCRDV TABLE AS DIVERTS ARE ALREADY   *'10110006
                     DISPLAY '*PRESENT                                *'10120006
                     DISPLAY '*OPER-UNT-ID   = ' SU945-OPER-UNT-ID      10120006
                     DISPLAY '*DIVERT-NBR    = ' SU945-DIVERT-NBR             10
                     DISPLAY '*DIVERT-LOC-ID = ' SU945-DIVERT-LOC-ID          10
                     DISPLAY '*DIVERT-TMST   = ' SU945-DIVERT-TMST            10
                     DISPLAY '*BRCDE-NBR     = ' SU945-BRCDE-NBR              10
                     DISPLAY '*PRC-RSN-CDE   = ' SU945-PRC-RSN-CDE            10
                     DISPLAY '*****************************************'10140006
                                                                        10060006
                 WHEN SQLCODE = -904                                            
                 WHEN SQLCODE = -911                                            
                 WHEN SQLCODE = -913                                            
                     CONTINUE                                                   
                                                                        10060006
                 WHEN OTHER                                             10070006
                     DISPLAY '*****************************************'10080006
                     DISPLAY '*ABEND                                  *'10090006
                     DISPLAY '*PROGRAM       =  SUKUP902              *'10100006
                     DISPLAY '*PARAGRAPH     =  3000-INSERT-TSUCRDV   *'10110006
                     DISPLAY '*DB2 OPER      =  INSERT TO TSUCRDV     *'10120006
                     DISPLAY '*SQLCODE       = '  SQLCODE               10120006
                     DISPLAY '*OPER-UNT-ID   = ' SU945-OPER-UNT-ID      10120006
                     DISPLAY '*DIVERT-NBR    = ' SU945-DIVERT-NBR             10
                     DISPLAY '*DIVERT-LOC-ID = ' SU945-DIVERT-LOC-ID          10
                     DISPLAY '*DIVERT-TMST   = ' SU945-DIVERT-TMST            10
                     DISPLAY '*BRCDE-NBR     = ' SU945-BRCDE-NBR              10
                     DISPLAY '*PRC-RSN-CDE   = ' SU945-PRC-RSN-CDE            10
                     DISPLAY '*****************************************'10140006
                     PERFORM 9100-SQL-ABEND                             10150006
               END-EVALUATE                                             10160006
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               DISPLAY '*****************************************'              
               DISPLAY '*ABEND                                  *'      10090006
               DISPLAY '*PROGRAM       =  SUKUP902              *'      10100006
               DISPLAY '*PARAGRAPH     =  3000-INSERT-TSUCRDV   *'      10110006
               DISPLAY '*DB2 OPER      =  INSERT TO TSUCRDV     *'      10120006
               DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO      *'              
               DISPLAY 'INSERT TSUCRDV                          *'              
               DISPLAY '*SQLCODE       = ' SQLCODE                      10120006
               DISPLAY '*OPER-UNT-ID   = ' SU945-OPER-UNT-ID            10120006
               DISPLAY '*DIVERT-NBR    = ' SU945-DIVERT-NBR             10120006
               DISPLAY '*DIVERT-LOC-ID = ' SU945-DIVERT-LOC-ID          10120006
               DISPLAY '*DIVERT-TMST   = ' SU945-DIVERT-TMST            10120006
               DISPLAY '*BRCDE-NBR     = ' SU945-BRCDE-NBR              10120006
               DISPLAY '*PRC-RSN-CDE   = ' SU945-PRC-RSN-CDE            10120006
               DISPLAY '*****************************************'              
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           IF PV-COMMIT-COUNTER > PC-COMMIT-HOLD                                
               EXEC SQL                                                         
                  COMMIT                                                        
               END-EXEC                                                         
                                                                                
               IF SQLCODE NOT = +0                                              
                   DISPLAY '*****************************************'          
                   DISPLAY '*ABEND                                  *'  10090006
                   DISPLAY '### COMMIT FAILED ! ###'                            
                   DISPLAY '*PROGRAM   =  SUKUP902                  *'  10100006
                   DISPLAY '*PARAGRAPH =  3000-INSERT-TSUCRDV       *'  10110006
                   DISPLAY '*SQLCODE   = '  SQLCODE                     10120006
                   DISPLAY '*****************************************'          
                   PERFORM 9100-SQL-ABEND                                       
               END-IF                                                           
                                                                                
               ADD 1                       TO PA-COMMIT-COUNT                   
               MOVE 0                      TO PV-COMMIT-COUNTER                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *                                                                         
      ******************************************************************        
      ******************************************************************        
       8000-EOJ-ROUTINE.                                                        
                                                                                
           IF PV-COMMIT-COUNTER > 0                                             
               EXEC SQL                                                         
                  COMMIT                                                        
               END-EXEC                                                         
                                                                                
               IF SQLCODE NOT = +0                                              
                   DISPLAY '*****************************************'          
                   DISPLAY '*ABEND                                  *'  10090006
                   DISPLAY '### COMMIT FAILED ! ###'                            
                   DISPLAY '*PROGRAM   =  SUKUP902                  *'  10100006
                   DISPLAY '*PARAGRAPH =  8000-EOJ-ROUTINE          *'  10110006
                   DISPLAY '*SQLCODE   = '  SQLCODE                     10120006
                   DISPLAY '*****************************************'          
                   PERFORM 9100-SQL-ABEND                                       
               END-IF                                                           
                                                                                
               ADD 1                       TO PA-COMMIT-COUNT                   
           END-IF.                                                              
                                                                                
           CLOSE TSUCRDV-INPUT-FILE.                                            
                                                                                
                                                                                
           DISPLAY '*************************************************'.         
           DISPLAY '**             SUKUP902 SUMMARY                **'.         
           DISPLAY '*************************************************'.         
           MOVE  PA-INPUT-COUNT            TO PV-DISPLAY-COUNT.                 
           DISPLAY 'TOTAL INPUT RECORDS READ                 = '                
                                                      PV-DISPLAY-COUNT.         
           MOVE  PA-INSERT-COUNT           TO PV-DISPLAY-COUNT.                 
           DISPLAY 'TOTAL DIVERTS INSERTED INTO TSUCRDV      = '                
                                                      PV-DISPLAY-COUNT.         
           MOVE  PA-DUP-COUNT              TO PV-DISPLAY-COUNT.                 
           DISPLAY 'TOTAL DIVERTS ALREADY PRESENT IN TSUCRDV = '                
                                                      PV-DISPLAY-COUNT.         
           MOVE  PA-COMMIT-COUNT           TO PV-DISPLAY-COUNT.                 
           DISPLAY 'TOTAL COMMITS TAKEN                      = '                
                                                      PV-DISPLAY-COUNT.         
           DISPLAY '*************************************************'.         
           DISPLAY 'END OF PROGRAM   : ' PC-PGM-NAME.                           
                                                                                
       EJECT                                                                    
      *                                                                         
      ******************************************************************        
      * PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL    *        
      * ERROR OR WARNING CODE OCCURS.                                  *        
      ******************************************************************        
       9100-SQL-ABEND.                                                          
                                                                                
           COPY DPPD004.                                                12410000
           MOVE +4013                      TO ABEND-CODE.               12570000
           CALL 'ILBOABN0' USING ABEND-CODE.                            12580000
                                                                                
