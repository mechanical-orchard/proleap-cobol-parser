00001 *                                                                 02/24/92
00002 *                                                                 MLKUP007
00003 *                                                                    LV002
00004 ***************************************************************** MLKUP007
00005  IDENTIFICATION DIVISION.                                         MLKUP007
00006 ***************************************************************** MLKUP007
00007                                                                   MLKUP007
00008  PROGRAM-ID.             MLKUP007.                                MLKUP007
00009  AUTHOR.                 M JOHNSON.                               MLKUP007
00010  INSTALLATION.           KOHLS DEPARTMENT STORES.                 MLKUP007
00011  DATE-WRITTEN            FEBRUARY 16,1989.                        MLKUP007
00012  DATE-COMPILED.                                                   MLKUP007
00013                                                                   MLKUP007
00014 ***************************************************************** MLKUP007
00015 *    THIS PROGRAM IS PART OF THE MERCHANDISE LEDGER SYSTEM IN   * MLKUP007
00016 *    THE UNAUDITED SALES JOBS. IT READS IN EITHER THE DAILY     * MLKUP007
00017 *    SALES FILES OR THE MONTH TO DATE SALES DATA AND ADJUSTS THE* MLKUP007
00018 *    DIVISION NUMBER OR THE BUYER NUMBER IF THE INPUT DIVISION  * MLKUP007
00019 *    NUMBER OR BUYER NUMBER ARE DIFFERENT THAN THOSE ON THE     * MLKUP007
00020 *    MERCHANDISE DATA BASE, WHICH MAY HAVE CHANGED DURING THE   *    CL**2
00021 *    WEEK. A TABLE CAPABLE OF HANDLING 200 RECORDS, (ANY MORE   * MLKUP007
00022 *    THE PROGRAM WILL ABEND) IS USED TO STORE DATA, CONSISTING  * MLKUP007
00023 *    OF DEPARTMENT NUMBER, OLD DIVISION NUMBER, NEW DIVISION    * MLKUP007
00024 *    NUMBER, OLD BUYER NUMBER AND NEW BUYER NUMBER. IF THERE    * MLKUP007
00025 *    IS A DIFFERENCE IN THE DIVISION NUMBER OR THE BUYER NUMBER * MLKUP007
00026 *    THE INFORMATION IS STORED IN THE TABLE TO BE DISPLAYED AT  * MLKUP007
00027 *    THE END OF THE PROGRAM. ALL CHANGES INCLUDING DUPLICATE    * MLKUP007
00028 *    CHANGES ARE TRACED VIA A COUNTER, AND IF THE DEPARTMENT HAS* MLKUP007
00029 *    A CHANGE IT, IS LISTED IN THE TABLE. ANY DUPLICATE         * MLKUP007
00030 *    CHANGES ARE NOT LISTED IN THE TABLE. TO ELIMINATE A SEARCH * MLKUP007
00031 *    THROUGH THE WHOLE TABLE, THE TABLE IS INITIALIZED TO HIGH  * MLKUP007
00032 *    VALUES. A SEARCH IS PERFORMED IF THERE IS A NEED TO CHANGE * MLKUP007
00033 *    THE DIVISION OR BUYER NUMBER. THE KEY TO THE SEARCH IS THE * MLKUP007
00034 *    DEPARTMENT NUMBER. IF THE DEPARTMENT NUMBER IS NOT LISTED  * MLKUP007
00035 *    IN THE TABLE, NO MATCH IS FOUND AND HIGH VALUES ARE        * MLKUP007
00036 *    ENCOUNTERED, THE SEARCH IS TERMINATED. THE DEPARTMENT RECORD MLKUP007
00037 *    IS ADDED ALONG WITH EITHER THE CHANGE MADE IN THE DIVISION * MLKUP007
00038 *    NUMBER OR THE BUYER NUMBER. IF THE DEPARTMENT RECORD       * MLKUP007
00039 *    NUMBER HAS PREVIOUSLY BEEN LOADED INTO THE TABLE AND       * MLKUP007
00040 *    EITHER THE DIVISION NUMBER OR THE BUYER NUMBER HAVE NOT    * MLKUP007
00041 *    BEEN ADDED THEY ARE ADDED TO THE TABLE. THE TABLE IS NOT IN* MLKUP007
00042 *    DEPARTMENT NUMBER SEQUENCE AS THE INPUT DATA IS IN STORE   * MLKUP007
00043 *    NUMBER SEQUENCE SO THERE IS NO ORDER TO THE DEPARTMENT     * MLKUP007
00044 *    NUMBERS. EVERYTIME THERE IS A CHANGE IN THE INPUT          * MLKUP007
00045 *    DEPARTMENT NUMBER, THE MERCHANDISE DATA BASE IS READ USING *    CL**2
00046 *    DEPARTMENT NUMBER. IF THERE IS NO MATCH THE INPUT RECORD   *    CL**2
00047 *    THE RECORD IS DISPLAYED AND WRITTEN, BUT NOT UPDATED. IF   *    CL**2
00048 *    THERE IS AN ERROR ON THE READ OF THE MERCHANDISE DATA BASE,*    CL**2
00049 *    THE PROGRAM WILL ABEND.                                    *    CL**2
00049 *                                                               *    CL**2
207210* CHG0207210 08-29-2018  JIM HUNTER  RENAME KDU1806 TO PSWS010  *         
00050 ***************************************************************** MLKUP007
00051                                                                   MLKUP007
00052 /                                                                 MLKUP007
00053 *                                                                 MLKUP007
00054 *                                                                 MLKUP007
00055 ***************************************************************** MLKUP007
00056  ENVIRONMENT DIVISION.                                            MLKUP007
00057 ***************************************************************** MLKUP007
00058                                                                   MLKUP007
00059 *                                                                 MLKUP007
00060 *                                                                 MLKUP007
00061  CONFIGURATION SECTION.                                           MLKUP007
00062                                                                   MLKUP007
00063  SOURCE-COMPUTER.        IBM-3090.                                MLKUP007
00064  OBJECT-COMPUTER.        IBM-3090.                                MLKUP007
00065                                                                   MLKUP007
00066 *                                                                 MLKUP007
00067 *                                                                 MLKUP007
00068  INPUT-OUTPUT SECTION.                                            MLKUP007
00069                                                                   MLKUP007
00070  FILE-CONTROL.                                                    MLKUP007
00071                                                                   MLKUP007
00072      SELECT INP-UNEDITED-SALES-FILE                               MLKUP007
00073          ASSIGN TO INP01.                                         MLKUP007
00074                                                                   MLKUP007
00075      SELECT OUT-EDITED-SALES-FILE                                 MLKUP007
00076          ASSIGN TO OUT01.                                         MLKUP007
00077                                                                   MLKUP007
00078 /                                                                 MLKUP007
00079 *                                                                 MLKUP007
00080 *                                                                 MLKUP007
00081 *                                                                 MLKUP007
00082 ******************************************************************MLKUP007
00083  DATA DIVISION.                                                   MLKUP007
00084 ******************************************************************MLKUP007
00085                                                                   MLKUP007
00086 *                                                                 MLKUP007
00087 *                                                                 MLKUP007
00088  FILE SECTION.                                                    MLKUP007
00089                                                                   MLKUP007
00090  FD  INP-UNEDITED-SALES-FILE                                      MLKUP007
00091      RECORDING MODE IS F                                          MLKUP007
00092      LABEL RECORDS ARE STANDARD                                   MLKUP007
00093      BLOCK CONTAINS 0 RECORDS.                                    MLKUP007
00094                                                                   MLKUP007
00095  01  INP-UNDITED-SALES-RECORD    PIC X(65).                       MLKUP007
00096                                                                   MLKUP007
00097  FD  OUT-EDITED-SALES-FILE                                        MLKUP007
00098      RECORDING MODE IS F                                          MLKUP007
00099      LABEL RECORDS ARE STANDARD                                   MLKUP007
00100      BLOCK CONTAINS 0 RECORDS.                                    MLKUP007
00101                                                                   MLKUP007
00102  01  OUT-EDITED-SALES-RECORD    PIC X(65).                        MLKUP007
00103                                                                   MLKUP007
00104 /                                                                 MLKUP007
00105 *                                                                 MLKUP007
00106 *                                                                 MLKUP007
00107  WORKING-STORAGE SECTION.                                         MLKUP007
00108  01  FILLER                           PIC X(8)       VALUE        MLKUP007
00109      'BEGIN WS'.                                                  MLKUP007
00110                                                                   MLKUP007
00111  01  ABEND-CODE                       PIC S9(04)     VALUE +0     MLKUP007
00112                                                      COMP SYNC.   MLKUP007
00113      88  ABEND-4013                                  VALUE 4013.     CL**2
00114                                                                   MLKUP007
00115  01  PS-PROGRAM-SWITCHES.                                         MLKUP007
00116      05  PS-UNEDITED-SALES-EOF-SW     PIC X(01)      VALUE 'N'.   MLKUP007
00117          88  PS-UNEDITED-SALES-EOF                   VALUE 'Y'.   MLKUP007
00118      05  PS-INVALID-DIV-SW            PIC X(01)      VALUE 'N'.   MLKUP007
00119          88  PS-INVALID-DIV                          VALUE 'Y'.   MLKUP007
00120      05  PS-INVALID-BYR-SW            PIC X(01)      VALUE 'N'.   MLKUP007
00121          88  PS-INVALID-BYR                          VALUE 'Y'.   MLKUP007
00122      05  PS-FOUND-RECORD-SW           PIC X(01)      VALUE 'N'.   MLKUP007
00123          88  PS-FOUND-RECORD                         VALUE 'Y'.   MLKUP007
00124                                                                   MLKUP007
00125  01  WS-MISC-VARIABLES.                                           MLKUP007
00126      05  WS-DEPT-FROM-D040            PIC X(03)      VALUE SPACES.MLKUP007
00127                                                                   MLKUP007
00128 *   LOADED FROM THE DB2 'TMDSEAR' TABLE                           MLKUP007
00129  01  PV-PROGRAM-VARIABLES.                                        MLKUP007
00130      05  PV-DEPT                      PIC X(03)      VALUE SPACES.MLKUP007
00131      05  PV-DIV.                                                     CL**2
00132          10  FILLER                   PIC X(01)      VALUE '0'.      CL**2
00133          10  PV-DIV-2                 PIC X(01)      VALUE SPACE.    CL**2
00134      05  PV-BUYER.                                                   CL**2
00135          10  FILLER                   PIC X(01)      VALUE '0'.      CL**2
00136          10  PV-BUYER-2               PIC X(02)      VALUE SPACES.   CL**2
00137      05  PV-PARAGRAPH-NAME            PIC X(30)      VALUE SPACES.   CL**2
00138      05  PV-DB2-OPER-ATTEMPTED        PIC X(30)      VALUE SPACES.   CL**2
00139                                                                   MLKUP007
00140 *   LOADED FROM THE INPUT FILE                                       CL**2
00141  01  WS-PROGRAM-VARIABLES.                                           CL**2
00142      05  WS-DIV.                                                     CL**2
00143          10  FILLER                       PIC X(01)  VALUE '0'.      CL**2
00144          10  WS-DIV-2                     PIC X(01)  VALUE SPACE.    CL**2
00145      05  WS-BUYER.                                                   CL**2
00146          10  FILLER                       PIC X(01)  VALUE '0'.      CL**2
00147          10  WS-BUYER-2                   PIC X(02)  VALUE SPACES.   CL**2
00148                                                                      CL**2
00149  01  TI-TABLE-INVALID-BUYER-DIV.                                  MLKUP007
00150      05  TI-BUYER-DIV-RECORD          OCCURS 200 TIMES            MLKUP007
00151                                       INDEXED BY TI-INDEX         MLKUP007
00152                                                  MX-INDEX         MLKUP007
00153                                                  TE-INDEX.        MLKUP007
00154          10  TI-DEPT                  PIC X(05).                  MLKUP007
00155          10  TI-DEPT-NO               PIC X(03).                  MLKUP007
00156          10  TI-OLD-DIV               PIC X(09).                  MLKUP007
00157          10  TI-OLD-DIV-NO            PIC X(02).                     CL**2
00158          10  TI-NEW-DIV               PIC X(09).                  MLKUP007
00159          10  TI-NEW-DIV-NO            PIC X(02).                     CL**2
00160          10  TI-OLD-BYR               PIC X(09).                  MLKUP007
00161          10  TI-OLD-BYR-NO            PIC X(03).                     CL**2
00162          10  TI-NEW-BYR               PIC X(09).                  MLKUP007
00163          10  TI-NEW-BYR-NO            PIC X(03).                     CL**2
00164                                                                   MLKUP007
00165  01  TC-TOTAL-COUNTS.                                             MLKUP007
00166      05  TC-BUYER-DIFFERENCES         PIC 9(05)      VALUE ZEROS  MLKUP007
00167                                                      COMP-3.      MLKUP007
00168      05  TC-DIV-DIFFERENCES           PIC 9(05)      VALUE ZEROS  MLKUP007
00169                                                      COMP-3.      MLKUP007
00170      05  TC-NUMBER-OF-CHANGES         PIC 9(09)      VALUE ZEROS  MLKUP007
00171                                                      COMP-3.      MLKUP007
00172      05  TC-NMBR-RECORDS-PROCESSED    PIC 9(09)      VALUE ZEROS  MLKUP007
00173                                                      COMP-3.      MLKUP007
00174                                                                   MLKUP007
00175  01  TD-TOTAL-DISPLAY-COUNTS.                                     MLKUP007
00176      05  TD-BUYER-DIFFERENCES         PIC Z(04)9     VALUE ZEROS. MLKUP007
00177      05  TD-DIV-DIFFERENCES           PIC Z(04)9     VALUE ZEROS. MLKUP007
00178      05  TD-NUMBER-OF-CHANGES         PIC Z(08)9     VALUE ZEROS. MLKUP007
00179      05  TD-NMBR-RECORDS-PROCESSED    PIC Z(08)9     VALUE ZEROS. MLKUP007
00180                                                                   MLKUP007
00181 /                                                                 MLKUP007
00182 ******************************************************************MLKUP007
00183 *    SALES RECORD USED FOR BOTH INPUT AND OUTPUT                 *MLKUP007
00184 ******************************************************************MLKUP007
00185 *                                                                 MLKUP007
207210     COPY PSWS010.                                                MLKUP007
00187                                                                   MLKUP007
00188 /                                                                 MLKUP007
00189 ******************************************************************MLKUP007
00190 *    DB2  I/O  AREA:                                             *   CL**2
00191 *                                                                *   CL**2
00192 *    DECLGEN FOR THE MERCHANDISING AREA TABLE                    *   CL**2
00193 ******************************************************************MLKUP007
00194 *                                                                 MLKUP007
00195      EXEC SQL                                                     MLKUP007
00196          INCLUDE TMDSEAR                                          MLKUP007
00197      END-EXEC.                                                    MLKUP007
00198                                                                   MLKUP007
00199 ******************************************************************MLKUP007
00200 *    SQL COMMUNICATIONS AREA DCLGEN                              *MLKUP007
00201 ******************************************************************MLKUP007
00202 *                                                                 MLKUP007
00203      COPY SQLCA2.                                                    CL**2
00204 *                                                                    CL**2
00205      EXEC SQL                                                     MLKUP007
00206          INCLUDE SQLCA                                            MLKUP007
00207      END-EXEC.                                                    MLKUP007
00208                                                                   MLKUP007
00209 ******************************************************************MLKUP007
00210 *    DB2 ERROR MESSAGE AREA                                      *MLKUP007
00211 ******************************************************************MLKUP007
00212 *                                                                 MLKUP007
00213      COPY DPWS004.                                                MLKUP007
00214                                                                   MLKUP007
00215 /                                                                 MLKUP007
00216 ******************************************************************MLKUP007
00217 *                                                                 MLKUP007
00218  01  FILLER                           PIC X(6)       VALUE        MLKUP007
00219      'END WS'.                                                    MLKUP007
00220                                                                   MLKUP007
00221 /                                                                 MLKUP007
00222 ******************************************************************MLKUP007
00223  PROCEDURE DIVISION.                                              MLKUP007
00224 ******************************************************************MLKUP007
00225                                                                   MLKUP007
00226 ******************************************************************MLKUP007
00227 *    START OF 'A' PARAGRAPHS                                     *MLKUP007
00228 ******************************************************************MLKUP007
00229                                                                   MLKUP007
00230 ******************************************************************MLKUP007
00231 * A100-MAINLINE                                                  *MLKUP007
00232 *     CONTROLS THE FLOW OF THE PROGRAM. PERFORMS HOUSEKEEPING    *MLKUP007
00233 *     OF ONE TIME ITEMS. PROCESSES THE DATA. CLOSES DATA SETS.   *MLKUP007
00234 *     DISPLAYS THE INVALID DIVISION AND BUYER NUMBER RECORDS.    *MLKUP007
00235 *     DISPLAYS THE FINAL COUNTS.                                 *MLKUP007
00236 ******************************************************************MLKUP007
00237  A100-MAINLINE.                                                   MLKUP007
00238                                                                   MLKUP007
00239      PERFORM B100-HOUSEKEEPING.                                   MLKUP007
00240      PERFORM B300-PROCESS-DATA.                                   MLKUP007
00241      PERFORM B500-CLOSE-DATASETS.                                 MLKUP007
00242      PERFORM B700-DUMP-INVALID                                    MLKUP007
00243          UNTIL TI-INDEX > TE-INDEX.                               MLKUP007
00244      PERFORM B900-DISPLAY-FINAL-COUNTS.                           MLKUP007
00245      GOBACK.                                                      MLKUP007
00246                                                                   MLKUP007
00247 /                                                                 MLKUP007
00248 ******************************************************************MLKUP007
00249 *    START OF 'B' PARAGRAPHS                                     *MLKUP007
00250 ******************************************************************MLKUP007
00251                                                                   MLKUP007
00252 ******************************************************************MLKUP007
00253 * B100-HOUSEKEEPING                                              *MLKUP007
00254 *     OPENS INPUT AND OUTPUT DATASETS. SETS MX-INDEX TO THE      *MLKUP007
00255 *     NUMBER OF POSSIBLE TABLE ENTRIES. SETS TE-INDEX TO ZERO.   *MLKUP007
00256 *     VALUE IS USED TO DETERMINE THE NUMBER OF ENTRIES IN THE    *MLKUP007
00257 *     TABLE. TI-INDEX IS SET TO 1 TO INITIALIZE THE TABLE.       *MLKUP007
00258 *     THE TABLE IS INITIALIZED TO HIGH VALUES TO CONTROL THE     *MLKUP007
00259 *     SEARCH UNTIL THE MAXIMUM POSSIBLE ENTRIES IS REACHED.      *MLKUP007
00260 *                                                                *MLKUP007
00261 *     PERFORMED BY A100-MAINLINE                                 *MLKUP007
00262 ******************************************************************MLKUP007
00263  B100-HOUSEKEEPING.                                               MLKUP007
00264                                                                   MLKUP007
00265      OPEN INPUT  INP-UNEDITED-SALES-FILE                          MLKUP007
00266           OUTPUT OUT-EDITED-SALES-FILE.                           MLKUP007
00267      SET MX-INDEX                     TO 200.                     MLKUP007
00268      SET TE-INDEX                     TO 1.                       MLKUP007
00269      SET TE-INDEX                     DOWN BY 1.                  MLKUP007
00270      SET TI-INDEX                     TO 1.                       MLKUP007
00271      PERFORM C100-INIT-INVALID-TABLE                              MLKUP007
00272          UNTIL TI-INDEX > MX-INDEX.                               MLKUP007
00273                                                                   MLKUP007
00274 /                                                                 MLKUP007
00275 ******************************************************************MLKUP007
00276 * B300-PROCESS-DATA                                              *MLKUP007
00277 *     PERFORMS INITIAL READ. PERFORMS PROCESS OF DATA UNTIL      *MLKUP007
00278 *     THE END OF THE INPUT SALES DATA IS REACHED.                *MLKUP007
00279 *                                                                *MLKUP007
00280 *     PERFORMED BY A100-MAINLINE                                 *MLKUP007
00281 ******************************************************************MLKUP007
00282  B300-PROCESS-DATA.                                               MLKUP007
00283                                                                   MLKUP007
00284      PERFORM R100-READ-UNEDITED-SALES-FILE.                       MLKUP007
00285      PERFORM C300-PROCESS-SALES-RECORDS                           MLKUP007
00286             UNTIL PS-UNEDITED-SALES-EOF.                          MLKUP007
00287                                                                   MLKUP007
00288 /                                                                 MLKUP007
00289 ******************************************************************MLKUP007
00290 * B500-CLOSE-DATASETS                                            *MLKUP007
00291 *     CLOSE INPUT AND OUTPUT DATASETS. SETS TI-INDEX TO ALLOW    *MLKUP007
00292 *     DISPLAY OF THE INVALID RECORD TABLE                        *MLKUP007
00293 *                                                                *MLKUP007
00294 *     PERFORMED BY A100-MAINLINE                                 *MLKUP007
00295 ******************************************************************MLKUP007
00296  B500-CLOSE-DATASETS.                                             MLKUP007
00297                                                                   MLKUP007
00298      CLOSE INP-UNEDITED-SALES-FILE                                MLKUP007
00299            OUT-EDITED-SALES-FILE.                                 MLKUP007
00300      SET TI-INDEX TO 1.                                           MLKUP007
00301                                                                   MLKUP007
00302 /                                                                 MLKUP007
00303 ******************************************************************MLKUP007
00304 * B700-DUMP-INVALID                                              *MLKUP007
00305 *     DISPLAYS THE TABLE ENTRIES THAT WERE MADE DURING THE       *MLKUP007
00306 *     PROGRAM. SETS UP TI-INDEX TO TEST FOR THE NUMBER OF        *MLKUP007
00307 *     ENTRIES MADE.                                              *MLKUP007
00308 *                                                                *MLKUP007
00309 *     PERFORMED BY A100-MAINLINE                                 *MLKUP007
00310 ******************************************************************MLKUP007
00311  B700-DUMP-INVALID.                                               MLKUP007
00312                                                                   MLKUP007
00313      DISPLAY TI-BUYER-DIV-RECORD (TI-INDEX).                      MLKUP007
00314      SET TI-INDEX                     UP BY 1.                    MLKUP007
00315                                                                   MLKUP007
00316 /                                                                 MLKUP007
00317 ******************************************************************MLKUP007
00318 * B900-DISPLAY=FINAL-COUNTS                                      *MLKUP007
00319 *     COMPUTES NUMBER OF CHANGES MADE TO THE INPUT FILE.         *MLKUP007
00320 *     MOVES COUNTS TO DISPLAY FIELDS. DISPLAYS COUNTS.           *MLKUP007
00321 *                                                                *MLKUP007
00322 *     PERFORMED BY A100-MAINLINE                                 *MLKUP007
00323 ******************************************************************MLKUP007
00324  B900-DISPLAY-FINAL-COUNTS.                                       MLKUP007
00325                                                                   MLKUP007
00326      COMPUTE TC-NUMBER-OF-CHANGES = TC-BUYER-DIFFERENCES +        MLKUP007
00327          TC-DIV-DIFFERENCES.                                      MLKUP007
00328                                                                   MLKUP007
00329      MOVE TC-NMBR-RECORDS-PROCESSED TO TD-NMBR-RECORDS-PROCESSED. MLKUP007
00330      MOVE TC-BUYER-DIFFERENCES        TO TD-BUYER-DIFFERENCES.    MLKUP007
00331      MOVE TC-DIV-DIFFERENCES          TO TD-DIV-DIFFERENCES.      MLKUP007
00332      MOVE TC-NUMBER-OF-CHANGES        TO TD-NUMBER-OF-CHANGES.    MLKUP007
00333                                                                   MLKUP007
00334      DISPLAY 'TOTAL INPUT RECORDS = ' TD-NMBR-RECORDS-PROCESSED.  MLKUP007
00335      DISPLAY 'TOTAL BUYER CHGS = ' TD-BUYER-DIFFERENCES.          MLKUP007
00336      DISPLAY 'TOTAL DIV CHGS = ' TD-DIV-DIFFERENCES.              MLKUP007
00337      DISPLAY 'RECORDS CHANGED = ' TD-NUMBER-OF-CHANGES.           MLKUP007
00338                                                                   MLKUP007
00339 /                                                                 MLKUP007
00340 ******************************************************************MLKUP007
00341 *    START OF 'C' PARAGRAPHS                                     *MLKUP007
00342 ******************************************************************MLKUP007
00343                                                                   MLKUP007
00344 ******************************************************************MLKUP007
00345 * C100-INIT-INVALID-TABLE                                        *MLKUP007
00346 *     MOVES HIGH VALUES TO THE INVALID TABLE SO THE SEARCH DOES  *MLKUP007
00347 *     DOES NOT HAVE TO PROCESS THE WHOLE TABLE.                  *MLKUP007
00348 *                                                                *MLKUP007
00349 *     PERFORMED BY B100-HOUSEKEEPING                             *MLKUP007
00350 ******************************************************************MLKUP007
00351  C100-INIT-INVALID-TABLE.                                         MLKUP007
00352                                                                   MLKUP007
00353      MOVE HIGH-VALUES                 TO                          MLKUP007
00354          TI-BUYER-DIV-RECORD (TI-INDEX).                          MLKUP007
00355      SET TI-INDEX UP BY 1.                                        MLKUP007
00356                                                                   MLKUP007
00357 /                                                                 MLKUP007
00358 ******************************************************************MLKUP007
00359 * C300-PROCESS-SALES-RECORDS                                     *MLKUP007
00360 *     EVERYTIME THERE IS A CHANGE IN THE INPUT DEPARTMENT NUMBER *MLKUP007
00361 *     THE DEPARTMENT DATA BASE IS READ IF THERE IS NO MATCH ON   *MLKUP007
00362 *     THE INPUT DEPARTMENT NUMBER THE RECORD IS WRITTEN BUT NOT  *MLKUP007
00363 *     UPDATED. IF THERE IS DIFIFFERENCE IN THE INPUT DIVISION    *MLKUP007
00364 *     OR BUYER NUMBER THAN THOSE ON THE DEPARTMENT DATA BASE,    *MLKUP007
00365 *     THE DATA IS REPLACED WITH THE VALUES ON THE DEPARTMENT     *MLKUP007
00366 *     DEPARTMENT DATA BASE AND A COUNTER IS INCREMENTED TO       *MLKUP007
00367 *     REFLECT A CHANGE WAS MADE. IF THERE IS A CHANGE MADE A     *MLKUP007
00368 *     TABLE CONSISTING OF THE DEPARTMENT NUMBER, OLD DIVISION    *MLKUP007
00369 *     NUMBER, NEW DIVISION NUMBER, OLD BUYER NUMBER AND NEW      *MLKUP007
00370 *     BUYER NUMBER IS UPDATED, TO BE DISPLAYED AT THE END OF THE *MLKUP007
00371 *     PROGRAM. A SEARCH IS PERFORMED IF THERE IS A NEED TO       *MLKUP007
00372 *     CHANGE THE DIVISION OR BUYER NUMBER. A COMPLETE SEARCH IS  *MLKUP007
00373 *     ELIMINATED IF THERE IS NO MATCH AND HIGH VALUES ARE        *MLKUP007
00374 *     ENCOUNTERED. THE KEY TO THE TABLE IS THE DEPARTMENT NUMBER.*MLKUP007
00375 *     IF THERE IS A MATCH IN THE DEPARTMENT NUMBER THE TABLE     *MLKUP007
00376 *     IS UPDATED WITH CHANGES IF THEY ARE NOT LISTED TO EITHER   *MLKUP007
00377 *     THE DIVISION OR THE BUYER NUMBER. OTHER WISE THE NEW       *MLKUP007
00378 *     DEPARTMENT NUMBER IS ADDED ALONG WITH THE CHANGE IN EITHER *MLKUP007
00379 *     THE DIVISION OR THE BUYER NUMBER. THE RECORD IS WRITTEN    *MLKUP007
00380 *     AND A NEW RECORD IS READ.                                  *MLKUP007
00381 *                                                                *MLKUP007
00382 *     PERFORMED BY B300-PROCESS-DATA                             *MLKUP007
00383 ******************************************************************MLKUP007
00384  C300-PROCESS-SALES-RECORDS.                                      MLKUP007
00385                                                                   MLKUP007
00386      IF D040-DEPT NOT EQUAL PV-DEPT                               MLKUP007
00387          PERFORM R200-READ-DEPT-DATA-BASE                         MLKUP007
00388      END-IF.                                                      MLKUP007
00389                                                                   MLKUP007
00390      MOVE D040-DIV                TO WS-DIV-2.                       CL**2
00391                                                                      CL**2
00392 *    TEST THE DIV ( DB2 VALUE AGAINST THE D040 VALUE )               CL**2
00393      IF PV-DIV = WS-DIV                                              CL**2
00394          NEXT SENTENCE                                               CL**2
00395      ELSE                                                            CL**2
00396          MOVE 'Y'                 TO PS-INVALID-DIV-SW               CL**2
00397          MOVE PV-DIV-2            TO D040-DIV                        CL**2
00398          ADD 1                    TO TC-DIV-DIFFERENCES              CL**2
00399      END-IF.                                                         CL**2
00400                                                                   MLKUP007
00401      MOVE D040-BUYER              TO WS-BUYER-2.                     CL**2
00402                                                                      CL**2
00403      IF PV-BUYER = WS-BUYER                                          CL**2
00404          NEXT SENTENCE                                               CL**2
00405      ELSE                                                            CL**2
00406          MOVE 'Y'                 TO PS-INVALID-BYR-SW               CL**2
00407          MOVE PV-BUYER-2          TO D040-BUYER                      CL**2
00408          ADD 1                    TO TC-BUYER-DIFFERENCES            CL**2
00409      END-IF.                                                         CL**2
00410                                                                   MLKUP007
00411      IF PS-INVALID-DIV OR PS-INVALID-BYR                             CL**2
00412              SET TI-INDEX             TO 1                        MLKUP007
00413              SEARCH TI-BUYER-DIV-RECORD                           MLKUP007
00414                  AT END MOVE 'N'      TO PS-FOUND-RECORD-SW       MLKUP007
00415                  WHEN D040-DEPT = TI-DEPT-NO (TI-INDEX)           MLKUP007
00416                      MOVE 'Y'         TO PS-FOUND-RECORD-SW       MLKUP007
00417                  WHEN TI-DEPT-NO (TI-INDEX) = HIGH-VALUES         MLKUP007
00418                      MOVE 'N'         TO PS-FOUND-RECORD-SW       MLKUP007
00419              END-SEARCH                                           MLKUP007
00420                                                                   MLKUP007
00421              IF PS-FOUND-RECORD                                   MLKUP007
00422                  PERFORM E100-INVALID-MOVE                        MLKUP007
00423              ELSE                                                 MLKUP007
00424                  PERFORM D300-ADD-DEPT-TO-TABLE                   MLKUP007
00425              END-IF                                               MLKUP007
00426                                                                   MLKUP007
00427      END-IF.                                                      MLKUP007
00428                                                                   MLKUP007
00429      PERFORM D100-WRITE-EDITED-SALES-DATA                         MLKUP007
00430                                                                   MLKUP007
00431      PERFORM R100-READ-UNEDITED-SALES-FILE.                       MLKUP007
00432                                                                   MLKUP007
00433 /                                                                 MLKUP007
00434 ******************************************************************MLKUP007
00435 * D100-WRITE-EDITED-SALES-DATA                                   *MLKUP007
00436 *     WRITES EDITED SALES DATA                                   *MLKUP007
00437 *                                                                *MLKUP007
00438 *     PERFORMED BY C300-PROCESS-SALES-DATA                       *MLKUP007
00439 ******************************************************************MLKUP007
00440  D100-WRITE-EDITED-SALES-DATA.                                    MLKUP007
00441                                                                   MLKUP007
00442          WRITE OUT-EDITED-SALES-RECORD                            MLKUP007
00443              FROM D040-RECORD.                                    MLKUP007
00444                                                                   MLKUP007
00445 /                                                                 MLKUP007
00446 ******************************************************************MLKUP007
00447 * D300-ADD-DEPT-TO-TABLE                                         *MLKUP007
00448 *     ADVANCES THE TE-INDEX TO KEEP TRACK OF THE NUMBER OF       *MLKUP007
00449 *     IN THE INVALID TABLE. IF THE TE-INDEX IS LARGER THAN THE   *MLKUP007
00450 *     NUMBER OF POSSIBLE ENTRIES THE PROGRAM ABENDS. OTHERWISE   *MLKUP007
00451 *     THE DEPARTMENT THAT IS NOT IN THE INVALID BUYER DIVISION   *MLKUP007
00452 *     TABLE IS ADDED AND SPACES ARE MOVED TO THE DISPLAY FIELDS  *MLKUP007
00453 *     OF THE OLD AND NEW DEPARTMENT AND BUYER FIELDS.            *MLKUP007
00454 *     THEN PERFORMS INVALID MOVE TO MOVE THE INVALID DIVISION OR *MLKUP007
00455 *     BUYER NUMBER INTO THE TABLE.                               *MLKUP007
00456 *                                                                *MLKUP007
00457 *     PERFORMED BY C300-PROCESS-SALES-RECORDS                    *MLKUP007
00458 ******************************************************************MLKUP007
00459  D300-ADD-DEPT-TO-TABLE.                                          MLKUP007
00460                                                                   MLKUP007
00461      SET TE-INDEX                     UP BY 1.                    MLKUP007
00462      SET TI-INDEX                     TO TE-INDEX.                MLKUP007
00463                                                                   MLKUP007
00464      IF TI-INDEX > MX-INDEX                                       MLKUP007
00465          DISPLAY '** ABEND **'                                    MLKUP007
00466          DISPLAY ' - PROGRAM = MLKUP007 - '                       MLKUP007
00467          DISPLAY 'ERROR ON INVALID TABLE'                         MLKUP007
00468          DISPLAY 'INVALID TABLE NEEDS TO BE INCREASED'            MLKUP007
00469          MOVE +4004                   TO ABEND-CODE               MLKUP007
00470          CALL 'ILBOABN0' USING ABEND-CODE                         MLKUP007
00471      ELSE                                                         MLKUP007
00472          MOVE D040-DEPT               TO TI-DEPT-NO (TI-INDEX)    MLKUP007
00473          MOVE 'DEPT '                 TO TI-DEPT (TI-INDEX)       MLKUP007
00474          MOVE SPACES                  TO TI-OLD-DIV-NO (TI-INDEX) MLKUP007
00475                                          TI-NEW-DIV-NO (TI-INDEX) MLKUP007
00476                                          TI-OLD-BYR-NO (TI-INDEX) MLKUP007
00477                                          TI-NEW-BYR-NO (TI-INDEX) MLKUP007
00478          MOVE 'Y'                     TO PS-FOUND-RECORD-SW       MLKUP007
00479                                                                   MLKUP007
00480          PERFORM E100-INVALID-MOVE                                MLKUP007
00481                                                                   MLKUP007
00482      END-IF.                                                      MLKUP007
00483                                                                   MLKUP007
00484 /                                                                 MLKUP007
00485 ******************************************************************MLKUP007
00486 * E100-INVALID-MOVE                                              *MLKUP007
00487 *     IF THE BUYER NUMBER OR THE DIVISION NUMBER ARE DIFFERENT   *MLKUP007
00488 *     THAN THE INFORMATION ON THE DEPT DATA BASE THE OLD AND     *MLKUP007
00489 *     NEW NUMBERS ARE MOVED INTO THE INVALID TABLE.              *MLKUP007
00490 *                                                                *MLKUP007
00491 *     PERFORMED BY C300-PROCESS-SALES-RECORDS                    *MLKUP007
00492 *                  D300-ADD-DEPT-TO-TABLE                        *MLKUP007
00493 ******************************************************************MLKUP007
00494  E100-INVALID-MOVE.                                               MLKUP007
00495                                                                   MLKUP007
00496      IF PS-INVALID-DIV                                            MLKUP007
00497              MOVE WS-DIV      TO TI-OLD-DIV-NO (TI-INDEX)            CL**2
00498              MOVE PV-DIV      TO TI-NEW-DIV-NO (TI-INDEX)            CL**2
00499              MOVE ' OLD DIV ' TO TI-OLD-DIV (TI-INDEX)            MLKUP007
00500              MOVE ' NEW DIV ' TO TI-NEW-DIV (TI-INDEX)            MLKUP007
00501              MOVE 'N'         TO PS-INVALID-DIV-SW                MLKUP007
00502      END-IF.                                                      MLKUP007
00503                                                                   MLKUP007
00504      IF PS-INVALID-BYR                                            MLKUP007
00505              MOVE WS-BUYER    TO TI-OLD-BYR-NO (TI-INDEX)            CL**2
00506              MOVE PV-BUYER    TO TI-NEW-BYR-NO (TI-INDEX)            CL**2
00507              MOVE ' OLD BYR ' TO TI-OLD-BYR (TI-INDEX)            MLKUP007
00508              MOVE ' NEW BYR ' TO TI-NEW-BYR (TI-INDEX)            MLKUP007
00509              MOVE 'N'         TO PS-INVALID-BYR-SW                MLKUP007
00510      END-IF.                                                      MLKUP007
00511                                                                   MLKUP007
00512 /                                                                 MLKUP007
00513 ******************************************************************MLKUP007
00514 * R100-READ-UNEDITED-SALES-FILE                                  *MLKUP007
00515 *     READS THE SORTED DAY SALES FILE OR THE SORTED MONTH TO     *MLKUP007
00516 *     DATE SALES FILE                                            *MLKUP007
00517 *                                                                *MLKUP007
00518 *     PERFORMED BY B300-PROCESS-DATA                             *MLKUP007
00519 *                  C300-PROCESS-SALES-RECORDS                    *MLKUP007
00520 ******************************************************************MLKUP007
00521  R100-READ-UNEDITED-SALES-FILE.                                   MLKUP007
00522                                                                   MLKUP007
00523      READ INP-UNEDITED-SALES-FILE INTO D040-RECORD                MLKUP007
00524          AT END                                                   MLKUP007
00525              MOVE 'Y'         TO PS-UNEDITED-SALES-EOF-SW.        MLKUP007
00526      ADD 1                    TO TC-NMBR-RECORDS-PROCESSED.       MLKUP007
00527                                                                   MLKUP007
00528 /                                                                 MLKUP007
00529 ******************************************************************MLKUP007
00530 * R200-READ-DEPT-DATA-BASE                                       *MLKUP007
00531 *     READS THE MERCHANDISING DATA BASE USING THE DEPARTMENT     *MLKUP007
00532 *     NUMBER OF THE INPUT RECORD. IF THE DEPARTMENT NUMBER IS    *MLKUP007
00533 *     IS NOT FOUND AN ERROR IS DISPLAYED. FOR ANY STATUS CODE    *MLKUP007
00534 *     OTHER THAN A NORMAL COMPLETION, THE PROGRAM IS ABENDED     *MLKUP007
00535 *                                                                *MLKUP007
00536 *     PERFORMED BY C300-PROCESS-SALES-RECORDS                    *MLKUP007
00537 ******************************************************************MLKUP007
00538  R200-READ-DEPT-DATA-BASE.                                        MLKUP007
00539                                                                   MLKUP007
00540      MOVE D040-DEPT                   TO PV-DEPT.                 MLKUP007
00541                                                                   MLKUP007
00542      EXEC SQL                                                     MLKUP007
00543           SELECT                                                     CL**2
00544                   GMA_NBR                                            CL**2
00545                 , BYR_NBR                                            CL**2
00546           INTO                                                       CL**2
00547                   :MDSEAR-GMA-NBR                                    CL**2
00548                 , :MDSEAR-BYR-NBR                                    CL**2
00549          FROM                                                        CL**2
00550                   TMDSEAR                                            CL**2
00551          WHERE                                                       CL**2
00552                   (DEPT_NBR = :PV-DEPT)                              CL**2
00553            AND    (STRT_DTE <= DATE(CURRENT TIMESTAMP))              CL**2
00554            AND    (END_DTE >= DATE(CURRENT TIMESTAMP))               CL**2
00555            AND    (OPERCO_NBR = '03')                                CL**2
00556            AND    (MAJ_CL_NBR = '00')                                CL**2
00557            AND    (SUB_CL_NBR = '000')                               CL**2
00558            AND    (MDSELV_CDE = 'DPT')                               CL**2
00559      END-EXEC.                                                       CL**2
00560                                                                      CL**2
00561      MOVE  MDSEAR-GMA-NBR  TO  PV-DIV.                               CL**2
00562      MOVE  MDSEAR-BYR-NBR  TO  PV-BUYER.                             CL**2
00563                                                                      CL**2
00564      EVALUATE TRUE                                                   CL**2
00565          WHEN SQLCODE = -904                                         CL**2
00566          WHEN SQLCODE = -911                                         CL**2
00567          WHEN SQLCODE = 0                                            CL**2
00568              CONTINUE                                                CL**2
00569          WHEN SQLCODE = +100                                         CL**2
00570              DISPLAY 'UNABLE TO MATCH ON DEPARTMENT NUMBER'          CL**2
00571              DISPLAY 'INPUT REC DEPARTMENT NUMBER = ' D040-DEPT      CL**2
00572              DISPLAY 'INPUT REC = ' D040-RECORD                      CL**2
00573              MOVE D040-DIV   TO  PV-DIV-2                            CL**2
00574              MOVE D040-BUYER TO  PV-BUYER-2                          CL**2
00575              CONTINUE                                                CL**2
00576          WHEN SQLWARN NOT = SPACES                                   CL**2
00577          WHEN SQLCODE NOT = ZERO                                     CL**2
00578              MOVE 'R200-READ-DEPT-DATA-BASE'                         CL**2
00579                              TO PV-PARAGRAPH-NAME                    CL**2
00580              MOVE 'SELECT DEPT FROM TMDSEAR'                         CL**2
00581                              TO PV-DB2-OPER-ATTEMPTED                CL**2
00582              PERFORM X100-SQL-ABEND                                  CL**2
00583      END-EVALUATE.                                                   CL**2
00584                                                                      CL**2
00585 /  EJECT                                                             CL**2
00586 ******************************************************************MLKUP007
00587 * X100-SQL-ABEND                                                 *   CL**2
00588 *     THIS PARAGRAPH WILL FORCE THE PROGRAM TO ABEND BECAUSE     *   CL**2
00589 *     OF A SERIOUS DB2 ERROR.                                    *   CL**2
00590 *                                                                *   CL**2
00591 *     PERFORMED BY R200-READ-DEPT-DATA-BASE                      *   CL**2
00592 ******************************************************************MLKUP007
00593  X100-SQL-ABEND.                                                     CL**2
00594                                                                      CL**2
00595      SET ABEND-4013                   TO TRUE.                       CL**2
00596                                                                      CL**2
00597      DISPLAY '**  ABEND     **'                                      CL**2
00598      DISPLAY '**  PROGRAM   **  =  MLKUP007'.                        CL**2
00599      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.               CL**2
00600      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.           CL**2
00601                                                                      CL**2
00602      DISPLAY 'INPUT REC = ' D040-RECORD.                             CL**2
00603                                                                   MLKUP007
00604 ******************************************************************   CL**2
00605 *    THE FOLLOWING INCLUDE COPYBOOK USES THE DB2 ABEND MODULE    *   CL**2
00606 *    'DSNTIAR' TO CONVERT THE SQLCA INTO MEANINGFUL VERBAGE.     *   CL**2
00607 ******************************************************************   CL**2
00608 *                                                                 MLKUP007
00609      COPY DPPD004.                                                   CL**2
00610 *                                                                 MLKUP007
00611      CALL 'ILBOABN0' USING ABEND-CODE.                               CL**2
00612                                                                   MLKUP007
