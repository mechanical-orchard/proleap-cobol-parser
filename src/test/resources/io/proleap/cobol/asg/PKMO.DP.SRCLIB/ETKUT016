       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    ETKUT016.                                                 
       AUTHOR.        C WYSOCKI.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  01/29/2004.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *                                                                         
      *    THIS PROGRAM PREPARES THE EXTRACTED SALES, INVENTORY AND             
      *    ON ORDER DATA FOR THE EDI 852 - PRODUCT ACTIVITY DATA                
      *    TRANSLATION PROCESS.                                                 
      *                                                                         
*******************************************************************             
JF0305*  DATE  02/17/2005                                                       
JF0305*  CHANGE MADE:                                                           
JF0305*   UPDATED PROGRAM FOR STORE EXPANSION TO FIVE DIGITS.                   
JF0305*  MADE BY: JON FIEDLER             WR# : WR26913                         
************************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATA-EXTRACT-FILE                                             
              ASSIGN TO INP01.                                                  
                                                                                
           SELECT SOURCE-EDI-FILE                                               
              ASSIGN TO OUT01.                                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  DATA-EXTRACT-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS DATA-EXTRACT-RECORD.                                  
                                                                                
       01  DATA-EXTRACT-RECORD                  PIC X(80).                      
                                                                                
       FD  SOURCE-EDI-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
JF0305     RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS SOURCE-EDI-RECORD.                                    
                                                                                
JF0305 01  SOURCE-EDI-RECORD                   PIC X(150).                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *****************************************************************         
      *    INPUT RECORD LAYOUT - EDI 852 DATA EXTRACT RECORD                    
      *****************************************************************         
           COPY ETRD008.                                                        
                                                                                
      *****************************************************************         
      *    OUTPUT RECORD LAYOUT - EDI 852 DATA SOURCE RECORD                    
      *****************************************************************         
           COPY ETRD019.                                                        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
                                                                                
           05  PC-PROGRAM-NAME                  PIC X(8)  VALUE                 
                                                          'ETKUT016'.           
           05  PC-YES                           PIC X(1)  VALUE 'Y'.            
           05  PC-NO                            PIC X(1)  VALUE 'N'.            
           05  PC-MAX-SUB                       PIC 9(2)  VALUE 10.             
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
                                                                                
           05  PS-INPUT-EOF-SW                  PIC X(1)  VALUE 'N'.            
               88  PS-EOF-INPUT                           VALUE 'Y'.            
                                                                                
           05  PS-FIRST-RECORD-SW               PIC X(1)  VALUE 'Y'.            
               88  PS-FIRST-RECORD                        VALUE 'Y'.            
                                                                                
           05  PS-DETAIL-PROCESSED-SW           PIC X(1)  VALUE 'N'.            
               88  PS-DETAIL-PROCESSED                    VALUE 'Y'.            
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
                                                                                
           05  PV-PARAGRAPH-NAME                PIC X(30).                      
           05  PV-DB2-OP-ATTEMPTED              PIC X(30).                      
           05  PV-REC-TYPE                      PIC 9(1).                       
           05  PV-ENT-DEPT.                                                     
               10 PV-ENT-ID                     PIC 9(10).                      
               10 PV-DEPT                       PIC X(4).                       
           05  IN-ENT-DEPT.                                                     
               10 IN-ENT-ID                     PIC 9(10).                      
               10 IN-DEPT                       PIC X(4).                       
           05  PV-PARENT                        PIC 9(10).                      
           05  PV-UPC-CODE                      PIC 9(15).                      
           05  PV-SUB                           PIC 9(2).                       
SP1104                                                                          
SP1104     05  PV-CHAR-UPC-CODE      PIC X(15).                                 
SP1104     05  FILLER REDEFINES PV-CHAR-UPC-CODE.                               
SP1104         10  FILLER            PIC X(2).                                  
SP1104         10  PV-EAN            PIC X(1).                                  
SP1104         10  FILLER            PIC X(12).                                 
                                                                                
       01  PV-DETAIL-DATA.                                                      
           05  PV-STORE-TABLE                   OCCURS 10 TIMES.                
JF0305         10 PV-STORE                      PIC X(5).                       
               10 PV-QUANTITY                   PIC S9(7).                      
JF0305     05  FILLER                           PIC X(28) VALUE SPACE.          
                                                                                
       01  ABEND-CODE                           PIC S9(04) COMP SYNC            
                                                VALUE +4013.                    
                                                                                
           COPY DPWS004.                                                        
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    EDI TRADING PARTNER DB2 TABLE LAYOUTS                                
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDMLT                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    CURSOR TO GET PARENT ID                                              
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
             DECLARE PARENT_ENT CURSOR FOR                                      
             SELECT  PRNT_ENT_ID                                                
             FROM    TTRDMLT,                                                   
                     TTRDPRT                                                    
             WHERE   VEND_INCHG_ID_DESC = VND_INCHG_ID_DESC                     
               AND   VEND_INCHG_ID = VND_INCHG_ID                               
               AND   ENT_ID = :TRDPRT-ENT-ID                                    
           END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
                                                                                
      ******************************************************************        
      *     OPEN ALL FILES                                                      
      *     PERFORM INITIALIZATIONS                                             
      *     PROCESS EACH INPUT RECORD UNTIL END-OF-FILE                         
      *     CLOSE ALL FILES                                                     
      ******************************************************************        
                                                                                
           MOVE 'A100-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                        
                                                                                
           OPEN INPUT  DATA-EXTRACT-FILE                                        
                OUTPUT SOURCE-EDI-FILE.                                         
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM X100-READ-INPUT-FILE THRU X100-EXIT.                         
                                                                                
           PERFORM B200-PROCESS-INPUT THRU B200-EXIT                            
             UNTIL PS-EOF-INPUT.                                                
                                                                                
           IF PV-SUB > ZERO                                                     
              MOVE PC-MAX-SUB TO PV-SUB                                         
              PERFORM C400-PROCESS-DETAIL THRU C400-EXIT                        
           END-IF.                                                              
                                                                                
           CLOSE DATA-EXTRACT-FILE                                              
                 SOURCE-EDI-FILE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       B100-HOUSEKEEPING.                                                       
                                                                                
      ******************************************************************        
      *     ONE TIME PROCESSING AND INITIALIZATIONS PERFORMED                   
      ******************************************************************        
                                                                                
           MOVE 'B100-HOUSEKEEPING' TO PV-PARAGRAPH-NAME.                       
                                                                                
           MOVE ZEROES TO PV-REC-TYPE.                                          
           MOVE ZEROES TO PV-ENT-ID.                                            
           MOVE SPACES TO PV-DEPT.                                              
           MOVE ZEROES TO PV-UPC-CODE.                                          
           MOVE SPACES TO ET019-RECORD-TYPE.                                    
           MOVE SPACES TO ET019-DATA-AREA.                                      
                                                                                
           PERFORM C410-CLEAR-TABLE THRU C410-EXIT.                             
                                                                                
       B100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       B200-PROCESS-INPUT.                                                      
                                                                                
           MOVE 'B200-PROCESS-INPUT' TO PV-PARAGRAPH-NAME.                      
                                                                                
           MOVE PC-NO TO PS-DETAIL-PROCESSED-SW.                                
                                                                                
           IF PS-FIRST-RECORD                                                   
              MOVE PC-NO TO PS-FIRST-RECORD-SW                                  
              PERFORM C100-PROCESS-HEADER THRU C100-EXIT                        
              PERFORM C200-PROCESS-ITEM THRU C200-EXIT                          
              PERFORM C300-PROCESS-DETAIL-TYPE THRU C300-EXIT                   
           END-IF.                                                              
                                                                                
           IF IN-ENT-DEPT = PV-ENT-DEPT                                         
              CONTINUE                                                          
           ELSE                                                                 
              MOVE PC-MAX-SUB TO PV-SUB                                         
              PERFORM C400-PROCESS-DETAIL THRU C400-EXIT                        
              PERFORM C100-PROCESS-HEADER THRU C100-EXIT                        
              PERFORM C200-PROCESS-ITEM THRU C200-EXIT                          
              PERFORM C300-PROCESS-DETAIL-TYPE THRU C300-EXIT                   
           END-IF.                                                              
                                                                                
           IF ET008-UPC-CODE = PV-UPC-CODE                                      
              CONTINUE                                                          
           ELSE                                                                 
              MOVE PC-MAX-SUB TO PV-SUB                                         
              PERFORM C400-PROCESS-DETAIL THRU C400-EXIT                        
              PERFORM C200-PROCESS-ITEM THRU C200-EXIT                          
              PERFORM C300-PROCESS-DETAIL-TYPE THRU C300-EXIT                   
           END-IF.                                                              
                                                                                
           IF ET008-REC-TYPE = PV-REC-TYPE                                      
              CONTINUE                                                          
           ELSE                                                                 
              MOVE PC-MAX-SUB TO PV-SUB                                         
              PERFORM C400-PROCESS-DETAIL THRU C400-EXIT                        
              PERFORM C300-PROCESS-DETAIL-TYPE THRU C300-EXIT                   
           END-IF.                                                              
                                                                                
           IF PS-DETAIL-PROCESSED                                               
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM C400-PROCESS-DETAIL THRU C400-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM X100-READ-INPUT-FILE THRU X100-EXIT.                         
                                                                                
       B200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       C100-PROCESS-HEADER.                                                     
                                                                                
           MOVE 'C100-PROCESS-HEADER' TO PV-PARAGRAPH-NAME.                     
                                                                                
            IF ET008-ENT-ID = PV-ENT-ID                                         
               CONTINUE                                                         
            ELSE                                                                
               PERFORM C110-CHECK-FOR-PARENT THRU C110-EXIT                     
               MOVE ET008-ENT-ID TO PV-ENT-ID                                   
            END-IF.                                                             
                                                                                
            MOVE ET008-DEPT TO PV-DEPT.                                         
                                                                                
            SET ET019-HEADER-RECORD TO TRUE.                                    
            MOVE PV-PARENT TO ET019-ENT-ID.                                     
            MOVE ET008-START-DATE TO ET019-START-DATE.                          
            MOVE ET008-END-DATE TO ET019-END-DATE.                              
            MOVE ET008-DEPT TO ET019-DEPARTMENT.                                
                                                                                
            PERFORM X200-WRITE-SOURCE-FILE THRU X200-EXIT.                      
                                                                                
       C100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       C110-CHECK-FOR-PARENT.                                                   
                                                                                
           MOVE 'C110-CHECK-FOR-PARENT' TO PV-PARAGRAPH-NAME.                   
                                                                                
           MOVE ET008-ENT-ID TO TRDPRT-ENT-ID.                                  
                                                                                
           EXEC SQL                                                             
               OPEN PARENT_ENT                                                  
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
              CONTINUE                                                          
           ELSE                                                                 
              MOVE 'OPEN CURSOR' TO PV-DB2-OP-ATTEMPTED                         
              PERFORM Z999-SQL-ABEND THRU Z999-EXIT                             
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
                FETCH PARENT_ENT                                                
                INTO :TRDMLT-PRNT-ENT-ID                                        
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
              MOVE TRDMLT-PRNT-ENT-ID TO PV-PARENT                              
           ELSE                                                                 
              IF SQLCODE = 100                                                  
                 MOVE ET008-ENT-ID TO PV-PARENT                                 
              ELSE                                                              
                 MOVE 'FETCH CURSOR' TO PV-DB2-OP-ATTEMPTED                     
                 PERFORM Z999-SQL-ABEND THRU Z999-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               CLOSE PARENT_ENT                                                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
              CONTINUE                                                          
           ELSE                                                                 
              MOVE 'CLOSE CURSOR' TO PV-DB2-OP-ATTEMPTED                        
              PERFORM Z999-SQL-ABEND THRU Z999-EXIT                             
           END-IF.                                                              
                                                                                
       C110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       C200-PROCESS-ITEM.                                                       
                                                                                
            MOVE 'C200-PROCESS-ITEM' TO PV-PARAGRAPH-NAME.                      
                                                                                
            MOVE ET008-UPC-CODE TO PV-UPC-CODE.                                 
SP1104      MOVE ET008-UPC-CODE TO PV-CHAR-UPC-CODE.                            
SP1104      IF PV-EAN = '0'                                                     
SP1104         MOVE 'UP' TO ET019-UPC-TYPE                                      
SP1104      ELSE                                                                
SP1104         MOVE 'EN' TO ET019-UPC-TYPE.                                     
            MOVE ET008-UPC-CODE TO ET019-UPC-CODE.                              
                                                                                
            SET ET019-ITEM-RECORD TO TRUE.                                      
            PERFORM X200-WRITE-SOURCE-FILE THRU X200-EXIT.                      
                                                                                
       C200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       C300-PROCESS-DETAIL-TYPE.                                                
                                                                                
            MOVE 'C300-PROCESS-DETAIL-TYPE' TO PV-PARAGRAPH-NAME.               
                                                                                
            MOVE ET008-REC-TYPE TO PV-REC-TYPE.                                 
                                                                                
            EVALUATE TRUE                                                       
               WHEN ET008-SALES-REC                                             
                    MOVE 'QS' TO ET019-DETAIL-TYPE                              
               WHEN ET008-ONHAND-REC                                            
                    MOVE 'QA' TO ET019-DETAIL-TYPE                              
               WHEN ET008-ON-ORDER-REC                                          
                    MOVE 'QP' TO ET019-DETAIL-TYPE                              
            END-EVALUATE.                                                       
                                                                                
            SET ET019-DETAIL-TYPE-RECORD TO TRUE.                               
            PERFORM X200-WRITE-SOURCE-FILE THRU X200-EXIT.                      
                                                                                
       C300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       C400-PROCESS-DETAIL.                                                     
                                                                                
            IF PV-SUB = PC-MAX-SUB                                              
               SET ET019-DETAIL-QTY-RECORD TO TRUE                              
               MOVE PV-DETAIL-DATA TO ET019-DETAIL-QTY-DATA                     
               PERFORM X200-WRITE-SOURCE-FILE THRU X200-EXIT                    
               PERFORM C410-CLEAR-TABLE THRU C410-EXIT                          
            END-IF.                                                             
                                                                                
            ADD 1 TO PV-SUB.                                                    
            MOVE ET008-STORE-NUMBER TO PV-STORE(PV-SUB).                        
            MOVE ET008-ITEM-QUANTITY TO PV-QUANTITY(PV-SUB).                    
                                                                                
            SET PS-DETAIL-PROCESSED TO TRUE.                                    
                                                                                
       C400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       C410-CLEAR-TABLE.                                                        
                                                                                
            PERFORM C411-INITIALIZE THRU C411-EXIT                              
               VARYING PV-SUB FROM 1 BY 1                                       
               UNTIL PV-SUB > PC-MAX-SUB.                                       
                                                                                
            MOVE ZERO TO PV-SUB.                                                
                                                                                
       C410-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       C411-INITIALIZE.                                                         
                                                                                
            MOVE SPACES TO PV-STORE(PV-SUB).                                    
            MOVE ZEROES TO PV-QUANTITY(PV-SUB).                                 
                                                                                
       C411-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       X100-READ-INPUT-FILE.                                                    
                                                                                
           MOVE 'X100-READ-INPUT-FILE' TO PV-PARAGRAPH-NAME.                    
                                                                                
           READ DATA-EXTRACT-FILE                                               
                INTO ET008-EDI-852-EXTRACT-REC                                  
                AT END MOVE PC-YES TO PS-INPUT-EOF-SW.                          
                                                                                
           IF PS-EOF-INPUT                                                      
              CONTINUE                                                          
           ELSE                                                                 
              MOVE ET008-ENT-ID TO IN-ENT-ID                                    
              MOVE ET008-DEPT   TO IN-DEPT                                      
           END-IF.                                                              
                                                                                
       X100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       X200-WRITE-SOURCE-FILE.                                                  
                                                                                
           MOVE 'X200-WRITE-SOURCE-FILE' TO PV-PARAGRAPH-NAME.                  
                                                                                
           WRITE SOURCE-EDI-RECORD                                              
              FROM ET019-PRODUCT-ACTIVITY-RECORD.                               
                                                                                
           MOVE SPACES TO ET019-RECORD-TYPE.                                    
           MOVE SPACES TO ET019-DATA-AREA.                                      
                                                                                
       X200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       Z999-SQL-ABEND.                                                          
                                                                                
           MOVE 'Z999-SQL-ABEND' TO PV-PARAGRAPH-NAME.                          
                                                                                
           DISPLAY '  '.                                                        
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'PROGRAM   = ' PC-PROGRAM-NAME.                              
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OP-ATTEMPTED.                          
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       Z999-EXIT.                                                               
           EXIT.                                                                
