       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    EIKUT007.                                                 
       AUTHOR.        KARI HAAK.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  11/17/11.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM READS THE PO ON-ORDER EXTRACT FILE AND UNPACKS IT.         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT PO-ONORD-EXT-FILE ASSIGN TO INP01.                            
           SELECT EI-ONORD-EXT-FILE ASSIGN TO OUT01.                            
           SELECT EI-ONORD-CNT-FILE ASSIGN TO OUT02.                            
                                                                                
      ****************************************************************          
      * DATA DIVISION:                                                          
      ****************************************************************          
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PO-ONORD-EXT-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PO-ONORD-EXT-RECORD              PIC X(104).                         
                                                                                
       FD  EI-ONORD-EXT-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  EI-ONORD-EXT-RECORD              PIC X(173).                         
                                                                                
       FD  EI-ONORD-CNT-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  EI-ONORD-CNT-RECORD              PIC X(15).                          
                                                                                
      ****************************************************************          
      * WORKING STORAGE:                                                        
      ****************************************************************          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04) COMP VALUE +0.           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'EIKUT007'.        
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05 PA-INPUT-REC-COUNT            PIC S9(9)  COMP-3 VALUE 0.          
           05 PA-OUTPUT-REC-COUNT           PIC S9(9)  COMP-3 VALUE 0.          
           05 PA-OUT2-REC-COUNT             PIC S9(9)  COMP-3 VALUE 0.          
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05 PV-ONORD-ITEM-NBR             PIC S9(15)V     VALUE 0.            
           05 PV-ONORD-PO-NBR               PIC S9(09)V     VALUE 0.            
           05 PV-ONORD-SKU                  PIC S9(08)V     VALUE 0.            
           05 PV-TEMP-QTY                   PIC S9(09)V     VALUE 0.            
           05 PV-TEMP-AMT                   PIC S9(09)V9(2) VALUE 0.            
           05 PV-OUTPUT-REC-COUNT           PIC +9(14).                         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-PO-ONORD-EOF-SW           PIC X(01)  VALUE 'N'.               
               88  PS-PO-ONORD-EOF                     VALUE 'Y'.               
                                                                                
      ********************************************************                  
      * INPUT RECORD LAYOUT - PO ON-ORDER EXTRACT FILE                          
      ********************************************************                  
           COPY PORD018.                                                        
                                                                                
      ********************************************************                  
      * OUTPUT RECORD LAYOUT - EI ON-ORDER EXTRACT FILE (UNPACKED)              
      ********************************************************                  
           COPY EIRD003.                                                        
                                                                                
      ******************************************************************        
      *  PROCEDURE DIVISION.                                                    
      ******************************************************************        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE-SECTION.                                                   
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS-EXTRACT-FILE                                    
               UNTIL PS-PO-ONORD-EOF.                                           
                                                                                
           PERFORM 8500-WRITE-COUNT-FILE.                                       
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  SET UP FOR PROGRAM PROCESSING:                                         
      *    OPEN ALL FILES.                                                      
      *    READ THE FIRST PO ON-ORDER EXTRACT RECORD.                           
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  PO-ONORD-EXT-FILE                                        
                OUTPUT EI-ONORD-EXT-FILE                                        
                OUTPUT EI-ONORD-CNT-FILE.                                       
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 7000-READ-PO-ONORD-EXT-FILE.                                 
                                                                                
      ******************************************************************        
      *  PROCESS THE PO ON-ORDER EXTRACT FILE.                                  
      ******************************************************************        
       2000-PROCESS-EXTRACT-FILE.                                               
                                                                                
           INITIALIZE EI003-ONORD-EXTRACT-RECORD.                               
                                                                                
           MOVE PO018-ONORD-ITEM-NBR    TO PV-ONORD-ITEM-NBR.                   
           MOVE PV-ONORD-ITEM-NBR       TO EI003-ONORD-ITEM-NBR.                
                                                                                
           MOVE PO018-ONORD-WEEK-DATE   TO EI003-ONORD-WEEK-DATE.               
           MOVE PO018-ONORD-IN-DC-DATE  TO EI003-ONORD-IN-DC-DATE.              
                                                                                
           MOVE PO018-ONORD-STORE-NBR   TO EI003-ONORD-STORE-NBR.               
                                                                                
           MOVE PO018-ONORD-STATUS-CODE TO EI003-ONORD-STATUS-CODE.             
                                                                                
           MOVE PO018-ONORD-UNIT-QTY    TO PV-TEMP-QTY.                         
           MOVE PV-TEMP-QTY             TO EI003-ONORD-UNIT-QTY.                
                                                                                
           MOVE PO018-ONORD-COST-AMT    TO PV-TEMP-AMT.                         
           MOVE PV-TEMP-AMT             TO EI003-ONORD-COST-AMT.                
                                                                                
           MOVE PO018-ONORD-RTL-AMT     TO PV-TEMP-AMT.                         
           MOVE PV-TEMP-AMT             TO EI003-ONORD-RTL-AMT.                 
                                                                                
           MOVE PO018-ONORD-PO-NBR      TO PV-ONORD-PO-NBR.                     
           MOVE PV-ONORD-PO-NBR         TO EI003-ONORD-PO-NBR.                  
                                                                                
           MOVE PO018-ONORD-BOUGHT-DATE TO EI003-ONORD-BOUGHT-DATE.             
           MOVE PO018-ONORD-PROMO-IND   TO EI003-ONORD-PROMO-IND.               
                                                                                
           MOVE PO018-ONORD-PROMO-QTY   TO PV-TEMP-QTY.                         
           MOVE PV-TEMP-QTY             TO EI003-ONORD-PROMO-QTY.               
                                                                                
           MOVE PO018-ONORD-SKU         TO PV-ONORD-SKU.                        
           MOVE PV-ONORD-SKU            TO EI003-ONORD-SKU.                     
                                                                                
           MOVE PO018-ONORD-ENT-ID      TO EI003-ONORD-ENT-ID.                  
                                                                                
           MOVE PO018-ONORD-GROUP-CODE  TO EI003-ONORD-GROUP-CODE.              
           MOVE PO018-ONORD-LONG-VENDOR-STYLE                                   
                                       TO EI003-ONORD-LONG-VENDOR-STYLE.        
           MOVE PO018-ONORD-PO-TYP-CDE  TO EI003-ONORD-PO-TYP-CDE.              
           MOVE PO018-ONORD-DEPT-NBR    TO EI003-ONORD-DEPT-NBR.                
                                                                                
           PERFORM 8000-WRITE-OUTPUT-RECORD.                                    
                                                                                
           PERFORM 7000-READ-PO-ONORD-EXT-FILE.                                 
                                                                                
      ******************************************************************        
      *  READ A PO ON-ORDER EXTRACT RECORD.                                     
      ******************************************************************        
       7000-READ-PO-ONORD-EXT-FILE.                                             
                                                                                
           READ PO-ONORD-EXT-FILE INTO PO018-ONORD-EXTRACT-RECORD               
             AT END                                                             
                MOVE 'Y' TO PS-PO-ONORD-EOF-SW.                                 
                                                                                
           IF NOT PS-PO-ONORD-EOF                                               
                ADD 1 TO PA-INPUT-REC-COUNT.                                    
                                                                                
      ******************************************************************        
      *  WRITE AN EI ON-ORDER EXTRACT RECORD.                                   
      ******************************************************************        
       8000-WRITE-OUTPUT-RECORD.                                                
                                                                                
           WRITE EI-ONORD-EXT-RECORD                                            
               FROM EI003-ONORD-EXTRACT-RECORD.                                 
                                                                                
           ADD 1 TO PA-OUTPUT-REC-COUNT.                                        
                                                                                
      ******************************************************************        
      *  WRITE OUT THE NUMBER OF RECORDS THAT WERE WRITTEN TO THE               
      *  EXTRACT FILE.                                                          
      ******************************************************************        
       8500-WRITE-COUNT-FILE.                                                   
                                                                                
           INITIALIZE PV-OUTPUT-REC-COUNT.                                      
           MOVE PA-OUTPUT-REC-COUNT      TO PV-OUTPUT-REC-COUNT.                
                                                                                
           WRITE EI-ONORD-CNT-RECORD                                            
               FROM PV-OUTPUT-REC-COUNT.                                        
                                                                                
           ADD 1 TO PA-OUT2-REC-COUNT.                                          
                                                                                
      ******************************************************************        
      *  DISPLAY CONTROL TOTALS AND CLOSE ALL FILES.                            
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
                                                                                
           DISPLAY ' PO ON-ORDER RECORDS READ:    ' PA-INPUT-REC-COUNT.         
           DISPLAY ' EI ON-ORDER RECORDS WRITTEN: ' PA-OUTPUT-REC-COUNT.        
           DISPLAY ' EI COUNT RECORD WRITTEN:     ' PA-OUT2-REC-COUNT.          
                                                                                
           CLOSE PO-ONORD-EXT-FILE                                              
                 EI-ONORD-EXT-FILE                                              
                 EI-ONORD-CNT-FILE.                                             
                                                                                
