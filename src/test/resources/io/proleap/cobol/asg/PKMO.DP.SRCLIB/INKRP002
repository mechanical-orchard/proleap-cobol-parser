000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    INKRP002.                                                 
000300 AUTHOR.        JOHN SELWITSCHKA.                                         
000400 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000500 DATE-WRITTEN.  JANUARY 2008.                                             
000600 DATE-COMPILED.                                                           
000700******************************************************************        
000900* PHYSICAL INVENTORY STORE EXPECTED ADJUSTMENT SUMMARY REPORT             
000900*                                                                         
000900* THIS PROGRAM READS A FILE FROM INKUT298 (PHYSICAL INVENTORY             
000900* STORE EXPECTED ADJUSTMENTS) AND CREATES A STORE LEVEL SUMMARY           
000900* REPORT.                                                                 
001300******************************************************************        
006500 ENVIRONMENT DIVISION.                                                    
006600*                                                                         
006700 CONFIGURATION SECTION.                                                   
006800*                                                                         
006900 SOURCE-COMPUTER.        IBM-3090.                                        
007000 OBJECT-COMPUTER.        IBM-3090.                                        
007100*                                                                         
007200 INPUT-OUTPUT SECTION.                                                    
007300 FILE-CONTROL.                                                            
007400                                                                          
007500     SELECT REPORT-SUMMARY-FILE                                           
007600         ASSIGN TO INP01.                                                 
007700                                                                          
008400     SELECT STORE-EXP-SUM-ADJ-RPT                                         
008500         ASSIGN TO RPT01.                                                 
008600                                                                          
008700******************************************************************        
008800 DATA DIVISION.                                                           
008900******************************************************************        
009000                                                                          
009100 FILE SECTION.                                                            
009200                                                                          
009300 FD  REPORT-SUMMARY-FILE                                                  
009400     LABEL RECORDS ARE STANDARD                                           
009500     RECORDING MODE IS F                                                  
009600     BLOCK CONTAINS 0 RECORDS.                                            
009700 01  REPORT-SUMMARY-REC          PIC X(150).                              
009800                                                                          
011300 FD  STORE-EXP-SUM-ADJ-RPT                                                
011400     LABEL RECORDS ARE OMITTED                                            
011500     RECORDING MODE IS F                                                  
011600     BLOCK CONTAINS 0 RECORDS.                                            
011800 01  STORE-EXP-SUM-ADJ-REC       PIC X(132).                              
012000                                                                          
012100******************************************************************        
012200 WORKING-STORAGE SECTION.                                                 
012300******************************************************************        
012400                                                                          
012500 01  PS-PROGRAM-SWITCHES.                                                 
012600     05  PS-MORE-SUM-RECS-SW                                              
012700                                 PIC X       VALUE 'Y'.                   
012800         88  PS-MORE-SUM-RECS                VALUE 'Y'.                   
012900         88  PS-NO-MORE-SUM-RECS             VALUE 'N'.                   
013000                                                                          
013100 01  PA-PROGRAM-ACCUMULATORS                 COMP-3.                      
013200     05  PA-SUM-RECORDS-READ     PIC S9(13)  VALUE ZERO.                  
013300     05  PA-REPORT-LINES-WRITTEN PIC S9(13)  VALUE ZERO.                  
           05  PA-TOT-ADJ-UNITS        PIC S9(09)  VALUE ZERO.                  
           05  PA-TOT-EXTD-RTL-AMT     PIC S9(14)V99 VALUE ZERO.                
013400                                                                          
       01  CD-COUNTER-DISPLAY.                                                  
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.                   
                                                                                
013800 01  PC-PROGRAM-CONSTANTS.                                                
013900     05  PC-MAX-LINE-COUNT       PIC S9(3) COMP-3 VALUE +55.              
014000     05  PC-MSG-IDX-MAX          PIC S9(3) COMP-3 VALUE +3.               
           05  PC-PROGRAN-NAME         PIC X(08) VALUE 'INKRP002'.              
014900                                                                          
015000 01  PV-PROGRAM-VARIABLES.                                                
015100     05  PV-LINE-COUNT           PIC S9(3) COMP-3.                        
015200     05  PV-PAGE-COUNT           PIC S9(7) COMP-3.                        
015300                                                                          
015400     05  PV-DATE-YYMMDD.                                                  
015500         10  PV-DATE-YYMMDD-YY   PIC XX.                                  
015600         10  PV-DATE-YYMMDD-MM   PIC XX.                                  
015700         10  PV-DATE-YYMMDD-DD   PIC XX.                                  
015800     05  PV-TIME-HHMMSS.                                                  
015900         10  PV-TIME-HHMMSS-HH   PIC XX.                                  
016000         10  PV-TIME-HHMMSS-MM   PIC XX.                                  
016100         10  PV-TIME-HHMMSS-SS   PIC XX.                                  
016200                                                                          
015800     05  PV-DKEY.                                                         
015900         10  PV-DKEY-NBR         PIC X(09).                               
016000         10  FILLER              PIC X(06).                               
016200                                                                          
016300     05  PV-SPLIT-DATE.                                                   
016400         10  PV-SPLIT-DATE-CCYY  PIC X(4).                                
016500         10  PV-HYPHEN-1         PIC X.                                   
016600         10  PV-SPLIT-DATE-MM    PIC X(2).                                
016700         10  PV-HYPHEN-2         PIC X.                                   
016800         10  PV-SPLIT-DATE-DD    PIC X(2).                                
016900     05  PV-FRMT-DATE.                                                    
017000         10  PV-FRMT-DATE-MM     PIC X(2).                                
017100         10  PV-HYPHEN-1ST       PIC X.                                   
017200         10  PV-FRMT-DATE-DD     PIC X(2).                                
017300         10  PV-HYPHEN-2ND       PIC X.                                   
017400         10  PV-FRMT-DATE-CCYY   PIC X(4).                                
017500                                                                          
017600     05  PV-DIFFERENCE           PIC S9(9)V99 COMP-3.                     
           05  PV-ADJ-UNITS            PIC S9(06)   COMP-3 VALUE +0.            
           05  PV-EXTD-RLT-AMT         PIC S9(11)V99 COMP-3 VALUE +0.           
024500                                                                          
024600 01 ABEND-AREAS.                                                          
024700     05 ABEND-CODE               PIC S9(04)  VALUE ZERO                   
024800                                             COMP SYNC.                   
024900         88  AC-BAD-DATA                     VALUE +4003.                 
025000         88  AC-DB2-ERROR                    VALUE +4013.                 
025100     05  AA-ABEND-LIT            PIC X(40)   VALUE                        
025200           '*****       ABEND'.                                           
025300     05  AA-PROGRAM-LIT          PIC X(40)   VALUE                        
025400             '*****   PROGRAM: INKRP002'.                                 
025500     05  AA-PARAGRAPH-LIT.                                                
025600         10  FILLER              PIC X(17)   VALUE                        
025700             '***** PARAGRAPH: '.                                         
025800         10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.                
025900     05  AA-MESSAGE-LINE.                                                 
026000         10  FILLER              PIC X(06)   VALUE '*****'.               
026100         10  AA-MESSAGE          PIC X(127)  VALUE SPACES.                
026200     05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                        
026300             '*****    DB2 ERROR'.                                        
026400     05  AA-DB2-OPERATION-LIT.                                            
026500         10  FILLER              PIC X(17)   VALUE                        
026600             '***** OPERATION: '.                                         
026700         10  AA-DB2-OPERATION.                                            
026800             15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.                
026900             15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.                
027000     05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.                
027100     05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.                
027200     05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.                
027300     05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.                
027400                                                                          
027500 EJECT                                                                    
027600                                                                          
028400*** STORE EXPECTED REPORT DETAIL COPYBOOK                                 
028500     COPY INRD030.                                                        
028600                                                                          
029000 EJECT                                                                    
029100                                                                          
029200******************************************************************        
029300***  STANDARD REPORT HEADING (150 BYTES)                                  
029400******************************************************************        
029700     COPY DPWS132O.                                                       
029800                                                                          
029900******************************************************************        
030000***  REPORT TITLE LINE                                                    
030100******************************************************************        
030200 01  HD-REPORT-TITLE-2.                                                   
030300     05  FILLER                  PIC X       VALUE SPACE.                 
030400     05  FILLER                  PIC X(38)   VALUE SPACES.                
030500     05  FILLER                  PIC X(56)   VALUE                        
030600       'PHYSICAL INVENTORY STORE EXPECTED ADJUSTMENT SUMMARY RPT'.        
031200     05  FILLER                  PIC X(38)   VALUE SPACES.                
031300                                                                          
032400******************************************************************        
032500***  REPORT COLUMN HEADING LINE                                           
032600******************************************************************        
035200 01  HD-COLUMN-HEADING-1.                                                 
035300     05  FILLER                  PIC X       VALUE SPACE.                 
035400     05  FILLER                  PIC X(05)   VALUE 'STORE'.               
035500     05  FILLER                  PIC X(04)   VALUE SPACES.                
035600     05  FILLER                  PIC X(10)   VALUE 'STORE NAME'.          
035610     05  FILLER                  PIC X(30)   VALUE SPACES.                
035620     05  FILLER                  PIC X(11)   VALUE 'TOTAL UNITS'.         
035700     05  FILLER                  PIC X(13)   VALUE SPACES.                
035620     05  FILLER                  PIC X(21)   VALUE                        
035620                                          'TOTAL EXTENDED RETAIL'.        
035700     05  FILLER                  PIC X(37)   VALUE SPACES.                
035900                                                                          
037100******************************************************************        
037200***  REPORT DETAIL LINE                                                   
037300******************************************************************        
037400 01  DL-LINE-1.                                                           
037500     05  FILLER                  PIC X(02)   VALUE SPACE.                 
037600     05  DL-LOC-NBR              PIC ZZZ9.                                
           05  FILLER                  PIC X(04)   VALUE SPACE.                 
037800     05  DL-LOC-NAME             PIC X(40).                               
037900     05  FILLER                  PIC X(03)   VALUE SPACE.                 
           05  DL-ADJ-UNITS            PIC ------9.                             
           05  FILLER                  PIC X(17)   VALUE SPACE.                 
038200     05  DL-EXTD-RTL-AMT         PIC ---,---,---,---.99.                  
           05  FILLER                  PIC X(37)   VALUE SPACE.                 
040200                                                                          
037100******************************************************************        
037200***  REPORT TOTAL LINE                                                    
037300******************************************************************        
037400 01  TL-LINE-1.                                                           
037500     05  FILLER                  PIC X(01)   VALUE SPACE.                 
037600     05  FILLER                  PIC X(13)   VALUE                        
037600                                                  'COMPANY TOTAL'.        
           05  FILLER                  PIC X(36)   VALUE SPACE.                 
           05  TL-ADJ-UNITS            PIC ---------9.                          
           05  FILLER                  PIC X(13)   VALUE SPACE.                 
038200     05  TL-EXTD-RTL-AMT         PIC ---,---,---,---,---.99.              
           05  FILLER                  PIC X(37)   VALUE SPACE.                 
040200                                                                          
041300******************************************************************        
041400***  REPORT END OF REPORT LINE                                            
041500******************************************************************        
041600 01  ER-END-OF-REPORT-LINE.                                               
041700     05  FILLER                  PIC X(21)   VALUE                        
041800                                    '*** END OF REPORT ***'.              
041900                                                                          
       01  FILLER                          PIC X(40)       VALUE                
           '*** SQL WORK AREA BEGIN ***'.                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      *  SQL COMUNICATIONS AREA                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DB2 COPYBOOK XMT00001 (XMT_LOC)                                        
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
042000 EJECT                                                                    
042100                                                                          
042200******************************************************************        
042300 PROCEDURE DIVISION.                                                      
042400******************************************************************        
042500                                                                          
042600******************************************************************        
042700 0000-MAINLINE-INKRP002.                                                  
042800******************************************************************        
042900                                                                          
043000     PERFORM 1000-INITIALIZATION.                                         
043100                                                                          
043200     PERFORM 2000-MAIN-PROCESS.                                           
043300                                                                          
043400     PERFORM 8000-EOJ.                                                    
043500                                                                          
044200     STOP RUN.                                                            
044300                                                                          
044400 EJECT                                                                    
044500                                                                          
044600******************************************************************        
044700 1000-INITIALIZATION.                                                     
044800******************************************************************        
044900*                                                                         
045200     OPEN INPUT  REPORT-SUMMARY-FILE.                                     
045300     OPEN OUTPUT STORE-EXP-SUM-ADJ-RPT.                                   
045400                                                                          
046700     INITIALIZE PV-PROGRAM-VARIABLES.                                     
046800                                                                          
046900     ACCEPT PV-DATE-YYMMDD FROM DATE.                                     
           MOVE PV-DATE-YYMMDD-YY      TO DP132O-RUN-YEAR.                      
047100     MOVE PV-DATE-YYMMDD-MM      TO DP132O-RUN-MONTH.                     
047200     MOVE PV-DATE-YYMMDD-DD      TO DP132O-RUN-DAY.                       
047300     ACCEPT PV-TIME-HHMMSS FROM TIME.                                     
047400     MOVE PV-TIME-HHMMSS-HH      TO DP132O-RUN-HOUR.                      
047500     MOVE PV-TIME-HHMMSS-MM      TO DP132O-RUN-MINUTE.                    
047600     MOVE PC-PROGRAN-NAME        TO DP132O-PROGRAM-NAME.                  
           MOVE 1                      TO DP132O-REPORT-NUMBER.                 
052300                                                                          
055600     PERFORM 4200-READ-REPORT-SUMM-FILE.                                  
                                                                                
054300     PERFORM 6100-WRITE-REPORT-TITLE.                                     
054400                                                                          
055000                                                                          
055100                                                                          
055200******************************************************************        
055300 2000-MAIN-PROCESS.                                                       
055400******************************************************************        
055500*                                                                         
055800     PERFORM                                                              
055900       UNTIL PS-NO-MORE-SUM-RECS                                          
056000         PERFORM 6000-CREATE-REPORT                                       
056100         PERFORM 4200-READ-REPORT-SUMM-FILE                               
056200     END-PERFORM.                                                         
056300                                                                          
056500                                                                          
060000******************************************************************        
060100 4200-READ-REPORT-SUMM-FILE.                                              
060200******************************************************************        
060300*                                                                         
060400     READ REPORT-SUMMARY-FILE                                             
060500       INTO IN030-REPORT-DETAIL                                           
060600         AT END                                                           
060700             SET PS-NO-MORE-SUM-RECS                                      
060800                                 TO TRUE                                  
060900     END-READ.                                                            
061000                                                                          
061100     IF PS-MORE-SUM-RECS                                                  
061200         ADD 1                   TO PA-SUM-RECORDS-READ                   
061300     END-IF.                                                              
061400                                                                          
061500 EJECT                                                                    
061600                                                                          
064700******************************************************************        
064800 6000-CREATE-REPORT.                                                      
064900******************************************************************        
065000                                                                          
065100     IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                                 
065200         PERFORM 6100-WRITE-REPORT-TITLE                                  
           END-IF.                                                              
                                                                                
           MOVE IN030-LOC-NBR          TO DL-LOC-NBR                            
                                          XMT00001-LOC-NBR.                     
           PERFORM 6200-SELECT-XMT-LOC.                                         
           MOVE XMT00001-LOC-NM        TO DL-LOC-NAME.                          
           COMPUTE PV-ADJ-UNITS = IN030-ADJ-UNITS * -1.                         
           MOVE PV-ADJ-UNITS           TO DL-ADJ-UNITS.                         
           COMPUTE PV-EXTD-RLT-AMT = IN030-EXTD-RTL-AMT * -1.                   
           MOVE PV-EXTD-RLT-AMT        TO DL-EXTD-RTL-AMT.                      
           ADD PV-ADJ-UNITS            TO PA-TOT-ADJ-UNITS.                     
           ADD PV-EXTD-RLT-AMT         TO PA-TOT-EXTD-RTL-AMT.                  
066500                                                                          
067800     WRITE STORE-EXP-SUM-ADJ-REC FROM DL-LINE-1                           
067900         AFTER ADVANCING 1 LINE.                                          
068000     ADD 1                       TO PV-LINE-COUNT.                        
068100     ADD 1                       TO PA-REPORT-LINES-WRITTEN.              
068200                                                                          
068300 EJECT                                                                    
068400                                                                          
061700******************************************************************        
061800 6100-WRITE-REPORT-TITLE.                                                 
061900******************************************************************        
062000*                                                                         
062100     ADD 1                       TO PV-PAGE-COUNT.                        
062200     MOVE PV-PAGE-COUNT          TO DP132O-PAGE-NUMBER.                   
062300                                                                          
062400     WRITE STORE-EXP-SUM-ADJ-REC FROM                                     
062400                                    DP132O-STANDRD-HEADER-132-COLS        
062500         AFTER ADVANCING PAGE.                                            
062600                                                                          
062700     WRITE STORE-EXP-SUM-ADJ-REC FROM HD-REPORT-TITLE-2                   
062800         AFTER ADVANCING 1 LINE.                                          
062900                                                                          
063300     WRITE STORE-EXP-SUM-ADJ-REC FROM HD-COLUMN-HEADING-1                 
063400         AFTER ADVANCING 2 LINES.                                         
063500                                                                          
063900     MOVE SPACES                 TO STORE-EXP-SUM-ADJ-REC.                
064000     WRITE STORE-EXP-SUM-ADJ-REC                                          
064100         AFTER ADVANCING 1 LINE.                                          
064200                                                                          
064300     MOVE +5                     TO PV-LINE-COUNT.                        
064400                                                                          
064500 EJECT                                                                    
064600                                                                          
      ******************************************************************07560099
       6200-SELECT-XMT-LOC.                                             08340099
      ******************************************************************07560099
                                                                                
           EXEC SQL                                                             
                SELECT LOC_NM                                                   
                  INTO :XMT00001-LOC-NM                                         
                  FROM XMT_LOC                                                  
                  WHERE LOC_NBR       = :XMT00001-LOC-NBR                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   MOVE 'UNKNOWN'  TO XMT00001-LOC-NM                           
               WHEN OTHER                                                       
                   MOVE '6200-SELECT-XMT-LOC'                           20630099
                                       TO AA-PARAGRAPH-NAME             20640099
                   MOVE 'XNT_LOC TABLE ERROR'                           20650099
                                       TO AA-MESSAGE                    20660099
                   MOVE 'SELECT'       TO AA-DB2-OPERATION              20670099
                   PERFORM 9901-SQL-ERROR                               20680099
           END-EVALUATE.                                                        
                                                                                
068500******************************************************************        
068600 8000-EOJ.                                                                
068700******************************************************************        
068800*                                                                         
           MOVE PA-TOT-ADJ-UNITS    TO TL-ADJ-UNITS.                            
           MOVE PA-TOT-EXTD-RTL-AMT TO TL-EXTD-RTL-AMT.                         
                                                                                
           WRITE STORE-EXP-SUM-ADJ-REC FROM TL-LINE-1                           
               AFTER ADVANCING 2 LINES.                                         
                                                                                
069100     WRITE STORE-EXP-SUM-ADJ-REC FROM ER-END-OF-REPORT-LINE               
069200         AFTER ADVANCING 3 LINES.                                         
           ADD +5                   TO PA-REPORT-LINES-WRITTEN.                 
069300                                                                          
069400     PERFORM 8100-DISPLAY-RPT-MSG-SUMMARY.                                
069500                                                                          
069800     CLOSE REPORT-SUMMARY-FILE                                            
069900           STORE-EXP-SUM-ADJ-RPT.                                         
070000                                                                          
070100 EJECT                                                                    
070200                                                                          
074500******************************************************************        
074600 8100-DISPLAY-RPT-MSG-SUMMARY.                                            
074700******************************************************************        
074800*                                                                         
074900     DISPLAY ' '.                                                         
075000     DISPLAY '  PROGRAM INKRP002 PROCESSING SUMMARY'.                     
075100     DISPLAY '  -----------------------------------'.                     
075200     DISPLAY ' '.                                                         
075300*                                                                         
075400     MOVE PA-SUM-RECORDS-READ TO CD-COUNT-HOLD1.                          
075500     DISPLAY 'MESSAGE RECORDS READ = ' CD-COUNT-HOLD1.                    
075600*                                                                         
075700     MOVE PA-REPORT-LINES-WRITTEN TO CD-COUNT-HOLD1.                      
075800     DISPLAY 'REPORT RECORDS WRITTEN =  ' CD-COUNT-HOLD1.                 
075900*                                                                         
076000     DISPLAY ' '.                                                         
076100                                                                          
076400******************************************************************        
076500 9900-COMMON-ABEND-ROUTINE.                                               
076600******************************************************************        
076700*                                                                         
076800     DISPLAY AA-ABEND-LIT.                                                
076900     DISPLAY AA-PROGRAM-LIT.                                              
077000     DISPLAY AA-PARAGRAPH-LIT.                                            
077100     DISPLAY AA-MESSAGE-LINE.                                             
077200     DISPLAY ABEND-CODE.                                                  
077300                                                                          
077400     CALL 'ILBOABN0' USING ABEND-CODE.                                    
077500                                                                          
      ******************************************************************        
       9901-SQL-ERROR.                                                          
      ******************************************************************        
                                                                                
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-MESSAGE-LINE.                                             
           DISPLAY AA-DB2-OPERATION-LIT.                                        
           MOVE +4013                  TO ABEND-CODE.                           
                                                                                
           COPY DPPD004.                                                        
           PERFORM 9999-ABEND.                                                  
                                                                                
       9999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
