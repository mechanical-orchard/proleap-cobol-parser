       IDENTIFICATION DIVISION.                                         00010023
                                                                        00020024
       PROGRAM-ID.    IMKUT012                                          00030024
       AUTHOR.        PAUL KEBER.                                       00040024
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050024
       DATE-WRITTEN.  11-03-99.                                         00060024
       DATE-COMPILED.                                                   00070024
                                                                        00080024
      ******************************************************************00090024
      *     IMKUT012 - VSM PURGE NON-PURGED SELECTION                  *00100024
      ******************************************************************00110024
      *     THIS PROGRAM WILL FIND UNPURGED ROWS FROM THE TVNDSTL,     *00120024
      * TMDARVS, AND TUPC DB2 TABLES AFTER THE PURGE HAS TAKEN PLACE.  *00120024
      * THE DB2 KEY INFO FROM EACH PURGE BACKUP FILE RECORD IS USED    *00130024
      * TO ACCESS THE APPROPRIATE DATA BASE.                           *00200031
      ******************************************************************00270024
      *  ECOMM     12/20/00  P. KEBER   E-COMM PHASE 2 SKU LEVEL CHGS  *00220031
      *  DECREASE SIZE OF TMDARVS-PURGE-RECORD AND TMDARVS-FOUND-RECORD*00220031
      *  TO REMOVE SHIP_RSTRC_SEQ_NBR, E_PRD_GP_NBR AND E_PRD_GP_IND.  *        
      ******************************************************************        
      *  ECOMM      3/5/01   D. THEEL                                  *00220031
      *  INCREASED SIZE OF TUPC-PURGE-RECORD AND TUPC-FOUND-RECORD TO  *00220031
      *  REFLECT NEW TUPC COLUMN TX_PRD_CDE_SEQ_NBR.                   *        
      ******************************************************************        
      *  ECOMM      4/6/01   D. THEEL                                  *00220031
      *  INCREASED SIZE OF TUPC-PURGE-RECORD AND TUPC-FOUND-RECORD TO  *00220031
      *  REFLECT NEW TUPC COLUMN CUSTFACG_CLOR_DESC.                   *        
      ******************************************************************        
      *  WR22746   12/12/01  D. THEEL                                  *00220031
      *  INCREASED SIZE OF TUPC-PURGE-RECORD AND TUPC-FOUND-RECORD TO  *00220031
      *  REFLECT NEW TUPC COLUMN INTR_UPC_DESC.                        *        
      ******************************************************************        
      *  WR23443    3/05/02  D. THEEL                                  *00220031
      *  INCREASED SIZE OF TMDARVS-PURGE-RECORD FOR NEW STYL-RECLS-IND.*00220031
      ******************************************************************        
      *  WR23220    5/08/02  D. THEEL                                  *00220031
      *  INCREASED SIZE OF TMDARVS-PURGE-RECORD FOR NEW RTL-CLSN-SEQ-  *00220031
      *  NBR.                                                          *        
      ******************************************************************        
      *  WR23749    5/28/02  D. THEEL                                  *00220031
      *  INCREASED SIZE OF TMDARVS-PURGE-RECORD FOR NEW DEPT-NBR, MAJ- *00220031
      *  CL-NBR, AND SUB-CL-NBR.                                       *        
      ******************************************************************        
      *  WR23861    8/07/02  D. THEEL                                  *00220031
      *  INCREASED SIZE OF TMDARVS-PURGE-RECORD FOR NEW TMDARVS COLUMN *00220031
      *  ESL SEQ NBR.                                                  *        
      ******************************************************************        
      *  WR25951    01/21/04 D. THEEL                                  *00220031
      *  INCREASED TMDARVS FILE LRECL FOR ADDITION OF NEW COLUMNS.     *        
      ******************************************************************        
      *  WR26741    04/23/04 P. KEBER                                  *        
      *  INCREASED TMDARVS FILE LRECL FOR ADDITION OF NEW COLUMN.      *        
      ******************************************************************        
      *  WR27400    08/26/04 D. THEEL                                  *        
      *  CHANGED WHERE CLAUSE FROM 60 TO 30 DAYS.                      *        
      ******************************************************************        
      *  WR29925    04/13/06 D. THEEL                                  *        
      *  CHANGED WHERE CLAUSE FROM 30 TO 20 DAYS.                      *        
      ******************************************************************        
                                                                        00280024
                                                                        00290024
       ENVIRONMENT DIVISION.                                            00300024
                                                                        00310024
       CONFIGURATION SECTION.                                           00320024
       SOURCE-COMPUTER.        IBM-3090.                                00330024
       OBJECT-COMPUTER.        IBM-3090.                                00340024
       INPUT-OUTPUT SECTION.                                            00350024
       FILE-CONTROL.                                                    00360024
           SELECT TVNDSTL-PURGE-FILE                                    00370024
               ASSIGN TO INP01.                                         00380024
                                                                        00390024
           SELECT TMDARVS-PURGE-FILE                                    00370024
               ASSIGN TO INP02.                                         00380024
                                                                        00390024
           SELECT TUPC-PURGE-FILE                                       00370024
               ASSIGN TO INP03.                                         00380024
                                                                        00390024
           SELECT TVNDSTL-FOUND-FILE                                    00370024
               ASSIGN TO OUT01.                                         00380024
                                                                        00390024
           SELECT TMDARVS-FOUND-FILE                                    00370024
               ASSIGN TO OUT02.                                         00380024
                                                                        00390024
           SELECT TUPC-FOUND-FILE                                       00370024
               ASSIGN TO OUT03.                                         00380024
                                                                        00390024
                                                                        00400024
       DATA DIVISION.                                                   00410024
       FILE SECTION.                                                    00420024
                                                                        00430024
       FD  TVNDSTL-PURGE-FILE                                           00440024
           LABEL RECORDS ARE STANDARD                                   00450024
           RECORDING MODE IS F                                          00460024
           BLOCK CONTAINS 0 RECORDS.                                    00470024
       01  TVNDSTL-PURGE-RECORD        PIC X(95).                       00480024
                                                                        00500024
       FD  TMDARVS-PURGE-FILE                                           00440024
           LABEL RECORDS ARE STANDARD                                   00450024
           RECORDING MODE IS F                                          00460024
           BLOCK CONTAINS 0 RECORDS.                                    00470024
       01  TMDARVS-PURGE-RECORD        PIC X(93).                       00480024
                                                                        00500024
       FD  TUPC-PURGE-FILE                                              00440024
           LABEL RECORDS ARE STANDARD                                   00450024
           RECORDING MODE IS F                                          00460024
           BLOCK CONTAINS 0 RECORDS.                                    00470024
       01  TUPC-PURGE-RECORD           PIC X(214).                      00480024
                                                                        00500024
       FD  TVNDSTL-FOUND-FILE                                           00440024
           LABEL RECORDS ARE STANDARD                                   00450024
           RECORDING MODE IS F                                          00460024
           BLOCK CONTAINS 0 RECORDS.                                    00470024
       01  TVNDSTL-FOUND-RECORD        PIC X(95).                       00480024
                                                                        00500024
       FD  TMDARVS-FOUND-FILE                                           00440024
           LABEL RECORDS ARE STANDARD                                   00450024
           RECORDING MODE IS F                                          00460024
           BLOCK CONTAINS 0 RECORDS.                                    00470024
       01  TMDARVS-FOUND-RECORD        PIC X(93).                       00480024
                                                                        00500024
       FD  TUPC-FOUND-FILE                                              00440024
           LABEL RECORDS ARE STANDARD                                   00450024
           RECORDING MODE IS F                                          00460024
           BLOCK CONTAINS 0 RECORDS.                                    00470024
       01  TUPC-FOUND-RECORD           PIC X(214).                      00480024
                                                                        00500024
                                                                        00510024
       WORKING-STORAGE SECTION.                                         00520024
                                                                        00530024
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00540024
                                                                        00550024
       01  PC-PROGRAM-CONSTANTS.                                        00560024
           05  PC-PROGRAM-NAME         PIC  X(08)  VALUE 'IMKUT012'.    00580024
           05  PC-MAX-RETRIES          PIC S9(03)  VALUE +3 COMP-3.     00590024
                                                                        00610024
       01  PS-PROGRAM-SWITCHES.                                         00900024
           05  PS-TVNDSTL-FILE-EOF-SW       PIC  X        VALUE 'N'.    00910024
               88  PS-MORE-TVNDSTL-RECORDS                VALUE 'N'.    00920024
               88  PS-NO-MORE-TVNDSTL-RECORDS             VALUE 'Y'.    00930024
           05  PS-TMDARVS-FILE-EOF-SW       PIC  X        VALUE 'N'.    00910024
               88  PS-MORE-TMDARVS-RECORDS                VALUE 'N'.    00920024
               88  PS-NO-MORE-TMDARVS-RECORDS             VALUE 'Y'.    00930024
           05  PS-TUPC-FILE-EOF-SW          PIC  X        VALUE 'N'.    00910024
               88  PS-MORE-TUPC-RECORDS                   VALUE 'N'.    00920024
               88  PS-NO-MORE-TUPC-RECORDS                VALUE 'Y'.    00930024
      *                                                                 01040024
       01  PROGRAM-VARIABLES.                                           01050024
           05  PV-SQL-SUB                      PIC  9(03) VALUE ZERO.           
           05  PV-TVNDSTL-COUNT                PIC S9(4)  VALUE 0 COMP.         
           05  PV-TMDARVS-COUNT                PIC S9(4)  VALUE 0 COMP.         
           05  PV-TUPC-COUNT                   PIC S9(4)  VALUE 0 COMP.         
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 01070024
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 01090024
           05  PV-SQLCODE-DISPLAY              PIC  ++++9 VALUE ZERO.   01120024
      *                                                                 01130024
      *                                                                 01140024
       01  PA-PROGRAM-ACCUMULATORS.                                     01150024
           05  PA-RECORD-COUNTS.                                        01160024
               10  PA-TUPC-FOUND-COUNT         PIC  9(9)  VALUE ZEROES. 01191037
               10  PA-TUPC-INPUT-COUNT         PIC  9(9)  VALUE ZEROES. 01191037
               10  PA-TMDARVS-FOUND-COUNT      PIC  9(9)  VALUE ZEROES. 01191037
               10  PA-TMDARVS-INPUT-COUNT      PIC  9(9)  VALUE ZEROES. 01191037
               10  PA-TVNDSTL-FOUND-COUNT      PIC  9(9)  VALUE ZEROES. 01191037
               10  PA-TVNDSTL-INPUT-COUNT      PIC  9(9)  VALUE ZEROES. 01170024
      *                                                                 01500024
      ******************************************************************01510024
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01520024
      ******************************************************************01530024
           COPY DPWS004.                                                01540024
      *                                                                 01550024
      ******************************************************************01560024
      *  USED FOR DB2 ERRORS                                            01570024
      ******************************************************************01580024
           EXEC SQL                                                     01590024
               INCLUDE SQLCA                                            01600024
           END-EXEC.                                                    01610024
                                                                                
      *****************************************************************         
      * DCLGEN FOR THE VENDOR STYLE TABLE                                       
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TVNDSTL                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DCLGEN FOR THE MERCHANDISE AREA VENDOR STYLE TABLE                      
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TMDARVS                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DCLGEN FOR THE UPC TABLE                                                
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TUPC                                                     
           END-EXEC.                                                            
                                                                                
      *                                                                 02110024
      *-------------------*                                             02120024
       PROCEDURE DIVISION.                                              02130024
      *-------------------*                                             02140024
                                                                        02150024
       0000-MAIN-MODULE.                                                02160024
                                                                        02170024
           PERFORM 1000-INITIALIZATION.                                 02180024
      *                                                                 02190024
           PERFORM 2000-PROCESS-TVNDSTL                                 02200024
                UNTIL PS-NO-MORE-TVNDSTL-RECORDS.                       02210024
      *                                                                 02190024
           PERFORM 2100-PROCESS-TMDARVS                                 02200024
                UNTIL PS-NO-MORE-TMDARVS-RECORDS.                       02210024
      *                                                                 02190024
           PERFORM 2200-PROCESS-TUPC                                    02200024
                UNTIL PS-NO-MORE-TUPC-RECORDS.                          02210024
      *                                                                 02220024
           PERFORM 8000-EOJ-ROUTINE.                                    02230024
                                                                        02240024
           STOP RUN.                                                    02250024
      *                                                                 02260024
      *                                                                 02270024
      *                                                                 02280024
      ******************************************************************02290024
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02300024
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02310024
      ******************************************************************02320024
      *                                                                 02330024
       1000-INITIALIZATION.                                             02340024
      *                                                                 02350024
           OPEN INPUT TVNDSTL-PURGE-FILE                                02360024
                      TMDARVS-PURGE-FILE                                02360024
                      TUPC-PURGE-FILE.                                  02360024
           OPEN OUTPUT TVNDSTL-FOUND-FILE                               02360024
                       TMDARVS-FOUND-FILE                               02360024
                       TUPC-FOUND-FILE.                                 02360024
      *                                                                 02370024
           PERFORM 4000-READ-TVNDSTL-PURGE-FILE.                        02580024
           PERFORM 4100-READ-TMDARVS-PURGE-FILE.                        02580024
           PERFORM 4200-READ-TUPC-PURGE-FILE.                           02580024
      *                                                                 02800024
      *                                                                 02810024
      ******************************************************************02820024
      * PROCESS THE TVNDSTL PURGE FILE - CHECK EACH ROW                 02830024
      ******************************************************************02840024
       2000-PROCESS-TVNDSTL.                                            02850024
      *                                                                 02860024
           PERFORM 6000-SELECT-FROM-TVNDSTL.                            02890037
      *                                                                 02900024
           IF PV-TVNDSTL-COUNT > ZERO                                   03060024
               ADD 1 TO PA-TVNDSTL-FOUND-COUNT                          04792037
               WRITE TVNDSTL-FOUND-RECORD                                       
                   FROM DCLTVNDSTL                                              
           END-IF.                                                      03101037
      *                                                                 02900024
           PERFORM 4000-READ-TVNDSTL-PURGE-FILE.                        03100037
      *                                                                 03480024
      *                                                                 03490024
      ******************************************************************02820024
      * PROCESS THE TMDARVS PURGE FILE - CHECK EACH ROW                 02830024
      ******************************************************************02840024
       2100-PROCESS-TMDARVS.                                            02850024
      *                                                                 02860024
           PERFORM 6100-SELECT-FROM-TMDARVS.                            02890037
      *                                                                 02900024
           IF PV-TMDARVS-COUNT > ZERO                                   03060024
               ADD 1 TO PA-TMDARVS-FOUND-COUNT                          04792037
               WRITE TMDARVS-FOUND-RECORD                                       
                   FROM DCLTMDARVS                                              
           END-IF.                                                      03101037
      *                                                                 02900024
           PERFORM 4100-READ-TMDARVS-PURGE-FILE.                        03100037
      *                                                                 03480024
      *                                                                 03490024
      ******************************************************************02820024
      * PROCESS THE TUPC PURGE FILE - CHECK EACH ROW                    02830024
      ******************************************************************02840024
       2200-PROCESS-TUPC.                                               02850024
      *                                                                 02860024
           PERFORM 6200-SELECT-FROM-TUPC.                               02890037
      *                                                                 02900024
           IF PV-TUPC-COUNT > ZERO                                      03060024
               ADD 1 TO PA-TUPC-FOUND-COUNT                             04792037
               WRITE TUPC-FOUND-RECORD                                          
                   FROM DCLTUPC                                                 
           END-IF.                                                      03101037
      *                                                                 02900024
           PERFORM 4200-READ-TUPC-PURGE-FILE.                           03100037
      *                                                                 03480024
      *                                                                 03490024
      ******************************************************************03500024
      * READS THE TVNDSTL PURGE FILE INTO THE DCLGEN LAYOUT             03510024
      ******************************************************************03520024
       4000-READ-TVNDSTL-PURGE-FILE.                                    03530024
           READ TVNDSTL-PURGE-FILE INTO DCLTVNDSTL                      03540024
               AT END MOVE 'Y' TO PS-TVNDSTL-FILE-EOF-SW                03550024
               NOT AT END                                                       
                   ADD 1 TO PA-TVNDSTL-INPUT-COUNT.                             
      *                                                                 03560024
      *                                                                 05710024
      ******************************************************************03500024
      * READS THE TMDARVS PURGE FILE INTO THE DCLGEN LAYOUT             03510024
      ******************************************************************03520024
       4100-READ-TMDARVS-PURGE-FILE.                                    03530024
           READ TMDARVS-PURGE-FILE INTO DCLTMDARVS                      03540024
               AT END MOVE 'Y' TO PS-TMDARVS-FILE-EOF-SW                03550024
               NOT AT END                                                       
                   ADD 1 TO PA-TMDARVS-INPUT-COUNT.                             
      *                                                                 03560024
      *                                                                 05710024
      ******************************************************************03500024
      * READS THE TUPC PURGE FILE INTO THE DCLGEN LAYOUT                03510024
      ******************************************************************03520024
       4200-READ-TUPC-PURGE-FILE.                                       03530024
           READ TUPC-PURGE-FILE INTO DCLTUPC                            03540024
               AT END MOVE 'Y' TO PS-TUPC-FILE-EOF-SW                   03550024
               NOT AT END                                                       
                   ADD 1 TO PA-TUPC-INPUT-COUNT.                                
      *                                                                 03560024
      *                                                                 05710024
      ******************************************************************        
      *  CHECK TO SEE IF THE VS ID EXISTS ON TVNDSTL                            
      ******************************************************************        
       6000-SELECT-FROM-TVNDSTL.                                                
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0)                                             
                                                                                
               EXEC SQL                                                         
                   SELECT COUNT(*)                                              
                   INTO :PV-TVNDSTL-COUNT                                       
                   FROM TVNDSTL                                                 
                   WHERE VS_ID = :VNDSTL-VS-ID                                  
                     AND EFF_END_DTE < (CURRENT DATE - 20 DAYS)                 
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '6000-SELECT-FROM-TVNDSTL'                          
                           TO PV-PARAGRAPH-NAME                                 
                       MOVE 'SELECT FROM TVNDSTL'                               
                           TO PV-OPERATION-ATTEMPTED                            
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '6000-SELECT-FROM-TVNDSTL' TO PV-PARAGRAPH-NAME             
               MOVE 'SELECT FROM TVNDSTL' TO PV-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                 03560024
      *                                                                 05710024
      ******************************************************************        
      *  CHECK TO SEE IF THE VS ID EXISTS ON TMDARVS                            
      ******************************************************************        
       6100-SELECT-FROM-TMDARVS.                                                
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0)                                             
                                                                                
               EXEC SQL                                                         
                   SELECT COUNT(*)                                              
                   INTO :PV-TMDARVS-COUNT                                       
                   FROM TMDARVS                                                 
                   WHERE VS_ID = :MDARVS-VS-ID                                  
                     AND SUB_CL_MDSEAR_ID = :MDARVS-SUB-CL-MDSEAR-ID            
                     AND ACTV_IND = 'N'                                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '6100-SELECT-FROM-TMDARVS'                          
                           TO PV-PARAGRAPH-NAME                                 
                       MOVE 'SELECT FROM TMDARVS'                               
                           TO PV-OPERATION-ATTEMPTED                            
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '6100-SELECT-FROM-TMDARVS' TO PV-PARAGRAPH-NAME             
               MOVE 'SELECT FROM TMDARVS' TO PV-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                 03560024
      *                                                                 05710024
      ******************************************************************        
      *  CHECK TO SEE IF THE VS ID EXISTS ON TUPC                               
      ******************************************************************        
       6200-SELECT-FROM-TUPC.                                                   
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
               UNTIL  (PV-SQL-SUB > PC-MAX-RETRIES                              
                  OR   SQLCODE = 0)                                             
                                                                                
               EXEC SQL                                                         
                   SELECT COUNT(*)                                              
                   INTO :PV-TUPC-COUNT                                          
                   FROM TUPC                                                    
                   WHERE UPC_ID = :UPC-UPC-ID                                   
                     AND EFF_END_DTE < (CURRENT DATE - 20 DAYS)                 
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '6200-SELECT-FROM-TUPC'                             
                           TO PV-PARAGRAPH-NAME                                 
                       MOVE 'SELECT FROM TUPC'                                  
                           TO PV-OPERATION-ATTEMPTED                            
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB > PC-MAX-RETRIES                                      
               MOVE '6200-SELECT-FROM-TUPC' TO PV-PARAGRAPH-NAME                
               MOVE 'SELECT FROM TUPC' TO PV-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *                                                                 08250024
      ******************************************************************08260024
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *08270024
      ******************************************************************08280024
       8000-EOJ-ROUTINE.                                                08290024
      *                                                                 08300024
           DISPLAY '                                             '.     08340024
           DISPLAY 'TVNDSTL INPUT    ', PA-TVNDSTL-INPUT-COUNT.         08350024
           DISPLAY 'TVNDSTL UNPURGED ', PA-TVNDSTL-FOUND-COUNT.         08360238
           DISPLAY '                                             '.     08390024
           DISPLAY 'TMDARVS INPUT    ', PA-TMDARVS-INPUT-COUNT.         08350024
           DISPLAY 'TMDARVS UNPURGED ', PA-TMDARVS-FOUND-COUNT.         08360238
           DISPLAY '                                             '.     08390024
           DISPLAY 'TUPC INPUT    ', PA-TUPC-INPUT-COUNT.               08350024
           DISPLAY 'TUPC UNPURGED ', PA-TUPC-FOUND-COUNT.               08360238
           DISPLAY '                                             '.     08390024
      *                                                                 08410024
           CLOSE TVNDSTL-PURGE-FILE                                     08420024
                 TMDARVS-PURGE-FILE                                     08420024
                 TUPC-PURGE-FILE                                        08420024
                 TVNDSTL-FOUND-FILE                                             
                 TMDARVS-FOUND-FILE                                             
                 TUPC-FOUND-FILE.                                               
      *                                                                 08430024
      *                                                                 08440024
      ******************************************************************08540024
      *  STANDARD DB2 ERROR ABEND ROUTINE                               08550024
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             08560024
      ******************************************************************08570024
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE               TO PV-SQLCODE-DISPLAY.            08610024
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-OPERATION-ATTEMPTED.                 
           DISPLAY '**  SQLCODE   **  = ' PV-SQLCODE-DISPLAY.           08660024
                                                                                
      *-----------------------------------------------------------------        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      *-----------------------------------------------------------------        
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                               
