      ***************************************************************** 00010001
       IDENTIFICATION DIVISION.                                         00020001
      ***************************************************************** 00030001
                                                                        00040001
       PROGRAM-ID.             SAKUT150.                                00050001
       AUTHOR.                 JIM HUNTER.                              00060001
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070001
       DATE-WRITTEN            AUGUST 2021.                             00080001
       DATE-COMPILED.                                                   00090001
                                                                        00100001
      ***************************************************************** 00110001
      *                                                                 00120001
      *  THIS PROGRAM CREATES THE SALES CORRECTION DETAIL FILE.         00130001
      *  THE SAKRP014 PROGRAM WAS USED AS A STARTING POINT.             00140001
      *                                                                 00150001
      *   INPUT: INP01 - CONTAINS THE START AND END DATES FOR THE       00160001
      *                  PREVIOUS FISCAL WEEK.                          00170001
      *  OUTPUT: OUT01 - SALES CORRECTION DETAIL FILE.                  00180001
      *                                                                 00190001
      ***************************************************************** 00200001
       ENVIRONMENT DIVISION.                                            00210001
      ***************************************************************** 00220001
                                                                        00230001
       CONFIGURATION SECTION.                                           00240001
                                                                        00250001
       SOURCE-COMPUTER.        IBM-3090.                                00260001
                                                                        00270001
       OBJECT-COMPUTER.        IBM-3090.                                00280001
                                                                        00290001
       INPUT-OUTPUT SECTION.                                            00300001
                                                                        00310001
       FILE-CONTROL.                                                    00320001
                                                                        00330001
           SELECT DATE-RANGE-CARD-FILE                                  00340001
               ASSIGN TO INP01.                                         00350001
                                                                        00360001
           SELECT SALES-CORRECTION-CSV-FILE                             00370001
               ASSIGN TO OUT01.                                         00380001
                                                                        00390001
      ******************************************************************00400001
       DATA DIVISION.                                                   00410001
      ******************************************************************00420001
                                                                        00430001
       FILE SECTION.                                                    00440001
                                                                        00450001
       FD  DATE-RANGE-CARD-FILE                                         00460001
           RECORDING MODE IS F                                          00470001
           BLOCK CONTAINS 0 RECORDS                                     00480001
           LABEL RECORDS ARE STANDARD.                                  00490001
                                                                        00500001
       01  DATE-RANGE-CARD-RECORD          PIC X(80).                   00510001
                                                                        00520001
       FD  SALES-CORRECTION-CSV-FILE                                    00530001
           RECORDING MODE IS F                                          00540001
           BLOCK CONTAINS 0  RECORDS.                                   00550001
                                                                        00560001
       01  SALES-CORRECTION-CSV-RECORD     PIC  X(120).                 00570001
                                                                        00580001
      ******************************************************************00590001
       WORKING-STORAGE SECTION.                                         00600001
      ******************************************************************00610001
       01  FILLER                            PIC X(50)  VALUE           00620001
           ' *** WORKING STORAGE STARTS HERE FOR SAKUT150 *** '.        00630002
                                                                        00640001
      ******************************************************************00650001
      * RECORD LAYOUT FOR INPUT DATE CARD (POS DATE)                    00660001
      ******************************************************************00670001
       01  CC-CONTROL-CARD.                                             00680001
           05  CC-CONTROL-CARD-DISPLAY.                                 00690001
               10  CC-START-DATE       PIC X(10)    VALUE SPACES.       00700001
               10  FILLER              PIC X        VALUE SPACE.        00710001
               10  CC-END-DATE         PIC X(10)    VALUE SPACES.       00720001
               10  FILLER              PIC X(59)    VALUE SPACE.        00730001
                                                                        00740001
      ******************************************************************00750001
      *  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)             00760001
      ******************************************************************00770001
           COPY DPWS500.                                                00780001
                                                                        00790001
       01  AC-ABEND-CODE                     PIC S9(04) VALUE ZERO      00800001
                                                        COMP SYNC.      00810001
           88  AC-CONTROL-CARD-ERROR                    VALUE +4003.    00820001
           88  AC-DB2-ERROR                             VALUE +4013.    00830001
           88  AC-CALENDAR-ROUTINE-ERROR                VALUE +4019.    00840001
                                                                        00850001
       01  PS-PROGRAM-SWITCHES.                                         00860001
           05  PS-END-OF-KEY-INFO-SW         PIC X(01)  VALUE 'N'.      00870001
               88  PS-END-OF-KEY-INFO-YES               VALUE 'Y'.      00880001
           05  PS-END-OF-CARD-FILE-SW        PIC X(01)  VALUE 'N'.      00890001
               88  PS-END-OF-CARD-FILE-YES              VALUE 'Y'.      00900001
           05  PS-ORIG-TRAN-FOUND-SW         PIC X(01)  VALUE 'Y'.      00910001
               88  PS-ORIG-TRAN-FOUND                   VALUE 'Y'.      00920001
               88  PS-ORIG-TRAN-NOT-FOUND               VALUE 'N'.      00930001
                                                                        00940001
       01  PC-CONSTANTS.                                                00950001
           05  PC-NOT-FOUND                PIC S9(04)    VALUE +100     00960001
                                                         COMP SYNC.     00970001
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE          00980001
                                                         'SAKUT150'.    00990002
                                                                        01000001
       01  DK-DB2-KEYS.                                                 01010001
           05  DK-BEGIN-DATE-ISO       PIC X(10)    VALUE SPACES.       01020001
           05  DK-END-DATE-ISO         PIC X(10)    VALUE SPACES.       01030001
                                                                        01040001
       01  PV-MISC-FIELDS.                                              01050001
           05  PV-REC-CNT                  PIC  9(07)     VALUE ZERO.   01060001
           05  PV-ORIG-SLS-CORR-SEQ-NBR    PIC S9(4) COMP VALUE ZERO.   01070001
           05  PV-ORIG-TRN-STAT-CDE        PIC  X(2)      VALUE SPACE.  01080001
           05  PV-PARAGRAPH-NAME           PIC  X(30)     VALUE SPACE.  01090001
           05  PV-OPERATION-MSG-1          PIC  X(40)     VALUE SPACE.  01100001
           05  PV-OPERATION-MSG-2          PIC  X(40)     VALUE SPACE.  01110001
           05  PV-DB2-OPERATION            PIC  X(30)     VALUE SPACE.  01120001
           05  PV-SAVE-STORE               PIC  9(4)      VALUE ZERO.   01130001
           05  PV-SAVE-TERRITORY           PIC  9(4)      VALUE ZERO.   01140001
           05  PV-DPG52-LK-DATE-INPUT      PIC X(10)      VALUE SPACES. 01150001
                                                                        01160001
      ******************************************************************01170001
      * RECORD LAYOUT FOR OUTPUT SALES CORRECTION CSV FILE.             01180001
      ******************************************************************01190001
       01  SA-CORR-HEADER-RECORD.                                       01200001
           05  SA-HDR-PRCG-DATE         PIC X(10) VALUE 'PRCG DATE'.    01210001
           05  FILLER                   PIC X     VALUE ','.            01220001
           05  SA-HDR-SALES-DATE        PIC X(10) VALUE 'SALES DATE'.   01230001
           05  FILLER                   PIC X     VALUE ','.            01240001
           05  SA-HDR-STORE             PIC X(5)  VALUE 'STORE'.        01250001
           05  FILLER                   PIC X     VALUE ','.            01260001
           05  SA-HDR-REG               PIC X(5)  VALUE 'REG'.          01270001
           05  FILLER                   PIC X     VALUE ','.            01280001
           05  SA-HDR-TRAN              PIC X(5)  VALUE 'TRAN'.         01290001
           05  FILLER                   PIC X     VALUE ','.            01300001
           05  SA-HDR-ASSOC-ID          PIC X(8)  VALUE 'ASSOC ID'.     01310001
           05  FILLER                   PIC X     VALUE ','.            01320001
           05  SA-HDR-USER-ID           PIC X(10) VALUE 'SA USER ID'.   01330001
           05  FILLER                   PIC X     VALUE ','.            01340001
           05  SA-HDR-TRAN-STATUS       PIC X(4)  VALUE 'STAT'.         01350001
           05  FILLER                   PIC X     VALUE ','.            01360001
           05  SA-HDR-TRAN-TYPE         PIC X(4)  VALUE 'TYPE'.         01370001
           05  FILLER                   PIC X     VALUE ','.            01380001
           05  SA-HDR-VOID-CD           PIC X(4)  VALUE 'VOID'.         01390001
           05  FILLER                   PIC X     VALUE ','.            01400001
           05  SA-HDR-ORIG-TRN-STAT-CDE PIC X(13) VALUE 'ORIG TRN STAT'.01410001
           05  FILLER                   PIC X     VALUE ','.            01420001
           05  SA-HDR-TRAN-AMT          PIC X(13) VALUE 'TRAN AMOUNT'.  01430001
           05  FILLER                   PIC X     VALUE ','.            01440001
           05  SA-HDR-CORR-SEQ          PIC X(8)  VALUE 'CORR SEQ'.     01450001
           05  FILLER                   PIC X     VALUE ','.            01460001
           05  SA-HDR-TERR-NBR          PIC X(4)  VALUE 'TERR'.         01470001
           05  FILLER                   PIC X     VALUE ','.            01480001
                                                                        01490001
       01  SA-CORR-DETAIL-RECORD.                                       01500001
           05  SA-DTL-PRCG-DATE         PIC X(10).                      01510001
           05  FILLER                   PIC X     VALUE ','.            01520001
           05  SA-DTL-SALES-DATE        PIC X(10).                      01530001
           05  FILLER                   PIC X     VALUE ','.            01540001
           05  SA-DTL-STORE             PIC 9(4).                       01550001
           05  FILLER                   PIC X     VALUE ','.            01560001
           05  SA-DTL-REG               PIC 9(4).                       01570001
           05  FILLER                   PIC X     VALUE ','.            01580001
           05  SA-DTL-TRAN              PIC 9(4).                       01590001
           05  FILLER                   PIC X     VALUE ','.            01600001
           05  SA-DTL-ASSOC-ID          PIC X(8).                       01610001
           05  FILLER                   PIC X     VALUE ','.            01620001
           05  SA-DTL-USER-ID           PIC X(8).                       01630001
           05  FILLER                   PIC X     VALUE ','.            01640001
           05  SA-DTL-TRAN-STATUS       PIC X.                          01650001
           05  FILLER                   PIC X     VALUE ','.            01660001
           05  SA-DTL-TRAN-TYPE         PIC X(2).                       01670001
           05  FILLER                   PIC X     VALUE ','.            01680001
           05  SA-DTL-VOID-CD           PIC X.                          01690001
           05  FILLER                   PIC X     VALUE ','.            01700001
           05  SA-DTL-ORIG-TRN-STAT     PIC X(25).                      01710001
           05  FILLER                   PIC X     VALUE ','.            01720001
           05  SA-DTL-TRAN-AMT          PIC -$$$$$9.99.                 01730001
           05  FILLER                   PIC X     VALUE ','.            01740001
           05  SA-DTL-CORR-SEQ          PIC 9.                          01750001
           05  FILLER                   PIC X     VALUE ','.            01760001
           05  SA-DTL-TERR-NBR          PIC ZZZ9.                       01770001
           05  FILLER                   PIC X     VALUE ','.            01780001
                                                                        01790001
                                                                        01800001
      ******************************************************************01810001
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                            01820001
      ******************************************************************01830001
           COPY DPWS004.                                                01840001
                                                                        01850001
      ******************************************************************01860001
      *  DB2 COMMUNICATION AREA.                                        01870001
      ******************************************************************01880001
           EXEC SQL                                                     01890001
                INCLUDE SQLCA                                           01900001
           END-EXEC.                                                    01910001
                                                                        01920001
      ******************************************************************01930001
      *  DB2 TABLE DB2PDBA.XST_LOC - LOCATION TABLE.                    01940001
      ******************************************************************01950001
           EXEC SQL                                                     01960001
                INCLUDE XST00001                                        01970001
           END-EXEC.                                                    01980001
                                                                        01990001
      ******************************************************************02000001
      *  DB2 TABLE DB2PDBA.XST_LOC_DIST - LOCATION / DISTRICT TABLE.    02010001
      ******************************************************************02020001
           EXEC SQL                                                     02030001
                INCLUDE XST00002                                        02040001
           END-EXEC.                                                    02050001
                                                                        02060001
      ******************************************************************02070001
      *  DB2 TABLE DB2PDBA.XST_DIST -  DISTRICT TABLE.                  02080001
      ******************************************************************02090001
           EXEC SQL                                                     02100001
                INCLUDE XST00003                                        02110001
           END-EXEC.                                                    02120001
                                                                        02130001
      ******************************************************************02140001
      *  DB2 TABLE DB2PDBA.XST_DIST_REGN - DISTRICT / REGION TABLE.     02150001
      ******************************************************************02160001
           EXEC SQL                                                     02170001
                INCLUDE XST00004                                        02180001
           END-EXEC.                                                    02190001
                                                                        02200001
      ******************************************************************02210001
      *  DB2 TABLE DB2PDBA.XST_REGN - REGION TABLE.                     02220001
      ******************************************************************02230001
           EXEC SQL                                                     02240001
                INCLUDE XST00005                                        02250001
           END-EXEC.                                                    02260001
                                                                        02270001
      ******************************************************************02280001
      *  DB2 TABLE DB2PDBA.XST_REGN_TERR - REGION / TERRITORY TABLE.    02290001
      ******************************************************************02300001
           EXEC SQL                                                     02310001
                INCLUDE XST00006                                        02320001
           END-EXEC.                                                    02330001
                                                                        02340001
      ******************************************************************02350001
      *  DB2 TABLE DB2PDBA.XST_TERR - TERRITORY TABLE.                  02360001
      ******************************************************************02370001
           EXEC SQL                                                     02380001
                INCLUDE XST00007                                        02390001
           END-EXEC.                                                    02400001
                                                                        02410001
      ******************************************************************02420001
      *  DB2 TABLE DB2PDBA.TEPTRN - TRANSACTION TABLE.                  02430001
      ******************************************************************02440001
           EXEC SQL                                                     02450001
                INCLUDE TEPTRN                                          02460001
           END-EXEC.                                                    02470001
                                                                        02480001
      ******************************************************************02490001
      *  DB2 TABLE DB2PDBA.TEPDKEY - CURRENT PROCESSING DAY KEYS TBL.   02500001
      ******************************************************************02510001
           EXEC SQL                                                     02520001
                INCLUDE TEPDKEY                                         02530001
           END-EXEC.                                                    02540001
                                                                        02550001
      ******************************************************************02560001
      *  DB2 TABLE DB2PDBA.TEPSAT - SALES AUDIT TRANSACTION TABLE.      02570001
      ******************************************************************02580001
           EXEC SQL                                                     02590001
                INCLUDE TEPSAT                                          02600001
           END-EXEC.                                                    02610001
                                                                        02620001
      ******************************************************************02630001
      *  DB2 TABLE DB2PDBA.TEPPART - SALES PARTITION TABLE.             02640001
      ******************************************************************02650001
           EXEC SQL                                                     02660001
                INCLUDE TEPPART                                         02670001
           END-EXEC.                                                    02680001
                                                                        02690001
      ******************************************************************02700001
      *  KEY-INFO - LOOK FOR KEY INFOMATION FROM THE TRANSACTION TABLE. 02710001
      ******************************************************************02720001
           EXEC SQL                                                     02730001
              DECLARE KEY-INFO CURSOR FOR                               02740001
               SELECT B.PARTN_NBR,                                      02750001
                      B.STR_NBR,                                        02760001
                      B.RGST_ID,                                        02770001
                      B.TRN_NBR,                                        02780001
                      B.STRT_TM,                                        02790001
                      B.SLS_CORR_SEQ_NBR,                               02800001
                      B.VOID_ABRT_CDE,                                  02810001
                      B.TRN_STAT_CDE,                                   02820001
                      B.TRN_TYP_CDE,                                    02830001
                      B.TRN_DTE,                                        02840001
                      B.SLS_DTE,                                        02850001
                      B.PRCG_DTE,                                       02860001
                      B.TRN_TOT_AMT,                                    02870001
                      B.ASSOC_ID                                        02880001
                 FROM TEPDKEY  A,                                       02890001
                      TEPTRN   B,                                       02900001
                      TEPPART  D                                        02910001
                WHERE A.PRCG_DTE BETWEEN :DK-BEGIN-DATE-ISO             02920001
                                     AND :DK-END-DATE-ISO               02930001
                  AND A.SLS_DTE          = D.SLS_DTE                    02940001
                  AND B.PARTN_NBR        = D.PARTN_NBR                  02950001
                  AND A.STR_NBR          = B.STR_NBR                    02960001
                  AND A.RGST_ID          = B.RGST_ID                    02970001
                  AND A.TRN_NBR          = B.TRN_NBR                    02980001
                  AND A.STRT_TM          = B.STRT_TM                    02990001
                  AND A.SLS_CORR_SEQ_NBR = B.SLS_CORR_SEQ_NBR           03000001
                  AND B.SLS_CORR_SEQ_NBR > 0                            03010001
             ORDER BY PRCG_DTE ASC,                                     03020001
                      STR_NBR ASC,                                      03030001
                      TRN_DTE ASC,                                      03040001
                      RGST_ID ASC,                                      03050001
                      TRN_NBR ASC,                                      03060001
                      SLS_CORR_SEQ_NBR DESC,                            03070001
                      SLS_DTE ASC                                       03080001
                      WITH UR                                           03090001
           END-EXEC.                                                    03100001
                                                                        03110001
      ***************************************************************** 03120001
       PROCEDURE DIVISION.                                              03130001
      ******************************************************************03140001
      * 0000-MAINLINE-PARAGRAPH                                         03150001
      *      PRODUCE SALES AUDIT CORRECTIONS FILE                       03160001
      ******************************************************************03170001
       0000-MAINLINE-PARAGRAPH.                                         03180001
                                                                        03190001
           PERFORM 1000-HOUSE-KEEPING.                                  03200001
                                                                        03210001
           PERFORM 2000-OPEN-KEY-INFO.                                  03220001
                                                                        03230001
           PERFORM 2200-FETCH-KEY-INFO                                  03240001
             UNTIL PS-END-OF-KEY-INFO-YES.                              03250001
                                                                        03260001
           PERFORM 2400-CLOSE-KEY-INFO.                                 03270001
                                                                        03280001
           DISPLAY 'SAKUT150 OUTPUT RECORDS INCLUDING HEADER = '        03290001
                    PV-REC-CNT.                                         03300001
                                                                        03310001
           STOP RUN.                                                    03320001
                                                                        03330001
      ******************************************************************03340001
       1000-HOUSE-KEEPING.                                              03350001
      *     OPEN FILES, PROCESS CONTROL CARD, FORMAT REPORT HEADINGS.   03360001
      ******************************************************************03370001
                                                                        03380001
           OPEN INPUT  DATE-RANGE-CARD-FILE                             03390001
                OUTPUT SALES-CORRECTION-CSV-FILE.                       03400001
                                                                        03410001
           PERFORM 1100-PROCESS-CONTROL-CARD.                           03420001
                                                                        03430001
           PERFORM 1200-WRITE-HEADER-RECORD.                            03440001
                                                                        03450001
      ******************************************************************03460001
       1100-PROCESS-CONTROL-CARD.                                       03470001
      *     READ CONTROL CARD TO GET START AND END DATES.               03480001
      *     CALL THE DATE ROUTINE TO VALIDATE BOTH.                     03490001
      ******************************************************************03500001
                                                                        03510001
           PERFORM 1110-READ-DATE-RANGE-CARD-FILE.                      03520001
           CLOSE DATE-RANGE-CARD-FILE.                                  03530001
           IF PS-END-OF-CARD-FILE-YES                                   03540001
               MOVE '1100-PROCESS-CONTROL-CARD'  TO PV-PARAGRAPH-NAME   03550001
               MOVE 'NO CONTROL CARD TO PROCESS' TO PV-OPERATION-MSG-1  03560001
               SET AC-CONTROL-CARD-ERROR TO TRUE                        03570001
               PERFORM 9999-ABEND                                       03580001
           END-IF.                                                      03590001
           DISPLAY PC-CURRENT-PROGRAM-NAME                              03600001
                   ' - CONTROL CARD = (' CC-CONTROL-CARD-DISPLAY  ')'.  03610001
                                                                        03620001
      ******************************************************************03630001
       1110-READ-DATE-RANGE-CARD-FILE.                                  03640001
      *     READ DATE CONTROL CARD.                                     03650001
      *     CALL THE DATE ROUTINE TO VALIDATE START AND END DATES.      03660001
      ******************************************************************03670001
                                                                        03680001
           READ DATE-RANGE-CARD-FILE INTO CC-CONTROL-CARD               03690001
              AT END                                                    03700001
                 SET PS-END-OF-CARD-FILE-YES TO TRUE                    03710001
              NOT AT END                                                03720001
                  MOVE CC-START-DATE        TO PV-DPG52-LK-DATE-INPUT   03730001
                  PERFORM 1125-VALIDATE-INPUT-DATE                      03740001
                  MOVE CC-START-DATE        TO DK-BEGIN-DATE-ISO        03750001
                                                                        03760001
                  MOVE CC-END-DATE          TO PV-DPG52-LK-DATE-INPUT   03770001
                  PERFORM 1125-VALIDATE-INPUT-DATE                      03780001
                  MOVE CC-END-DATE          TO DK-END-DATE-ISO.         03790001
                                                                        03800001
                                                                        03810001
      ******************************************************************03820001
      * CALL KOHLS CALENDAR ROUTINE:                                    03830001
      *     PASS: CONTROL CARD POS DATE (CCYY-MM-DD FORMAT).            03840001
      *   RETURN: ACTUAL CALENDAR DB2 ISO DATE (CCYY-MM-DD FORMAT).     03850001
      ******************************************************************03860001
       1125-VALIDATE-INPUT-DATE.                                        03870001
                                                                        03880001
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03890001
                                                                        03900001
           SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   03910001
           SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   03920001
           SET  DPG52-LK-DTE-ISO-FMT         TO TRUE.                   03930001
                                                                        03940001
           MOVE PV-DPG52-LK-DATE-INPUT       TO DPG52-LK-DATE-INPUT.    03950001
                                                                        03960001
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    03970001
                                                   DPG54 DPG55 DPG56.   03980001
           EVALUATE TRUE                                                03990001
               WHEN DPG54-SEVERE-ERROR                                  04000001
               WHEN DPG54-DATE-INVALID                                  04010001
                   MOVE '1125-VALIDATE-INPUT-DATE'                      04020001
                                             TO PV-PARAGRAPH-NAME       04030001
                   MOVE 'ERROR CONVERTING INPUT CARD DATE'              04040001
                                             TO PV-OPERATION-MSG-1      04050001
                   SET AC-CALENDAR-ROUTINE-ERROR                        04060001
                                             TO TRUE                    04070001
                   MOVE DPG54-ERROR-MESSAGE                             04080001
                                             TO PV-OPERATION-MSG-2      04090001
                   PERFORM 9999-ABEND                                   04100001
           END-EVALUATE.                                                04110001
                                                                        04120001
                                                                        04130001
      ******************************************************************04140001
       1200-WRITE-HEADER-RECORD.                                        04150001
      ******************************************************************04160001
                                                                        04170001
           WRITE SALES-CORRECTION-CSV-RECORD                            04180001
               FROM SA-CORR-HEADER-RECORD.                              04190001
                                                                        04200001
           ADD +1   TO PV-REC-CNT.                                      04210001
                                                                        04220001
      ******************************************************************04230001
       2000-OPEN-KEY-INFO.                                              04240001
      *     GET THE CURRENT PROCESSING DAY' TRANSACTION.                04250001
      ******************************************************************04260001
                                                                        04270001
           EXEC SQL                                                     04280001
                OPEN KEY-INFO                                           04290001
           END-EXEC.                                                    04300001
                                                                        04310001
           IF NOT SQLCODE = ZERO                                        04320001
              MOVE '2000-OPEN-KEY-INFO'     TO PV-PARAGRAPH-NAME        04330001
              MOVE SQLCODE                  TO PV-DB2-OPERATION         04340001
              PERFORM 9100-SQL-ABEND                                    04350001
           END-IF.                                                      04360001
                                                                        04370001
      ******************************************************************04380001
       2200-FETCH-KEY-INFO.                                             04390001
      ******************************************************************04400001
                                                                        04410001
           EXEC SQL                                                     04420001
                FETCH KEY-INFO                                          04430001
                 INTO :EPTRN-PARTN-NBR,                                 04440001
                      :EPTRN-STR-NBR,                                   04450001
                      :EPTRN-RGST-ID,                                   04460001
                      :EPTRN-TRN-NBR,                                   04470001
                      :EPTRN-STRT-TM,                                   04480001
                      :EPTRN-SLS-CORR-SEQ-NBR,                          04490001
                      :EPTRN-VOID-ABRT-CDE,                             04500001
                      :EPTRN-TRN-STAT-CDE,                              04510001
                      :EPTRN-TRN-TYP-CDE,                               04520001
                      :EPTRN-TRN-DTE,                                   04530001
                      :EPTRN-SLS-DTE,                                   04540001
                      :EPTRN-PRCG-DTE,                                  04550001
                      :EPTRN-TRN-TOT-AMT,                               04560001
                      :EPTRN-ASSOC-ID                                   04570001
           END-EXEC.                                                    04580001
                                                                        04590001
           EVALUATE TRUE                                                04600001
              WHEN SQLCODE = ZERO                                       04610001
                   PERFORM 4000-SELECT-OPERATOR-ID                      04620001
                   PERFORM 4500-SELECT-NEW-OR-UPDATE                    04630001
                   IF EPTRN-STR-NBR = PV-SAVE-STORE                     04640001
                       CONTINUE                                         04650001
                   ELSE                                                 04660001
                       PERFORM 4600-SELECT-TERRITORY                    04670001
                       MOVE EPTRN-STR-NBR TO PV-SAVE-STORE              04680001
                   END-IF                                               04690001
                   PERFORM 6000-FORMAT-DETAIL-RECORD                    04700001
              WHEN SQLCODE = PC-NOT-FOUND                               04710001
                   SET PS-END-OF-KEY-INFO-YES      TO TRUE              04720001
              WHEN OTHER                                                04730001
                   MOVE '2200-FETCH-KEY-INFO'      TO PV-PARAGRAPH-NAME 04740001
                   MOVE SQLCODE                    TO PV-DB2-OPERATION  04750001
                   PERFORM 9100-SQL-ABEND                               04760001
           END-EVALUATE.                                                04770001
                                                                        04780001
      ******************************************************************04790001
       2400-CLOSE-KEY-INFO.                                             04800001
      ******************************************************************04810001
                                                                        04820001
           EXEC SQL                                                     04830001
                CLOSE KEY-INFO                                          04840001
           END-EXEC.                                                    04850001
                                                                        04860001
           IF NOT SQLCODE = ZERO                                        04870001
              MOVE '2400-CLOSE-KEY-INFO'     TO PV-PARAGRAPH-NAME       04880001
              MOVE SQLCODE                   TO PV-DB2-OPERATION        04890001
              PERFORM 9100-SQL-ABEND                                    04900001
           END-IF.                                                      04910001
                                                                        04920001
                                                                        04930001
      ******************************************************************04940001
       4000-SELECT-OPERATOR-ID.                                         04950001
      ******************************************************************04960001
                                                                        04970001
           EXEC SQL                                                     04980001
                SELECT SLS_AUD_USR_ID                                   04990001
                  INTO :EPSAT-SLS-AUD-USR-ID                            05000001
                  FROM TEPSAT                                           05010001
                 WHERE PARTN_NBR = :EPTRN-PARTN-NBR                     05020001
                   AND STR_NBR   = :EPTRN-STR-NBR                       05030001
                   AND RGST_ID   = :EPTRN-RGST-ID                       05040001
                   AND TRN_NBR   = :EPTRN-TRN-NBR                       05050001
                   AND STRT_TM   = :EPTRN-STRT-TM                       05060001
                   AND SLS_CORR_SEQ_NBR = :EPTRN-SLS-CORR-SEQ-NBR       05070001
                                   WITH UR                              05080001
           END-EXEC.                                                    05090001
                                                                        05100001
           EVALUATE SQLCODE                                             05110001
              WHEN ZERO                                                 05120001
                 CONTINUE                                               05130001
              WHEN PC-NOT-FOUND                                         05140001
                 MOVE '********'              TO EPSAT-SLS-AUD-USR-ID   05150001
              WHEN OTHER                                                05160001
                 MOVE '4000-SELECT-OPERATOR-ID'  TO PV-PARAGRAPH-NAME   05170001
                 MOVE SQLCODE                    TO PV-DB2-OPERATION    05180001
                 PERFORM 9100-SQL-ABEND                                 05190001
           END-EVALUATE.                                                05200001
                                                                        05210001
                                                                        05220001
      ******************************************************************05230001
       4500-SELECT-NEW-OR-UPDATE.                                       05240001
      ******************************************************************05250001
                                                                        05260001
           EXEC SQL                                                     05270001
                SELECT A.SLS_CORR_SEQ_NBR                               05280001
                     , A.TRN_STAT_CDE                                   05290001
                  INTO :PV-ORIG-SLS-CORR-SEQ-NBR                        05300001
                     , :PV-ORIG-TRN-STAT-CDE                            05310001
                  FROM TEPTRN  A                                        05320001
                 WHERE A.PARTN_NBR = :EPTRN-PARTN-NBR                   05330001
                   AND A.STR_NBR   = :EPTRN-STR-NBR                     05340001
                   AND A.RGST_ID   = :EPTRN-RGST-ID                     05350001
                   AND A.TRN_NBR   = :EPTRN-TRN-NBR                     05360001
                   AND A.STRT_TM   = :EPTRN-STRT-TM                     05370001
                   AND SLS_CORR_SEQ_NBR < :EPTRN-SLS-CORR-SEQ-NBR       05380001
                   AND A.SLS_CORR_SEQ_NBR =                             05390001
                      (SELECT MAX(B.SLS_CORR_SEQ_NBR)                   05400001
                         FROM TEPTRN  B                                 05410001
                        WHERE B.PARTN_NBR = :EPTRN-PARTN-NBR            05420001
                          AND B.STR_NBR   = :EPTRN-STR-NBR              05430001
                          AND B.RGST_ID   = :EPTRN-RGST-ID              05440001
                          AND B.TRN_NBR   = :EPTRN-TRN-NBR              05450001
                          AND B.STRT_TM   = :EPTRN-STRT-TM              05460001
                          AND B.SLS_CORR_SEQ_NBR                        05470001
                            < :EPTRN-SLS-CORR-SEQ-NBR)                  05480001
                WITH UR                                                 05490001
           END-EXEC.                                                    05500001
                                                                        05510001
           EVALUATE SQLCODE                                             05520001
              WHEN ZERO                                                 05530001
              WHEN -811                                                 05540001
                 SET PS-ORIG-TRAN-FOUND TO TRUE                         05550001
              WHEN PC-NOT-FOUND                                         05560001
                 SET PS-ORIG-TRAN-NOT-FOUND TO TRUE                     05570001
              WHEN OTHER                                                05580001
                 MOVE '4500-SELECT-NEW-OR-UPDATE' TO PV-PARAGRAPH-NAME  05590001
                 MOVE SQLCODE                     TO PV-DB2-OPERATION   05600001
                 PERFORM 9100-SQL-ABEND                                 05610001
           END-EVALUATE.                                                05620001
                                                                        05630001
                                                                        05640001
      ******************************************************************05650001
       4600-SELECT-TERRITORY.                                           05660001
      ******************************************************************05670001
                                                                        05680001
           EXEC SQL                                                     05690001
               SELECT  DIST.DIST_NBR                                    05700001
                      ,DIST.DIST_NM                                     05710001
                      ,REGN.REGN_NBR                                    05720001
                      ,REGN.REGN_NM                                     05730001
                      ,TERR.TERR_NBR                                    05740001
                      ,TERR.TERR_NM                                     05750001
                 INTO :XST00003-DIST-NBR                                05760001
                    , :XST00003-DIST-NM                                 05770001
                    , :XST00005-REGN-NBR                                05780001
                    , :XST00005-REGN-NM                                 05790001
                    , :XST00007-TERR-NBR                                05800001
                    , :XST00007-TERR-NM                                 05810001
                 FROM  XST_LOC_DIST        LD                           05820001
                      ,XST_DIST            DIST                         05830001
                      ,XST_DIST_REGN       DR                           05840001
                      ,XST_REGN            REGN                         05850001
                      ,XST_REGN_TERR       RT                           05860001
                      ,XST_TERR            TERR                         05870001
                WHERE  LD.LOC_NBR           = :EPTRN-STR-NBR            05880001
                  AND  LD.BEG_DTE          <= CURRENT DATE              05890001
                  AND (LD.END_DTE          >= CURRENT DATE - 1 DAY      05900001
                   OR  LD.END_DTE          IS NULL)                     05910001
                                                                        05920001
                  AND  LD.DIST_NBR          = DIST.DIST_NBR             05930001
                  AND  DIST.DIST_NBR        = DR.DIST_NBR               05940001
                  AND  DR.BEG_DTE          <= CURRENT DATE              05950001
                  AND (DR.END_DTE          >= CURRENT DATE - 1 DAY      05960001
                   OR  DR.END_DTE          IS NULL)                     05970001
                                                                        05980001
                  AND  DR.REGN_NBR          = REGN.REGN_NBR             05990001
                  AND  REGN.REGN_NBR        = RT.REGN_NBR               06000001
                  AND  RT.BEG_DTE          <= CURRENT DATE              06010001
                  AND (RT.END_DTE          >= CURRENT DATE - 1 DAY      06020001
                   OR  RT.END_DTE          IS NULL)                     06030001
                                                                        06040001
                  AND  RT.TERR_NBR          = TERR.TERR_NBR             06050001
                                                                        06060001
                WITH UR                                                 06070001
           END-EXEC.                                                    06080001
                                                                        06090001
           EVALUATE SQLCODE                                             06100001
              WHEN ZERO                                                 06110001
                 MOVE XST00007-TERR-NBR TO  PV-SAVE-TERRITORY           06120001
              WHEN PC-NOT-FOUND                                         06130001
                 MOVE ZEROES            TO  PV-SAVE-TERRITORY           06140001
              WHEN OTHER                                                06150001
                 MOVE '4600-SELECT-TERRITORY'     TO PV-PARAGRAPH-NAME  06160001
                 MOVE SQLCODE                     TO PV-DB2-OPERATION   06170001
                 PERFORM 9100-SQL-ABEND                                 06180001
           END-EVALUATE.                                                06190001
                                                                        06200001
                                                                        06210001
      ******************************************************************06220001
       6000-FORMAT-DETAIL-RECORD.                                       06230001
      *     FORMAT SALES CORRECTION DETAIL RECORD.                      06240001
      ******************************************************************06250001
                                                                        06260001
           MOVE EPTRN-PRCG-DTE           TO SA-DTL-PRCG-DATE.           06270001
           MOVE EPTRN-SLS-DTE            TO SA-DTL-SALES-DATE.          06280001
           MOVE EPTRN-STR-NBR            TO SA-DTL-STORE.               06290001
           MOVE EPTRN-RGST-ID            TO SA-DTL-REG.                 06300001
           MOVE EPTRN-TRN-NBR            TO SA-DTL-TRAN.                06310001
           MOVE EPTRN-ASSOC-ID           TO SA-DTL-ASSOC-ID.            06320001
           MOVE EPSAT-SLS-AUD-USR-ID     TO SA-DTL-USER-ID.             06330001
           MOVE EPTRN-TRN-STAT-CDE       TO SA-DTL-TRAN-STATUS.         06340001
           MOVE EPTRN-TRN-TYP-CDE        TO SA-DTL-TRAN-TYPE.           06350001
           MOVE EPTRN-VOID-ABRT-CDE      TO SA-DTL-VOID-CD.             06360001
           IF PS-ORIG-TRAN-FOUND                                        06370001
      **       MOVE 'CORRECTION'         TO SA-DTL-ORIG-TRN-STAT        06380001
               PERFORM 6100-EVALUATE-ORIG-TRN-STAT-CD                   06390001
           ELSE                                                         06400001
               MOVE 'MISSING TRAN'       TO SA-DTL-ORIG-TRN-STAT        06410001
           END-IF.                                                      06420001
           MOVE EPTRN-TRN-TOT-AMT        TO SA-DTL-TRAN-AMT.            06430001
           MOVE EPTRN-SLS-CORR-SEQ-NBR   TO SA-DTL-CORR-SEQ.            06440001
           MOVE PV-SAVE-TERRITORY        TO SA-DTL-TERR-NBR.            06450001
                                                                        06460001
           PERFORM 8200-WRITE-DETAIL-RECORD.                            06470001
                                                                        06480001
      ******************************************************************06490001
       6100-EVALUATE-ORIG-TRN-STAT-CD.                                  06500001
      *     CONVERT TRN-STAT-CDE TO A LITERAL.                          06510001
      ******************************************************************06520001
                                                                        06530001
           EVALUATE TRUE                                                06540001
             WHEN PV-ORIG-TRN-STAT-CDE = 'A'                            06550001
               MOVE 'INVALID ACCT NBR LENGTH'   TO SA-DTL-ORIG-TRN-STAT 06560001
             WHEN PV-ORIG-TRN-STAT-CDE = 'B'                            06570001
               MOVE 'TENDER OUT OF BALANCE'     TO SA-DTL-ORIG-TRN-STAT 06580001
             WHEN PV-ORIG-TRN-STAT-CDE = 'C'                            06590001
               MOVE 'INVALID ACCT NBR CHKDIGIT' TO SA-DTL-ORIG-TRN-STAT 06600001
             WHEN PV-ORIG-TRN-STAT-CDE = 'D'                            06610001
               MOVE 'DB2 ERROR'                 TO SA-DTL-ORIG-TRN-STAT 06620001
             WHEN PV-ORIG-TRN-STAT-CDE = 'E'                            06630001
               MOVE 'ERROR'                     TO SA-DTL-ORIG-TRN-STAT 06640001
             WHEN PV-ORIG-TRN-STAT-CDE = 'H'                            06650001
               MOVE 'HARD TOTAL INCOMPLETE'     TO SA-DTL-ORIG-TRN-STAT 06660001
             WHEN PV-ORIG-TRN-STAT-CDE = 'I'                            06670001
               MOVE 'INVALID STORE NBR'         TO SA-DTL-ORIG-TRN-STAT 06680001
             WHEN PV-ORIG-TRN-STAT-CDE = 'L'                            06690001
               MOVE 'LATE DATA COLLECT'         TO SA-DTL-ORIG-TRN-STAT 06700001
             WHEN PV-ORIG-TRN-STAT-CDE = 'M'                            06710001
               MOVE 'MISSING ACCT NBR'          TO SA-DTL-ORIG-TRN-STAT 06720001
             WHEN PV-ORIG-TRN-STAT-CDE = 'N'                            06730001
               MOVE 'NON-NUMERIC ACCT NBR'      TO SA-DTL-ORIG-TRN-STAT 06740001
             WHEN PV-ORIG-TRN-STAT-CDE = 'O'                            06750001
               MOVE 'ITEM OUT OF BALANCE'       TO SA-DTL-ORIG-TRN-STAT 06760001
             WHEN PV-ORIG-TRN-STAT-CDE = 'P'                            06770001
               MOVE 'INVALID ACCT NBR PREFIX'   TO SA-DTL-ORIG-TRN-STAT 06780001
             WHEN PV-ORIG-TRN-STAT-CDE = 'R'                            06790001
               MOVE 'REVERSAL ENTRY'            TO SA-DTL-ORIG-TRN-STAT 06800001
             WHEN PV-ORIG-TRN-STAT-CDE = 'S'                            06810001
               MOVE 'MISSING TOTAL'             TO SA-DTL-ORIG-TRN-STAT 06820001
             WHEN PV-ORIG-TRN-STAT-CDE = 'T'                            06830001
               MOVE 'MISSING TENDER'            TO SA-DTL-ORIG-TRN-STAT 06840001
             WHEN PV-ORIG-TRN-STAT-CDE = 'U'                            06850001
               MOVE 'SUSPENDED TRAN'            TO SA-DTL-ORIG-TRN-STAT 06860001
             WHEN PV-ORIG-TRN-STAT-CDE = 'V'                            06870001
               MOVE 'VALID SALES CORRECTION'    TO SA-DTL-ORIG-TRN-STAT 06880001
             WHEN PV-ORIG-TRN-STAT-CDE = 'W'                            06890001
               MOVE 'INVALID SALES CORRECTION'  TO SA-DTL-ORIG-TRN-STAT 06900001
             WHEN PV-ORIG-TRN-STAT-CDE = 'Z'                            06910001
               MOVE 'VALID SALES TRANSACTION'   TO SA-DTL-ORIG-TRN-STAT 06920001
             WHEN OTHER                                                 06930001
                MOVE PV-ORIG-TRN-STAT-CDE       TO SA-DTL-ORIG-TRN-STAT 06940001
           END-EVALUATE.                                                06950001
                                                                        06960001
      ******************************************************************06970001
       8200-WRITE-DETAIL-RECORD.                                        06980001
      *     WRITE REPORT HEADINGS OTHER THAN THE FIRST LINE.            06990001
      ******************************************************************07000001
                                                                        07010001
           WRITE SALES-CORRECTION-CSV-RECORD                            07020001
               FROM SA-CORR-DETAIL-RECORD.                              07030001
                                                                        07040001
           ADD +1   TO PV-REC-CNT.                                      07050001
                                                                        07060001
                                                                        07070001
      ******************************************************************07080001
       9100-SQL-ABEND.                                                  07090001
      *     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.07100001
      ******************************************************************07110001
           DISPLAY '** ABEND     **'.                                   07120001
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        07130001
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              07140001
           DISPLAY '** SQLCODE   ** = ' PV-DB2-OPERATION.               07150001
           DISPLAY '** STORE NBR ** = ' EPTRN-STR-NBR.                  07160001
           DISPLAY '** REGISTER  ** = ' EPTRN-RGST-ID.                  07170001
           DISPLAY '** TRAN NBR  ** = ' EPTRN-TRN-NBR.                  07180001
                                                                        07190001
      ******************************************************************07200001
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     07210001
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      07220001
      ******************************************************************07230001
           COPY DPPD004.                                                07240001
                                                                        07250001
           SET AC-DB2-ERROR TO TRUE.                                    07260001
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         07270001
                                                                        07280001
      ******************************************************************07290001
       9999-ABEND.                                                      07300001
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  07310001
      ******************************************************************07320001
           DISPLAY '** ABEND           **'.                             07330001
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  07340001
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        07350001
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       07360001
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       07370001
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         07380001
      ******************************************************************07390001
      ******************************************************************07400001
