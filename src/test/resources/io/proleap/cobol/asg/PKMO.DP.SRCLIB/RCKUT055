000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    RCKUT055.                                                 
000600 AUTHOR.        RON KRIER.                                                
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   JANUARY 1998.                                             
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM CREATES A MERGE FILE TO BE USED TO CREATE THE             
001300*  RECEIPT ADJUSTMENTS TO RECEIVERS HAVING STORES WITH PHYSICAL           
001310*  INVENTORY DC CUTOFF DATES REPORT (RCKRP020).                           
001320*                                                                         
001330*  THIS PROGRAM WILL MERGE A TRIGGER FILE OF STORES THAT COULD            
001340*  NOT BE ADJUSTED ALONG WITH THE ACTUAL ADJUSTMENT FILE OF STORES        
001350*  THAT DID GET ADJUSTED.                                                 
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
002900                                                                          
003000     SELECT TRIGGER-FILE                                                  
003100         ASSIGN TO INP01.                                                 
003200                                                                          
003300     SELECT ADJUST-FILE                                                   
003400         ASSIGN TO INP02.                                                 
003500                                                                          
003600     SELECT MERGE-FILE                                                    
003700         ASSIGN TO OUT01.                                                 
003800******************************************************************        
003900 DATA DIVISION.                                                           
004000******************************************************************        
004100                                                                          
004200 FILE SECTION.                                                            
004300                                                                          
004400 FD  TRIGGER-FILE                                                         
004500     RECORDING MODE IS F                                                  
004600     LABEL RECORDS ARE STANDARD                                           
004700     BLOCK CONTAINS 0 RECORDS.                                            
004800                                                                          
004900 01  TRIGGER-REC                PIC  X(175).                              
005000                                                                          
005100 FD  ADJUST-FILE                                                          
005200     RECORDING MODE IS F                                                  
005300     LABEL RECORDS ARE STANDARD                                           
005400     BLOCK CONTAINS 0 RECORDS.                                            
005500                                                                          
005600 01  ADJUST-REC                 PIC  X(135).                              
005900                                                                          
006000 FD  MERGE-FILE                                                           
006100     RECORDING MODE IS F                                                  
006200     LABEL RECORDS ARE STANDARD                                           
006300     BLOCK CONTAINS 0 RECORDS.                                            
006400                                                                          
006500 01  MERGE-REC                  PIC  X(125).                              
006600*                                                                         
006700*                                                                         
006800******************************************************************        
006900 WORKING-STORAGE SECTION.                                                 
007000******************************************************************        
012000*                                                                         
012100 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
012200                                                   COMP SYNC.             
012300                                                                          
012400 01  PS-PROGRAM-SWITCHES.                                                 
012500     05  PS-EOF-TRIGGER-SW           PIC  X(01)    VALUE 'N'.             
012600         88  PS-EOF-TRIGGER                        VALUE 'Y'.             
012700         88  PS-MORE-TRIGGER                       VALUE 'N'.             
012800     05  PS-EOF-ADJUST-SW            PIC  X(01)    VALUE 'N'.             
012900         88  PS-EOF-ADJUST                         VALUE 'Y'.             
013000         88  PS-MORE-ADJUST                        VALUE 'N'.             
013100                                                                          
013200                                                                          
013300 01  PC-PROGRAM-CONSTANTS.                                                
013400     05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100             
013500                                                   COMP SYNC.             
013600     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
013700         'RCKUT055'.                                                      
013800     05  PC-CURRENT-OPERATING-COMPANY                                     
013900                                     PIC  X(02)    VALUE '03'.            
014000     05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3               
014100                                                   COMP SYNC.             
014200     05  PC-CURRENT-TIMESTAMP        PIC  X(26)    VALUE SPACES.          
014300                                                                          
014400 01  PV-PROGRAM-CONSTANTS.                                                
014401     05  PC-ADJUST-LIT               PIC X(10)     VALUE                  
014402         'ADJUST    '.                                                    
014403     05  PC-NO-ADJUST-LIT            PIC X(10)     VALUE                  
014404         'NO ADJUST '.                                                    
014405                                                                          
014406 01  PV-PROGRAM-VARIABLES.                                                
014410     05  PV-CURR-TRIGGER-KEY.                                             
014420         10  PV-CURR-TRIGGER-PO      PIC S9(09)    COMP                   
014430                                                   VALUE ZERO.            
014440         10  PV-CURR-TRIGGER-REC     PIC S9(09)    COMP                   
014450                                                   VALUE ZERO.            
014451     05  PV-PREV-TRIGGER-KEY.                                             
014452         10  PV-PREV-TRIGGER-PO      PIC S9(09)    COMP                   
014453                                                   VALUE ZERO.            
014454         10  PV-PREV-TRIGGER-REC     PIC S9(09)    COMP                   
014455                                                   VALUE ZERO.            
014456     05  PV-ADJUST-KEY.                                                   
014457         10  PV-ADJUST-PO            PIC S9(09)    COMP                   
014458                                                   VALUE ZERO.            
014459         10  PV-ADJUST-REC           PIC S9(09)    COMP                   
014460                                                   VALUE ZERO.            
014470                                                                          
014500     05  PV-ITEM-NBR                 PIC  9(15)    VALUE ZERO.            
014600     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
014700     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
014800     05  PV-OPEN-COUNTER             PIC S9(04)    VALUE ZERO             
014900                                                   COMP SYNC.             
015000     05  PV-CLOSE-COUNTER            PIC S9(04)    VALUE ZERO             
015100                                                   COMP SYNC.             
015200     05  PV-FETCH-COUNTER            PIC S9(04)    VALUE ZERO             
015300                                                   COMP SYNC.             
015400     05  PV-WRITE-COUNTER            PIC S9(09)    VALUE ZERO             
015500                                                   COMP SYNC.             
015600     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                     
015700                                                                          
015800     05  PV-DB2-COUNT                PIC S9(09)    VALUE ZERO             
015900                                                   COMP-3.                
018700                                                                          
018800******************************************************************        
018900*  COPYBOOK FOR TRIGGER FILE                                              
019000******************************************************************        
019010                                                                          
019011     COPY RCRD014.                                                        
019012                                                                          
019013                                                                          
019014******************************************************************        
019015*  COPYBOOK FOR ADJUST FILE                                               
019016******************************************************************        
019017                                                                          
019018     COPY RCRD015.                                                        
019019                                                                          
019020                                                                          
019021******************************************************************        
019022*  COPYBOOK FOR MERGE FILE                                                
019023******************************************************************        
019024                                                                          
019025     COPY RCRD034.                                                        
019026                                                                          
019030******************************************************************        
019040 PROCEDURE DIVISION.                                                      
019050******************************************************************        
019100                                                                          
019200 0000-PROGRAM-MAINLINE.                                                   
019300******************************************************************        
019400* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
019500*      PROGRAM.                                                           
019600******************************************************************        
019700                                                                          
019800     PERFORM 1000-INITIALIZE.                                             
019810                                                                          
019900     IF PS-MORE-TRIGGER                                                   
020000         PERFORM 2000-PROCESS-TRIGGER                                     
020100            UNTIL PS-EOF-TRIGGER                                          
020200     END-IF.                                                              
020210                                                                          
020300     PERFORM 9000-EOJ-ROUTINE.                                            
020400                                                                          
020500     STOP RUN.                                                            
020600                                                                          
020700 1000-INITIALIZE.                                                         
020800******************************************************************        
020900*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
021000*    -- OPEN FILES                                                        
021100*    -- PERFORM FIRST READ                                                
021300******************************************************************        
021400                                                                          
021500     OPEN INPUT TRIGGER-FILE                                              
021600                ADJUST-FILE                                               
021700          OUTPUT MERGE-FILE.                                              
021800                                                                          
021810     PERFORM 8000-READ-TRIGGER-REC.                                       
021900                                                                          
022000                                                                          
022010 2000-PROCESS-TRIGGER.                                                    
022200******************************************************************        
022300*  LOOP THROUGH THE TRIGGER FILE.  IF NEED TO, MATCH TO THE ADJUST        
022400*  FILE TO GET ALL THE ADJUSTMENTS FOR THE PO/RECEIVER.                   
022600******************************************************************        
022700                                                                          
022710     IF  RC014-UPD-PGM-ID = 'RCKCS277'                                    
022711         INITIALIZE RC034-ADJUST-COMBINE-RECORD                           
022712         MOVE 'A'                      TO                                 
022713              RC034-REC-TYPE-CDE                                          
022714         MOVE ZEROS                    TO                                 
022715              RC034-ORD-LNE-NBR                                           
022720         PERFORM 2100-FORMAT-WRITE                                        
022721         MOVE 'D'                      TO                                 
022722              RC034-REC-TYPE-CDE                                          
022723         MOVE ZEROS                    TO                                 
022725              RC034-ORD-LNE-NBR                                           
022726         PERFORM 2100-FORMAT-WRITE                                        
022730     ELSE                                                                 
022731         INITIALIZE RC034-ADJUST-COMBINE-RECORD                           
022732         MOVE 'C'                      TO                                 
022733              RC034-REC-TYPE-CDE                                          
022734         MOVE RC014-ORD-LNE-NBR        TO                                 
022735              RC034-ORD-LNE-NBR                                           
022736         PERFORM 2100-FORMAT-WRITE                                        
022737         MOVE 'D'                      TO                                 
022738              RC034-REC-TYPE-CDE                                          
022739         MOVE 99999                    TO                                 
022740              RC034-ORD-LNE-NBR                                           
022741         PERFORM 2100-FORMAT-WRITE                                        
022742         IF  PV-CURR-TRIGGER-KEY =                                        
022743             PV-PREV-TRIGGER-KEY                                          
022744             CONTINUE                                                     
022745         ELSE                                                             
022746             MOVE PV-CURR-TRIGGER-KEY TO                                  
022747                  PV-PREV-TRIGGER-KEY                                     
022748             IF  PV-CURR-TRIGGER-KEY <                                    
022749                 PV-ADJUST-KEY                                            
022750                 CONTINUE                                                 
022751             ELSE                                                         
022752                 PERFORM 2200-PROCESS-ADJUST                              
022753                     UNTIL PV-ADJUST-KEY > PV-CURR-TRIGGER-KEY            
022754                        OR PS-EOF-ADJUST                                  
022755             END-IF                                                       
022756         END-IF                                                           
022757     END-IF.                                                              
022758                                                                          
022759     PERFORM 8000-READ-TRIGGER-REC.                                       
022760                                                                          
024300                                                                          
024310 2100-FORMAT-WRITE.                                                       
024320******************************************************************        
024330*  FORMAT AND WRITE A MERGED RECORD FROM THE TRIGGER FILE                 
024331*  BECAUSE THE TRIGGER RECORD WAS CREATED FROM THE ONLINE                 
024332*  SYSTEM BASED ON THE RCKCS277 PROGRAM WHICH MEANS THAT A                
024333*  SPECIFIC STORE WAS ATTEMPTING TO BE ADJUSTED AND THE REPORT            
024334*  SHOULD DISPLAY THE LITERAL 'ALL' FOR THE LINE.                         
024340******************************************************************        
024350                                                                          
024370                                                                          
024380     MOVE RC014-ORD-NBR            TO                                     
024381          RC034-ORD-NBR.                                                  
024391     MOVE RC014-REC-SEQ-NBR        TO                                     
024392          RC034-REC-SEQ-NBR.                                              
024393     MOVE RC014-OPER-UNT-ID        TO                                     
024394          RC034-OPER-UNT-ID.                                              
024395     MOVE RC014-FCLTY-ID           TO                                     
024396          RC034-FCLTY-ID.                                                 
024397     MOVE RC014-STR-NBR            TO                                     
024398          RC034-STR-NBR.                                                  
024399     MOVE RC014-UPD-TMST           TO                                     
024400          RC034-UPD-TMST.                                                 
024401     MOVE RC014-UPD-PGM-ID         TO                                     
024402          RC034-UPD-PGM-ID.                                               
024403                                                                          
024404     WRITE MERGE-REC FROM RC034-ADJUST-COMBINE-RECORD.                    
024405                                                                          
024406 2200-PROCESS-ADJUST.                                                     
024407******************************************************************        
024408*  FIND ADJUSTMENT RECORDS FOR THE NEEDED PO/RECEIVER AND WRITE           
024409*  THEM TO THE MERGE FILE.                                                
024410******************************************************************        
024411                                                                          
024412     IF  PV-ADJUST-KEY < PV-CURR-TRIGGER-KEY                              
024413         PERFORM 8100-READ-AJDUST-REC                                     
024414     ELSE                                                                 
024415         IF  PV-ADJUST-KEY = PV-CURR-TRIGGER-KEY                          
024416             PERFORM 2300-FORMAT-WRITE                                    
024417             PERFORM 8100-READ-AJDUST-REC                                 
024418         END-IF                                                           
024419     END-IF.                                                              
024420                                                                          
024421 2300-FORMAT-WRITE.                                                       
024423******************************************************************        
024424*  FORMAT AND WRITE A MERGED RECORD FROM THE ADJUST FILE                  
024425*  BECAUSE THE ADJUST RECORD WAS CREATED BASED ON AN ADJUSTMENT           
024426*  WHICH TOOK PLACE IN THE ONLINE SYSTEM ON A LINE IN WHICH STORES        
024427*  COULD NOT BE UPDATED BECAUSE OF A PHYSICAL INVENTORY CYCLE (THE        
024428*  STORES FOUND ON THE TRIGGER FILE).                                     
024432******************************************************************        
024434                                                                          
024435     INITIALIZE RC034-ADJUST-COMBINE-RECORD.                              
024436                                                                          
024438     MOVE RC015-ORD-LNE-NBR        TO                                     
024439          RC034-ORD-LNE-NBR.                                              
024440     MOVE RC015-ORD-NBR            TO                                     
024441          RC034-ORD-NBR.                                                  
024442     MOVE RC015-REC-SEQ-NBR        TO                                     
024443          RC034-REC-SEQ-NBR.                                              
024444     MOVE RC015-OPER-UNT-ID        TO                                     
024445          RC034-OPER-UNT-ID.                                              
024446     MOVE RC015-FCLTY-ID-ID        TO                                     
024447          RC034-FCLTY-ID.                                                 
024448     MOVE RC015-STR-NBR            TO                                     
024449          RC034-STR-NBR.                                                  
024450     MOVE RC015-UPD-TMST           TO                                     
024451          RC034-UPD-TMST.                                                 
024452     MOVE RC015-UPD-PGM-ID         TO                                     
024453          RC034-UPD-PGM-ID.                                               
024454     MOVE 'B'                      TO                                     
024455          RC034-REC-TYPE-CDE.                                             
024456     MOVE RC015-ADJ-ITM-QTY        TO                                     
024457          RC034-ADJ-ITM-QTY.                                              
024458     MOVE RC015-ADJ-RTL-EXTN-AMT   TO                                     
024459          RC034-ADJ-RTL-EXTN-AMT.                                         
024460     MOVE RC015-ADJ-COST-EXTN-AMT  TO                                     
024461          RC034-ADJ-COST-EXTN-AMT.                                        
024462     MOVE RC015-B4-ITM-QTY         TO                                     
024463          RC034-B4-ITM-QTY.                                               
024464     MOVE RC015-AFT-ITM-QTY        TO                                     
024465          RC034-AFT-ITM-QTY.                                              
024466                                                                          
024467     WRITE MERGE-REC FROM RC034-ADJUST-COMBINE-RECORD.                    
024468                                                                          
024470 8000-READ-TRIGGER-REC.                                                   
024600******************************************************************        
024700*  READ THE SHIPMENT-LOC-REC                                              
024800******************************************************************        
024900     READ TRIGGER-FILE                                                    
025000         INTO RC014-TRECDIS-AUDIT-RECORD                                  
025100          AT END                                                          
025200             SET PS-EOF-TRIGGER TO TRUE.                                  
025210                                                                          
025220     IF  PS-EOF-TRIGGER                                                   
025230         CONTINUE                                                         
025240     ELSE                                                                 
025250         MOVE RC014-ORD-NBR TO                                            
025260              PV-CURR-TRIGGER-PO                                          
025270         MOVE RC014-REC-SEQ-NBR TO                                        
025280              PV-CURR-TRIGGER-REC                                         
025290     END-IF.                                                              
025300                                                                          
025310 8100-READ-AJDUST-REC.                                                    
025500******************************************************************        
025600*  READ THE PACKSLIP-REC                                                  
025700******************************************************************        
025800     READ ADJUST-FILE                                                     
025900         INTO RC015-TRECDIS-ADJ-RECORD                                    
026000          AT END                                                          
026100             SET PS-EOF-ADJUST TO TRUE.                                   
026110                                                                          
026120     IF  PS-EOF-ADJUST                                                    
026130         MOVE 999999999 TO                                                
026131              PV-ADJUST-PO                                                
026132         MOVE 999999999 TO                                                
026133              PV-ADJUST-REC                                               
026140     ELSE                                                                 
026141         MOVE RC015-ORD-NBR TO                                            
026142              PV-ADJUST-PO                                                
026143         MOVE RC015-REC-SEQ-NBR TO                                        
026144              PV-ADJUST-REC                                               
026145     END-IF.                                                              
027300                                                                          
027400 9000-EOJ-ROUTINE.                                                        
027500******************************************************************        
027600*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
027700******************************************************************        
028010     CLOSE TRIGGER-FILE                                                   
028020           ADJUST-FILE                                                    
028030           MERGE-FILE.                                                    
