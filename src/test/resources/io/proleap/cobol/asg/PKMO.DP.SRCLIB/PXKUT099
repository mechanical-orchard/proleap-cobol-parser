       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.           PXKUT099.                                  00020000
       AUTHOR.               DAVE WELLER.                               00030000
       INSTALLATION.         KOHLS DEPARTMENT STORES.                   00040000
       DATE-WRITTEN          05/2010.                                   00050000
       DATE-COMPILED.                                                   00060000
                                                                        00070000
      ******************************************************************00080000
      *   PXKUT099 - MATCH EXTRACT FILE TO EXCLUSION FILE AND DISCARD   00090000
      *              AND RECORD FROM EXTRACT FILE FOR ANY MATCH ON SKU  00100000
      *              AND STORE FOUND ON EXCLUSION FILE.                 00101000
      ******************** HISTORY OF CHANGES **************************00110000
      * CHG ID/                                                        *00120000
      * WR/PROJ          DATE        DESCRIPTION OF CHANGES            *00130000
      * ---------------  ----------  --------------------------------  *00140000
      * WR36056          08/01/2010  INITIAL IMPLEMENTATION             00150000
      *                                                                 00160000
      ******************************************************************00170000
                                                                        00180000
       ENVIRONMENT DIVISION.                                            00190000
                                                                        00200000
       CONFIGURATION SECTION.                                           00210000
                                                                        00220000
       SOURCE-COMPUTER. IBM-3090.                                       00230000
       OBJECT-COMPUTER. IBM-3090.                                       00240000
                                                                        00250000
       INPUT-OUTPUT SECTION.                                            00260000
       FILE-CONTROL.                                                    00270000
           SELECT EVENT-EXTR-FILE                                       00280000
               ASSIGN TO INP01.                                         00290000
                                                                        00300000
           SELECT EXCL-FILE                                             00310000
               ASSIGN TO INP02.                                         00320000
                                                                        00330000
           SELECT NON-EXCLUDED-FILE                                     00340000
               ASSIGN TO OUT01.                                         00350000
                                                                        00360000
       DATA DIVISION.                                                   00370000
                                                                        00380000
       FILE SECTION.                                                    00390000
       FD  EVENT-EXTR-FILE                                              00400000
           LABEL RECORDS ARE STANDARD                                   00410000
           RECORDING MODE IS F                                          00420000
           BLOCK CONTAINS 0 RECORDS.                                    00430000
       01  EVENT-EXTR-REC                  PIC  X(080).                 00440000
                                                                        00450000
       FD  EXCL-FILE                                                    00460000
           LABEL RECORDS ARE STANDARD                                   00470000
           RECORDING MODE IS F                                          00480000
           BLOCK CONTAINS 0 RECORDS.                                    00490000
       01  EXCL-REC                        PIC  X(007).                 00500000
                                                                        00510000
       FD  NON-EXCLUDED-FILE                                            00520000
           LABEL RECORDS ARE STANDARD                                   00530000
           RECORDING MODE IS F                                          00540000
           BLOCK CONTAINS 0 RECORDS.                                    00550000
       01  NON-EXCLUDED-REC                PIC X(080).                  00560000
                                                                        00570000
       WORKING-STORAGE SECTION.                                         00580000
                                                                        00590000
                                                                        00600000
                                                                        00610000
      ***************************************************************** 00620000
      *  PROGRAM'S ACCUMULATORS                                       * 00630000
      ***************************************************************** 00640000
       01  PA-PROGRAM-ACCUMULATORS.                                     00650000
           05  PA-RECORDS-READ             PIC 9(10)       VALUE ZEROES.00660000
           05  PA-RECORDS-DISCARDED        PIC 9(10)       VALUE ZEROES.00662000
           05  PA-RECORDS-WRITTEN          PIC 9(10)       VALUE ZEROES.00663000
                                                                        00664000
      ***************************************************************** 00665000
      *  PROGRAM'S CONSTANTS                                          * 00666000
      ***************************************************************** 00667000
       01  PC-PROGRAM-CONSTANTS.                                        00668000
           05  PC-PROGRAM-NAME             PIC X(08)       VALUE        00669000
               'PXKUT099'.                                              00670000
                                                                        00680000
      ***************************************************************** 00690000
      *  PROGRAM'S SWITCHES                                           * 00700000
      ***************************************************************** 00710000
       01  PS-PROGRAM-SWITCHES.                                         00720000
           05  PS-END-OF-EXTR-INP-FILE-SW PIC X(01)        VALUE 'N'.   00730000
               88  PS-END-OF-EXTR-INP-FILE                 VALUE 'Y'.   00740000
               88  PS-NOT-END-OF-EXTR-INP-FILE             VALUE 'N'.   00750000
           05  PS-END-OF-EXC-INP-FILE-SW PIC X(01)         VALUE 'N'.   00760002
               88  PS-END-OF-EXC-INP-FILE                  VALUE 'Y'.   00770002
               88  PS-NOT-END-OF-EXC-INP-FILE              VALUE 'N'.   00780002
                                                                        00820000
      ***************************************************************** 00830000
      *  PROGRAM'S VARIABLES                                          * 00840000
      ***************************************************************** 00850000
       01  PV-PROGRAM-VARIABLES.                                        00860000
           05  PV-PARAGRAPH-NAME           PIC X(40)   VALUE SPACES.    00870000
           05  PV-OPERATION-MSG            PIC  X(60)  VALUE SPACES.    00880000
           05  PV-OPERATION-MSG-1          PIC  X(40)  VALUE SPACES.    00890000
           05  PV-OPERATION-MSG-2          PIC  X(40)  VALUE SPACES.    00900000
      ******************************************************************01190000
      **COPYBOOKS                                                       01200000
      ******************************************************************01210000
                                                                        01240000
      ****CTA EXCLUSION INPUT RECORD LAYOUT                             01250001
           COPY PXRD053.                                                01260000
                                                                        01270000
      ****YELLOW TICKET EVENT EXTRACT OUTPUT RECORD LAYOUT              01280000
           COPY PXRD050.                                                01290000
                                                                        01300000
                                                                        01310000
       PROCEDURE DIVISION.                                              01320000
                                                                        01330000
      ******************************************************************01340000
      *    MAINLINE LOGIC                                              *01350000
      ******************************************************************01360000
                                                                        01370000
       0000-MAINLINE.                                                   01380000
                                                                        01390000
           PERFORM 1000-INITIALIZATION.                                 01400000
                                                                        01410000
           PERFORM 2000-PRCSS-EVENT-EXTR-FL                             01420000
               UNTIL PS-END-OF-EXTR-INP-FILE.                           01430000
                                                                        01440000
           PERFORM 9900-EOJ-LOGIC.                                      01450000
                                                                        01460000
           GOBACK.                                                      01470000
                                                                        01480000
      ******************************************************************01490000
      *    OPEN FILES AND READ FIRST RECORDS FROM INPUT FILES.         *01500000
      ******************************************************************01510000
       1000-INITIALIZATION.                                             01520000
                                                                        01530000
           OPEN INPUT  EVENT-EXTR-FILE                                  01540000
                       EXCL-FILE                                        01550000
               OUTPUT  NON-EXCLUDED-FILE.                               01560000
                                                                        01570000
           PERFORM 6100-READ-EVENT-EXTR-FL.                             01580000
           PERFORM 6200-READ-EXCL-FL.                                   01590000
           IF PS-END-OF-EXTR-INP-FILE                                   01600000
               DISPLAY ' *** NOTHING TO PROCESS ***'                    01610000
               DISPLAY ' '                                              01620000
           END-IF.                                                      01630000
                                                                        01640000
      ******************************************************************01650000
      *    2000-PRCSS-EVENT-EXTR-FL                                    *01660000
      *    THE INPUT FILES ARE IN SKU, STORE SEQUENCE.                 *01670000
      ******************************************************************01680000
       2000-PRCSS-EVENT-EXTR-FL.                                        01690000
                                                                        01700000
           IF PX053-SKU-NBR = PXRD050-SKU-NBR                           01710000
             IF PX053-STR-NBR = PXRD050-STR-NBR                         01720000
               ADD 1 TO PA-RECORDS-DISCARDED                            01770000
      * DO NOT WRITE WHEN A MATCH IS FOUND                              01780000
               PERFORM 6200-READ-EXCL-FL                                01800000
               PERFORM 6100-READ-EVENT-EXTR-FL                          01810000
             ELSE                                                       01820000
               IF PX053-STR-NBR < PXRD050-STR-NBR                       01840000
                 PERFORM 6200-READ-EXCL-FL                              01850000
               ELSE                                                     01860000
      * WRITE NON-EXCLUDED RECORD WHEN NO MATCH IS FOUND                01870000
                 PERFORM 6300-WRITE-NON-EXCLUDED-FL                     01890000
                 PERFORM 6100-READ-EVENT-EXTR-FL                        01910000
           ELSE                                                         01920000
           IF PX053-SKU-NBR < PXRD050-SKU-NBR                           01940000
             PERFORM 6200-READ-EXCL-FL                                  01950000
           ELSE                                                         01960000
      * WRITE NON-EXCLUDED RECORD WHEN NO MATCH IS FOUND                01961000
             PERFORM 6300-WRITE-NON-EXCLUDED-FL                         01990000
             PERFORM 6100-READ-EVENT-EXTR-FL.                           02010000
                                                                        02020000
      ******************************************************************02030000
      *  READ EXTRACT INPUT FILE                                        02040000
      ******************************************************************02050000
       6100-READ-EVENT-EXTR-FL.                                         02060000
                                                                        02070000
           READ EVENT-EXTR-FILE INTO PXRD050-DATA-RECORD                02080000
               AT END SET PS-END-OF-EXTR-INP-FILE TO TRUE.              02090000
                                                                        02100000
           IF PS-NOT-END-OF-EXTR-INP-FILE                               02110000
               ADD 1 TO PA-RECORDS-READ                                 02150000
           END-IF.                                                      02170000
                                                                        02180000
      ******************************************************************02190000
      *  READ ADJUSTMENT INPUT FILE                                     02200000
      ******************************************************************02210000
       6200-READ-EXCL-FL.                                               02220000
                                                                        02230000
           READ EXCL-FILE INTO PX053-SKU-STR-EXCLSN-REC                 02240000
               AT END SET PS-END-OF-EXC-INP-FILE TO TRUE.               02250002
                                                                        02260000
           IF PS-END-OF-EXC-INP-FILE                                    02270002
               MOVE ALL '9' TO PX053-SKU-NBR                            02280000
                               PX053-STR-NBR                            02290000
           END-IF.                                                      02300000
                                                                        02310000
      ******************************************************************02320000
      *  WRITE OH QTY ADJ FILE                                          02330000
      ******************************************************************02340000
       6300-WRITE-NON-EXCLUDED-FL.                                      02350000
           WRITE NON-EXCLUDED-REC                                       02360000
             FROM PXRD050-DATA-RECORD.                                  02370000
           ADD 1 TO PA-RECORDS-WRITTEN.                                 02380000
                                                                        02390000
      ******************************************************************02400000
      *    9900-EOJ-LOGIC.                                              02410000
      *      THIS IS PERFORMED AT END OF INPUT FILE.                    02420000
      *      PURPOSE: CLOSE INPUT FILE,                                 02430000
      *      DISPLAY RECORD COUNTS.                                     02440000
      ******************************************************************02450000
       9900-EOJ-LOGIC.                                                  02460000
                                                                        02470000
           CLOSE EVENT-EXTR-FILE                                        02480000
                 EXCL-FILE                                              02490000
                 NON-EXCLUDED-FILE.                                     02500000
                                                                        02510000
           DISPLAY ' '.                                                 02520000
           DISPLAY 'RECORDS READ       ' PA-RECORDS-READ.               02540000
           DISPLAY 'RECORDS DISCARDED  ' PA-RECORDS-DISCARDED.          02550000
           DISPLAY 'RECORDS WRITTEN    ' PA-RECORDS-WRITTEN.            02560000
           DISPLAY '**------ END ' PC-PROGRAM-NAME ' ------**'.         02570000
                                                                        02580000
      ******************************************************************02590000
      *    END OF SOURCE CODE FOR PXKUT099                             *02600000
      ******************************************************************02610000
