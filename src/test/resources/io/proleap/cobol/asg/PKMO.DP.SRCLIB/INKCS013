      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    INKCS013.                                                 
       AUTHOR.        SCOTT JACKSON.                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  10/2003.                                                  
       DATE-COMPILED.                                                           
                                                                                
      *---------------------------------------------------------------*         
      *    I013 - PI PHYSICAL INVENTORY ADJUSTMENT LIST               *         
      *                                                               *         
      *    THIS PROGRAM GENERATES A LIST OF THE UPCS FOR THE REQUESTED*         
      *    SKU/STORE/AREA. QUANTITY, PRICE, AND EXTENDED PRICE ARE THE*         
      *    FIELDS THAT ARE DISPLAYED.  THE USER CAN SELECT ONE OF THE *         
      *    UPCS FOR PROCESSING IN INKCS012.                           *         
      *                                                               *         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
W26600* W26600  06-29-2004 ROD JANSSEN INV STORE EXPANSION                      
S26600* S26600  10-29-2004 SCOTT JACKSON (INCL ADJUST MESSAGE WHEN APPR)        
S27710* S27710  01-24-2005 SCOTT JACKSON CHECK FOR  -811 IN PARA 4350-          
W29908* WR29908 01-23-2006 LEE YANG ADD TADJDTL TO CURSOR FOR TYPE 'SI'         
W28545* WR28545 05-01-2006 B. GRAHAM ADD INV DATE IN JCL FOR INKRP193           
W30692* W30692  02-01-2006 ROD JANSSEN DEFINE THE DEFAULT PRINTER FOR           
W30692*                                THE USER.                                
S28545* S28545  08-14-2006 S.JACKSON  ISSUE WHEN SKU/UPC IS ADJUSTED TO         
S28545*                    MATCH ANOTHER SKU/UPC/AREA/RETAIL EXACTLY            
      *---------------------------------------------------------------*         
263126* CHANGED 10/25/2021    BY JIM HUNTER      WR: CHG0263126                 
263126* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263126* MAKING THE CALL DYNAMIC VS STATIC.                                      
263126*----------------------------------------------------------------*        
1122RM* CHANGED 11/03/2022    BY BOB MCASEY      WR: CHG0277188                 
1122RM* ADDED THE DPKCS930 COPYBOOK. CHANGED THE PARAMETERS FOR CALLING         
1122RM* THE CICS ARCHITECTURE API.                                              
1122RM*---------------------------------------------------------------*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  TD-TRANSIENT-DATA-AREA.                                              
           05  TD-QUEUE-NAME           PIC X(4)     VALUE 'IRTD'.               
           05  TD-QUEUE-NAME-LENGTH    PIC S9(4)    VALUE +4                    
                                       COMP SYNC.                               
           05  TD-JCL-LINE             PIC X(80)    VALUE SPACES.               
           05  TD-JCL-LINE-LENGTH      PIC S9(4)    VALUE +80                   
                                       COMP SYNC.                               
                                                                                
       01  JL-JCL-WORK-AREA.                                                    
           05  JL-JCL-INK950-LINE-1.                                            
               10  FILLER                     PIC X(02)    VALUE '//'.          
               10  JL-JCL-INK950-JOBNAME      PIC X(08)    VALUE SPACES.        
               10  FILLER REDEFINES JL-JCL-INK950-JOBNAME.                      
                   15  FILLER                 PIC X(07).                        
                   15  JL-JCL-INK950-JOB-UNQ  PIC X(01).                        
               10  FILLER                     PIC X(11)    VALUE                
                   ' JOB KOHLS,'.                                               
               10  FILLER                     PIC X(01)    VALUE QUOTE.         
               10  FILLER                     PIC X(18)    VALUE                
                   'INV ADJUST DTL RPT'.                                        
               10  FILLER                     PIC X(01)    VALUE QUOTE.         
               10  FILLER                     PIC X(01)    VALUE ','.           
           05  JL-JCL-INK950-LINE-2.                                            
               10  FILLER                     PIC X(21)    VALUE                
                   '//             CLASS='.                                     
               10  JL-JCL-INK950-JOBCLASS     PIC X(01)    VALUE 'Q'.           
               10  FILLER                     PIC X(10)    VALUE                
                   ',MSGCLASS='.                                                
               10  JL-JCL-INK950-MSGCLASS     PIC X(01)    VALUE 'P'.           
               10  JL-JCL-INK950-NOTIFY-OPT   PIC X(16)    VALUE SPACES.        
           05  JL-JCL-INK950-LINE-3.                                            
               10  FILLER                     PIC X(13)    VALUE                
                   '/*JOBPARM K=0'.                                             
           05  JL-JCL-INK950-CMT-SHORT.                                         
               10  FILLER                     PIC X(03)    VALUE '//*'.         
           05  JL-JCL-INK950-CMT-LONG.                                          
               10  FILLER                     PIC X(40)    VALUE                
                   '//**************************************'.                  
               10  FILLER                     PIC X(32)    VALUE                
                   '********************************'.                          
           05  JL-JCL-INK950-CMT-USER-INFO.                                     
               10  FILLER                     PIC X(26)    VALUE                
                   '//* INK950 - SUBMITTED BY '.                                
               10  JL-JCL-INK950-CMT-USRID    PIC X(08)    VALUE SPACES.        
               10  FILLER                     PIC X(06)    VALUE                
                   ' FROM '.                                                    
               10  JL-JCL-INK950-CMT-REGION   PIC X(08)    VALUE SPACES.        
               10  FILLER                     PIC X(04)    VALUE                
                   ' ON '.                                                      
               10  JL-JCL-INK950-CMT-SUBDATE  PIC X(10)    VALUE SPACES.        
               10  FILLER                     PIC X(02)    VALUE SPACES.        
               10  JL-JCL-INK950-CMT-SUBTIME  PIC X(08)    VALUE SPACES.        
           05  JL-JCL-INK950-CMT-1.                                             
               10  FILLER                     PIC X(40)    VALUE                
                   '//* THIS JOB PRODUCES A DETAILED REPORT '.                  
               10  FILLER                     PIC X(32)    VALUE                
                   'OF SKU / UPC ADJUSTMENTS        '.                          
           05  JL-JCL-INK950-CMT-2.                                             
               10  FILLER                     PIC X(40)    VALUE                
                   '//* REPORT AND PRINT THEM AT THE USER DE'.                  
               10  FILLER                     PIC X(32)    VALUE                
                   'FAULT PRINT DESTINATION OR      '.                          
           05  JL-JCL-INK950-CMT-3.                                             
               10  FILLER                     PIC X(40)    VALUE                
                   '//* LOCAL, IF THERE IS NO DEFAULT PRINTE'.                  
               10  FILLER                     PIC X(32)    VALUE                
                   'R DEFINED TO THE USER.          '.                          
           05  JL-JCL-INK950-CMT-4.                                             
               10  FILLER                     PIC X(22)    VALUE                
                   '//* OPTION REQUESTED: '.                                    
               10  JL-JCL-INK950-CMT-OPTION   PIC X(35)    VALUE SPACES.        
           05  JL-JCL-INK950-LINE-10.                                           
               10  FILLER                     PIC X(39)    VALUE                
                   '//JOBLIB   DD DSN=PKMO.LOADLIB,DISP=SHR'.                   
           05  JL-JCL-INK950-LINE-11.                                           
               10  FILLER                     PIC X(03)    VALUE                
                   '//*'.                                                       
           05  JL-JCL-INK950-LINE-12.                                           
               10  FILLER                     PIC X(32)    VALUE                
                   '//PS020    EXEC INK9501,PRTDEST='.                          
               10  JL-JCL-INK950-PRTDEST      PIC X(08)    VALUE                
                   'LOCAL'.                                                     
           05  JL-JCL-INK950-LINE-13.                                           
               10  FILLER                     PIC X(20)    VALUE                
                   '//S010.SYSUT1   DD *'.                                      
           05  JL-JCL-INK950-LINE-14.                                           
W28545         10  JL-JCL-INK950-REQ-OPTION   PIC X(56).                        
           05  JL-JCL-INK950-LINE-EOD.                                          
               10  FILLER                     PIC X(02)    VALUE '/*'.          
           05  JL-JCL-INK950-LINE-EOF.                                          
               10  FILLER                     PIC X(05)    VALUE                
                   '/*EOF'.                                                     
           05  JL-JCL-TEST-LINE-10.                                             
               10  FILLER                     PIC X(35)    VALUE                
                   '//PROCS  JCLLIB ORDER=(TKMA.PROCLIB'.                       
W29908         10  FILLER                     PIC X(19)    VALUE                
W29908             ',QKMO.PROCLIB.STAGE'.                                       
W29908         10  FILLER                     PIC X(18)    VALUE                
W29908             ',PKMO.PROCLIB)'.                                            
           05  JL-JCL-TEST-LINE-11A.                                            
               10  FILLER                     PIC X(39)    VALUE                
                   '//JOBLIB   DD DSN=TKMA.LOADLIB,DISP=SHR'.                   
           05  JL-JCL-TEST-LINE-11B.                                            
               10  FILLER                     PIC X(39)    VALUE                
                   '//         DD DSN=QKMO.LOADLIB,DISP=SHR'.                   
           05  JL-JCL-TEST-LINE-11C.                                            
               10  FILLER                     PIC X(39)    VALUE                
                   '//         DD DSN=PKMO.LOADLIB,DISP=SHR'.                   
           05  JL-JCL-TEST-NOTIFY.                                              
               10  FILLER                     PIC X(08)    VALUE                
                   ',NOTIFY='.                                                  
               10  JL-JCL-TEST-USER           PIC X(08)    VALUE SPACES.        
           05  JL-JCL-INK950-CC-INFO.                                           
               10  FILLER                     PIC X(03)    VALUE                
                   'UPC'.                                                       
               10  FILLER                     PIC X(01)    VALUE SPACES.        
W26600         10  JL-JCL-INK950-STORE        PIC X(04)    VALUE SPACES.        
W26600*        10  JL-JCL-INK950-STR-NBR.                                       
W26600*            15  FILLER                 PIC X(01)    VALUE '0'.           
W26600*            15  JL-JCL-INK950-STORE    PIC X(03)    VALUE SPACES.        
               10  FILLER                     PIC X(01)    VALUE SPACES.        
               10  JL-JCL-INK950-AREA         PIC X(07)    VALUE SPACES.        
               10  FILLER                     PIC X(01)    VALUE SPACES.        
               10  JL-JCL-INK950-SKU          PIC X(08)    VALUE SPACES.        
               10  FILLER                     PIC X(01)    VALUE SPACES.        
               10  JL-JCL-INK950-RETAIL       PIC X(09)    VALUE SPACES.        
                                                                                
W26600         10  FILLER                     PIC X(01)    VALUE SPACES.        
W26600         10  JL-JCL-INK950-USERID       PIC X(07)    VALUE SPACES.        
W26600         10  FILLER                     PIC X(01)    VALUE SPACES.        
W26600         10  JL-JCL-INK950-PRT-IND      PIC X(01)    VALUE SPACES.        
W28545         10  FILLER                     PIC X(01)    VALUE SPACES.        
W28545         10  JL-JCL-DB2-ACTL-INV-DTE    PIC X(10)    VALUE SPACES.        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
                                                          'IN013A  '.           
           05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
                                                          'INKM013 '.           
           05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
                                                          'INKCS013'.           
      *        INKCS011 (SELECTED SKUS)                                         
           05  PC-SEL-SKU-QUEUE-SEQ           PIC  9(01)  VALUE 2.              
      *        INKCS013 (SELECTED UPCS)                                         
           05  PC-SEL-UPC-QUEUE-SEQ           PIC  9(01)  VALUE 3.              
      *        INKCS013 (SCREEN LIST)                                           
           05  PC-LIST-SEL-QUEUE              PIC  9(01)  VALUE 4.              
                                                                                
           05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +13             
                                                          COMP SYNC.            
           05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
                                                          COMP SYNC.            
           05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
                                                          +1000000              
                                                          COMP-3.               
           05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
                                                          COMP SYNC.            
           05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
                                                          COMP-3.               
           05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.          
           05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.          
           05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.          
           05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.          
           05  PC-Y                           PIC  X(01)  VALUE 'Y'.            
           05  PC-CORPORATE                   PIC  X(02)  VALUE 'HQ'.           
           05  PC-COMPLETE                    PIC  X(08)  VALUE                 
                                                         'COMPLETE'.            
           05  PC-ADJ-MESSAGE.                                                  
               10  PC-ADJ-MSG1                PIC  X(07)  VALUE                 
W26600*                                                  'TADJDTL'.             
W26600                                                   'ADJUST '.             
               10  PC-ADJ-MSG2                PIC  X(07)  VALUE                 
                                                         'TADJADD'.             
               10  PC-NO-ADJ-MSG              PIC  X(07)  VALUE                 
W26600*                                                  'TSUMINV'.             
W26600                                                    SPACES.               
           05  PC-TOTAL                       PIC  X(03)  VALUE 'TTL'.          
           05  PC-TSYMSG-NUMBERS.                                               
               10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
               10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
               10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
               10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
               10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
               10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
               10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
               10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
               10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
               10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
               10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
               10  PC-TSYMSG-00098            PIC  9(05)  VALUE 00098.          
               10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
               10  PC-TSYMSG-00148            PIC  9(05)  VALUE 00148.          
               10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
               10  PC-TSYMSG-00551            PIC  9(05)  VALUE 00551.          
W30692         10  PC-TSYMSG-01714            PIC  9(05)  VALUE 01714.          
               10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.          
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
               88  PS-NO-ERROR                            VALUE 'Y'.            
               88  PS-ERROR                               VALUE 'N'.            
           05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
               88  PS-COMMAREA-VALID                      VALUE 'Y'.            
               88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
           05  PS-DONE-SW                     PIC  X(01)  VALUE 'N'.            
               88  PS-DONE                                VALUE 'Y'.            
               88  PS-NOT-DONE                            VALUE 'N'.            
           05  PS-STORE-REC-SW                PIC  X(01)  VALUE 'N'.            
               88  PS-STORE-REC                           VALUE 'Y'.            
               88  PS-NO-STORE-REC                        VALUE 'N'.            
           05  PS-INTERNAL-READER-ENQ-DEQ-IND PIC X(1)    VALUE 'D'.            
               88  PS-INTERNAL-READER-ENQUEUED            VALUE 'E'.            
               88  PS-INTERNAL-READER-DEQUEUED            VALUE 'D'.            
NEXT       05  PS-USE-SELECTION-SW         PIC    X(1)    VALUE 'N'.            
               88  PS-USE-NEXT-SELECTION                  VALUE 'Y'.            
               88  PS-USE-CURRENT-SELECTION               VALUE 'N'.            
S26600     05  PS-ADJUSTMENTS-SW           PIC    X(1)    VALUE 'N'.            
               88  PS-ADJUSTMENTS-EXIST                   VALUE 'Y'.            
               88  PS-NO-ADJUSTMENTS                      VALUE 'N'.            
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
S26600     05  PV-LOC-NBR           PIC X(04)  VALUE SPACES.            INKCS006
S26600     05  PV-LOC-NBR-NUM REDEFINES                                 INKCS006
S26600         PV-LOC-NBR           PIC 9(04).                          INKCS006
           05  PV-CICS-RESPONSE              PIC S9(04)  VALUE +0               
                                                          COMP SYNC.            
           05  PV-SUB                         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-ITEM                        PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-INV-QTY-TTL                 PIC S9(08)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-PR-EXT-AMT-TTL              PIC S9(12)V99 VALUE +0            
                                                          COMP-3.               
           05  PV-START-ITEM-REQUEST          PIC  X(05).                       
           05  PV-ROW-TYPE                    PIC  X(03).                       
               88  PV-TSUMINV-ROW                         VALUE 'SUM'.          
               88  PV-TADJADD                             VALUE 'ADJ'.          
               88  PV-TADJDTL                             VALUE 'DTL'.          
W26600     05  PV-NULL                        PIC S9(04)  VALUE +0         CL*34
W26600                                                    COMP SYNC.       CL*34
W26600     05  PV-COUNT                       PIC S9(04)  VALUE +0         CL*34
W26600                                                    COMP SYNC.       CL*34
           05  PV-TADJDTL-ROW.                                                  
               10  PV-SKU                     PIC 9(08)   VALUE ZEROES.         
               10  PV-TADJDTL-PR-AMT          PIC S9(07)V99  COMP-3             
                                                          VALUE +0.             
           05  PV-HOLD-DEPT                   PIC 9(03)   VALUE  0.             
           05  PV-HOLD-MCL                    PIC 9(02)   VALUE  0.             
           05  PV-PRINT-SKU                   PIC 9(08).                        
           05  PV-PRINT-SKU-X REDEFINES                                         
               PV-PRINT-SKU                   PIC X(08).                        
           05  PV-PRINT-UNIT-RETAIL           PIC S9(7)V99.                     
           05  PV-PRINT-RETAIL                PIC 9(7)V99.                      
           05  PV-PRINT-RETAIL-X REDEFINES                                      
               PV-PRINT-RETAIL                PIC X(09).                        
           05  PV-HOLD-UPC                    PIC S9(15)V COMP-3.               
           05  PV-UPC-NUMBER                  PIC S9(15).                       
           05  PV-UPC                         PIC  9(15).                       
    **** SKU PASSED FROM LIST SCREEN *****                                      
           05  KEY-SKU-NBR                    PIC S9(8)V COMP-3.                
           05  PV-HOLD-TEMP-MCL.                                                
               10 FILLER                      PIC 9(02)   VALUE ZEROES.         
               10 PV-HOLD-MCL-HLQ             PIC 9(01)   VALUE ZEROES.         
               10 FILLER                      PIC 9(01)   VALUE ZEROES.         
           05  PV-MAJOR-CLASS                 PIC 9(04)   VALUE ZEROES.         
                                                                                
           05  PV-CICS-ID.                                                      
               10  PV-FIRST-4              PIC X(4)       VALUE SPACES.         
               10  PV-CICS-REGION-TYPE     PIC X          VALUE SPACE.          
                   88  PV-CICS-REGION-PRODUCTION          VALUE 'P'.            
                   88  PV-CICS-REGION-ACCEPTANCE          VALUE 'A'.            
                   88  PV-CICS-REGION-QUALITY             VALUE 'Q'.            
                   88  PV-CICS-REGION-LEARNING            VALUE 'L'.            
                   88  PV-CICS-REGION-TEST                VALUE 'T'.            
                   88  PV-CICS-REGION-SYSTEST             VALUE 'S'.            
               10  PV-CICS-REGION-CODE     PIC X          VALUE SPACES.         
               10  PV-LAST-2               PIC X(2)       VALUE SPACES.         
W30692                                                                          
W30692     05  PV-EDIT-LOC-NBR           PIC 9(04)  VALUE ZEROS.        INKCS011
W30692     05  PV-EDIT-LOC-NBR-RED REDEFINES                            INKCS011
W30692         PV-EDIT-LOC-NBR.                                         INKCS011
W30692         10  PV-EDIT-LOC-NBR-P1    PIC 9(01).                     INKCS011
W30692         10  PV-EDIT-LOC-NBR-P2    PIC 9(03).                     INKCS011
W30692     05  PV-USER-USER-CODE         PIC X(08)  VALUE SPACES.       INKCS011
W30692                                                                          
      *----------------------------------------------------------------*        
      *   THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.    *        
      *   THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE            *        
      *   APPLICATION-SPECIFIC COMMAREA.                               *        
      *----------------------------------------------------------------*        
       01  LQW-LIST-QUEUE-WORK-AREA.                                            
           05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
           05  LQW-ITEM-ENTRIES               OCCURS 13 TIMES                   
                                              INDEXED BY LQW-IDX.               
               10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
               10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
               10  LQW-SELECTED-SW            PIC  X(01).                       
                   88  LQW-SELECT                         VALUE 'S'.            
                   88  LQW-DESELECT                       VALUE ' '.            
               10  LQW-ACTION                 PIC  X(03).                       
               10  LQW-KEY.                                                     
                   15  LQW-UPC                PIC  9(15)  VALUE ZEROES.         
               10  LQW-INV-QTY                PIC S9(06)        COMP.           
               10  LQW-PR-AMT                 PIC S9(05)V99     COMP-3.         
               10  LQW-PR-EXT-AMT             PIC S9(09)V99     COMP-3.         
               10  LQW-COLOR                  PIC X.                            
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *  RECORD LAYOUT FOR THE MESSAGE SELECTION TEMP STORAGE QUEUE.  *         
      *---------------------------------------------------------------*         
           COPY INWS012.                                                        
      *---------------------------------------------------------------*         
      *  RECORD LAYOUT FOR THE MESSAGE SELECTION TEMP STORAGE QUEUE.  *         
      *---------------------------------------------------------------*         
           COPY INWS027.                                                        
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    MAP LAYOUT                                                 *         
      *---------------------------------------------------------------*         
                                                                                
W26600     COPY INKM013.                                                        
      *                                                                         
       01  MR-OCCURS-LAYOUT                   REDEFINES  IN013AO.               
W26600     05  FILLER                         PIC X(106).                       
           05  MR-SCROLL-AREA                 OCCURS 13 TIMES.                  
               10  MR-SELECTL                 PIC S9(04) COMP.                  
               10  MR-SELECTA                 PIC  X(01).                       
               10  MR-SELECTC                 PIC  X(01).                       
               10  MR-SELECTH                 PIC  X(01).                       
               10  MR-SELECTI                 PIC  X(01).                       
                                                                                
               10  MR-ACTIONL                 PIC S9(04) COMP.                  
               10  MR-ACTIONA                 PIC  X(01).                       
               10  MR-ACTIONC                 PIC  X(01).                       
               10  MR-ACTIONH                 PIC  X(01).                       
               10  MR-ACTION                  PIC  X(03).                       
                                                                                
               10  MR-UPCL                    PIC S9(04) COMP.                  
               10  MR-UPCA                    PIC  X(01).                       
               10  MR-UPCC                    PIC  X(01).                       
               10  MR-UPCH                    PIC  X(01).                       
      ****     10  MR-UPC                     PIC  X(15).                       
               10  MR-UPC-X.                                                    
                   15  MR-UPC                 PIC  ZZZZZZZZZZZZZZ9.             
                                                                                
               10  MR-INV-QTYL                PIC S9(04) COMP.                  
               10  MR-INV-QTYA                PIC  X(01).                       
               10  MR-INV-QTYC                PIC  X(01).                       
               10  MR-INV-QTYH                PIC  X(01).                       
               10  MR-INV-QTY-X.                                                
                   15  MR-INV-QTY             PIC  -(5)9.                       
                                                                                
               10  MR-PR-AMTL                 PIC S9(04) COMP.                  
               10  MR-PR-AMTA                 PIC  X(01).                       
               10  MR-PR-AMTC                 PIC  X(01).                       
               10  MR-PR-AMTH                 PIC  X(01).                       
               10  MR-PR-AMT-X.                                                 
                   15  MR-PR-AMT              PIC  Z(7).99.                     
                                                                                
               10  MR-PR-EXT-AMTL             PIC S9(04) COMP.                  
               10  MR-PR-EXT-AMTA             PIC  X(01).                       
               10  MR-PR-EXT-AMTC             PIC  X(01).                       
               10  MR-PR-EXT-AMTH             PIC  X(01).                       
               10  MR-PR-EXT-AMT-X.                                             
                   15  MR-PR-EXT-AMT          PIC  -(09).99.                    
                                                                                
               10  MR-ADJ-MSGL                PIC S9(04) COMP.                  
               10  MR-ADJ-MSGA                PIC  X(01).                       
               10  MR-ADJ-MSGC                PIC  X(01).                       
               10  MR-ADJ-MSGH                PIC  X(01).                       
               10  MR-ADJ-MSG                 PIC  X(10).                       
                                                                                
      /                                                                         
      *---------------------------------------------------------------*         
      *    ATTRIBUTE SETTINGS COPYBOOK.                               *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS015.                                                        
                                                                                
W30692     COPY DPWS026.                                                        
W30692                                                                  INKCS055
      *---------------------------------------------------------------*         
      *    FUNCTION KEYS COPYBOOK.                                    *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS016.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    NUMERIC EDIT LAYOUT.                                       *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS010I.                                                       
      /                                                                         
      *---------------------------------------------------------------*         
      *    PARAMETERS FOR CALLING CICS ARCHITECTURE API (DPKCS030).   *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS030.                                                        
1122RM     COPY DPWS930.                                                        
      /                                                                         
      *---------------------------------------------------------------*         
      *    STANDARD COMMAREA.                                         *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
               10  INTER-APPL-COMM-AREA       PIC X(200).                       
W26600     COPY INWS001.                                                        
      *---------------------------------------------------------------*         
      *  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
      *---------------------------------------------------------------*         
               10  ASC-SPECIFIC-COMMAREA.                                       
                   15  ASC-INT-APPL-FORMAT-IND             PIC  9(01).          
                   15  ASC-LIST-KEYS.                                           
W26600                 20  ASC-KEY-STORE-NBR               PIC  X(04).          
                       20  ASC-KEY-AREA-NBR                PIC S9(08).          
                   15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
                       20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
                           88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
                           88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
                       20  ASC-ITEM-ARRAY.                                      
                           25  ASC-ITEM-ENTRIES OCCURS 13 TIMES.                
                               30  ASC-LIST-ITEM-NUMBER    PIC S9(09)           
                                                           COMP-3.              
                               30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
                                   88  ASC-TEMP-NO-SELECT  VALUE ' '.           
                                   88  ASC-TEMP-SELECT     VALUE 'S'.           
                                   88  ASC-TEMP-BEGIN      VALUE 'B'.           
                                   88  ASC-TEMP-END        VALUE 'E'.           
                               30  ASC-SELECTED-SW         PIC  X(01).          
                                   88  ASC-NO-SELECT       VALUE ' '.           
                                   88  ASC-SELECT          VALUE 'S'.           
                                   88  ASC-BEGIN           VALUE 'B'.           
                                   88  ASC-END             VALUE 'E'.           
                               30  ASC-ACTION              PIC  X(03).          
                               30  ASC-UPC                 PIC  9(15).          
                               30  ASC-UPC-X REDEFINES                          
                                   ASC-UPC                 PIC  X(15).          
                               30  ASC-INV-QTY             PIC S9(06)           
                                                                COMP.           
                               30  ASC-PR-AMT              PIC S9(05)V99        
                                                                COMP-3.         
                               30  ASC-PR-EXT-AMT          PIC S9(09)V99        
                                                                COMP-3.         
                               30  ASC-COLOR               PIC X.               
                   15  ASC-KEY-DATA.                                            
                       20  ASC-KEY-SKU-NBR     PIC  9(08).                      
                       20  ASC-KEY-PR-AMT      PIC S9(07)V99    COMP-3.         
                                                                                
                   15  ASC-BLK-SUB                         PIC S9(04)           
                                                                COMP            
                                                                SYNC.           
                   15  ASC-ITEM-SUB                        PIC S9(04)           
                                                                COMP            
                                                                SYNC.           
                   15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-NUMBER-OF-TS-ITEMS              PIC S9(05)           
                                                                COMP-3.         
                   15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
                                                                COMP-3.         
                   15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
                                                                COMP            
                                                                SYNC.           
                   15  ASC-START-OF-SELECTED-RANGE          PIC S9(09)          
                                                                COMP-3.         
                   15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
                       88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
                       88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
                   15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
                       88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
                       88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
      * ASC-LIST-QUEUE (I013 SCREEN UPC LISTING - TEMPORARY STORAGE)            
                   15  ASC-LIST-QUEUE-NAME.                                     
                       20  ASC-LIST-TERM-ID                PIC  X(04).          
                       20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-LIST-SEQ                    PIC  9(01).          
      * ASC-FROM-QUEUE (I011 SELECTED SKUS - PASSED FROM INKCS011)              
                   15  ASC-FROM-QUEUE-NAME.                                     
                       20  ASC-FROM-TERM-ID                PIC  X(04).          
                       20  ASC-FROM-TASK-NUMBER            PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-FROM-SEQ                    PIC  9(01).          
      * ASC-SEL-QUEUE  (I013 SELECTED UPCS - PASSED TO INKCS012)                
                   15  ASC-SEL-QUEUE-NAME.                                      
                       20  ASC-SEL-TERM-ID                 PIC  X(04).          
                       20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-SEL-SEQ                     PIC  9(01).          
                                                                                
                   15  ASC-CURSOR-SW                       PIC  X(01).          
                       88  ASC-NO-CURSOR                   VALUE 'Z'.           
                       88  ASC-SYSTEM-ID-CURSOR            VALUE 'S'.           
                       88  ASC-MESSAGE-NUMBER-CURSOR       VALUE 'N'.           
                       88  ASC-MESSAGE-TEXT-CURSOR         VALUE 'T'.           
                   15  ASC-USE-SELECTION-SW                PIC  X(01).          
                       88  ASC-USE-SELECTION-QUEUE         VALUE 'S'.           
                       88  ASC-NO-SELECTION-QUEUE          VALUE 'N'.           
                   15  ASC-START-ITEM-REQUEST.                                  
                       20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
                   15  ASC-HOLD-INTER-APPL                 PIC X(200).          
                   15  ASC-SEL-SUB                         PIC S9(04)           
                                                                  COMP.         
                   15  ASC-SEL-ITEM                        PIC S9(04)           
                                                                  COMP.         
                10 FILLER                                  PIC X(1242).         
      /                                                                         
      *---------------------------------------------------------------*         
      *    ABEND PROCESSING COPYBOOK.                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS013.                                                        
                                                                                
      /                                                                         
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR SUMMARIZED INVENTORY DATA TABLE               *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TSUMINV                                                 
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR ADJUSTMENT ADD TABLE                          *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TADJADD                                                 
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR ADJUSTMENT DETAIL TABLE                       *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TADJDTL                                                 
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR SKU CROSS-REFERENCE TABLE                     *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TSKXREF                                                 
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR UPC TABLE                                     *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TUPC                                                    
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR USER PRINTER DEFAULT                          *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TUSER                                                   
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR COMMUNICATIONS                                *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TSUMINV UNION WITH TADJADD CURSOR DECLARATION.             *         
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
                DECLARE SUMMARY-CSR CURSOR                                      
                FOR SELECT                                                      
                      SKU_NBR                                                   
                    , UPC_NBR                                                   
                    , INV_UNIT_PR_AMT                                           
                    , INV_QTY                                                   
S28545              , DKEY_NBR                                                  
                    , EXTD_AMT                                                  
                    , 'SUM'                                                     
                FROM  TSUMINV                                                   
W26600          WHERE LOC_NBR       = :SUMINV-LOC-NBR                           
                  AND AREA_NBR        = :SUMINV-AREA-NBR                        
                  AND SKU_NBR         = :SUMINV-SKU-NBR                         
                  AND INV_UNIT_PR_AMT = :SUMINV-INV-UNIT-PR-AMT                 
           UNION                                                                
                    SELECT                                                      
                      SKU_NBR                                                   
                    , UPC_NBR                                                   
                    , ADJ_UNIT_PR_AMT                                           
                    , ADJ_QTY                                                   
S28545              , DKEY_NBR                                                  
                    , ADJ_EXTD_AMT                                              
                    , 'ADD'                                                     
                FROM  TADJADD                                                   
W26600          WHERE LOC_NBR       = :SUMINV-LOC-NBR                           
                  AND AREA_NBR        = :SUMINV-AREA-NBR                        
                  AND SKU_NBR         = :SUMINV-SKU-NBR                         
                  AND ADJ_UNIT_PR_AMT = :SUMINV-INV-UNIT-PR-AMT                 
           ORDER BY 1, 2                                                        
           END-EXEC.                                                            
                                                                                
      /                                                                         
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      **  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
      **  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
      **  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
      ******************************************************************        
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
                                                                                
           MOVE LENGTH OF IN013AI        TO DP030-MAP-LENGTH (1).               
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
           PERFORM 1000-CONTROL-PROCESSING                                      
                                                                                
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
      *----------------------------------------------------------------*        
      *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *        
      *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *        
      *----------------------------------------------------------------*        
                                                                                
       0001-CALL-CICS-ARCH-API.                                                 
                                                                                
1122RM*    CALL 'DPKCS030' USING DFHEIBLK                                       
1122RM     CALL DP930-CICS-ARCH-API                                             
1122RM                     USING DFHEIBLK                                       
                                 DFHCOMMAREA                                    
                                 DP030-CICS-API-FIELDS                          
                                 DP020-STANDARD-COMMAREA                        
                                 IN013AI.                                       
                                                                                
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK                                            
                   DP013-XCTL-DISPLAY-RESTART                                   
                   DP013-CICS-ABEND      TO TRUE                                
               MOVE '0001-CALL-CICS-ARCH-API'                                   
                                         TO DP013-PARAGRAPH                     
1122RM*        MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEX        
1122RM*             'T LINE'             TO DP013-MESSAGE-TEXT (2)              
1122RM         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
1122RM-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
      * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
      *                                                                *        
      * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
      *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
      *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
      *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
      *----------------------------------------------------------------*        
                                                                                
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                    INITIALIZE ASC-SPECIFIC-COMMAREA                            
                    MOVE EIBTRMID              TO  ASC-LIST-TERM-ID             
                                                   ASC-SEL-TERM-ID              
                                                   ASC-FROM-TERM-ID             
                    MOVE DP020-SRC-TASK-NUMBER TO  ASC-LIST-TASK-NUMBER         
                                                   ASC-SEL-TASK-NUMBER          
                                                   ASC-FROM-TASK-NUMBER         
                    MOVE PC-LIST-SEL-QUEUE     TO  ASC-LIST-SEQ                 
                    MOVE PC-SEL-UPC-QUEUE-SEQ  TO  ASC-SEL-SEQ                  
                    MOVE PC-SEL-SKU-QUEUE-SEQ  TO  ASC-FROM-SEQ                 
                                                                                
                    PERFORM 1100-FIND-OBJECT-KEYS                               
                    IF  PS-ERROR                                                
                        CONTINUE                                                
                    ELSE                                                        
                        MOVE INTER-APPL-COMM-AREA TO ASC-HOLD-INTER-APPL        
                        MOVE -1                   TO ASEL01L                    
                        SET DP030-SET-CURSOR-APPL-1   TO TRUE                   
                                                                                
                        PERFORM 4000-BUILD-INITIAL-PANEL                        
                        PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    
                                                                                
***********             PERFORM 3200-RESET-ATTRIBUTES                           
                    END-IF                                                      
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                    MOVE ASC-ITEM-AT-TOP-OF-PANEL                               
                                         TO PV-TOP-ITEM                         
                    PERFORM 2000-PROCESS-PANEL                                  
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                    MOVE ASC-ITEM-AT-TOP-OF-PANEL                               
                                         TO PV-TOP-ITEM                         
                    MOVE ZERO            TO ASC-ITEM-AT-TOP-OF-PANEL            
                    MOVE ASC-HOLD-INTER-APPL                                    
                                         TO INTER-APPL-COMM-AREA                
                    PERFORM 6000-REFRESH-PANEL-FROM-TEMP                        
                    PERFORM 3000-EDIT-DATA-IN-COMMAREA                          
                    PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                        
                                                                                
               WHEN OTHER                                                       
                    SET DP013-LOGIC-ABEND TO TRUE                               
                    SET DP013-NO-ROLLBACK TO TRUE                               
                    MOVE '1000-CONTROL-PROCESSING'                              
                                         TO DP013-PARAGRAPH                     
                    MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'           
                                         TO DP013-MESSAGE-TEXT(1)               
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
      /                                                                         
      *----------------------------------------------------------------*        
      *  DETERMINE THE KEY OF THE OBJECT TO BE UPDATED BASED ON THE    *        
      *  INTER-APPLICATION FORMAT CODE.                                *        
      *----------------------------------------------------------------*        
                                                                                
       1100-FIND-OBJECT-KEYS.                                                   
                                                                                
           MOVE DP020-INT-APPL-FORMAT-IND  TO  ASC-INT-APPL-FORMAT-IND.         
                                                                                
           IF (IN001-STORE-NBR NUMERIC                                          
           AND IN001-AREA-NBR NUMERIC)                                          
               MOVE IN001-STORE-NBR        TO  ASC-KEY-STORE-NBR                
               MOVE IN001-AREA-NBR         TO  ASC-KEY-AREA-NBR                 
               MOVE IN001-NUMBER-OF-SELECTED-ITEMS                              
                                         TO ASC-NUMBER-OF-SELECTED-ITEMS        
               MOVE IN001-NUMBER-OF-TS-ITEMS                                    
                                           TO  ASC-NUMBER-OF-TS-ITEMS           
    *****      CONTAINS LAST SCREEN INFO                                        
               MOVE IN001-SEL-QUEUE-NAME   TO  ASC-FROM-QUEUE-NAME              
    *****                                  TO  ASC-SEL-QUEUE-NAME               
W28545         MOVE IN001-DB2-ACTL-INV-DTE TO                                   
W28545                                     JL-JCL-DB2-ACTL-INV-DTE              
               SET ASC-USE-SELECTION-QUEUE TO  TRUE                             
               PERFORM 1120-PREPARE-LIST-ENTRY                                  
                                                                                
           ELSE                                                                 
               MOVE PC-TSYMSG-00148 TO  DP020-MSG-NUMBER                        
                    SET DP020-MSG-FATAL                                         
                        DP020-NEXT-ACT-APPL-ERROR TO TRUE                       
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * ENTRY IS FROM A LIST.  GET THE MESSAGE NUMBER FROM THE TEMP    *        
      * STORAGE QUEUE PASSED FROM THE LIST.                            *        
      *----------------------------------------------------------------*        
                                                                                
       1120-PREPARE-LIST-ENTRY.                                                 
                                                                                
           MOVE IN027-MAX-ITEMS          TO ASC-SEL-SUB.                        
           MOVE ZERO                     TO ASC-SEL-ITEM.                       
           PERFORM 2105-GET-NEXT-LINE.                                          
      *----------------------------------------------------------------*        
      * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
      * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
      * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
      * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
      * FUNCTION KEYS CAN GET THIS FAR.                                *        
      *----------------------------------------------------------------*        
                                                                                
       2000-PROCESS-PANEL.                                                      
                                                                                
           IF  IN001-STORE-NBR NOT NUMERIC                                      
               MOVE ASC-HOLD-INTER-APPL TO INTER-APPL-COMM-AREA                 
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                   MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
                   PERFORM 2050-DELETE-LIST-QUEUE                               
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                                                                                
               WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                     
                    IF  ASC-USE-SELECTION-QUEUE                                 
                        SET PS-USE-NEXT-SELECTION TO TRUE                       
                        PERFORM 2105-GET-NEXT-LINE                              
                        PERFORM 4000-BUILD-INITIAL-PANEL                        
      *                 PERFORM 3200-RESET-ATTRIBUTES                           
                        PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    
                    ELSE                                                        
      *                 --- BOTTOM OF LIST ---                                  
                        MOVE PC-TSYMSG-00070                                    
                                         TO DP020-MSG-NUMBER                    
                        SET DP020-MSG-INFORMATIONAL                             
                                         TO TRUE                                
                    END-IF                                                      
                                                                                
               WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)                     
                    IF  ASC-USE-SELECTION-QUEUE                                 
                        SET PS-USE-NEXT-SELECTION TO TRUE                       
                        PERFORM 2107-GET-PREV-LINE                              
                        PERFORM 4000-BUILD-INITIAL-PANEL                        
                        PERFORM 3200-RESET-ATTRIBUTES                           
PREV                    PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    
                    ELSE                                                        
      *                 --- TOP OF LIST ---                                     
                        MOVE PC-TSYMSG-00069                                    
                                         TO DP020-MSG-NUMBER                    
                        SET DP020-MSG-INFORMATIONAL                             
                                         TO TRUE                                
                    END-IF                                                      
                                                                                
                                                                                
               WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
                   PERFORM 2150-SELECT-ALL-ITEMS                                
                   PERFORM 3200-RESET-ATTRIBUTES                                
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN OTHER                                                       
                   PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   IF  PS-NO-ERROR                                              
                       SET DP030-CONTINUE-GO                                    
                                       TO TRUE                                  
                       PERFORM 2100-CHECK-FUNCTION-KEY                          
                   ELSE                                                         
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
                       SET DP030-OVERRIDE-GO                                    
                                       TO TRUE                                  
                   END-IF                                                       
           END-EVALUATE.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
      *----------------------------------------------------------------*        
       2050-DELETE-LIST-QUEUE.                                                  
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF (PV-CICS-RESP = DFHRESP(NORMAL)                                   
           OR  PV-CICS-RESP = DFHRESP(QIDERR))                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
                                                                                
               MOVE '2050-DELETE-LIST-QUEUE'                                    
                                         TO DP013-PARAGRAPH                     
               MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
      /                                                                         
      *----------------------------------------------------------------*        
      * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
      * NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
      * CICS ARCHITECTURE API.                                         *        
      *----------------------------------------------------------------*        
                                                                                
       2100-CHECK-FUNCTION-KEY.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
                   IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
                       MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
                                      TO IN012-SELECTED-ITEMS                   
                                         IN001-NUMBER-OF-SELECTED-ITEMS         
                       PERFORM 7000-PREPARE-SEL-QUEUE                           
                   ELSE                                                         
                       SET DP030-OVERRIDE-GO                                    
                                         TO TRUE                                
                       MOVE PC-TSYMSG-00057                                     
                                         TO DP020-MSG-NUMBER                    
                       SET DP020-MSG-WARNING                                    
                                         TO TRUE                                
                   END-IF                                                       
                                                                                
00640          WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                  INKCS011
00641              MOVE +1               TO PV-TOP-ITEM                 INKCS011
00642              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS011
00643                                                                   INKCS011
00644          WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                   INKCS011
00645              PERFORM 2110-BROWSE-BACKWARD                         INKCS011
00646              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS011
00647                                                                   INKCS011
00648          WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                   INKCS011
00649              PERFORM 2120-BROWSE-FORWARD                          INKCS011
00650              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS011
00651                                                                   INKCS011
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                   PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-06 (DP020-SRC-AID)                      
W30692             PERFORM DP026-0000-GET-LOGON-INFO                            
W30692             IF DP026-CICS-RESPONSE = DFHRESP(NORMAL)                     
W30692                 IF DP026-DFLT-PRTR-ID EQUAL SPACES                       
W30692                   IF DP026-LOCATION-TYPE NOT EQUAL 'HQ'                  
W30692                     PERFORM 8XXX-USE-STORE-DEFAULT-PRTR                  
W30692                     IF JL-JCL-INK950-PRTDEST EQUAL SPACES                
W30692                         SET DP030-OVERRIDE-GO                            
W30692                             DP020-MSG-FATAL TO TRUE                      
W30692                         MOVE PC-TSYMSG-01714                             
W30692                                           TO DP020-MSG-NUMBER            
W30692                     ELSE                                                 
W30692                         PERFORM 8000-FORMAT-AND-SUBMIT                   
W30692                         PERFORM 2130-CHECK-FOR-JUMP-BROWSE       INKCS046
W30692                         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN     INKCS046
W30692                         MOVE PC-TSYMSG-00098                             
W30692                                              TO DP020-MSG-NUMBER         
W30692                         SET DP020-MSG-INFORMATIONAL TO TRUE              
W30692                     END-IF                                               
W30692                   ELSE                                                   
W30692                       MOVE 'LOCAL'  TO JL-JCL-INK950-PRTDEST             
W30692                       MOVE 'Y'      TO JL-JCL-INK950-PRT-IND             
W30692                       PERFORM 8000-FORMAT-AND-SUBMIT                     
W30692                       PERFORM 2130-CHECK-FOR-JUMP-BROWSE         INKCS046
W30692                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN       INKCS046
W30692                       MOVE PC-TSYMSG-00098   TO DP020-MSG-NUMBER         
W30692                       SET DP020-MSG-INFORMATIONAL TO TRUE                
W30692                   END-IF                                                 
W30692                 ELSE                                                     
W30692                     MOVE DP026-DFLT-PRTR-ID                              
W30692                                        TO JL-JCL-INK950-PRTDEST          
W30692                     MOVE 'Y'           TO JL-JCL-INK950-PRT-IND          
W30692                     PERFORM 8000-FORMAT-AND-SUBMIT                       
W30692                     PERFORM 2130-CHECK-FOR-JUMP-BROWSE           INKCS046
W30692                     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN         INKCS046
W30692                     MOVE PC-TSYMSG-00098   TO DP020-MSG-NUMBER           
W30692                     SET DP020-MSG-INFORMATIONAL TO TRUE                  
W30692                 END-IF                                                   
W30692             ELSE                                                         
W30692                 SET DP013-NO-ROLLBACK                            INKCS055
W30692                     DP013-XCTL-DISPLAY-RESTART                   INKCS055
W30692                     DP013-CICS-ABEND  TO TRUE                    INKCS055
W30692                 MOVE '2100-CHECK-FUNCTION-KEY'                   INKCS055
W30692                                       TO DP013-PARAGRAPH         INKCS055
W30692                 MOVE 'DP026-0000-GET-LOGON-INFO ERROR'           INKCS055
W30692                                       TO DP013-MESSAGE-TEXT (1)  INKCS055
W30692                 PERFORM DP013-0000-PROCESS-ABEND                         
W30692             END-IF                                                       
W30692*            PERFORM 8000-FORMAT-AND-SUBMIT                               
W30692*            PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
W30692*            PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
W30692*            MOVE PC-TSYMSG-00098 TO DP020-MSG-NUMBER                     
W30692*            SET DP020-MSG-INFORMATIONAL TO TRUE                          
                                                                                
               WHEN OTHER                                                       
                   SET DP013-NO-ROLLBACK                                        
                       DP013-XCTL-DISPLAY-RESTART                               
                       DP013-CICS-ABEND  TO TRUE                                
                   MOVE '2100-CHECK-FUNCTION-KEY'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
                                         TO DP013-MESSAGE-TEXT (1)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    RETRIEVE THE NEXT KEY FOR PROCESSING.                       *        
      *----------------------------------------------------------------*        
                                                                                
       2105-GET-NEXT-LINE.                                                      
                                                                                
           ADD +1                        TO  ASC-SEL-SUB.                       
           IF  ASC-SEL-SUB > IN027-MAX-ITEMS                                    
               ADD +1                    TO  ASC-SEL-ITEM                       
               MOVE +1                   TO  ASC-SEL-SUB                        
           END-IF.                                                              
                                                                                
           IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                        
               PERFORM 9100-READ-SEL-QUEUE                                      
               IF  IN027-ITEM (ASC-SEL-SUB) > ZERO                              
                   MOVE IN027-SKU (ASC-SEL-SUB)                                 
KEY                                      TO  ASC-KEY-SKU-NBR                    
KEY                MOVE IN027-PRICE (ASC-SEL-SUB)                               
KEY                                      TO  ASC-KEY-PR-AMT                     
                   SET IN027-INQ (ASC-SEL-SUB)                                  
                                         TO  TRUE                               
                   PERFORM 9200-REWRITE-SEL-QUEUE                               
               ELSE                                                             
                   SUBTRACT +1 FROM ASC-SEL-SUB                                 
      *            --- BOTTOM OF LIST ---                                       
                   MOVE PC-TSYMSG-00070  TO  DP020-MSG-NUMBER                   
                   SET DP020-MSG-INFORMATIONAL                                  
                                         TO  TRUE                               
               END-IF                                                           
           ELSE                                                                 
               SUBTRACT +1 FROM ASC-SEL-ITEM                                    
               MOVE IN027-MAX-ITEMS      TO  ASC-SEL-SUB                        
               MOVE PC-TSYMSG-00070      TO  DP020-MSG-NUMBER                   
               SET DP020-MSG-INFORMATIONAL                                      
                                         TO  TRUE                               
           END-IF.                                                              
                                                                                
088800*----------------------------------------------------------------*        
088900*  GET THE MESSAGE NUMBER (KEY) OF THE PREVIOUS SELECTED         *        
089000*  OBJECT FROM THE SELECTED-ITEMS TEMP. STORAGE QUEUE.           *        
089100*----------------------------------------------------------------*        
089200                                                                          
089300 2107-GET-PREV-LINE.                                                      
089400                                                                          
089500     SUBTRACT +1 FROM ASC-SEL-SUB.                                        
089600     IF  ASC-SEL-SUB < +1                                                 
089700         SUBTRACT +1 FROM ASC-SEL-ITEM                                    
089800         MOVE IN027-MAX-ITEMS      TO ASC-SEL-SUB                         
089900     END-IF.                                                              
090000                                                                          
090100     IF  ASC-SEL-ITEM > ZERO                                              
090300         PERFORM 9100-READ-SEL-QUEUE                                      
090400*        MOVE IN027-KEY-DATA (ASC-SEL-SUB)                                
090500*                                  TO ASC-KEY-DATA                        
               MOVE IN027-SKU (ASC-SEL-SUB)                                     
KEY                                      TO  ASC-KEY-SKU-NBR                    
KEY            MOVE IN027-PRICE (ASC-SEL-SUB)                                   
KEY                                      TO  ASC-KEY-PR-AMT                     
               SET IN027-INQ (ASC-SEL-SUB)                                      
                                         TO  TRUE                               
090600     ELSE                                                                 
090700         MOVE +1                   TO ASC-SEL-ITEM                        
090800                                      ASC-SEL-SUB                         
090900*        --- TOP OF LIST ---                                              
091000         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
091100         SET DP020-MSG-INFORMATIONAL                                      
091200                                   TO TRUE                                
091300     END-IF.                                                              
091400/                                                                         
00667 *----------------------------------------------------------------*INKCS011
00668 *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *INKCS011
00669 *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *INKCS011
00670 *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *INKCS011
00671 *    MESSAGE.                                                    *INKCS011
00672 *----------------------------------------------------------------*INKCS011
00673                                                                   INKCS011
00674  2110-BROWSE-BACKWARD.                                            INKCS011
00675                                                                   INKCS011
00676      SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL    INKCS011
00677          GIVING PV-TOP-ITEM.                                      INKCS011
00678                                                                   INKCS011
00679      IF  ((PV-TOP-ITEM < +1)                                      INKCS011
00680        OR (PV-TOP-ITEM = +1))                                     INKCS011
00681          MOVE +1                   TO PV-TOP-ITEM                 INKCS011
00682 *        -- TOP OF LIST --                                        INKCS011
00683          MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER            INKCS011
00684          SET DP020-MSG-INFORMATIONAL TO TRUE                      INKCS011
00685      END-IF.                                                      INKCS011
00686                                                                   INKCS011
00687 /                                                                 INKCS011
00688 *----------------------------------------------------------------*INKCS011
00689 *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *INKCS011
00690 *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *INKCS011
00691 *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *INKCS011
00692 *----------------------------------------------------------------*INKCS011
00693                                                                   INKCS011
00694  2120-BROWSE-FORWARD.                                             INKCS011
00695                                                                   INKCS011
00696      ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL              INKCS011
00697          GIVING PV-TOP-ITEM.                                      INKCS011
00698      IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                   INKCS011
00699          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            INKCS011
00700                                    TO PV-TOP-ITEM                 INKCS011
00701 *        -- BOTTOM OF LIST --                                     INKCS011
00702          MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER            INKCS011
00703          SET DP020-MSG-INFORMATIONAL                              INKCS011
00704                                    TO TRUE                        INKCS011
00705      ELSE                                                         INKCS011
00706          COMPUTE PV-BOTTOM-ITEM =                                 INKCS011
00707              PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                 INKCS011
00708          IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ            INKCS011
00709 *            -- BOTTOM OF LIST --                                 INKCS011
00710              MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER            INKCS011
00711              SET DP020-MSG-INFORMATIONAL                          INKCS011
00712                                    TO TRUE                        INKCS011
00713          END-IF                                                   INKCS011
00714      END-IF.                                                      INKCS011
00715 /                                                                 INKCS011
      *----------------------------------------------------------------*        
      *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
      *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
      *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
      *    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
      *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
      *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
      *----------------------------------------------------------------*        
                                                                                
       2130-CHECK-FOR-JUMP-BROWSE.                                              
                                                                                
           SET PS-NO-ERROR               TO TRUE.                               
           IF  ASC-START-ITEM-REQUEST = SPACE                                   
               MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
                                         TO PV-TOP-ITEM                         
           ELSE                                                                 
               MOVE ASC-START-ITEM-REQUEST                                      
                                         TO DP010I-UNEDITED-FIELD               
                                            PV-START-ITEM-REQUEST               
               MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
               MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
               SET DP010I-NEGATIVE-NOT-ALLOWED                                  
                                         TO TRUE                                
263126         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263126              USING DP010I-NUMERIC-EDIT-AREA                              
                                                                                
               IF  NOT DP010I-ERROR-DETECTED                                    
                   MOVE DP010I-NUMERIC-FIELD                                    
                                         TO  ASC-START-ITEM-REQUEST-N           
                   IF  ASC-START-ITEM-REQUEST-N > ZERO                          
                       IF  ASC-START-ITEM-REQUEST-N >                           
                                                ASC-NUMBER-OF-ITEMS-READ        
                           MOVE SPACE TO ASC-START-ITEM-REQUEST                 
                           COMPUTE PV-TOP-ITEM =                                
                                   ASC-NUMBER-OF-ITEMS-READ                     
                                   - PC-ITEMS-PER-PANEL + 1                     
                           IF  PV-TOP-ITEM < +1                                 
                               MOVE +1   TO PV-TOP-ITEM                         
                               MOVE PC-TSYMSG-00070                             
                                         TO DP020-MSG-NUMBER                    
                               SET DP020-MSG-INFORMATIONAL                      
                                         TO TRUE                                
                           END-IF                                               
                       ELSE                                                     
                           MOVE ASC-START-ITEM-REQUEST-N                        
                                         TO PV-TOP-ITEM                         
                           MOVE SPACE    TO ASC-START-ITEM-REQUEST              
                           COMPUTE PV-BOTTOM-ITEM =                             
                               PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
                           IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
      *                        -- BOTTOM OF LIST --                             
                               MOVE PC-TSYMSG-00070                             
                                         TO DP020-MSG-NUMBER                    
                               SET DP020-MSG-INFORMATIONAL                      
                                         TO TRUE                                
                           END-IF                                               
                       END-IF                                                   
                   ELSE                                                         
      *                --- VALUE OUT OF RANGE ----                              
                       MOVE PC-TSYMSG-00101                                     
                                         TO DP020-MSG-NUMBER                    
                       MOVE -1           TO ASTRTITL                            
                       SET DP030-SET-CURSOR-APPL-1                              
                                         TO TRUE                                
                       MOVE DP015-REVERSE                                       
                                         TO ASTRTITH                            
                       MOVE DP015-RED    TO ASTRTITC                            
                       MOVE DP015-UNP-NUM-BRT-OFF                               
                                         TO ASTRTITA                            
                       SET DP020-MSG-FATAL                                      
                                         TO TRUE                                
                       SET PS-ERROR      TO TRUE                                
                   END-IF                                                       
                                                                                
               ELSE                                                             
                   MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
                   MOVE -1               TO ASTRTITL                            
                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE DP015-REVERSE    TO ASTRTITH                            
                   MOVE DP015-RED        TO ASTRTITC                            
                   MOVE DP015-UNP-NUM-BRT-OFF                                   
                                         TO ASTRTITA                            
                   SET DP020-MSG-FATAL   TO TRUE                                
                   SET PS-ERROR          TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
      /                                                                         
      /                                                                         
      *----------------------------------------------------------------*        
      *  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *        
      *  FOR ALL ITEMS.                                                *        
      *----------------------------------------------------------------*        
                                                                                
       2150-SELECT-ALL-ITEMS.                                                   
                                                                                
           MOVE +1                       TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2151-SET-SELECTED-INDS                                       
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           ADD +1                        TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2151-SET-SELECTED-INDS                                       
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
                                         TO PV-LIST-ITEM-NUMBER.                
                                                                                
           COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
              (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
               MOVE +1                   TO ASC-BLK-SUB                         
                                                                                
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
                                                                                
               MOVE +1                   TO PV-BLOCK-NUMBER                     
                                                                                
               COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY - 1                                  
                                                                                
               PERFORM                                                          
                   UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2151-SET-SELECTED-INDS                               
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-BLOCK-NUMBER                     
                                                                                
               END-PERFORM                                                      
                                                                                
               COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
                                                                                
               PERFORM                                                          
                   UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
                         ASC-HIGHEST-BLOCK-WRITTEN                              
                                                                                
                   MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
                                         TO PV-BLOCK-NUMBER                     
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2151-SET-SELECTED-INDS                               
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
                                                                                
               END-PERFORM                                                      
                                                                                
               MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
                                         TO PV-BLOCK-NUMBER                     
                                                                                
               PERFORM 4410-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
                                                                                
           MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
                             ASC-NUMBER-OF-SELECTED-ITEMS.                      
           IF ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                               
               SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS                    
           END-IF.                                                              
                                                                                
           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *    MARK EACH ITEM AS BEING SELECTED.                           *        
      *----------------------------------------------------------------*        
                                                                                
       2151-SET-SELECTED-INDS.                                                  
                                                                                
           IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
                   NOT EQUAL ZERO                                               
               IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
                   SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
                       ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
                       ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
                                     TO  TRUE                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *  MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE     *        
      *  SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM.  *        
      *----------------------------------------------------------------*        
                                                                                
       2200-MOVE-SCREEN-TO-COMMAREA.                                            
                                                                                
           MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
           MOVE +1                       TO PV-SUB.                             
                                                                                
           IF  ASTRTITL > ZERO                                                  
               MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
           ELSE                                                                 
               IF  ASTRTITF EQUAL DP015-ERASE-EOF                               
                   INITIALIZE ASC-START-ITEM-REQUEST                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 3100-SET-SUBSCRIPTS.                                         
                                                                                
           PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
                                                                                
      ******************************************************************        
      ** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
      ******************************************************************        
                                                                                
       2210-MOVE-LINES-TO-COMMAREA.                                             
                                                                                
           IF  MR-SELECTL (PV-SUB) > ZERO                                       
               SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
               MOVE MR-SELECTI (PV-SUB)                                         
                                         TO ASC-TEMP-SELECTED-SW                
                                       (ASC-BLK-SUB, ASC-ITEM-SUB)              
           ELSE                                                                 
               IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
                       SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
                       MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
                                       (ASC-BLK-SUB, ASC-ITEM-SUB)              
               END-IF                                                           
           END-IF.                                                              
                                                                                
           ADD +1                        TO ASC-ITEM-SUB.                       
           IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
               ADD +1                    TO ASC-BLK-SUB                         
               MOVE +1                   TO ASC-ITEM-SUB                        
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *  EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE       *        
      *  COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY).  *        
      *----------------------------------------------------------------*        
                                                                                
       3000-EDIT-DATA-IN-COMMAREA.                                              
                                                                                
           SET PS-NO-ERROR               TO TRUE.                               
           PERFORM 3200-RESET-ATTRIBUTES.                                       
           MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
      *                                                                         
      * CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
      * EDITTING IS DONE FROM THE BOTTOM UP.                                    
      **                                                                        
           COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
               ASC-ITEMS-DISPLAYED - 1.                                         
           PERFORM 3100-SET-SUBSCRIPTS.                                         
           PERFORM 3300-EDIT-SELECTIONS                                         
               VARYING PV-SUB                                                   
               FROM PV-SUB BY -1                                                
               UNTIL PV-SUB < +1.                                               
                                                                                
           IF  PS-NO-ERROR                                                      
               PERFORM 3400-CHECK-RANGE-SEQUENCE                                
           END-IF.                                                              
                                                                                
           IF  ((PS-NO-ERROR)                                                   
             AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
             AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
                   PERFORM 3500-MARK-RANGE-AS-SELECTED                          
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
      *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
      *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
      *                                                                *        
      *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
      *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
      *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
      *    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
      *----------------------------------------------------------------*        
                                                                                
       3100-SET-SUBSCRIPTS.                                                     
                                                                                
           COMPUTE ASC-ITEM-SUB =                                               
               (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
                                                                                
           IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
               SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
                                                                                
               MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
           ELSE                                                                 
               MOVE +1                     TO ASC-BLK-SUB                       
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *  RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.      *        
      *----------------------------------------------------------------*        
                                                                                
       3200-RESET-ATTRIBUTES.                                                   
                                                                                
           MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
           MOVE DP015-GREEN              TO ASTRTITC.                           
           MOVE DP015-UNDERLINE          TO ASTRTITH.                           
                                                                                
           PERFORM                                                              
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                         TO MR-SELECTA (PV-SUB)                 
               MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
               MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
           END-PERFORM.                                                         
                                                                                
      *----------------------------------------------------------------*        
      *  EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS     *        
      *  WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'    *        
      *  IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR     *        
      *  WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.              *        
      *----------------------------------------------------------------*        
                                                                                
       3300-EDIT-SELECTIONS.                                                    
                                                                                
           EVALUATE TRUE                                                        
               WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
                   = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
                       CONTINUE                                                 
                                                                                
               WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
                   IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
                       ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
                       MOVE ASC-TEMP-SELECTED-SW                                
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                                      TO ASC-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                   END-IF                                                       
                                                                                
               WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
                       = SPACE                                                  
                   EVALUATE TRUE                                                
                       WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
                           SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
                           MOVE SPACE TO ASC-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                       WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
                           MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
                           MOVE SPACE TO ASC-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                       WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
                           MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
                           MOVE SPACE TO ASC-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                   END-EVALUATE                                                 
                                                                                
               WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
                   IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
                       SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
                   END-IF                                                       
                   PERFORM 3310-EDIT-RANGE                                      
                                                                                
               WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
                   IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
                       SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
                   END-IF                                                       
                   PERFORM 3310-EDIT-RANGE                                      
                                                                                
               WHEN OTHER                                                       
                   MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
                   SET DP020-MSG-FATAL    TO TRUE                               
                   MOVE -1                TO MR-SELECTL (PV-SUB)                
                   SET DP030-SET-CURSOR-APPL-1                                  
                                          TO TRUE                               
                   MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
                   MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
                   SET PS-ERROR TO TRUE                                         
                   IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
                       SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
                       MOVE SPACE         TO ASC-SELECTED-SW                    
                                              (ASC-BLK-SUB ASC-ITEM-SUB)        
                   END-IF                                                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
           SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
           SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
           IF  ASC-ITEM-SUB < 1                                                 
               SUBTRACT +1 FROM ASC-BLK-SUB                                     
               MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *  IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A       *        
      *  RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1     *        
      *  ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) *        
      *  THE ENDING.                                                   *        
      *----------------------------------------------------------------*        
                                                                                
       3310-EDIT-RANGE.                                                         
                                                                                
           IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
               IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
                   MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
                                         TO ASC-SELECTED-SW                     
                                              (ASC-BLK-SUB ASC-ITEM-SUB)        
                   MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
               ELSE                                                             
                   IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
                       CONTINUE                                                 
                   ELSE                                                         
      *                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
                       MOVE PC-TSYMSG-00054                                     
                                         TO DP020-MSG-NUMBER                    
                       SET DP020-MSG-FATAL                                      
                                         TO TRUE                                
                       MOVE -1           TO MR-SELECTL (PV-SUB)                 
                       SET DP030-SET-CURSOR-APPL-1                              
                                         TO TRUE                                
                       MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
                       MOVE DP015-UNDERLINE                                     
                                         TO MR-SELECTH (PV-SUB)                 
                       SET PS-ERROR      TO TRUE                                
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
                   MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO ASC-SELECTED-SW                     
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
                   MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
               ELSE                                                             
                   IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
                       CONTINUE                                                 
                   ELSE                                                         
      *                -- ONLY ONE END-OF-RANGE ALLOWED --                      
                       MOVE PC-TSYMSG-00055                                     
                                         TO DP020-MSG-NUMBER                    
                       SET DP020-MSG-FATAL                                      
                                         TO TRUE                                
                       MOVE -1           TO MR-SELECTL (PV-SUB)                 
                       SET DP030-SET-CURSOR-APPL-1                              
                                         TO TRUE                                
                       SET PS-ERROR      TO TRUE                                
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *  IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,     *        
      *  MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.   *        
      *----------------------------------------------------------------*        
                                                                                
       3400-CHECK-RANGE-SEQUENCE.                                               
                                                                                
           IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
             AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
             AND  ASC-START-OF-SELECTED-RANGE >                                 
                                               ASC-END-OF-SELECTED-RANGE        
      *        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
               MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
               SET DP020-MSG-FATAL TO TRUE                                      
               SET PS-ERROR TO TRUE                                             
               PERFORM 3410-POSITION-AT-ERROR                                   
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *  BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE     *        
      *  PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR  *        
      *  SHOULD BE POSITIONED ON A RANGE ERROR.                        *        
      *----------------------------------------------------------------*        
                                                                                
       3410-POSITION-AT-ERROR.                                                  
                                                                                
           SET DP030-SET-CURSOR-APPL-1                                          
                                         TO TRUE.                               
           COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
      *                                                                         
           COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
      *    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
      *    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
      *----------------------------------------------------------------*        
                                                                                
       3500-MARK-RANGE-AS-SELECTED.                                             
                                                                                
           MOVE +1                       TO  ASC-BLK-SUB.                       
           PERFORM 3510-MARK-SELECTED-INDS                                      
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           ADD +1                        TO ASC-BLK-SUB.                        
           PERFORM 3510-MARK-SELECTED-INDS                                      
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
                                         TO  PV-LIST-ITEM-NUMBER.               
           COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
               1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
                     PC-ITEMS-PER-PANEL).                                       
                                                                                
           MOVE ASC-START-OF-SELECTED-RANGE                                     
                                         TO PV-START-OF-SELECTED-RANGE.         
           COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
               1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
                     PC-ITEMS-PER-PANEL).                                       
                                                                                
           MOVE ASC-END-OF-SELECTED-RANGE                                       
                                         TO  PV-END-OF-SELECTED-RANGE.          
           COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
               1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
                     PC-ITEMS-PER-PANEL).                                       
           COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
               PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
                                                                                
           IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
              (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
               MOVE +1                   TO ASC-BLK-SUB                         
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
               COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY - 1                                  
                                                                                
               PERFORM                                                          
                   UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
                         PV-PREV-BLOCK-IN-ARRAY                                 
                   MOVE PV-FIRST-BLOCK-IN-RANGE                                 
                                         TO PV-BLOCK-NUMBER                     
                   PERFORM 4410-READ-TEMP-STORAGE                               
                   PERFORM 3510-MARK-SELECTED-INDS                              
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
               END-PERFORM                                                      
                                                                                
               COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
               PERFORM                                                          
                   UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
                         PV-LAST-BLOCK-IN-RANGE                                 
                   MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
                                         TO PV-BLOCK-NUMBER                     
                   PERFORM 4410-READ-TEMP-STORAGE                               
                   PERFORM 3510-MARK-SELECTED-INDS                              
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
               END-PERFORM                                                      
                                                                                
               MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
                                         TO PV-BLOCK-NUMBER                     
               PERFORM 4410-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
           MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
                                            ASC-END-OF-SELECTED-RANGE.          
      /                                                                         
      *----------------------------------------------------------------*        
      * SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
      *----------------------------------------------------------------*        
                                                                                
       3510-MARK-SELECTED-INDS.                                                 
                                                                                
           IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
                                      ASC-ITEM-SUB) >                           
               ASC-START-OF-SELECTED-RANGE)                                     
           OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
                                      ASC-ITEM-SUB) =                           
               ASC-START-OF-SELECTED-RANGE)                                     
                                                                                
               IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
                                          ASC-ITEM-SUB) <                       
                   ASC-END-OF-SELECTED-RANGE)                                   
               OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
                                          ASC-ITEM-SUB) =                       
                   ASC-END-OF-SELECTED-RANGE)                                   
                                                                                
                   IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
                       SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
                           ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
                                     TO TRUE                                    
                       SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      
                                     TO TRUE                                    
                       ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS            
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
      *    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
      *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
      *    DISPLAY THE PANEL TO USER.                                  *        
      *----------------------------------------------------------------*        
                                                                                
       4000-BUILD-INITIAL-PANEL.                                                
                                                                                
           INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
                                       ASC-BLOCK-ENTRIES (2).                   
           MOVE +1                 TO  ASC-BLK-SUB.                             
           SET ASC-ITEMS-LEFT-ON-DB                                             
                                   TO TRUE.                                     
                                                                                
           MOVE +0                 TO ASC-ITEM-SUB                              
                                      ASC-NUMBER-OF-ITEMS-READ                  
                                      ASC-NUMBER-OF-SELECTED-ITEMS              
                                      ASC-HIGHEST-BLOCK-WRITTEN                 
                                      ASC-START-OF-SELECTED-RANGE               
                                      ASC-END-OF-SELECTED-RANGE                 
                                      ASC-ITEM-AT-TOP-OF-PANEL.                 
           PERFORM 4100-PREPARE-CURSOR.                                         
                                                                                
           PERFORM 4010-OPEN-CURSOR.                                            
                                                                                
           PERFORM 4300-READ-ITEMS-FROM-DB.                                     
                                                                                
           PERFORM 4020-CLOSE-CURSOR.                                           
                                                                                
           MOVE +1                   TO  PV-TOP-ITEM.                           
           MOVE SPACE                TO  ASC-START-ITEM-REQUEST.                
           IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
               SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
               SET DP020-MSG-FATAL   TO  TRUE                                   
               MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
           ELSE                                                                 
               MOVE -1               TO MR-SELECTL (1)                          
               SET DP030-SET-CURSOR-APPL-1                                      
                                     TO TRUE                                    
               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      * OPEN THE CURSOR.                                               *        
      *----------------------------------------------------------------*        
                                                                                
       4010-OPEN-CURSOR.                                                        
                                                                                
W26600         MOVE ASC-KEY-STORE-NBR      TO PV-LOC-NBR.                       
W26600         MOVE PV-LOC-NBR-NUM         TO SUMINV-LOC-NBR                    
W26600                                        ADJADD-LOC-NBR                    
W26600                                        ADJDTL-LOC-NBR.                   
               MOVE ASC-KEY-AREA-NBR       TO SUMINV-AREA-NBR                   
                                              ADJADD-AREA-NBR                   
                                              ADJDTL-AREA-NBR.                  
               EXEC SQL                                                         
                    OPEN SUMMARY-CSR                                            
               END-EXEC.                                                        
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                        PERFORM 9900-SOFT-ABORT                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '4010-OPEN-CURSOR'                                  
                                         TO DP013-PARAGRAPH                     
                       MOVE 'OPEN SUMMARY INVENTORY CURSOR - '                  
                                         TO DP013-MESSAGE-TEXT (1)              
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TSUMINV '   TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE.                                                    
      /                                                                         
      *----------------------------------------------------------------*        
      * CLOSE THE TMESSAGE CURSOR.                                     *        
      *   IN THIS PARTICULAR APPLICATION ONE OF 3 DIFFERENT CURSORS    *        
      *   WILL BE CLOSED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *        
      *   USER.                                                        *        
      *----------------------------------------------------------------*        
                                                                                
       4020-CLOSE-CURSOR.                                                       
                                                                                
           EXEC SQL                                                             
                CLOSE SUMMARY-CSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '4020-CLOSE-CURSOR'                                     
                                     TO DP013-PARAGRAPH                         
                   MOVE 'CLOSE SUMMARY INVENTORY CURSOR'                        
                                     TO DP013-MESSAGE-TEXT (1)                  
                   MOVE SQLCA        TO DP013-SQLCA                             
                   MOVE 'TSUMINV '   TO DP013-DB2-TABLE-NAME (1)                
                   SET DP013-DB2-ABEND                                          
                       DP013-XCTL-DISPLAY-RESTART TO TRUE                       
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
      /                                                                         
      *----------------------------------------------------------------*        
      *    SET UP THE HOST VARIABLES.                                  *        
      *----------------------------------------------------------------*        
                                                                                
       4100-PREPARE-CURSOR.                                                     
                                                                                
           MOVE IN001-STORE-NBR    TO  ASC-KEY-STORE-NBR.                       
           MOVE IN001-AREA-NBR     TO  ASC-KEY-AREA-NBR.                        
                                                                                
W26600     MOVE ASC-KEY-STORE-NBR  TO PV-LOC-NBR.                               
W26600     MOVE PV-LOC-NBR-NUM     TO SUMINV-LOC-NBR.                           
           MOVE ASC-KEY-AREA-NBR   TO SUMINV-AREA-NBR.                          
                                                                                
           MOVE ASC-KEY-SKU-NBR    TO SUMINV-SKU-NBR.                           
           MOVE ASC-KEY-PR-AMT     TO SUMINV-INV-UNIT-PR-AMT.                   
                                                                                
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
      *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
      *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
      *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
      *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
      *                                                                *        
      *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
      *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
      *    UPON INITIALIZATION.                                        *        
      *----------------------------------------------------------------*        
                                                                                
       4300-READ-ITEMS-FROM-DB.                                                 
                                                                                
           PERFORM                                                              
               VARYING PV-READ-COUNT                                            
               FROM 1 BY 1                                                      
               UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
                  OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
                                                                                
               PERFORM 4310-FETCH                                               
                                                                                
               IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                            
                   ADD +1                TO  ASC-ITEM-SUB                       
               ELSE                                                             
                   ADD +1                TO  ASC-BLK-SUB                        
                   MOVE +1               TO  ASC-ITEM-SUB                       
                                                                                
                   IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                     
                       MOVE +1           TO  ASC-BLK-SUB                        
                       SET ASC-BLOCK-MODIFIED (1)                               
                                         TO  TRUE                               
                       PERFORM 5100-WRITE-TEMP-STORAGE                          
***************        PERFORM 7100-WRITE-TEMP-STORAGE                          
                       MOVE ASC-BLOCK-ENTRIES                                   
                                 (PC-MAX-BLOCKS-IN-ARRAY)                       
                                  TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)             
                       ADD +1            TO  ASC-BLK-SUB                        
                       INITIALIZE ASC-BLOCK-ENTRIES(ASC-BLK-SUB)                
                       SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)                  
                                         TO  TRUE                               
                   END-IF                                                       
               END-IF                                                           
                                                                                
               IF  ASC-ITEMS-LEFT-ON-DB                                         
                   ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
                   MOVE ASC-NUMBER-OF-ITEMS-READ                                
                                         TO ASC-LIST-ITEM-NUMBER                
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                   MOVE SUMINV-UPC-NBR   TO  PV-HOLD-UPC                        
                   MOVE PV-HOLD-UPC      TO  PV-UPC-NUMBER                      
                   MOVE PV-UPC-NUMBER    TO  ASC-UPC                            
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE SUMINV-INV-UNIT-PR-AMT                                  
                                         TO  ASC-PR-AMT                         
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE SUMINV-INV-QTY   TO  ASC-INV-QTY                        
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   ADD SUMINV-INV-QTY    TO  PV-INV-QTY-TTL                     
                   MOVE SUMINV-EXTD-AMT  TO  ASC-PR-EXT-AMT                     
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   ADD SUMINV-EXTD-AMT   TO  PV-PR-EXT-AMT-TTL                  
      *            ROWS ARE COLOR-CODED BASED ON SOURCE                         
                   IF  PV-TSUMINV-ROW                                           
                       MOVE DP015-BLUE                                          
                           TO ASC-COLOR (ASC-BLK-SUB ASC-ITEM-SUB)              
                   ELSE                                                         
                       MOVE DP015-TURQUOISE                                     
                            TO ASC-COLOR (ASC-BLK-SUB ASC-ITEM-SUB)             
                   END-IF                                                       
      *            DETERMINE IF ANY ADJUSTMENTS WERE MADE TO BASE ITEMS         
S26600             PERFORM 4350-CHECK-FOR-ADJUSTMENTS                           
               ELSE                                                             
      *            NO MORE ITEMS LEFT FROM CURSOR CALL (ADD +1 = TOTAL)         
                   IF ASC-NUMBER-OF-ITEMS-READ > ZERO                           
                       ADD +1            TO ASC-NUMBER-OF-ITEMS-READ            
                       MOVE ASC-NUMBER-OF-ITEMS-READ                            
                                         TO ASC-LIST-ITEM-NUMBER                
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                       MOVE '     AREA TOTAL' TO  ASC-UPC-X                     
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                       MOVE ZERO         TO  ASC-PR-AMT                         
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                       MOVE PV-INV-QTY-TTL                                      
                                         TO  ASC-INV-QTY                        
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                       MOVE PV-PR-EXT-AMT-TTL                                   
                                         TO  ASC-PR-EXT-AMT                     
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
WHITE                  MOVE DP015-WHITE  TO  ASC-COLOR                          
WHITE                                        (ASC-BLK-SUB ASC-ITEM-SUB)         
                   END-IF                                                       
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
NEXT         IF PS-USE-NEXT-SELECTION                                           
 |             MOVE +1               TO ASC-BLK-SUB                             
 |             PERFORM 7100-WRITE-TEMP-STORAGE                                  
 |             ADD +1                TO ASC-BLK-SUB                             
 |             PERFORM 7100-WRITE-TEMP-STORAGE                                  
 |                                                                              
 |             MOVE 1                TO ASC-BLK-SUB                             
 |             MOVE ASC-SEL-SUB      TO PV-BLOCK-NUMBER                         
 |             PERFORM 4410-READ-TEMP-STORAGE                                   
 |           ELSE                                                               
               MOVE +1               TO ASC-BLK-SUB                             
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
               ADD +1                TO ASC-BLK-SUB                             
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
                                                                                
               MOVE 1                TO ASC-BLK-SUB                             
                                        PV-BLOCK-NUMBER                         
               PERFORM 4410-READ-TEMP-STORAGE                                   
               IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
                   MOVE 2            TO ASC-BLK-SUB                             
                                        PV-BLOCK-NUMBER                         
                   PERFORM 4410-READ-TEMP-STORAGE                               
               END-IF                                                           
             END-IF                                                             
NEXT       END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *    FETCH THE CURSOR.                                           *        
      *----------------------------------------------------------------*        
                                                                                
       4310-FETCH.                                                              
                                                                                
           EXEC SQL                                                             
                FETCH                                                           
                      SUMMARY-CSR                                               
                INTO                                                            
                      :SUMINV-SKU-NBR                                           
                    , :SUMINV-UPC-NBR                                           
                    , :SUMINV-INV-UNIT-PR-AMT                                   
                    , :SUMINV-INV-QTY                                           
S28545              , :SUMINV-DKEY-NBR                                          
                    , :SUMINV-EXTD-AMT                                          
                    , :PV-ROW-TYPE                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                    PERFORM 9900-SOFT-ABORT                                     
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE NOT = ZERO                                          
                    MOVE '4310-FETCH-CURSOR'                                    
                                     TO DP013-PARAGRAPH                         
                    MOVE 'FETCH SUMMARY INVENTORY CURSOR'                       
                                     TO DP013-MESSAGE-TEXT (1)                  
                    MOVE SQLCA       TO DP013-SQLCA                             
                    MOVE 'TSUMINV '  TO DP013-DB2-TABLE-NAME (1)                
                    SET DP013-DB2-ABEND                                         
                                     TO TRUE                                    
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
           IF  SQLCODE EQUAL +100                                               
               SET ASC-NO-ITEMS-LEFT-ON-DB                                      
                                     TO TRUE                                    
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
S26600*    DETERMINE IF ANY ADJUSTMENTS HAVE BEEN MADE TO THE ORIGINAL          
  |   *----------------------------------------------------------------*        
  |                                                                             
  |    4350-CHECK-FOR-ADJUSTMENTS.                                              
  |                                                                             
  |        EXEC SQL                                                             
  |            SELECT 1                                                         
  |              INTO :PV-COUNT :PV-NULL                                        
  |              FROM TADJDTL                                                   
  |             WHERE LOC_NBR       = :SUMINV-LOC-NBR                           
  |               AND AREA_NBR      = :SUMINV-AREA-NBR                          
  |               AND SKU_NBR       = :SUMINV-SKU-NBR                           
  |               AND UPC_NBR       = :SUMINV-UPC-NBR                           
S28545            AND DKEY_NBR      = :SUMINV-DKEY-NBR                          
  |        END-EXEC.                                                            
  |                                                                             
  |        EVALUATE TRUE                                                        
  |            WHEN SQLCODE = ZERO                                              
W27710         WHEN SQLCODE = -811                                              
  |              IF PV-NULL < 0                                                 
  |   *             NO ADJUSTMENTS TO TSUMINV/TADJADD ROWS WERE FOUND           
  |                 CONTINUE                                                    
  |              ELSE                                                           
  |                 SET PS-ADJUSTMENTS-EXIST TO TRUE                            
  |              END-IF                                                         
  |            WHEN SQLCODE = +100                                              
  |                 CONTINUE                                                    
  |            WHEN SQLCODE = -904                                              
  |            WHEN SQLCODE = -913                                              
  |                 PERFORM 9900-SOFT-ABORT                                     
  |            WHEN SQLWARN0 NOT = SPACE                                        
  |            WHEN SQLCODE NOT = ZERO                                          
  |                 MOVE '4350-CHECK-FOR-ADJUSTMENTS'                           
  |                                  TO DP013-PARAGRAPH                         
  |                 MOVE 'SELECT TADJDTL FOR ADJUSTMENTS'                       
  |                                  TO DP013-MESSAGE-TEXT (1)                  
  |                 MOVE SQLCA       TO DP013-SQLCA                             
  |                 MOVE 'TADJDTL '  TO DP013-DB2-TABLE-NAME (1)                
  |                 SET DP013-DB2-ABEND                                         
  |                                  TO TRUE                                    
  |                 PERFORM DP013-0000-PROCESS-ABEND                            
  |        END-EVALUATE.                                                        
W26600                                                                          
      *----------------------------------------------------------------*        
      * MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
      *----------------------------------------------------------------*        
                                                                                
       4400-MOVE-COMMAREA-TO-SCREEN.                                            
                                                                                
           IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
               AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
               OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
                   MOVE +1    TO ASC-BLK-SUB                                    
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   ADD +1     TO ASC-BLK-SUB                                    
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
                       GIVING PV-BLOCK-NUMBER                                   
                       REMAINDER PV-REMAINDER                                   
                   IF  PV-REMAINDER > ZERO                                      
                       ADD +1 TO PV-BLOCK-NUMBER                                
                   END-IF                                                       
                   MOVE +1    TO ASC-BLK-SUB                                    
                   PERFORM 4410-READ-TEMP-STORAGE                               
                   ADD +1     TO ASC-BLK-SUB                                    
                                 PV-BLOCK-NUMBER                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
           END-IF.                                                              
                                                                                
           MOVE +1            TO ASC-BLK-SUB.                                   
           COMPUTE ASC-ITEM-SUB =                                               
               ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
               + PC-ITEMS-PER-PANEL).                                           
                                                                                
           IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
               ADD +1         TO ASC-BLK-SUB                                    
               SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
           END-IF.                                                              
                                                                                
           COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
               ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
           PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
           PERFORM 4420-FORMAT-LIST-LINES                                       
               VARYING PV-SUB                                                   
               FROM PC-ITEMS-PER-PANEL BY -1                                    
               UNTIL PV-SUB < +1.                                               
                                                                                
           COMPUTE ASC-ITEMS-DISPLAYED =                                        
               ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
           MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL.           
           IF  PS-NO-ERROR                                                      
               MOVE PV-TOP-ITEM          TO ASTRTITO                            
           END-IF.                                                              
                                                                                
           MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
           MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
           MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
                                         TO ASELITO.                            
           MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
W26600     MOVE IN001-STORE-NBR          TO ASTRNBRO.                           
           MOVE IN001-STORE-NAME         TO ASTRNAMO.                           
           MOVE IN001-AREA-NBR           TO AAREAO.                             
                                                                                
           MOVE ASC-KEY-SKU-NBR          TO ASKUO.                              
                                                                                
           IF  DP020-MSG-NUMBER-X = SPACE                                       
               IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
      *            --- MORE ITEMS TO DISPLAY ----                               
                   MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-INFORMATIONAL TO TRUE                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
      *----------------------------------------------------------------*        
                                                                                
       4410-READ-TEMP-STORAGE.                                                  
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
             OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND     TO TRUE                                 
               SET DP013-NO-ROLLBACK    TO TRUE                                 
               MOVE '4410-READ-TEMP-STORAGE'                                    
                                        TO DP013-PARAGRAPH                      
               MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
                                        TO DP013-MESSAGE-TEXT(1)                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
                                                                                
           IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
               INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
      *----------------------------------------------------------------*        
                                                                                
       4420-FORMAT-LIST-LINES.                                                  
                                                                                
           IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
               SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
               MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA      (PV-SUB)              
               MOVE SPACE              TO MR-SELECTI      (PV-SUB)              
                                          MR-ACTION       (PV-SUB)              
                                          MR-UPC-X        (PV-SUB)              
                                          MR-INV-QTY-X    (PV-SUB)              
                                          MR-PR-AMT-X     (PV-SUB)              
                                          MR-PR-EXT-AMT-X (PV-SUB)              
                                          MR-ADJ-MSG      (PV-SUB)              
           ELSE                                                                 
               IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
                                                         > SPACE                
                   MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
                                       TO MR-SELECTI (PV-SUB)                   
               ELSE                                                             
                   MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
                                       TO MR-SELECTI (PV-SUB)                   
               END-IF                                                           
                                                                                
               IF  DP030-SET-CURSOR-TRAILER                                     
                   IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
                       > SPACE)                                                 
                     OR (PV-SUB = 1))                                           
                      SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
                      MOVE -1          TO MR-SELECTL (PV-SUB)                   
                   END-IF                                                       
               END-IF                                                           
                                                                                
               IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
                   MOVE ASC-ACTION         (ASC-BLK-SUB ASC-ITEM-SUB)           
                                       TO  MR-ACTION        (PV-SUB)            
      * SIGNED-DECIMAL TO SIGNED NUMERIC                                        
                   MOVE ASC-UPC            (ASC-BLK-SUB ASC-ITEM-SUB)           
                                       TO  PV-HOLD-UPC                          
                   MOVE PV-HOLD-UPC    TO  PV-UPC-NUMBER                        
                   MOVE PV-UPC-NUMBER  TO  PV-UPC                               
                   MOVE PV-UPC         TO  MR-UPC           (PV-SUB)            
                   MOVE ASC-INV-QTY        (ASC-BLK-SUB ASC-ITEM-SUB)           
                                       TO  MR-INV-QTY       (PV-SUB)            
                   MOVE ASC-PR-AMT         (ASC-BLK-SUB ASC-ITEM-SUB)           
                                       TO  MR-PR-AMT        (PV-SUB)            
                   MOVE ASC-PR-EXT-AMT     (ASC-BLK-SUB ASC-ITEM-SUB)           
                                       TO  MR-PR-EXT-AMT    (PV-SUB)            
                   MOVE ASC-COLOR          (ASC-BLK-SUB ASC-ITEM-SUB)           
                                       TO  MR-UPCC          (PV-SUB)            
                                           MR-INV-QTYC      (PV-SUB)            
                                           MR-PR-AMTC       (PV-SUB)            
                                           MR-PR-EXT-AMTC   (PV-SUB)            
      ******************************************************************        
      ** THE LIST SCREEN INDICATES WHAT THE INITIAL SOURCE OF THESE   **        
      ** RECORDS IS FROM TSUMINV, TADJADD OR TADJDTL.                 **        
      ** 1) FROM AN INVENTORY SCANNING SERVICE FILE (MO MESSAGE)      **        
      ** 2) FROM THE ADJUSTMENT ADD OR MASS ADD SCREENS (TADJADD)     **        
      ** 3) AN ADJUSTMENT TO ROWS FROM EITHER OF THE TWO TABLES ABOVE.**        
      **    (FROM TADJDTL - LIST SIMPLY AS 'ADJUST')                  **        
      ******************************************************************        
      ** TSUMINV ROW                                                            
                   IF   ASC-COLOR (ASC-BLK-SUB ASC-ITEM-SUB) EQUAL              
                           DP015-BLUE                                           
S26600                     IF PS-ADJUSTMENTS-EXIST                              
S26600                         MOVE PC-ADJ-MSG1 TO MR-ADJ-MSG (PV-SUB)          
S26600                     ELSE                                                 
                               MOVE PC-NO-ADJ-MSG TO MR-ADJ-MSG (PV-SUB)        
S26600                     END-IF                                               
      ** TADJADD ROW                                                            
                   ELSE                                                         
                       IF  ASC-COLOR (ASC-BLK-SUB ASC-ITEM-SUB) EQUAL           
                           DP015-TURQUOISE                                      
S26600                     IF PS-ADJUSTMENTS-EXIST                              
S26600                         MOVE PC-ADJ-MSG1 TO MR-ADJ-MSG (PV-SUB)          
S26600                     ELSE                                                 
                               MOVE PC-ADJ-MSG2   TO MR-ADJ-MSG (PV-SUB)        
S26600                     END-IF                                               
      ** TOTAL ROW                                                              
                       ELSE                                                     
WHITE                      IF ASC-COLOR (ASC-BLK-SUB ASC-ITEM-SUB) EQUAL        
                              DP015-WHITE                                       
                               MOVE SPACES    TO MR-ADJ-MSG (PV-SUB)            
                               MOVE ASC-UPC-X(ASC-BLK-SUB ASC-ITEM-SUB)         
                                              TO  MR-UPC-X  (PV-SUB)            
*********DE-SELECT TOTAL LINE                                                   
                               MOVE DP015-ASK-DRK-OFF  TO                       
                                              MR-SELECTA (PV-SUB)               
                               MOVE SPACE  TO MR-SELECTI (PV-SUB)               
                               MOVE ZEROES TO ASC-LIST-ITEM-NUMBER              
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                               MOVE SPACES TO ASC-TEMP-SELECTED-SW              
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
      ** TADJDTL ROW                                                            
                           ELSE                                                 
                               MOVE PC-ADJ-MSG1   TO MR-ADJ-MSG (PV-SUB)        
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           SUBTRACT +1 FROM ASC-ITEM-SUB                                        
           IF  ASC-ITEM-SUB < + 1                                               
               MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
               MOVE +1                 TO ASC-BLK-SUB                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
      ** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
      ** ISSUE A WARNING.                                             **        
      ******************************************************************        
                                                                                
       4430-CHECK-FOR-BEGIN-AND-END.                                            
                                                                                
           IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
               IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
      *            -- NO END-OF-RANGE SPECFIED ---                              
                   MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-WARNING TO TRUE                                
                   PERFORM 4431-POSITION-AT-WARNING                             
               END-IF                                                           
           ELSE                                                                 
               IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
      *            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
                   MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-WARNING TO TRUE                                
                   PERFORM 4431-POSITION-AT-WARNING                             
               END-IF                                                           
           END-IF.                                                              
      ******************************************************************        
      ** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
      ** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
      ** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
      ** POSITION THE CURSOR THERE.                                   **        
      ******************************************************************        
                                                                                
       4431-POSITION-AT-WARNING.                                                
                                                                                
           COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP030-SET-CURSOR-APPL-1                                      
                                         TO TRUE                                
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
                                                                                
           COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP030-SET-CURSOR-APPL-1                                      
                                         TO TRUE                                
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      * PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
      * ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
      * FOR THE SELECTED ITEMS QUEUE.                                  *        
      *                                                                *        
      * FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
      * QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
      * THE ARRAY IS FULL.                                             *        
      *----------------------------------------------------------------*        
                                                                                
       5000-PREPARE-SEL-QUEUE.                                                  
                                                                                
           MOVE +1                   TO  ASC-BLK-SUB.                           
           PERFORM 5100-WRITE-TEMP-STORAGE.                                     
           ADD +1                    TO  ASC-BLK-SUB.                           
           PERFORM 5100-WRITE-TEMP-STORAGE.                                     
                                                                                
           MOVE ASC-FROM-QUEUE-NAME  TO  IN001-SEL-QUEUE-NAME.                  
           PERFORM 6010-DELETE-SEL-QUEUE.                                       
                                                                                
           SET ASC-UPDATE-TSQ        TO  TRUE.                                  
                                                                                
           SET IN012-IDX             TO  1.                                     
           MOVE ZERO                 TO IN001-NUMBER-OF-TS-ITEMS.               
           INITIALIZE IN012-SELECTED-ITEM-ARRAY.                                
                                                                                
           PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
                   VARYING PV-ITEM                                              
                   FROM 1 BY 1                                                  
                   UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
                                                                                
           IF  IN012-IDX > 1                                                    
               PERFORM 5205-WRITE-SEL-QUEUE                                     
               ADD +1                TO IN001-NUMBER-OF-TS-ITEMS                
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
      *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
      *----------------------------------------------------------------*        
                                                                                
       5100-WRITE-TEMP-STORAGE.                                                 
                                                                                
           MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
                                         TO PV-LIST-ITEM-NUMBER.                
           COMPUTE PV-BLOCK-NUMBER =                                            
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
           OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
               IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
                   PERFORM 5105-REWRITE-ASC-LISTQ                               
               END-IF                                                           
           ELSE                                                                 
               COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
               ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
               PERFORM 5110-WRITE-ASC-LISTQ                                     
               ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
           END-IF.                                                              
                                                                                
           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
                                                                                
      ******************************************************************        
      ** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
      ** SPECIFIC COMMAREA.                                           **        
      ******************************************************************        
                                                                                
       5105-REWRITE-ASC-LISTQ.                                                  
                                                                                
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   REWRITE                                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND            TO TRUE                          
               SET DP013-NO-ROLLBACK           TO TRUE                          
               MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
               MOVE 'ATTEMPTING TO REWRITE LIST TEMP SKU STORAGE QUEUE'         
                                               TO DP013-MESSAGE-TEXT(1)         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
      ** SPECIFIC COMMAREA.                                           **        
      ******************************************************************        
                                                                                
       5110-WRITE-ASC-LISTQ.                                                    
                                                                                
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
           IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '5110-WRITE-ASC-LISTQ'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO WRITE TEMP STORAGE QUEUE'                    
                                         TO DP013-MESSAGE-TEXT(1)               
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      * READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
      * TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
      *----------------------------------------------------------------*        
                                                                                
       5200-FILL-SEL-QUEUE-RECORD.                                              
                                                                                
           PERFORM 6112-READ-LIST-QUEUE.                                        
                                                                                
           IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
               PERFORM                                                          
               VARYING LQW-IDX                                                  
               FROM 1 BY 1                                                      
               UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
                 OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
                                                                                
                   IF  LQW-SELECT (LQW-IDX)                                     
                       MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
                                         TO IN012-ITEM      (IN012-IDX)         
                       MOVE ASC-KEY-SKU-NBR                                     
                                         TO IN012-SKU-NBR   (IN012-IDX)         
                       MOVE LQW-KEY  (LQW-IDX)                                  
                                         TO IN012-UPC-NBR   (IN012-IDX)         
                       MOVE SPACE        TO IN012-ACTION-SW (IN012-IDX)         
                       MOVE LQW-INV-QTY (LQW-IDX)                               
                                         TO IN012-QTY       (IN012-IDX)         
                       MOVE LQW-PR-AMT (LQW-IDX)                                
                                         TO IN012-PRICE     (IN012-IDX)         
                       IF  LQW-COLOR (LQW-IDX) EQUAL DP015-BLUE                 
                           SET IN012-FROM-TSUMINV (IN012-IDX) TO TRUE           
                       ELSE                                                     
                           SET IN012-FROM-TADJADD (IN012-IDX) TO TRUE           
                       END-IF                                                   
                                                                                
                       SET IN012-IDX UP BY 1                                    
                       IF  IN012-IDX > IN012-MAX-ITEMS                          
                           PERFORM 5205-WRITE-SEL-QUEUE                         
                           ADD +1        TO IN001-NUMBER-OF-TS-ITEMS            
                           INITIALIZE IN012-SELECTED-ITEM-ARRAY                 
                           SET IN012-IDX TO  1                                  
                       END-IF                                                   
                   END-IF                                                       
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
      *----------------------------------------------------------------*        
                                                                                
       5205-WRITE-SEL-QUEUE.                                                    
                                                                                
           MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
                                         TO IN012-SELECTED-ITEMS.               
                                                                                
           EXEC CICS                                                            
                WRITEQ TS                                                       
                    QUEUE (ASC-SEL-QUEUE-NAME)                                  
                    FROM  (IN012-SELECTED-SKUS)                                 
                    ITEM  (PV-SEL-QUEUE-ITEM)                                   
                    RESP  (PV-CICS-RESP)                                        
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '5205-WRITE-SEL-QUEUE'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
                                         TO DP013-MESSAGE-TEXT(1)               
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *  WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION,  *        
      *  SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT  *        
      *  THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO     *        
      *  THE USER.                                                     *        
      *----------------------------------------------------------------*        
                                                                                
       6000-REFRESH-PANEL-FROM-TEMP.                                            
                                                                                
           IF  ASC-UPDATE-TSQ                                                   
               SET ASC-NO-TSQ-UPDATE TO TRUE                                    
               MOVE ZERO                 TO PV-SAVE-ITEM                        
                                            PV-SEL-QUEUE-ITEM                   
               MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
                                         TO PV-HOLD-SELECTED-ITEMS              
               PERFORM 6100-UPDATE-TSQ                                          
                   UNTIL PV-ITEMS-PROCESSED >                                   
                                           ASC-NUMBER-OF-SELECTED-ITEMS         
               PERFORM 6010-DELETE-SEL-QUEUE                                    
               INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
                          ASC-BLOCK-ENTRIES (2)                                 
                                                                                
               DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                         
                      GIVING PV-BLOCK-NUMBER                                    
                      REMAINDER PV-REMAINDER                                    
               IF  PV-REMAINDER > ZERO                                          
                   ADD +1     TO PV-BLOCK-NUMBER                                
               END-IF                                                           
                                                                                
               MOVE +1        TO ASC-BLK-SUB                                    
               PERFORM 4410-READ-TEMP-STORAGE                                   
               ADD +1         TO ASC-BLK-SUB                                    
                                 PV-BLOCK-NUMBER                                
               PERFORM 4410-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
      *----------------------------------------------------------------*        
                                                                                
       6010-DELETE-SEL-QUEUE.                                                   
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE (ASC-SEL-QUEUE-NAME)                                   
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
              (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-NO-ROLLBACK TO TRUE                                    
               MOVE '6010-DELETE-SEL-QUEUE'                                     
                              TO DP013-PARAGRAPH                                
               MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
                              TO DP013-MESSAGE-TEXT (1)                         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
      *    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
      *    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
      *    THE TEMP QUEUE.                                             *        
      *----------------------------------------------------------------*        
                                                                                
       6100-UPDATE-TSQ.                                                         
                                                                                
           ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
           PERFORM 6105-READ-SEL-QUEUE.                                         
           PERFORM                                                              
               VARYING IN012-IDX                                                
               FROM 1 BY 1                                                      
               UNTIL ((PV-ITEMS-PROCESSED >                                     
                                    PV-HOLD-SELECTED-ITEMS)                     
               OR (IN012-IDX GREATER THAN IN012-MAX-ITEMS))                     
               IF  IN012-ACTION-SW (IN012-IDX) > SPACE                          
                   PERFORM 6110-PROCESS-ITEMS                                   
               END-IF                                                           
               ADD +1                TO PV-ITEMS-PROCESSED                      
           END-PERFORM.                                                         
           PERFORM 6111-WRITE-LIST-QUEUE.                                       
      *----------------------------------------------------------------*        
      *    READ THE LIST QUEUE RECORD.                                 *        
      *----------------------------------------------------------------*        
                                                                                
       6105-READ-SEL-QUEUE.                                                     
                                                                                
           EXEC CICS                                                            
                READQ TS                                                        
                    QUEUE (ASC-SEL-QUEUE-NAME)                                  
                    INTO  (IN012-SELECTED-SKUS)                                 
                    ITEM  (PV-SEL-QUEUE-ITEM)                                   
                    RESP  (PV-CICS-RESP)                                        
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO  TRUE                               
               SET DP013-NO-ROLLBACK     TO  TRUE                               
               MOVE '6105-READ-SEL-QUEUE'                                       
                                         TO  DP013-PARAGRAPH                    
               MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
                                         TO  DP013-MESSAGE-TEXT(1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
      *    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
      *    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
      *----------------------------------------------------------------*        
                                                                                
       6110-PROCESS-ITEMS.                                                      
                                                                                
           COMPUTE PV-ITEM = 1 + ((IN012-ITEM (IN012-IDX) - 1)                  
                           / PC-ITEMS-PER-PANEL).                               
                                                                                
           IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
               IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
                   PERFORM 6111-WRITE-LIST-QUEUE                                
               END-IF                                                           
               MOVE PV-ITEM          TO PV-SAVE-ITEM                            
                                                                                
               PERFORM 6112-READ-LIST-QUEUE                                     
           END-IF.                                                              
                                                                                
           COMPUTE PV-SUB = (IN012-ITEM (IN012-IDX)                             
                          - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
           SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
           MOVE LQW-SELECTED-SW (PV-SUB)                                        
                                     TO LQW-TEMP-SELECTED-SW (PV-SUB).          
           EVALUATE TRUE                                                        
               WHEN IN012-INQ (IN012-IDX)                                       
                    MOVE PC-INQ      TO LQW-ACTION (PV-SUB)                     
                                                                                
               WHEN IN012-UPD (IN012-IDX)                                       
                    MOVE PC-UPD      TO LQW-ACTION (PV-SUB)                     
                                                                                
               WHEN IN012-DEL (IN012-IDX)                                       
                    MOVE PC-DEL      TO LQW-ACTION (PV-SUB)                     
                                                                                
               WHEN IN012-ERR (IN012-IDX)                                       
                    MOVE PC-ERR      TO LQW-ACTION (PV-SUB)                     
           END-EVALUATE.                                                        
                                                                                
           SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
                                                                                
      *----------------------------------------------------------------*        
      *    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *        
      *----------------------------------------------------------------*        
                                                                                
       6111-WRITE-LIST-QUEUE.                                                   
                                                                                
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
                   ITEM  (PV-SAVE-ITEM)                                         
                   REWRITE                                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '6111-WRITE-LIST-QUEUE'                                     
                                     TO  DP013-PARAGRAPH                        
               MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
                                     TO  DP013-MESSAGE-TEXT(1)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
      *----------------------------------------------------------------*        
                                                                                
       6112-READ-LIST-QUEUE.                                                    
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
                   ITEM  (PV-ITEM)                                              
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND        TO TRUE                              
               SET DP013-NO-ROLLBACK       TO TRUE                              
               MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
               MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
                                           TO DP013-MESSAGE-TEXT(1)             
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    PREPARE SELECTED ITEMS FOR QUEUE INTO A TEMPORARY WORK AREA.*        
      *----------------------------------------------------------------*        
       7000-PREPARE-SEL-QUEUE.                                                  
                                                                                
           MOVE +1                   TO  ASC-BLK-SUB.                           
           PERFORM 7100-WRITE-TEMP-STORAGE.                                     
           ADD +1                    TO  ASC-BLK-SUB.                           
           PERFORM 7100-WRITE-TEMP-STORAGE.                                     
                                                                                
           MOVE ASC-SEL-QUEUE-NAME   TO  IN001-SEL-QUEUE-NAME.                  
           PERFORM 7510-DELETE-SEL-QUEUE.                                       
                                                                                
           SET ASC-UPDATE-TSQ        TO  TRUE.                                  
                                                                                
           SET IN012-IDX             TO  1.                                     
           MOVE ZERO                 TO IN001-NUMBER-OF-TS-ITEMS.               
           INITIALIZE IN012-SELECTED-ITEM-ARRAY.                                
                                                                                
           PERFORM 7200-FILL-SEL-QUEUE-RECORD                                   
                   VARYING PV-ITEM                                              
                   FROM 1 BY 1                                                  
                   UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
                                                                                
           IF  IN012-IDX > 1                                                    
               PERFORM 7205-WRITE-SEL-QUEUE                                     
               ADD +1                TO IN001-NUMBER-OF-TS-ITEMS                
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
      *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
      *----------------------------------------------------------------*        
                                                                                
       7100-WRITE-TEMP-STORAGE.                                                 
                                                                                
           MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
                                         TO PV-LIST-ITEM-NUMBER.                
           COMPUTE PV-BLOCK-NUMBER =                                            
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
           OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
               IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
                   PERFORM 7105-REWRITE-ASC-LISTQ                               
               END-IF                                                           
           ELSE                                                                 
               COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
               ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
               PERFORM 7110-WRITE-ASC-LISTQ                                     
               ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
           END-IF.                                                              
                                                                                
           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
                                                                                
      ******************************************************************        
      ** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
      ** SPECIFIC COMMAREA.                                           **        
      ******************************************************************        
                                                                                
       7105-REWRITE-ASC-LISTQ.                                                  
                                                                                
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   REWRITE                                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND            TO TRUE                          
               SET DP013-NO-ROLLBACK           TO TRUE                          
               MOVE '7105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
               MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
                                               TO DP013-MESSAGE-TEXT(1)         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
      ** SPECIFIC COMMAREA.                                           **        
      ******************************************************************        
                                                                                
       7110-WRITE-ASC-LISTQ.                                                    
                                                                                
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
           IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '7110-WRITE-ASC-LISTQ'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
                                         TO DP013-MESSAGE-TEXT(1)               
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      * READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
      * TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
      *----------------------------------------------------------------*        
                                                                                
       7200-FILL-SEL-QUEUE-RECORD.                                              
                                                                                
           PERFORM 7612-READ-LIST-QUEUE.                                        
                                                                                
           IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
               PERFORM                                                          
               VARYING LQW-IDX                                                  
               FROM 1 BY 1                                                      
               UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
                 OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
                                                                                
                   IF  LQW-SELECT (LQW-IDX)                                     
                       MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
                                         TO IN012-ITEM      (IN012-IDX)         
                       MOVE ASC-KEY-SKU-NBR                                     
                                         TO IN012-SKU-NBR   (IN012-IDX)         
                       MOVE LQW-KEY  (LQW-IDX)                                  
                                         TO IN012-UPC-NBR   (IN012-IDX)         
                       MOVE SPACE        TO IN012-ACTION-SW (IN012-IDX)         
                       MOVE LQW-PR-AMT (LQW-IDX)                                
                                         TO IN012-PRICE     (IN012-IDX)         
                                                                                
                       IF  LQW-COLOR (LQW-IDX) EQUAL DP015-TURQUOISE            
                           SET IN012-FROM-TADJADD (IN012-IDX) TO TRUE           
                       ELSE                                                     
                           IF LQW-COLOR (LQW-IDX) EQUAL DP015-BLUE              
                              SET IN012-FROM-TSUMINV (IN012-IDX) TO TRUE        
                           END-IF                                               
                       END-IF                                                   
                                                                                
                       SET IN012-IDX UP BY 1                                    
                       IF  IN012-IDX > IN012-MAX-ITEMS                          
                           PERFORM 7205-WRITE-SEL-QUEUE                         
                           ADD +1        TO IN001-NUMBER-OF-TS-ITEMS            
                           INITIALIZE IN012-SELECTED-ITEM-ARRAY                 
                           SET IN012-IDX TO  1                                  
                       END-IF                                                   
                   END-IF                                                       
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
      *----------------------------------------------------------------*        
                                                                                
       7205-WRITE-SEL-QUEUE.                                                    
                                                                                
           MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
                                         TO IN012-SELECTED-ITEMS.               
                                                                                
           EXEC CICS                                                            
                WRITEQ TS                                                       
                    QUEUE (ASC-SEL-QUEUE-NAME)                                  
                    FROM  (IN012-SELECTED-SKUS)                                 
                    ITEM  (PV-SEL-QUEUE-ITEM)                                   
                    RESP  (PV-CICS-RESP)                                        
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '7205-WRITE-SEL-QUEUE'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
                                         TO DP013-MESSAGE-TEXT(1)               
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *  WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION,  *        
      *  SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT  *        
      *  THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO     *        
      *  THE USER.                                                     *        
      *----------------------------------------------------------------*        
                                                                                
       7500-REFRESH-PANEL-FROM-TEMP.                                            
                                                                                
           IF  ASC-UPDATE-TSQ                                                   
               SET ASC-NO-TSQ-UPDATE TO TRUE                                    
               MOVE ZERO                 TO PV-SAVE-ITEM                        
                                            PV-SEL-QUEUE-ITEM                   
               MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
                                         TO PV-HOLD-SELECTED-ITEMS              
               PERFORM 7600-UPDATE-TSQ                                          
                   UNTIL PV-ITEMS-PROCESSED >                                   
                                           ASC-NUMBER-OF-SELECTED-ITEMS         
               PERFORM 7510-DELETE-SEL-QUEUE                                    
               INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
                          ASC-BLOCK-ENTRIES (2)                                 
                                                                                
               DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                         
                      GIVING PV-BLOCK-NUMBER                                    
                      REMAINDER PV-REMAINDER                                    
               IF  PV-REMAINDER > ZERO                                          
                   ADD +1     TO PV-BLOCK-NUMBER                                
               END-IF                                                           
                                                                                
               MOVE +1        TO ASC-BLK-SUB                                    
               PERFORM 4410-READ-TEMP-STORAGE                                   
               ADD +1         TO ASC-BLK-SUB                                    
                                 PV-BLOCK-NUMBER                                
               PERFORM 4410-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
      *----------------------------------------------------------------*        
                                                                                
       7510-DELETE-SEL-QUEUE.                                                   
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE (ASC-SEL-QUEUE-NAME)                                   
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
              (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-NO-ROLLBACK TO TRUE                                    
               MOVE '7510-DELETE-SEL-QUEUE'                                     
                              TO DP013-PARAGRAPH                                
               MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
                              TO DP013-MESSAGE-TEXT (1)                         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
      *    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
      *    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
      *    THE TEMP QUEUE.                                             *        
      *----------------------------------------------------------------*        
                                                                                
       7600-UPDATE-TSQ.                                                         
                                                                                
           ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
           PERFORM 7605-READ-SEL-QUEUE.                                         
           PERFORM                                                              
               VARYING IN012-IDX                                                
               FROM 1 BY 1                                                      
               UNTIL ((PV-ITEMS-PROCESSED >                                     
                                    PV-HOLD-SELECTED-ITEMS)                     
               OR (IN012-IDX GREATER THAN IN012-MAX-ITEMS))                     
               IF  IN012-ACTION-SW (IN012-IDX) > SPACE                          
                   PERFORM 7610-PROCESS-ITEMS                                   
               END-IF                                                           
               ADD +1                TO PV-ITEMS-PROCESSED                      
           END-PERFORM.                                                         
           PERFORM 7611-WRITE-LIST-QUEUE.                                       
      *----------------------------------------------------------------*        
      *    READ THE LIST QUEUE RECORD.                                 *        
      *----------------------------------------------------------------*        
                                                                                
       7605-READ-SEL-QUEUE.                                                     
                                                                                
           EXEC CICS                                                            
                READQ TS                                                        
                    QUEUE (ASC-SEL-QUEUE-NAME)                                  
                    INTO  (IN012-SELECTED-SKUS)                                 
                    ITEM  (PV-SEL-QUEUE-ITEM)                                   
                    RESP  (PV-CICS-RESP)                                        
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO  TRUE                               
               SET DP013-NO-ROLLBACK     TO  TRUE                               
               MOVE '7605-READ-SEL-QUEUE'                                       
                                         TO  DP013-PARAGRAPH                    
               MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
                                         TO  DP013-MESSAGE-TEXT(1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
      *    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
      *    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
      *----------------------------------------------------------------*        
                                                                                
       7610-PROCESS-ITEMS.                                                      
                                                                                
           COMPUTE PV-ITEM = 1 + ((IN012-ITEM (IN012-IDX) - 1)                  
                           / PC-ITEMS-PER-PANEL).                               
                                                                                
           IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
               IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
                   PERFORM 7611-WRITE-LIST-QUEUE                                
               END-IF                                                           
               MOVE PV-ITEM          TO PV-SAVE-ITEM                            
                                                                                
               PERFORM 7612-READ-LIST-QUEUE                                     
           END-IF.                                                              
                                                                                
           COMPUTE PV-SUB = (IN012-ITEM (IN012-IDX)                             
                          - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
           SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
           MOVE LQW-SELECTED-SW (PV-SUB)                                        
                                     TO LQW-TEMP-SELECTED-SW (PV-SUB).          
           EVALUATE TRUE                                                        
               WHEN IN012-INQ (IN012-IDX)                                       
                    MOVE PC-INQ      TO LQW-ACTION (PV-SUB)                     
                                                                                
               WHEN IN012-UPD (IN012-IDX)                                       
                    MOVE PC-UPD      TO LQW-ACTION (PV-SUB)                     
                                                                                
               WHEN IN012-DEL (IN012-IDX)                                       
                    MOVE PC-DEL      TO LQW-ACTION (PV-SUB)                     
                                                                                
               WHEN IN012-ERR (IN012-IDX)                                       
                    MOVE PC-ERR      TO LQW-ACTION (PV-SUB)                     
           END-EVALUATE.                                                        
                                                                                
           SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
                                                                                
      *----------------------------------------------------------------*        
      *    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *        
      *----------------------------------------------------------------*        
                                                                                
       7611-WRITE-LIST-QUEUE.                                                   
                                                                                
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
                   ITEM  (PV-SAVE-ITEM)                                         
                   REWRITE                                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '7611-WRITE-LIST-QUEUE'                                     
                                     TO  DP013-PARAGRAPH                        
               MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
                                     TO  DP013-MESSAGE-TEXT(1)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      /                                                                         
      *----------------------------------------------------------------*        
      *    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
      *----------------------------------------------------------------*        
                                                                                
       7612-READ-LIST-QUEUE.                                                    
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
                   ITEM  (PV-ITEM)                                              
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND        TO TRUE                              
               SET DP013-NO-ROLLBACK       TO TRUE                              
               MOVE '7612-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
               MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
                                           TO DP013-MESSAGE-TEXT(1)             
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
W30692                                                                          
W30692/******************************************************************       
W30692* 8XXX-USE-STORE-DEFAULT-PRTR.                                            
W30692*******************************************************************       
W30692 8XXX-USE-STORE-DEFAULT-PRTR.                                             
W30692                                                                          
W30692     MOVE DP026-LOCATION-NBR TO PV-EDIT-LOC-NBR.                          
W30692     IF PV-EDIT-LOC-NBR-P1 IS EQUAL TO ZERO                               
W30692         STRING 'TKST' DELIMITED BY SIZE                                  
W30692                PV-EDIT-LOC-NBR-P2 DELIMITED BY SIZE                      
W30692                            INTO PV-USER-USER-CODE                        
W30692     ELSE                                                                 
W30692         STRING 'TKS' DELIMITED BY SIZE                                   
W30692                PV-EDIT-LOC-NBR DELIMITED BY SIZE                         
W30692                            INTO PV-USER-USER-CODE                        
W30692     END-IF.                                                              
W30692                                                                          
W30692     MOVE PV-USER-USER-CODE TO USER-USER-CODE.                            
W30692                                                                          
W30692     EXEC SQL                                                             
W30692         SELECT DFLT_PRTR_ID                                              
W30692         INTO  :USER-DFLT-PRTR-ID                                         
W30692         FROM  TUSER                                                      
W30692         WHERE USER_CODE = :USER-USER-CODE                                
W30692         WITH UR                                                          
W30692     END-EXEC.                                                            
W30692                                                                          
W30692     EVALUATE TRUE                                                        
W30692         WHEN SQLCODE = ZERO                                              
W30692             IF USER-DFLT-PRTR-ID GREATER SPACES                          
W30692                 MOVE USER-DFLT-PRTR-ID                                   
W30692                               TO JL-JCL-INK950-PRTDEST                   
W30692                 MOVE 'Y'      TO JL-JCL-INK950-PRT-IND                   
W30692             ELSE                                                         
W30692                 MOVE SPACES   TO JL-JCL-INK950-PRTDEST                   
W30692                 MOVE 'N'      TO JL-JCL-INK950-PRT-IND                   
W30692             END-IF                                                       
W30692         WHEN SQLCODE = +100                                              
W30692             MOVE SPACES       TO JL-JCL-INK950-PRTDEST                   
W30692             MOVE 'N'          TO JL-JCL-INK950-PRT-IND                   
W30692         WHEN OTHER                                                       
W30692             MOVE SPACES       TO JL-JCL-INK950-PRTDEST                   
W30692             MOVE 'N'          TO JL-JCL-INK950-PRT-IND                   
W30692             MOVE '8XXX-USE-STORE-DEFAULT-PRTR'                           
W30692                               TO DP013-PARAGRAPH                         
W30692             MOVE 'SELECT DFLT-PRT-ID'                                    
W30692                               TO DP013-MESSAGE-TEXT (1)                  
W30692             MOVE SQLCA        TO DP013-SQLCA                             
W30692             MOVE 'TUSER  '    TO DP013-DB2-TABLE-NAME (1)                
W30692             SET DP013-DB2-ABEND                                          
W30692                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
W30692             PERFORM DP013-0000-PROCESS-ABEND                             
W30692     END-EVALUATE.                                                        
W30692                                                                          
      ******************************************************************        
      ** FORMAT AND SUBMIT BATCH JOB.                                 **        
      ******************************************************************        
       8000-FORMAT-AND-SUBMIT.                                                  
                                                                                
           PERFORM 9500-ENQUEUE-INTERNAL-READER.                                
                                                                                
           EXEC CICS                                                            
             ASSIGN                                                             
               APPLID (PV-CICS-ID)                                              
           END-EXEC.                                                            
                                                                                
           IF PV-CICS-REGION-PRODUCTION                                         
               MOVE 'INK950'     TO JL-JCL-INK950-JOBNAME                       
               MOVE 'Q'          TO JL-JCL-INK950-JOBCLASS                      
               MOVE 'P'          TO JL-JCL-INK950-MSGCLASS                      
               MOVE SPACES       TO JL-JCL-INK950-NOTIFY-OPT                    
           ELSE                                                                 
               MOVE DP020-USERID TO JL-JCL-INK950-JOBNAME                       
                                    JL-JCL-TEST-USER                            
               MOVE 'P'          TO JL-JCL-INK950-JOB-UNQ                       
               MOVE 'A'          TO JL-JCL-INK950-JOBCLASS                      
               MOVE 'X'          TO JL-JCL-INK950-MSGCLASS                      
               MOVE JL-JCL-TEST-NOTIFY TO JL-JCL-INK950-NOTIFY-OPT              
           END-IF.                                                              
           MOVE JL-JCL-INK950-LINE-1 TO TD-JCL-LINE.                            
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-LINE-2 TO TD-JCL-LINE.                            
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-LINE-3 TO TD-JCL-LINE.                            
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-CMT-LONG      TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE DP020-USERID TO JL-JCL-INK950-CMT-USRID                         
           MOVE PV-CICS-ID   TO JL-JCL-INK950-CMT-REGION.                       
           MOVE JL-JCL-INK950-CMT-USER-INFO TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-CMT-1         TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-CMT-2         TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-CMT-3         TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
      *    CONTROL CARD INFORMATION (JL-JCL-INK950-LINE-14)                     
           MOVE IN001-STORE-NBR         TO  JL-JCL-INK950-STORE.                
           MOVE IN001-AREA-NBR          TO  JL-JCL-INK950-AREA.                 
           MOVE ASC-KEY-SKU-NBR         TO  PV-PRINT-SKU.                       
           MOVE PV-PRINT-SKU-X          TO  JL-JCL-INK950-SKU.                  
           MOVE ASC-KEY-PR-AMT          TO  PV-PRINT-UNIT-RETAIL.               
           MOVE PV-PRINT-UNIT-RETAIL    TO  PV-PRINT-RETAIL.                    
           MOVE PV-PRINT-RETAIL-X       TO  JL-JCL-INK950-RETAIL.               
                                                                                
W26600     EXEC CICS ASSIGN                                                     
W26600         USERID(JL-JCL-INK950-USERID)                                     
W26600     END-EXEC.                                                            
W26600                                                                          
           MOVE JL-JCL-INK950-CC-INFO       TO JL-JCL-INK950-CMT-OPTION.        
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-CMT-4         TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-CMT-LONG      TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           IF PV-CICS-REGION-PRODUCTION                                         
               MOVE JL-JCL-INK950-LINE-10       TO TD-JCL-LINE                  
               PERFORM 8100-WRITE-TD-LINE                                       
           ELSE                                                                 
               MOVE JL-JCL-TEST-LINE-10         TO TD-JCL-LINE                  
               PERFORM 8100-WRITE-TD-LINE                                       
               MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE                  
               PERFORM 8100-WRITE-TD-LINE                                       
                                                                                
               MOVE JL-JCL-TEST-LINE-11A        TO TD-JCL-LINE                  
               PERFORM 8100-WRITE-TD-LINE                                       
               MOVE JL-JCL-TEST-LINE-11B        TO TD-JCL-LINE                  
               PERFORM 8100-WRITE-TD-LINE                                       
               MOVE JL-JCL-TEST-LINE-11C        TO TD-JCL-LINE                  
               PERFORM 8100-WRITE-TD-LINE                                       
           END-IF.                                                              
                                                                                
           MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-LINE-11       TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
W30692*    EXEC SQL                                                             
W30692*        SELECT DFLT_PRTR_ID                                              
W30692*        INTO  :USER-DFLT-PRTR-ID                                         
W30692*        FROM  TUSER                                                      
W30692*        WHERE USER_CODE = :DP020-USERID                                  
W30692*    END-EXEC.                                                            
W30692*    MOVE 'LOCAL'           TO JL-JCL-INK950-PRTDEST.                     
W30692*    MOVE 'N'               TO JL-JCL-INK950-PRT-IND.                     
W30692*    EVALUATE TRUE                                                        
W30692*        WHEN SQLCODE = ZERO                                              
W30692*            IF USER-DFLT-PRTR-ID NOT = SPACES                            
W30692*                MOVE USER-DFLT-PRTR-ID TO JL-JCL-INK950-PRTDEST          
W30692*                MOVE 'Y'               TO JL-JCL-INK950-PRT-IND          
W30692*            END-IF                                                       
W30692*        WHEN SQLCODE = +100                                              
W30692*            CONTINUE                                                     
W30692*        WHEN OTHER                                                       
W30692*            MOVE '8100-FORMAT-AND-SUBMIT'                                
W30692*                              TO DP013-PARAGRAPH                         
W30692*            MOVE 'SELECT DFLT-PRT-ID'                                    
W30692*                              TO DP013-MESSAGE-TEXT (1)                  
W30692*            MOVE SQLCA        TO DP013-SQLCA                             
W30692*            MOVE 'TUSER  '    TO DP013-DB2-TABLE-NAME (1)                
W30692*            SET DP013-DB2-ABEND                                          
W30692*                DP013-XCTL-DISPLAY-RESTART                               
W30692*                              TO TRUE                                    
W30692*            PERFORM DP013-0000-PROCESS-ABEND                             
W30692*    END-EVALUATE.                                                        
W30692*                                                                         
           MOVE JL-JCL-INK950-LINE-12       TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-LINE-13       TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
      *    CONTROL CARD INFORMATION (IN-STREAM DATA ON JOB CARD)                
      *                                     JL-JCL-INK950-UPC                   
           MOVE JL-JCL-INK950-CC-INFO       TO JL-JCL-INK950-REQ-OPTION.        
           MOVE JL-JCL-INK950-LINE-14       TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-LINE-EOD      TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-CMT-SHORT     TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           MOVE JL-JCL-INK950-LINE-EOF      TO TD-JCL-LINE.                     
           PERFORM 8100-WRITE-TD-LINE.                                          
                                                                                
           PERFORM 9600-DEQUEUE-INTERNAL-READER.                                
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE A JCL LINE TO THE TRANSIENT DATA QUEUE FOR THE INTERNAL *        
      *  READER.  ON ANY ERROR, DEQUEUE THE INTERNAL READER AND ABEND. *        
      ******************************************************************        
       8100-WRITE-TD-LINE.                                                      
           EXEC CICS                                                            
               WRITEQ TD                                                        
                   QUEUE(TD-QUEUE-NAME)                                         
                   FROM(TD-JCL-LINE)                                            
                   LENGTH(TD-JCL-LINE-LENGTH)                                   
                   RESP(PV-CICS-RESP)                                           
           END-EXEC.                                                            
                                                                                
           IF PV-CICS-RESP NOT = DFHRESP(NORMAL)                                
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '8100-WRITE-TD-LINE' TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO WRITE TO INTERNAL READER TD QUEUE'           
                                         TO DP013-MESSAGE-TEXT(1)               
               PERFORM 9600-DEQUEUE-INTERNAL-READER                             
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
      *----------------------------------------------------------------*        
                                                                                
       9100-READ-SEL-QUEUE.                                                     
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-FROM-QUEUE-NAME)                                  
                   INTO  (IN027-SELECTED-SKUS)                                  
                   ITEM  (ASC-SEL-ITEM)                                         
                   RESP  (PV-CICS-RESPONSE)                                     
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESPONSE EQUAL DFHRESP(NORMAL)                           
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-LOGIC-ABEND                                            
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9100-READ-SEL-QUEUE'                                       
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO READ THE FROM SELECTIONS TS QUEUE'           
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
      *----------------------------------------------------------------*        
                                                                                
       9200-REWRITE-SEL-QUEUE.                                                  
                                                                                
           EXEC CICS                                                            
                WRITEQ TS                                                       
                    QUEUE  (ASC-FROM-QUEUE-NAME)                                
                    FROM   (IN027-SELECTED-SKUS)                                
                    ITEM   (ASC-SEL-ITEM)                                       
                    RESP   (PV-CICS-RESPONSE)                                   
                    REWRITE                                                     
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESPONSE NOT EQUAL DFHRESP(NORMAL)                       
               SET DP013-CICS-ABEND                                             
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9200-REWRITE-SEL-QUEUE'                                    
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO REWRITE THE FROM SELECTIONS QUEUE'           
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      ******************************************************************        
      *  ENQUEUE THE TRANSIENT DATA QUEUE FOR THE INTERNAL READER.     *        
      *  THIS WILL PREVENT INTERLEAVING OF JCL SUBMITTED CONCURRENTLY  *        
      *  BY MULTIPLE USERS.  IF THE QUEUE IS BUSY, THIS PROGRAM WILL   *        
      *  WAIT TO ENQUEUE IT, BY NOT SPECIFICALLY HANDLING THE QBUSY    *        
      *  CONDITION.                                                    *        
      ******************************************************************        
       9500-ENQUEUE-INTERNAL-READER.                                            
           EXEC CICS                                                            
               ENQ                                                              
                   RESOURCE(TD-QUEUE-NAME)                                      
                   LENGTH(TD-QUEUE-NAME-LENGTH)                                 
                   RESP(PV-CICS-RESP)                                           
           END-EXEC.                                                            
                                                                                
           IF PV-CICS-RESP NOT = DFHRESP(NORMAL)                                
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9500-ENQUEUE-INTERNAL-READER'                              
                                         TO DP013-PARAGRAPH                     
               STRING 'ENQUEUE '            DELIMITED BY SIZE                   
                      TD-QUEUE-NAME         DELIMITED BY SIZE                   
                      ' (INTERNAL READER)'  DELIMITED BY SIZE                   
                   INTO DP013-MESSAGE-TEXT (1)                                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           SET PS-INTERNAL-READER-ENQUEUED TO TRUE.                             
                                                                                
      ******************************************************************        
      *  DEQUEUE THE TRANSIENT DATA QUEUE FOR THE INTERNAL READER.     *        
      *  THIS SHOULD BE DONE FOLLOWING A SUCCESSFUL REPORT REQUEST     *        
      *  SUBMISSION, A CANCELLATION OF THE CURRENT REPORT REQUEST, OR  *        
      *  ANY DETECTED ABENDS.  THIS WILL FREE UP THE INTERNAL READER   *        
      *  FOR THE NEXT USER.                                            *        
      ******************************************************************        
       9600-DEQUEUE-INTERNAL-READER.                                            
           SET PS-INTERNAL-READER-DEQUEUED TO TRUE.                             
                                                                                
           EXEC CICS                                                            
               DEQ                                                              
                   RESOURCE(TD-QUEUE-NAME)                                      
                   LENGTH(TD-QUEUE-NAME-LENGTH)                                 
                   RESP(PV-CICS-RESP)                                           
           END-EXEC.                                                            
                                                                                
           IF PV-CICS-RESP NOT = DFHRESP(NORMAL)                                
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9600-DEQUEUE-INTERNAL-READER'                              
                                         TO DP013-PARAGRAPH                     
               STRING 'DEQUEUE '            DELIMITED BY SIZE                   
                      TD-QUEUE-NAME         DELIMITED BY SIZE                   
                      ' (INTERNAL READER)'  DELIMITED BY SIZE                   
                   INTO DP013-MESSAGE-TEXT (1)                                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *    THE DB2 RESOURCE IS NOT AVAILABLE, SET THE ERROR MESSAGE    *        
      *    AND THE ERROR SWITCHES.                                     *        
      *----------------------------------------------------------------*        
                                                                                
       9900-SOFT-ABORT.                                                         
                                                                                
           SET PS-ERROR                TO  TRUE.                                
           SET DP020-MSG-FATAL         TO  TRUE.                                
           MOVE PC-TSYMSG-00551        TO  DP020-MSG-NUMBER.                    
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
           COPY DPPD013.                                                        
                                                                                
W30692*----------------------------------------------------------------*        
W30692*    RETRIEVE LOGON INFORMATION FOR DEFAULT PRINTER DESTINATION  *        
W30692*----------------------------------------------------------------*        
W30692     COPY DPPD026.                                                        
