      ***************************************************************** 00010000
       IDENTIFICATION DIVISION.                                         00020000
      ***************************************************************** 00030000
                                                                        00040000
       PROGRAM-ID.             SAKUT160.                                00050000
       AUTHOR.                 JIM HUNTER.                              00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            MARCH, 2022.                             00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ***************************************************************** 00110000
      *                                                                 00120000
      *  THIS PROGRAM CREATES A CSV FILE OF THE PREVIOUS WEEK'S         00130000
      *  MARKETPLACE RETURNS OF SKUS PURCHASED AT BRICK AND MORTAR      00131000
      *  STORES FOR THE FINANCE TEAM.                                   00132000
      *                                                                 00133000
      *  THE SAKUT150 PROGRAM WAS USED AS A STARTING POINT.             00140000
      *                                                                 00150000
      *   INPUT: INP01 - CONTAINS THE START AND END DATES FOR THE       00160000
      *                  PREVIOUS FISCAL WEEK.                          00170000
      *  OUTPUT: OUT01 - MARKETPLACE RETURNS FILE.                      00180000
      *                                                                 00190000
      ***************************************************************** 00200000
       ENVIRONMENT DIVISION.                                            00210000
      ***************************************************************** 00220000
                                                                        00230000
       CONFIGURATION SECTION.                                           00240000
                                                                        00250000
       SOURCE-COMPUTER.        IBM-3090.                                00260000
                                                                        00270000
       OBJECT-COMPUTER.        IBM-3090.                                00280000
                                                                        00290000
       INPUT-OUTPUT SECTION.                                            00300000
                                                                        00310000
       FILE-CONTROL.                                                    00320000
                                                                        00330000
           SELECT DATE-RANGE-CARD-FILE                                  00340000
               ASSIGN TO INP01.                                         00350000
                                                                        00360000
           SELECT MARKETPLACE-RETURNS-CSV-FILE                          00370000
               ASSIGN TO OUT01.                                         00380000
                                                                        00390000
      ******************************************************************00400000
       DATA DIVISION.                                                   00410000
      ******************************************************************00420000
                                                                        00430000
       FILE SECTION.                                                    00440000
                                                                        00450000
       FD  DATE-RANGE-CARD-FILE                                         00460000
           RECORDING MODE IS F                                          00470000
           BLOCK CONTAINS 0 RECORDS                                     00480000
           LABEL RECORDS ARE STANDARD.                                  00490000
                                                                        00500000
       01  DATE-RANGE-CARD-RECORD          PIC X(80).                   00510000
                                                                        00520000
       FD  MARKETPLACE-RETURNS-CSV-FILE                                 00530000
           RECORDING MODE IS F                                          00540000
           BLOCK CONTAINS 0  RECORDS.                                   00550000
                                                                        00560000
       01  MARKETPLACE-RETURNS-CSV-RECORD  PIC  X(100).                 00570000
                                                                        00580000
      ******************************************************************00590000
       WORKING-STORAGE SECTION.                                         00600000
      ******************************************************************00610000
       01  FILLER                            PIC X(50)  VALUE           00620000
           ' *** WORKING STORAGE STARTS HERE FOR SAKUT160 *** '.        00630000
                                                                        00640000
      ******************************************************************00650000
      * RECORD LAYOUT FOR INPUT DATE CARD (POS DATE)                    00660000
      ******************************************************************00670000
       01  CC-CONTROL-CARD.                                             00680000
           05  CC-CONTROL-CARD-DISPLAY.                                 00690000
               10  CC-START-DATE       PIC X(10)    VALUE SPACES.       00700000
               10  FILLER              PIC X        VALUE SPACE.        00710000
               10  CC-END-DATE         PIC X(10)    VALUE SPACES.       00720000
               10  FILLER              PIC X(59)    VALUE SPACE.        00730000
                                                                        00740000
      ******************************************************************00750000
      *  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)             00760000
      ******************************************************************00770000
           COPY DPWS500.                                                00780000
                                                                        00790000
       01  AC-ABEND-CODE                     PIC S9(04) VALUE ZERO      00800000
                                                        COMP SYNC.      00810000
           88  AC-CONTROL-CARD-ERROR                    VALUE +4003.    00820000
           88  AC-DB2-ERROR                             VALUE +4013.    00830000
           88  AC-CALENDAR-ROUTINE-ERROR                VALUE +4019.    00840000
                                                                        00850000
       01  PS-PROGRAM-SWITCHES.                                         00860000
           05  PS-END-OF-MKTPLC-RETURN-CSR   PIC X(01)  VALUE 'N'.      00870000
               88  PS-END-OF-MKTPLC-RETN-CSR-YES        VALUE 'Y'.      00880000
           05  PS-END-OF-CARD-FILE-SW        PIC X(01)  VALUE 'N'.      00890000
               88  PS-END-OF-CARD-FILE-YES              VALUE 'Y'.      00900000
                                                                        00940000
       01  PC-CONSTANTS.                                                00950000
           05  PC-NOT-FOUND                PIC S9(04)    VALUE +100     00960000
                                                         COMP SYNC.     00970000
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE          00980000
                                                         'SAKUT160'.    00990000
                                                                        01000000
       01  DK-DB2-KEYS.                                                 01010000
           05  DK-BEGIN-DATE-ISO       PIC X(10)    VALUE SPACES.       01020000
           05  DK-END-DATE-ISO         PIC X(10)    VALUE SPACES.       01030000
                                                                        01040000
       01  PV-MISC-FIELDS.                                              01050000
           05  PV-REC-CNT                  PIC  9(07)     VALUE ZERO.   01060000
           05  PV-PARAGRAPH-NAME           PIC  X(30)     VALUE SPACE.  01090000
           05  PV-OPERATION-MSG-1          PIC  X(40)     VALUE SPACE.  01100000
           05  PV-OPERATION-MSG-2          PIC  X(40)     VALUE SPACE.  01110000
           05  PV-DB2-OPERATION            PIC  X(30)     VALUE SPACE.  01120000
           05  PV-DPG52-LK-DATE-INPUT      PIC X(10)      VALUE SPACES. 01150000
                                                                        01160000
      ******************************************************************01170000
      * RECORD LAYOUT FOR MARKETPLACE RETURNS CSV FILE.                 01180000
      ******************************************************************01190000
       01  MKTPLC-HEADER-RECORD.                                        01200000
           05  MKTPLC-HDR-RETURN-STORE  PIC X(12) VALUE 'RETURN STORE'. 01210000
           05  FILLER                   PIC X     VALUE ','.            01220000
           05  MKTPLC-HDR-SALES-DATE    PIC X(10) VALUE 'SALES DATE'.   01230000
           05  FILLER                   PIC X     VALUE ','.            01240000
           05  MKTPLC-HDR-SELLER-PRICE  PIC X(12) VALUE 'SELLER PRICE'. 01270000
           05  FILLER                   PIC X     VALUE ','.            01280000
           05  MKTPLC-HDR-SALE-PRICE    PIC X(10) VALUE 'SALE PRICE'.   01281000
           05  FILLER                   PIC X     VALUE ','.            01282000
           05  MKTPLC-HDR-SALE-STORE    PIC X(10) VALUE 'SALE STORE'.   01290000
                                                                        01490000
       01  MKTPLC-DETAIL-RECORD.                                        01500000
           05  MKTPLC-DTL-RETURN-STORE  PIC 9(4).                       01510000
           05  FILLER                   PIC X     VALUE ','.            01520000
           05  MKTPLC-DTL-SALES-DATE    PIC X(10).                      01530000
           05  FILLER                   PIC X     VALUE ','.            01540000
           05  MKTPLC-DTL-SELLER-PRICE  PIC -$$$$$9.99.                 01550000
           05  FILLER                   PIC X     VALUE ','.            01560000
           05  MKTPLC-DTL-SALE-PRICE    PIC -$$$$$9.99.                 01570000
           05  FILLER                   PIC X     VALUE ','.            01580000
           05  MKTPLC-DTL-SALE-STORE    PIC 9(4).                       01590000
     **    05  FILLER                   PIC X     VALUE ','.            01780001
                                                                        01790000
                                                                        01800000
      ******************************************************************01810000
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                            01820000
      ******************************************************************01830000
           COPY DPWS004.                                                01840000
                                                                        01850000
      ******************************************************************01860000
      *  DB2 COMMUNICATION AREA.                                        01870000
      ******************************************************************01880000
           EXEC SQL                                                     01890000
                INCLUDE SQLCA                                           01900000
           END-EXEC.                                                    01910000
                                                                        01920000
      ******************************************************************01930000
      *  DB2 TABLE DB2PDBA.XST_LOC - LOCATION TABLE.                    01940000
      ******************************************************************01950000
           EXEC SQL                                                     01960000
                INCLUDE XST00001                                        01970000
           END-EXEC.                                                    01980000
                                                                        01990000
      ******************************************************************02630000
      *  DB2 TABLE DB2PDBA.TEPPART - SALES PARTITION TABLE.             02640000
      ******************************************************************02650000
           EXEC SQL                                                     02660000
                INCLUDE TEPPART                                         02670000
           END-EXEC.                                                    02680000
                                                                        02690000
      ******************************************************************02691000
      *  DB2 TABLE DB2PDBA.TEPTRN - TRANSACTION TABLE.                  02692000
      ******************************************************************02693000
           EXEC SQL                                                     02694000
                INCLUDE TEPTRN                                          02695000
           END-EXEC.                                                    02696000
                                                                        02697000
      ******************************************************************02698000
      *  DB2 TABLE DB2PDBA.TEPITM - ITEM TABLE.                         02699000
      ******************************************************************02699100
           EXEC SQL                                                     02699200
                INCLUDE TEPITM                                          02699300
           END-EXEC.                                                    02699400
                                                                        02699500
      ******************************************************************02699600
      *  DB2 TABLE DB2PDBA.TEPIRT - ITEM RETURN TABLE.                  02699700
      ******************************************************************02699800
           EXEC SQL                                                     02699900
                INCLUDE TEPIRT                                          02700000
           END-EXEC.                                                    02700100
                                                                        02700200
      ******************************************************************02701000
      *  MKTPLC-RETURN-CSR - GET MARKEPLACE RETURNS FOR SKUS SOLD AT    02710000
      *                      BRICK AND MORTAR STORES.                   02711000
      ******************************************************************02720000
           EXEC SQL                                                     02730000
              DECLARE MKTPLC-RETURN-CSR  CURSOR FOR                     02740000
                SELECT ITM.STR_NBR                                      02750000
                      ,TRN.SLS_DTE                                      02760000
                      ,SUM(ITM.CUST_CHRGD_AMT)                          02770000
                      ,SUM(ITM.NET_CHRGD_AMT)                           02780000
                      ,COALESCE(IRT.ORIG_STR_NBR, 0)                    02790000
                      ,COALESCE(LOC.E_BUS_IND, 'Y')                     02800000
                  FROM TEPTRN  TRN                                      02820000
                      ,TEPPART PRT                                      02830000
                      ,TEPITM  ITM                                      02850000
                       LEFT OUTER JOIN    TEPIRT  IRT                   02860000
                         ON ITM.PARTN_NBR        = IRT.PARTN_NBR        02870000
                        AND ITM.STR_NBR          = IRT.STR_NBR          02880000
                        AND ITM.RGST_ID          = IRT.RGST_ID          02890000
                        AND ITM.TRN_NBR          = IRT.TRN_NBR          02900000
                        AND ITM.STRT_TM          = IRT.STRT_TM          02910000
                        AND ITM.SLS_CORR_SEQ_NBR = IRT.SLS_CORR_SEQ_NBR 02911000
                        AND ITM.LNE_ITM_SEQ_NBR  = IRT.LNE_ITM_SEQ_NBR  02912000
                          LEFT OUTER JOIN XST_LOC LOC                   02914000
                            ON IRT.ORIG_STR_NBR  = LOC.LOC_NBR          02915000
                 WHERE PRT.SLS_DTE BETWEEN :DK-BEGIN-DATE-ISO           02920000
                                       AND :DK-END-DATE-ISO             02930000
                   AND PRT.PARTN_NBR        = TRN.PARTN_NBR             02930100
                   AND TRN.TRN_TYP_CDE     IN ('11', '12')              02930200
                   AND TRN.TRN_STAT_CDE    IN ('Z','L','V','R')         02931000
                   AND TRN.VOID_ABRT_CDE   IN ('N','U')                 02932000
                   AND ITM.PARTN_NBR        = TRN.PARTN_NBR             02936000
                   AND ITM.STR_NBR          = TRN.STR_NBR               02937000
                   AND ITM.RGST_ID          = TRN.RGST_ID               02938000
                   AND ITM.TRN_NBR          = TRN.TRN_NBR               02939000
                   AND ITM.STRT_TM          = TRN.STRT_TM               02939100
                   AND ITM.SLS_CORR_SEQ_NBR = TRN.SLS_CORR_SEQ_NBR      02939200
                   AND ITM.DEPT_NBR         = '985'                     02939300
                   AND ITM.LIV_RSLU_CDE    IN ('+','-')                 02939400
                   AND LOC.E_BUS_IND        = 'N'                       02939500
              GROUP BY ITM.STR_NBR                                      03020000
                      ,TRN.SLS_DTE                                      03030000
                      ,IRT.ORIG_STR_NBR                                 03040000
                      ,LOC.E_BUS_IND                                    03050000
              ORDER BY ITM.STR_NBR                                      03060000
                      ,TRN.SLS_DTE                                      03070000
                      ,IRT.ORIG_STR_NBR                                 03080000
                      ,LOC.E_BUS_IND                                    03090000
              WITH UR                                                   03091000
           END-EXEC.                                                    03100000
                                                                        03110000
      ***************************************************************** 03120000
       PROCEDURE DIVISION.                                              03130000
      ******************************************************************03140000
      * 0000-MAINLINE-PARAGRAPH                                         03150000
      *      PRODUCE SALES AUDIT CORRECTIONS FILE                       03160000
      ******************************************************************03170000
       0000-MAINLINE-PARAGRAPH.                                         03180000
                                                                        03190000
           PERFORM 1000-INITIALIZATION.                                 03200000
                                                                        03210000
           PERFORM 2000-OPEN-MKTPLC-RETURN-CSR.                         03220000
                                                                        03230000
           PERFORM 2200-FETCH-MKTPLC-RETURN-CSR                         03240000
             UNTIL PS-END-OF-MKTPLC-RETN-CSR-YES.                       03250000
                                                                        03260000
           PERFORM 2400-CLOSE-MKTPLC-RETURN-CSR.                        03270000
                                                                        03280000
           DISPLAY 'SAKUT160 OUTPUT RECORDS INCLUDING HEADER = '        03290000
                    PV-REC-CNT.                                         03300000
                                                                        03310000
           STOP RUN.                                                    03320000
                                                                        03330000
      ******************************************************************03340000
       1000-INITIALIZATION.                                             03350000
      *     OPEN FILES, PROCESS CONTROL CARD, FORMAT REPORT HEADINGS.   03360000
      ******************************************************************03370000
                                                                        03380000
           OPEN INPUT  DATE-RANGE-CARD-FILE                             03390000
                OUTPUT MARKETPLACE-RETURNS-CSV-FILE.                    03400000
                                                                        03410000
           PERFORM 1100-PROCESS-CONTROL-CARD.                           03420000
                                                                        03430000
           PERFORM 1200-WRITE-HEADER-RECORD.                            03440000
                                                                        03450000
      ******************************************************************03460000
       1100-PROCESS-CONTROL-CARD.                                       03470000
      *     READ CONTROL CARD TO GET START AND END DATES.               03480000
      *     CALL THE DATE ROUTINE TO VALIDATE BOTH.                     03490000
      ******************************************************************03500000
                                                                        03510000
           PERFORM 1110-READ-DATE-RANGE-CARD-FILE.                      03520000
           CLOSE DATE-RANGE-CARD-FILE.                                  03530000
           IF PS-END-OF-CARD-FILE-YES                                   03540000
               MOVE '1100-PROCESS-CONTROL-CARD'  TO PV-PARAGRAPH-NAME   03550000
               MOVE 'NO CONTROL CARD TO PROCESS' TO PV-OPERATION-MSG-1  03560000
               SET AC-CONTROL-CARD-ERROR TO TRUE                        03570000
               PERFORM 9999-ABEND                                       03580000
           END-IF.                                                      03590000
           DISPLAY PC-CURRENT-PROGRAM-NAME                              03600000
                   ' - CONTROL CARD = (' CC-CONTROL-CARD-DISPLAY  ')'.  03610000
                                                                        03620000
      ******************************************************************03630000
       1110-READ-DATE-RANGE-CARD-FILE.                                  03640000
      *     READ DATE CONTROL CARD.                                     03650000
      *     CALL THE DATE ROUTINE TO VALIDATE START AND END DATES.      03660000
      ******************************************************************03670000
                                                                        03680000
           READ DATE-RANGE-CARD-FILE INTO CC-CONTROL-CARD               03690000
              AT END                                                    03700000
                 SET PS-END-OF-CARD-FILE-YES TO TRUE                    03710000
              NOT AT END                                                03720000
                  MOVE CC-START-DATE        TO PV-DPG52-LK-DATE-INPUT   03730000
                  PERFORM 1125-VALIDATE-INPUT-DATE                      03740000
                  MOVE CC-START-DATE        TO DK-BEGIN-DATE-ISO        03750000
                                                                        03760000
                  MOVE CC-END-DATE          TO PV-DPG52-LK-DATE-INPUT   03770000
                  PERFORM 1125-VALIDATE-INPUT-DATE                      03780000
                  MOVE CC-END-DATE          TO DK-END-DATE-ISO.         03790000
                                                                        03800000
                                                                        03810000
      ******************************************************************03820000
      * CALL KOHLS CALENDAR ROUTINE:                                    03830000
      *     PASS: CONTROL CARD POS DATE (CCYY-MM-DD FORMAT).            03840000
      *   RETURN: ACTUAL CALENDAR DB2 ISO DATE (CCYY-MM-DD FORMAT).     03850000
      ******************************************************************03860000
       1125-VALIDATE-INPUT-DATE.                                        03870000
                                                                        03880000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03890000
                                                                        03900000
           SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   03910000
           SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   03920000
           SET  DPG52-LK-DTE-ISO-FMT         TO TRUE.                   03930000
                                                                        03940000
           MOVE PV-DPG52-LK-DATE-INPUT       TO DPG52-LK-DATE-INPUT.    03950000
                                                                        03960000
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    03970000
                                                   DPG54 DPG55 DPG56.   03980000
           EVALUATE TRUE                                                03990000
               WHEN DPG54-SEVERE-ERROR                                  04000000
               WHEN DPG54-DATE-INVALID                                  04010000
                   MOVE '1125-VALIDATE-INPUT-DATE'                      04020000
                                             TO PV-PARAGRAPH-NAME       04030000
                   MOVE 'ERROR CONVERTING INPUT CARD DATE'              04040000
                                             TO PV-OPERATION-MSG-1      04050000
                   SET AC-CALENDAR-ROUTINE-ERROR                        04060000
                                             TO TRUE                    04070000
                   MOVE DPG54-ERROR-MESSAGE                             04080000
                                             TO PV-OPERATION-MSG-2      04090000
                   PERFORM 9999-ABEND                                   04100000
           END-EVALUATE.                                                04110000
                                                                        04120000
                                                                        04130000
      ******************************************************************04140000
       1200-WRITE-HEADER-RECORD.                                        04150000
      ******************************************************************04160000
                                                                        04170000
           WRITE MARKETPLACE-RETURNS-CSV-RECORD                         04180000
               FROM MKTPLC-HEADER-RECORD.                               04190000
                                                                        04200000
           ADD +1   TO PV-REC-CNT.                                      04210000
                                                                        04220000
      ******************************************************************04230000
       2000-OPEN-MKTPLC-RETURN-CSR.                                     04240000
      ******************************************************************04260000
                                                                        04270000
           EXEC SQL                                                     04280000
                OPEN MKTPLC-RETURN-CSR                                  04290000
           END-EXEC.                                                    04300000
                                                                        04310000
           IF NOT SQLCODE = ZERO                                        04320000
              MOVE '2000-OPEN-MKTPLC-RETURN-CSR' TO PV-PARAGRAPH-NAME   04330000
              MOVE SQLCODE                       TO PV-DB2-OPERATION    04340000
              PERFORM 9100-SQL-ABEND                                    04350000
           END-IF.                                                      04360000
                                                                        04370000
      ******************************************************************04380000
       2200-FETCH-MKTPLC-RETURN-CSR.                                    04390000
      ******************************************************************04400000
                                                                        04410000
           EXEC SQL                                                     04420000
                FETCH MKTPLC-RETURN-CSR                                 04430000
                 INTO :EPITM-STR-NBR                                    04450000
                    , :EPTRN-SLS-DTE                                    04540000
                    , :EPITM-CUST-CHRGD-AMT                             04550000
                    , :EPITM-NET-CHRGD-AMT                              04560000
                    , :EPIRT-ORIG-STR-NBR                               04570000
                    , :XST00001-E-BUS-IND                               04580000
           END-EXEC.                                                    04581000
                                                                        04590000
           EVALUATE TRUE                                                04600000
              WHEN SQLCODE = ZERO                                       04610000
                   PERFORM 6000-FORMAT-DETAIL-RECORD                    04700000
              WHEN SQLCODE = PC-NOT-FOUND                               04710000
                   SET PS-END-OF-MKTPLC-RETN-CSR-YES TO TRUE            04720000
              WHEN OTHER                                                04730000
                MOVE '2000-FETCH-MKTPLC-RETURN-CSR' TO PV-PARAGRAPH-NAME04740000
                MOVE SQLCODE                        TO PV-DB2-OPERATION 04750000
                PERFORM 9100-SQL-ABEND                                  04760000
           END-EVALUATE.                                                04770000
                                                                        04780000
      ******************************************************************04790000
       2400-CLOSE-MKTPLC-RETURN-CSR.                                    04800000
      ******************************************************************04810000
                                                                        04820000
           EXEC SQL                                                     04830000
                CLOSE MKTPLC-RETURN-CSR                                 04840000
           END-EXEC.                                                    04850000
                                                                        04860000
           IF NOT SQLCODE = ZERO                                        04870000
              MOVE '2400-CLOSE-MKTPLC-RETURN-CSR' TO PV-PARAGRAPH-NAME  04880000
              MOVE SQLCODE                        TO PV-DB2-OPERATION   04890000
              PERFORM 9100-SQL-ABEND                                    04900000
           END-IF.                                                      04910000
                                                                        04920000
                                                                        06210000
      ******************************************************************06220000
       6000-FORMAT-DETAIL-RECORD.                                       06230000
      *     FORMAT MARKETPLACE RETURNS RECORD.                          06240000
      ******************************************************************06250000
                                                                        06260000
           MOVE EPITM-STR-NBR            TO MKTPLC-DTL-RETURN-STORE     06270000
           MOVE EPTRN-SLS-DTE            TO MKTPLC-DTL-SALES-DATE       06280000
           MOVE EPITM-CUST-CHRGD-AMT     TO MKTPLC-DTL-SELLER-PRICE     06450000
           MOVE EPITM-NET-CHRGD-AMT      TO MKTPLC-DTL-SALE-PRICE       06452000
           MOVE EPIRT-ORIG-STR-NBR       TO MKTPLC-DTL-SALE-STORE       06454000
                                                                        06460000
           PERFORM 8200-WRITE-DETAIL-RECORD.                            06470000
                                                                        06480000
                                                                        06960000
      ******************************************************************06970000
       8200-WRITE-DETAIL-RECORD.                                        06980000
      *     WRITE REPORT HEADINGS OTHER THAN THE FIRST LINE.            06990000
      ******************************************************************07000000
                                                                        07010000
           WRITE MARKETPLACE-RETURNS-CSV-RECORD                         07031000
               FROM MKTPLC-DETAIL-RECORD.                               07032000
                                                                        07040000
           ADD +1   TO PV-REC-CNT.                                      07050000
                                                                        07060000
                                                                        07070000
      ******************************************************************07080000
       9100-SQL-ABEND.                                                  07090000
      *     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.07100000
      ******************************************************************07110000
           DISPLAY '** ABEND     **'.                                   07120000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        07130000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              07140000
           DISPLAY '** SQLCODE   ** = ' PV-DB2-OPERATION.               07150000
           DISPLAY '** STORE NBR ** = ' EPTRN-STR-NBR.                  07160000
           DISPLAY '** REGISTER  ** = ' EPTRN-RGST-ID.                  07170000
           DISPLAY '** TRAN NBR  ** = ' EPTRN-TRN-NBR.                  07180000
                                                                        07190000
      ******************************************************************07200000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     07210000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      07220000
      ******************************************************************07230000
           COPY DPPD004.                                                07240000
                                                                        07250000
           SET AC-DB2-ERROR TO TRUE.                                    07260000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         07270000
                                                                        07280000
      ******************************************************************07290000
       9999-ABEND.                                                      07300000
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  07310000
      ******************************************************************07320000
           DISPLAY '** ABEND           **'.                             07330000
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  07340000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        07350000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       07360000
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       07370000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         07380000
      ******************************************************************07390000
      ******************************************************************07400000
