/* REXX */                                                                      
/* RMKRX011 - Prepare Infopac Summary XREF records for all dynamic    */        
/*      distribution types tied to the Stores file for store and      */        
/*      district locations, and store manager and all district roles  */        
/*      with a single person fulfilling it.  The Stores file has a    */        
/*      requirement to list all the district roles before any store   */        
/*      roles for the district, and that each district will actively  */        
/*      have a district manager and a district loss prevention        */        
/*      manager defined.  The store manager record will be used to    */        
/*      generate the generic store distribution key, the              */        
/*      store/district role keys, and district/store key.             */        
/**********************************************************************/        
/* Set values for constants.                                          */        
/**********************************************************************/        
CURRPGM = ' RMKRX011 - '                                                        
STORES_FILE_END_OF_FILE = 'Y'                                                   
STORES_FILE_NOT_END_OF_FILE = 'N'                                               
COMMENT_LINE = '*'                                                              
XREF_TYPE_DIST_MANAGER = 'DISTMGR'                                              
XREF_TYPE_DIST_LP_MANAGER = 'DISTLPM'                                           
XREF_TYPE_DIST_STORE = 'DISTSTR'                                                
XREF_TYPE_STORE_MANAGER = 'STORMGR'                                             
XREF_TYPE_STORE = 'STORE__'                                                     
XREF_TYPE_STORE_LP_MANAGER = 'STORLPM'                                          
XREF_TYPE_STORE_DIST_MANAGER = 'STORDMG'                                        
XREF_TYPE_STORE_DIST_LP_MANAGER = 'STORDLP'                                     
UNKNOWN_USERID = 'TK?     '                                                     
STORE_NUMBER_UNDER_1000 = '0'                                                   
STORE_USERID_PFX_UNDER_1000 = 'TKST'                                            
STORE_USERID_PFX_OVER_1000 = 'TKS'                                              
                                                                                
/**********************************************************************/        
/* Initialize local variables.                                        */        
/**********************************************************************/        
STORES_FILE_EOF = STORES_FILE_NOT_END_OF_FILE                                   
CURRENT_DIST_NBR = ''                                                           
DIST_MANAGER_USERID = ''                                                        
DIST_LP_MANAGER_USERID = ''                                                     
STORE_DEFAULT_USERID = ''                                                       
                                                                                
/**********************************************************************/        
/* Program mainline.                                                  */        
/**********************************************************************/        
SAY CURRPGM                                                                     
CALL READ_STORES_RECORD                                                         
DO WHILE (STORES_FILE_EOF = STORES_FILE_NOT_END_OF_FILE)                        
    PARSE PULL INP01_RECORD                                                     
    IF (SUBSTR(INP01_RECORD,1,1) ^= COMMENT_LINE) THEN DO                       
        STORES_DIST_NBR = SUBSTR(INP01_RECORD,1,4)                              
        STORES_STORE_NBR = SUBSTR(INP01_RECORD,7,4)                             
        STORES_XREF_TYPE = SUBSTR(INP01_RECORD,13,7)                            
        STORES_LOC_NAME = SUBSTR(INP01_RECORD,23,18)                            
        STORES_USERID = SUBSTR(INP01_RECORD,42,8)                               
        STORES_RECIPIENT_NAME = SUBSTR(INP01_RECORD,51,30)                      
/*                                                                    */        
/*   Reset the current associated district recipients at change in    */        
/*   district.                                                        */        
/*                                                                    */        
        IF (STORES_DIST_NBR ^= CURRENT_DIST_NBR) THEN DO                        
            CURRENT_DIST_NBR = STORES_DIST_NBR                                  
            DIST_MANAGER_USERID = UNKNOWN_USERID                                
            DIST_LP_MANAGER_USERID = UNKNOWN_USERID                             
        END                                                                     
/*                                                                    */        
/*   Build the default store userid.                                  */        
/*                                                                    */        
        IF (SUBSTR(STORES_STORE_NBR,1,1) = STORE_NUMBER_UNDER_1000) THEN        
            STORE_DEFAULT_USERID = STORE_USERID_PFX_UNDER_1000 ||,              
                      SUBSTR(STORES_STORE_NBR,2,3) || ' '                       
        ELSE                                                                    
            STORE_DEFAULT_USERID = STORE_USERID_PFX_OVER_1000 ||,               
                      STORES_STORE_NBR || ' '                                   
        IF (STORES_XREF_TYPE = XREF_TYPE_DIST_MANAGER) THEN DO                  
            DIST_MANAGER_USERID = STORES_USERID                                 
            SUM_XREF_RECORD = STORES_XREF_TYPE || '  ' ||,                      
                              LEFT(STORES_DIST_NBR,10) ||,                      
                              STORES_USERID || '            ' ||,               
                              STORES_STORE_NBR || '                ' ||,        
                              STORES_RECIPIENT_NAME                             
            CALL WRITE_SUM_XREF_RECORD                                          
        END                                                                     
        IF (STORES_XREF_TYPE = XREF_TYPE_DIST_LP_MANAGER) THEN DO               
            DIST_LP_MANAGER_USERID = STORES_USERID                              
            SUM_XREF_RECORD = STORES_XREF_TYPE || '  ' ||,                      
                              LEFT(STORES_DIST_NBR,10) ||,                      
                              STORES_USERID || '            ' ||,               
                              STORES_STORE_NBR || '                ' ||,        
                              STORES_RECIPIENT_NAME                             
            CALL WRITE_SUM_XREF_RECORD                                          
        END                                                                     
        IF (STORES_XREF_TYPE = XREF_TYPE_STORE_LP_MANAGER) THEN DO              
            SUM_XREF_RECORD = STORES_XREF_TYPE || '  ' ||,                      
                              LEFT(STORES_STORE_NBR,10) ||,                     
                              STORES_USERID || '            ' ||,               
                              '                    ' ||,                        
                              STORES_RECIPIENT_NAME                             
            CALL WRITE_SUM_XREF_RECORD                                          
        END                                                                     
        IF (STORES_XREF_TYPE = XREF_TYPE_STORE_MANAGER) THEN DO                 
            SUM_XREF_RECORD = XREF_TYPE_STORE_MANAGER || '  ' ||,               
                              LEFT(STORES_STORE_NBR,10) ||,                     
                              STORES_USERID || '            ' ||,               
                              STORES_LOC_NAME || '  ' ||,                       
                              STORES_RECIPIENT_NAME                             
            CALL WRITE_SUM_XREF_RECORD                                          
            SUM_XREF_RECORD = XREF_TYPE_STORE || '  ' ||,                       
                              LEFT(STORES_STORE_NBR,10) ||,                     
                              STORE_DEFAULT_USERID || '            ' ||,        
                              STORES_LOC_NAME                                   
            CALL WRITE_SUM_XREF_RECORD                                          
            SUM_XREF_RECORD = XREF_TYPE_STORE_DIST_MANAGER || '  ' ||,          
                              LEFT(STORES_STORE_NBR,10) ||,                     
                              DIST_MANAGER_USERID ||,                           
                              '            ' ||,                                
                              '                    ' ||,                        
                              STORES_RECIPIENT_NAME                             
            CALL WRITE_SUM_XREF_RECORD                                          
            SUM_XREF_RECORD = XREF_TYPE_STORE_DIST_LP_MANAGER ||,               
                              '  ' || LEFT(STORES_STORE_NBR,10) ||,             
                              DIST_LP_MANAGER_USERID ||,                        
                              '            ' ||,                                
                              '                    ' ||,                        
                              STORES_RECIPIENT_NAME                             
            CALL WRITE_SUM_XREF_RECORD                                          
            SUM_XREF_RECORD = XREF_TYPE_DIST_STORE || '  ' ||,                  
                              LEFT(STORES_DIST_NBR,10) ||,                      
                              STORE_DEFAULT_USERID                              
            CALL WRITE_SUM_XREF_RECORD                                          
        END                                                                     
    END                                                                         
    CALL READ_STORES_RECORD                                                     
END                                                                             
                                                                                
"EXECIO 0 DISKR INP01 (FINIS"                                                   
"EXECIO 0 DISKR OUT01 (FINIS"                                                   
EXIT 0                                                                          
/* ----------------------------------------------------------------- */         
/* Subroutines                                                       */         
/* ----------------------------------------------------------------- */         
/* ----------------------------------------------------------------- */         
/* READ_STORES_RECORD                                                */         
/*                                                                   */         
/* Read an Infopac stores record.                                    */         
/* ----------------------------------------------------------------- */         
READ_STORES_RECORD:                                                             
    "EXECIO 1 DISKR INP01"              /* read one record           */         
    IF RC = 2 THEN                                                              
        STORES_FILE_EOF = STORES_FILE_END_OF_FILE                               
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* WRITE_SUM_XREF_RECORD                                             */         
/*                                                                   */         
/* Write an Infopac Summary XREF record.                             */         
/* ----------------------------------------------------------------- */         
WRITE_SUM_XREF_RECORD:                                                          
    OUT01_RECORD = LEFT(SUM_XREF_RECORD,150)                                    
    PUSH OUT01_RECORD                                                           
    "EXECIO 1 DISKW OUT01"                                                      
    RETURN                                                                      
