       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    IMKUT060.                                                 
       AUTHOR.        PAUL KEBER.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  09/23/2004.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************00080000
      * IMKUT060 - NON-CONTINUED CLEARANCE PERMANENT PRICE CHANGES      00090002
      *            EXTRACT                                              00090002
      ******************************************************************00100000
      * THIS PROGRAM WILL EXTRACT NON-CONTINUED CLEARANCE PERM PRICE    00110001
      * CHANGES THAT NEED TO BE APPLIED EFFECTIVE THE NEXT DAY FROM     00111001
      * THE XMT PERMANENT PRICING TABLES.                               00120001
      ******************************************************************00100000
      *    WR/PITS#       DATE        DESCRIPTION                               
      *    --------   ----------   ------------------------------------         
      *    WR27472    09/23/2004   INITIAL IMPLEMENTATION.                      
      *    WR27472    03/31/2005   P.KEBER-CHANGES TO AVOID UNNECESSARY         
      *                            CALLS TO GET RCP LOCATIONS.                  
      ******************************************************************00140000
                                                                                
      ******************************************************************00290094
       ENVIRONMENT DIVISION.                                            00280094
      ******************************************************************00290094
      *                                                                 00300094
       CONFIGURATION SECTION.                                           00310094
       SOURCE-COMPUTER.    IBM-3090.                                    00320094
       OBJECT-COMPUTER.    IBM-3090.                                    00330094
      *                                                                 00340094
       INPUT-OUTPUT SECTION.                                            00350094
       FILE-CONTROL.                                                    00360094
      *                                                                 00370094
           SELECT CONTROL-CARD-FILE                                     00400094
               ASSIGN TO INP01.                                         00410094
      *                                                                 00420094
           SELECT PRICE-CHANGE-FILE                                     00430094
               ASSIGN TO OUT01.                                         00440094
      *                                                                 00470094
      ******************************************************************00480094
       DATA DIVISION.                                                   00490094
      ******************************************************************00500094
      *                                                                 00510094
       FILE SECTION.                                                    00520094
      *                                                                 00530094
       FD  CONTROL-CARD-FILE                                            00590094
           RECORDING MODE IS F                                          00600094
           BLOCK CONTAINS 0 RECORDS.                                    00610094
       01  SD-CONTROL-RECORD       PIC X(80).                           00620094
      *                                                                 00630094
       FD  PRICE-CHANGE-FILE                                            00640094
           RECORDING MODE IS F                                          00650094
           BLOCK CONTAINS 0 RECORDS.                                    00660094
       01  SD-OUTPUT-RECORD        PIC X(48).                           00670094
      *                                                                 00680094
      *                                                                 00730094
       WORKING-STORAGE SECTION.                                         00740094
      ****************************************************************  00830094
      * RECORD LAYOUT FOR CONTROL CARD FILE.                            00840094
      * DATE WILL BE ESP CONTROLLED - SYSTEM DATE PLUS 1 DAY            00840094
      ****************************************************************  00850094
       01  CC-CONTROL-CARD.                                             00860094
           05  CC-CONTROL-DATE              PIC X(10).                  00870094
           05  FILLER                       PIC X(70).                  00880094
      *                                                                 00890094
      ******************************************************************00790094
      *  OUTPUT FILE PERM PRICE CHANGE LAYOUT                           00800094
      ******************************************************************00810094
           COPY IMRD041.                                                00820094
      *                                                                 00920094
      *                                                                 01110094
       01  PA-PROGRAM-ACCUMULATORS.                                     01120094
           05  PA-CURSOR-ROWS-READ         PIC 9(09)    VALUE ZERO.     01190094
           05  PA-WRITE-COUNT              PIC 9(09)    VALUE ZERO.     01190094
           05  PA-RETRY-COUNT              PIC S9(04)   VALUE 0         01390094
                                                        COMP SYNC.      01400094
      *                                                                 01410094
       01  PC-PROGRAM-CONSTANTS.                                        01420094
           05  PC-PROGRAM-NAME             PIC X(08)     VALUE          01430094
               'IMKUT060'.                                              01440094
           05  PC-MAX-RETRY-COUNT          PIC S9(04)    VALUE 3        01530094
                                                       COMP SYNC.       01540094
           05  PC-MORE-THAN-MAX-RETRY      PIC S9(04)    VALUE 4        01550094
                                                       COMP SYNC.       01560094
           05  PC-CORP-LEVEL               PIC S9(04)    VALUE +1000    01550094
                                                       COMP.            01560094
           05  PC-MAX-RETRIES              PIC S9(04)  COMP SYNC                
                                                         VALUE +3.              
      *                                                                 01570094
       01  PS-PROGRAM-SWITCHES.                                         01580094
           05  PS-END-OF-CONTROL-CARDS-SW  PIC X(01)  VALUE 'N'.        01590094
               88  END-OF-CONTROL-CARDS               VALUE 'Y'.        01600094
           05  PS-END-OF-NCC-CSR-SW        PIC X(01)  VALUE 'N'.        01590094
               88  PS-END-NCC-CSR                     VALUE 'Y'.        01600094
      *                                                                 01760094
       01  PV-PROGRAM-VARIABLES.                                        01770094
           05  PV-PREV-PRCHG-ID            PIC S9(9) COMP VALUE +0.             
           05  PV-ALL-RMNG-STR-NULL        PIC S9(04)     COMP                  
                                                         VALUE +0.              
           05  PV-GP-PRIC-ID-NULL          PIC S9(04)     COMP                  
                                                         VALUE +0.              
           05  PV-GP-RTL-QTY-NULL          PIC S9(04)     COMP                  
                                                         VALUE +0.              
           05  PV-GP-RTL-AMT-NULL          PIC S9(04)     COMP                  
                                                         VALUE +0.              
           05  PV-UNT-RTL-AMT-NULL         PIC S9(04)     COMP                  
                                                         VALUE +0.              
           05  PV-SQL-SUB                  PIC 9(03)  VALUE ZERO.               
           05  PV-SUB                      PIC S9(03)     COMP-3        01780094
                                                          VALUE 1.      01790094
           05  PV-CURRENT-TMST             PIC X(26)      VALUE SPACES. 01840094
           05  PV-PARA-NAME                PIC X(35)      VALUE SPACES. 02030094
           05  PV-DB2-OPERATION-ATTEMPTED  PIC X(35)      VALUE SPACES. 02030094
           05  PV-LAST-TIER-EXISTS-IND     PIC X(01)      VALUE SPACES. 02030094
           05  PV-SQLCODE                  PIC S9(9)      VALUE +0.             
           05  PV-SQLCODE-DISPLAY          PIC +(8)9      VALUE ZEROES. 00830000
                                                                        01850094
                                                                        01930094
       01  ABEND-CODE                      PIC S9(04)    VALUE ZERO     00900094
                                                         COMP SYNC.     00910094
           88  PV-ABEND-4001                   VALUE +4001.                     
      *        EMPTY CONTROL CARD                                               
           88  AC-ABEND-4013                   VALUE +4013.                     
      *        SQL ERROR                                                        
                                                                        02010094
           COPY SQLCA2.                                                 02110094
                                                                        02670099
      ***************************************************************** 02670199
      *  XMT_LOC DCLGEN                                                 02672099
      ***************************************************************** 02673099
           EXEC SQL                                                     02674099
               INCLUDE XMT00001                                         02675099
           END-EXEC.                                                    02676099
                                                                        02677099
      ***************************************************************** 02670199
      *  XMT_DC_LOC_ASGMT DCLGEN                                        02672099
      ***************************************************************** 02673099
           EXEC SQL                                                     02674099
               INCLUDE XMT00002                                         02675099
           END-EXEC.                                                    02676099
                                                                        02677099
      ***************************************************************** 02670199
      *  XMT_STR_GP_MBSHP DCLGEN                                        02672099
      ***************************************************************** 02673099
           EXEC SQL                                                     02674099
               INCLUDE XMT00003                                         02675099
           END-EXEC.                                                    02676099
                                                                        02677099
      ***************************************************************** 02670199
      *  XMT_PRCHG DCLGEN                                               02672099
      ***************************************************************** 02673099
           EXEC SQL                                                     02674099
               INCLUDE XMT00009                                         02675099
           END-EXEC.                                                    02676099
                                                                        02677099
      ***************************************************************** 02677199
      *  XMT_STR_GP_PRCHG DCLGEN                                        02677299
      ***************************************************************** 02677399
           EXEC SQL                                                     02677499
               INCLUDE XMT00010                                         02677599
           END-EXEC.                                                    02677699
                                                                        02677799
      ***************************************************************** 02678099
      *  XMT_PRCHG_MDSE DCLGEN                                          02679099
      ***************************************************************** 02679199
           EXEC SQL                                                     02679299
               INCLUDE XMT00011                                         02679399
           END-EXEC.                                                    02679499
                                                                        02679599
      *                                                                 02680094
           EXEC SQL                                                     02690094
                INCLUDE SQLCA                                           02700094
           END-EXEC.                                                    02710094
                                                                        02720094
                                                                                
      ***************************************************************** 02670199
      *  IMWS005 - STORE SELECTION WORKING STORAGE                      02672099
      ***************************************************************** 02673099
           COPY IMWS005.                                                02100094
                                                                                
      ***************************************************************** 02678099
      *   CURSOR FOR NON-CONTINUED CLEARANCE PRICE CHANGES FOR THE              
      *   EFFECTIVE PROCESSING DATE - DRIVING CURSOR OF PROGRAM                 
      ***************************************************************** 02678099
           EXEC SQL                                                             
               DECLARE XMT_PRCHG_NCC_CSR CURSOR FOR                             
                  SELECT S.PRCHG_ID                                             
                        ,S.STR_GP_TYP_CDE                                       
                        ,S.STR_GP_ID                                            
                        ,P.PRCHG_TYP_CDE                                        
                        ,P.ALL_RMNG_STR_IND                                     
                        ,M.SKU_NBR                                              
                        ,M.GP_PRIC_ID                                           
                        ,M.GP_RTL_QTY                                           
                        ,M.GP_RTL_AMT                                           
                        ,M.UNT_RTL_AMT                                          
                   FROM  XMT_STR_GP_PRCHG S                                     
                        ,XMT_PRCHG P                                            
                        ,XMT_PRCHG_MDSE M                                       
                   WHERE S.PRCHG_EFF_DTE  = :CC-CONTROL-DATE                    
                     AND P.PRCHG_ID       = S.PRCHG_ID                          
                     AND P.PRCHG_ST_CDE   = 'APR'                               
                     AND P.EVNT_CTG_CDE   <> 'CON'                              
                     AND M.PRCHG_ID       = S.PRCHG_ID                          
                     AND M.STR_GP_TYP_CDE = S.STR_GP_TYP_CDE                    
                     AND M.STR_GP_ID      = S.STR_GP_ID                         
                     AND M.MDSE_HIER_CDE  = 'SKU'                               
                   ORDER BY S.PRCHG_ID                                          
                           ,S.STR_GP_TYP_CDE                                    
                           ,S.STR_GP_ID                                         
           END-EXEC.                                                            
      ******************************************************************02730094
      *  DB2 ERROR MESSAGES FORMAT AREA.                                02740094
      ******************************************************************02750094
           COPY DPWS004.                                                02760094
                                                                        02770094
      *                                                                 02790094
      ******************************************************************02800094
       PROCEDURE DIVISION.                                              02810094
      ******************************************************************02820094
      *                                                                 02830094
       0000-IMKUT060.                                                   02850094
      ******************************************************************02860094
      * MAIN PROCESSING ROUTINE                                        *02870094
      ******************************************************************02880094
                                                                        02890094
           PERFORM 1000-HOUSEKEEPING.                                   02900094
      *                                                                 02910094
           PERFORM 2000-PROCESS-PRICE-CHANGES                           02920094
               UNTIL PS-END-NCC-CSR.                                            
      *                                                                 02940094
           PERFORM 1500-END-OF-JOB.                                     02950094
                                                                                
           IF PA-WRITE-COUNT = 0                                                
               MOVE 4095 TO RETURN-CODE                                         
           END-IF.                                                              
                                                                                
           GOBACK.                                                      02960094
      *                                                                 02970094
                                                                        02980094
       1000-HOUSEKEEPING.                                               02990094
           MOVE '*1000-HOUSEKEEPING.              *' TO PV-PARA-NAME.   03010094
**TEMP*    DISPLAY ' PARAGRAPH ' PV-PARA-NAME.                                  
                                                                        03020094
           OPEN INPUT  CONTROL-CARD-FILE                                03040094
                OUTPUT PRICE-CHANGE-FILE.                               03050094
                                                                        03070094
           PERFORM 6200-READ-CONTROL-CARD.                              03240094
           IF NOT END-OF-CONTROL-CARDS                                          
               MOVE 'N'             TO PS-END-OF-NCC-CSR-SW             03190094
                                                                                
      **-- SET UP INITIAL CALL TO IMPD005                                       
               MOVE PC-PROGRAM-NAME TO IM005-PC-PROGRAM-NAME                    
               MOVE CC-CONTROL-DATE TO IM005-PV-EFF-DTE                         
               PERFORM IM005-A000-GET-PROCESS-STORES                            
               IF IM005-PV-ABEND-CODE = 0                                       
                   CONTINUE                                                     
               ELSE                                                             
                   DISPLAY '***********************************'                
                   DISPLAY 'ERROR RETURNED FROM IMPD005 '                       
                   MOVE IM005-PV-ABEND-PARAGRAPH TO PV-PARA-NAME                
                   MOVE IM005-PV-ABEND-CODE TO ABEND-CODE                       
                   IF AC-ABEND-4013                                             
                       MOVE IM005-PV-ABEND-MESSAGE                              
                           TO PV-DB2-OPERATION-ATTEMPTED                        
                       MOVE IM005-PV-ABEND-SQLCODE TO SQLCODE                   
                       PERFORM 9980-SQL-ERROR                                   
                   ELSE                                                         
                       DISPLAY IM005-PV-ABEND-MESSAGE                           
                       PERFORM 9999-ABEND                                       
                   END-IF                                                       
               END-IF                                                           
               PERFORM 8500-OPEN-NCC-CSR                                04010094
           END-IF.                                                              
      *                                                                 03250094
                                                                        03680094
       1500-END-OF-JOB.                                                 03690094
                                                                        03700094
      ******************************************************************03710094
      *  CLOSE FILES AND DISPLAY COUNTS                                *03720094
      ******************************************************************03730094
                                                                        03740094
           PERFORM 8520-CLOSE-NCC-CSR.                                          
           CLOSE CONTROL-CARD-FILE                                      03750094
                 PRICE-CHANGE-FILE.                                     03780094
                                                                        03790094
           DISPLAY '*******************************'.                           
           DISPLAY 'IMKUT060 - END OF PROGRAM STATS'.                           
           DISPLAY 'CURSOR ROWS READ: ' PA-CURSOR-ROWS-READ.                    
           DISPLAY 'RECORDS WRITTEN : ' PA-WRITE-COUNT.                         
           DISPLAY '*******************************'.                           
                                                                        03900094
      ******************************************************************07160094
      * PROCESS PRICE CHANGE CURSOR                                    *07170094
      ******************************************************************07180094
       2000-PROCESS-PRICE-CHANGES.                                      03910094
           MOVE '*2000-PROCESS-PRICE-CHANGES.'    TO PV-PARA-NAME.      03920094
**TEMP*    DISPLAY ' PARAGRAPH ' PV-PARA-NAME.                                  
                                                                                
           PERFORM 8510-FETCH-PRCHG-NCC-CSR.                                    
           IF NOT PS-END-NCC-CSR                                                
      *-- ONLY HAVE TO PERFORM CHECK ONCE PER PRICE-CHG ID                      
               IF XMT00010-PRCHG-ID = PV-PREV-PRCHG-ID                          
                   PERFORM 2010-OUTPUT-CONTROL                                  
               ELSE                                                             
                   IF XMT00009-PRCHG-TYP-CDE = 'RCP' AND                        
                      XMT00009-ALL-RMNG-STR-IND = 'Y'                           
                       PERFORM 8600-CHECK-TIER                                  
                   END-IF                                                       
                   PERFORM 2010-OUTPUT-CONTROL                                  
                   MOVE XMT00010-PRCHG-ID TO PV-PREV-PRCHG-ID                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************07160094
      * CONTROL WRITES OF OUTPUT FILE                                  *07170094
      ******************************************************************07180094
       2010-OUTPUT-CONTROL.                                                     
           MOVE '*2010-OUTPUT-CONTROL.       '    TO PV-PARA-NAME.      03920094
**TEMP*    DISPLAY ' PARAGRAPH ' PV-PARA-NAME.                                  
                                                                                
      *-- IF CORPORATE LEVEL, ONLY WRITE 1 STORE 0 RECORD                       
      *-- OTHERWISE WRITE ONE RECORD PER STORE                                  
           IF XMT00010-STR-GP-TYP-CDE = PC-CORP-LEVEL                           
               MOVE 0                   TO IM041-PUR-STR-NBR                    
               PERFORM 7200-WRITE-PRICE-CHANGE-FILE                             
           ELSE                                                                 
               IF NOT(XMT00010-STR-GP-TYP-CDE = IM005-PV-STR-GP-TYP-CDE         
                      AND XMT00010-STR-GP-ID = IM005-PV-STR-GP-ID)              
                   MOVE XMT00010-STR-GP-TYP-CDE TO                              
                        IM005-PV-STR-GP-TYP-CDE                                 
                   MOVE XMT00010-STR-GP-ID     TO                               
                        IM005-PV-STR-GP-ID                                      
                   MOVE CC-CONTROL-DATE        TO                               
                        IM005-PV-EFF-DTE                                        
                   INITIALIZE IM005-GROUP-STORE-TABLE                           
                   SET IM005-GP-STR-IDX TO 1                                    
                   MOVE 0               TO IM005-PV-ABEND-CODE                  
                   MOVE SPACES          TO IM005-PV-ABEND-PARAGRAPH             
                   PERFORM IM005-B000-RCP-LOCATIONS                             
                   IF IM005-PV-ABEND-CODE NOT = 0                               
                       DISPLAY '***********************************'            
                       DISPLAY 'ERROR RETURNED FROM IMPD005 '                   
                       MOVE IM005-PV-ABEND-PARAGRAPH TO PV-PARA-NAME            
                       MOVE IM005-PV-ABEND-CODE TO ABEND-CODE                   
                       IF AC-ABEND-4013                                         
                           MOVE IM005-PV-ABEND-MESSAGE                          
                               TO PV-DB2-OPERATION-ATTEMPTED                    
                           MOVE IM005-PV-ABEND-SQLCODE TO SQLCODE               
                           PERFORM 9980-SQL-ERROR                               
                       ELSE                                                     
                           DISPLAY IM005-PV-ABEND-MESSAGE                       
                           PERFORM 9999-ABEND                                   
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
               PERFORM                                                          
                  VARYING IM005-GP-STR-IDX FROM 1 BY 1                          
                    UNTIL IM005-GP-STR-IDX > IM005-PV-MAX-STR-GP-STR            
                 IF IM005-STR-GRP-STORE (IM005-GP-STR-IDX) NOT = 0              
                     MOVE IM005-STR-GRP-STORE (IM005-GP-STR-IDX) TO             
                          IM041-PUR-STR-NBR                                     
                     PERFORM 7200-WRITE-PRICE-CHANGE-FILE                       
                 END-IF                                                         
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *              IMPD005 - STORE EXTRACT PROCEDURE                          
      *   THIS PROCEDURE DIVISION WILL BE USED WITH XMWS005 TO EXTRACT          
      *   STORE INFORMATION.                                                    
      *   THE PROGRAMS WHICH USE THIS PROCEDURE MUST SUPPLY THE                 
      *   FOLLOWING:                                                            
      *     1.  MOVE EFFECTIVE DATE TO IM005-PV-EFF-DTE BEFORE                  
      *         PERFORMING IM005-A000-GET-PROCESS-STORES.                       
      *     2.  MOVE PROGRAM NAME TO IM005-PC-PROGRAM-NAME BEFORE               
      *         PERFORMING IM005-A000-GET-PROCESS-STORES.                       
      *     3.  MOVE STORE GROUP TYPE CODE TO IM005-PV-STR-GP-TYP-CDE           
      *         BEFORE PERFORMING IM005-B000-RCP-LOCATIONS.                     
      *     4.  MOVE STORE GROUP ID TO IM005-PV-STR-GP-ID BEFORE                
      *         PERFORMING IM005-B000-RCP-LOCATIONS.                            
      *     5.  THE PROGRAM MUST INCLUDE THE DCLGENS FOR THE FOLLOWING          
      *         TABLES: XMT_LOC                                                 
      *                 XMT_DC_LOC_ASGMT                                        
      *                 XMT_STR_GP_MBSHP                                        
      *-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-        
      *                                                                         
      *   IM005-A000-GET-PROCESS-STORES                                         
      *      SHOULD BE PERFORMED AS PART OF INITIALIZATION TO PROGRAM.          
      *      THIS PARAGRAPH LOADS ALL DS(DEPARTMENT STORE) LOCATIONS            
      *      TO BE PROCESSED INTO A TABLE.                                      
      *                                                                         
      *-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-        
      *   IM005-B000-RCP-LOCATIONS                                              
      *      THIS PARAGRAPH SHOULD BE PERFORMED FOR ALL RCP PRICE               
      *      CHANGES BEING PROCESSED.  FOR EACH LOCATION THAT SHOULD BE         
      *      PROCESSED FOR THE PRICE CHANGE, IM005-STR-GRP-STORE                
      *      (IM005-STORE-INDEX) WILL BE SET TO TRUE.                           
      *                                                                         
      ******************************************************************        
           COPY IMPD005.                                                04330094
                                                                                
      *                                                                 06760094
      ******************************************************************07160094
      * READ DATE CONTROL FILE.                                        *07170094
      ******************************************************************07180094
       6200-READ-CONTROL-CARD.                                          07140094
           MOVE '*6200-READ-CONTROL-CARD.         *' TO PV-PARA-NAME.   07150094
                                                                        07210094
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                  07190094
               AT END SET END-OF-CONTROL-CARDS TO TRUE.                 07200094
                                                                        07210094
           IF END-OF-CONTROL-CARDS                                      07220094
               DISPLAY '*** NO DATE CONTROL CARD ***'                   07230094
               SET PV-ABEND-4001 TO TRUE                                        
               PERFORM 9999-ABEND                                       07240094
           ELSE                                                         07250094
               DISPLAY ' CONTROL-DATE ' CC-CONTROL-DATE                 07260094
           END-IF.                                                      07280094
                                                                        07290094
      ******************************************************************07320094
      * WRITE PRICE CHANGE RECORDS                                     *07330094
      ******************************************************************07340094
       7200-WRITE-PRICE-CHANGE-FILE.                                            
           IF PV-GP-PRIC-ID-NULL < 0                                            
               MOVE 0                   TO IM041-PUR-NEW-GP-PRIC-ID             
           ELSE                                                                 
               MOVE XMT00011-GP-PRIC-ID TO IM041-PUR-NEW-GP-PRIC-ID             
           END-IF.                                                              
           IF PV-GP-RTL-QTY-NULL < 0                                            
               MOVE 0                   TO IM041-PUR-NEW-GP-RTL-QTY             
           ELSE                                                                 
               MOVE XMT00011-GP-RTL-QTY TO IM041-PUR-NEW-GP-RTL-QTY             
           END-IF.                                                              
           IF PV-GP-RTL-AMT-NULL < 0                                            
               MOVE 0                   TO IM041-PUR-NEW-GP-RTL-AMT             
           ELSE                                                                 
               MOVE XMT00011-GP-RTL-AMT TO IM041-PUR-NEW-GP-RTL-AMT             
           END-IF.                                                              
                                                                                
           MOVE XMT00011-SKU-NBR        TO IM041-PUR-SKU.                       
           MOVE XMT00011-UNT-RTL-AMT    TO IM041-PUR-NEW-UNT-RTL-AMT.           
           MOVE CC-CONTROL-DATE         TO IM041-PUR-EFF-DTE.                   
           MOVE XMT00010-PRCHG-ID       TO IM041-PUR-PRCHG-ID.                  
           MOVE XMT00009-PRCHG-TYP-CDE  TO IM041-PUR-PRCHG-TYP-CDE.             
                                                                                
           IF XMT00009-PRCHG-TYP-CDE = 'RCP'  AND                               
              XMT00009-ALL-RMNG-STR-IND = 'Y' AND                               
              PV-LAST-TIER-EXISTS-IND = 'Y'                                     
               MOVE 'N'  TO IM041-PUR-ALL-RMNG-STR-IND                          
           ELSE                                                                 
               MOVE XMT00009-ALL-RMNG-STR-IND TO                                
                IM041-PUR-ALL-RMNG-STR-IND                                      
           END-IF.                                                              
                                                                                
           WRITE SD-OUTPUT-RECORD   FROM IM041-PRICE-UPDATE-RECORD.     07370094
           ADD +1 TO PA-WRITE-COUNT.                                    07380094
      *                                                                 07390094
      ******************************************************************07320094
      * OPEN NON-CONTINUED CLEARANCE PRICE CHANGE CURSOR               *07330094
      ******************************************************************07340094
       8500-OPEN-NCC-CSR.                                                       
           MOVE '8500-OPEN-NCC-CSR.    '  TO PV-PARA-NAME.                      
**TEMP*    DISPLAY ' PARAGRAPH ' PV-PARA-NAME.                                  
                                                                                
           INITIALIZE PA-RETRY-COUNT.                                           
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
               EXEC SQL                                                         
                   OPEN XMT_PRCHG_NCC_CSR                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -904                                          
                        CONTINUE                                                
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '8500-OPEN-NCC-CSR.'                               
                                          TO PV-PARA-NAME                       
                        MOVE 'OPEN OPERATION'                                   
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                        PERFORM 9980-SQL-ERROR                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '8500-OPEN-NCC-CSR'                                         
                                      TO  PV-PARA-NAME                          
               MOVE 'OPEN CURSOR RETRIES EXCEEDED'                              
                                      TO  PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9980-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      ************************************************************              
      *  FETCH ALL NON-CONTINUED CLEARANCE PERM PRICES          **              
      ************************************************************              
       8510-FETCH-PRCHG-NCC-CSR.                                                
           MOVE '8510-FETCH-PRCHG-NCC-CSR.' TO PV-PARA-NAME.                    
**TEMP*    DISPLAY ' PARAGRAPH ' PV-PARA-NAME.                                  
                                                                                
           EXEC SQL                                                             
               FETCH XMT_PRCHG_NCC_CSR                                          
                INTO :XMT00010-PRCHG-ID                                         
                   , :XMT00010-STR-GP-TYP-CDE                                   
                   , :XMT00010-STR-GP-ID                                        
                   , :XMT00009-PRCHG-TYP-CDE                                    
                   , :XMT00009-ALL-RMNG-STR-IND :PV-ALL-RMNG-STR-NULL           
                   , :XMT00011-SKU-NBR                                          
                   , :XMT00011-GP-PRIC-ID     :PV-GP-PRIC-ID-NULL               
                   , :XMT00011-GP-RTL-QTY     :PV-GP-RTL-QTY-NULL               
                   , :XMT00011-GP-RTL-AMT     :PV-GP-RTL-AMT-NULL               
                   , :XMT00011-UNT-RTL-AMT    :PV-UNT-RTL-AMT-NULL              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                    ADD 1 TO PA-CURSOR-ROWS-READ                                
               WHEN SQLCODE = +100                                              
                   SET PS-END-NCC-CSR TO TRUE                                   
               WHEN SQLWARN NOT = SPACES                                        
               WHEN SQLCODE NOT = ZERO                                          
                    MOVE '8510-FETCH-PRCHG-NCC-CSR'                             
                      TO  PV-PARA-NAME                                          
                    MOVE 'FETCH PERM PRICE CHANGES'                             
                      TO  PV-DB2-OPERATION-ATTEMPTED                            
                    PERFORM 9980-SQL-ERROR                                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************07320094
      * CLOSE NON-CONTINUED CLEARANCE PRICE CHANGE CURSOR              *07330094
      ******************************************************************07340094
       8520-CLOSE-NCC-CSR.                                                      
           MOVE '2230-CLOSE-PRCHG-NCC-CSR.' TO PV-PARA-NAME.                    
**TEMP*    DISPLAY ' PARAGRAPH ' PV-PARA-NAME.                                  
                                                                                
           INITIALIZE PA-RETRY-COUNT.                                           
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                  CLOSE XMT_PRCHG_NCC_CSR                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                        CONTINUE                                                
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '2230-CLOSE-PRCHG-NCC-CSR'                         
                                      TO  PV-PARA-NAME                          
                        MOVE 'CLOSE NEW STORE CURSOR'                           
                                      TO  PV-DB2-OPERATION-ATTEMPTED            
                        PERFORM 9980-SQL-ERROR                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '2230-CLOSE-PRCHG-NCC-CSR'                                  
                                      TO  PV-PARA-NAME                          
               MOVE 'CLOSE CURSOR RETRIES EXCEEDED'                             
                                      TO  PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9980-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************07160094
      * CHECK IF FUTURE PRICE CHANGE RECORDS EXIST                      07170094
      ******************************************************************07180094
       8600-CHECK-TIER.                                                         
           MOVE '*8600-CHECK-TIER.           '    TO PV-PARA-NAME.      03920094
**TEMP*    DISPLAY ' PARAGRAPH ' PV-PARA-NAME.                                  
           MOVE 'N' TO PV-LAST-TIER-EXISTS-IND.                                 
                                                                                
      **--    IF FOUND - INDICATES THAT THIS IS NOT THE LAST TIER               
      **--    AND CHANGE RECORDS ARE TO BE WRITTEN.                             
                                                                                
           EXEC SQL                                                             
                                                                                
              SELECT 'Y'                                                        
                INTO :PV-LAST-TIER-EXISTS-IND                                   
                FROM  XMT_STR_GP_PRCHG                                          
               WHERE PRCHG_ID     =  :XMT00010-PRCHG-ID                         
                 AND PRCHG_EFF_DTE > :CC-CONTROL-DATE                           
               FETCH FIRST 1 ROWS ONLY                                          
                                                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLCODE NOT = ZERO                                          
                    MOVE '8600-CHECK-TIER.'                                     
                                      TO PV-PARA-NAME                           
                    MOVE 'SELECT OPERATION '                                    
                               TO PV-DB2-OPERATION-ATTEMPTED                    
                    PERFORM 9980-SQL-ERROR                                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
       9980-SQL-ERROR.                                                  07500094
      ******************************************************************07510094
      * ABEND ROUTINE FOR BAD SQL CALLS                                *07520094
      ******************************************************************07530094
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
           DISPLAY '** ABEND **          = SQL ERROR'.                  07540094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07550094
           DISPLAY '** PARAGRAPH **      = ' PV-PARA-NAME.              07560094
           DISPLAY '** OPERATION **      = ' PV-DB2-OPERATION-ATTEMPTED.        
           DISPLAY '** SQL CODE **       = ' PV-SQLCODE-DISPLAY.                
           DISPLAY 'CURSOR ROWS READ     = ' PA-CURSOR-ROWS-READ.               
           DISPLAY 'RECORDS WRITTEN      = ' PA-WRITE-COUNT.                    
      *                                                                 07570094
      ******************************************************************07580094
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    07590094
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         07600094
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     07610094
      * FORMAT.                                                         07620094
      ******************************************************************07630094
           COPY DPPD004.                                                07640094
                                                                        07650094
             EXEC SQL                                                   07660094
                COMMIT                                                  07670094
             END-EXEC.                                                  07680094
      *                                                                 07690094
           SET AC-ABEND-4013 TO TRUE.                                   07700094
           CALL 'ILBOABN0' USING ABEND-CODE.                            07710094
                                                                        07720094
       9999-ABEND.                                                      07730094
      ******************************************************************07740094
      * ABEND ROUTINE MISC ERRORS                                      *07750094
      ******************************************************************07760094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07770094
           DISPLAY '** PARAGRAPH **      = ' PV-PARA-NAME.              07780094
           DISPLAY 'CURSOR ROWS READ     = ' PA-CURSOR-ROWS-READ.               
           DISPLAY 'RECORDS WRITTEN      = ' PA-WRITE-COUNT.                    
           CALL 'ILBOABN0' USING ABEND-CODE.                            07800094
