      *-----------------------------------------------------------------        
       IDENTIFICATION DIVISION.                                                 
      *-----------------------------------------------------------------        
       PROGRAM-ID.      INKRP150.                                               
       AUTHOR.          JAN BLAZICH.                                            
       DATE-WRITTEN.    01/24/06.                                               
      *                                                                         
      *-----------------------------------------------------------------        
      * PURPOSE. SKU RECLASS STORE/DEPT REPORTS - DETAIL AND SUMMARY            
      *     INPUT RECORDS FROM PROGRAM INKUP143 WILL BE                         
      *     SORTED PRIOR TO THIS PROGRAM.  THE REPORT INDICATOR                 
      *     INSERTED BY THE SORT DEFINES WHICH REPORT(S) WILL BE                
      *     PRODUCED.                                                           
      *     REPORT IND                                                          
      *         1 - STORE/DEPT DETAIL REPORT (FROM DEPT)                        
      *           - STORE/DEPT SUMMARY REPORT (FROM DEPT)                       
      *         2 - STORE/DEPT DETAIL REPORT (TO DEPT)                          
      *           - STORE/DEPT SUMMARY REPORT (TO DEPT)                         
      *-----------------------------------------------------------------        
      *       DB2 TABLES: NONE                                                  
      *      DB2 CURSORS: NONE                                                  
      *-----------------------------------------------------------------        
W28545* 06-29-2004 J BLAZICH    LATE RECLASS CHANGES --                         
W28545*                         MAKE SURE DEPT/MCL/SCL VALUES ARE               
W28545*                         SET ACCORDING TO THE VALUES IN EFFECT           
W28545*                         ON THE PLANNED INVENTORY DATE                   
W28545*                                                                         
W28545*---------------------------------------------------------------- -       
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT RECLS-EXTRACT-FILE      ASSIGN TO INP01.                      
           SELECT SKU-RECLS-DTL-RPT-FILE  ASSIGN TO RPT01.                      
           SELECT SKU-RECLS-SUM-RPT-FILE  ASSIGN TO RPT02.                      
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  RECLS-EXTRACT-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RECLS-EXTRACT-REC                PIC  X(131).                        
                                                                                
       FD  SKU-RECLS-DTL-RPT-FILE                                               
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SKU-RECLS-DTL-RPT-REC            PIC  X(132).                        
                                                                                
       FD  SKU-RECLS-SUM-RPT-FILE                                               
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SKU-RECLS-SUM-RPT-REC            PIC  X(132).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  ABEND-CODE                       PIC S9(4) COMP   VALUE +0.          
                                                                                
       01  PROGRAM-ACCUMULATORS COMP-3.                                         
           05  PA-LINE-CNT                  PIC S9(3)  VALUE +0.                
           05  PA-PAGE-CNT                  PIC S9(5)  VALUE +0.                
           05  PA-RECS-READ                 PIC S9(11) VALUE +0.                
           05  PA-EXTD-AMT                  PIC S9(11)V99 VALUE +0.             
      * DETAIL LEVEL TOTALS                                                     
           05  PA-D1-QTY                    PIC S9(11)    VALUE +0.             
           05  PA-D1-UNIT-RETAIL            PIC S9(5)V99  VALUE +0.             
           05  PA-D1-EXTD-AMT               PIC S9(11)V99 VALUE +0.             
      * FIRST LEVEL TOTALS                                                      
           05  PA-T1-QTY                    PIC S9(11)    VALUE +0.             
           05  PA-T1-UNIT-RETAIL            PIC S9(5)V99  VALUE +0.             
           05  PA-T1-EXTD-AMT               PIC S9(11)V99 VALUE +0.             
      * SECOND LEVEL TOTALS                                                     
           05  PA-T2-QTY                    PIC S9(11)    VALUE +0.             
           05  PA-T2-UNIT-RETAIL            PIC S9(5)V99  VALUE +0.             
           05  PA-T2-EXTD-AMT               PIC S9(11)V99 VALUE +0.             
      * THIRD LEVEL TOTALS                                                      
           05  PA-T3-QTY                    PIC S9(11)    VALUE +0.             
           05  PA-T3-UNIT-RETAIL            PIC S9(5)V99  VALUE +0.             
           05  PA-T3-EXTD-AMT               PIC S9(11)V99 VALUE +0.             
      * FOURTH LEVEL TOTALS                                                     
           05  PA-T4-QTY                    PIC S9(11)    VALUE +0.             
           05  PA-T4-UNIT-RETAIL            PIC S9(5)V99  VALUE +0.             
           05  PA-T4-EXTD-AMT               PIC S9(11)V99 VALUE +0.             
                                                                                
      * FIFTH LEVEL TOTALS                                                      
           05  PA-T5-QTY                    PIC S9(11)    VALUE +0.             
           05  PA-T5-UNIT-RETAIL            PIC S9(5)V99  VALUE +0.             
           05  PA-T5-EXTD-AMT               PIC S9(11)V99 VALUE +0.             
                                                                                
      * SUMMARY ACCUMS                                                          
           05  PA-SUM-LINE-CNT              PIC S9(3)  VALUE +0.                
           05  PA-SUM-PAGE-CNT              PIC S9(5)  VALUE +0.                
                                                                                
       01  PROGRAM-CONSTANTS.                                                   
           05  PC-LINES-PER-PAGE            PIC S9(3) COMP-3 VALUE +55.         
           05  PC-PROGRAM-NAME              PIC X(08) VALUE 'INKRP150'.         
           05  PC-MAIN-HEAD-2          PIC X(38)  VALUE                         
               ' SKU RECLASS - LOC/DEPT DETAIL REPORT '.                        
           05  PC-MAIN-HEAD-FROM       PIC X(11)  VALUE                         
               '(FROM DEPT)'.                                                   
           05  PC-MAIN-HEAD-TO         PIC X(09)  VALUE                         
               '(TO DEPT)'.                                                     
           05  PC-SUM-HEAD-2           PIC X(39)  VALUE                         
               ' SKU RECLASS - LOC/DEPT SUMMARY REPORT '.                       
                                                                                
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05  PV-PARAGRAPH-NAME      PIC X(31)  VALUE SPACES.                  
           05  PV-CURRENT-DATE        PIC 9(6).                                 
           05  PV-CURRENT-DATE-R  REDEFINES PV-CURRENT-DATE.                    
               10  PV-CURR-YY         PIC 99.                                   
               10  PV-CURR-MM         PIC 99.                                   
               10  PV-CURR-DD         PIC 99.                                   
                                                                                
           05  PV-CURRENT-TIME        PIC 9(8).                                 
           05  PV-CURRENT-TIME-R  REDEFINES PV-CURRENT-TIME.                    
               10  PV-CURR-HR         PIC 99.                                   
               10  PV-CURR-MN         PIC 99.                                   
               10  FILLER             PIC X(4).                                 
                                                                                
           05  PV-PROCESS-DATE.                                                 
               10  PV-PROCESS-CCYY    PIC X(4).                                 
               10  FILLER             PIC X.                                    
               10  PV-PROCESS-MM      PIC X(2).                                 
               10  FILLER             PIC X.                                    
               10  PV-PROCESS-DD      PIC X(2).                                 
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  PS-END-OF-FILE-SW      PIC X  VALUE 'N'.                         
               88 PS-END-OF-FILE             VALUE 'Y'.                         
               88 PS-END-OF-FILE-NO          VALUE 'N'.                         
           05  PS-DOUBLE-SPACE-SW     PIC X  VALUE SPACE.                       
               88  PS-DOUBLE-SPACE           VALUE 'Y'.                         
               88  PS-SINGLE-SPACE           VALUE 'N'.                         
           05  PS-SUM-DOUBLE-SPACE-SW PIC X  VALUE SPACE.                       
               88  PS-SUM-DOUBLE-SPACE       VALUE 'Y'.                         
               88  PS-SUM-SINGLE-SPACE       VALUE 'N'.                         
       01  WORK-AREAS.                                                          
           05  CONTROL-BREAK-SW             PIC 9(2)  VALUE ZEROS.              
               88  NO-BREAK                 VALUE  ZEROS.                       
               88  BREAK-1                  VALUES ARE 1 THRU 99.               
               88  BREAK-2                  VALUES ARE 2 THRU 99.               
               88  BREAK-3                  VALUES ARE 3 THRU 99.               
               88  BREAK-4                  VALUES ARE 4 THRU 99.               
               88  BREAK-5                  VALUES ARE 5 THRU 99.               
               88  END-OF-PROCESS           VALUE  99.                          
                                                                                
                                                                                
      *  KEYS FOR INKRP150-1   STORE/DEPT DETAIL (FROM DEPT)                    
      *      (INV DATE),(LOC), (FROM(OLD) ATTRIBUTES), (TO(NEW) ATTR),          
      *       AND (SKU, AND UNIT RETAIL, AREA)                                  
      *  KEYS FOR INKRP150-2   STORE/DEPT DETAIL (TO DEPT)                      
      *      (INV DATE),(LOC), (TO(OLD) ATTRIBUTES), (FROM(NEW) ATTR),          
      *       AND (SKU, AND UNIT RETAIL, AREA)                                  
      *  KEYS FOR INKRP150-3   STORE/DEPT SUMMARY (FROM DEPT)                   
      *       INV DATE, LOC, FROM(OLD) ATTRIBUTES                               
      *  KEYS FOR INKRP150-4   STORE/DEPT SUMMARY (TO DEPT)                     
      *       INV DATE, LOC, TO(NEW) ATTRIBUTES                                 
                                                                                
       01  IN-KEY-INFO.                                                         
           05  IN-KEY.                                                          
      *  INVENTORY DATE                                                         
               10  IN-FLD-5                 PIC X(10).                          
      *  STORE (LOC)                                                            
               10  IN-FLD-4                 PIC  9(4).                          
      *  DEPT, MCL, AND SCL                                                     
               10  IN-FLD-3                 PIC  9(4).                          
               10  IN-FLD-3X.                                                   
                   15  IN-FLD-3-SLS1        PIC  X     VALUE '/'.               
                   15  IN-FLD-3B            PIC  9(3).                          
                   15  IN-FLD-3-SLS2        PIC  X     VALUE '/'.               
                   15  IN-FLD-3A            PIC  9(3).                          
      *  DEPT, MCL, AND SCL                                                     
               10  IN-FLD-2                 PIC  9(4).                          
               10  IN-FLD-2X.                                                   
                   15  IN-FLD-2-SLS1        PIC  X     VALUE '/'.               
                   15  IN-FLD-2B            PIC  9(3).                          
                   15  IN-FLD-2-SLS2        PIC  X     VALUE '/'.               
                   15  IN-FLD-2A            PIC  9(3).                          
      *  AREA, SKU, AND UNIT-RETAIL                                             
               10  IN-FLD-1.                                                    
                   15  IN-FLD-1B            PIC  9(8).                          
                   15  IN-FLD-1A            PIC S9(5)V99 VALUE ZERO.            
                   15  IN-FLD-1C            PIC  9(9).                          
           05  IN-KEY-X              REDEFINES IN-KEY.                          
               10  FILLER                   PIC X(23).                          
               10  IN-FLD-X                 PIC X(39).                          
      *  OTHER FIELDS                                                           
           05  IN-QTY                       PIC S9(9)    VALUE ZERO.            
           05  IN-LOC-NM                    PIC X(40)    VALUE SPACES.          
           05  IN-EXTD-AMT                  PIC S9(9)V99 VALUE ZERO.            
W28545     05  IN-LATE-IND                  PIC X        VALUE SPACES.          
                                                                                
       01  PREV-KEY-INFO.                                                       
W28545     05  PREV-IN-KEY                  PIC X(62).                          
           05  FILLER                       PIC X(61).                          
                                                                                
       01  CURR-KEY-INFO.                                                       
           05  CURR-KEY.                                                        
               10  CURR-FLD-5               PIC X(10).                          
               10  CURR-FLD-4               PIC  9(4).                          
               10  CURR-FLD-3               PIC  9(4).                          
               10  CURR-FLD-3X.                                                 
                   15  CURR-FLD-3-SLS1      PIC  X     VALUE '/'.               
                   15  CURR-FLD-3B          PIC  9(3).                          
                   15  CURR-FLD-3-SLS2      PIC  X     VALUE '/'.               
                   15  CURR-FLD-3A          PIC  9(3).                          
               10  CURR-FLD-2               PIC  9(4).                          
               10  CURR-FLD-2X.                                                 
                   15  CURR-FLD-2-SLS1      PIC  X     VALUE '/'.               
                   15  CURR-FLD-2B          PIC  9(3).                          
                   15  CURR-FLD-2-SLS2      PIC  X     VALUE '/'.               
                   15  CURR-FLD-2A          PIC  9(3).                          
               10  CURR-FLD-1.                                                  
                   15  CURR-FLD-1B          PIC  9(8).                          
                   15  CURR-FLD-1A          PIC S9(5)V99.                       
                   15  CURR-FLD-1C          PIC  9(9).                          
           05  CURR-KEY-X            REDEFINES CURR-KEY.                        
               10  FILLER                   PIC X(23).                          
               10  CURR-FLD-X               PIC X(39).                          
           05  CURR-QTY                     PIC S9(9)    VALUE ZERO.            
           05  CURR-LOC-NM                  PIC X(40)    VALUE SPACES.          
           05  CURR-EXTD-AMT                PIC S9(9)V99 VALUE ZERO.            
W28545     05  CURR-LATE-IND                PIC X        VALUE SPACES.          
                                                                                
       01  NEXT-KEY-INFO.                                                       
           05  NEXT-KEY.                                                        
               10  NEXT-FLD-5               PIC X(10).                          
               10  NEXT-FLD-4               PIC  9(4).                          
               10  NEXT-FLD-3               PIC  9(4).                          
               10  NEXT-FLD-3X.                                                 
                   15  NEXT-FLD-3-SLS1      PIC  X     VALUE '/'.               
                   15  NEXT-FLD-3B          PIC  9(3).                          
                   15  NEXT-FLD-3-SLS2      PIC  X     VALUE '/'.               
                   15  NEXT-FLD-3A          PIC  9(3).                          
               10  NEXT-FLD-2               PIC  9(4).                          
               10  NEXT-FLD-2X.                                                 
                   15  NEXT-FLD-2-SLS1      PIC  X     VALUE '/'.               
                   15  NEXT-FLD-2B          PIC  9(3).                          
                   15  NEXT-FLD-2-SLS2      PIC  X     VALUE '/'.               
                   15  NEXT-FLD-2A          PIC  9(3).                          
               10  NEXT-FLD-1.                                                  
                   15  NEXT-FLD-1B          PIC  9(8).                          
                   15  NEXT-FLD-1A          PIC S9(5)V99.                       
                   15  NEXT-FLD-1C          PIC  9(9).                          
           05  NEXT-KEY-X            REDEFINES NEXT-KEY.                        
               10  FILLER                   PIC X(23).                          
               10  NEXT-FLD-X               PIC X(39).                          
           05  NEXT-QTY                     PIC S9(9)    VALUE ZERO.            
           05  NEXT-LOC-NM                  PIC X(40)    VALUE SPACES.          
           05  NEXT-EXTD-AMT                PIC S9(9)V99 VALUE ZERO.            
W28545     05  NEXT-LATE-IND                PIC X        VALUE SPACES.          
                                                                                
                                                                                
      *----------------------------------------------------------------         
      *    REPORTING AREA.                                            -         
      *----------------------------------------------------------------         
                                                                                
      *01  DP132O-STANDRD-HEADER-132-COLS.                                      
           COPY DPWS132O.                                                       
                                                                                
       01  REPORT-HEAD-01.                                                      
           05  FILLER                  PIC X(41)  VALUE SPACES.                 
           05  HD01-HEAD-1             PIC X(39).                               
           05  HD01-HEAD-2             PIC X(38).                               
                                                                                
       01  REPORT-HEAD-02.                                                      
           05  FILLER                  PIC X(01)  VALUE SPACES.                 
           05  FILLER                  PIC X(12)  VALUE 'INV DATE  - '.         
           05  HD02-DATE               PIC X(10).                               
                                                                                
       01  REPORT-HEAD-03.                                                      
           05  FILLER                  PIC X(01)  VALUE SPACES.                 
           05  FILLER                  PIC X(04)  VALUE 'LOC '.                 
           05  HD03-LOC-NBR            PIC 9(04)  BLANK WHEN ZERO.              
           05  FILLER                  PIC X(04)  VALUE '  - '.                 
           05  HD03-LOC-DESC           PIC X(50).                               
                                                                                
       01  REPORT-HEAD-A1.                                                      
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  HDA1-FROM               PIC X(11) VALUE 'FROM DPT/CL'.           
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  HDA1-TO                 PIC X(11) VALUE 'TO DPT/CL'.             
           05  FILLER                  PIC X(5)  VALUE SPACES.                  
           05  FILLER                  PIC X(3)  VALUE 'SKU'.                   
           05  FILLER                  PIC X(7)  VALUE SPACES.                  
           05  FILLER                  PIC X(3)  VALUE 'QTY'.                   
           05  FILLER                  PIC X(7)  VALUE SPACES.                  
           05  FILLER                  PIC X(8)  VALUE 'UNIT RTL'.              
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  FILLER                  PIC X(8)  VALUE 'AREA NBR'.              
           05  FILLER                  PIC X(8)  VALUE SPACES.                  
           05  FILLER                  PIC X(11) VALUE 'EXTD AMOUNT'.           
                                                                                
       01  REPORT-DTL-A1.                                                       
W28545     05  FILLER                  PIC X(01)  VALUE SPACES.                 
W28545     05  DTA1-LATE               PIC X.                                   
W28545     05  FILLER                  PIC X(03)  VALUE SPACES.                 
           05  DTA1-A-DEPT             PIC Z999.                                
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  DTA1-A-MCL              PIC Z99.                                 
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  DTA1-A-SCL              PIC Z99.                                 
           05  FILLER                  PIC X(2)  VALUE SPACES.                  
           05  DTA1-B-DEPT             PIC Z999.                                
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  DTA1-B-MCL              PIC Z99.                                 
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  DTA1-B-SCL              PIC Z99.                                 
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  DTA1-SKU                PIC 9(8).                                
           05  FILLER                  PIC X(3)  VALUE SPACES.                  
W28545     05  DTA1-QTY                PIC Z(4)9.                               
           05  FILLER                  PIC X(3)  VALUE SPACES.                  
           05  DTA1-UNIT-RETAIL        PIC $$$$,$$$.99-.                        
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
W28545     05  DTA1-AREA               PIC Z(8)9.                               
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  DTA1-EXTD-AMT           PIC $$$$,$$$,$$$.99-.                    
                                                                                
       01  REPORT-MCL-TOTS-A1.                                                  
           05  FILLER                  PIC X(06)  VALUE SPACES.                 
           05  FILLER                  PIC X(12)  VALUE                         
               'MCL TOTALS ('.                                                  
           05  TA1-MCL-MCL             PIC Z99.                                 
           05  FILLER                  PIC X(25)  VALUE '):'.                   
           05  TA1-MCL-QTY             PIC Z(4)9.                               
           05  FILLER                  PIC X(32)  VALUE SPACES.                 
           05  TA1-MCL-EXTD-AMT        PIC $$$$,$$$,$$$.99-.                    
                                                                                
       01  REPORT-DEPT-TOTS-A1.                                                 
           05  FILLER                  PIC X(04)  VALUE SPACES.                 
           05  FILLER                  PIC X(13)  VALUE                         
               'DEPT TOTALS ('.                                                 
           05  TA1-DEPT-DEPT           PIC Z999.                                
           05  FILLER                  PIC X(25)  VALUE '):'.                   
           05  TA1-DEPT-QTY            PIC Z(4)9.                               
           05  FILLER                  PIC X(32)  VALUE SPACES.                 
           05  TA1-DEPT-EXTD-AMT       PIC $$$$,$$$,$$$.99-.                    
                                                                                
       01  REPORT-LOC-TOTS-A1.                                                  
           05  FILLER                  PIC X(02)  VALUE SPACES.                 
           05  FILLER                  PIC X(15)  VALUE                         
               '  LOC TOTALS ('.                                                
           05  TA1-LOC-LOC             PIC 9999.                                
           05  FILLER                  PIC X(25)  VALUE '):'.                   
           05  TA1-LOC-QTY             PIC Z(4)9.                               
           05  FILLER                  PIC X(32)  VALUE SPACES.                 
           05  TA1-LOC-EXTD-AMT        PIC $$$$,$$$,$$$.99-.                    
                                                                                
       01  REPORT-FINAL-TOTS-A1.                                                
           05  FILLER                  PIC X(01)  VALUE SPACES.                 
           05  FILLER                  PIC X(20)  VALUE                         
               'TOTALS FOR INV DATE '.                                          
           05  TA1-FINAL-LOC-DATE      PIC X(10).                               
           05  FILLER                  PIC X(15)  VALUE ':'.                    
           05  TA1-FINAL-QTY           PIC Z(4)9.                               
           05  FILLER                  PIC X(32)  VALUE SPACES.                 
           05  TA1-FINAL-EXTD-AMT      PIC $$$$,$$$,$$$.99-.                    
                                                                                
       01  REPORT-SUM-HEAD-A1.                                                  
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  FILLER                  PIC X(54) VALUE ' LOC '.                 
           05  FILLER                  PIC X(10) VALUE 'DEPARTMENT'.            
           05  FILLER                  PIC X(17) VALUE SPACES.                  
           05  FILLER                  PIC X(11) VALUE 'EXTD AMOUNT'.           
                                                                                
       01  REPORT-SUM-DEPT.                                                     
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  SUM-DEPT-LOC            PIC Z999.                                
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  SUM-DEPT-LOC-NM         PIC X(50).                               
           05  SUM-DEPT                PIC Z999.                                
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  SUM-DEPT-EXTD-AMT       PIC $$$,$$$,$$$,$$$.99-.                 
                                                                                
       01  REPORT-SUM-STORE.                                                    
           05  FILLER                  PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE 'TOTAL ('.               
           05  SUM-STORE               PIC Z999.                                
           05  FILLER                  PIC X(61) VALUE ') :'.                   
           05  SUM-LOC-EXTD-AMT        PIC $$$,$$$,$$$,$$$.99-.                 
                                                                                
       01  REPORT-SUM-DATE.                                                     
           05  FILLER                  PIC X(21) VALUE                          
               ' TOTALS FOR INV DATE '.                                         
           05  SUM-DATE                PIC X(10).                               
           05  FILLER                  PIC X(51) VALUE ' :'.                    
           05  SUM-DTE-EXTD-AMT        PIC $$$,$$$,$$$,$$$.99-.                 
                                                                                
       01  BLANK-LINE                  PIC XX     VALUE SPACES.                 
                                                                                
      *01  IN144-SKU-RECLASS-EXTRACT.                                           
           COPY INRD144.                                                        
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------         
      *   PROGRAM MAINLINE                                                      
      *----------------------------------------------------------------         
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-SETUP-PROGRAM.                                          
           PERFORM 2100-PROCESS-BRK-5                                           
               UNTIL IN-KEY = HIGH-VALUES                                       
                  OR PS-END-OF-FILE.                                            
           PERFORM 1200-CLEANUP-PROGRAM.                                        
           GOBACK.                                                              
                                                                                
       1000-SETUP-PROGRAM.                                                      
           OPEN INPUT  RECLS-EXTRACT-FILE                                       
                OUTPUT SKU-RECLS-DTL-RPT-FILE                                   
                       SKU-RECLS-SUM-RPT-FILE.                                  
                                                                                
           ACCEPT PV-CURRENT-DATE         FROM DATE.                            
           ACCEPT PV-CURRENT-TIME         FROM TIME.                            
           MOVE PV-CURR-YY                  TO DP132O-RUN-YEAR.                 
           MOVE PV-CURR-MM                  TO DP132O-RUN-MONTH                 
           MOVE PV-CURR-DD                  TO DP132O-RUN-DAY.                  
           MOVE PV-CURR-HR                  TO DP132O-RUN-HOUR.                 
           MOVE PV-CURR-MN                  TO DP132O-RUN-MINUTE.               
           MOVE PC-PROGRAM-NAME             TO DP132O-PROGRAM-NAME.             
           INITIALIZE CURR-KEY-INFO.                                            
           MOVE LOW-VALUES                  TO PREV-KEY-INFO.                   
           PERFORM 5000-READ-IN-REC.                                            
           IF PS-END-OF-FILE-NO                                                 
               PERFORM 5500-MOVE-IN-TO-NEXT                                     
               PERFORM 5600-MOVE-NEXT-TO-CURR                                   
               PERFORM 7000-PRINT-HEADINGS                                      
               PERFORM 2800-CHECK-FOR-CONTROL-BREAK                             
               PERFORM 7500-PRINT-SUM-HEADS-1                                   
           END-IF.                                                              
                                                                                
       1200-CLEANUP-PROGRAM.                                                    
           CLOSE RECLS-EXTRACT-FILE                                             
                 SKU-RECLS-DTL-RPT-FILE                                         
                 SKU-RECLS-SUM-RPT-FILE.                                        
                                                                                
      *----------------------------------------------------------------         
      *  INVENTORY DATE BREAK - DETAIL AND SUMMARY REPORTS                      
      *----------------------------------------------------------------         
       2100-PROCESS-BRK-5.                                                      
           IF IN-FLD-5 NOT EQUAL HIGH-VALUES                                    
               PERFORM 3100-INITIALIZE-BRK-5                                    
               PERFORM 2200-PROCESS-BRK-4 UNTIL BREAK-5                         
               PERFORM 7170-PRINT-DATE-TOTALS-A                                 
               PERFORM 4100-BRK-5-SUMMARY                                       
               MOVE IN144-INV-DATE        TO IN-FLD-5                           
               PERFORM 5500-MOVE-IN-TO-NEXT                                     
               PERFORM 5600-MOVE-NEXT-TO-CURR                                   
               PERFORM 7500-PRINT-SUM-HEADS-1                                   
               PERFORM 7000-PRINT-HEADINGS                                      
                                                                                
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------         
      *  STORE(LOC) BREAK - DETAIL AND SUMMARY REPORTS                          
      *----------------------------------------------------------------         
       2200-PROCESS-BRK-4.                                                      
           PERFORM 3200-INITIALIZE-BRK-4.                                       
           PERFORM 2300-PROCESS-BRK-3 UNTIL BREAK-4.                            
           PERFORM 7160-PRINT-STORE-TOTALS-A.                                   
           PERFORM 4200-BRK-4-SUMMARY.                                          
           MOVE IN144-LOC-NBR               TO IN-FLD-4                         
           MOVE IN144-LOC-NM                TO IN-LOC-NM.                       
                                                                                
           IF NOT BREAK-5                                                       
               PERFORM 5500-MOVE-IN-TO-NEXT                                     
               PERFORM 5600-MOVE-NEXT-TO-CURR                                   
               PERFORM 7000-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------         
      *  'FROM' OR 'TO' DEPT BREAK - DETAIL AND SUMMARY REPORTS                 
      *    DEPENDING ON IN144-REPORT-IND                                        
      *    PRINT DEPT TOTALS                                                    
      *----------------------------------------------------------------         
       2300-PROCESS-BRK-3.                                                      
           PERFORM 3300-INITIALIZE-BRK-3.                                       
           PERFORM 2400-PROCESS-BRK-2 UNTIL BREAK-3.                            
           PERFORM 7140-PRINT-DEPT-TOTALS-A.                                    
           PERFORM 4300-BRK-3-SUMMARY.                                          
                                                                                
      *----------------------------------------------------------------         
      * 'FROM' OR 'TO' MCL BREAK - DETAIL REPORTS ONLY                          
      *    DEPENDING ON IN144-REPORT-IND                                        
      *    PRINT MCL TOTALS                                                     
      *----------------------------------------------------------------         
       2400-PROCESS-BRK-2.                                                      
           PERFORM 3400-INITIALIZE-BRK-2.                                       
           PERFORM 2500-PROCESS-BRK-1 UNTIL BREAK-2.                            
           PERFORM 7120-PRINT-MCL-TOTALS-A.                                     
                                                                                
           PERFORM 4400-BRK-2-SUMMARY.                                          
      *----------------------------------------------------------------         
      *  SKU BREAK - DETAIL REPORTS ONLY                                        
      *    PRINT DETAIL LINE                                                    
      *----------------------------------------------------------------         
       2500-PROCESS-BRK-1.                                                      
           PERFORM 3500-INITIALIZE-BRK-1.                                       
           PERFORM 2600-PROCESS-DETAIL UNTIL BREAK-1.                           
           PERFORM 7100-PRINT-DETAIL-LINE-A                                     
           PERFORM 5500-MOVE-IN-TO-NEXT                                         
                                                                                
           PERFORM 4500-BRK-1-SUMMARY.                                          
      *----------------------------------------------------------------         
      *  AREA, SKU, UNIT-RETAIL BREAK - DETAIL REPORTS ONLY                     
      *    PRINT DETAIL LINE                                                    
      *----------------------------------------------------------------         
       2600-PROCESS-DETAIL.                                                     
                                                                                
           IF IN144-AREA-NUMBER = IN-FLD-1C                                     
           AND IN144-SKU        = IN-FLD-1B                                     
           AND IN144-UNIT-RETAIL = IN-FLD-1A                                    
W28545         MOVE IN144-LOC-NM            TO IN-LOC-NM                        
               ADD IN144-UNIT-RETAIL        TO IN-FLD-1A                        
               MOVE IN144-SKU               TO IN-FLD-1B                        
               MOVE IN144-AREA-NUMBER       TO IN-FLD-1C                        
               ADD PA-EXTD-AMT              TO IN-EXTD-AMT                      
               ADD IN144-QUANTITY           TO IN-QTY                           
W28545         MOVE IN144-LATE-RECLS-IND    TO IN-LATE-IND                      
               PERFORM 5000-READ-IN-REC                                         
           ELSE                                                                 
               MOVE IN144-LOC-NM            TO IN-LOC-NM                        
               MOVE IN144-UNIT-RETAIL       TO IN-FLD-1A                        
               MOVE IN144-SKU               TO IN-FLD-1B                        
               MOVE IN144-AREA-NUMBER       TO IN-FLD-1C                        
               MOVE PA-EXTD-AMT             TO IN-EXTD-AMT                      
               MOVE IN144-QUANTITY          TO IN-QTY                           
W28545         MOVE IN144-LATE-RECLS-IND    TO IN-LATE-IND                      
           END-IF.                                                              
                                                                                
           PERFORM 2800-CHECK-FOR-CONTROL-BREAK.                                
                                                                                
      *                                                                         
       2800-CHECK-FOR-CONTROL-BREAK.                                            
           EVALUATE TRUE                                                        
               WHEN IN-KEY-INFO      EQUAL TO HIGH-VALUES                       
                    MOVE 99                TO CONTROL-BREAK-SW                  
               WHEN IN-FLD-5     NOT EQUAL TO NEXT-FLD-5                        
                    MOVE 5                 TO CONTROL-BREAK-SW                  
               WHEN IN-FLD-4     NOT EQUAL TO NEXT-FLD-4                        
                    MOVE 4                 TO CONTROL-BREAK-SW                  
               WHEN IN-FLD-3     NOT EQUAL TO NEXT-FLD-3                        
                    MOVE 3                 TO CONTROL-BREAK-SW                  
               WHEN IN-FLD-3B    NOT EQUAL TO NEXT-FLD-3B                       
                    MOVE 2                 TO CONTROL-BREAK-SW                  
               WHEN IN-FLD-X     NOT EQUAL TO NEXT-FLD-X                        
                    MOVE 1                 TO CONTROL-BREAK-SW                  
               WHEN OTHER                                                       
                    MOVE ZEROS             TO CONTROL-BREAK-SW                  
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------         
      *  INVENTORY DATE                                                         
      *----------------------------------------------------------------         
       3100-INITIALIZE-BRK-5.                                                   
           MOVE ZEROS                       TO CONTROL-BREAK-SW.                
           MOVE NEXT-FLD-5                  TO CURR-FLD-5.                      
                                                                                
      *----------------------------------------------------------------         
      *  STORE(LOC) BREAK                                                       
      *----------------------------------------------------------------         
       3200-INITIALIZE-BRK-4.                                                   
           MOVE ZEROS                       TO CONTROL-BREAK-SW.                
           MOVE NEXT-FLD-4                  TO CURR-FLD-4.                      
           MOVE NEXT-LOC-NM                 TO CURR-LOC-NM.                     
                                                                                
      *----------------------------------------------------------------         
      *  'FROM' OR 'TO' DEPT BREAK                                              
      *----------------------------------------------------------------         
       3300-INITIALIZE-BRK-3.                                                   
           MOVE ZEROS                       TO CONTROL-BREAK-SW.                
           MOVE NEXT-FLD-3                  TO CURR-FLD-3.                      
                                                                                
      *----------------------------------------------------------------         
      * 'FROM' OR 'TO' MCL BREAK                                                
      *----------------------------------------------------------------         
       3400-INITIALIZE-BRK-2.                                                   
           MOVE ZEROS                       TO CONTROL-BREAK-SW.                
           MOVE NEXT-FLD-3B                 TO CURR-FLD-3B.                     
                                                                                
      *----------------------------------------------------------------         
      * SKU BREAK                                                               
      *----------------------------------------------------------------         
       3500-INITIALIZE-BRK-1.                                                   
           MOVE ZEROS                       TO CONTROL-BREAK-SW.                
           MOVE NEXT-FLD-X                  TO CURR-FLD-X.                      
           ADD PA-D1-QTY                    TO PA-T1-QTY.                       
           ADD PA-D1-UNIT-RETAIL            TO PA-T1-UNIT-RETAIL.               
           ADD PA-D1-EXTD-AMT               TO PA-T1-EXTD-AMT.                  
                                                                                
           MOVE ZERO                        TO PA-D1-QTY                        
                                               PA-D1-UNIT-RETAIL                
                                               PA-D1-EXTD-AMT.                  
      *----------------------------------------------------------------         
      *  INVENTORY DATE                                                         
      *----------------------------------------------------------------         
       4100-BRK-5-SUMMARY.                                                      
           MOVE CURR-FLD-5               TO SUM-DATE                            
           WRITE SKU-RECLS-SUM-RPT-REC                                          
             FROM REPORT-SUM-DATE                                               
               AFTER ADVANCING 2 LINES.                                         
           SET PS-SUM-DOUBLE-SPACE       TO TRUE.                               
                                                                                
                                                                                
      *----------------------------------------------------------------         
      *  STORE(LOC) BREAK                                                       
      *----------------------------------------------------------------         
       4200-BRK-4-SUMMARY.                                                      
           MOVE CURR-FLD-4               TO SUM-STORE                           
                                                                                
           IF PA-SUM-LINE-CNT > PC-LINES-PER-PAGE                               
               PERFORM 7500-PRINT-SUM-HEADS-1.                                  
           WRITE SKU-RECLS-SUM-RPT-REC                                          
             FROM REPORT-SUM-STORE                                              
               AFTER ADVANCING 2 LINES.                                         
           SET PS-SUM-DOUBLE-SPACE       TO TRUE.                               
                                                                                
           IF IN-KEY-INFO NOT EQUAL TO HIGH-VALUES                              
               MOVE IN-FLD-4             TO SUM-DEPT-LOC                        
               MOVE IN-LOC-NM            TO SUM-DEPT-LOC-NM                     
               WRITE SKU-RECLS-SUM-RPT-REC FROM REPORT-SUM-HEAD-A1              
                   AFTER ADVANCING 2 LINES                                      
W28545        ADD 3                       TO PA-SUM-LINE-CNT                    
W28545     END-IF.                                                              
                                                                                
      *----------------------------------------------------------------         
      *  'FROM' OR 'TO' DEPT BREAK                                              
      *----------------------------------------------------------------         
       4300-BRK-3-SUMMARY.                                                      
           MOVE CURR-FLD-3               TO SUM-DEPT                            
                                                                                
           IF PA-SUM-LINE-CNT > PC-LINES-PER-PAGE                               
               PERFORM 7500-PRINT-SUM-HEADS-1.                                  
           IF PS-SUM-DOUBLE-SPACE                                               
               MOVE IN-LOC-NM            TO SUM-DEPT-LOC-NM                     
               SET PS-SUM-SINGLE-SPACE  TO TRUE                                 
               WRITE SKU-RECLS-SUM-RPT-REC                                      
                 FROM REPORT-SUM-DEPT                                           
                   AFTER ADVANCING 2 LINES                                      
               MOVE SPACES              TO SUM-DEPT-LOC-NM                      
               ADD 2                    TO PA-SUM-LINE-CNT                      
           ELSE                                                                 
               WRITE SKU-RECLS-SUM-RPT-REC                                      
                 FROM REPORT-SUM-DEPT                                           
                   AFTER ADVANCING 1 LINES                                      
               MOVE SPACES              TO SUM-DEPT-LOC-NM                      
               ADD 1                    TO PA-SUM-LINE-CNT                      
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------         
      * 'FROM' OR 'TO' MCL BREAK                                                
      *----------------------------------------------------------------         
       4400-BRK-2-SUMMARY.                                                      
           MOVE SPACE                  TO BLANK-LINE.                           
                                                                                
      *----------------------------------------------------------------         
      * SKU BREAK                                                               
      *----------------------------------------------------------------         
       4500-BRK-1-SUMMARY.                                                      
                                                                                
           MOVE SPACE                  TO BLANK-LINE.                           
                                                                                
                                                                                
       5000-READ-IN-REC.                                                        
           IF PS-END-OF-FILE-NO                                                 
               READ RECLS-EXTRACT-FILE                                          
                   INTO IN144-SKU-RECLASS-EXTRACT                               
               AT END                                                           
                   MOVE HIGH-VALUES                 TO IN-KEY-INFO.             
                                                                                
           IF IN-KEY-INFO = HIGH-VALUES                                         
               SET PS-END-OF-FILE               TO TRUE                         
           ELSE                                                                 
               ADD 1                            TO PA-RECS-READ                 
               EVALUATE IN144-REPORT-IND                                        
                 WHEN '1'                                                       
                     PERFORM 5100-BUILD-KEY-RPT-1                               
                 WHEN '2'                                                       
                     PERFORM 5200-BUILD-KEY-RPT-2                               
                 WHEN OTHER                                                     
                     DISPLAY 'REPORT INDICATOR INVALID'                         
               END-EVALUATE                                                     
               PERFORM 5900-SEQUENCE-ERR UNTIL                                  
                   IN-KEY IS NOT LESS THAN PREV-IN-KEY                          
           END-IF.                                                              
                                                                                
                                                                                
       5100-BUILD-KEY-RPT-1.                                                    
           MOVE IN144-INV-DATE              TO IN-FLD-5.                        
           MOVE IN144-LOC-NBR               TO IN-FLD-4.                        
           MOVE IN144-OLD-DEPT              TO IN-FLD-3.                        
           MOVE IN144-OLD-MCL               TO IN-FLD-3B.                       
           MOVE IN144-OLD-SCL               TO IN-FLD-3A.                       
           MOVE IN144-NEW-DEPT              TO IN-FLD-2.                        
           MOVE IN144-NEW-MCL               TO IN-FLD-2B.                       
           MOVE IN144-NEW-SCL               TO IN-FLD-2A.                       
           MOVE IN144-AREA-NUMBER           TO IN-FLD-1C.                       
           MOVE IN144-SKU                   TO IN-FLD-1B.                       
           MOVE IN144-LOC-NM                TO IN-LOC-NM.                       
           MOVE IN144-EXTD-AMT              TO IN-EXTD-AMT.                     
           MOVE IN144-QUANTITY              TO IN-QTY.                          
                                                                                
           PERFORM 5300-BUILD-OTHER-FLDS.                                       
                                                                                
                                                                                
       5200-BUILD-KEY-RPT-2.                                                    
           MOVE IN144-INV-DATE              TO IN-FLD-5.                        
           MOVE IN144-LOC-NBR               TO IN-FLD-4.                        
           MOVE IN144-NEW-DEPT              TO IN-FLD-3.                        
           MOVE IN144-NEW-MCL               TO IN-FLD-3B.                       
           MOVE IN144-NEW-SCL               TO IN-FLD-3A.                       
           MOVE IN144-OLD-DEPT              TO IN-FLD-2.                        
           MOVE IN144-OLD-MCL               TO IN-FLD-2B.                       
           MOVE IN144-OLD-SCL               TO IN-FLD-2A.                       
           MOVE IN144-AREA-NUMBER           TO IN-FLD-1C.                       
           MOVE IN144-SKU                   TO IN-FLD-1B.                       
           MOVE IN144-LOC-NM                TO IN-LOC-NM.                       
           MOVE IN144-EXTD-AMT              TO IN-EXTD-AMT.                     
           MOVE IN144-QUANTITY              TO IN-QTY.                          
                                                                                
           PERFORM 5300-BUILD-OTHER-FLDS.                                       
                                                                                
      *----------------------------------------------------------------         
      *  PUT INPUT INTO SAVE AREA FORMATTED CORRECTLY.                          
      *----------------------------------------------------------------         
       5300-BUILD-OTHER-FLDS.                                                   
           IF IN144-UNIT-RETAIL NUMERIC                                         
           AND  IN-FLD-1A NUMERIC                                               
               MOVE IN144-UNIT-RETAIL       TO IN-FLD-1A                        
           END-IF.                                                              
           MOVE IN144-QUANTITY              TO IN-QTY                           
           MOVE IN144-LOC-NM                TO IN-LOC-NM                        
           MOVE IN144-LATE-RECLS-IND        TO IN-LATE-IND.                     
                                                                                
           EVALUATE IN144-REPORT-IND                                            
             WHEN '1'                                                           
               COMPUTE PA-EXTD-AMT = IN144-EXTD-AMT * -1                        
             WHEN '2'                                                           
               MOVE IN144-EXTD-AMT          TO PA-EXTD-AMT                      
           END-EVALUATE.                                                        
                                                                                
           MOVE PA-EXTD-AMT                 TO IN-EXTD-AMT.                     
                                                                                
       5500-MOVE-IN-TO-NEXT.                                                    
           MOVE IN-KEY-INFO             TO NEXT-KEY-INFO.                       
                                                                                
       5600-MOVE-NEXT-TO-CURR.                                                  
           MOVE NEXT-KEY-INFO           TO CURR-KEY-INFO.                       
                                                                                
       5900-SEQUENCE-ERR.                                                       
           DISPLAY '-- INKRP150 - SEQUENCE ERROR --'.                           
           DISPLAY '-- INKRP150 - KEY AREA      = ' IN-KEY-INFO                 
           DISPLAY '-- INKRP150 - PREV KEY AREA = ' PREV-KEY-INFO.              
           MOVE IN-KEY-INFO            TO PREV-KEY-INFO.                        
                                                                                
      *----------------------------------------------------------------         
      *    REPORT ROUTINES                                                      
      *----------------------------------------------------------------         
       7000-PRINT-HEADINGS.                                                     
           ADD 1                       TO PA-PAGE-CNT.                          
           MOVE 1                      TO PA-LINE-CNT.                          
           MOVE PA-PAGE-CNT            TO DP132O-PAGE-NUMBER.                   
           MOVE PC-PROGRAM-NAME        TO DP132O-PROGRAM-NAME.                  
           MOVE IN144-REPORT-IND-R     TO DP132O-REPORT-NUMBER.                 
           WRITE SKU-RECLS-DTL-RPT-REC                                          
             FROM DP132O-STANDRD-HEADER-132-COLS                                
               AFTER ADVANCING PAGE.                                            
                                                                                
           EVALUATE IN144-REPORT-IND                                            
             WHEN '1'                                                           
               MOVE 'TO DPT/CL'        TO HDA1-TO                               
               MOVE 'FROM DPT/CL'      TO HDA1-FROM                             
               PERFORM 7010-PRINT-HEADINGS-1                                    
             WHEN '2'                                                           
               MOVE 'TO DPT/CL'        TO HDA1-FROM                             
               MOVE 'FROM DPT/CL'      TO HDA1-TO                               
               PERFORM 7020-PRINT-HEADINGS-1-TO                                 
             WHEN OTHER                                                         
               MOVE '7000-PRINT-HEADINGS   '  TO PV-PARAGRAPH-NAME              
               PERFORM 9999-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------         
      * HEADINGS FOR 'FROM DEPT' REPORTS                                        
      *----------------------------------------------------------------         
       7010-PRINT-HEADINGS-1.                                                   
           MOVE PC-MAIN-HEAD-2         TO HD01-HEAD-1.                          
           MOVE PC-MAIN-HEAD-FROM      TO HD01-HEAD-2                           
                                                                                
           WRITE SKU-RECLS-DTL-RPT-REC FROM REPORT-HEAD-01                      
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           ADD 1                       TO PA-LINE-CNT.                          
           IF IN-FLD-5 NOT EQUAL HIGH-VALUES                                    
               MOVE CURR-FLD-5         TO HD02-DATE                             
               MOVE CURR-FLD-4         TO HD03-LOC-NBR                          
               MOVE CURR-LOC-NM        TO HD03-LOC-DESC                         
               PERFORM 7090-GENERAL-HEADINGS.                                   
                                                                                
      *----------------------------------------------------------------         
      * HEADINGS FOR 'TO DEPT' REPORTS                                          
      *----------------------------------------------------------------         
       7020-PRINT-HEADINGS-1-TO.                                                
           MOVE PC-MAIN-HEAD-2         TO HD01-HEAD-1.                          
           MOVE PC-MAIN-HEAD-TO        TO HD01-HEAD-2                           
                                                                                
           WRITE SKU-RECLS-DTL-RPT-REC FROM REPORT-HEAD-01                      
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           ADD 1                       TO PA-LINE-CNT.                          
           IF IN-FLD-5 NOT EQUAL HIGH-VALUES                                    
               MOVE CURR-FLD-5         TO HD02-DATE                             
               MOVE CURR-FLD-4         TO HD03-LOC-NBR                          
               MOVE CURR-LOC-NM        TO HD03-LOC-DESC                         
               PERFORM 7090-GENERAL-HEADINGS.                                   
                                                                                
      *----------------------------------------------------------------         
      * HEADINGS FOR 'FROM AND TO DEPT' REPORTS                                 
      *----------------------------------------------------------------         
       7090-GENERAL-HEADINGS.                                                   
           WRITE SKU-RECLS-DTL-RPT-REC FROM REPORT-HEAD-02                      
               AFTER ADVANCING 2 LINES.                                         
           ADD 2                       TO PA-LINE-CNT.                          
                                                                                
           WRITE SKU-RECLS-DTL-RPT-REC FROM REPORT-HEAD-03                      
               AFTER ADVANCING 1 LINES.                                         
           ADD 1                       TO PA-LINE-CNT.                          
                                                                                
           WRITE SKU-RECLS-DTL-RPT-REC FROM REPORT-HEAD-A1                      
               AFTER ADVANCING 2 LINES.                                         
           ADD 2                       TO PA-LINE-CNT.                          
                                                                                
           WRITE SKU-RECLS-DTL-RPT-REC FROM BLANK-LINE                          
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           ADD 1                       TO PA-LINE-CNT.                          
      *----------------------------------------------------------------         
      *  DETAIL LINE FOR 'FROM AND TO DEPT' REPORTS                             
      *----------------------------------------------------------------         
       7100-PRINT-DETAIL-LINE-A.                                                
           MOVE NEXT-FLD-3             TO DTA1-A-DEPT.                          
           MOVE NEXT-FLD-3B            TO DTA1-A-MCL.                           
           MOVE NEXT-FLD-3A            TO DTA1-A-SCL.                           
           MOVE NEXT-FLD-2             TO DTA1-B-DEPT.                          
           MOVE NEXT-FLD-2B            TO DTA1-B-MCL.                           
           MOVE NEXT-FLD-2A            TO DTA1-B-SCL.                           
           MOVE NEXT-FLD-1C            TO DTA1-AREA.                            
           MOVE NEXT-FLD-1B            TO DTA1-SKU.                             
           MOVE NEXT-FLD-1A            TO DTA1-UNIT-RETAIL.                     
           MOVE NEXT-EXTD-AMT          TO DTA1-EXTD-AMT                         
           MOVE NEXT-QTY               TO DTA1-QTY.                             
W28545     MOVE NEXT-LATE-IND          TO DTA1-LATE.                            
                                                                                
           ADD  NEXT-FLD-1A         TO PA-T1-UNIT-RETAIL                        
           ADD  NEXT-EXTD-AMT       TO PA-T1-EXTD-AMT                           
           ADD  NEXT-QTY            TO PA-T1-QTY.                               
                                                                                
           IF PS-DOUBLE-SPACE                                                   
               SET PS-SINGLE-SPACE        TO TRUE                               
               WRITE SKU-RECLS-DTL-RPT-REC FROM REPORT-DTL-A1                   
                   AFTER ADVANCING 2 LINE                                       
               ADD 2                       TO PA-LINE-CNT                       
           ELSE                                                                 
               WRITE SKU-RECLS-DTL-RPT-REC FROM REPORT-DTL-A1                   
                   AFTER ADVANCING 1 LINE                                       
               ADD 1                       TO PA-LINE-CNT                       
           END-IF.                                                              
           IF PA-LINE-CNT > PC-LINES-PER-PAGE                                   
               PERFORM 7000-PRINT-HEADINGS.                                     
                                                                                
W28545     MOVE SPACES                 TO DTA1-LATE.                            
      *----------------------------------------------------------------         
      *  TOTAL LINES FOR 'FROM' REPORTS                                         
      *----------------------------------------------------------------         
       7120-PRINT-MCL-TOTALS-A.                                                 
           MOVE CURR-FLD-3B              TO TA1-MCL-MCL.                        
           MOVE PA-T1-QTY                TO TA1-MCL-QTY                         
           MOVE PA-T1-EXTD-AMT           TO TA1-MCL-EXTD-AMT.                   
                                                                                
           ADD  PA-T1-UNIT-RETAIL        TO PA-T2-UNIT-RETAIL                   
           ADD  PA-T1-EXTD-AMT           TO PA-T2-EXTD-AMT                      
           ADD  PA-T1-QTY                TO PA-T2-QTY.                          
                                                                                
           MOVE ZEROS                    TO PA-T1-UNIT-RETAIL                   
                                            PA-T1-EXTD-AMT                      
                                            PA-T1-QTY.                          
           WRITE SKU-RECLS-DTL-RPT-REC FROM REPORT-MCL-TOTS-A1                  
               AFTER ADVANCING 2 LINES.                                         
           SET PS-DOUBLE-SPACE         TO TRUE                                  
           ADD 2                       TO PA-LINE-CNT.                          
           IF PA-LINE-CNT > PC-LINES-PER-PAGE                                   
               PERFORM 7000-PRINT-HEADINGS.                                     
                                                                                
       7140-PRINT-DEPT-TOTALS-A.                                                
           MOVE CURR-FLD-3               TO TA1-DEPT-DEPT.                      
           MOVE PA-T2-QTY                TO TA1-DEPT-QTY                        
           MOVE PA-T2-EXTD-AMT           TO TA1-DEPT-EXTD-AMT                   
                                            SUM-DEPT-EXTD-AMT.                  
                                                                                
           ADD  PA-T2-UNIT-RETAIL        TO PA-T3-UNIT-RETAIL                   
           ADD  PA-T2-EXTD-AMT           TO PA-T3-EXTD-AMT                      
           ADD  PA-T2-QTY                TO PA-T3-QTY.                          
                                                                                
           MOVE ZEROS                    TO PA-T2-UNIT-RETAIL                   
                                            PA-T2-EXTD-AMT                      
                                            PA-T2-QTY.                          
           WRITE SKU-RECLS-DTL-RPT-REC FROM REPORT-DEPT-TOTS-A1                 
               AFTER ADVANCING 2 LINES.                                         
           SET PS-DOUBLE-SPACE         TO TRUE                                  
           ADD 2                       TO PA-LINE-CNT.                          
           IF PA-LINE-CNT > PC-LINES-PER-PAGE                                   
               PERFORM 7000-PRINT-HEADINGS.                                     
                                                                                
       7160-PRINT-STORE-TOTALS-A.                                               
           MOVE CURR-FLD-4               TO TA1-LOC-LOC.                        
           MOVE PA-T3-QTY                TO TA1-LOC-QTY                         
           MOVE PA-T3-EXTD-AMT           TO TA1-LOC-EXTD-AMT                    
                                            SUM-LOC-EXTD-AMT                    
                                                                                
           ADD  PA-T3-UNIT-RETAIL        TO PA-T4-UNIT-RETAIL                   
           ADD  PA-T3-EXTD-AMT           TO PA-T4-EXTD-AMT                      
           ADD  PA-T3-QTY                TO PA-T4-QTY.                          
                                                                                
           MOVE ZEROS                    TO PA-T3-UNIT-RETAIL                   
                                            PA-T3-EXTD-AMT                      
                                            PA-T3-QTY.                          
                                                                                
           WRITE SKU-RECLS-DTL-RPT-REC FROM REPORT-LOC-TOTS-A1                  
               AFTER ADVANCING 2 LINES.                                         
           SET PS-DOUBLE-SPACE         TO TRUE                                  
           ADD 2                       TO PA-LINE-CNT.                          
                                                                                
       7170-PRINT-DATE-TOTALS-A.                                                
           MOVE ZEROS                  TO HD03-LOC-NBR.                         
           MOVE SPACES                 TO HD03-LOC-DESC.                        
           MOVE CURR-FLD-5             TO HD02-DATE                             
                                          TA1-FINAL-LOC-DATE.                   
           WRITE SKU-RECLS-DTL-RPT-REC FROM REPORT-HEAD-02                      
               AFTER ADVANCING 2 LINES.                                         
           ADD 2                       TO PA-LINE-CNT.                          
                                                                                
           MOVE PA-T4-QTY                TO TA1-FINAL-QTY                       
           MOVE PA-T4-EXTD-AMT           TO TA1-FINAL-EXTD-AMT                  
                                            SUM-DTE-EXTD-AMT                    
                                                                                
           ADD  PA-T4-UNIT-RETAIL        TO PA-T5-UNIT-RETAIL                   
           ADD  PA-T4-EXTD-AMT           TO PA-T5-EXTD-AMT                      
           ADD  PA-T4-QTY                TO PA-T5-QTY.                          
                                                                                
           MOVE ZEROS                    TO PA-T4-UNIT-RETAIL                   
                                            PA-T4-EXTD-AMT                      
                                            PA-T4-QTY.                          
                                                                                
           WRITE SKU-RECLS-DTL-RPT-REC FROM REPORT-FINAL-TOTS-A1                
               AFTER ADVANCING 2 LINES.                                         
                                                                                
                                                                                
       7180-PRINT-FINAL-TOTALS-A.                                               
           MOVE ZEROS                  TO HD03-LOC-NBR.                         
           MOVE 'CORPORATE'            TO HD03-LOC-DESC.                        
           MOVE CURR-FLD-5             TO HD02-DATE                             
                                          TA1-FINAL-LOC-DATE.                   
           PERFORM 7090-GENERAL-HEADINGS.                                       
                                                                                
           MOVE PA-T5-QTY                TO TA1-FINAL-QTY                       
           MOVE PA-T5-EXTD-AMT           TO TA1-FINAL-EXTD-AMT                  
                                            SUM-DTE-EXTD-AMT                    
                                                                                
           WRITE SKU-RECLS-DTL-RPT-REC FROM REPORT-FINAL-TOTS-A1                
               AFTER ADVANCING 2 LINES.                                         
      *----------------------------------------------------------------         
      *  HEADINGS FOR 'FROM AND TO DEPT' SUMMARY REPORTS                        
      *----------------------------------------------------------------         
       7500-PRINT-SUM-HEADS-1.                                                  
                                                                                
           EVALUATE IN144-REPORT-IND                                            
             WHEN '1'                                                           
               MOVE 3               TO DP132O-REPORT-NUMBER                     
             WHEN '2'                                                           
               MOVE 4               TO DP132O-REPORT-NUMBER                     
             WHEN OTHER                                                         
               MOVE '7500-PRINT-SUM-HEADS-1'  TO PV-PARAGRAPH-NAME              
               PERFORM 9999-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
           ADD 1                       TO PA-SUM-PAGE-CNT.                      
           MOVE 1                      TO PA-SUM-LINE-CNT.                      
           MOVE PA-SUM-PAGE-CNT        TO DP132O-PAGE-NUMBER.                   
           WRITE SKU-RECLS-SUM-RPT-REC                                          
             FROM DP132O-STANDRD-HEADER-132-COLS                                
               AFTER ADVANCING PAGE.                                            
                                                                                
           EVALUATE IN144-REPORT-IND                                            
             WHEN '1'                                                           
               PERFORM 7510-PRINT-SUM-HEADS-2-FROM                              
             WHEN '2'                                                           
               PERFORM 7520-PRINT-SUM-HEADS-2-TO                                
             WHEN OTHER                                                         
               CONTINUE                                                         
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------         
      * HEADINGS FOR 'FROM DEPT' SUMMARY REPORTS                                
      *----------------------------------------------------------------         
       7510-PRINT-SUM-HEADS-2-FROM.                                             
           MOVE PC-SUM-HEAD-2          TO HD01-HEAD-1.                          
           MOVE PC-MAIN-HEAD-FROM      TO HD01-HEAD-2.                          
           WRITE SKU-RECLS-SUM-RPT-REC FROM REPORT-HEAD-01                      
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           MOVE 1                      TO PA-SUM-LINE-CNT.                      
           PERFORM 7550-GENL-SUM-HEADINGS.                                      
                                                                                
      *----------------------------------------------------------------         
      * HEADINGS FOR 'TO DEPT' SUMMARY REPORTS                                  
      *----------------------------------------------------------------         
       7520-PRINT-SUM-HEADS-2-TO.                                               
           MOVE PC-SUM-HEAD-2          TO HD01-HEAD-1.                          
           MOVE PC-MAIN-HEAD-TO        TO HD01-HEAD-2.                          
           WRITE SKU-RECLS-SUM-RPT-REC FROM REPORT-HEAD-01                      
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           MOVE 1                      TO PA-SUM-LINE-CNT.                      
           PERFORM 7550-GENL-SUM-HEADINGS.                                      
                                                                                
      *----------------------------------------------------------------         
      * HEADINGS FOR 'FROM AND TO DEPT' REPORTS                                 
      *----------------------------------------------------------------         
       7550-GENL-SUM-HEADINGS.                                                  
           MOVE CURR-FLD-5               TO HD02-DATE.                          
           WRITE SKU-RECLS-SUM-RPT-REC FROM REPORT-HEAD-02                      
               AFTER ADVANCING 2 LINES.                                         
           ADD 2                       TO PA-SUM-LINE-CNT.                      
                                                                                
           MOVE CURR-FLD-4             TO HD03-LOC-NBR                          
                                          SUM-DEPT-LOC                          
           MOVE IN144-LOC-NM           TO HD03-LOC-DESC                         
                                          SUM-DEPT-LOC-NM                       
                                                                                
           WRITE SKU-RECLS-SUM-RPT-REC FROM REPORT-SUM-HEAD-A1                  
               AFTER ADVANCING 2 LINES.                                         
           ADD 2                       TO PA-SUM-LINE-CNT.                      
                                                                                
           WRITE SKU-RECLS-SUM-RPT-REC FROM BLANK-LINE                          
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           ADD 1                       TO PA-SUM-LINE-CNT.                      
       8500-BUILD-HEADER-A.                                                     
           MOVE IN144-INV-DATE           TO PV-PROCESS-DATE.                    
                                                                                
       EJECT                                                                    
       9999-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND **         ** = '.                                 
           DISPLAY '** REPORT INDICATOR ** = ' IN144-REPORT-IND.                
           DISPLAY '** PROGRAM          ** = INKRP150'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           MOVE +4003                      TO ABEND-CODE.                       
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
