      *----------------------------------------------------------               
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------               
       PROGRAM-ID.             PEKUT113.                                        
       AUTHOR.                 DARYL WOELFEL.                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           MAY 2005.                                        
       DATE-COMPILED.                                                           
      ******************************************************************        
      * SAS - UNSUBMIT/RESUBMIT/APPROVE OPTIMIZED PRICE CHANGES                 
      ******************************************************************        
      *----------------------------------------------------------               
      * HISTORY LOG:                                                            
      *----------------------------------------------------------               
      * DATE:         10/26/2005                                                
      * WR/IR#:       WR#28048                                                  
      * PROGRAMMER:   D. WOELFEL                                                
      * DESCRIPTION:  FILL IN SUBMIT/APPROVE AUDIT FIELDS FOR                   
      *               MK2 AND FNL (INDICATOR OF Y IN CONTROL CARD).             
      *----------------------------------------------------------               
      * DATE:         07/31/2006                                                
      * WR/IR#:       WR#28048                                                  
      * PROGRAMMER:   D. WOELFEL                                                
      * DESCRIPTION:  ALLOW PROGRAM TO DISCRETELY END WHEN INPUT                
      *               CONTROL CARD ACTION FILE IS EMPTY.                        
      *----------------------------------------------------------               
      *----------------------------------------------------------               
      * DATE:         01/27/2009                                                
      * WR/IR#:       INC000000677604                                           
      * PROGRAMMER:   SRINIVASAN RAMAMURTHY                                     
      * DESCRIPTION:  ALLOW PROGRAM TO DISCRETELY END WHEN INPUT                
      *               ID FILE IS EMPTY.                                         
      *----------------------------------------------------------               
      * DATE:         07/30/2018                                                
      * WR/IR#:       CHG0204126                                                
      * PROGRAMMER:   COGNIZANT                                                 
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE                     
      *----------------------------------------------------------               
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------               
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT ACTION-FILE                                                   
             ASSIGN TO INP01.                                                   
                                                                                
           SELECT ID-FILE                                                       
             ASSIGN TO INP02.                                                   
                                                                                
           SELECT ORACLE-LOGON-FILE                                             
             ASSIGN TO ORALOGON.                                                
                                                                                
      *----------------------------------------------------------               
       DATA DIVISION.                                                           
      *----------------------------------------------------------               
       FILE SECTION.                                                            
                                                                                
       FD  ACTION-FILE                                                          
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS ACTION-REC.                                           
       01  ACTION-REC                       PIC  X(80).                         
                                                                                
       FD  ID-FILE                                                              
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS ID-REC.                                               
       01  ID-REC                           PIC  X(43).                         
                                                                                
       FD  ORACLE-LOGON-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
       01  OL-ORACLE-LOGON-RECORD      PIC X(80).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------               
      * ABEND- ABEND VARIABLES                                                  
      *----------------------------------------------------------               
       01  ABEND-CODE                       PIC S9(04) COMP VALUE +0.           
           88  ABEND-4001-WRITE-ERROR                  VALUE +4001.             
           88  ABEND-4002-READ-ERROR                   VALUE +4002.             
           88  ABEND-4003-CTRL-CARD-ERROR              VALUE +4003.             
           88  ABEND-4004-TABLE-ERROR                  VALUE +4004.             
           88  ABEND-4005-OPEN-ERROR                   VALUE +4005.             
           88  ABEND-4006-CLOSE-ERROR                  VALUE +4006.             
           88  ABEND-4007-SEQUENCE-ERROR               VALUE +4007.             
           88  ABEND-4008-DATA-ERROR                   VALUE +4008.             
           88  ABEND-4009-KEY-DATA-ERROR               VALUE +4009.             
           88  ABEND-4013-SQL-ERROR                    VALUE +4013.             
           88  ABEND-4014-ORACLE-LOGON-ERROR           VALUE +4014.             
           88  ABEND-4019-DATE-ROUTINE-ERROR           VALUE +4019.             
           88  ABEND-4444-FORCED-ABEND                 VALUE +4444.             
                                                                                
      *----------------------------------------------------------               
      * ACTION CONTROL CARD                                                     
      * U=UNSUBMIT, S=SUBMIT, A=APPROVE                                         
      *----------------------------------------------------------               
       01  CC-ACTION-REC.                                                       
           05  CC-ACTION                    PIC  X(01) VALUE SPACE.             
           05  FILLER                       PIC  X(01) VALUE SPACE.             
           05  CC-UPDATE-SUB-APR-FIELDS     PIC  X(01) VALUE SPACE.             
           05  FILLER                       PIC  X(77) VALUE SPACE.             
                                                                                
      *----------------------------------------------------------               
      * PS- PROGRAM SWITCHES                                                    
      *----------------------------------------------------------               
       01  PS-PROGRAM-SWITCHES.                                                 
006510     05  PS-END-OF-LOGON-FILE-SW      PIC X(01)  VALUE 'N'.               
006530         88  PS-END-OF-LOGON-FILE                VALUE 'Y'.               
           05  PS-ORA-SRC-CTL-DONE-SW       PIC X(01)  VALUE 'N'.               
               88  PS-ORA-SRC-EMPTY                    VALUE 'Y'.               
           05  PS-END-OF-ACTION-FILE-SW     PIC  X(01) VALUE 'N'.               
               88  PS-END-OF-CC-ACTION                 VALUE 'Y'.               
               88  PS-NOT-END-OF-CC-ACTION             VALUE 'N'.               
           05  PS-END-OF-ID-FILE-SW         PIC  X(01) VALUE 'N'.               
               88  PS-END-OF-ID-FILE                   VALUE 'Y'.               
               88  PS-NOT-END-OF-ID-FILE               VALUE 'N'.               
           05  PS-NOTHING-TO-PROCESS-SW     PIC  X(01) VALUE 'N'.               
               88  PS-NOTHING-TO-DO                    VALUE 'Y'.               
               88  PS-SOMETHING-TO-DO                  VALUE 'N'.               
                                                                                
       01  CC-ORACLE-CONNECTION.                                                
           05  CC-ORACLE-USERID             PIC X(40).                          
               88 CC-AUTO-LOGON                         VALUE '/'.              
           05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
      *----------------------------------------------------------               
      * PC- PROGRAM CONSTANTS                                                   
      *----------------------------------------------------------               
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE                    
                                                       'PEKUT113'.              
       01  CT-CPU-TIME.                                                         
           05  CT-CPU-HR                        PIC  9(02) VALUE ZERO.          
           05  CT-CPU-MIN                       PIC  9(02) VALUE ZERO.          
           05  CT-CPU-SEC                       PIC  9(02) VALUE ZERO.          
           05  CT-CPU-HSEC                      PIC  9(02) VALUE ZERO.          
                                                                                
       01  PV-CPU-TIME.                                                         
           05  PV-CPU-HR                        PIC  9(02) VALUE ZERO.          
           05  FILLER                           PIC  X(01) VALUE ":".           
           05  PV-CPU-MIN                       PIC  9(02) VALUE ZERO.          
           05  FILLER                           PIC  X(01) VALUE ":".           
           05  PV-CPU-SEC                       PIC  9(02) VALUE ZERO.          
           05  FILLER                           PIC  X(01) VALUE ":".           
           05  PV-CPU-HSEC                      PIC  9(02) VALUE ZERO.          
                                                                                
      *----------------------------------------------------------               
      * PV- PROGRAM VARIABLES                                                   
      *----------------------------------------------------------               
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-ORACLE-USERID-LEN         PIC S9(4)  VALUE ZEROS              
                                                       COMP SYNC.               
           05  PV-ORACLE-PASSWORD-LEN       PIC S9(4)  VALUE ZEROS              
                                                       COMP SYNC.               
           05  PV-FILE-STATUS               PIC  X(02) VALUE '00'.              
           05  PV-TABLE-NAME                PIC  X(30) VALUE SPACES.            
           05  PV-SQL-CODE                  PIC  9(09) VALUE 0.                 
           05  PV-OPERATION-MSG             PIC  X(60) VALUE SPACES.            
           05  PV-OPERATION-MSG-1           PIC  X(40) VALUE SPACES.            
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACES.            
                                                                                
       01  PV-SELECT-VARIABLES.                                                 
           05  PV-PREV-PRCHG-ID             PIC  9(10) VALUE 0.                 
           05  PV-PRCHG-ID                  PIC S9(10) VALUE +0                 
                                                       COMP-3.                  
                                                                                
      *----------------------------------------------------------               
      * PA- PROGRAM ACCUMULATORS                                                
      *----------------------------------------------------------               
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-READ                 PIC  9(09) VALUE ZEROES.            
           05  PA-PRICE-CHANGES-UPDATED     PIC  9(09) VALUE ZEROES.            
                                                                                
      *----------------------------------------------------------               
      * COPYBOOK FOR THE ID INPUT FILE                                          
      *----------------------------------------------------------               
           COPY PERD113.                                                        
                                                                                
      *----------------------------------------------------------               
      *                      O  R  A  C  L  E                                   
      *==========================================================               
      *                                                                         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *                                                                         
      *----------------------------------------------------------               
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *----------------------------------------------------------               
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *----------------------------------------------------------               
                                                                                
       01  PC-ENT01-TYPE-CDE                PIC  X(02)  VALUE '01'.             
       01  PV-ORACLE-ID                     PIC  X(1)   VALUE '/'.              
                                                                                
       01  USERNAME                         PIC  X(10)  VARYING.                
       01  PASSWD                           PIC  X(10)  VARYING.                
                                                                                
       01  ORACLE-RETURN                    PIC  X(200) VALUE SPACES.           
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
       EJECT                                                                    
                                                                                
      *----------------------------------------------------------               
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------               
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           IF PS-NOTHING-TO-DO                                                  
              DISPLAY 'CONTROL CARD EMPTY'                                      
           ELSE                                                                 
              PERFORM 2100-READ-ID-FILE                                         
              IF PS-END-OF-ID-FILE AND                                          
                 PA-RECS-READ = 0                                               
                 DISPLAY '** 2100-READ-ID-FILE **'                              
                 DISPLAY '** EMPTY INPUT FILE  **'                              
              ELSE                                                              
                 PERFORM 2000-PROCESS-ID-FILE                                   
                         UNTIL PS-END-OF-ID-FILE                                
                 PERFORM 9000-COMMIT                                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 9999-WRAPUP.                                                 
                                                                                
           GOBACK.                                                              
                                                                                
      *----------------------------------------------------------               
      * 1000-INITIALIZATION.                                                    
      *----------------------------------------------------------               
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  ACTION-FILE                                              
                       ID-FILE.                                                 
                                                                                
           PERFORM 1010-ORACLE-SETUP.                                           
                                                                                
           PERFORM 1050-READ-ACTION-FILE.                                       
                                                                                
      *----------------------------------------------------------               
      * 1010-ORACLE-SETUP.                                                      
      *----------------------------------------------------------               
       1010-ORACLE-SETUP.                                                       
                                                                                
           OPEN INPUT ORACLE-LOGON-FILE.                                        
                                                                                
           PERFORM 1020-READ-ORACLE-LOGON.                                      
                                                                                
           CLOSE ORACLE-LOGON-FILE.                                             
                                                                                
           IF ((PS-END-OF-LOGON-FILE)                                           
                   OR (CC-ORACLE-CONNECTION = SPACES))                          
               DISPLAY '** ABEND **'                                            
               DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
                         'NOT PROVIDED'                                         
               SET PS-ORA-SRC-EMPTY TO TRUE                                     
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
           IF CC-AUTO-LOGON                                                     
               DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
               EXEC SQL                                                         
                    CONNECT :USERNAME                                           
               END-EXEC                                                         
           ELSE                                                                 
               INITIALIZE PV-ORACLE-USERID-LEN                                  
                          PV-ORACLE-PASSWORD-LEN                                
               INSPECT CC-ORACLE-USERID                                         
                   TALLYING PV-ORACLE-USERID-LEN                                
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            INSPECT CC-ORACLE-PASSWORD                                          
                TALLYING PV-ORACLE-PASSWORD-LEN                                 
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
                      USERNAME-ARR                                              
            MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
            MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
                      PASSWD-ARR                                                
            MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
            DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
                      USERNAME-ARR                                              
            EXEC SQL                                                            
              CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
            END-EXEC                                                            
            INITIALIZE CC-ORACLE-PASSWORD                                       
                       PASSWD                                                   
            END-IF.                                                             
                                                                                
020500     IF (SQLCODE NOT = 0)                                                 
020900         DISPLAY PV-PARAGRAPH-NAME                                        
020910         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
021000         PERFORM Z100-ABEND                                               
021200     END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------               
034504** READ THE ORACLE LOGON CONTROL CARD.                                    
      *----------------------------------------------------------               
034506 1020-READ-ORACLE-LOGON.                                                  
                                                                                
034510     READ ORACLE-LOGON-FILE                                               
034520         INTO CC-ORACLE-CONNECTION                                        
034530         AT END                                                           
034540             SET PS-END-OF-LOGON-FILE TO TRUE                             
034550     END-READ.                                                            
034600*                                                                         
                                                                                
      *----------------------------------------------------------               
      * 1050-READ-ACTION-FILE.                                                  
      *----------------------------------------------------------               
       1050-READ-ACTION-FILE.                                                   
                                                                                
           READ ACTION-FILE INTO CC-ACTION-REC                                  
              AT END                                                            
                 SET PS-END-OF-CC-ACTION TO TRUE                                
           END-READ.                                                            
                                                                                
           IF PS-END-OF-CC-ACTION                                               
              SET PS-NOTHING-TO-DO TO TRUE                                      
           ELSE                                                                 
             IF (CC-ACTION NOT = 'U' AND                                        
                 CC-ACTION NOT = 'S' AND                                        
                 CC-ACTION NOT = 'A')                                           
                                                                                
               MOVE '1050-READ-ACTION-FILE' TO PV-PARAGRAPH-NAME                
               MOVE 'INVALID ACTION - MUST BE U, S, OR A'                       
                                               TO PV-OPERATION-MSG-1            
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM 9800-ABEND                                               
             ELSE                                                               
               DISPLAY 'ACTION = ' CC-ACTION                                    
               DISPLAY 'FILL SUB/APPR AUDIT FIELDS = '                          
                                            CC-UPDATE-SUB-APR-FIELDS            
             END-IF                                                             
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------          
      * 2000-PROCESS-ID-FILE.                                                   
      *     PROCESS THE ID RECS.                                                
      *---------------------------------------------------------------          
       2000-PROCESS-ID-FILE.                                                    
                                                                                
      *    ONLY NEED TO DO THE ACTION FOR THE FIRST OCCURRENCE OF               
      *    EACH PRICE CHANGE ID                                                 
           IF PE113-PRCHG-ID NOT = PV-PREV-PRCHG-ID                             
                                                                                
               PERFORM 2200-UPDATE-PRICE-CHANGE                                 
                                                                                
               MOVE PE113-PRCHG-ID TO PV-PREV-PRCHG-ID                          
                                                                                
           END-IF                                                               
                                                                                
           PERFORM 2100-READ-ID-FILE.                                           
                                                                                
      *---------------------------------------------------------------          
      * 2100-READ-ID-FILE.                                                      
      *   COMMENTED OUT ABEND ROUTINUE IF THE FILE IS EMPTY                     
      *---------------------------------------------------------------          
       2100-READ-ID-FILE.                                                       
                                                                                
           READ ID-FILE INTO PE113-ID-REC                                       
                AT END SET PS-END-OF-ID-FILE TO TRUE.                           
                                                                                
      *    IF PS-END-OF-ID-FILE AND                                             
      *       PA-RECS-READ = 0                                                  
      *        MOVE '2100-READ-ID-FILE' TO PV-PARAGRAPH-NAME                    
      *        MOVE 'EMPTY INPUT FILE'  TO PV-OPERATION-MSG-1                   
      *        SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
      *        PERFORM 9800-ABEND                                               
      *    END-IF.                                                              
                                                                                
           IF NOT PS-END-OF-ID-FILE                                             
               ADD +1 TO PA-RECS-READ                                           
           END-IF.                                                              
                                                                                
      *--------------------------------------------------------------           
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT              
      * LOGS ON TO ORACLE.                                                      
      * FOR TESTING CHANGE PEPD700 TO PEPD70A (PERM LOGIN)                      
      *---------------------------------------------------------------          
      *   COPY PEPD700.                                                         
                                                                                
      *---------------------------------------------------------------          
      * 2200-UPDATE-PRICE-CHANGE.                                               
      *---------------------------------------------------------------          
       2200-UPDATE-PRICE-CHANGE.                                                
                                                                                
           MOVE PE113-PRCHG-ID TO PV-PRCHG-ID.                                  
                                                                                
           EVALUATE TRUE                                                        
             WHEN CC-ACTION = 'U'                                               
               PERFORM 2210-UNSUBMIT-PRICE-CHANGE                               
             WHEN CC-ACTION = 'S'                                               
               PERFORM 2220-SUBMIT-PRICE-CHANGE                                 
             WHEN CC-ACTION = 'A'                                               
               PERFORM 2230-APPROVE-PRICE-CHANGE                                
             WHEN OTHER                                                         
               MOVE '2200-UPDATE-PRICE-CHANGE' TO PV-PARAGRAPH-NAME             
               MOVE 'INVALID ACTION - MUST BE U, S, OR A'                       
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM 9800-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *---------------------------------------------------------------          
      * 2210-UNSUBMIT-PRICE-CHANGE.                                             
      *---------------------------------------------------------------          
       2210-UNSUBMIT-PRICE-CHANGE.                                              
                                                                                
           EXEC SQL                                                             
               UPDATE                                                           
                   PET_PRCHG                                                    
               SET                                                              
                   PRCHG_ST_CDE = 'WRK'                                         
                  ,LAST_UPD_TMST = SYSDATE                                      
                  ,LAST_UPD_BY_USR_ID = 'SAS'                                   
               WHERE                                                            
                   PRCHG_ID = :PV-PRCHG-ID                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
                  ADD +1 TO PA-PRICE-CHANGES-UPDATED                            
                  DISPLAY 'UNSUBMITTED PC#' PV-PRCHG-ID                         
                                                                                
             WHEN OTHER                                                         
                  MOVE SQLCODE     TO PV-SQL-CODE                               
                  SET ABEND-4013-SQL-ERROR TO TRUE                              
                  MOVE 'PET_PRCHG' TO PV-TABLE-NAME                             
                  MOVE '2210-UNSUBMIT'       TO PV-PARAGRAPH-NAME               
                  MOVE 'ERROR UNSUBMITTING ' TO PV-OPERATION-MSG                
                  DISPLAY '*** PRCHG_ID = ' PV-PRCHG-ID                         
                  PERFORM 9900-ORACLE-ABEND                                     
           END-EVALUATE.                                                        
                                                                                
      *---------------------------------------------------------------          
      * 2220-SUBMIT-PRICE-CHANGE.                                               
      *---------------------------------------------------------------          
       2220-SUBMIT-PRICE-CHANGE.                                                
                                                                                
           IF CC-UPDATE-SUB-APR-FIELDS = 'Y'                                    
                                                                                
           EXEC SQL                                                             
               UPDATE                                                           
                   PET_PRCHG                                                    
               SET                                                              
                   PRCHG_ST_CDE = 'PND'                                         
                  ,SUBMTD_TMST = SYSDATE                                        
                  ,SUBMTD_BY_USR_ID = 'SAS'                                     
                  ,LAST_UPD_TMST = SYSDATE                                      
                  ,LAST_UPD_BY_USR_ID = 'SAS'                                   
               WHERE                                                            
                   PRCHG_ID = :PV-PRCHG-ID                                      
           END-EXEC                                                             
                                                                                
           ELSE                                                                 
                                                                                
           EXEC SQL                                                             
               UPDATE                                                           
                   PET_PRCHG                                                    
               SET                                                              
                   PRCHG_ST_CDE = 'PND'                                         
                  ,LAST_UPD_TMST = SYSDATE                                      
                  ,LAST_UPD_BY_USR_ID = 'SAS'                                   
               WHERE                                                            
                   PRCHG_ID = :PV-PRCHG-ID                                      
           END-EXEC                                                             
                                                                                
           END-IF                                                               
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
                  ADD +1 TO PA-PRICE-CHANGES-UPDATED                            
                  DISPLAY 'SUBMITTED PC#' PV-PRCHG-ID                           
                                                                                
             WHEN OTHER                                                         
                  MOVE SQLCODE     TO PV-SQL-CODE                               
                  SET ABEND-4013-SQL-ERROR TO TRUE                              
                  MOVE 'PET_PRCHG' TO PV-TABLE-NAME                             
                  MOVE '2220-SUBMIT'       TO PV-PARAGRAPH-NAME                 
                  MOVE 'ERROR SUBMITTING ' TO PV-OPERATION-MSG                  
                  DISPLAY '*** PRCHG_ID = ' PV-PRCHG-ID                         
                  PERFORM 9900-ORACLE-ABEND                                     
           END-EVALUATE.                                                        
                                                                                
      *---------------------------------------------------------------          
      * 2230-APPROVE-PRICE-CHANGE.                                              
      *---------------------------------------------------------------          
       2230-APPROVE-PRICE-CHANGE.                                               
                                                                                
           IF CC-UPDATE-SUB-APR-FIELDS = 'Y'                                    
                                                                                
           EXEC SQL                                                             
               UPDATE                                                           
                   PET_PRCHG                                                    
               SET                                                              
                   PRCHG_ST_CDE = 'APR'                                         
                  ,APP_TMST = SYSDATE                                           
                  ,APP_BY_USR_ID = 'SAS'                                        
                  ,LAST_UPD_TMST = SYSDATE                                      
                  ,LAST_UPD_BY_USR_ID = 'SAS'                                   
               WHERE                                                            
                   PRCHG_ID = :PV-PRCHG-ID                                      
           END-EXEC                                                             
                                                                                
           ELSE                                                                 
                                                                                
           EXEC SQL                                                             
               UPDATE                                                           
                   PET_PRCHG                                                    
               SET                                                              
                   PRCHG_ST_CDE = 'APR'                                         
                  ,LAST_UPD_TMST = SYSDATE                                      
                  ,LAST_UPD_BY_USR_ID = 'SAS'                                   
               WHERE                                                            
                   PRCHG_ID = :PV-PRCHG-ID                                      
           END-EXEC                                                             
                                                                                
           END-IF                                                               
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
                  ADD +1 TO PA-PRICE-CHANGES-UPDATED                            
                  DISPLAY 'APPROVED PC#' PV-PRCHG-ID                            
                                                                                
             WHEN OTHER                                                         
                  MOVE SQLCODE     TO PV-SQL-CODE                               
                  SET ABEND-4013-SQL-ERROR TO TRUE                              
                  MOVE 'PET_PRCHG' TO PV-TABLE-NAME                             
                  MOVE '2230-APPROVE'       TO PV-PARAGRAPH-NAME                
                  MOVE 'ERROR APPROVING '   TO PV-OPERATION-MSG                 
                  DISPLAY '*** PRCHG_ID = ' PV-PRCHG-ID                         
                  PERFORM 9900-ORACLE-ABEND                                     
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * 9000-COMMIT                                                   *         
      *****************************************************************         
       9000-COMMIT.                                                             
                                                                                
           DISPLAY 'COMMITTING...'                                              
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = ZEROES                                                  
               DISPLAY 'COMMITTED.'                                             
           ELSE                                                                 
               MOVE 'ERROR DURING COMMIT' TO PV-OPERATION-MSG                   
               MOVE '9000-COMMIT'         TO PV-PARAGRAPH-NAME                  
               PERFORM 9900-ORACLE-ABEND                                        
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 9200-GET-TIME                                                 *         
      *****************************************************************         
       9200-GET-TIME.                                                           
                                                                                
           ACCEPT CT-CPU-TIME FROM TIME.                                        
           MOVE CT-CPU-HR     TO  PV-CPU-HR.                                    
           MOVE CT-CPU-MIN    TO  PV-CPU-MIN.                                   
           MOVE CT-CPU-SEC    TO  PV-CPU-SEC.                                   
           MOVE CT-CPU-HSEC   TO  PV-CPU-HSEC.                                  
                                                                                
      *----------------------------------------------------------               
      * 9800-ABEND.                                                             
      *     PERFORMS A NON-SQL-ABEND.                                           
      *----------------------------------------------------------               
       9800-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      *----------------------------------------------------------               
      * 9900-ORACLE-ABEND.                                                      
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       9900-ORACLE-ABEND.                                                       
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' PV-SQL-CODE.                       
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      *-----------------------------------------------------------              
      * Z100-ABEND                                                              
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE                      
      *  - CALLED BY PEPD700!                                                   
      *-----------------------------------------------------------              
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                           
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      *----------------------------------------------------------               
      * 9999-WRAPUP.                                                            
      *     END OF PROGRAM ROUTINE.  CLOSES ALL FILES AND DISPLAYS              
      *     ANY DATA FROM THE PROGRAM.                                          
      *----------------------------------------------------------               
       9999-WRAPUP.                                                             
                                                                                
           CLOSE ACTION-FILE                                                    
                 ID-FILE.                                                       
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'NUMBER OF RECS READ...................: '                   
                                        PA-RECS-READ.                           
           EVALUATE TRUE                                                        
             WHEN CC-ACTION = 'U'                                               
               DISPLAY 'NUMBER OF PRICE CHANGES UNSUBMITTED...: '               
                                        PA-PRICE-CHANGES-UPDATED                
             WHEN CC-ACTION = 'S'                                               
               DISPLAY 'NUMBER OF PRICE CHANGES SUBMITTED.....: '               
                                        PA-PRICE-CHANGES-UPDATED                
             WHEN CC-ACTION = 'A'                                               
               DISPLAY 'NUMBER OF PRICE CHANGES APPROVED......: '               
                                        PA-PRICE-CHANGES-UPDATED                
             WHEN OTHER                                                         
               DISPLAY 'NUMBER OF PRICE CHANGES ???ERROR???...: '               
                                        PA-PRICE-CHANGES-UPDATED                
           END-EVALUATE.                                                        
                                                                                
