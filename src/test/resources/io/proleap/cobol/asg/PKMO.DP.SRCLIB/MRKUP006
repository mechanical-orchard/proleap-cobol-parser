000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400                                                                          
000500 PROGRAM-ID.             MRKUP006.                                        
000600 AUTHOR.                 DONALD TOMLINSON.                                
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000800 DATE-WRITTEN            12-27-2002.                                      
000900 DATE-COMPILED.                                                           
001000                                                                          
001100*****************************************************************         
001200*                    ITEM MAINTENANCE                           *         
001300*                                                               *         
001400*  THIS PROGRAM IS A COPY OF PROGRAM MRKUP005. IT IS TO BE USED *         
001410*  FOR THE SECOND RUNNING OF THE O1 AND THE O2 SYSTEM ON SAT.   *         
001420*  THIS PROGRAM WILL UPDATE THE ORDER POINT AND ORDER UP TO     *         
001500*  LEVEL ONLY.  ALL OTHER FUNCTIONS FROM THE OLD MRKUP005 HAVE  *         
001600*  BEEN REMOVED.                                                *         
001800*                                                               *         
004800*****************************************************************         
004810* CHANGED 08/11/03 BY DONALD TOMLINSON  WR# 24161               *         
004840*    NEW PGM TO UPDATE TMRITST OP-QTY AND OUTL-QTY.             *         
004850*****************************************************************         
004810* CHANGED 06/03/04 BY BOB FORD WR# 26363- STORE NUMBER EXPANSION*         
004840*    MODIFIED PGM TO HANDLE 5 DIGIT STORE NUMBERS.  DB CHANGES  *         
004840*    NOT AVAILABLE AT THIS TIME.                                *         
004850*****************************************************************         
004900 ENVIRONMENT DIVISION.                                                    
005000*****************************************************************         
005100                                                                          
005200 CONFIGURATION SECTION.                                                   
005300                                                                          
005400 SOURCE-COMPUTER.        IBM-3090.                                        
005500 OBJECT-COMPUTER.        IBM-3090.                                        
005600                                                                          
005700 INPUT-OUTPUT SECTION.                                                    
005800                                                                          
005900 FILE-CONTROL.                                                            
006000*                                                                         
006100     SELECT IC-SUPP-FILE          ASSIGN   TO  INP01.                     
006200*                                                                         
006400*                                                                         
006500*****************************************************************         
006600 DATA DIVISION.                                                           
006700*****************************************************************         
006800*                                                                         
006900 FILE SECTION.                                                            
007000*                                                                         
007100 FD  IC-SUPP-FILE                                                         
007200     LABEL RECORDS ARE STANDARD                                           
007300     BLOCK CONTAINS 0 RECORDS                                             
007400     RECORD CONTAINS 2090 CHARACTERS                                      
007500     DATA RECORD IS IC-SUPP-REC.                                          
007600 01  IC-SUPP-REC                     PIC  X(2090).                        
007700*                                                                         
008400*                                                                         
008500 WORKING-STORAGE SECTION.                                                 
008600*                                                                         
008700 01  FILLER.                                                              
008800     05  FILLER                      PIC  X(36)    VALUE                  
008900         ' ***** WORKING STORAGE PGM=MRKUP006 '.                          
009000     05  PGM-COMPILED                PIC  X(20)    VALUE SPACE.           
009100     05  FILLER                      PIC  X(10)    VALUE                  
009200         ' LOCATION='.                                                    
009300     05  PGM-LOCATION                PIC  X(04)    VALUE SPACE.           
009400     05  FILLER                      PIC  X(06)    VALUE                  
009500         ' *****'.                                                        
009600 EJECT                                                                    
009700 01  PROGRAM-SWITCHES.                                                    
009800     05  PS-EOF-SUPP-FILE            PIC  X(01)    VALUE 'N'.             
009900         88  EOF-SUPP-FILE                         VALUE 'Y'.             
010400*                                                                         
010500/                                                                         
010600 01  PC-CONSTANTS.                                                        
010900     05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100             
011000                                                   COMP SYNC.             
011700     05  PC-Y                        PIC  X(01)    VALUE 'Y'.             
012500 EJECT                                                                    
012600*                                                                         
012700 01  PV-ACCUMULATOR-FIELDS.                                               
013400     05  PV-TMRITST-UPDATED          PIC 9(09)    VALUE ZERO.             
013410     05  PV-TSKXREF-NOT-FOUND        PIC 9(09)    VALUE ZERO.             
013420     05  PV-TMRITST-NOT-FOUND        PIC 9(09)    VALUE ZERO.             
013600 EJECT                                                                    
013700*                                                                         
013800 01  PV-MISC-FIELDS.                                                      
013801*** START OF TEST FIELDS.                                                 
013810*    05  WS-ITEM          PIC 9(15).                                      
013820*    05  WS-STORE         PIC X(03).                                      
013830*    05  WS-OP-QTY        PIC 9(09).                                      
013840*    05  WS-OUTL-QTY      PIC 9(09).                                      
013841*** END OF TEST FIELDS.                                                   
013842     05  CC-DATE.                                                         
013843         10  CC-CURRENT-YEAR.                                             
013844             15 CC-CURRENT-DATE-CC   PIC  9(02)    VALUE ZERO.            
013845             15 CC-CURRENT-DATE-YR   PIC  9(02)    VALUE ZERO.            
013846         10  FILLER                  PIC  X(01)    VALUE '-'.             
013847         10  CC-CURRENT-DATE-MO      PIC  9(02)    VALUE ZERO.            
013848         10  FILLER                  PIC  X(01)    VALUE '-'.             
013849         10  CC-CURRENT-DATE-DY      PIC  9(02)    VALUE ZERO.            
013850                                                                          
013900     05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO             
014000                                                   COMP SYNC.             
014300     05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.          
014400     05  PV-CURR-TMST                PIC  X(26)    VALUE SPACE.           
014500     05  PV-COMMIT-TMST              PIC  X(26)    VALUE SPACE.           
014600     05  PV-TODAYS-DAY               PIC  9(01)    VALUE ZERO.            
018100     05  PV-TODAYS-TIME.                                                  
018200         10  PV-TODAYS-TIME-HH       PIC  9(02)    VALUE ZERO.            
018300         10  PV-TODAYS-TIME-MM       PIC  9(02)    VALUE ZERO.            
018400         10  PV-TODAYS-TIME-SS       PIC  9(02)    VALUE ZERO.            
018500         10  FILLER                  PIC  9(02)    VALUE ZERO.            
022100     05  PV-SUP-KEY.                                                      
022200         10 PV-SUP-KEY-1-08          PIC  9(08).                          
022700         10 FILLER                   PIC  X(03).                          
022710         10 FILLER                   PIC  X(04).                          
022800         10 PV-SUP-KEY-16-20         PIC  9(05).                          
022900         10 FILLER                   PIC  X(10).                          
025000** TEMP CODE UNTIL TMRITST IS CHANGED                                     
025000     05  PV-9-5                      PIC  9(05).                          
025000     05  PV-X-5 REDEFINES PV-9-5.                                         
025000         10 PV-X-2                   PIC  X(2).                           
025000         10 PV-X-3                   PIC  X(3).                           
023300 EJECT                                                                    
023400*                                                                         
023500     COPY ISWS006.                                                        
023600*                                                                         
023601     COPY DPWS500.                                                        
023602*                                                                         
023900 01 SUPP-RECORD.                                                          
024000     COPY ICI165C.                                                        
024100  02 TR-INPUT-RECORD REDEFINES SUP-TR-RCD-IN.                             
024200     COPY ICI030C.                                                        
024300     COPY MRRD165.                                                        
024400 EJECT                                                                    
024500* DB2 TABLE LAYOUTS                                                       
024600     EXEC SQL                                                             
024700         INCLUDE TMRITST                                                  
024800     END-EXEC.                                                            
024900                                                                          
025000     EXEC SQL                                                             
025100         INCLUDE TCKRSTCN                                                 
025200     END-EXEC.                                                            
026110*----------------------------------------------------------------*        
026120* INCLUDED DCLGEN COPYBOOK                                       *        
026130* TSKXREF FOR CROSS REFERENCE TABLE.                   WR # 21601*        
026140*----------------------------------------------------------------*        
026510     EXEC SQL                                                             
026520         INCLUDE TSKXREF                                                  
026530     END-EXEC.                                                            
026540                                                                          
026600* DB2 COMMUNICATION AREA                                                  
026700*                                                                         
026800     EXEC SQL                                                             
026900         INCLUDE SQLCA                                                    
027000     END-EXEC.                                                            
027100                                                                          
027200* DB2 COMMUNICATION AREA2                                                 
027300*                                                                         
027400     COPY SQLCA2.                                                         
027500 EJECT                                                                    
027600*                                                                         
027700 LINKAGE SECTION.                                                         
027800*****************************************************************         
027900*PROCEDURE DIVISION.                                                      
028000*****************************************************************         
028100 PROCEDURE DIVISION.                                                      
028200*                                                                         
028300 A000-MAINLINE-PROCESSING.                                                
028400*                                                                         
028500     PERFORM A100-INITIALIZATION.                                         
028600*                                                                         
028700     PERFORM B000-PROCESS-SUPPLEMENTAL-REC                                
028800             UNTIL EOF-SUPP-FILE.                                         
028810     DISPLAY  'TMRITST ROWS UPDATED = ' PV-TMRITST-UPDATED.               
028830     DISPLAY  'TMRITST ROWS NOT FOUND = ' PV-TMRITST-NOT-FOUND.           
028840     DISPLAY  'TSKXREF ROWS NOT FOUND = ' PV-TSKXREF-NOT-FOUND.           
028900*                                                                         
029000     PERFORM Y100-END-ROUTINE.                                            
029100*                                                                         
029200     GOBACK.                                                              
029300*                                                                         
029400 EJECT                                                                    
029500******************************************************************        
029600** OPEN THE SYSTEM FILE AND ACCEPT CONTROL CARDS.                         
029700** READ THE FIRST RECORD.                                                 
029800******************************************************************        
029900 A100-INITIALIZATION.                                                     
030000*                                                                         
030100     OPEN  INPUT IC-SUPP-FILE.                                            
030300*                                                                         
030500     ACCEPT CC-DATE.                                                      
030600*                                                                         
031800     PERFORM D000-READ-SUPP-FILE.                                         
031900*                                                                         
032000      IF EOF-SUPP-FILE                                                    
032100         CLOSE  IC-SUPP-FILE                                              
032300         GOBACK.                                                          
032400*                                                                         
032500*  SET UP FIRST SCHEDULED COMMIT TIME                                     
032600                                                                          
032700     EXEC SQL                                                             
032800         SELECT  (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)            
032900           INTO  :PV-COMMIT-TMST                                          
033000           FROM  TCKRSTCNTL                                               
033100          WHERE  PLAN_NAME = 'MRKUP006'                                   
033200     END-EXEC.                                                            
033300                                                                          
033400     IF SQLCODE = +0                                                      
033500         CONTINUE                                                         
033600     ELSE                                                                 
033700         DISPLAY 'PARAGRAPH A100, BAD SELECT FOR TIMESTAMP'               
033800         DISPLAY ' '                                                      
033900         PERFORM Z999-SQL-ERROR.                                          
034000                                                                          
034200*                                                                         
034300 EJECT                                                                    
034400******************************************************************        
034500*  THIS IS THE MAIN CONTROLLING PARAGRAPH FOR PROCESSING THE     *        
034600*  SUPPLEMENTAL RECORD.                                          *        
034700******************************************************************        
034800 B000-PROCESS-SUPPLEMENTAL-REC.                                           
034900*                                                                         
035000     MOVE SUP-KEY TO PV-SUP-KEY.                                          
035310     MOVE PV-SUP-KEY-1-08  TO NI-SKU-9-8.                                 
035311     MOVE NI-SKU-9-8       TO SKXREF-SKU.                                 
035400     PERFORM E000-READ-SKU-TABLE.                                         
035500*                                                                         
035600     IF SQLCODE = 100                                                     
035700**      DISPLAY 'TSKXREF NOT FOUND ' NI-SKU-9-8                           
035710        CONTINUE                                                          
035800     ELSE                                                                 
035900         PERFORM C000-FORMAT-UPDATE-ITEM-STORE                            
036200         PERFORM Z900-COMMIT-TEST                                         
036300     END-IF.                                                              
036400*                                                                         
036500     PERFORM D000-READ-SUPP-FILE.                                         
036600*                                                                         
036700 EJECT                                                                    
038900*                                                                *        
039000******************************************************************        
039100*   FORMAT FIELDS AND UPDATE ON THE ITEM/STORE TABLE WITH THE    *        
039200*   DATA FROM THE SUPPLEMENTAL RECORD.                           *        
039300******************************************************************        
039400 C000-FORMAT-UPDATE-ITEM-STORE.                                           
039500*                                                                         
039600*    DISPLAY 'C000-FORMAT-UPDATE-ITEM-STORE'.                             
039700*                                                                         
039700** TEMP CODE UNTIL TMRITST IS CHANGED                                     
039800     MOVE PV-SUP-KEY-16-20     TO  PV-9-5                                 
039800     MOVE PV-X-3               TO  MRITST-STR-NBR.                        
042400     MOVE SUP-OP-QTY           TO  MRITST-OP-QTY.                         
042500     MOVE SUP-OUTL-QTY         TO  MRITST-OUTL-QTY.                       
042700*                                                                *        
042800     PERFORM C100-UPDATE-TMRITST.                                         
042810*                                                                         
042820*                                                                         
042830******************************************************************        
042840* THIS PARAGRAPH PERFORMS THE UPDATE OF THE ITEM/STORE TABLE     *        
042850******************************************************************        
042860 C100-UPDATE-TMRITST.                                                     
042870*                                                                         
042880*    DISPLAY 'C100-UPDATE-TMRITST'.                                       
042890*                                                                         
042891     MOVE 'C100'                   TO PGM-LOCATION.                       
042892     MOVE 'C100-UPDATE-TMRITST'    TO PV-ABEND-PARAGRAPH.                 
042893                                                                          
042894     EXEC SQL                                                             
042895         UPDATE TMRITST                                                   
042896            SET   OP_QTY       =  :MRITST-OP-QTY,                         
042897                  OUTL_QTY     =  :MRITST-OUTL-QTY                        
042898          WHERE   ITEM_NBR     =  :SKXREF-ITM-NBR                         
042899            AND   STR_NBR      =  :MRITST-STR-NBR                         
042900     END-EXEC.                                                            
042910*                                                                         
042995                                                                          
042996** START OF TEMPORARY TEST CODE.                                          
042997*        MOVE MRITST-OP-QTY   TO WS-OP-QTY.                               
042998*        MOVE MRITST-OUTL-QTY TO WS-OUTL-QTY.                             
042999*        MOVE SKXREF-ITM-NBR  TO WS-ITEM.                                 
043000*        MOVE MRITST-STR-NBR  TO WS-STORE.                                
043001*        DISPLAY WS-ITEM '*' WS-STORE '*'                                 
043002*                ' OP QTY = ' WS-OP-QTY                                   
043003*                ' OUTL QTY = ' WS-OUTL-QTY.                              
043004**       IF PV-TMRITST-UPDATED > 500                                      
043005**           MOVE PC-Y TO PS-EOF-SUPP-FILE                                
043006**       END-IF                                                           
043007** END OF TEMPORARY TEST CODE.                                            
043008                                                                          
043010     EVALUATE TRUE                                                        
043011       WHEN SQLCODE         = ZERO                                        
043012         ADD 1 TO PV-TMRITST-UPDATED                                      
043013       WHEN SQLCODE         = PC-ROW-NOT-FOUND                            
043014         ADD 1 TO PV-TMRITST-NOT-FOUND                                    
043016       WHEN OTHER                                                         
043020         DISPLAY PV-ABEND-PARAGRAPH                                       
043030         PERFORM Z999-SQL-ERROR                                           
043040     END-EVALUATE.                                                        
043050                                                                          
043100*                                                                *        
047200******************************************************************        
047300*  THIS PARAGRAPH READS THE SUPPLEMENTAL FILE FOR PROCESSING     *        
047400******************************************************************        
047500 D000-READ-SUPP-FILE.                                                     
047600*                                                                         
047700     READ IC-SUPP-FILE  INTO  SUPP-RECORD                                 
047800         AT END                                                           
047900            MOVE PC-Y TO PS-EOF-SUPP-FILE.                                
059500*                                                                         
059600******************************************************************        
059700*  THIS PARAGRAPH READS THE SKU TABLE FOR PROCESSING             *        
059800******************************************************************        
059900 E000-READ-SKU-TABLE.                                                     
060000*                                                                         
060100     MOVE 'E000'                   TO PGM-LOCATION.                       
060200     MOVE 'E000-READ-SKU-TABLE '   TO PV-ABEND-PARAGRAPH.                 
060300*                                                                         
061110     EXEC SQL                                                             
061120         SELECT   ITM_NBR                                                 
061130           INTO   :SKXREF-ITM-NBR                                         
061140           FROM   TSKXREF                                                 
061150          WHERE   SKU = :SKXREF-SKU                                       
061180     END-EXEC.                                                            
061200*                                                                         
061300     EVALUATE TRUE                                                        
061400       WHEN SQLCODE         = ZERO                                        
061410         CONTINUE                                                         
061500       WHEN SQLCODE         = PC-ROW-NOT-FOUND                            
061510         ADD 1 TO PV-TSKXREF-NOT-FOUND                                    
061700       WHEN OTHER                                                         
061800         DISPLAY PV-ABEND-PARAGRAPH                                       
061900         PERFORM Z999-SQL-ERROR                                           
062000     END-EVALUATE.                                                        
062100*                                                                         
073310 EJECT                                                                    
073400******************************************************************        
073500*   CLOSES FILES AND DISPLAYS MESSAGES.                          *        
073600******************************************************************        
073700 Y100-END-ROUTINE.                                                        
073800*                                                                         
073900     CLOSE  IC-SUPP-FILE.                                                 
074100*                                                                         
074200 EJECT                                                                    
074300******************************************************************        
074400*     DB2 COMMIT TEST                                            *        
074500*                                                                *        
074600******************************************************************        
074700*                                                                         
074800 Z900-COMMIT-TEST.                                                        
074900*-----------------                                                        
075000*                                                                         
075100*  GET THE CURRENT TIMESTAMP                                              
075200*                                                                         
075300     EXEC SQL                                                             
075400         SET :PV-CURR-TMST = CURRENT TIMESTAMP                            
075500     END-EXEC.                                                            
075600                                                                          
075700     IF SQLCODE = +0                                                      
075800         CONTINUE                                                         
075900     ELSE                                                                 
076000         DISPLAY 'PARAGRAPH Z900, BAD SET FOR CURRENT TIMESTAMP'          
076100         DISPLAY ' '                                                      
076200         PERFORM Z999-SQL-ERROR.                                          
076300*                                                                         
076400*  COMPARE TO THE NEXT SCHEDULED COMMIT TIME                              
076500*                                                                         
076600     IF PV-CURR-TMST > PV-COMMIT-TMST                                     
076700         PERFORM Z910-COMMIT.                                             
076800                                                                          
076900 EJECT                                                                    
077000******************************************************************        
077100*     DB2 COMMIT PROCESSING                                      *        
077200*                                                                *        
077300******************************************************************        
077400                                                                          
077500 Z910-COMMIT.                                                             
077600*------------                                                             
077700*                                                                         
077800*  TAKE COMMIT                                                            
077900                                                                          
078000     EXEC SQL                                                             
078100         COMMIT                                                           
078200     END-EXEC.                                                            
078300                                                                          
078400     IF SQLCODE = +0                                                      
078500         CONTINUE                                                         
078600     ELSE                                                                 
078700         DISPLAY 'PARAGRAPH Z910, BAD COMMIT'                             
078800         DISPLAY ' '                                                      
078900         PERFORM Z999-SQL-ERROR.                                          
079000*                                                                         
079100*  GET THE NEXT SCHEDULED COMMIT TIME                                     
079200*                                                                         
079300     EXEC SQL                                                             
079400         SELECT  (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)            
079500           INTO  :PV-COMMIT-TMST                                          
079600           FROM  TCKRSTCNTL                                               
079700          WHERE  PLAN_NAME = 'MRKUP006'                                   
079800     END-EXEC.                                                            
079900                                                                          
080000     IF SQLCODE = +0                                                      
080100         CONTINUE                                                         
080200     ELSE                                                                 
080300         DISPLAY 'PARAGRAPH Z910, SELECT FOR NEXT TIMESTAMP'              
080400         DISPLAY ' '                                                      
080500         PERFORM Z999-SQL-ERROR.                                          
080600                                                                          
080700*                                                                         
080800 Z999-SQL-ERROR.                                                          
080900*---------------                                                          
081000                                                                          
081100     MOVE SQLCODE    TO SQLCODE2.                                         
081200     MOVE SQLERRD(3) TO SQLERRD3.                                         
081300     DISPLAY '** ABEND **       ** = SQLERROR'.                           
081400     DISPLAY '** PROGRAM        ** = MRKUP006'.                           
081500     DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
081600     DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
081700     DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
081800     DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
081900     MOVE +4013 TO PV-ABEND-CODE.                                         
082000     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
082100*                                                                         
082300******************************************************************        
