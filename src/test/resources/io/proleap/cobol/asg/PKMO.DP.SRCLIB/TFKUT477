      ******************************************************************00010030
       IDENTIFICATION DIVISION.                                         00020030
      ******************************************************************00030030
                                                                        00040030
       PROGRAM-ID.             TFKUT477.                                00050030
       AUTHOR.                 JACK KRAMER.                             00060030
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070030
       DATE-WRITTEN            DECEMBER 96.                             00080030
       DATE-COMPILED.                                                   00090030
                                                                        00100030
      ******************************************************************00110030
      *       THIS PROGRAM WILL CREATE AN ASSUMPTIVE RECEIPT FOR ALL    00120030
      * TRANSFERS NOT ALREADY ASSUMPTIVELY OR POSITIVELY RECEIVED WITHIN00130030
      * THE ASSUMPTIVE TIME PERIOD.                                     00140030
      ******************************************************************00150030
SR3403*** SPECIAL FOR TESTING ******************************************00151049
SR3403*** TO RUN THIS PROGRAM IN TEST YOU MAY NEED TO CHANGE CODE IN    00152049
SR3403*** THE TRANSFER_CSR TO  <= CURRENT DATE  INSTEAD OF              00153049
SR3403***                      <= CURRENT DATE  -  1 DAY                00154049
SR3403*** ELSE YOU WILL NOT RECEIVE ANY DATA BACK                       00155049
      ******************************************************************00156049
111703* 11/17/2003 - RAY GRAF                                           00160031
111703*   SORT1/SORT2 TRANSFER TYPES.                                   00170037
      ******************************************************************00180031
121205* 12/12/2005 - RAY GRAF                                           00190039
121205*   CHANGE TO SELECTION OF TRANSFERS TO A/R                       00200039
      ******************************************************************00210039
021006* 02/10/2006 - JULIE TEWES                                        00220046
021006*   ALLOW PROGRAM TO PICK TRANSFERS UP FOR THE CURRENT AND        00230046
021006*   PREVIOUS DAYS.                                                00240046
      ******************************************************************00240147
SR3403* 03/15/2012 - ANUJ GUPTA / CHERI PARMLEY                         00241049
SR3403*   LARGE RETAIL EXPANSION - TTFITPS INSERT ADJUSTMENT            00242048
SR3403*   COMPILE FOR CHANGES TO TTFITEM TABLE, ADDED COMMENTS FOR      00243049
SR3403*   TESTING PURPOSES.                                             00244049
      ******************************************************************00250046
C63498* 09/25/2013 - COGNIZANT                                          00251074
C63498*   REMOVED THE COMMIT LOGIC TO SOLVE DUPLICATE INSERT BY CICS    00252074
C63498*   APPLICATION AND BATCH JOB                                     00260074
      ******************************************************************00270030
       ENVIRONMENT DIVISION.                                            00280030
      ******************************************************************00290030
                                                                        00300030
       CONFIGURATION SECTION.                                           00310030
                                                                        00320030
       SOURCE-COMPUTER.        IBM-3090.                                00330030
       OBJECT-COMPUTER.        IBM-3090.                                00340030
                                                                        00350030
      *                                                                 00360030
      ******************************************************************00370030
       DATA DIVISION.                                                   00380030
      ******************************************************************00390030
                                                                        00400030
       WORKING-STORAGE SECTION.                                         00410030
                                                                        00420030
      *---------------------------------------------------------------* 00430030
      *    DB2 AREA FOR TFFMSTR (TRANSFER TABLE)                        00440030
      *---------------------------------------------------------------* 00450030
           EXEC SQL                                                     00460030
                INCLUDE TTFMSTR                                         00470030
           END-EXEC.                                                    00480030
      *---------------------------------------------------------------* 00490030
      *    DB2 AREA FOR TTFITEM (TRANSFER ITEM TABLE)                   00500030
      *---------------------------------------------------------------* 00510030
           EXEC SQL                                                     00520030
                INCLUDE TTFITEM                                         00530030
           END-EXEC.                                                    00540030
      *---------------------------------------------------------------* 00550030
      *    DB2 AREA FOR TTFTYCD (TRANSFER TYPE TABLE)                   00560030
      *---------------------------------------------------------------* 00570030
           EXEC SQL                                                     00580030
                INCLUDE TTFTYCD                                         00590030
           END-EXEC.                                                    00600030
      *---------------------------------------------------------------* 00610030
      *    DB2 AREA FOR TTFRCPT (TRANSFER RECEIPT TABLE)                00620030
      *---------------------------------------------------------------* 00630030
           EXEC SQL                                                     00640030
                INCLUDE TTFRCPT                                         00650030
           END-EXEC.                                                    00660030
      *---------------------------------------------------------------* 00670030
      *    DB2 AREA FOR TTFSTAT (TRANSFER STATUS TABLE)                 00680030
      *---------------------------------------------------------------* 00690030
           EXEC SQL                                                     00700030
                INCLUDE TTFSTAT                                         00710030
           END-EXEC.                                                    00720030
      *---------------------------------------------------------------* 00730030
      *    DB2 AREA FOR TTFITPS (TRANSFER ITEM POSTING TABLE)         * 00740030
      *---------------------------------------------------------------* 00750030
           EXEC SQL                                                     00760030
                INCLUDE TTFITPS                                         00770030
           END-EXEC.                                                    00780030
      *---------------------------------------------------------------* 00790030
      *    DB2 AREA FOR TCKRSTCN   (CHECKPOINT RESTART CONTROL TABLE) * 00800030
      *---------------------------------------------------------------* 00810030
           EXEC SQL                                                     00820030
                INCLUDE TCKRSTCN                                        00830030
           END-EXEC.                                                    00840030
      *---------------------------------------------------------------* 00850030
      *  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                00860030
      *---------------------------------------------------------------* 00870030
           EXEC SQL                                                     00880030
               INCLUDE SQLCA                                            00890030
           END-EXEC.                                                    00900030
      *                                                                 00910030
      *  EXTENSION TO THE SQL COMMUNICATIONS AREA FOR EDITING           00920030
      *  SQL CODES FOR DISPLAY                                          00930030
      *                                                                 00940030
           COPY SQLCA2.                                                 00950030
      *                                                                 00960030
       01  ABEND-CODE                  PIC S9(4)    VALUE ZERO          00970030
                                       COMP SYNC.                       00980030
                                                                        00990030
       01  PS-PROGRAM-SWITCHES.                                         01000030
           05  PS-END-OF-TABLE-SWITCH  PIC X(01)    VALUE 'N'.          01010030
               88  PS-EOT-YES                       VALUE 'Y'.          01020030
           05  PS-ITEM-LEFT-SWITCH     PIC X(01)    VALUE 'N'.          01030030
               88  PS-ITEMS-LEFT                    VALUE 'Y'.          01040030
               88  PS-NO-ITEMS-LEFT                 VALUE 'N'.          01050030
           05  PS-PGM-EOT-SWITCH       PIC X(01)    VALUE 'N'.          01060030
               88  PS-PGM-EOT-YES                   VALUE 'Y'.          01070030
               88  PS-PGM-EOT-NO                    VALUE 'N'.          01080030
                                                                        01090030
       01  PC-PROGRAM-CONSTANTS.                                        01100030
           05  PC-MAX-RETRIES          PIC S9(4)    VALUE +3            01110030
                                       COMP SYNC.                       01120030
           05  PC-PROG-NAME            PIC X(08)    VALUE 'TFKUT477'.   01130030
           05  PC-JOB-NAME             PIC X(08)    VALUE 'TFK005'.     01140030
           05  PC-ASSUMPTIVE-RECEIPT   PIC X(02)    VALUE 'AR'.         01160030
           05  PC-COMPLETE             PIC X(02)    VALUE 'CM'.         01170030
           05  PC-NO                   PIC X(01)    VALUE 'N'.          01180030
           05  PC-ONE                  PIC S9(4)    VALUE +1            01190030
                                       COMP SYNC.                       01200030
                                                                        01210030
       01  PV-PROGRAM-VARIABLES.                                        01220030
           05  PV-RETRY-COUNT          PIC S9(4)    VALUE ZERO          01230030
                                       COMP SYNC.                       01240030
           05  PV-TMST                 PIC X(26)   VALUE SPACES.        01250030
           05  PV-HOLD-TMST            PIC X(26)   VALUE SPACES.        01260030
           05  PV-CURRENT-TIMESTAMP    PIC X(26)   VALUE SPACES.        01270030
           05  PV-COMMIT-COUNT         PIC 9(05)   VALUE ZERO.          01280030
           05  PV-XFER-COUNT           PIC 9(05)   VALUE ZERO.          01290030
           05  PV-ROWS-INSERTED        PIC 9(05)   VALUE ZERO.          01310030
SR3403     05  PV-XFER-PRIC-AMT        PIC S9(5)V9(2) COMP-3.           01311067
                                                                        01320030
           EXEC SQL                                                     01330030
                DECLARE TRANSFER_CSR CURSOR WITH HOLD FOR               01340070
                SELECT DISTINCT                                         01350030
                      M.XFER_DKEY_ID                                    01360030
                     ,M.XFER_TYP_CDE                                    01370030
                     ,M.FR_STR_NBR                                      01380030
                     ,M.TO_STR_NBR                                      01390030
                FROM                                                    01400030
                      TTFMSTR M                                         01410030
                     ,TTFTYCD T                                         01420030
                     ,TTFSTAT S                                         01430030
                WHERE M.XFER_DKEY_ID       = S.XFER_DKEY_ID        AND  01440030
                      M.XFER_SEQ_NBR       > 0                     AND  01450030
                      S.XFER_STAT_TMST     >= M.CMPT_XFER_END_TMST AND  01460030
121205                DATE(M.CMPT_XFER_END_TMST)                        01470043
021006                                     <= CURRENT DATE - 1 DAY AND  01480046
                                                                        01490038
                      T.BYP_ASMTV_RCPT_IND = :PC-NO                AND  01500030
                      M.XFER_TYP_CDE       = T.XFER_TYP_CDE        AND  01510030
                      NOT EXISTS                                        01520030
                        (SELECT XFER_DKEY_ID                            01530030
                         FROM TTFSTAT                                   01540030
                         WHERE XFER_DKEY_ID = S.XFER_DKEY_ID       AND  01550030
                               XFER_STAT_CDE IN ('AR','PR','VD')        01560030
                         GROUP BY XFER_DKEY_ID)                         01570030
                ORDER BY                                                01580030
                      M.XFER_DKEY_ID                                    01590030
                                                                        01600030
           END-EXEC.                                                    01610030
                                                                        01620030
      *----------------------------------------------------------------*01630030
      * CURSOR TO RETRIEVE TRANSFER ITEMS                               01640030
      *----------------------------------------------------------------*01650030
           EXEC SQL                                                     01660030
              DECLARE  XFER_ITEM_CSR CURSOR FOR                         01670030
               SELECT  XFER_LNE_NBR                                     01680030
                      ,CPLD_RTL_PRIC_AMT                                01690030
                      ,XFER_PRIC_AMT                                    01700030
                      ,XFER_QTY                                         01710030
                 FROM  TTFITEM                                          01720030
                WHERE  (XFER_DKEY_ID  =  :TFMSTR-XFER-DKEY-ID)          01730030
             ORDER BY  XFER_LNE_NBR                                     01740030
           END-EXEC.                                                    01750030
                                                                        01760030
      ******************************************************************01770030
       PROCEDURE DIVISION.                                              01780030
      ******************************************************************01790030
       0000-MAINLINE.                                                   01800030
           PERFORM 1000-OPEN-TRANSFER-CURSOR.                           01810030
           PERFORM 2000-PROCESS-RECORDS                                 01830030
               UNTIL PS-EOT-YES.                                        01840030
           PERFORM 9000-EOJ-ROUTINE.                                    01850030
           STOP RUN.                                                    01860030
                                                                        01870030
      ******************************************************************01880030
      *  OPEN THE TRANSFER CURSOR.                                      01890030
      ******************************************************************01900030
       1000-OPEN-TRANSFER-CURSOR.                                       01910030
                                                                        01920030
           PERFORM WITH TEST AFTER                                      01930030
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       01940030
               UNTIL SQLCODE = 0 OR                                     01950030
                     PV-RETRY-COUNT > PC-MAX-RETRIES                    01960030
                   EXEC SQL                                             01970030
                        OPEN TRANSFER_CSR                               01980030
                   END-EXEC                                             01990030
                                                                        02000030
               EVALUATE TRUE                                            02010030
                   WHEN SQLCODE = +0                                    02020030
                   WHEN SQLCODE = -904                                  02030030
                   WHEN SQLCODE = -911                                  02040030
                       CONTINUE                                         02050030
                   WHEN SQLCODE = +100                                  02060030
                       SET PS-EOT-YES TO TRUE                           02070030
                   WHEN SQLWARN0 NOT = SPACES                           02080030
                   WHEN SQLCODE < 0                                     02090030
                   WHEN SQLCODE > 0                                     02100030
                       DISPLAY '1000-OPEN-TRANSFER-CURSOR'              02110030
                       DISPLAY 'OPEN TRANSFER-CSR CURSOR'               02120030
                       PERFORM 9100-SQL-ERROR                           02130030
               END-EVALUATE                                             02140030
           END-PERFORM.                                                 02150030
                                                                        02160030
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                           02170030
             DISPLAY '1000-OPEN-TRANSFER-CURSOR'                        02180030
             DISPLAY 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO '          02190030
             DISPLAY 'OPEN THE TRANSFER CURSOR'                         02200030
             PERFORM 9100-SQL-ERROR                                     02210030
           END-IF.                                                      02220030
      ******************************************************************02230030
      * GET THE TIMESTAMP FOR THE COMMITS                               02240030
      ******************************************************************02250030
      ******************************************************************02480030
      * PROCESS TRANSFER RECORDS TO BE ASSUMPTIVELY BOOKED              02490030
      ******************************************************************02500030
       2000-PROCESS-RECORDS.                                            02510030
                                                                        02520030
           PERFORM 2100-FETCH-TRANSFER-DATA.                            02530030
                                                                        02540030
           IF NOT PS-EOT-YES                                            02550030
      *        DISPLAY 'DKEY       = ' TFMSTR-XFER-DKEY-ID              02560030
      *        DISPLAY 'FROM STORE = ' TFMSTR-FR-STR-NBR                02570030
      *        DISPLAY ' '                                              02580030
               PERFORM 3000-INSERT-TTFSTAT                              02590030
               PERFORM 4000-INSERT-TTFRCPT                              02600030
               PERFORM 5000-PROCESS-DETAIL                              02610030
C63498         PERFORM 8000-CHECK-TO-COMMIT                             02630074
           END-IF.                                                      02660030
                                                                        02670030
      ******************************************************************02680030
      * FETCH THE NEXT TRANSFER ROW.                                    02690030
      ******************************************************************02700030
       2100-FETCH-TRANSFER-DATA.                                        02710030
           PERFORM                                                      02720030
             WITH TEST AFTER                                            02730030
             VARYING PV-RETRY-COUNT                                     02740030
             FROM 1 BY 1                                                02750030
             UNTIL ( SQLCODE = ZERO OR                                  02760030
                     SQLCODE = +100 OR                                  02770030
                    PV-RETRY-COUNT > PC-MAX-RETRIES)                    02780030
                                                                        02790030
               EXEC SQL                                                 02800030
                    FETCH TRANSFER_CSR                                  02810030
                       INTO :TFMSTR-XFER-DKEY-ID                        02820030
                           ,:TFMSTR-XFER-TYP-CDE                        02830030
                           ,:TFMSTR-FR-STR-NBR                          02840030
                           ,:TFMSTR-TO-STR-NBR                          02850030
               END-EXEC                                                 02860030
               EVALUATE TRUE                                            02870030
                   WHEN SQLCODE = +0                                    02880030
                   WHEN SQLCODE = -904                                  02890030
                   WHEN SQLCODE = -911                                  02900030
                       CONTINUE                                         02910030
                   WHEN SQLCODE = +100                                  02920030
                       SET PS-EOT-YES TO TRUE                           02930030
                   WHEN SQLWARN0 NOT = SPACES                           02940030
                   WHEN SQLCODE < 0                                     02950030
                   WHEN SQLCODE > 0                                     02960030
                       DISPLAY '2100-FETCH-TRANSFER-DATA'               02970030
                       DISPLAY 'READ TABLES TTFMSTR TTFTYCD TTFSTAT'    02980030
                       PERFORM 9100-SQL-ERROR                           02990030
               END-EVALUATE                                             03000030
           END-PERFORM.                                                 03010030
                                                                        03020030
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                           03030030
             DISPLAY '2100-FETCH-TRANSFER-DATA'                         03040030
             DISPLAY 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO '          03050030
             DISPLAY 'READ TABLES TTFMSTR TTFTYCD TTFSTAT'              03060030
             PERFORM 9100-SQL-ERROR                                     03070030
           END-IF.                                                      03080030
                                                                        03090030
      *----------------------------------------------------------------*03100030
      *  INSERT A ROW IN THE TRANSFER STATUS TABLE FOR ASSUMPTIVE RCPT *03110030
      *----------------------------------------------------------------*03120030
       3000-INSERT-TTFSTAT.                                             03130030
                                                                        03140030
C63498     ADD +1                TO PV-XFER-COUNT                       03160074
                                    PV-ROWS-INSERTED.                   03170030
           EXEC SQL                                                     03180030
               SET  :PV-CURRENT-TIMESTAMP  =  CURRENT TIMESTAMP         03190030
           END-EXEC.                                                    03200030
                                                                        03210030
           EVALUATE TRUE                                                03220030
               WHEN SQLCODE = ZERO                                      03230030
                   CONTINUE                                             03240030
               WHEN SQLWARN0 NOT = SPACES                               03250030
               WHEN SQLCODE < ZERO                                      03260030
               WHEN SQLCODE > ZERO                                      03270030
                   DISPLAY '3000-INSERT-TTFSTAT'                        03280030
                   DISPLAY 'SELECT CURRENT TIMESTAMP'                   03290030
                   PERFORM 9100-SQL-ERROR                               03300030
           END-EVALUATE.                                                03310030
                                                                        03320030
           PERFORM WITH TEST AFTER                                      03330030
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       03340030
               UNTIL SQLCODE = 0 OR                                     03350030
                     PV-RETRY-COUNT > PC-MAX-RETRIES                    03360030
               EXEC SQL                                                 03370030
                    INSERT                                              03380030
                      INTO TTFSTAT                                      03390030
                          (XFER_DKEY_ID                                 03400030
                         , XFER_STAT_TMST                               03410030
                         , XFER_STAT_CDE)                               03420030
                    VALUES                                              03430030
                          (:TFMSTR-XFER-DKEY-ID                         03440030
                         , :PV-CURRENT-TIMESTAMP                        03450030
                         , :PC-ASSUMPTIVE-RECEIPT)                      03460030
               END-EXEC                                                 03470030
                                                                        03480030
               EVALUATE TRUE                                            03490030
                   WHEN SQLCODE = 0                                     03500030
                   WHEN SQLCODE = -904                                  03510030
                   WHEN SQLCODE = -913                                  03520030
                        CONTINUE                                        03530030
                   WHEN OTHER                                           03540030
                       DISPLAY '3000-INSERT-TTFSTAT'                    03550030
                       DISPLAY 'INSERT TABLE TTFSTAT'                   03560030
                       DISPLAY 'TRANSFER DKEY : ' TFMSTR-XFER-DKEY-ID   03570030
                       PERFORM 9100-SQL-ERROR                           03580030
               END-EVALUATE                                             03590030
           END-PERFORM.                                                 03600030
                                                                        03610030
           IF PV-RETRY-COUNT > PC-MAX-RETRIES AND                       03620030
              SQLCODE NOT = 0                                           03630030
               DISPLAY '3000-INSERT-TTFSTAT'                            03640030
               DISPLAY 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO INSERT ' 03650030
               DISPLAY 'TABLE TTFSTAT'                                  03660030
               DISPLAY 'TRANSFER DKEY : ' TFMSTR-XFER-DKEY-ID           03670030
               PERFORM 9100-SQL-ERROR                                   03680030
           END-IF.                                                      03690030
       EJECT                                                            03700030
      *----------------------------------------------------------------*03710030
      *  INSERT A ROW IN THE TRANSFER RECEIPT TABLE.                   *03720030
      *----------------------------------------------------------------*03730030
       4000-INSERT-TTFRCPT.                                             03740030
                                                                        03750030
C63498     ADD +1                TO PV-ROWS-INSERTED.                   03770074
           PERFORM WITH TEST AFTER                                      03780030
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       03790030
               UNTIL SQLCODE = 0 OR                                     03800030
                     PV-RETRY-COUNT > PC-MAX-RETRIES                    03810030
               EXEC SQL                                                 03820030
                   INSERT INTO TTFRCPT                                  03830030
                       (XFER_DKEY_ID                                    03840030
                       ,RCV_XFER_BEG_TMST                               03850030
                       ,RCPT_MTHD_CDE                                   03860030
                       ,STR_NBR                                         03870030
                       ,RCV_XFER_END_TMST                               03880030
                       ,ASMTV_RCPT_TMST)                                03890030
                   VALUES                                               03900030
                       (:TFMSTR-XFER-DKEY-ID                            03910030
                       ,:PV-CURRENT-TIMESTAMP                           03920030
                       ,'03'                                            03930030
                       ,:TFMSTR-TO-STR-NBR                              03940030
                       ,CURRENT TIMESTAMP                               03950030
                       ,:PV-CURRENT-TIMESTAMP)                          03960030
               END-EXEC                                                 03970030
                                                                        03980030
               EVALUATE TRUE                                            03990030
                   WHEN SQLCODE = -904                                  04000030
                   WHEN SQLCODE = -913                                  04010030
                   WHEN SQLCODE = ZERO                                  04020030
                       CONTINUE                                         04030030
                   WHEN SQLWARN0 NOT = SPACES                           04040030
                   WHEN SQLCODE < ZERO                                  04050030
                   WHEN SQLCODE > ZERO                                  04060030
                       DISPLAY '4000-INSERT-TTFRCPT'                    04070030
                       DISPLAY 'INSERT TABLE TTFRCPT'                   04080030
                       DISPLAY 'TRANSFER DKEY : ' TFMSTR-XFER-DKEY-ID   04090030
                       PERFORM 9100-SQL-ERROR                           04100030
               END-EVALUATE                                             04110030
           END-PERFORM.                                                 04120030
                                                                        04130030
           IF PV-RETRY-COUNT > PC-MAX-RETRIES AND                       04140030
              SQLCODE NOT = 0                                           04150030
               DISPLAY '4000-INSERT-TTFRCPT'                            04160030
               DISPLAY 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO INSERT ' 04170030
               DISPLAY 'TABLE TTFRCPT'                                  04180030
               DISPLAY 'TRANSFER DKEY : ' TFMSTR-XFER-DKEY-ID           04190030
               PERFORM 9100-SQL-ERROR                                   04200030
           END-IF.                                                      04210030
       EJECT                                                            04220030
      *----------------------------------------------------------------*04230030
      *  READ ALL TRANSFER ITEMS FOR THE TRANSFER AND INSERT A ROW     *04240030
      *  IN THE TRANSFER ITEM RECEIPT AND TRANSFER POSTING TABLES.     *04250030
      *----------------------------------------------------------------*04260030
       5000-PROCESS-DETAIL.                                             04270030
           PERFORM 5100-OPEN-ITEM-CURSOR.                               04280030
           SET PS-ITEMS-LEFT                TO TRUE.                    04290030
           PERFORM 5200-FETCH-ITEM-CURSOR                               04300030
             UNTIL PS-NO-ITEMS-LEFT.                                    04310030
           PERFORM 5300-CLOSE-ITEM-CURSOR.                              04320030
       EJECT                                                            04330030
       5100-OPEN-ITEM-CURSOR.                                           04340030
                                                                        04350030
           PERFORM WITH TEST AFTER                                      04360030
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       04370030
               UNTIL SQLCODE = 0 OR                                     04380030
                     PV-RETRY-COUNT > PC-MAX-RETRIES                    04390030
               EXEC SQL                                                 04400030
                    OPEN XFER_ITEM_CSR                                  04410030
               END-EXEC                                                 04420030
                                                                        04430030
               EVALUATE TRUE                                            04440030
                   WHEN SQLCODE = +0                                    04450030
                   WHEN SQLCODE = -904                                  04460030
                   WHEN SQLCODE = -911                                  04470030
                       CONTINUE                                         04480030
                   WHEN SQLCODE = +100                                  04490030
                       SET PS-NO-ITEMS-LEFT    TO TRUE                  04500030
                   WHEN SQLWARN0 NOT = SPACES                           04510030
                   WHEN SQLCODE < 0                                     04520030
                   WHEN SQLCODE > 0                                     04530030
                       DISPLAY '5100-OPEN-ITEM-CURSOR'                  04540030
                       DISPLAY 'OPEN XFER_ITEM_CSR CURSOR'              04550030
                       PERFORM 9100-SQL-ERROR                           04560030
               END-EVALUATE                                             04570030
           END-PERFORM.                                                 04580030
                                                                        04590030
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                           04600030
             DISPLAY '5100-OPEN-ITEM-CURSOR'                            04610030
             DISPLAY 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO '          04620030
             DISPLAY 'OPEN THE TRANSFER ITEM CURSOR'                    04630030
             PERFORM 9100-SQL-ERROR                                     04640030
           END-IF.                                                      04650030
      ******************************************************************04660030
      * FETCH THE NEXT TRANSFER ITEM ROW.                               04670030
      ******************************************************************04680030
       5200-FETCH-ITEM-CURSOR.                                          04690030
           PERFORM                                                      04700030
             WITH TEST AFTER                                            04710030
             VARYING PV-RETRY-COUNT                                     04720030
             FROM 1 BY 1                                                04730030
             UNTIL ( SQLCODE = ZERO OR                                  04740030
                     SQLCODE = +100 OR                                  04750030
                    PV-RETRY-COUNT > PC-MAX-RETRIES)                    04760030
                                                                        04770030
               EXEC SQL                                                 04780030
                    FETCH XFER_ITEM_CSR                                 04790030
                       INTO :TFITEM-XFER-LNE-NBR                        04800030
                           ,:TFITEM-CPLD-RTL-PRIC-AMT                   04810030
                           ,:TFITEM-XFER-PRIC-AMT                       04820030
                           ,:TFITEM-XFER-QTY                            04830030
               END-EXEC                                                 04840030
               EVALUATE TRUE                                            04850030
                   WHEN SQLCODE = +0                                    04860030
                   WHEN SQLCODE = -904                                  04870030
                   WHEN SQLCODE = -911                                  04880030
                       CONTINUE                                         04890030
                   WHEN SQLCODE = +100                                  04900030
                       SET PS-NO-ITEMS-LEFT  TO TRUE                    04910030
                   WHEN SQLWARN0 NOT = SPACES                           04920030
                   WHEN SQLCODE < 0                                     04930030
                   WHEN SQLCODE > 0                                     04940030
                       DISPLAY '5200-FETCH-ITEM-CURSOR'                 04950030
                       DISPLAY 'READ TABLE TTFITEM'                     04960030
                       PERFORM 9100-SQL-ERROR                           04970030
               END-EVALUATE                                             04980030
           END-PERFORM.                                                 04990030
                                                                        05000030
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                           05010030
             DISPLAY '5200-FETCH-ITEM-CURSOR'                           05020030
             DISPLAY 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO '          05030030
             DISPLAY 'READ TABLE TTFITEM'                               05040030
             PERFORM 9100-SQL-ERROR                                     05050030
           END-IF.                                                      05060030
                                                                        05070030
           IF PS-ITEMS-LEFT                                             05080030
               PERFORM 7000-INSERT-TTFITPS                              05090030
C63498         ADD +1            TO PV-ROWS-INSERTED                    05110074
           END-IF.                                                      05120030
       EJECT                                                            05130030
       5300-CLOSE-ITEM-CURSOR.                                          05140030
                                                                        05150030
           PERFORM WITH TEST AFTER                                      05160030
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       05170030
               UNTIL SQLCODE = 0 OR                                     05180030
                     PV-RETRY-COUNT > PC-MAX-RETRIES                    05190030
                   EXEC SQL                                             05200030
                       CLOSE XFER_ITEM_CSR                              05210030
                   END-EXEC                                             05220030
                                                                        05230030
               EVALUATE TRUE                                            05240030
                   WHEN SQLCODE = +0                                    05250030
                   WHEN SQLCODE = -904                                  05260030
                   WHEN SQLCODE = -911                                  05270030
                   WHEN SQLCODE = +100                                  05280030
                       CONTINUE                                         05290030
                   WHEN SQLWARN0 NOT = SPACES                           05300030
                   WHEN SQLCODE < 0                                     05310030
                   WHEN SQLCODE > 0                                     05320030
                       DISPLAY '5300-CLOSE-ITEM-CURSOR'                 05330030
                       DISPLAY 'CLOSE XFER_ITEM_CSR CURSOR'             05340030
                       PERFORM 9100-SQL-ERROR                           05350030
               END-EVALUATE                                             05360030
           END-PERFORM.                                                 05370030
                                                                        05380030
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                           05390030
             DISPLAY '5300-CLOSE-ITEM-CURSOR'                           05400030
             DISPLAY 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO '          05410030
             DISPLAY 'CLOSE THE TRANSFER ITEM CURSOR'                   05420030
             PERFORM 9100-SQL-ERROR                                     05430030
           END-IF.                                                      05440030
      *                                                                 05450030
       7000-INSERT-TTFITPS.                                             05460030
111703*    IF TFMSTR-XFER-TYP-CDE = '01' OR '02'                        05470031
111703     IF TFMSTR-XFER-TYP-CDE = '00' OR '01' OR '02'                05480031
               MOVE 'ML'             TO TFITPS-EXTL-SYS-PRCG-CDE        05490030
           ELSE                                                         05500030
               MOVE 'FS'             TO TFITPS-EXTL-SYS-PRCG-CDE        05510030
           END-IF.                                                      05520030
SR3403     MOVE TFITEM-XFER-PRIC-AMT TO PV-XFER-PRIC-AMT                05530048
           PERFORM WITH TEST AFTER                                      05540030
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       05550030
               UNTIL SQLCODE = 0 OR                                     05560030
C63498               SQLCODE = -803 OR                                  05561074
                     PV-RETRY-COUNT > PC-MAX-RETRIES                    05570062
               EXEC SQL                                                 05580030
                   INSERT INTO TTFITPS                                  05590030
                       (XFER_DKEY_ID                                    05600030
                       ,XFER_LNE_NBR                                    05610030
                       ,PSTG_SEQ_NBR                                    05620030
                       ,XFER_RCPT_TMST                                  05630030
                       ,ADJ_RSN_CDE                                     05640030
                       ,ITM_PSTG_PRPD_TMST                              05650030
                       ,FRM_PSTG_STR_NBR                                05660030
                       ,TO_PSTG_STR_NBR                                 05670030
                       ,ITM_RTL_PRCE_AMT                                05680030
                       ,ITM_QTY                                         05690030
                       ,EXTL_SYS_PRCG_CDE)                              05700030
                   VALUES                                               05710030
                       (:TFMSTR-XFER-DKEY-ID                            05720030
                       ,:TFITEM-XFER-LNE-NBR                            05730030
                       ,:PC-ONE                                         05740030
                       ,:PV-CURRENT-TIMESTAMP                           05750030
                       ,:PC-COMPLETE                                    05760030
                       ,:PV-CURRENT-TIMESTAMP                           05770030
                       ,:TFMSTR-FR-STR-NBR                              05780030
                       ,:TFMSTR-TO-STR-NBR                              05790030
SR3403                 ,:PV-XFER-PRIC-AMT                               05801048
                       ,:TFITEM-XFER-QTY                                05810030
                       ,:TFITPS-EXTL-SYS-PRCG-CDE)                      05820030
               END-EXEC                                                 05830030
               EVALUATE TRUE                                            05840030
                   WHEN SQLCODE = ZERO                                  05850030
                   WHEN SQLCODE = -904                                  05860030
                   WHEN SQLCODE = -913                                  05870030
C63498             WHEN SQLCODE = -803                                  05871074
                       CONTINUE                                         05884072
                   WHEN SQLWARN0 NOT = SPACES                           05890030
                   WHEN SQLCODE < ZERO                                  05900030
                   WHEN SQLCODE > ZERO                                  05910030
                       DISPLAY '7000-INSERT-TTFITPS'                    05920030
                       DISPLAY 'INSERT DATA INTO TTFITPS'               05930056
                       PERFORM 9100-SQL-ERROR                           05940030
               END-EVALUATE                                             05950030
           END-PERFORM.                                                 05960030
                                                                        05970030
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                           05980030
             DISPLAY '7000-INSERT-TTFITPS'                              05990030
             DISPLAY 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO '          06000030
             DISPLAY 'INSERT DATA INTO TTFITPS'                         06010030
             PERFORM 9100-SQL-ERROR                                     06020030
           END-IF.                                                      06030030
      *                                                                 06040030
      ******************************************************************06050030
      *  CHECK IF IT IS TIME TO COMMIT                                 *06060030
      ******************************************************************06070030
       8000-CHECK-TO-COMMIT.                                            06080030
      *                                                                 06090030
                   EXEC SQL                                             06160030
                      COMMIT                                            06170030
                   END-EXEC                                             06180030
                   IF SQLCODE = +0                                      06190030
                       CONTINUE                                         06200030
                   ELSE                                                 06210030
                       DISPLAY '8000-CHECK-TO-COMMIT'                   06220030
                       DISPLAY 'COMMIT'                                 06230030
                       PERFORM 9100-SQL-ERROR                           06240030
                   END-IF                                               06250065
C63498             ADD 1 TO PV-COMMIT-COUNT.                            06260074
                                                                        06340030
      ******************************************************************06350030
      *  CLOSE THE TRANSFER CURSOR.                                    *06360030
      ******************************************************************06370030
       9000-EOJ-ROUTINE.                                                06380030
                                                                        06390030
           EXEC SQL                                                     06400030
                CLOSE TRANSFER_CSR                                      06410030
           END-EXEC.                                                    06420030
                                                                        06430030
           DISPLAY '*** COMMITS TAKEN:        ', PV-COMMIT-COUNT.       06440030
           DISPLAY '*** ROWS INSERTED:        ', PV-ROWS-INSERTED.      06450030
           DISPLAY '*** TRANSFERS PROCESSED:  ', PV-XFER-COUNT.         06460030
           EXEC SQL                                                     06470030
               COMMIT                                                   06480030
           END-EXEC                                                     06490030
           IF SQLCODE = +0                                              06500030
               CONTINUE                                                 06510030
           ELSE                                                         06520030
               DISPLAY '9000-EOJ-ROUTINE'                               06530030
               DISPLAY 'COMMIT'                                         06540030
               PERFORM 9100-SQL-ERROR                                   06550030
           END-IF.                                                      06560030
                                                                        06570030
      ******************************************************************06580030
      *  FORMAT A DB2 ERROR MESSAGE, RELEASE ALL LOCKS, AND ABEND THE  *06590030
      *  PROGRAM.                                                      *06600030
      ******************************************************************06610030
       9100-SQL-ERROR.                                                  06620030
           MOVE SQLCODE TO SQLCODE2.                                    06630030
           MOVE SQLERRD(3) TO SQLERRD3.                                 06640030
           DISPLAY '** ABEND **'.                                       06650030
           DISPLAY 'TFKUT477 - SQL ERROR'.                              06660030
           DISPLAY '       SQLCODE = ' SQLCODE2.                        06670030
           DISPLAY '      SQLWARN0 = ' SQLWARN0.                        06680030
           DISPLAY '      SQLWARN1 = ' SQLWARN1.                        06690030
           DISPLAY '      SQLWARN2 = ' SQLWARN2.                        06700030
           DISPLAY '      SQLWARN3 = ' SQLWARN3.                        06710030
           DISPLAY '      SQLWARN4 = ' SQLWARN4.                        06720030
           DISPLAY '      SQLWARN5 = ' SQLWARN5.                        06730030
           DISPLAY '      SQLWARN6 = ' SQLWARN6.                        06740030
           DISPLAY '      SQLWARN7 = ' SQLWARN7.                        06750030
           DISPLAY 'ROWS PROCESSED = ' SQLERRD3.                        06760030
           DISPLAY '*** COMMITS TAKEN:        ', PV-COMMIT-COUNT.       06770030
           DISPLAY '*** ROWS INSERTED:        ', PV-ROWS-INSERTED.      06780030
           DISPLAY '*** TRANSFERS PROCESSED:  ', PV-XFER-COUNT.         06790030
                                                                        06800030
           MOVE +4013 TO ABEND-CODE.                                    06810030
           CALL 'ILBOABN0' USING ABEND-CODE.                            06820030
                                                                        06830030
