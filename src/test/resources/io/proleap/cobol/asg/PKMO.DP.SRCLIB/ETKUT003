       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    ETKUT003.                                                 
       AUTHOR.        DAN FRANKLIN.                                             
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  03/30/92.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  DATE RE-WRITTEN : 03/11/93  (BY  JO ANN  SCHMIDT)                      
      ******************************************************************        
      *                                                                         
      *    THIS PROGRAM IS USED TO EXTRACT AND REPORT SALES AND                 
      *    ONHANDS, FOR EACH SPECIFIED DEPT/CL/INHSE.                           
      *                                                                         
      * ** SPECIAL NOTE **                                                      
      *    THE SALES AND ONHAND FIGURES AT STORE LEVEL DETAIL WILL NOT          
      *    INCLUDE FIGURES FOR CLOSED STORES.  AT THE CORPORATE LEVEL OF        
      *    DETAIL, THE STORE 000 FIGURES INCLUDE SALES AND ONHANDS FOR          
      *    CLOSED STORES.  NOTE THAT THIS IS BECAUSE OF HOW THE DATA IS         
      *    ON THE DATABASES.  THEREFORE THE STORE LEVEL AND CORPORORATE         
      *    LEVEL WILL NOT ALWAYS MATCH.                                         
      *                                                                         
      *    A SPECIFIC DEPT/CL/INHSE COMBINATION, UNIQUELY IDENTIFES A           
      *    PARTICULAR VENDOR WHO PROVIDES THE GOODS WITHIN THAT                 
      *    UNIQUE DEPT/CL/INHSE.                                                
      *                                                                         
      *    SALES AND ONHANDS MAY BE REPORTED FOR THE DAY OF THE WEEK            
      *    (THE DAY PREVIOUS TO THE DATE THE JOB IS EXECUTED) OR                
      *                                                                         
      *    ( NOTE:  THE CONTROL CARD INPUT PROCESSING DATE IS THE DATE          
      *             THE JOB IS EXECUTED MINUS 1 DAY  (PREV DAY). )              
      *                                                                         
      *    ON A CERTAIN DAY OF THE WEEK, BUT FOR A PERIOD OF ONE WEEK           
      *    (ENDING THE PREVIOUS SATURDAY FROM THE EXECUTION DATE).              
      *                                                                         
      *    ( NOTE:  THE WEEK REPORTED (SUNDAY THRU SATUDAY) IS THE              
      *             WEEK PRIOR TO THE DATE THE JOB WAS EXECUTED. )              
      *                                                                         
      *    ON-HANDS REPORTED ARE ALWAYS FOR THE BEGINING                        
      *    OF THE WEEK OR DAY BEING REPORTED.                                   
      *                                                                         
      *     A.  CREATES THE EDI SALES EXTRACT CONTROL REPORT.                   
      *     B.  CREATES AN EDI SALES EXTRACT FILE.                              
      *                                                                         
      *    THE 'TRANSMIT SALES' FILE CONTAINS A 'DAILY' OR 'WEEKLY'             
      *    INDICATOR, WHICH DETERMINES IF THE PROGRAM IS TO EXTRACT             
      *    SALES HISTORY FOR A WHOLE WEEK, OR JUST FOR THE CURRENT              
      *    CONTROL CARD PROCESSING DATE.                                        
      *                                                                         
      *    THE 'TRANSMIT SALES' FILE ALSO CONTAINS 7 BYTES WHICH                
      *    DIRECTLY RELATE TO THE WEEKDAY THAT THE SALES HISTORY                
      *    IS TO BE TRANSMITTED BACK TO OUR EDI VENDORS. THIS MEANS             
      *    THAT IF A SPECIFIC WEEKDAY IS FLAGGED FOR A GIVEN VENDOR             
      *    (A SPECIFIC DEPT/INHOUSE COMBINATION), THE SALES                     
      *    HISTORY TRANSMITTED WILL BE FOR THE PREVIOUS WEEKDAY                 
      *    (OR THE 7 DAYS ENDING ON THE PREVIOUS SATURDAY, IF SALES             
      *    HISTORY IS TO BE REPORTED ONLY ONCE A WEEK).                         
      *                                                                         
      *    ** TRANSMIT SALES, VENDOR RECORD, DAY-OF-THE-WEEK PORTION:           
      *    THE FOLLOWING POSITIONS INDICATE THE DATE TO TRANSMIT SALES          
      *      ( 1=SUN, 2=MON, 3=TUES, 4=WED, 5=THUR, 6=FRI, 7=SAT )              
      *                                                                         
      *    IF THE CURRENT DATE IS A MONDAY, AND POSITION TWO OF THE             
      *    VENDOR'S RECORD, DAY-OF-THE-WEEK SEGMENT, HAS A VALUE OF 'X'         
      *    (MEANING THIS EDI VENDOR WANTS SALES HISTORY TRANSMITTED ON          
      *    MONDAYS), ALL SALES HISTORY WILL BE EXTRACTED FOR SUNDAY.            
      *                                                                         
      *    IF ALL OF THE SEVEN WEEKDAYS HAVE BEEN FLAGGED, EACH DAY WHEN        
      *    THIS PROGRAM RUNS, THE PREVIOUS DAYS SALES WILL BE EXTRACTED,        
      *    REPORTED, AND TRANSMITTED.                                           
      *                                                                         
      *    THE 'TRANSMIT SALES' FILE ALSO CONTAINS AN INDICATOR WHICH           
      *    INDENTIFIES WHETHER THE SALES SHOULD BE REPORTED FOR ALL             
      *    STORES, OR FOR EACH STORE IN THE COMPANY. THE VALUE IS EITHER        
      *    'CORP' OR BLANK (FOR STORE LEVEL DETAILS).                           
      *                                                                         
      *    IN ADDITION, THE 'TRANSMIT SALES' FILE WILL ALSO CONTAIN AN          
      *    'ALL SALES' OR 'INDIVIDUAL SALES' INDICATOR, WHICH WILL              
      *    INDICATE IF THE VENDOR WANTS TO SEE SALES BROKEN UP BY               
      *    RETAIL, PROMOTIONAL, AND CLEARANCE OR IF THE VENDOR WANTS            
      *    THEM ALL COMBINED INTO ONE SALES TOTAL.                              
      *                                                                         
      *    THIS PROGRAM USES THE NEW 'F4' CALENDAR TO DETERMINE                 
      *    ALL DATES REQUIRED, FROM THE CALENDAR ROUTINE.                       
      *    THUS, THE ONLY REQUIRED INPUTS WILL BE THE SCHEDULED                 
      *    RUN DATE (- 1 DAY), AND THE FISCAL CALENDAR TYPE OF 'F4'.            
      *                                                                         
      *    REPORTING OF STORE '000' IS USED TO CREATE A SUMMARY                 
      *    VIEW OF DETAIL INFORMATION, ACROSS ALL STORES.                       
      *                                                                         
      *    PROCESSING :                                                         
      *                                                                         
      *       O  CREATE A RECORD (EITHER AT THE STORE OR CORP LEVEL             
      *            (STORE 000) ) FOR EACH ITEM HAVING NON-ZERO SALES            
      *            AND ON-HAND COUNTS.                                          
      *       O  CREATE A RECORD (EITHER AT THE STORE OR CORP LEVEL             
      *            (STORE 000) ) FOR EACH NON-ZERO ON-HAND QUANTITY             
      *       O  CREATE A RECORD (EITHER AT THE STORE OR CORP LEVEL             
      *            (STORE 000) ) FOR EACH NON-ZERO SALES QUANTITY               
      *                                                                         
      ******************************************************************        
      *    REMOVE THE PROCESSING OF THE VSAM FILE (EDI-VENDOR-PROFILE-          
      *    FILE).  REMOVE THE PROCESSING OF THE IMS VENDOR DATABASE.            
      *    REPLACE BOTH WITH PROCESSING OF THE DB2 EXTERNAL ENTERPRISE          
      *    TABLES AND THE VENDOR AGREEMENT TABLE.                               
      *                                                                         
      *    CHANGE MADE BY: KIM WRIGHT    04/22/94                               
      *                                                                         
      *                                                                         
      *    CHANGED THE WEEKLY_ONHANDS CURSOR TO USE THE NEW WEEKLY              
      *      INVENTORY TABLE TSKSTWKI INSTEAD OF TSKSTWKINV.  ADDED             
      *      PV-BOW- VARIABLES TO ACCOMODATE THIS CHANGE.                       
      *                                                                         
      *    CHANGE MADE BY: TOM MEDREK    05/27/98     WR: 6041                  
      *                                                                         
      *    CHANGED THE WEEKLY AND DAILY SALES CURSORS TO USE THE NEW            
      *      WEEKLY SALES TABLE TSKSTWKS INSTEAD OF TSKSTWKSL.  ADDED           
      *      PV-BOW- VARIABLES TO ACCOMODATE THIS CHANGE.                       
      *                                                                         
      *    CHANGE MADE BY: STEVE EASKER  09/25/98     WR: 6041                  
************************************************************************        
JF0403*  DATE  04/16/03                                                         
JF0403*  CHANGE MADE:                                                           
JF0403*   UPDATED PROGRAM SO IT IS COMPLIANT WITH NI-SKU OBJECTIVES.            
JF0403*  MADE BY: JON FIEDLER             WR# : WR22777                         
************************************************************************        
JF0503*  DATE  05/05/03                                                         
JF0503*  CHANGE MADE:                                                           
JF0503*  CHANGED CALPHALON AND NIKE TO RECEIVE END-OF-WEEK ONHANDS.             
JF0503*  MADE BY: JON FIEDLER             WR# : WR24553 WR25101                 
************************************************************************        
JF0503*  DATE  05/08/03                                                         
JF0503*  CHANGE MADE:                                                           
JF0503*  CHANGED DEPT_CL_INHSE_ITEM CURSOR SO IT SELECTS USING THE DEPT         
JF0503*  FIELD FROM TSKXREF TABLE INSTEAD OF THE DEPT_NBR FIELD. THE            
JF0503*  DEPT_NBR IS APART OF THE INTELLIGENT SKU AND WILL NOT BE USED          
JF0503*  ONCE THE NON-INTELLIGENT SKU GOES LIVE.                                
JF0503*  MADE BY: JON FIEDLER             WR# : P000546588                      
************************************************************************        
VF0105*  DATE  01/11/05                                                         
VF0105*  CHANGE MADE:                                                           
VF0105*         CHANGED THE PROGRAM SO THAT IT ONLY EXTRACTS EOW                
      *         ONHANDS.                                                        
      *  MADE BY: VLADIMIR FILIPOVIC      WR# : WR26913                         
*******************************************************************             
JF0305*  DATE  02/17/2005                                                       
JF0305*  CHANGE MADE:                                                           
JF0305*   UPDATED PROGRAM FOR STORE EXPANSION TO FIVE DIGITS.                   
JF0305*  MADE BY: JON FIEDLER             WR# : WR26913                         
************************************************************************        
*******************************************************************             
C96566*  DATE  10/22/2014                                                       
C96566*  CHANGE MADE:                                                           
C96566*   ADDED WITH UR IN SELECT QUERY FOR SELECTING ROWS FROM XIT_LOC         
C96566*   TABLE TO AVOID CONTENTION ISSUES.                                     
C96566*  MADE BY: COGNIZANT               WR# : CHG0096566                      
************************************************************************        
      *                                                                         
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
      *                                                                         
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT ITEM-SELECT-FILE                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT EDI-SALES-EXTRACT-FILE                                        
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT EDI-REPORT-FILE                                               
               ASSIGN TO RPT01.                                                 
      *                                                                         
      *                                                                         
       DATA DIVISION.                                                           
      *                                                                         
      *                                                                         
       FILE SECTION.                                                            
                                                                                
       FD  ITEM-SELECT-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS ITEM-SELECT-RECORD.                                   
                                                                                
       01  ITEM-SELECT-RECORD                   PIC X(80).                      
                                                                                
       FD  EDI-SALES-EXTRACT-FILE                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           DATA RECORD IS EDI-SALES-EXTRACT-RECORD.                             
                                                                                
       01  EDI-SALES-EXTRACT-RECORD             PIC X(350).                     
                                                                                
       FD  EDI-REPORT-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS EDI-REPORT-LINE.                                      
                                                                                
       01  EDI-REPORT-LINE                      PIC X(132).                     
      *                                                                         
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CC-CONTROL-CARD.                                                     
           05  CC-RUN-DATE.                                                     
               10  CC-RUN-MM                    PIC X(02)  VALUE SPACES.        
               10  CC-RUN-DD                    PIC X(02)  VALUE SPACES.        
               10  CC-RUN-YY                    PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(01)  VALUE SPACES.        
           05  CC-FISCAL-CALENDAR-TYPE          PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(71)  VALUE SPACES.        
                                                                                
       01  RD-REFORMATTED-DATE.                                                 
WR6041     05 RD-RUN-CC                         PIC 9(02)  VALUE ZEROS.         
           05 RD-RUN-DATE.                                                      
              10  RD-YY                         PIC 9(02)  VALUE ZEROS.         
              10  RD-MM                         PIC 9(02)  VALUE ZEROS.         
              10  RD-DD                         PIC 9(02)  VALUE ZEROS.         
                                                                                
       01  OD-OPENED-DATE.                                                      
           05 OD-OPENED-YY                      PIC 9(04)  VALUE ZEROS.         
           05 FILLER                            PIC 9(01)  VALUE ZEROS.         
           05 OD-OPENED-MM                      PIC 9(02)  VALUE ZEROS.         
           05 FILLER                            PIC 9(01)  VALUE ZEROS.         
           05 OD-OPENED-DD                      PIC 9(02)  VALUE ZEROS.         
                                                                                
       01  RO-REFORMATTED-OPENED-DATE.                                          
           05 RO-OPENED-YY                      PIC 9(04)  VALUE ZEROS.         
           05 RO-OPENED-MM                      PIC 9(02)  VALUE ZEROS.         
           05 RO-OPENED-DD                      PIC 9(02)  VALUE ZEROS.         
                                                                                
       01  CD-CLOSED-DATE.                                                      
           05 CD-CLOSED-YY                      PIC 9(04)  VALUE ZEROS.         
           05 FILLER                            PIC 9(01)  VALUE ZEROS.         
           05 CD-CLOSED-MM                      PIC 9(02)  VALUE ZEROS.         
           05 FILLER                            PIC 9(01)  VALUE ZEROS.         
           05 CD-CLOSED-DD                      PIC 9(02)  VALUE ZEROS.         
                                                                                
       01  RC-REFORMATTED-CLOSED-DATE.                                          
           05 RC-CLOSED-YY                      PIC 9(04)  VALUE ZEROS.         
           05 RC-CLOSED-MM                      PIC 9(02)  VALUE ZEROS.         
           05 RC-CLOSED-DD                      PIC 9(02)  VALUE ZEROS.         
                                                                                
       01  FS-FILE-STATUS.                                                      
           05  FS-EDI-VP-STATUS                 PIC X(02)  VALUE SPACES.        
               88  FS-SUCCESSFUL-COMPLETION                VALUE '00'.          
               88  FS-RECORD-NOT-FOUND                     VALUE '23'.          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
                                                                                
           05  PC-PROGRAM-NAME                  PIC X(08)  VALUE                
                                                           'ETKUT003'.          
           05  PC-DPKUT500-CALENDAR             PIC X(08)  VALUE                
                                                           'DPKUT500'.          
                                                                                
           05  PC-MAX-RETRIES                   PIC S9(3)  VALUE +3             
                                                           COMP-3.              
JF0305*    05  PC-STORE-ZERO                    PIC X(03)  VALUE '000'.         
           05  PC-YES                           PIC X(01)  VALUE 'Y'.           
           05  PC-RETAIL                        PIC X(02)  VALUE '01'.          
           05  PC-PROMO                         PIC X(02)  VALUE '02'.          
           05  PC-CLEAR                         PIC X(02)  VALUE '03'.          
           05  PC-TRANSACTION-SET-ID            PIC X(03)  VALUE '852'.         
           05  PC-MAX-LINES                     PIC S9(04) VALUE +63            
                                                           COMP SYNC.           
JF0305     05  PC-MAX-STORES                    PIC S9(5)  VALUE +99999         
                                                           COMP-3.              
                                                                                
       01  PC-PROGRAM-COUNTERS.                                                 
           05  PC-PAGE-CNTR                     PIC S9(03) VALUE +0             
                                                           COMP-3.              
           05  PC-LINE-CNTR                     PIC S9(04) VALUE +0             
                                                           COMP SYNC.           
           05  PC-TOT-DPT-CL-INHSE-ITEMS        PIC S9(07) VALUE +0             
                                                           COMP-3.              
           05  PC-NON-EDI-CNTR                  PIC S9(07) VALUE +0             
                                                           COMP-3.              
           05  PC-EDI-CNTR                      PIC S9(07) VALUE +0             
                                                           COMP-3.              
           05  PC-SALES-CNTR                    PIC S9(07) VALUE +0             
                                                           COMP-3.              
           05  PC-NO-SALES-CNTR                 PIC S9(07) VALUE +0             
                                                           COMP-3.              
           05  PC-ONHANDS-CNTR                  PIC S9(07) VALUE +0             
                                                           COMP-3.              
           05  PC-NO-ONHANDS-CNTR               PIC S9(07) VALUE +0             
                                                           COMP-3.              
                                                                                
       01  PS-PROGRAM-SUBSCRIPTS.                                               
           05  PS-SUB                           PIC S9(04) VALUE +0             
                                                           COMP SYNC.           
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
                                                                                
           05  PS-ITEM-SELECT-EOF-SW            PIC X(01)  VALUE 'N'.           
               88  PS-ITEM-SELECT-EOF                      VALUE 'Y'.           
                                                                                
           05  PS-VALID-STORES-SW               PIC X(01)  VALUE 'N'.           
               88  PS-VALID-STORE                          VALUE 'Y'.           
               88  PS-NOT-VALID-STORE                      VALUE 'N'.           
                                                                                
           05  PS-DEPT-CL-INHSE-SW              PIC X(01)  VALUE 'N'.           
               88  PS-NEW-DEPT-CL-INHSE-REC                VALUE 'Y'.           
               88  PS-NO-DEPT-CL-INHSE-REC                 VALUE 'N'.           
                                                                                
           05  PS-DEPT-CL-INHSE-OPEN-SW         PIC X(01)  VALUE 'N'.           
               88  PS-DEPT-CL-INHSE-OPENED                 VALUE 'Y'.           
               88  PS-DEPT-CL-INHSE-NOT-OPENED             VALUE 'N'.           
                                                                                
           05  PS-PROCESS-ENTRY-SW              PIC X(01)  VALUE 'N'.           
               88  PS-PROCESS-ENTRY                        VALUE 'Y'.           
               88  PS-DO-NOT-PROCESS-ENTRY                 VALUE 'N'.           
                                                                                
           05  PS-PROCESSING-COMPLETE-SW        PIC X(01)  VALUE 'N'.           
               88  PS-PROCESSING-COMPLETE                  VALUE 'Y'.           
               88  PS-PROCESS-NOT-COMPLETE                 VALUE 'N'.           
                                                                                
           05  PS-IMS-VENDOR-FOUND-SW           PIC X(01)  VALUE 'N'.           
               88  PS-VENDOR-FOUND                         VALUE 'Y'.           
               88  PS-VENDOR-NOT-FOUND                     VALUE 'N'.           
                                                                                
           05  PS-VALID-EDI-VENDOR-SW           PIC X(01)  VALUE 'N'.           
               88  PS-VALID-EDI-VENDOR                     VALUE 'Y'.           
               88  PS-INVALID-EDI-VENDOR                   VALUE 'N'.           
                                                                                
           05  PS-FIRST-ITEM-SW                 PIC X(01)  VALUE 'N'.           
               88  PS-FIRST-ITEM                           VALUE 'Y'.           
               88  PS-NOT-FIRST-ITEM                       VALUE 'N'.           
                                                                                
           05  PS-EDI-ITEM-SW                   PIC X(01)  VALUE 'N'.           
               88  PS-EDI-ITEM                             VALUE 'Y'.           
               88  PS-NON-EDI-ITEM                         VALUE 'N'.           
                                                                                
           05  PS-MORE-ITEMS-SW                 PIC X(01)  VALUE 'N'.           
               88  PS-MORE-ITEMS                           VALUE 'Y'.           
               88  PS-NO-MORE-ITEMS                        VALUE 'N'.           
                                                                                
           05  PS-ITEM-SALES-SW                 PIC X(01)  VALUE 'N'.           
               88  PS-ITEM-SALES                           VALUE 'Y'.           
               88  PS-NO-ITEM-SALES                        VALUE 'N'.           
                                                                                
           05  PS-SALES-RETREIVED-SW            PIC X(01)  VALUE 'N'.           
               88  PS-SALES-RETREIVED                      VALUE 'Y'.           
               88  PS-NO-SALES-RETREIVED                   VALUE 'N'.           
                                                                                
           05  PS-MORE-ONHANDS-SW               PIC X(01)  VALUE 'N'.           
               88  PS-MORE-ONHANDS                         VALUE 'Y'.           
               88  PS-NO-MORE-ONHANDS                      VALUE 'N'.           
                                                                                
           05  PS-CREATE-000-REC-SW             PIC X(01)  VALUE 'N'.           
               88  PS-CREATE-000-REC                       VALUE 'Y'.           
               88  PS-NO-000-REC                           VALUE 'N'.           
                                                                                
           05  PS-FIRST-STORE-SW                PIC X(01)  VALUE 'N'.           
               88  PS-FIRST-STORE                          VALUE 'Y'.           
               88  PS-NOT-FIRST-STORE                      VALUE 'N'.           
                                                                                
           05  PS-ITEM-SUMMARY-SW               PIC X(01)  VALUE 'N'.           
               88  PS-CREATE-ITEM-SUMMARY                  VALUE 'Y'.           
               88  PS-NO-ITEM-SUMMARY                      VALUE 'N'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
                                                                                
           05  PV-PARAGRAPH-NAME                PIC X(30)  VALUE SPACES.        
           05  PV-DB2-OPERATION-ATTEMPTED       PIC X(30)  VALUE SPACES.        
                                                                                
WR6041     05  PV-BOW-ISO-DTE                   PIC X(10)  VALUE SPACES.        
WR6041     05  PV-FSCL-BOW-DTE                  PIC X(10)  VALUE SPACES.        
WR6041     05  PV-BOW-CURR-DTE                  PIC X(10)  VALUE SPACES.        
WR6041     05  PV-BOW-CURR-DTE-DLY              PIC X(10)  VALUE SPACES.        
WR6041     05  PV-BOW-LAST-DTE                  PIC X(10)  VALUE SPACES.        
WR6041     05  PV-GREG-DTE                      PIC X(06)  VALUE SPACES.        
                                                                                
JF0305     05  PV-STORE-NMBR                    PIC X(05)  VALUE SPACES.        
JF0305     05  PV-STORE-NMBR-NUM                PIC 9(05).                      
           05  PV-DEPT-INHSE-NBR                PIC S9(03) VALUE +0             
                                                           COMP-3.              
           05  PV-DEPT-POINTER-KEY.                                             
               10  PV-OP-CO                     PIC X(02)  VALUE '03'.          
JF0503         10  PV-DEPT                      PIC S9(04)V COMP-3.             
               10  PV-DEPT-INHOUSE              PIC X(03)  VALUE SPACES.        
                                                                                
JF0305     05  PV-LAST-STORE                    PIC X(05)  VALUE SPACES.        
                                                                                
           05  PV-QTY-UNITS-ONHAND              PIC S9(07) VALUE +0             
                                                           COMP-3.              
                                                                                
           05  PV-QTY-UNITS-SOLD                PIC S9(07) VALUE +0             
                                                           COMP-3.              
           05  PV-SALES-AMT                     PIC S9(09)V99                   
                                                           VALUE +0             
                                                           COMP-3.              
                                                                                
           05  PV-RETAIL-SALES-AMT              PIC S9(09)V99                   
                                                           VALUE +0             
                                                           COMP-3.              
           05  PV-PROMO-SALES-AMT               PIC S9(09)V99                   
                                                           VALUE +0             
                                                           COMP-3.              
           05  PV-CLEAR-SALES-AMT               PIC S9(09)V99                   
                                                           VALUE +0             
                                                           COMP-3.              
           05  PV-ALL-UNITS-SOLD                PIC S9(07) VALUE +0             
                                                           COMP-3.              
           05  PV-RETAIL-UNITS-SOLD             PIC S9(07) VALUE +0             
                                                           COMP-3.              
           05  PV-PROMO-UNITS-SOLD              PIC S9(07) VALUE +0             
                                                           COMP-3.              
           05  PV-CLEAR-UNITS-SOLD              PIC S9(07) VALUE +0             
                                                           COMP-3.              
                                                                                
           05  PV-DPT-CL-INHSE-DUNNS            PIC X(9)   VALUE SPACES.        
                                                                                
           05  PV-DEPT-CL-INHSE-TOTALS.                                         
               10  PV-DPT-CL-INHSE-UNITS-SOLD   PIC S9(15) VALUE +0             
                                                           COMP-3.              
               10  PV-DPT-CL-INHSE-ONHANDS      PIC S9(15) VALUE +0             
                                                           COMP-3.              
                                                                                
       01  PV-PROCESSING-DATES.                                                 
           05  PV-YESTERDAY-WKDY                PIC 9(01)  VALUE ZERO.          
               88  PV-YESTERDAY-SUNDAY          VALUE 1.                        
               88  PV-YESTERDAY-MONDAY          VALUE 2.                        
               88  PV-YESTERDAY-TUESDAY         VALUE 3.                        
               88  PV-YESTERDAY-WEDNESDAY       VALUE 4.                        
               88  PV-YESTERDAY-THURSDAY        VALUE 5.                        
               88  PV-YESTERDAY-FRIDAY          VALUE 6.                        
               88  PV-YESTERDAY-SATURDAY        VALUE 7.                        
           05  PV-RUN-DATE-WKDY                 PIC 9(01)  VALUE ZERO.          
               88  PV-RUN-DATE-SUNDAY           VALUE 1.                        
               88  PV-RUN-DATE-MONDAY           VALUE 2.                        
               88  PV-RUN-DATE-TUESDAY          VALUE 3.                        
               88  PV-RUN-DATE-WEDNESDAY        VALUE 4.                        
               88  PV-RUN-DATE-THURSDAY         VALUE 5.                        
               88  PV-RUN-DATE-FRIDAY           VALUE 6.                        
               88  PV-RUN-DATE-SATURDAY         VALUE 7.                        
           05  PV-PREV-WK-START-DATE.                                           
               10  PV-PREV-WK-START-MM          PIC X(02)  VALUE SPACES.        
               10  PV-PREV-WK-START-DD          PIC X(02)  VALUE SPACES.        
               10  PV-PREV-WK-START-YY          PIC X(02)  VALUE SPACES.        
           05  PV-PREV-WK-END-DATE.                                             
               10  PV-PREV-WK-END-MM            PIC X(02)  VALUE SPACES.        
               10  PV-PREV-WK-END-DD            PIC X(02)  VALUE SPACES.        
               10  PV-PREV-WK-END-YY            PIC X(02)  VALUE SPACES.        
                                                                                
       01  TD-TODAYS-DATE.                                                      
           05  TD-YY                            PIC 9(02)  VALUE ZEROES.        
           05  TD-MM                            PIC 9(02)  VALUE ZEROES.        
           05  TD-DD                            PIC 9(02)  VALUE ZEROES.        
                                                                                
       01  TT-TODAYS-TIME.                                                      
           05  TT-HR                            PIC 9(02)  VALUE ZEROES.        
           05  TT-MN                            PIC 9(02)  VALUE ZEROES.        
           05  TT-SC                            PIC 9(02)  VALUE ZEROES.        
           05  FILLER                           PIC 9(02)  VALUE ZEROES.        
                                                                                
       01  ABEND-CODE                           PIC S9(04) VALUE +0             
                                                           COMP SYNC.           
                                                                                
      *****************************************************************         
      *    THE FOLLOWING TABLE IS USED TO HOLD ALL STORE NUMBERS FOR            
      *    STORES THAT OPENED BEFORE THE CONTROL CARD RUN DATE AND              
      *    HAVE THE CLOSED AFTER THE CONTROL CARD RUN DATE.                     
      *****************************************************************         
       01  VALID-STORES-TABLE.                                                  
JF0305     05  VALID-STORES OCCURS 9999 TIMES INDEXED BY STORE-IDX.             
JF0305         10  STORE-NMBR                  PIC X(05)  VALUE SPACES.         
                                                                                
       EJECT                                                                    
      *****************************************************************         
      *    CALENDAR ROUTINE WORKING STORAGE  'DPKUT500'                         
      *****************************************************************         
           COPY DPWS500.                                                        
                                                                                
       EJECT                                                                    
      *****************************************************************         
      *    KOHLS STANDARD 132 COLUMN HEADER LAYOUT.                             
      *****************************************************************         
           COPY DPWS132O.                                                       
                                                                                
       EJECT                                                                    
      *****************************************************************         
      *   EDI SALES EXTRACT REPORT LINES                                        
      *****************************************************************         
                                                                                
       01  RPT-TITLE.                                                           
           05  FILLER                           PIC X(44)  VALUE SPACES.        
           05  FILLER                           PIC X(44)  VALUE                
           'EDI  SALES / ON-HAND  EXTRACT CONTROL REPORT'.                      
           05  FILLER                           PIC X(44)  VALUE SPACES.        
                                                                                
       01  RPT-DETAIL-HEADING-1.                                                
           05  FILLER                           PIC X(56)  VALUE SPACES.        
           05  FILLER                           PIC X(08)  VALUE                
           'TOT NMBR'.                                                          
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(07)  VALUE                
           'NMBR OF'.                                                           
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(07)  VALUE                
           'NMBR OF'.                                                           
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(07)  VALUE                
           'EXTRACT'.                                                           
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(08)  VALUE                
           'ITMS WTH'.                                                          
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(08)  VALUE                
           'ITMS WTH'.                                                          
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(08)  VALUE                
           'ITMS WTH'.                                                          
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(08)  VALUE                
           'ITMS WTH'.                                                          
           05  FILLER                           PIC X(01)  VALUE SPACES.        
                                                                                
       01  RPT-DETAIL-HEADING-2.                                                
           05  FILLER                           PIC X(01)  VALUE SPACES.        
           05  FILLER                           PIC X(35)  VALUE                
           '----------- VENDOR NAME -----------'.                               
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(03)  VALUE                
           'DPT'.                                                               
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(02)  VALUE                
           '  '.                                                                
      *    'CL'.                                                                
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(05)  VALUE                
           'INHSE'.                                                             
           05  FILLER                           PIC X(04)  VALUE SPACES.        
           05  FILLER                           PIC X(08)  VALUE                
           'OF ITEMS'.                                                          
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(07)  VALUE                
           'NON-EDI'.                                                           
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(07)  VALUE                
           '  EDI  '.                                                           
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(07)  VALUE                
           'ONHANDS'.                                                           
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(08)  VALUE                
           ' SALES  '.                                                          
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(08)  VALUE                
           'NO SALES'.                                                          
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(08)  VALUE                
           'ON-HANDS'.                                                          
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(08)  VALUE                
           'NO ONHDS'.                                                          
           05  FILLER                           PIC X(01)  VALUE SPACES.        
                                                                                
       01  RPT-DETAIL-LINE.                                                     
           05  FILLER                           PIC X(01)  VALUE SPACE.         
           05  RPT-DTL-VENDOR-NAME              PIC X(35)  VALUE SPACES.        
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  RPT-DTL-DEPT                     PIC X(03)  VALUE SPACES.        
           05  FILLER                           PIC X(02)  VALUE SPACES.        
      *    05  RPT-DTL-CLASS                    PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  FILLER                           PIC X(03)  VALUE SPACES.        
           05  RPT-DTL-DEPT-INHOUSE             PIC X(03)  VALUE SPACES.        
           05  FILLER                           PIC X(05)  VALUE SPACES.        
           05  RPT-DTL-TOT-NMBR-ITEMS           PIC Z(07)9 VALUE ZEROES.        
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  RPT-DTL-TOT-NON-EDI-ITEMS        PIC Z(06)9 VALUE ZEROES.        
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  RPT-DTL-TOT-EDI-ITEMS            PIC Z(06)9 VALUE ZEROES.        
           05  FILLER                           PIC X(04)  VALUE SPACES.        
           05  RPT-DTL-REPORT-ONHDS             PIC X(03)  VALUE ZEROES.        
               88  RPT-DTL-REPORT-ONHANDS-YES      VALUE 'YES'.                 
               88  RPT-DTL-REPORT-ONHANDS-NO       VALUE 'NO '.                 
           05  FILLER                           PIC X(04)  VALUE SPACES.        
           05  RPT-DTL-EDI-ITEMS-SALES          PIC Z(07)9 VALUE ZEROES.        
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  RPT-DTL-EDI-ITEMS-NO-SALES       PIC Z(07)9 VALUE ZEROES.        
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  RPT-DTL-ITEMS-WITH-ONHANDS       PIC Z(07)9 VALUE ZEROES.        
           05  FILLER                           PIC X(02)  VALUE SPACES.        
           05  RPT-DTL-ITEMS-NO-ONHANDS         PIC Z(07)9 VALUE ZEROES.        
           05  FILLER                           PIC X(01)  VALUE SPACES.        
                                                                                
       01  RPT-DETAIL-SUMMARY-LINE.                                             
           05  FILLER                           PIC X(84)  VALUE SPACES.        
           05  FILLER                           PIC X(17)  VALUE                
           'TOTALS :     SOLD'.                                                 
           05  RPT-DPT-CL-INHSE-UNITS-SOLD     PIC Z(09)9- VALUE ZEROES.        
           05  FILLER                           PIC X(09)  VALUE                
           ' ON-HANDS'.                                                         
           05  RPT-DPT-CL-INHSE-ONHANDS        PIC Z(09)9- VALUE ZEROES.        
                                                                                
       EJECT                                                                    
      *****************************************************************         
      *    ITEM SELECT RECORD.                                                  
      *****************************************************************         
           COPY ETRD004.                                                        
                                                                                
       EJECT                                                                    
      *****************************************************************         
      *    EDI SALES HISTORY EXTRACT RECORD.                                    
      *****************************************************************         
           COPY ETRD005.                                                        
                                                                                
       EJECT                                                                    
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      *    SQL COMMUNICATIONS AREA                                              
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
       EJECT                                                                    
      *****************************************************************         
JF0305*    LOCATION TABLE LAYOUT                                                
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
JF0305         INCLUDE XIT00000                                                 
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
      *****************************************************************         
      *    SKU DB LAYOUT                                                        
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TSK                                                      
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    SKU CROSS REFERENCE DB LAYOUT                                        
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    UPC TO SKU CROSS REFERENCE DB LAYOUT                                 
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TUPCSKU                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    SKU/STORE DB LAYOUT     ( FOR RETREVING DAILY ON-HANDS )             
      *                                                                         
      *    NOTE :  ONHANDS ARE AS OF THE BEGINNING OF THE TRANSMIT DAY,         
      *            PLUS OR MINUS ANY ADJUSTMENTS MADE ONLINE THUS FAR.          
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TSKST                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    SKU/STORE WEEKLY INVENTORY DB LAYOUT                                 
      *                                                                         
      *    NOTE :  ONHANDS ARE AS OF THE BEGINNING OF THE WEEK.                 
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TSKSTWKI                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    SKU/STORE WEEKLY SALES DB LAYOUT                                     
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
WR6041         INCLUDE TSKSTWKS                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    ENTITY NAME DB LAYOUT                                                
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TENTNME                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *                                                                         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TEXTENT                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    TRADING PARTNER DB LAYOUT                                            
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TTRDPRT                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    TRADING TRANSACTION DB LAYOUT                                        
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TTRDTRN                                                  
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
      *****************************************************************         
      *     SELECT ALL ITEMS FOR A DEPT/CL/INHSE COMBINATION                    
      *                                                                         
      *****************************************************************         
                                                                                
            EXEC SQL                                                            
               DECLARE DEPT_CL_INHSE_ITEM CURSOR FOR                            
               SELECT ITEM_NMBR,                                                
                      UPC_NBR,                                                  
                      EDI_ORD_IND                                               
JF0403         FROM   TSK, TUPCSKU, TSKXREF                                     
JF0503         WHERE  TSKXREF.DEPT      = :PV-DEPT             AND              
                      VNDR_IN_HSE_NMBR  = :PV-DEPT-INHSE-NBR   AND              
JF0403                TSK.ITEM_NMBR     = TUPCSKU.ITEM_NBR     AND              
JF0403                TSK.ITEM_NMBR     = TSKXREF.ITM_NBR                       
               ORDER BY TSK.ITEM_NMBR                                           
            END-EXEC.                                                           
      *                                                                         
       EJECT                                                                    
      *****************************************************************         
      *     SELECT DAILY ON-HANDS INFORMATION                                   
      *        RELATING TO A SPECIFIC ITEM                                      
      *****************************************************************         
                                                                                
            EXEC SQL                                                            
               DECLARE DAILY_ONHANDS CURSOR FOR                                 
               SELECT ITEM_NMBR,                                                
JF0305                LOC_NBR,                                                  
                      ONHAND_QTY                                                
               FROM   TSKST                                                     
               WHERE  ITEM_NMBR = : TSK-ITEM-NMBR                               
JF0305         ORDER BY LOC_NBR                                                 
            END-EXEC.                                                           
      *                                                                         
       EJECT                                                                    
      *****************************************************************         
      *     SELECT DAILY SALES INFORMATION                                      
      *        RELATING TO A SPECIFIC ITEM                                      
      *****************************************************************         
                                                                                
            EXEC SQL                                                            
               DECLARE DAILY_SALES CURSOR FOR                                   
               SELECT ITEM_NMBR,                                                
WR6041                FSCL_BOW_DTE,                                             
JF0305                STR_NBR,                                                  
                      SALES_TYPE_CODE,                                          
                      SALES_UNIT_QTY,                                           
                      SALES_AMT,                                                
                      SUN_SLS_UNIT_QTY,                                         
                      MON_SLS_UNIT_QTY,                                         
                      TUE_SLS_UNIT_QTY,                                         
                      WED_SLS_UNIT_QTY,                                         
                      THU_SLS_UNIT_QTY,                                         
                      FRI_SLS_UNIT_QTY,                                         
                      SAT_SLS_UNIT_QTY                                          
WR6041         FROM   TSKSTWKS                                                  
               WHERE  ITEM_NMBR    = : TSK-ITEM-NMBR   AND                      
WR6041                FSCL_BOW_DTE = :PV-FSCL-BOW-DTE                           
JF0305         ORDER BY STR_NBR,                                                
                        SALES_TYPE_CODE                                         
            END-EXEC.                                                           
      *                                                                         
       EJECT                                                                    
      *****************************************************************         
      *     SELECT WEEKLY ON-HANDS INFORMATION                                  
      *        RELATING TO A SPECIFIC ITEM                                      
      *****************************************************************         
                                                                                
            EXEC SQL                                                            
               DECLARE WEEKLY_ONHANDS CURSOR FOR                                
               SELECT ITEM_NMBR,                                                
JF0305                LOC_NBR,                                                  
                      FSCL_BOW_DTE,                                             
                      INVEN_ONHAND_QTY                                          
WR6041         FROM   TSKSTWKI                                                  
               WHERE  ITEM_NMBR    = :TSK-ITEM-NMBR                             
WR6041           AND  FSCL_BOW_DTE = :PV-BOW-ISO-DTE                            
JF0305         ORDER BY LOC_NBR                                                 
            END-EXEC.                                                           
      *                                                                         
       EJECT                                                                    
      *****************************************************************         
      *     SELECT WEEKLY SALES INFORMATION                                     
      *        RELATING TO A SPECIFIC ITEM                                      
      *****************************************************************         
                                                                                
            EXEC SQL                                                            
               DECLARE WEEKLY_SALES CURSOR FOR                                  
               SELECT ITEM_NMBR,                                                
JF0305                STR_NBR,                                                  
WR6041                FSCL_BOW_DTE,                                             
                      SALES_TYPE_CODE,                                          
                      SALES_UNIT_QTY,                                           
                      SALES_AMT                                                 
WR6041         FROM   TSKSTWKS                                                  
               WHERE  ITEM_NMBR    = :TSK-ITEM-NMBR   AND                       
WR6041                FSCL_BOW_DTE = :PV-FSCL-BOW-DTE                           
JF0305         ORDER BY STR_NBR,                                                
                        SALES_TYPE_CODE                                         
            END-EXEC.                                                           
      *                                                                         
       EJECT                                                                    
      *                                                                         
      *****************************************************************         
      *     SELECT ALL STORE NUMBERS FOR USE IN DETERMINING VALUES FOR          
      *        THE VALID STORES TABLE.                                          
      *****************************************************************         
                                                                                
            EXEC SQL                                                            
               DECLARE VALID_STORES CURSOR FOR                                  
JF0305         SELECT LOC_NBR,                                                  
JF0305                OPN_DTE,                                                  
JF0305                CLO_DTE                                                   
JF0305         FROM   XIT_LOC                                                   
JF0305         ORDER BY LOC_NBR                                                 
C96566         WITH UR                                                          
            END-EXEC.                                                           
      *                                                                         
       EJECT                                                                    
      *****************************************************************         
      *                                                                         
       LINKAGE SECTION.                                                         
                                                                                
                                                                                
       EJECT                                                                    
      *                                                                         
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
           MOVE 'A100-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                        
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    OPEN ALL FILES, AND VERIFY VSAM FILE STATUS                          
      *    PERFORM INITIALIZATIONS                                              
      *    PROCESS EACH ITEM SELECT FILE RECORD UNTIL END-OF-FILE               
      *    CLOSE ALL FILES, AND VERIFY VSAM FILE STATUS                         
      ******************************************************************        
                                                                                
           OPEN INPUT  ITEM-SELECT-FILE                                         
                OUTPUT EDI-SALES-EXTRACT-FILE                                   
                       EDI-REPORT-FILE.                                         
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM B200-LOAD-STORE-TABLE.                                       
                                                                                
           PERFORM B300-PROCESS-SELECT-FILE                                     
             UNTIL PS-ITEM-SELECT-EOF.                                          
                                                                                
           CLOSE ITEM-SELECT-FILE                                               
                 EDI-SALES-EXTRACT-FILE                                         
                 EDI-REPORT-FILE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
       EJECT                                                                    
       B100-HOUSEKEEPING.                                                       
           MOVE 'B100-HOUSEKEEPING' TO PV-PARAGRAPH-NAME.                       
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    ONE TIME PROCESSING AND INITIALIZATIONS PERFORMED                    
      *    -   EDIT THE CONTROL CARD                                            
      *    -   INITIALIZE EXTRACT FILE DATES WITH CONTROL CARD DATE             
      *    -   SETUP BASIC PAGE HEADING INFORMATION                             
      *                                                                         
      *    PERFORMED BY A100-MAIN-MODULE.                                       
      ******************************************************************        
                                                                                
           INITIALIZE ET005-EDI-SALES-EXTRACT-REC.                              
                                                                                
      *    ** REFER TO PARA E110/E210 FOR USAGE **                              
           SET PS-FIRST-ITEM TO TRUE.                                           
                                                                                
           ACCEPT TD-TODAYS-DATE                                                
               FROM DATE.                                                       
           ACCEPT TT-TODAYS-TIME                                                
               FROM TIME.                                                       
                                                                                
           ACCEPT CC-CONTROL-CARD.                                              
           PERFORM Z300-EDIT-CONTROL-CARD.                                      
                                                                                
      *    ** SET UP THE STATIC RECORD DETAIL (FROM CONTROL CARD) **            
           MOVE CC-RUN-DATE                TO ET005-CURRENT-DATE.               
           MOVE PV-PREV-WK-START-DATE      TO ET005-WEEK-START-DATE.            
           MOVE PV-PREV-WK-END-DATE        TO ET005-WEEK-END-DATE.              
                                                                                
      *    ** SET UP OTHER STATIC EXTRACT RECORD DETAILS **                     
           MOVE PC-TRANSACTION-SET-ID     TO                                    
                ET005-TRANSACTION-SET-ID.                                       
                                                                                
      *    ** SET UP THE PAGE HEADINGS **                                       
           MOVE TD-MM           TO DP132O-RUN-MONTH.                            
           MOVE TD-DD           TO DP132O-RUN-DAY.                              
           MOVE TD-YY           TO DP132O-RUN-YEAR.                             
           MOVE TT-HR           TO DP132O-RUN-HOUR.                             
           MOVE TT-MN           TO DP132O-RUN-MINUTE.                           
           MOVE PC-PROGRAM-NAME TO DP132O-PROGRAM-NAME.                         
           MOVE 1               TO DP132O-REPORT-NUMBER.                        
                                                                                
       EJECT                                                                    
       B200-LOAD-STORE-TABLE.                                                   
           MOVE 'B200-LOAD-STORE-TABLE' TO PV-PARAGRAPH-NAME.                   
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    THE PARAGRAPH CONTROLS THE LOADING OF VALID-STORES-TABLE.            
      *                                                                         
      *    PERFORMED BY A100-MAIN-MODULE.                                       
      ******************************************************************        
           SET STORE-IDX                   TO 1.                                
                                                                                
           MOVE CC-RUN-YY   TO   RD-YY.                                         
           MOVE CC-RUN-MM   TO   RD-MM.                                         
           MOVE CC-RUN-DD   TO   RD-DD.                                         
                                                                                
WR6041     IF CC-RUN-YY > 80                                                    
WR6041        MOVE 19 TO RD-RUN-CC                                              
WR6041     ELSE                                                                 
WR6041        MOVE 20 TO RD-RUN-CC                                              
WR6041     END-IF.                                                              
                                                                                
           PERFORM D200-OPEN-STORE-CURSOR.                                      
           PERFORM D300-FETCH-STORES.                                           
           PERFORM D100-LOAD-STORES VARYING STORE-IDX                           
                   FROM 1 BY 1 UNTIL                                            
                   SQLCODE = +100 OR                                            
                   STORE-IDX > PC-MAX-STORES.                                   
                                                                                
           PERFORM D400-CLOSE-STORE-CURSOR.                                     
                                                                                
           IF  STORE-IDX > PC-MAX-STORES                                        
               DISPLAY '** ABEND **'                                            
               DISPLAY ' ETKUT003'                                              
               DISPLAY 'VALID_STORES TABLE OVERFLOW'                            
               MOVE +4004              TO ABEND-CODE                            
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
                                                                                
       B300-PROCESS-SELECT-FILE.                                                
           MOVE 'B300-PROCESS-SELECT-FILE' TO PV-PARAGRAPH-NAME.                
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    READ EACH SELECT FILE RECORD.                                        
      *                                                                         
      *    EVALUATE WETHER OR NOT THE RECORD WILL BE PROCESSED                  
      *       FOR EACH PARTICULAR EXECUTION OF THE PROGRAM.                     
      *       ( WHERE THE SELECT FILE TRANSMIT WEEK DAY FLAGGED,                
      *         IS THE SAME WEEK DAY AS TODAY'S DATE. )                         
      *                                                                         
      *    SET THE NECESSARY WORKING STORAGE VARIABLES                          
      *       FOR THAT PARTICULAR DEPT/CL/INHSE COMBINATION.                    
      *                                                                         
      *    PERFORMED BY A100-MAIN-MODULE.                                       
      ******************************************************************        
                                                                                
           SET PS-NO-DEPT-CL-INHSE-REC TO TRUE.                                 
           SET PS-DO-NOT-PROCESS-ENTRY TO TRUE.                                 
                                                                                
           PERFORM Z500-READ-ITEM-SELECT-FILE                                   
             UNTIL PS-NEW-DEPT-CL-INHSE-REC                                     
                OR PS-ITEM-SELECT-EOF.                                          
                                                                                
           IF PS-ITEM-SELECT-EOF                                                
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM C100-DETERMINE-IF-PROCESSING                             
           END-IF.                                                              
                                                                                
           SET PS-VENDOR-NOT-FOUND TO TRUE.                                     
           SET PS-INVALID-EDI-VENDOR TO TRUE.                                   
                                                                                
           IF PS-PROCESS-ENTRY                                                  
               PERFORM C200-VERIFY-EDI-VENDOR                                   
           END-IF.                                                              
                                                                                
           IF  PS-VENDOR-FOUND  AND  PS-VALID-EDI-VENDOR                        
                                                                                
                MOVE ENTNME-ENT-NAME-DESC TO RPT-DTL-VENDOR-NAME                
                                                                                
                MOVE ET004-DEPT         TO  PV-DEPT                             
                                            RPT-DTL-DEPT                        
                MOVE ET004-DEPT-INHOUSE TO  PV-DEPT-INHSE-NBR                   
                                            RPT-DTL-DEPT-INHOUSE                
                                                                                
                SET PS-PROCESS-NOT-COMPLETE TO TRUE                             
                PERFORM C300-PROCESS-EDI-VENDOR                                 
                  UNTIL PS-PROCESSING-COMPLETE                                  
                                                                                
           ELSE                                                                 
               IF PS-ITEM-SELECT-EOF                                            
                   CONTINUE                                                     
               ELSE                                                             
                   IF PS-PROCESS-ENTRY                                          
                      PERFORM C400-PROCESS-NON-EDI-VENDOR                       
                   END-IF                                                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
       C100-DETERMINE-IF-PROCESSING.                                            
           MOVE 'C100-DETERMINE-IF-PROCESSING' TO PV-PARAGRAPH-NAME.            
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    DETERMINE IF TODAY IS THE TRANSMIT DATE FOR THIS VENDOR :            
      *                                                                         
      *     O  DETERMINE THE NUMERIC DAY OF THE WEEK FOR TODAY'S DATE           
      *     O  EVALUATE THE TRANSMIT DATE FOR THE DEPT/CL/INHSE RECORD          
      *                                                                         
      *    PERFORMED BY B300-PROCESS-SELECT-FILE                                
      ******************************************************************        
                                                                                
      *    ** ------------------------------------------------------- **        
      *    **  DETERMINE THE NUMERIC DAY OF THE WEEK FOR TODAY'S DATE           
      *    ** ------------------------------------------------------- **        
                                                                                
           COMPUTE PV-RUN-DATE-WKDY =                                           
                    ( PV-YESTERDAY-WKDY + 1 )                                   
                                                                                
           IF PV-RUN-DATE-WKDY = 8                                              
               MOVE 1 TO PV-RUN-DATE-WKDY                                       
           END-IF.                                                              
                                                                                
      *    ** ------------------------------------------------------- **        
      *    **  ET004-TRANSMIT-TODAY, IS A FIELD THAT OCCURS ONCE FOR            
      *    **    EACH DAY OF THE WEEK (7 TIMES).                                
      *    **  IF AN 'X' IS CONTAINED IN THE POSITION REPRESENTING              
      *    **    THE DAY OF THE WEEK FOR TODAYS RUN DATE, THEN THE              
      *    **    VENDOR IS TO RECEIVE SALES/ONHANDS TODAY FOR EITHER            
      *    **    YESTERDAY OR THE LAST WEEK (PREV SUN THRU LAST SAT)            
      *    **                                                                   
      *    **  NOTE: 'PS-SUB' REPRESENTS THE DAY OF THE WEEK POSITION           
      *    **        ( 1=SUN, 2=MON, ..., 7=SAT ) FOR THE ITEM SELECT           
      *    **        FILE FIELD 'ET004-TRANSMIT-TODAY'                          
      *    ** ------------------------------------------------------- **        
                                                                                
           MOVE PV-RUN-DATE-WKDY  TO  PS-SUB.                                   
                                                                                
           IF ET004-TRANSMIT-TODAY(PS-SUB)                                      
               SET PS-PROCESS-ENTRY TO TRUE                                     
      *    ELSE                                                                 
      *        DISPLAY '  '                                                     
      *        DISPLAY '** VENDOR NOT TRANSMITTING TODAY **'                    
      *                '   DUNNS : ' ET004-DPT-CL-INHSE-DUNNS                   
      *                '   DEPT : ' ET004-DEPT                                  
      *                '   IN-HOUSE : ' ET004-DEPT-INHOUSE                      
      *        DISPLAY '  '                                                     
           END-IF.                                                              
                                                                                
       EJECT                                                                    
       C200-VERIFY-EDI-VENDOR.                                                  
           MOVE 'C200-VERIFY-EDI-VENDOR' TO PV-PARAGRAPH-NAME.                  
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    READ THE DB2 VENDOR DATABASE                                         
      *       RETREIVE THE VENDOR NAME AND ID NUMBER FROM THE ENTITY NAM        
      *       DATABASE.  VERIFY THAT THE VENDOR IS FOUND ON THE TRADING         
      *       PARTNER TABLES.  FINALLY, VERIFY THAT THE VENDOR IS VALID         
      *       USING THE VENDOR AGREEMENT TABLE.                                 
      *                                                                         
      *    PERFORMED BY B300-PROCESS-SELECT-FILE.                               
      ******************************************************************        
                                                                                
           MOVE ET004-DPT-CL-INHSE-DUNNS TO PV-DPT-CL-INHSE-DUNNS.              
                                                                                
           EXEC SQL                                                             
               SELECT TENTNME.ENT_ID,                                           
                      TENTNME.ENT_NAME_DESC                                     
                INTO  :ENTNME-ENT-ID,                                           
                      :ENTNME-ENT-NAME-DESC                                     
                FROM  TEXTENT,                                                  
                      TENTNME                                                   
               WHERE  DUN_NBR          = :PV-DPT-CL-INHSE-DUNNS AND             
                      TENTNME.ENT_ID   = TEXTENT.ENT_ID         AND             
                      TENTNME.TYPE_CDE = '01'                                   
                                                                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
              MOVE ENTNME-ENT-NAME-DESC TO RPT-DTL-VENDOR-NAME                  
                                                                                
              PERFORM Z600-SELECT-TTRDPRT                                       
                                                                                
              IF SQLCODE = +000 OR -811                                         
                 SET PS-VENDOR-FOUND TO TRUE                                    
                 SET PS-VALID-EDI-VENDOR TO TRUE                                
              ELSE                                                              
                 IF SQLCODE NOT = +100                                          
                    MOVE 'SELECT TTRDTRN/TTRDPRT' TO                            
                         PV-DB2-OPERATION-ATTEMPTED                             
                                                                                
                    PERFORM Z999-SQL-ABEND                                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF SQLCODE NOT = +100                                             
                 MOVE 'SELECT TEXTENT/TENTNME' TO                               
                      PV-DB2-OPERATION-ATTEMPTED                                
                                                                                
                 PERFORM Z999-SQL-ABEND                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       EJECT                                                                    
       C300-PROCESS-EDI-VENDOR.                                                 
           MOVE 'C300-PROCESS-EDI-VENDOR' TO PV-PARAGRAPH-NAME.                 
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    INITIALIZE THE WORKING STORAGE COUNT FIELDS USED                     
      *                                                                         
      *    MOVE ITEM SELECT FILE DATA TO THE EXTRACT RECORD                     
      *       AND INITITIALIZE CERTAIN EXTRACT RECORD FIELDS                    
      *                                                                         
      *    DETERMINE THE PROCESSING TO PERFORM BASED ON :                       
      *       -  ET004-DAILY-SALES                                              
      *       -  ET004-WEEKLY-SALES                                             
      *                                                                         
      *    WRITE THE CONTROL REPORT                                             
      *                                                                         
      *    PERFORMED BY B300-PROCESS-SELECT-FILE.                               
      ******************************************************************        
                                                                                
           MOVE ZEROES  TO  PC-TOT-DPT-CL-INHSE-ITEMS                           
                            PC-NON-EDI-CNTR                                     
                            PC-EDI-CNTR                                         
                            PC-SALES-CNTR                                       
                            PC-NO-SALES-CNTR                                    
                            PC-ONHANDS-CNTR                                     
                            PC-NO-ONHANDS-CNTR.                                 
                                                                                
           MOVE ENTNME-ENT-ID      TO  ET005-ENT-ID.                            
                                                                                
           MOVE ET004-DEPT         TO  ET005-DEPT.                              
      *    MOVE ET004-CLASS        TO  ET005-CLASS.                             
                                                                                
           MOVE ZEROES             TO  ET005-UPC-CODE                           
                                       ET005-STORE-NUMBER.                      
                                                                                
           MOVE ET004-TEST-PROD-IND                                             
                                   TO  ET005-TEST-PROD-IND.                     
                                                                                
           MOVE ET004-DAILY-WEEKLY-IND                                          
                                   TO  ET005-DAILY-WEEKLY-IND.                  
                                                                                
      *    ** ET005-SALES-TYPE-LEVEL **                                         
           IF ET004-INDIVIDUAL-SALES                                            
               SET ET005-INDIVIDUAL-SALES TO TRUE                               
           ELSE                                                                 
               SET ET005-ALL-SALES TO TRUE                                      
           END-IF.                                                              
                                                                                
           MOVE ZEROES             TO  ET005-UNITS-SOLD-RETAIL                  
                                       ET005-UNITS-SOLD-PROMO                   
                                       ET005-UNITS-SOLD-CLEAR                   
                                       ET005-UNITS-SOLD-ALL.                    
                                                                                
           MOVE ZEROES             TO  ET005-UNIT-PRICE-RETAIL                  
                                       ET005-UNIT-PRICE-PROMO                   
                                       ET005-UNIT-PRICE-CLEAR                   
                                       ET005-UNIT-PRICE-ALL.                    
                                                                                
           MOVE ET004-ONHAND-IND TO ET005-ONHAND-IND.                           
                                                                                
           IF ET004-ONHAND                                                      
               SET RPT-DTL-REPORT-ONHANDS-YES TO TRUE                           
           ELSE                                                                 
               SET RPT-DTL-REPORT-ONHANDS-NO  TO TRUE                           
           END-IF.                                                              
                                                                                
           IF ET004-DAILY-SALES                                                 
      *        ** (DAILY SALES THRU END OF BUSINESS YESTERDAY)                  
JF0205         MOVE PV-BOW-CURR-DTE-DLY  TO PV-BOW-ISO-DTE                      
WR6041                                      PV-FSCL-BOW-DTE                     
           END-IF.                                                              
                                                                                
           IF ET004-WEEKLY-SALES                                                
VF0105*        ** (WEEKLY SALES THRU END OF BUSINESS LAST SATURDAY)             
VF0105*        MOVE PV-BOW-LAST-DTE      TO PV-BOW-ISO-DTE                      
VF0105*                                     PV-FSCL-BOW-DTE                     
VF0105*    END-IF.                                                              
VF0105*                                                                         
VF0105*    IF ET004-DPT-CL-INHSE-DUNNS = 007131501 OR                           
VF0105*       ET004-DPT-CL-INHSE-DUNNS = 001195650 OR                           
VF0105*       ET004-DPT-CL-INHSE-DUNNS = 009027780 OR                           
VF0105*       ET004-DPT-CL-INHSE-DUNNS = 001020684 OR                           
VF0105*       ET004-DPT-CL-INHSE-DUNNS = 009109273 OR                           
VF0105*       ET004-DPT-CL-INHSE-DUNNS = 005048400 OR                           
VF0105*       ET004-DPT-CL-INHSE-DUNNS = 049804594 OR                           
VF0105*       ET004-DPT-CL-INHSE-DUNNS = 183280973 OR                           
VF0105*       ET004-DPT-CL-INHSE-DUNNS = 006408116                              
VF0105*        ** (WEEKLY SALES THRU END OF BUSINESS YESTERDAY)                 
072000         MOVE PV-BOW-CURR-DTE      TO PV-BOW-ISO-DTE                      
072000         MOVE PV-BOW-LAST-DTE      TO PV-FSCL-BOW-DTE                     
072000     END-IF.                                                              
JF0205                                                                          
JF0205     DISPLAY 'VENDOR DUNS = ' ET004-KEY-VENDOR-DUNNS.                     
JF0205     DISPLAY 'PV-BOW-ISO-DTE = ' PV-BOW-ISO-DTE.                          
JF0205     DISPLAY 'PV-FSCL-BOW-DTE = ' PV-FSCL-BOW-DTE.                        
                                                                                
           PERFORM E100-PROCESS-DETAILS                                         
             UNTIL PS-PROCESSING-COMPLETE.                                      
                                                                                
           PERFORM Z800-PRINT-DETAIL-LINE.                                      
                                                                                
           SET PS-PROCESSING-COMPLETE TO TRUE.                                  
                                                                                
       EJECT                                                                    
       C400-PROCESS-NON-EDI-VENDOR.                                             
           MOVE 'C400-PROCESS-NON-EDI-VENDOR' TO PV-PARAGRAPH-NAME.             
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    NO VALID TRADING PARTNERSHIP AGREEMENT EXISTS FOR VENDOR             
      *                                                                         
      *    RESET THE WORKING STORAGE COUNT FIELDS                               
      *    AND RESET CERTAIN EXTRACT RECORD FIELDS                              
      *                                                                         
      *    PUT OUT A USEFUL SYSOUT MESSAGE                                      
      *                                                                         
      *    PERFORMED BY B300-PROCESS-SELECT-FILE                                
      ******************************************************************        
                                                                                
           MOVE ZEROES  TO  PC-TOT-DPT-CL-INHSE-ITEMS                           
                            PC-NON-EDI-CNTR                                     
                            PC-EDI-CNTR                                         
                            PC-SALES-CNTR                                       
                            PC-NO-SALES-CNTR                                    
                            PC-ONHANDS-CNTR                                     
                            PC-NO-ONHANDS-CNTR.                                 
                                                                                
           DISPLAY 'VENDOR NOT FOUND ON TTRDPRT'.                               
                                                                                
           DISPLAY '  '.                                                        
           DISPLAY '   DUNNS : ' ET004-DPT-CL-INHSE-DUNNS                       
                   '   DEPT : ' ET004-DEPT                                      
                   '   IN-HOUSE : ' ET004-DEPT-INHOUSE.                         
           DISPLAY '  '                                                         
                                                                                
           MOVE ZEROES             TO  ET005-ENT-ID.                            
                                                                                
JF0403     MOVE SPACES             TO  ET005-DEPT.                              
                                                                                
           MOVE ZEROES             TO  ET005-UPC-CODE                           
                                       ET005-STORE-NUMBER.                      
                                                                                
           MOVE SPACES             TO  ET005-DAILY-WEEKLY-IND                   
                                       ET005-TEST-PROD-IND                      
                                       ET005-SALES-TYPE-LEVEL                   
                                       ET005-ONHAND-IND.                        
                                                                                
           PERFORM Z800-PRINT-DETAIL-LINE.                                      
                                                                                
           SET PS-PROCESSING-COMPLETE TO TRUE.                                  
                                                                                
       D100-LOAD-STORES.                                                        
           MOVE 'D100-LOAD-STORES' TO PV-PARAGRAPH-NAME.                        
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    THIS PARAGRAPH CHECKS TO SEE IF THE CONTROL CARD RUN DATE            
      *    FALLS BETWEEN THE OPEN AND CLOSE DATE.  IF IT DOES, THE              
      *    STORE NUMBER IS ADDED TO VALID-STORES-TABLE.                         
      *    THE OPEN, CLOSE, AND CONTROL CARD DATES WERE REFORMATTED TO          
      *    MAKE THE COMPARISON A DIRECT NUMERIC COMPARISON.                     
      *    AFTER THE COMPARISON IS PERFORMED, THE NEXT STORE IS FETCHED         
      *    FOR COMPARISON.                                                      
      *                                                                         
      *    PERFORMED BY B200-LOAD-STORE-TABLE.                                  
      ******************************************************************        
                                                                                
JF0305     MOVE XIT00000-OPN-DTE      TO  OD-OPENED-DATE.                       
           MOVE OD-OPENED-YY          TO  RO-OPENED-YY.                         
           MOVE OD-OPENED-MM          TO  RO-OPENED-MM.                         
           MOVE OD-OPENED-DD          TO  RO-OPENED-DD.                         
                                                                                
JF0305     MOVE XIT00000-CLO-DTE      TO  CD-CLOSED-DATE.                       
           MOVE CD-CLOSED-YY          TO  RC-CLOSED-YY.                         
           MOVE CD-CLOSED-MM          TO  RC-CLOSED-MM.                         
           MOVE CD-CLOSED-DD          TO  RC-CLOSED-DD.                         
                                                                                
           IF (RD-REFORMATTED-DATE >= RO-REFORMATTED-OPENED-DATE                
               AND RD-REFORMATTED-DATE <= RC-REFORMATTED-CLOSED-DATE)           
JF0305         MOVE XIT00000-LOC-NBR TO PV-STORE-NMBR-NUM                       
JF0305         MOVE PV-STORE-NMBR-NUM TO STORE-NMBR(STORE-IDX)                  
JF0305         DISPLAY STORE-NMBR(STORE-IDX)                                    
           ELSE                                                                 
               SET STORE-IDX DOWN BY 1                                          
           END-IF.                                                              
                                                                                
                                                                                
           PERFORM D300-FETCH-STORES.                                           
                                                                                
       D200-OPEN-STORE-CURSOR.                                                  
           MOVE 'D200-OPEN-STORE-CURSOR' TO PV-PARAGRAPH-NAME.                  
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    THE PARAGRAPH OPENS VALID_STORES CURSOR.                             
      *                                                                         
      *    PERFORMED BY B200-LOAD-STORE-TABLE.                                  
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
                OPEN VALID_STORES                                               
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0 = 'W')                                                  
                                                                                
               MOVE 'OPEN VALID_STORES'    TO                                   
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-SQL-ABEND                                           
                                                                                
           END-IF.                                                              
                                                                                
       D300-FETCH-STORES.                                                       
           MOVE 'D300-FETCH-STORES' TO PV-PARAGRAPH-NAME.                       
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    THE RETRIEVES THE STORE-NUMBER, THE OPENED-DATE, AND THE             
      *    CLOSED-DATE.                                                         
      *    PERFORMED BY B200-LOAD-STORE-TABLE.                                  
      *    PERFORMED BY D100-LOAD-STORES.                                       
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               FETCH VALID_STORES                                               
JF0305            INTO :XIT00000-LOC-NBR,                                       
JF0305                 :XIT00000-OPN-DTE,                                       
JF0305                 :XIT00000-CLO-DTE                                        
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
                                                                                
               IF (SQLCODE NOT = +100) OR                                       
                  (SQLWARN0    = 'W')                                           
                                                                                
                   MOVE 'FETCH VALID_STORES CURSOR'  TO                         
                        PV-DB2-OPERATION-ATTEMPTED                              
                                                                                
                   PERFORM Z999-SQL-ABEND                                       
                                                                                
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
       D400-CLOSE-STORE-CURSOR.                                                 
           MOVE 'D400-CLOSE-STORE-CURSOR' TO PV-PARAGRAPH-NAME.                 
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    THE PARAGRAPH CLOSES VALID_STORES CURSOR.                            
      *                                                                         
      *    PERFORMED BY B200-LOAD-STORE-TABLE.                                  
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
                CLOSE VALID_STORES                                              
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0 = 'W')                                                  
                                                                                
               MOVE 'CLOSE VALID_STORES'    TO                                  
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-SQL-ABEND                                           
                                                                                
           END-IF.                                                              
       EJECT                                                                    
       E100-PROCESS-DETAILS.                                                    
           MOVE 'E100-PROCESS-DETAILS' TO PV-PARAGRAPH-NAME.                    
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    PROCESS ALL ITEMS FOR EACH ITEM SELECT RECORD                        
      *                                                                         
      *    PERFORMED BY C300-PROCESS-EDI-VENDOR.                                
      ******************************************************************        
      *                                                                         
      *    DISPLAY '** OPEN CURSOR FOR ** '                                     
      *            '   PV DEPT : ' PV-DEPT                                      
      *            '   PV INHSE : ' PV-DEPT-INHSE-NBR                           
                                                                                
           PERFORM U100-OPEN-DEPT-CL-INHSE-ITEM.                                
                                                                                
           SET PS-PROCESS-NOT-COMPLETE TO TRUE.                                 
                                                                                
           PERFORM E200-PROCESS-SALES-ONHANDS                                   
             UNTIL PS-PROCESSING-COMPLETE.                                      
                                                                                
           PERFORM U190-CLOSE-DEPT-CL-INHSE-ITEM.                               
                                                                                
       EJECT                                                                    
       E200-PROCESS-SALES-ONHANDS.                                              
           MOVE 'E200-PROCESS-SALES-ONHANDS' TO PV-PARAGRAPH-NAME.              
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    FOR EACH ITEM SELECT RECORD :                                        
      *                                                                         
      *    KEEP TRACK OF THE :                                                  
      *       O  TOTAL NUMBER OF ITEMS FOR THE DEPT, IN-HOUSE                   
      *       O  NUMBER OF NON-EDI ITEMS                                        
      *       O  NUMBER OF EDI ITEMS, ALONG WITH :                              
      *            # OF EDI ITEMS REPORTING SALES                               
      *            # OF EDI ITEMS REPORTING ZERO SALES                          
      *            # OF EDI ITEMS REPORTING ON-HANDS                            
      *            # OF EDI ITEMS REPORTING ZERO ON-HANDS                       
      *       (THIS INFORMATION IS WRITTEN ON THE CONTROL REPORT)               
      *                                                                         
      *    CREATES THE SALES HISTORY EXTRACT RECORDS                            
      *                                                                         
      *    PERFORMED BY E100-PROCESS-DETAILS.                                   
      ******************************************************************        
                                                                                
           PERFORM U150-FETCH-DEPT-CL-INHSE-ITEM.                               
                                                                                
           IF PS-NO-MORE-ITEMS                                                  
      *        ** SQLCODE = +100 MEANS END OF CURSOR **                         
               SET PS-PROCESSING-COMPLETE TO TRUE                               
                                                                                
           ELSE                                                                 
                                                                                
               ADD +1 TO PC-TOT-DPT-CL-INHSE-ITEMS                              
                                                                                
               MOVE TUPCSKU-UPC-NBR     TO ET005-UPC-CODE                       
                                                                                
               IF  TUPCSKU-EDI-ORD-IND = PC-YES                                 
                   ADD +1 TO  PC-EDI-CNTR                                       
                   SET PS-EDI-ITEM TO TRUE                                      
               ELSE                                                             
                   ADD +1 TO  PC-NON-EDI-CNTR                                   
                   SET PS-NON-EDI-ITEM TO TRUE                                  
               END-IF                                                           
                                                                                
               IF PS-EDI-ITEM                                                   
      *            ** OPEN THE APPROPRIATE DAILY/WEEKLY CURSORS **              
                   IF ET004-DAILY-SALES                                         
                       PERFORM U200-OPEN-DAILY-ONHANDS                          
                       PERFORM U300-OPEN-DAILY-SALES                            
                   END-IF                                                       
                   IF ET004-WEEKLY-SALES                                        
                       PERFORM U400-OPEN-WEEKLY-ONHANDS                         
                       PERFORM U500-OPEN-WEEKLY-SALES                           
                   END-IF                                                       
                                                                                
                   MOVE 0 TO PV-QTY-UNITS-ONHAND                                
                   MOVE 0 TO PV-QTY-UNITS-SOLD                                  
                                                                                
                   MOVE SPACES TO PV-LAST-STORE                                 
                   SET PS-FIRST-STORE TO TRUE                                   
                   SET PS-MORE-ONHANDS TO TRUE                                  
                   SET PS-NO-000-REC TO TRUE                                    
                                                                                
                   PERFORM E300-PROCESS-ONHANDS                                 
                     UNTIL PS-NO-MORE-ONHANDS                                   
                                                                                
                   IF PS-CREATE-000-REC                                         
      *                ** MISSING '000' RECORD FOR ON-HANDS **                  
                       DISPLAY '** CREATING MISSING (STORE 000) '               
                       'ON-HANDS RECORD FOR UPC ==> ' ET005-UPC-CODE            
                                                                                
JF0305                 MOVE '00000'                                             
                         TO ET005-STORE-NUMBER                                  
                       MOVE PV-QTY-UNITS-ONHAND                                 
                         TO ET005-ONHAND-QUANTITY                               
                                                                                
                       IF ET005-ONHAND-QUANTITY = 0                             
                           ADD +1 TO  PC-NO-ONHANDS-CNTR                        
                       ELSE                                                     
                           ADD +1 TO  PC-ONHANDS-CNTR                           
                       END-IF                                                   
                                                                                
      *                ** CREATE ALL STORES TOTAL EXTRACT RECORD **             
                       SET ET005-ALL-STORES-RECORD TO TRUE                      
                                                                                
      ***              IF ET004-ONHAND                                          
      *                    ** WRITE OUT THE EXTRACT RECORD **                   
                           PERFORM Z700-WRITE-EXTRACT-FILE                      
      ***              END-IF                                                   
                                                                                
                   END-IF                                                       
                                                                                
                   MOVE 0 TO PV-QTY-UNITS-ONHAND                                
                   MOVE 0 TO PV-QTY-UNITS-SOLD                                  
                                                                                
                   MOVE SPACES TO PV-LAST-STORE                                 
                   SET PS-FIRST-STORE TO TRUE                                   
                   SET PS-SALES-RETREIVED TO TRUE                               
                                                                                
                   PERFORM E400-PROCESS-SALES                                   
                     UNTIL PS-NO-SALES-RETREIVED                                
                                                                                
      *            ** CLOSE THE APPROPRIATE DAILY/WEEKLY CURSOR **              
                   IF ET004-DAILY-SALES                                         
                       PERFORM U290-CLOSE-DAILY-ONHANDS                         
                       PERFORM U390-CLOSE-DAILY-SALES                           
                   END-IF                                                       
                   IF ET004-WEEKLY-SALES                                        
                       PERFORM U490-CLOSE-WEEKLY-ONHANDS                        
                       PERFORM U590-CLOSE-WEEKLY-SALES                          
                   END-IF                                                       
                                                                                
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
       EJECT                                                                    
       E300-PROCESS-ONHANDS.                                                    
           MOVE 'E300-PROCESS-ONHANDS' TO PV-PARAGRAPH-NAME.                    
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *                                                                         
      ******************************************************************        
                                                                                
JF0305     MOVE '00000'   TO  ET005-STORE-NUMBER.                               
           MOVE ZEROES  TO  ET005-ONHAND-QUANTITY.                              
                                                                                
           IF ET004-DAILY-SALES                                                 
               PERFORM U250-FETCH-DAILY-ONHANDS                                 
               IF PS-MORE-ONHANDS                                               
                  IF PS-VALID-STORE                                             
JF0305               MOVE TSKST-LOC-NBR                                         
JF0305                 TO PV-STORE-NMBR-NUM                                     
JF0305               MOVE PV-STORE-NMBR-NUM                                     
                       TO ET005-STORE-NUMBER                                    
                     MOVE TSKST-ONHAND-QTY                                      
                       TO ET005-ONHAND-QUANTITY                                 
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF ET004-WEEKLY-SALES                                                
               PERFORM U450-FETCH-WEEKLY-ONHANDS                                
               IF PS-MORE-ONHANDS                                               
                  IF PS-VALID-STORE                                             
JF0305               MOVE SKSTWKI-LOC-NBR                                       
JF0305                 TO PV-STORE-NMBR-NUM                                     
JF0305               MOVE PV-STORE-NMBR-NUM                                     
                       TO ET005-STORE-NUMBER                                    
                     MOVE SKSTWKI-INVEN-ONHAND-QTY                              
                       TO ET005-ONHAND-QUANTITY                                 
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF PS-VALID-STORE                                                    
             IF PS-MORE-ONHANDS                                                 
                                                                                
JF0305         IF ET005-STORE-NUMBER = '00000'                                  
                                                                                
                   IF ET005-ONHAND-QUANTITY = 0                                 
                       ADD +1 TO  PC-NO-ONHANDS-CNTR                            
                   ELSE                                                         
                       ADD +1 TO  PC-ONHANDS-CNTR                               
                   END-IF                                                       
                                                                                
      *            ** CREATE ALL STORES TOTAL EXTRACT RECORD **                 
                   SET ET005-ALL-STORES-RECORD TO TRUE                          
                                                                                
                   IF ET004-ONHAND                                              
      *                ** WRITE OUT THE EXTRACT RECORD **                       
                       PERFORM Z700-WRITE-EXTRACT-FILE                          
                   ELSE                                                         
                       ADD ET005-ONHAND-QUANTITY                                
                        TO PV-DPT-CL-INHSE-ONHANDS                              
                       MOVE ZEROES TO ET005-ONHAND-QUANTITY                     
                   END-IF                                                       
                                                                                
                   IF ET004-CORP-LEVEL                                          
      *            ** DO NOT PROCESS ANY MORE STORE DETAIL **                   
                       IF ET004-DAILY-SALES                                     
                           PERFORM U250-FETCH-DAILY-ONHANDS                     
                             UNTIL PS-NO-MORE-ONHANDS                           
                       END-IF                                                   
                       IF ET004-WEEKLY-SALES                                    
                           PERFORM U450-FETCH-WEEKLY-ONHANDS                    
                             UNTIL PS-NO-MORE-ONHANDS                           
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   SET PS-NOT-FIRST-STORE TO TRUE                               
                                                                                
               ELSE                                                             
                                                                                
                   IF ET005-ONHAND-QUANTITY = 0                                 
      *                ** DO NOT REPORT STORES WITH 0 ON-HANDS **               
                       CONTINUE                                                 
                   ELSE                                                         
                                                                                
                       IF PS-FIRST-STORE                                        
      *                    ** MISSING '000' RECORD FOR ON-HANDS **              
                                                                                
                           IF ET004-CORP-LEVEL                                  
                               ADD +1 TO  PC-NO-ONHANDS-CNTR                    
                               DISPLAY '** MISSING (STORE 000) '                
                                       'ON-HANDS RECORD FOR '                   
                                       'UPC ==> ' ET005-UPC-CODE                
                               DISPLAY '   NOTE : THIS ITEM IS NOT '            
                                       'CREATED BECAUSE OF ZERO '               
                                       'ON-HANDS AT CORPORATE LEVEL '           
      *                       ** DO NOT PROCESS ANY MORE STORE DETAIL **        
                               IF ET004-DAILY-SALES                             
                                   PERFORM U250-FETCH-DAILY-ONHANDS             
                                     UNTIL PS-NO-MORE-ONHANDS                   
                               END-IF                                           
                               IF ET004-WEEKLY-SALES                            
                                   PERFORM U450-FETCH-WEEKLY-ONHANDS            
                                     UNTIL PS-NO-MORE-ONHANDS                   
                               END-IF                                           
                               MOVE ZEROES TO ET005-ONHAND-QUANTITY             
                           ELSE                                                 
                               IF ET004-ONHAND                                  
                                   SET PS-CREATE-000-REC TO TRUE                
                               ELSE                                             
                                   MOVE ZEROES                                  
                                     TO ET005-ONHAND-QUANTITY                   
                               END-IF                                           
                           END-IF                                               
                                                                                
                       END-IF                                                   
                                                                                
                       IF PS-MORE-ONHANDS                                       
                           ADD ET005-ONHAND-QUANTITY                            
                            TO PV-QTY-UNITS-ONHAND                              
                                                                                
      *                    ** CREATE INDIVIDUAL STORE EXTRACT RECORD **         
                           SET ET005-STORE-RECORD TO TRUE                       
                                                                                
                           IF ET004-ONHAND                                      
      *                        ** WRITE OUT THE EXTRACT RECORD **               
                               PERFORM Z700-WRITE-EXTRACT-FILE                  
                           ELSE                                                 
                               MOVE ZEROES TO ET005-ONHAND-QUANTITY             
                           END-IF                                               
                                                                                
                       END-IF                                                   
                                                                                
                       SET PS-NOT-FIRST-STORE TO TRUE                           
                                                                                
                   END-IF                                                       
                                                                                
               END-IF                                                           
                                                                                
           ELSE                                                                 
               IF PS-FIRST-STORE                                                
                   ADD +1 TO  PC-NO-ONHANDS-CNTR                                
                   SET PS-NOT-FIRST-STORE TO TRUE                               
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
       EJECT                                                                    
       E400-PROCESS-SALES.                                                      
           MOVE 'E400-PROCESS-SALES' TO PV-PARAGRAPH-NAME.                      
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *      NOTE:  1, 2, OR EVEN 3 RECORDS MAY EXIST PER STORE/ITEM            
      *                                                                         
      *      SALES-TYPE FIELD MAY BE :                                          
      *      '1' = RETAIL                                                       
      *      '2' = PROMOTIONAL                                                  
      *      '3' = CLEARANCE                                                    
      *                                                                         
      ******************************************************************        
                                                                                
           IF ET004-DAILY-SALES                                                 
               PERFORM U350-FETCH-DAILY-SALES                                   
           END-IF.                                                              
                                                                                
           IF ET004-WEEKLY-SALES                                                
               PERFORM U550-FETCH-WEEKLY-SALES                                  
           END-IF.                                                              
                                                                                
           IF PS-VALID-STORE                                                    
                                                                                
             IF PS-SALES-RETREIVED                                              
                                                                                
               IF PS-FIRST-STORE                                                
      *            ** SAVE FIRST STORE NUMBER PROCESSED **                      
JF0305             MOVE SKSTWKS-STR-NBR TO PV-STORE-NMBR-NUM                    
JF0305             MOVE PV-STORE-NMBR-NUM TO PV-LAST-STORE                      
                                                                                
               ELSE                                                             
                                                                                
JF0305             IF PV-STORE-NMBR = PV-LAST-STORE                             
                       CONTINUE                                                 
                                                                                
                   ELSE                                                         
                                                                                
      *                DISPLAY '  DPT ' ET005-DEPT                              
      *                        '  SEQ ' ET005-SEQUENCE                          
      *                        '  UPC ' ET005-UPC-CODE                          
      *                        '  STR ' ET005-STORE-NUMBER                      
      *                        '  SLD RTL ' ET005-UNITS-SOLD-RETAIL             
      *                        '  SLD PRO ' ET005-UNITS-SOLD-PROMO              
      *                        '  SLD CLR ' ET005-UNITS-SOLD-CLEAR              
      *                        '  SLD ALL ' ET005-UNITS-SOLD-ALL                
      *                     '     ONHAND ' ET005-ONHAND-QUANTITY                
      *                        '  $ RTL ' ET005-UNIT-PRICE-RETAIL               
      *                        '  $ PRO ' ET005-UNIT-PRICE-PROMO                
      *                        '  $ CLR ' ET005-UNIT-PRICE-CLEAR                
      *                        '  $ ALL ' ET005-UNIT-PRICE-ALL                  
      *                        '  SALES TYPE ' ET005-SALES-TYPE-LEVEL           
                                                                                
                                                                                
JF0305                 IF PV-LAST-STORE = '00000'                               
                                                                                
                           IF PS-ITEM-SALES                                     
                               ADD +1 TO  PC-SALES-CNTR                         
                           ELSE                                                 
                               ADD +1 TO  PC-NO-SALES-CNTR                      
                           END-IF                                               
                                                                                
                           IF ET004-CORP-LEVEL                                  
                               IF ET005-UNITS-SOLD-ALL = ZERO                   
      *                            ** SKIP ITEMS WITH 0 TOTAL SALES **          
                                   CONTINUE                                     
                               ELSE                                             
      *                            ** WRITE THE PREVIOUS EXTRACT REC **         
                                   PERFORM Z700-WRITE-EXTRACT-FILE              
                               END-IF                                           
      *                        ** DO NOT PROCESS ANYMORE STORE DETAIL **        
                               IF ET004-DAILY-SALES                             
                                   PERFORM U350-FETCH-DAILY-SALES               
                                     UNTIL PS-NO-SALES-RETREIVED                
                               END-IF                                           
                               IF ET004-WEEKLY-SALES                            
                                   PERFORM U550-FETCH-WEEKLY-SALES              
                                     UNTIL PS-NO-SALES-RETREIVED                
                               END-IF                                           
                           ELSE                                                 
      *                        ** CREATING STORE LEVEL DETAIL **                
                               IF ET005-UNITS-SOLD-ALL = ZERO                   
                                   DISPLAY 'STORE : 000 ;'                      
                                      'ITEM : ' ET005-UPC-CODE                  
                                      ' ; ZERO SALES WRITTEN'                   
                               END-IF                                           
      *                        ** WRITE OUT PREVIOUS EXTRACT REC **             
                               PERFORM Z700-WRITE-EXTRACT-FILE                  
                           END-IF                                               
                       ELSE                                                     
                           IF ET005-UNITS-SOLD-ALL = ZERO                       
      *                       ** SKIP STORES WITH 0 SALES **                    
                               CONTINUE                                         
                           ELSE                                                 
      *                       ** WRITE OUT PREVIOUS EXTRACT REC **              
                              PERFORM Z700-WRITE-EXTRACT-FILE                   
                           END-IF                                               
                                                                                
                       END-IF                                                   
                                                                                
      *                ** SAVE LAST STORE NUMBER PROCESSED **                   
JF0305                 MOVE SKSTWKS-STR-NBR TO PV-STORE-NMBR-NUM                
JF0305                 MOVE PV-STORE-NMBR-NUM TO PV-LAST-STORE                  
                                                                                
                       SET PS-NO-ITEM-SALES TO TRUE                             
                                                                                
                   END-IF                                                       
                                                                                
               END-IF                                                           
                                                                                
               IF PS-NO-SALES-RETREIVED                                         
                   CONTINUE                                                     
               ELSE                                                             
JF0305             MOVE SKSTWKS-STR-NBR                                         
JF0305               TO PV-STORE-NMBR-NUM                                       
JF0305             MOVE PV-STORE-NMBR-NUM                                       
                     TO ET005-STORE-NUMBER                                      
                                                                                
                   MOVE ZEROES TO PV-QTY-UNITS-SOLD                             
                                                                                
                   IF ET004-DAILY-SALES                                         
                       EVALUATE TRUE                                            
                           WHEN PV-YESTERDAY-SUNDAY                             
WR6041                         MOVE SKSTWKS-SUN-SLS-UNIT-QTY                    
                                 TO PV-QTY-UNITS-SOLD                           
                           WHEN PV-YESTERDAY-MONDAY                             
WR6041                         MOVE SKSTWKS-MON-SLS-UNIT-QTY                    
                                 TO PV-QTY-UNITS-SOLD                           
                           WHEN PV-YESTERDAY-TUESDAY                            
WR6041                         MOVE SKSTWKS-TUE-SLS-UNIT-QTY                    
                                 TO PV-QTY-UNITS-SOLD                           
                           WHEN PV-YESTERDAY-WEDNESDAY                          
WR6041                         MOVE SKSTWKS-WED-SLS-UNIT-QTY                    
                                 TO PV-QTY-UNITS-SOLD                           
                           WHEN PV-YESTERDAY-THURSDAY                           
WR6041                         MOVE SKSTWKS-THU-SLS-UNIT-QTY                    
                                 TO PV-QTY-UNITS-SOLD                           
                           WHEN PV-YESTERDAY-FRIDAY                             
WR6041                         MOVE SKSTWKS-FRI-SLS-UNIT-QTY                    
                                 TO PV-QTY-UNITS-SOLD                           
                           WHEN PV-YESTERDAY-SATURDAY                           
WR6041                         MOVE SKSTWKS-SAT-SLS-UNIT-QTY                    
                                 TO PV-QTY-UNITS-SOLD                           
                       END-EVALUATE                                             
                   END-IF                                                       
                                                                                
                   IF ET004-WEEKLY-SALES                                        
WR6041                 MOVE SKSTWKS-SALES-UNIT-QTY                              
                         TO PV-QTY-UNITS-SOLD                                   
                   END-IF                                                       
                                                                                
JF0305             IF ET005-STORE-NUMBER = '00000'                              
                       SET ET005-ALL-STORES-RECORD TO TRUE                      
                       IF PV-QTY-UNITS-SOLD NOT = 0                             
                           SET PS-ITEM-SALES TO TRUE                            
                       END-IF                                                   
                   ELSE                                                         
                       SET ET005-STORE-RECORD TO TRUE                           
                   END-IF                                                       
                                                                                
                   PERFORM U700-PROCESS-SALES                                   
                                                                                
                   SET PS-NOT-FIRST-STORE TO TRUE                               
                                                                                
               END-IF                                                           
                                                                                
           ELSE                                                                 
      *        ** NO MORE SALES FOUND **                                        
                                                                                
               IF PS-FIRST-STORE                                                
                   ADD +1 TO  PC-NO-SALES-CNTR                                  
               ELSE                                                             
                   IF  ET005-UNITS-SOLD-ALL = ZERO                              
                        CONTINUE                                                
                   ELSE                                                         
      **                ** WRITE OUT THE PREVIOUS EXTRACT RECORD **             
                        PERFORM Z700-WRITE-EXTRACT-FILE                         
                   END-IF                                                       
               END-IF                                                           
                                                                                
             END-IF                                                             
           END-IF.                                                              
                                                                                
       EJECT                                                                    
       U100-OPEN-DEPT-CL-INHSE-ITEM.                                            
           MOVE 'U100-OPEN-DEPT-CL-INHSE-ITEM' TO PV-PARAGRAPH-NAME.            
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *                                                                         
      *    PERFORMED BY                                                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               OPEN DEPT_CL_INHSE_ITEM                                          
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0 = 'W')                                                  
                                                                                
               MOVE 'OPEN DEPT/CL/INHSE ITEMS'    TO                            
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-SQL-ABEND                                           
                                                                                
           END-IF.                                                              
                                                                                
       U150-FETCH-DEPT-CL-INHSE-ITEM.                                           
           MOVE 'U150-FETCH-DEPT-CL-INHSE-ITEM' TO PV-PARAGRAPH-NAME.           
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    ALL ITEMS FOR A DEPT/CL/INHSE                                        
      *                                                                         
      *    PERFORMED BY                                                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
                FETCH DEPT_CL_INHSE_ITEM                                        
                INTO                                                            
                     :TSK-ITEM-NMBR,                                            
                     :TUPCSKU-UPC-NBR,                                          
                     :TUPCSKU-EDI-ORD-IND                                       
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +100                                                    
               SET PS-NO-MORE-ITEMS TO TRUE                                     
      *        DISPLAY 'NO MORE ITEMS'                                          
           END-IF.                                                              
                                                                                
           IF SQLCODE = +0                                                      
               SET PS-MORE-ITEMS TO TRUE                                        
           END-IF.                                                              
                                                                                
           IF SQLCODE NOT = +0                                                  
                                                                                
               IF (SQLCODE NOT = +100) OR                                       
                  (SQLWARN0    = 'W')                                           
                                                                                
                   MOVE 'FETCH DEPT/CL/INHSE CURSOR'  TO                        
                        PV-DB2-OPERATION-ATTEMPTED                              
                                                                                
                   PERFORM Z999-SQL-ABEND                                       
                                                                                
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
       U190-CLOSE-DEPT-CL-INHSE-ITEM.                                           
           MOVE 'U190-CLOSE-DEPT-CL-INHSE-ITEM' TO PV-PARAGRAPH-NAME.           
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *                                                                         
      *    PERFORMED BY                                                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               CLOSE DEPT_CL_INHSE_ITEM                                         
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0    = 'W')                                               
                                                                                
               MOVE 'CLOSE DEPT/CL/INHSE ITEMS'   TO                            
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-SQL-ABEND                                           
                                                                                
           END-IF.                                                              
                                                                                
       EJECT                                                                    
       U200-OPEN-DAILY-ONHANDS.                                                 
           MOVE 'U200-OPEN-DAILY-ONHANDS' TO PV-PARAGRAPH-NAME.                 
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *                                                                         
      *    PERFORMED BY                                                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               OPEN DAILY_ONHANDS                                               
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0    = 'W')                                               
                                                                                
               MOVE 'OPEN DAILY ONHANDS CURSOR'    TO                           
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-SQL-ABEND                                           
                                                                                
           END-IF.                                                              
                                                                                
       U250-FETCH-DAILY-ONHANDS.                                                
           MOVE 'U250-FETCH-DAILY-ONHANDS' TO PV-PARAGRAPH-NAME.                
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *                                                                         
      *    PERFORMED BY                                                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
                FETCH DAILY_ONHANDS                                             
                INTO                                                            
                     :TSKST-ITEM-NMBR,                                          
JF0305               :TSKST-LOC-NBR,                                            
                     :TSKST-ONHAND-QTY                                          
           END-EXEC.                                                            
                                                                                
                                                                                
           IF SQLCODE = +100                                                    
               SET PS-NO-MORE-ONHANDS TO TRUE                                   
      *        DISPLAY 'NO MORE ONHANDS'                                        
           END-IF.                                                              
                                                                                
           IF SQLCODE = +0                                                      
JF0305         MOVE TSKST-LOC-NBR TO PV-STORE-NMBR-NUM                          
JF0305         MOVE PV-STORE-NMBR-NUM TO PV-STORE-NMBR                          
               SET PS-MORE-ONHANDS TO TRUE                                      
               SET STORE-IDX  TO 1                                              
               PERFORM W100-VALIDATE-STORE                                      
           END-IF.                                                              
                                                                                
           IF SQLCODE NOT = +0                                                  
                                                                                
               IF (SQLCODE NOT = +100) OR                                       
                  (SQLWARN0    = 'W')                                           
                                                                                
                   MOVE 'FETCH DAILY ONHANDS CURSOR'  TO                        
                        PV-DB2-OPERATION-ATTEMPTED                              
                                                                                
                   PERFORM Z999-SQL-ABEND                                       
                                                                                
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
       U290-CLOSE-DAILY-ONHANDS.                                                
           MOVE 'U290-CLOSE-DAILY-ONHANDS' TO PV-PARAGRAPH-NAME.                
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *                                                                         
      *    PERFORMED BY                                                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               CLOSE DAILY_ONHANDS                                              
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0    = 'W')                                               
                                                                                
               MOVE 'CLOSE DAILY ONHANDS CURSOR'   TO                           
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-SQL-ABEND                                           
                                                                                
           END-IF.                                                              
                                                                                
       EJECT                                                                    
       U300-OPEN-DAILY-SALES.                                                   
           MOVE 'U300-OPEN-DAILY-SALES' TO PV-PARAGRAPH-NAME.                   
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *                                                                         
      *    PERFORMED BY                                                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               OPEN DAILY_SALES                                                 
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0    = 'W')                                               
                                                                                
               MOVE 'OPEN DAILY SALES CURSOR'    TO                             
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-SQL-ABEND                                           
                                                                                
           END-IF.                                                              
                                                                                
       U350-FETCH-DAILY-SALES.                                                  
           MOVE 'U350-FETCH-DAILY-SALES' TO PV-PARAGRAPH-NAME.                  
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *                                                                         
      *    PERFORMED BY                                                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
                FETCH DAILY_SALES                                               
                INTO                                                            
WR6041               :SKSTWKS-ITEM-NMBR,                                        
WR6041               :SKSTWKS-FSCL-BOW-DTE,                                     
JF0305               :SKSTWKS-STR-NBR,                                          
WR6041               :SKSTWKS-SALES-TYPE-CODE,                                  
WR6041               :SKSTWKS-SALES-UNIT-QTY,                                   
WR6041               :SKSTWKS-SALES-AMT,                                        
WR6041               :SKSTWKS-SUN-SLS-UNIT-QTY,                                 
WR6041               :SKSTWKS-MON-SLS-UNIT-QTY,                                 
WR6041               :SKSTWKS-TUE-SLS-UNIT-QTY,                                 
WR6041               :SKSTWKS-WED-SLS-UNIT-QTY,                                 
WR6041               :SKSTWKS-THU-SLS-UNIT-QTY,                                 
WR6041               :SKSTWKS-FRI-SLS-UNIT-QTY,                                 
WR6041               :SKSTWKS-SAT-SLS-UNIT-QTY                                  
           END-EXEC.                                                            
                                                                                
                                                                                
           IF SQLCODE = +100                                                    
               SET PS-NO-SALES-RETREIVED TO TRUE                                
      *        DISPLAY 'NO MORE SALES'                                          
           END-IF.                                                              
                                                                                
           IF SQLCODE = +0                                                      
JF0305         MOVE SKSTWKS-STR-NBR TO PV-STORE-NMBR-NUM                        
JF0305         MOVE PV-STORE-NMBR-NUM TO PV-STORE-NMBR                          
               SET PS-SALES-RETREIVED TO TRUE                                   
               SET STORE-IDX  TO 1                                              
               PERFORM W100-VALIDATE-STORE                                      
           END-IF.                                                              
                                                                                
           IF SQLCODE NOT = +0                                                  
                                                                                
               IF (SQLCODE NOT = +100) OR                                       
                  (SQLWARN0    = 'W')                                           
                                                                                
                   MOVE 'FETCH DAILY SALES CURSOR'  TO                          
                        PV-DB2-OPERATION-ATTEMPTED                              
                                                                                
                   PERFORM Z999-SQL-ABEND                                       
                                                                                
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
       U390-CLOSE-DAILY-SALES.                                                  
           MOVE 'U390-CLOSE-DAILY-SALES' TO PV-PARAGRAPH-NAME.                  
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *                                                                         
      *    PERFORMED BY                                                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               CLOSE DAILY_SALES                                                
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0    = 'W')                                               
                                                                                
               MOVE 'CLOSE DAILY SALES CURSOR'   TO                             
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-SQL-ABEND                                           
                                                                                
           END-IF.                                                              
                                                                                
       EJECT                                                                    
       U400-OPEN-WEEKLY-ONHANDS.                                                
           MOVE 'U400-OPEN-WEEKLY-ONHANDS' TO PV-PARAGRAPH-NAME.                
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *                                                                         
      *    PERFORMED BY                                                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               OPEN WEEKLY_ONHANDS                                              
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0    = 'W')                                               
                                                                                
               MOVE 'OPEN WEEKLY ONHANDS CURSOR'    TO                          
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-SQL-ABEND                                           
                                                                                
           END-IF.                                                              
                                                                                
       U450-FETCH-WEEKLY-ONHANDS.                                               
           MOVE 'U450-FETCH-WEEKLY-ONHANDS' TO PV-PARAGRAPH-NAME.               
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *                                                                         
      *    PERFORMED BY                                                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
                FETCH WEEKLY_ONHANDS                                            
                INTO                                                            
                     :SKSTWKI-ITEM-NMBR,                                        
JF0305               :SKSTWKI-LOC-NBR,                                          
                     :SKSTWKI-FSCL-BOW-DTE,                                     
                     :SKSTWKI-INVEN-ONHAND-QTY                                  
           END-EXEC.                                                            
                                                                                
                                                                                
           IF SQLCODE = +100                                                    
               SET PS-NO-MORE-ONHANDS TO TRUE                                   
      *        DISPLAY 'NO MORE ONHANDS'                                        
           END-IF.                                                              
                                                                                
           IF SQLCODE = +0                                                      
               SET PS-MORE-ONHANDS TO TRUE                                      
JF0305         MOVE SKSTWKI-LOC-NBR TO PV-STORE-NMBR-NUM                        
JF0305         MOVE PV-STORE-NMBR-NUM TO PV-STORE-NMBR                          
               SET STORE-IDX  TO 1                                              
               PERFORM W100-VALIDATE-STORE                                      
           END-IF.                                                              
                                                                                
           IF SQLCODE NOT = +0                                                  
                                                                                
               IF (SQLCODE NOT = +100) OR                                       
                  (SQLWARN0    = 'W')                                           
                                                                                
                   MOVE 'FETCH WEEKLY ONHANDS CURSOR'  TO                       
                        PV-DB2-OPERATION-ATTEMPTED                              
                                                                                
                   PERFORM Z999-SQL-ABEND                                       
                                                                                
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
       U490-CLOSE-WEEKLY-ONHANDS.                                               
           MOVE 'U490-CLOSE-WEEKLY-ONHANDS' TO PV-PARAGRAPH-NAME.               
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *                                                                         
      *    PERFORMED BY                                                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               CLOSE WEEKLY_ONHANDS                                             
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0    = 'W')                                               
                                                                                
               MOVE 'CLOSE WEEKLY ONHANDS CURSOR'   TO                          
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-SQL-ABEND                                           
                                                                                
           END-IF.                                                              
                                                                                
       EJECT                                                                    
       U500-OPEN-WEEKLY-SALES.                                                  
           MOVE 'U500-OPEN-WEEKLY-SALES' TO PV-PARAGRAPH-NAME.                  
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *                                                                         
      *    PERFORMED BY                                                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               OPEN WEEKLY_SALES                                                
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0    = 'W')                                               
                                                                                
               MOVE 'OPEN WEEKLY SALES CURSOR'   TO                             
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-SQL-ABEND                                           
                                                                                
           END-IF.                                                              
                                                                                
       U550-FETCH-WEEKLY-SALES.                                                 
           MOVE 'U550-FETCH-WEEKLY-SALES' TO PV-PARAGRAPH-NAME.                 
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *                                                                         
      *    PERFORMED BY                                                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
                FETCH WEEKLY_SALES                                              
                INTO                                                            
WR6041               :SKSTWKS-ITEM-NMBR,                                        
JF0305               :SKSTWKS-STR-NBR,                                          
WR6041               :SKSTWKS-FSCL-BOW-DTE,                                     
WR6041               :SKSTWKS-SALES-TYPE-CODE,                                  
WR6041               :SKSTWKS-SALES-UNIT-QTY,                                   
WR6041               :SKSTWKS-SALES-AMT                                         
           END-EXEC.                                                            
                                                                                
                                                                                
           IF SQLCODE = +100                                                    
               SET PS-NO-SALES-RETREIVED TO TRUE                                
      *        DISPLAY '** NO MORE SALES **'                                    
           END-IF.                                                              
                                                                                
           IF SQLCODE = +0                                                      
               SET PS-SALES-RETREIVED TO TRUE                                   
JF0305         MOVE SKSTWKS-STR-NBR TO PV-STORE-NMBR-NUM                        
JF0305         MOVE PV-STORE-NMBR-NUM TO PV-STORE-NMBR                          
               SET STORE-IDX  TO 1                                              
               PERFORM W100-VALIDATE-STORE                                      
           END-IF.                                                              
                                                                                
           IF SQLCODE NOT = +0                                                  
                                                                                
               IF (SQLCODE NOT = +100) OR                                       
                  (SQLWARN0    = 'W')                                           
                                                                                
                   MOVE 'FETCH WEEKLY SALES CURSOR'   TO                        
                        PV-DB2-OPERATION-ATTEMPTED                              
                                                                                
                   PERFORM Z999-SQL-ABEND                                       
                                                                                
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
       U590-CLOSE-WEEKLY-SALES.                                                 
           MOVE 'U590-CLOSE-WEEKLY-SALES' TO PV-PARAGRAPH-NAME.                 
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *                                                                         
      *    PERFORMED BY                                                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               CLOSE WEEKLY_SALES                                               
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0    = 'W')                                               
                                                                                
               MOVE 'CLOSE WEEKLY SALES CURSOR'  TO                             
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-SQL-ABEND                                           
                                                                                
           END-IF.                                                              
                                                                                
       EJECT                                                                    
       U700-PROCESS-SALES.                                                      
           MOVE 'U700-PROCESS-SALES' TO PV-PARAGRAPH-NAME.                      
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    PERFORMED BY E200-PROCESS-SALES-ONHANDS.                             
      ******************************************************************        
                                                                                
      *    ** 'PV-QTY-UNITS-SOLD' CONTAINS DAILY OR WEEKLY FIGURE **            
      *    **  FOR THE CURRENT SALES-TYPE RECORD BEING PROCESSED  **            
                                                                                
           ADD PV-QTY-UNITS-SOLD                                                
            TO ET005-UNITS-SOLD-ALL.                                            
                                                                                
WR6041     ADD SKSTWKS-SALES-AMT                                                
            TO PV-SALES-AMT.                                                    
                                                                                
      *    ** THE FOLLOWING IS MEANT TO BE AN AVERAGE SALE PRICE **             
           IF ET005-UNITS-SOLD-ALL NOT = 0                                      
               COMPUTE ET005-UNIT-PRICE-ALL =                                   
               ( PV-SALES-AMT / ET005-UNITS-SOLD-ALL )                          
           END-IF.                                                              
                                                                                
           IF ET004-INDIVIDUAL-SALES                                            
                                                                                
WR6041         IF SKSTWKS-SALES-TYPE-CODE = '1'                                 
                   MOVE PV-QTY-UNITS-SOLD                                       
                     TO ET005-UNITS-SOLD-RETAIL                                 
                   COMPUTE ET005-UNIT-PRICE-RETAIL =                            
WR6041               ( SKSTWKS-SALES-AMT / PV-QTY-UNITS-SOLD )                  
               END-IF                                                           
WR6041         IF SKSTWKS-SALES-TYPE-CODE = '2'                                 
                   MOVE PV-QTY-UNITS-SOLD                                       
                     TO ET005-UNITS-SOLD-PROMO                                  
                   COMPUTE ET005-UNIT-PRICE-PROMO =                             
WR6041               ( SKSTWKS-SALES-AMT / PV-QTY-UNITS-SOLD )                  
               END-IF                                                           
WR6041         IF SKSTWKS-SALES-TYPE-CODE = '3'                                 
                   MOVE PV-QTY-UNITS-SOLD                                       
                     TO ET005-UNITS-SOLD-CLEAR                                  
                   COMPUTE ET005-UNIT-PRICE-CLEAR =                             
WR6041               ( SKSTWKS-SALES-AMT / PV-QTY-UNITS-SOLD )                  
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
      *    DISPLAY '  PV QTY SOLD : ' PV-QTY-UNITS-SOLD                         
      *            '  PV SALES $ : ' PV-SALES-AMT                               
WR6041*            '  SKSTWKS AMT : ' SKSTWKS-SALES-AMT                         
      *            '  ALL SOLD : ' ET005-UNITS-SOLD-ALL                         
      *            '  UNIT PRICE ALL : ' ET005-UNIT-PRICE-ALL                   
WR6041*            '  SALES TYP CDE :' SKSTWKS-SALES-TYPE-CODE.                 
                                                                                
       EJECT                                                                    
       W100-VALIDATE-STORE.                                                     
           MOVE 'W100-VALIDATE-STORE' TO PV-PARAGRAPH-NAME.                     
                                                                                
      ******************************************************************        
      *    THIS PARAGRAPH VALIDATES THE STORE NUMBER READ DURING THE            
      *    CURSOR FETCH AGAINST THE VALID-STORES-TABLE TO DETERMINE IF          
      *    SALES FOR THE ITEM IN THE STORE SHOULD BE COUNTED.                   
      *    PERFORMED BY U250-FETCH-DAILY-ONHANDS                                
      *    PERFORMED BY U450-FETCH-WEEKLY-ONHANDS                               
      *    PERFORMED BY U350-FETCH-DAILY-SALES                                  
      *    PERFORMED BY U550-FETCH-WEEKLY-SALES                                 
      ******************************************************************        
JF0305     IF PV-STORE-NMBR = '00000'                                           
              SET PS-VALID-STORE                 TO TRUE                        
           ELSE                                                                 
              SEARCH VALID-STORES VARYING STORE-IDX                             
                     AT END                                                     
                        SET PS-NOT-VALID-STORE   TO TRUE                        
                     WHEN VALID-STORES(STORE-IDX) = PV-STORE-NMBR               
                     SET PS-VALID-STORE       TO TRUE                           
                     CONTINUE                                                   
              END-SEARCH                                                        
           END-IF.                                                              
                                                                                
       Z300-EDIT-CONTROL-CARD.                                                  
           MOVE 'Z300-EDIT-CONTROL-CARD' TO PV-PARAGRAPH-NAME.                  
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    VERIFY THAT THE RUN DATE RECEIVED ON THE CONTROL CARD IS             
      *    NUMERIC, THEN UTILIZE THIS DATE TO CALL THE CALENDAR ROUTINE         
      *    AND RETRIVE ALL THE OTHER REQUIRED PROCESSING DATES.                 
      *                                                                         
      *    PERFORMED BY A100-MAIN-MODULE.                                       
      ******************************************************************        
WR6041     PERFORM Z450-INIT-CALENDAR-DPKUT500                                  
                                                                                
           IF (CC-RUN-DATE NOT NUMERIC)          OR                             
              (CC-FISCAL-CALENDAR-TYPE = SPACES)                                
                                                                                
               DISPLAY '  '                                                     
               DISPLAY '** ABEND IN ETKUT003 **'                                
               DISPLAY 'CONTROL CARD RUN DATE OR CALENDAR TYPE INVALID'         
               DISPLAY 'CC-RUN-DATE          ' CC-RUN-DATE                      
               DISPLAY 'CC-FISCAL-CALENDAR-TYPE '                               
                                               CC-FISCAL-CALENDAR-TYPE          
               MOVE +4003 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           ELSE                                                                 
      *        ** USE THE CALENDAR ROUTINE RETREIVE THE FISCAL YEAR             
      *        ** AND WEEK FOR THE RUN DATE RECEIVED                            
                                                                                
               MOVE CC-RUN-DATE         TO  DPG52-GREG-DATE                     
               PERFORM Z400-CALL-CALENDAR-DPKUT500                              
                                                                                
               IF NOT DPG54-SEVERE-ERROR                                        
                                                                                
                   MOVE DPG55-NUMERIC-WEEK-DAY                                  
                                     TO PV-YESTERDAY-WKDY                       
JF0205             MOVE DPG55-1ST-WKDY-DB2ISO-FMT                               
JF0205                               TO PV-BOW-CURR-DTE                         
JF0205                                  PV-BOW-CURR-DTE-DLY                     
JF0205            DISPLAY PV-BOW-CURR-DTE     '= PV-BOW-CURR-DTE'               
JF0205            DISPLAY PV-BOW-CURR-DTE-DLY '= PV-BOW-CURR-DTE-DLY'           
                                                                                
                  IF DPG55-ALPHA-WEEK-DAY-ABBR3 = 'SAT'                         
                       MOVE DPG55-1ST-WKDY-GREG                                 
                                             TO  PV-PREV-WK-START-DATE          
                       MOVE DPG55-LAST-WKDY-GREG                                
                                             TO  PV-PREV-WK-END-DATE            
JF0205                 MOVE DPG55-1ST-WKDY-DB2ISO-FMT                           
JF0205                                             TO PV-BOW-LAST-DTE           
JF0205                 DISPLAY PV-BOW-LAST-DTE    '= PV-BOW-LAST-DTE'           
JF0205*           **  CALL THE CALENDAR ROUTINE A SECOND TIME, WITH             
JF0205*           **  END DATE OF RUN DATE WEEK, PLUS 1                         
JF0205*           **  (THIS WILL RETURN THE CURRENT WEEK'S START DATE           
JF0205*           **  FOR A SATURDAY CONTROL CARD DATE)                         
JF0205                                                                          
JF0205                 MOVE DPG55-LAST-WKDY-GREG TO  PV-GREG-DTE                
JF0205                 INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56           
JF0205                 MOVE PV-GREG-DTE          TO DPG52-GREG-DATE             
JF0205                                                                          
JF0205                 MOVE 1 TO DPG51-INCR-DECR-DAYS-9                         
JF0205                 SET DPG51-INCREMENT-DATE  TO TRUE                        
JF0205                 PERFORM Z400-CALL-CALENDAR-DPKUT500                      
JF0205                                                                          
JF0205                 IF NOT DPG54-SEVERE-ERROR                                
JF0205                                                                          
JF0205                    MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO                     
JF0205                                  PV-BOW-CURR-DTE                         
JF0205                    DISPLAY 'PV-BOW-CURR-DTE SAT' PV-BOW-CURR-DTE         
                       END-IF                                                   
                  ELSE                                                          
                                                                                
      *           **  CALL THE CALENDAR ROUTINE A SECOND TIME, WITH             
      *           **  END DATE OF RUN DATE WEEK, MINUS 1                        
      *           **  (THIS WILL RETURN THE PREVIOUS WEEK'S INFO)               
WR6041             MOVE DPG55-1ST-WKDY-GREG  TO  PV-GREG-DTE                    
WR6041             PERFORM Z450-INIT-CALENDAR-DPKUT500                          
WR6041             MOVE PV-GREG-DTE          TO DPG52-GREG-DATE                 
                                                                                
                   MOVE 1                    TO  DPG51-INCR-DECR-DAYS-9         
                   SET DPG51-DECREMENT-DATE  TO TRUE                            
                   PERFORM Z400-CALL-CALENDAR-DPKUT500                          
                                                                                
                   IF NOT DPG54-SEVERE-ERROR                                    
                       MOVE DPG55-1ST-WKDY-GREG                                 
                                             TO  PV-PREV-WK-START-DATE          
                       MOVE DPG55-LAST-WKDY-GREG                                
                                             TO  PV-PREV-WK-END-DATE            
WR6041                 MOVE DPG55-1ST-WKDY-DB2ISO-FMT                           
WR6041                                             TO PV-BOW-LAST-DTE           
WR6041                 DISPLAY PV-BOW-LAST-DTE     '= PV-BOW-LAST-DTE'          
                   END-IF                                                       
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
       Z400-CALL-CALENDAR-DPKUT500.                                             
           MOVE 'Z400-CALL-CALENDAR-DPKUT500' TO PV-PARAGRAPH-NAME.             
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    CALL THE KOHLS INHOUSE CALENDAR ROUTINE TO DETERMINE                 
      *    ALL REQUIRED PROCESSING DATE INFORMATION.                            
      *                                                                         
      *    PERFORMED BY A100-MAIN-MODULE.                                       
      ******************************************************************        
                                                                                
           MOVE TD-TODAYS-DATE          TO  DPG51-SYSTEM-DATE.                  
           MOVE CC-FISCAL-CALENDAR-TYPE TO  DPG51-CALENDAR-TYPE.                
                                                                                
           SET DPG52-LK-DTE-GREG        TO  TRUE.                               
      *    ** 'DPG52-GREG-DATE' IS SET BEFORE THIS PARAGRAPH **                 
                                                                                
           CALL  PC-DPKUT500-CALENDAR  USING  DPG51 DPG52                       
                                              DPG53 DPG54                       
                                              DPG55 DPG56.                      
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DPG54-SEVERE-ERROR                                          
                     DISPLAY '** DPKUT500 - SEVERE ERROR :   '                  
                             DPG54-ERROR-MESSAGE                                
                     DISPLAY '**       SYSTEM DATE : '                          
                             DPG51-SYSTEM-DATE                                  
                     DISPLAY '**     CALENDAR TYPE : '                          
                             DPG51-CALENDAR-TYPE                                
                     DISPLAY '**  DATE FORMAT CODE : '                          
                             DPG52-LK-DATE-FORMAT-CODE                          
                     DISPLAY '**        INPUT DATE : '                          
                             DPG52-LK-DATE-INPUT                                
                     MOVE +4019 TO ABEND-CODE                                   
                     CALL 'ILBOABN0' USING ABEND-CODE                           
               WHEN DPG54-WARNING-ERROR                                         
                     DISPLAY '** DPKUT500 - WARNING :   '                       
                             DPG54-ERROR-MESSAGE                                
                     DISPLAY '**       SYSTEM DATE : '                          
                             DPG51-SYSTEM-DATE                                  
                     DISPLAY '**     CALENDAR TYPE : '                          
                             DPG51-CALENDAR-TYPE                                
                     DISPLAY '**  DATE FORMAT CODE : '                          
                             DPG52-LK-DATE-FORMAT-CODE                          
                     DISPLAY '**        INPUT DATE : '                          
                             DPG52-LK-DATE-INPUT                                
               WHEN DPG54-NO-ERROR                                              
                     CONTINUE                                                   
                                                                                
           END-EVALUATE.                                                        
       EJECT                                                                    
                                                                                
WR6041 Z450-INIT-CALENDAR-DPKUT500.                                             
WR6041     INITIALIZE DPG51 DPG52                                               
WR6041                DPG53 DPG54                                               
WR6041                DPG55 DPG56.                                              
       EJECT                                                                    
                                                                                
       Z500-READ-ITEM-SELECT-FILE.                                              
           MOVE 'Z500-READ-ITEM-SELECT-FILE' TO PV-PARAGRAPH-NAME.              
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      ******************************************************************        
      *    READ A RECORD FROM THE ITEM SELECT FILE.                             
      *                                                                         
      *    PERFORMED BY A100-MAIN-MODULE.                                       
      *    PERFORMED BY B400-PROCESS-CNTL-TBL-ENTRIES.                          
      ******************************************************************        
                                                                                
           READ ITEM-SELECT-FILE                                                
               INTO ET004-ITEM-SELECT-RECORD                                    
               AT END                                                           
                   SET PS-ITEM-SELECT-EOF TO TRUE.                              
                                                                                
           IF ET004-DPT-CL-INHSE-RECORD                                         
               SET PS-NEW-DEPT-CL-INHSE-REC TO TRUE                             
           END-IF.                                                              
                                                                                
      *    DISPLAY 'FULL REC : ' ET004-ITEM-SELECT-RECORD.                      
      *    DISPLAY '   DUNNS : ' ET004-KEY-VENDOR-DUNNS.                        
      *    DISPLAY 'REC TYPE : ' ET004-KEY-RECORD-TYPE.                         
                                                                                
      *    IF ET004-VENDOR-RECORD                                               
      *        DISPLAY '            VENDOR RECORD'                              
      *    END-IF.                                                              
      *    IF ET004-DPT-CL-INHSE-RECORD                                         
      *        DISPLAY '            DEPT/CL/INHSE RECORD'                       
      *    END-IF.                                                              
                                                                                
      *    DISPLAY '    DEPT : ' ET004-DEPT.                                    
      *    DISPLAY 'IN-HOUSE : ' ET004-DEPT-INHOUSE.                            
                                                                                
       EJECT                                                                    
       Z600-SELECT-TTRDPRT.                                                     
           MOVE 'Z600-SELECT-TTRDPRT' TO PV-PARAGRAPH-NAME.                     
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      *****************************************************************         
                                                                                
            EXEC SQL                                                            
                 SELECT TTRDPRT.ENT_ID                                          
                   INTO :TRDPRT-ENT-ID                                          
                   FROM TTRDPRT,                                                
                        TTRDTRN                                                 
                  WHERE TTRDPRT.ENT_ID        = :ENTNME-ENT-ID   AND            
                        TTRDTRN.TP_DKEY       = TTRDPRT.TP_DKEY  AND            
                        TTRDTRN.TRAN_SET_CDE  = '852'            AND            
                        TTRDPRT.ACTV_DTE     <= CURRENT DATE     AND            
                        TTRDPRT.INACTV_DTE   >  CURRENT DATE     AND            
                        TTRDTRN.ACTV_DTE     <= CURRENT DATE     AND            
                        TTRDTRN.INACTV_DTE   >  CURRENT DATE                    
            END-EXEC.                                                           
                                                                                
       Z700-WRITE-EXTRACT-FILE.                                                 
           MOVE 'Z700-WRITE-EXTRACT-FILE' TO PV-PARAGRAPH-NAME.                 
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      *****************************************************************         
      *    WRITE A RECORD TO THE SALES HISTORY EXTRACT FILE.                    
      *                                                                         
      *       FOR EACH  DEPT / INHSE :                                          
      *            TOTAL NUMBER OF ITEMS SOLD                                   
      *            TOTAL NUMBER OF ITEMS ONHAND                                 
      *            ** FOR ALL EDI FLAGGED ITEMS                                 
      *       (THIS INFORMATION IS WRITTEN ON THE CONTROL REPORT)               
      *                                                                         
      *    PERFORMED BY F100-CREATE-ALL-STORES-RECORD.                          
      *    PERFORMED BY F200-CREATE-STORE-RECORD.                               
      *****************************************************************         
                                                                                
           WRITE EDI-SALES-EXTRACT-RECORD                                       
               FROM ET005-EDI-SALES-EXTRACT-REC.                                
                                                                                
           SET PS-NO-ITEM-SUMMARY TO TRUE.                                      
                                                                                
JF0305     IF ET005-STORE-NUMBER = '00000'                                      
               ADD ET005-ONHAND-QUANTITY                                        
                TO PV-DPT-CL-INHSE-ONHANDS                                      
               ADD ET005-UNITS-SOLD-ALL                                         
                TO PV-DPT-CL-INHSE-UNITS-SOLD                                   
                                                                                
               IF ET005-ALL-STORES-RECORD                                       
                   IF ET004-CORP-LEVEL                                          
                      SET PS-CREATE-ITEM-SUMMARY TO TRUE                        
                   END-IF                                                       
                   IF ET004-ITEM-SUMMARY                                        
                      SET PS-CREATE-ITEM-SUMMARY TO TRUE                        
                   END-IF                                                       
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
           IF PS-CREATE-ITEM-SUMMARY                                            
      *        *** CREATE A STORE '000' RECORD WITH TOTALS FOR ALL              
      *        *** ONHANDS AND/OR SALES, ACROSS ALL STORES.                     
      *        *** 1. FOR ALL CORP LEVEL VENDORS (TRANSLATOR REQ.)              
      *        *** 2. FOR THOSE VENDORS WHO WOULD LIKE ITEM SUMMARY             
      *        ***    TOTALS ALONG WITH THE STORE LEVEL DETAIL.                 
                                                                                
                SET ET005-STORE-RECORD TO TRUE                                  
                                                                                
                MOVE ZEROES  TO  ET005-UNIT-PRICE-RETAIL                        
                                 ET005-UNIT-PRICE-PROMO                         
                                 ET005-UNIT-PRICE-CLEAR                         
                                 ET005-UNIT-PRICE-ALL                           
                                                                                
                WRITE EDI-SALES-EXTRACT-RECORD                                  
                    FROM ET005-EDI-SALES-EXTRACT-REC                            
                                                                                
                SET PS-NO-ITEM-SUMMARY TO TRUE                                  
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
           MOVE ZEROES             TO  PV-SALES-AMT.                            
                                                                                
           MOVE SPACES             TO  ET005-STORE-NUMBER.                      
                                                                                
           MOVE ZEROES             TO  ET005-UNITS-SOLD-RETAIL                  
                                       ET005-UNITS-SOLD-PROMO                   
                                       ET005-UNITS-SOLD-CLEAR                   
                                       ET005-UNITS-SOLD-ALL.                    
                                                                                
           MOVE ZEROES             TO  ET005-ONHAND-QUANTITY.                   
                                                                                
           MOVE ZEROES             TO  ET005-UNIT-PRICE-RETAIL                  
                                       ET005-UNIT-PRICE-PROMO                   
                                       ET005-UNIT-PRICE-CLEAR                   
                                       ET005-UNIT-PRICE-ALL.                    
                                                                                
       EJECT                                                                    
       Z800-PRINT-DETAIL-LINE.                                                  
           MOVE 'Z800-PRINT-DETAIL-LINE' TO PV-PARAGRAPH-NAME.                  
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      *****************************************************************         
      *    PRINT A DETAIL LINE.                                                 
      *                                                                         
      *    PERFORMED BY B400-PROCESS-CNTL-TBL-ENTRIES.                          
      *****************************************************************         
                                                                                
           IF PC-LINE-CNTR = +0                                                 
               PERFORM Z850-PRINT-PAGE-HEADINGS                                 
           END-IF.                                                              
                                                                                
           IF PC-LINE-CNTR > PC-MAX-LINES                                       
               PERFORM Z850-PRINT-PAGE-HEADINGS                                 
           END-IF.                                                              
                                                                                
           MOVE PC-TOT-DPT-CL-INHSE-ITEMS TO RPT-DTL-TOT-NMBR-ITEMS.            
           MOVE PC-NON-EDI-CNTR           TO RPT-DTL-TOT-NON-EDI-ITEMS.         
           MOVE PC-EDI-CNTR               TO RPT-DTL-TOT-EDI-ITEMS.             
           MOVE PC-SALES-CNTR             TO RPT-DTL-EDI-ITEMS-SALES.           
           MOVE PC-NO-SALES-CNTR          TO RPT-DTL-EDI-ITEMS-NO-SALES.        
           MOVE PC-ONHANDS-CNTR           TO RPT-DTL-ITEMS-WITH-ONHANDS.        
           MOVE PC-NO-ONHANDS-CNTR        TO RPT-DTL-ITEMS-NO-ONHANDS.          
                                                                                
           WRITE EDI-REPORT-LINE                                                
               FROM RPT-DETAIL-LINE                                             
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           ADD +2 TO PC-LINE-CNTR.                                              
                                                                                
           INITIALIZE RPT-DETAIL-LINE.                                          
                                                                                
      *    ** CREATE SUMMARY LINE **                                            
           MOVE PV-DPT-CL-INHSE-ONHANDS                                         
             TO RPT-DPT-CL-INHSE-ONHANDS                                        
           MOVE PV-DPT-CL-INHSE-UNITS-SOLD                                      
             TO RPT-DPT-CL-INHSE-UNITS-SOLD                                     
                                                                                
           WRITE EDI-REPORT-LINE                                                
               FROM RPT-DETAIL-SUMMARY-LINE                                     
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           ADD +1 TO PC-LINE-CNTR.                                              
                                                                                
           MOVE ZEROES                                                          
             TO PV-DPT-CL-INHSE-ONHANDS                                         
                PV-DPT-CL-INHSE-UNITS-SOLD.                                     
                                                                                
       EJECT                                                                    
       Z850-PRINT-PAGE-HEADINGS.                                                
           MOVE 'Z850-PRINT-PAGE-HEADINGS' TO PV-PARAGRAPH-NAME.                
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      *****************************************************************         
      *    PRINT THE PAGE HEADINGS.                                             
      *                                                                         
      *    PERFORMED BY B150-SETUP-PAGE-HEADINGS.                               
      *    PERFORMED BY B400-PROCESS-CNTL-TBL-ENTRIES.                          
      *****************************************************************         
                                                                                
           ADD +1 TO PC-PAGE-CNTR.                                              
                                                                                
           MOVE PC-PAGE-CNTR TO DP132O-PAGE-NUMBER.                             
                                                                                
           WRITE EDI-REPORT-LINE                                                
               FROM DP132O-STANDRD-HEADER-132-COLS                              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE EDI-REPORT-LINE                                                
               FROM RPT-TITLE                                                   
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           WRITE EDI-REPORT-LINE                                                
               FROM RPT-DETAIL-HEADING-1                                        
               AFTER ADVANCING 2 LINES.                                         
           WRITE EDI-REPORT-LINE                                                
               FROM RPT-DETAIL-HEADING-2                                        
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           MOVE SPACES TO EDI-REPORT-LINE.                                      
           WRITE EDI-REPORT-LINE                                                
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE +7 TO PC-LINE-CNTR.                                             
                                                                                
       EJECT                                                                    
       Z999-SQL-ABEND.                                                          
           MOVE 'Z999-SQL-ABEND' TO PV-PARAGRAPH-NAME.                          
      *    DISPLAY PV-PARAGRAPH-NAME.                                           
      *****************************************************************         
      *    ABEND ON AN SQL ERROR OR WARNING                                     
      *****************************************************************         
                                                                                
           DISPLAY '  '.                                                        
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'ETKUT003 - ABEND'.                                          
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
                                                                                
           COPY DPPD004.                                                        
                                                                                
      *    EXEC SQL                                                             
      *        COMMIT                                                           
      *    END-EXEC.                                                            
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       EJECT                                                                    
