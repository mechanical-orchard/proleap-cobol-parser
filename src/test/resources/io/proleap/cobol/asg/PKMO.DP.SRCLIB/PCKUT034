000100 IDENTIFICATION DIVISION.                                                 
000200 TITLE 'PREPARE CHECKPOINT RESTART EXTRACT FOR IN-TRANSIT PURGE'.         
000300                                                                          
000400 PROGRAM-ID.         PCKUT034.                                            
000500 AUTHOR.             DENNIS STEFFEN.                                      
000600 INSTALLATION.       KOHLS DEPARTMENT STORES.                             
000700 DATE-WRITTEN.       AUTUMN 2004.                                         
000800 DATE-COMPILED.                                                           
000900************************************************************              
001000* THIS PROGRAM PREPS THE INTRANSIT FILE SO THAT IT CAN USE                
001100* CHECKPOINT/RESTART PROCESSING FUNCTIONS WHEN UPDATING THE               
001200* INTRANSIT TABLES IN PROGRAM PCKUP010.                                   
001300************************************************************              
001400 ENVIRONMENT DIVISION.                                                    
001500 CONFIGURATION SECTION.                                                   
001600                                                                          
001700 SOURCE-COMPUTER.    IBM-3090.                                            
001800 OBJECT-COMPUTER.    IBM-3090.                                            
001900                                                                          
002000 INPUT-OUTPUT SECTION.                                                    
002100 FILE-CONTROL.                                                            
002200                                                                          
002300     SELECT IN-TRANSIT-FILE              ASSIGN TO INP01.                 
002400                                                                          
002500 DATA DIVISION.                                                           
002600 FILE SECTION.                                                            
002700                                                                          
002800 FD  IN-TRANSIT-FILE                                                      
002900     LABEL RECORDS ARE STANDARD                                           
003000     RECORDING MODE IS F                                                  
003100     BLOCK CONTAINS 0 RECORDS                                             
003200     RECORD CONTAINS 022 CHARACTERS                                       
003300     DATA RECORD IS IN-TRANSIT-RECORD.                                    
003400 01  IN-TRANSIT-RECORD    PIC  X(022).                                    
003500                                                                          
003600*                                                                         
003700 WORKING-STORAGE SECTION.                                                 
003800                                                                          
003900 01  ABEND-CODE                  PIC S9(04)     VALUE +0  COMP.           
004000                                                                          
004100 01  PROGRAM-CONSTANTS.                                                   
004200     05  PC-PROGRAM-NAME         PIC X(08)      VALUE 'PCKUT034'.         
004300     05  PC-TRAN-PREP-FUNC-CODES.                                         
004400         10  PC-TRAN-PREP-FUNC-OPEN                                       
004500                                 PIC X(05)      VALUE 'OPEN '.            
004600         10  PC-TRAN-PREP-FUNC-WRITE                                      
004700                                 PIC X(05)      VALUE 'WRITE'.            
004800         10  PC-TRAN-PREP-FUNC-CLOSE                                      
004900                                 PIC X(05)      VALUE 'CLOSE'.            
005000     05  PC-TRANS-RECORD-LENGTH  PIC S9(04)     VALUE +100 COMP.          
005100     05  PC-JOB-NAME             PIC X(08)      VALUE 'PCK115  '.         
005200     05  PC-PLAN-NAME            PIC X(08)      VALUE 'PCKUP010'.         
005300                                                                          
005400 01  PROGRAM-VARIABLES.                                                   
005500     05  PV-CHECKPOINT-TYPE      PIC X(01)      VALUE SPACE.              
005600         88  CONTROL-BREAK                      VALUE 'C'.                
005700         88  LOGICAL-TRANSACTION                VALUE 'L'.                
005800         88  TIME-INTERVAL                      VALUE 'T'.                
005900         88  REQUESTED-BY-PROGRAM               VALUE 'R'.                
006000     05  PS-TRANS-EOF-SW     PIC X(01)          VALUE 'N'.                
006100         88  PS-TRANS-EOF                       VALUE 'Y'.                
006200     05  PV-SKIP-CLOSE-SW        PIC X(01)      VALUE 'N'.                
006300         88  SKIP-CLOSE                         VALUE 'Y'.                
006400     05  PV-CURRENT-DATE         PIC X(10).                               
006500                                                                          
006600*     IN-TRANSIT PURGE RECORD                                             
006700     COPY PCRD024.                                                        
006800*                                                                         
006900*     TRANSACTION PREPARATION MODULE LINKAGE SECTION.                     
007000*                                                                         
260107     COPY DPWS990.                                                        
007200                                                                          
007100     COPY DPLS000.                                                        
007200                                                                          
007300     EXEC SQL                                                             
007400         INCLUDE SQLCA                                                    
007500     END-EXEC.                                                            
007600     COPY SQLCA2.                                                         
007700                                                                          
007800 PROCEDURE DIVISION.                                                      
007900******************************************************************        
008000* 0000-MAINLINE-PROCESSING                                       *        
008100*      CONTROLS FLOW OF THE PROGRAM.                             *        
008200******************************************************************        
008300 0000-MAINLINE-PROCESSING.                                                
008400                                                                          
008500     PERFORM 1000-INITIALIZE.                                             
008600     PERFORM 2000-PROCESS UNTIL PS-TRANS-EOF                              
008700     PERFORM 9000-EOJ.                                                    
008800                                                                          
008900     GOBACK.                                                              
009000                                                                          
009100 1000-INITIALIZE.                                                         
009200                                                                          
009300     OPEN INPUT IN-TRANSIT-FILE.                                          
009400                                                                          
009500     ACCEPT PC-JOB-NAME FROM SYSIN.                                       
009600                                                                          
009700     EXEC SQL                                                             
009800          SET :PV-CURRENT-DATE = CURRENT DATE                             
009900     END-EXEC.                                                            
010000                                                                          
010100     PERFORM 8000-READ-ASN-TRANS-FILE.                                    
010200                                                                          
010300     IF PS-TRANS-EOF                                                      
010400        SET SKIP-CLOSE                TO TRUE                             
010500        MOVE +1                       TO RETURN-CODE                      
              DISPLAY ' PCKUT034 - TRANSACTION FILE IS EMPTY '                  
              DISPLAY ' PCKUT034 - PCKUP010 WILL BE SKIPPED  '                  
010600     ELSE                                                                 
010700        PERFORM 1100-ISSUE-OPEN-REQUEST                                   
010800     END-IF.                                                              
010900                                                                          
011000 1100-ISSUE-OPEN-REQUEST.                                                 
011100                                                                          
011200     MOVE PC-TRAN-PREP-FUNC-OPEN      TO DP000-FUNC-CODE.                 
011300     MOVE PC-JOB-NAME                 TO DP000-JOB-NAME.                  
011400     MOVE PC-PLAN-NAME                TO DP000-PLAN-NAME.                 
011500     PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
011600     MOVE DP000-CKPT-TYPE-CODE        TO PV-CHECKPOINT-TYPE.              
011700                                                                          
011800 2000-PROCESS.                                                            
011900     PERFORM 2100-MOVE-TRANS-DATA                                         
012000                                                                          
012100     PERFORM 8000-READ-ASN-TRANS-FILE.                                    
012200                                                                          
012300 2100-MOVE-TRANS-DATA.                                                    
012400                                                                          
012500     MOVE LENGTH OF PC024-PURGE-EXTRACT                                   
012600                                      TO DP000-TRANS-REC-LENGTH.          
012700                                                                          
012800     MOVE PC024-PURGE-EXTRACT         TO DP000-TRANS-REC.                 
012900                                                                          
013000     MOVE PC-TRAN-PREP-FUNC-WRITE     TO DP000-FUNC-CODE.                 
013100     MOVE PC-JOB-NAME                 TO DP000-JOB-NAME.                  
013200     MOVE PC-PLAN-NAME                TO DP000-PLAN-NAME.                 
013300                                                                          
013500     IF CONTROL-BREAK OR LOGICAL-TRANSACTION                              
013600         MOVE 'Y'                     TO DP000-CHECKPOINT-IND             
013700     ELSE                                                                 
013800         MOVE 'N'                     TO DP000-CHECKPOINT-IND             
013900     END-IF.                                                              
014000                                                                          
013400     PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
013500                                                                          
013600 8000-READ-ASN-TRANS-FILE.                                                
013700                                                                          
013800     READ IN-TRANSIT-FILE INTO PC024-PURGE-EXTRACT                        
013900         AT END                                                           
014000           SET PS-TRANS-EOF           TO TRUE                             
014100     END-READ.                                                            
014200                                                                          
014300 9000-EOJ.                                                                
014400     CLOSE IN-TRANSIT-FILE.                                               
014500                                                                          
014600     IF SKIP-CLOSE                                                        
014700        NEXT SENTENCE                                                     
014800     ELSE                                                                 
014900        PERFORM 9100-ISSUE-CLOSE-REQUEST                                  
015000     END-IF.                                                              
015100                                                                          
015200 9100-ISSUE-CLOSE-REQUEST.                                                
015300                                                                          
015400     MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.                     
015500     MOVE PC-JOB-NAME TO DP000-JOB-NAME.                                  
015600     MOVE PC-PLAN-NAME TO DP000-PLAN-NAME.                                
015700                                                                          
015800     PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
015900                                                                          
016000     COPY DPPD000.                                                        
016100*                                                                         
016200*     TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES.             
016300*                                                                         
