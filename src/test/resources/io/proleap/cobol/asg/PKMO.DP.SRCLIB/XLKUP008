000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    XLKUP008.                                                 
000300 AUTHOR.        PATRICK BURKE.                                            
000400 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000500 DATE-WRITTEN.  03/12/2003.                                               
000600 DATE-COMPILED.                                                           
000700******************************************************************        
000800* THIS PGM IS PART OF THE INTERFACING BETWEEN RMS PO & LEGACY PO.         
000900* MAINTAIN LEGACY TIMPORL TABLE FROM RMS PO SYSTEM.                       
001000******************************************************************        
001100* THIS DB2 PROGRAM IS CALLED TO PROCESS CERTAIN TYPES OF PO DATA          
001200* PASSED FROM RMS.  THE PROGRAM INSERTS, UPDATES OR DELETES ROWS          
001300* ON THE LEGACY TIMPORL TABLE.                                            
001400******************************************************************        
001500 ENVIRONMENT DIVISION.                                                    
001600 CONFIGURATION SECTION.                                                   
001700 SOURCE-COMPUTER.  IBM-3090.                                              
001800 OBJECT-COMPUTER.  IBM-3090.                                              
001900 DATA DIVISION.                                                           
002000                                                                          
002100 WORKING-STORAGE SECTION.                                                 
002200******************************************************************        
002300**PROGRAM SWITCHES                                                        
002400******************************************************************        
002500 01  PS-PROGRAM-SWITCHES.                                                 
002600     05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.                
002700         88  PS-PROCESS-CONTINUE                VALUE 'Y'.                
002800         88  PS-PROCESS-COMPLETE                VALUE 'N'.                
002900                                                                          
003000******************************************************************        
003100**PROGRAM CONSTANTS                                                       
003200******************************************************************        
003300 01  PC-PROGRAM-CONSTANTS.                                                
003400     05  PC-CURRENT-PROGRAM-NAME    PIC  X(08)  VALUE 'XLKUP008'.         
003500                                                                          
003600******************************************************************        
003700**SQL DATABASE KEYS                                                       
003800******************************************************************        
003900 01  DK-DATABASE-KEYS.                                                    
004000     05  DK-PO-NBR                   PIC S9(9)  COMP.                     
004100     05  DK-VER-NBR                  PIC S9(9)  COMP.                     
004200     05  DK-ENT-ID                   PIC S9(9)  COMP.                     
004300                                                                          
004400******************************************************************        
004500**PROGRAM VARIABLES                                                       
004600******************************************************************        
004700 01  PV-PROGRAM-VARIABLES.                                                
004800     05  PV-RETRY-COUNTER            PIC 9(01)  VALUE ZEROES.             
004900     05  PV-CLOSE-DATE.                                                   
005000         10  PV-CLOSE-DATE-CC        PIC X(02).                           
005100         10  PV-CLOSE-DATE-YY        PIC X(02).                           
005200         10  PV-CLOSE-DATE-MM        PIC X(02).                           
005300         10  PV-CLOSE-DATE-DD        PIC X(02).                           
005400         10  PV-CLOSE-DATE-HH        PIC X(02).                           
005500         10  PV-CLOSE-DATE-MI        PIC X(02).                           
005600         10  PV-CLOSE-DATE-SS        PIC X(02).                           
005700                                                                          
005800     05  PV-CURRENT-TMST             PIC X(26).                           
005900     05  PV-CURR-TMST-DATE           PIC X(10).                           
006000     05  PV-CURR-TMST-TIME           PIC X(08).                           
006100                                                                          
006200     05  PV-DATE-X08                 PIC X(08).                           
006300     05  FILLER REDEFINES PV-DATE-X08.                                    
006400         10  PV-X08-CC               PIC 9(02).                           
006500         10  PV-X08-YY               PIC 9(02).                           
006600         10  PV-X08-MM               PIC 9(02).                           
006700         10  PV-X08-DD               PIC 9(02).                           
006800                                                                          
006900     05  PV-DATE-X10                 PIC X(10).                           
007000     05  FILLER REDEFINES PV-DATE-X10.                                    
007100         10  PV-X10-CC               PIC 9(02).                           
007200         10  PV-X10-YY               PIC 9(02).                           
007300         10  PV-X10-H1               PIC X(01).                           
007400         10  PV-X10-MM               PIC 9(02).                           
007500         10  PV-X10-H2               PIC X(01).                           
007600         10  PV-X10-DD               PIC 9(02).                           
007700                                                                          
007800     05  PV-TIME-X16                 PIC X(16).                           
007900     05  FILLER REDEFINES PV-TIME-X16.                                    
008000         10  PV-X16-H1               PIC X(01).                           
008100         10  PV-X16-HH               PIC 9(02).                           
008200         10  PV-X16-P1               PIC X(01).                           
008300         10  PV-X16-MI               PIC 9(02).                           
008400         10  PV-X16-P2               PIC X(01).                           
008500         10  PV-X16-SS               PIC 9(02).                           
008600         10  PV-X16-P3               PIC X(01).                           
008700         10  PV-X16-999999           PIC 9(06).                           
008800                                                                          
008900     05  PV-TMST-X26.                                                     
009000         10  PV-X26-DATE             PIC X(10).                           
009100         10  PV-X26-TIME             PIC X(16).                           
009200                                                                          
009300******************************************************************        
009400**COPYBOOKS                                                               
009500******************************************************************        
009600***DB2 MESSAGE AREA                                                       
009700     COPY SQLCA2.                                                         
009800                                                                          
009900******************************************************************        
010000**TABLE INCLUSIONS                                                        
010100******************************************************************        
010200***DB2 COMMUNICATIONS AREA                                                
010300     EXEC SQL                                                             
010400          INCLUDE SQLCA                                                   
010500     END-EXEC.                                                            
010600                                                                          
010700* ------------------------------------------------------------- *         
010800*  SQL AND COBOL DECLARATIONS FOR IMPORT TABLE                            
010900* ------------------------------------------------------------- *         
011000     EXEC SQL                                                             
011100         INCLUDE TIMPORL                                                  
011200     END-EXEC.                                                            
011300                                                                          
011400* ------------------------------------------------------------- *         
011500*  SQL AND COBOL DECLARATIONS FOR PURCHASE TABLE                          
011600* ------------------------------------------------------------- *         
011700     EXEC SQL                                                             
011800         INCLUDE TPURCHS                                                  
011900     END-EXEC.                                                            
012000                                                                          
012100 LINKAGE SECTION.                                                         
012200 01  XLRD011-RECORD.                                                      
012300     COPY XLRD011.                                                        
012400******************************************************************        
012500**LINKAGE VARIABLES                                                       
012600******************************************************************        
012700 01  LV-LINKAGE-VARIABLES.                                                
012800     05  LV-XLRD011-RECORD               PIC X(1002).                     
012900     05  LV-RETURN-CODE                  PIC S9(04).                      
013000                                                                          
013100 PROCEDURE DIVISION USING XLRD011-RECORD.                                 
013200******************************************************************        
013300 0000-MAINLINE.                                                           
013400                                                                          
013500     PERFORM 1000-INITIALIZATION.                                         
013600                                                                          
013700     PERFORM 2000-PROCESS-MESSAGE                                         
013800         UNTIL PS-PROCESS-COMPLETE.                                       
013900                                                                          
014000     PERFORM 9999-WRAPUP.                                                 
014100                                                                          
014200     GOBACK.                                                              
014300*****************************************************************         
014400**INITIALIZATION AREA - ALL MOVES TO UPDATE/INSERT                        
014500*****************************************************************         
014600 1000-INITIALIZATION.                                                     
014700                                                                          
014800     IF XL011-DEBUG-YES                                                   
014900         DISPLAY 'XLKUP008'                                               
015000     END-IF                                                               
015100                                                                          
015200     SET PS-PROCESS-CONTINUE TO TRUE.                                     
015300                                                                          
015400     INITIALIZE XL011-RETURN-AREA                                         
015500     MOVE 'XLKUP008'                  TO XL011-ERROR-PROGRAM.             
015600                                                                          
015700     EXEC SQL                                                             
015800         SELECT                                                           
015900               CURRENT TIMESTAMP                                          
016000         ,     CHAR ( DATE (CURRENT TIMESTAMP),JIS)                       
016100         ,     TIME (CURRENT TIMESTAMP)                                   
016200         INTO                                                             
016300           :PV-CURRENT-TMST                                               
016400         , :PV-CURR-TMST-DATE                                             
016500         , :PV-CURR-TMST-TIME                                             
016600         FROM                                                             
016700             SYSIBM.SYSDUMMY1                                             
016800     END-EXEC.                                                            
016900                                                                          
017000     EVALUATE TRUE                                                        
017100       WHEN SQLCODE EQUAL +0                                              
017200           CONTINUE                                                       
017300       WHEN OTHER                                                         
017400           SET PS-PROCESS-COMPLETE TO TRUE                                
017500     END-EVALUATE.                                                        
017600                                                                          
017700     IF XL011-MSG-ID = 'HDR_ADD             '                             
017800        PERFORM 2050-CHECK-PO                                             
017900     END-IF.                                                              
018000                                                                          
018100*****************************************************************         
018200**SELECT PO TYPE FROM TPURCHS.                                            
018300*****************************************************************         
018400 1500-SELECT-PO-TYPE.                                                     
018500                                                                          
018600     MOVE '1500-SELECT-PO-TYPE'      TO XL011-ERROR-PARAGRAPH.            
018700     MOVE XL011-ORDER-NBR            TO DK-PO-NBR.                        
018800                                                                          
018900     EXEC SQL                                                             
019000          SELECT A.PO_TYP_CDE                                             
019100            INTO :PURCHS-PO-TYP-CDE                                       
019200            FROM TPURCHS A                                                
019300           WHERE A.PO_NBR         = :DK-PO-NBR                            
019400             AND A.VER_STATUS_CDE = 'A'                                   
019500     END-EXEC                                                             
019600                                                                          
019700     EVALUATE TRUE                                                        
019800       WHEN SQLCODE = 0                                                   
019900           CONTINUE                                                       
020000       WHEN OTHER                                                         
020100           SET XL011-SQL-ERROR       TO TRUE                              
020200           SET PS-PROCESS-COMPLETE   TO TRUE                              
020300     END-EVALUATE.                                                        
020400                                                                          
020500*****************************************************************         
020600**MAIN PROGRAM LOGIC - READS MESSAGE TYPE ON XLRD011-RECORD               
020700*****************************************************************         
020800 2000-PROCESS-MESSAGE.                                                    
020900                                                                          
021000     EVALUATE TRUE                                                        
021100         WHEN XL011-MSG-ID = 'HDR_ADD             '                       
021200             IF XL011-HA-PO-TYPE = 'IE' OR 'IM' OR 'IF'                   
021300                 PERFORM 2110-LOAD-INSERT-DATA                            
021400                 PERFORM 2100-INSERT-DATA                                 
021500             ELSE                                                         
021600                 SET PS-PROCESS-COMPLETE TO TRUE                          
021700             END-IF                                                       
021800         WHEN XL011-MSG-ID = 'HDR_IMPRT_ATT_CHANGE'                       
021900             PERFORM 1500-SELECT-PO-TYPE                                  
022000             IF PURCHS-PO-TYP-CDE  = 'IE' OR 'IM' OR 'IF'                 
022100                 PERFORM 2500-UPDATE-DATA                                 
022200             ELSE                                                         
022300                 SET PS-PROCESS-COMPLETE TO TRUE                          
022400             END-IF                                                       
022500         WHEN XL011-MSG-ID = 'HDR_DELETE          '                       
022600             PERFORM 2800-DELETE-DATA                                     
022700         WHEN OTHER                                                       
022800             SET PS-PROCESS-COMPLETE TO TRUE                              
022900     END-EVALUATE.                                                        
023000                                                                          
023100*****************************************************************         
023200**END PROCESSSING IF PO ALREADY EXISTS ON TIMPORL.                        
023300*****************************************************************         
023400 2050-CHECK-PO.                                                           
023500                                                                          
023600     MOVE XL011-ORDER-NBR            TO DK-PO-NBR.                        
023700                                                                          
023800     EXEC SQL                                                             
023900          SELECT A.PO_NBR                                                 
024000            INTO :IMPORL-PO-NBR                                           
024100            FROM TIMPORL A                                                
024200           WHERE A.PO_NBR         = :DK-PO-NBR                            
024300             AND A.VER_STATUS_CDE = 'A'                                   
024400     END-EXEC                                                             
024500                                                                          
024600     EVALUATE TRUE                                                        
024700        WHEN SQLCODE = 0                                                  
024800           SET PS-PROCESS-COMPLETE   TO TRUE                              
024900        WHEN OTHER                                                        
025000           CONTINUE                                                       
025100     END-EVALUATE.                                                        
025200                                                                          
025300*****************************************************************         
025400**INSERT ACTION TYPE                                                      
025500*****************************************************************         
025600 2100-INSERT-DATA.                                                        
025700                                                                          
025800         EXEC SQL                                                         
025900             INSERT INTO TIMPORL                                          
026000                   (PO_NBR                                                
026100                   ,VER_NBR                                               
026200                   ,VER_STATUS_CDE                                        
026300                   ,CONSLR_RLSE_CDE                                       
026400                   ,CONSLR_REQR_DTE                                       
026500                   ,CONSLR_ACTL_DTE                                       
026600                   ,CONSLR_ACTL_TM                                        
026700                   ,CSTM_BKR_RLSE_CDE                                     
026800                   ,CSTM_BKR_REQR_DTE                                     
026900                   ,CSTM_BKR_ACTL_DTE                                     
027000                   ,CSTM_BKR_ACTL_TM                                      
027100                   ,TKT_PRTR_RLSE_CDE                                     
027200                   ,TKT_PRTR_REQR_DTE                                     
027300                   ,TKT_PRTR_ACTL_DTE                                     
027400                   ,TKT_PRTR_ACTL_TM                                      
027500                   ,DCONSLR_RLSE_CDE                                      
027600                   ,DCONSLR_REQR_DTE                                      
027700                   ,DCONSLR_ACTL_DTE                                      
027800                   ,DCONSLR_ACTL_TM                                       
027900                   ,LAST_UPD_TMST                                         
028000                   ,LAST_UPD_BY_UID                                       
028000                   ,DCONSLR_ENT_ID)                                       
028100             VALUES (:IMPORL-PO-NBR                                       
028200                   , :IMPORL-VER-NBR                                      
028300                   , :IMPORL-VER-STATUS-CDE                               
028400                   , :IMPORL-CONSLR-RLSE-CDE                              
028500                   , :IMPORL-CONSLR-REQR-DTE                              
028600                   , :IMPORL-CONSLR-ACTL-DTE                              
028700                   , :IMPORL-CONSLR-ACTL-TM                               
028800                   , :IMPORL-CSTM-BKR-RLSE-CDE                            
028900                   , :IMPORL-CSTM-BKR-REQR-DTE                            
029000                   , :IMPORL-CSTM-BKR-ACTL-DTE                            
029100                   , :IMPORL-CSTM-BKR-ACTL-TM                             
029200                   , :IMPORL-TKT-PRTR-RLSE-CDE                            
029300                   , :IMPORL-TKT-PRTR-REQR-DTE                            
029400                   , :IMPORL-TKT-PRTR-ACTL-DTE                            
029500                   , :IMPORL-TKT-PRTR-ACTL-TM                             
029600                   , :IMPORL-DCONSLR-RLSE-CDE                             
029700                   , :IMPORL-DCONSLR-REQR-DTE                             
029800                   , :IMPORL-DCONSLR-ACTL-DTE                             
029900                   , :IMPORL-DCONSLR-ACTL-TM                              
030000                   , :IMPORL-LAST-UPD-TMST                                
030100                   , :IMPORL-LAST-UPD-BY-UID                              
030100                   , :IMPORL-DCONSLR-ENT-ID)                              
030200         END-EXEC                                                         
030300                                                                          
030400     MOVE SQLCODE                     TO XL011-SQLCODE                    
030500     EVALUATE TRUE                                                        
030600        WHEN SQLCODE = 0                                                  
030700             SET PS-PROCESS-COMPLETE   TO TRUE                            
030800        WHEN OTHER                                                        
030900             SET PS-PROCESS-COMPLETE   TO TRUE                            
031000             SET XL011-SQL-ERROR      TO TRUE                             
031100     END-EVALUATE.                                                        
031200                                                                          
031300******************************************************************        
031400 2110-LOAD-INSERT-DATA.                                                   
031500                                                                          
031600     MOVE XL011-ORDER-NBR            TO IMPORL-PO-NBR.                    
031700     MOVE 1                          TO IMPORL-VER-NBR.                   
031800     MOVE 'A'                        TO IMPORL-VER-STATUS-CDE.            
031900                                                                          
032000     IF XL011-HA-CONSLR-RLSE-CODE = 'D'                                   
032100         MOVE 'N'                   TO IMPORL-CONSLR-RLSE-CDE             
032200     ELSE                                                                 
032300         MOVE XL011-HA-CONSLR-RLSE-CODE                                   
032400                                     TO IMPORL-CONSLR-RLSE-CDE            
032500     END-IF.                                                              
032600                                                                          
032700     IF XL011-HA-CONSLR-REQR-DATE  > SPACES                               
032800         MOVE XL011-HA-CONSLR-REQR-DATE  TO PV-DATE-X08                   
032900         MOVE PV-X08-CC              TO PV-X10-CC                         
033000         MOVE PV-X08-YY              TO PV-X10-YY                         
033100         MOVE PV-X08-MM              TO PV-X10-MM                         
033200         MOVE PV-X08-DD              TO PV-X10-DD                         
033300         MOVE '-'                    TO PV-X10-H1                         
033400                                        PV-X10-H2                         
033500         MOVE PV-DATE-X10            TO IMPORL-CONSLR-REQR-DTE            
033600     ELSE                                                                 
033700         MOVE '9999-09-09'           TO IMPORL-CONSLR-REQR-DTE            
033800     END-IF.                                                              
033900                                                                          
034000     IF XL011-HA-CONSLR-ACT-DATE  > SPACES                                
034100         MOVE XL011-HA-CONSLR-ACT-DATE TO PV-DATE-X08                     
034200         MOVE PV-X08-CC              TO PV-X10-CC                         
034300         MOVE PV-X08-YY              TO PV-X10-YY                         
034400         MOVE PV-X08-MM              TO PV-X10-MM                         
034500         MOVE PV-X08-DD              TO PV-X10-DD                         
034600         MOVE '-'                    TO PV-X10-H1                         
034700                                        PV-X10-H2                         
034800         MOVE PV-DATE-X10            TO IMPORL-CONSLR-ACTL-DTE            
034900     ELSE                                                                 
035000         MOVE '9999-09-09'           TO IMPORL-CONSLR-ACTL-DTE            
035100     END-IF.                                                              
035200                                                                          
035300     IF XL011-HA-CONSLR-ACT-DATE       > SPACES                           
035400*        MOVE PV-CURR-TMST-TIME      TO PV-TIME-X16                       
035500*        MOVE PV-TIME-X16            TO IMPORL-CONSLR-ACTL-TM             
035600         MOVE PV-CURR-TMST-TIME      TO IMPORL-CONSLR-ACTL-TM             
035700     ELSE                                                                 
035800         MOVE '09.09.09'             TO IMPORL-CONSLR-ACTL-TM             
035900     END-IF.                                                              
036000                                                                          
036100     MOVE  'N'                       TO IMPORL-CSTM-BKR-RLSE-CDE.         
036200     MOVE  '9999-09-09'              TO IMPORL-CSTM-BKR-REQR-DTE.         
036300     MOVE  '9999-09-09'              TO IMPORL-CSTM-BKR-ACTL-DTE.         
036400     MOVE  '09.09.09'                TO IMPORL-CSTM-BKR-ACTL-TM.          
036500                                                                          
036600     IF XL011-HA-TKT-MKR-RLSE-CODE   = 'D'                                
036700         MOVE 'N'                   TO IMPORL-TKT-PRTR-RLSE-CDE           
036800     ELSE                                                                 
036900         MOVE XL011-HA-TKT-MKR-RLSE-CODE                                  
037000                                     TO IMPORL-TKT-PRTR-RLSE-CDE          
037100     END-IF.                                                              
037200                                                                          
037300     IF XL011-HA-TKT-MKR-REQ-DATE      > SPACES                           
037400         MOVE XL011-HA-TKT-MKR-REQ-DATE TO PV-DATE-X08                    
037500         MOVE PV-X08-CC              TO PV-X10-CC                         
037600         MOVE PV-X08-YY              TO PV-X10-YY                         
037700         MOVE PV-X08-MM              TO PV-X10-MM                         
037800         MOVE PV-X08-DD              TO PV-X10-DD                         
037900         MOVE '-'                    TO PV-X10-H1                         
038000                                        PV-X10-H2                         
038100         MOVE PV-DATE-X10            TO IMPORL-TKT-PRTR-REQR-DTE          
038200     ELSE                                                                 
038300         MOVE '9999-09-09'           TO IMPORL-TKT-PRTR-REQR-DTE          
038400     END-IF.                                                              
038500                                                                          
038600     IF XL011-HA-TKT-MKR-ACT-DATE      > SPACES                           
038700         MOVE XL011-HA-TKT-MKR-ACT-DATE TO PV-DATE-X08                    
038800         MOVE PV-X08-CC              TO PV-X10-CC                         
038900         MOVE PV-X08-YY              TO PV-X10-YY                         
039000         MOVE PV-X08-MM              TO PV-X10-MM                         
039100         MOVE PV-X08-DD              TO PV-X10-DD                         
039200         MOVE '-'                    TO PV-X10-H1                         
039300                                        PV-X10-H2                         
039400         MOVE PV-DATE-X10            TO IMPORL-TKT-PRTR-ACTL-DTE          
039500     ELSE                                                                 
039600         MOVE '9999-09-09'           TO IMPORL-TKT-PRTR-ACTL-DTE          
039700     END-IF.                                                              
039800                                                                          
039900     IF XL011-HA-TKT-MKR-ACT-DATE      > SPACES                           
040000*        MOVE PV-CURR-TMST-TIME      TO PV-TIME-X16                       
040100*        MOVE PV-TIME-X16            TO IMPORL-TKT-PRTR-ACTL-TM           
040200         MOVE PV-CURR-TMST-TIME      TO IMPORL-TKT-PRTR-ACTL-TM           
040300     ELSE                                                                 
040400         MOVE '09.09.09'             TO IMPORL-TKT-PRTR-ACTL-TM           
040500     END-IF.                                                              
040600                                                                          
040700     MOVE 'N'                        TO IMPORL-DCONSLR-RLSE-CDE           
040800     MOVE '9999-09-09'               TO IMPORL-DCONSLR-REQR-DTE           
040900     MOVE '9999-09-09'               TO IMPORL-DCONSLR-ACTL-DTE           
041000     MOVE '09.09.09'                 TO IMPORL-DCONSLR-ACTL-TM.           
041000     MOVE ZERO                       TO IMPORL-DCONSLR-ENT-ID.            
041100                                                                          
041200     MOVE PV-CURRENT-TMST            TO IMPORL-LAST-UPD-TMST.             
041300     MOVE PC-CURRENT-PROGRAM-NAME    TO IMPORL-LAST-UPD-BY-UID.           
041400                                                                          
041500******************************************************************        
041600**UPDATE ACTION TYPE - "HDR_IMPRT_ATT_CHANGE"                             
041700******************************************************************        
041800 2500-UPDATE-DATA.                                                        
041900                                                                          
042000*SET TABLE DB2 KEYS.                                                      
042100     MOVE XL011-ORDER-NBR           TO IMPORL-PO-NBR.                     
042200     PERFORM 5000-VERSION-TIMPORL                                         
042300     IF PS-PROCESS-CONTINUE                                               
042400         PERFORM 2510-HDR-IMPRT-ATT-CHANGE                                
042500     END-IF.                                                              
042600                                                                          
042700 2510-HDR-IMPRT-ATT-CHANGE.                                               
042800                                                                          
042900     MOVE '2510-HDR-IMPRT-ATT-CHANGE' TO XL011-ERROR-PARAGRAPH            
043000                                                                          
043100*SET FIELDS BEING UPDATED.                                                
043200     IF XL011-IAC-CONSLR-ACT-DATE   > SPACES                              
043300         MOVE XL011-IAC-CONSLR-ACT-DATE TO PV-DATE-X08                    
043400         MOVE PV-X08-CC              TO PV-X10-CC                         
043500         MOVE PV-X08-YY              TO PV-X10-YY                         
043600         MOVE PV-X08-MM              TO PV-X10-MM                         
043700         MOVE PV-X08-DD              TO PV-X10-DD                         
043800         MOVE '-'                    TO PV-X10-H1                         
043900                                        PV-X10-H2                         
044000         MOVE PV-DATE-X10            TO IMPORL-CONSLR-ACTL-DTE            
044100     ELSE                                                                 
044200         MOVE '9999-09-09'           TO IMPORL-CONSLR-ACTL-DTE            
044300     END-IF.                                                              
044400     IF XL011-IAC-CONSLR-ACT-DATE       > SPACES                          
044500*        MOVE PV-CURR-TMST-TIME      TO PV-TIME-X16                       
044600*        MOVE PV-TIME-X16            TO IMPORL-CONSLR-ACTL-TM             
044700         MOVE PV-CURR-TMST-TIME      TO IMPORL-CONSLR-ACTL-TM             
044800     ELSE                                                                 
044900         MOVE '09.09.09'             TO IMPORL-CONSLR-ACTL-TM             
045000     END-IF.                                                              
045100                                                                          
045200     IF XL011-IAC-CONSLR-REQR-DATE  > SPACES                              
045300         MOVE XL011-IAC-CONSLR-REQR-DATE  TO PV-DATE-X08                  
045400         MOVE PV-X08-CC              TO PV-X10-CC                         
045500         MOVE PV-X08-YY              TO PV-X10-YY                         
045600         MOVE PV-X08-MM              TO PV-X10-MM                         
045700         MOVE PV-X08-DD              TO PV-X10-DD                         
045800         MOVE '-'                    TO PV-X10-H1                         
045900                                        PV-X10-H2                         
046000         MOVE PV-DATE-X10            TO IMPORL-CONSLR-REQR-DTE            
046100     ELSE                                                                 
046200         MOVE '9999-09-09'           TO IMPORL-CONSLR-REQR-DTE            
046300     END-IF.                                                              
046400     IF XL011-IAC-TKT-MKR-ACT-DATE  > SPACES                              
046500         MOVE XL011-IAC-TKT-MKR-ACT-DATE TO PV-DATE-X08                   
046600         MOVE PV-X08-CC              TO PV-X10-CC                         
046700         MOVE PV-X08-YY              TO PV-X10-YY                         
046800         MOVE PV-X08-MM              TO PV-X10-MM                         
046900         MOVE PV-X08-DD              TO PV-X10-DD                         
047000         MOVE '-'                    TO PV-X10-H1                         
047100                                        PV-X10-H2                         
047200         MOVE PV-DATE-X10            TO IMPORL-TKT-PRTR-ACTL-DTE          
047300     ELSE                                                                 
047400         MOVE '9999-09-09'           TO IMPORL-TKT-PRTR-ACTL-DTE          
047500     END-IF.                                                              
047600     IF XL011-IAC-TKT-MKR-ACT-DATE   > SPACES                             
047700*        MOVE PV-CURR-TMST-TIME      TO PV-TIME-X16                       
047800*        MOVE PV-TIME-X16            TO IMPORL-TKT-PRTR-ACTL-TM           
047900         MOVE PV-CURR-TMST-TIME    TO IMPORL-TKT-PRTR-ACTL-TM             
048000     ELSE                                                                 
048100         MOVE '09.09.09'             TO IMPORL-TKT-PRTR-ACTL-TM           
048200     END-IF.                                                              
048300                                                                          
048400     IF XL011-IAC-TKT-MKR-REQ-DATE  > SPACES                              
048500         MOVE XL011-IAC-TKT-MKR-REQ-DATE TO PV-DATE-X08                   
048600         MOVE PV-X08-CC              TO PV-X10-CC                         
048700         MOVE PV-X08-YY              TO PV-X10-YY                         
048800         MOVE PV-X08-MM              TO PV-X10-MM                         
048900         MOVE PV-X08-DD              TO PV-X10-DD                         
049000         MOVE '-'                    TO PV-X10-H1                         
049100                                        PV-X10-H2                         
049200         MOVE PV-DATE-X10            TO IMPORL-TKT-PRTR-REQR-DTE          
049300     ELSE                                                                 
049400         MOVE '9999-09-09'           TO IMPORL-TKT-PRTR-REQR-DTE          
049500     END-IF.                                                              
049600                                                                          
049700     IF XL011-IAC-CONSLR-RLSE-CODE = 'D'                                  
049800         MOVE 'N'                   TO IMPORL-CONSLR-RLSE-CDE             
049900     ELSE                                                                 
050000         MOVE XL011-IAC-CONSLR-RLSE-CODE                                  
050100                                     TO IMPORL-CONSLR-RLSE-CDE            
050200     END-IF.                                                              
050300                                                                          
050400     IF XL011-IAC-TKT-MKR-RLSE-CODE   = 'D'                               
050500         MOVE 'N'                   TO IMPORL-TKT-PRTR-RLSE-CDE           
050600     ELSE                                                                 
050700         MOVE XL011-IAC-TKT-MKR-RLSE-CODE                                 
050800                                    TO IMPORL-TKT-PRTR-RLSE-CDE           
050900     END-IF.                                                              
051000                                                                          
051100     MOVE PV-CURRENT-TMST            TO IMPORL-LAST-UPD-TMST              
051200     MOVE PC-CURRENT-PROGRAM-NAME    TO IMPORL-LAST-UPD-BY-UID            
051300     ADD +1                          TO IMPORL-VER-NBR.                   
051400     MOVE 'A'                        TO IMPORL-VER-STATUS-CDE.            
051500                                                                          
051600                                                                          
051700     PERFORM 2100-INSERT-DATA.                                            
051800                                                                          
051900******************************************************************        
052000**DELETE TYPE ACTION    HDR_DELETE                                        
052100******************************************************************        
052200 2800-DELETE-DATA.                                                        
052300                                                                          
052400     MOVE XL011-ORDER-NBR        TO DK-PO-NBR.                            
052500                                                                          
052600                                                                          
052700         EXEC SQL                                                         
052800             DELETE FROM TIMPORL                                          
052900              WHERE PO_NBR   = :DK-PO-NBR                                 
053000         END-EXEC                                                         
053100                                                                          
053200     MOVE SQLCODE                     TO XL011-SQLCODE.                   
053300                                                                          
053400       EVALUATE TRUE                                                      
053500           WHEN SQLCODE = 0                                               
053600               SET PS-PROCESS-COMPLETE TO TRUE                            
053700           WHEN OTHER                                                     
053800               SET PS-PROCESS-COMPLETE TO TRUE                            
053900        END-EVALUATE.                                                     
054000                                                                          
054100******************************************************************        
054200**VERSIONING FOR TIMPORL.                                                 
054300******************************************************************        
054400 5000-VERSION-TIMPORL.                                                    
054500                                                                          
054600     INITIALIZE DCLTIMPORL.                                               
054700     MOVE '5000-VERSION-TIMPORL'      TO XL011-ERROR-PARAGRAPH.           
054800     MOVE XL011-ORDER-NBR             TO DK-PO-NBR.                       
054900                                                                          
055000         EXEC SQL                                                         
055100             SELECT PO_NBR                                                
055200                   ,VER_NBR                                               
055300                   ,VER_STATUS_CDE                                        
055400                   ,CONSLR_RLSE_CDE                                       
055500                   ,CONSLR_REQR_DTE                                       
055600                   ,CONSLR_ACTL_DTE                                       
055700                   ,CONSLR_ACTL_TM                                        
055800                   ,CSTM_BKR_RLSE_CDE                                     
055900                   ,CSTM_BKR_REQR_DTE                                     
056000                   ,CSTM_BKR_ACTL_DTE                                     
056100                   ,CSTM_BKR_ACTL_TM                                      
056200                   ,TKT_PRTR_RLSE_CDE                                     
056300                   ,TKT_PRTR_REQR_DTE                                     
056400                   ,TKT_PRTR_ACTL_DTE                                     
056500                   ,TKT_PRTR_ACTL_TM                                      
056600                   ,DCONSLR_RLSE_CDE                                      
056700                   ,DCONSLR_REQR_DTE                                      
056800                   ,DCONSLR_ACTL_DTE                                      
056900                   ,DCONSLR_ACTL_TM                                       
057000                   ,LAST_UPD_TMST                                         
057100                   ,LAST_UPD_BY_UID                                       
                         ,DCONSLR_ENT_ID                                        
057200               INTO  :IMPORL-PO-NBR                                       
057300                   , :IMPORL-VER-NBR                                      
057400                   , :IMPORL-VER-STATUS-CDE                               
057500                   , :IMPORL-CONSLR-RLSE-CDE                              
057600                   , :IMPORL-CONSLR-REQR-DTE                              
057700                   , :IMPORL-CONSLR-ACTL-DTE                              
057800                   , :IMPORL-CONSLR-ACTL-TM                               
057900                   , :IMPORL-CSTM-BKR-RLSE-CDE                            
058000                   , :IMPORL-CSTM-BKR-REQR-DTE                            
058100                   , :IMPORL-CSTM-BKR-ACTL-DTE                            
058200                   , :IMPORL-CSTM-BKR-ACTL-TM                             
058300                   , :IMPORL-TKT-PRTR-RLSE-CDE                            
058400                   , :IMPORL-TKT-PRTR-REQR-DTE                            
058500                   , :IMPORL-TKT-PRTR-ACTL-DTE                            
058600                   , :IMPORL-TKT-PRTR-ACTL-TM                             
058700                   , :IMPORL-DCONSLR-RLSE-CDE                             
058800                   , :IMPORL-DCONSLR-REQR-DTE                             
058900                   , :IMPORL-DCONSLR-ACTL-DTE                             
059000                   , :IMPORL-DCONSLR-ACTL-TM                              
059100                   , :IMPORL-LAST-UPD-TMST                                
059200                   , :IMPORL-LAST-UPD-BY-UID                              
059200                   , :IMPORL-DCONSLR-ENT-ID                               
059300          FROM TIMPORL                                                    
059400             WHERE PO_NBR         = :DK-PO-NBR                            
059500               AND VER_STATUS_CDE = 'A'                                   
059600         END-EXEC                                                         
059700                                                                          
059800*CHECK SQL RETURN CODE FOR SUCCESS.                                       
059900         EVALUATE TRUE                                                    
060000           WHEN SQLCODE = 0                                               
060100               PERFORM 5010-UPDATE-VER-STATUS-CDE                         
060200           WHEN OTHER                                                     
060300               SET PS-PROCESS-COMPLETE TO TRUE                            
060400               SET XL011-SQL-ERROR     TO TRUE                            
060500         END-EVALUATE.                                                    
060600                                                                          
060700     MOVE SQLCODE                     TO XL011-SQLCODE.                   
060800                                                                          
060900*****************************************************************         
061000**VERSIONING FOR TIMPORL.                                                 
061100*****************************************************************         
061200 5010-UPDATE-VER-STATUS-CDE.                                              
061300                                                                          
061400     EXEC SQL                                                             
061500         UPDATE TIMPORL                                                   
061600             SET VER_STATUS_CDE = 'I'                                     
061700             WHERE PO_NBR         = :DK-PO-NBR                            
061800             AND VER_STATUS_CDE = 'A'                                     
061900     END-EXEC                                                             
062000                                                                          
062100     MOVE SQLCODE                   TO XL011-SQLCODE                      
062200     EVALUATE TRUE                                                        
062300         WHEN SQLCODE = 0                                                 
062400             CONTINUE                                                     
062500         WHEN OTHER                                                       
062600             SET PS-PROCESS-COMPLETE TO TRUE                              
062700             SET XL011-SQL-ERROR     TO TRUE                              
062800     END-EVALUATE.                                                        
062900                                                                          
063000******************************************************************        
063100**PASS SQLCODE TO CALLING AND EXIT PROGRAM                                
063200******************************************************************        
063300 9999-WRAPUP.                                                             
063400                                                                          
063500     MOVE SQLCODE                     TO XL011-SQLCODE.                   
063600     MOVE SQLCA                       TO XL011-SQLCA.                     
063700                                                                          
063800*    EXIT PROGRAM.                                                        
