       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    XSKUP016.                                         00020011
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  09/29/2002.                                       00050016
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070016
      **       VSM ITEM MESSAGE - NUMBER 089 - WEB ITEM DELETE          00080016
      ******************************************************************00090016
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO UPDATE THE FOLLOWING   00100023
      ***     ATTRIBUTES XST_SKU TO 'N':                                00110023
      ***     1. E_BUS_ITM_IND                                          00120023
      ******************************************************************00130016
      **09/29/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00140016
121402**12/14/2002 CHANGED BY:JULIE SMITH  ADDITIONAL TIMESTAMP         00150021
011403**01/14/2003 CHANGED BY:JULIE SMITH  FIXES                        00160024
      ******************************************************************00170016
       ENVIRONMENT DIVISION.                                            00180000
       CONFIGURATION SECTION.                                           00190000
       SOURCE-COMPUTER.  IBM-3090.                                      00200000
       OBJECT-COMPUTER.  IBM-3090.                                      00210000
       DATA DIVISION.                                                   00220000
                                                                        00230005
       WORKING-STORAGE SECTION.                                         00240000
      ******************************************************************00250005
      **PROGRAM SWITCHES                                                00260005
      ******************************************************************00270005
       01  PS-PROGRAM-SWITCHES.                                         00280005
           05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.        00290005
               88  PS-PROCESS-CONTINUE                VALUE 'Y'.        00300005
               88  PS-PROCESS-COMPLETE                VALUE 'N'.        00310005
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00320018
               88  PS-ACTION-REVERSE                  VALUE 01.         00330018
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00340018
               88  PS-ACTION-NOTHING                  VALUE 02.         00350018
      ******************************************************************00360000
      **PROGRAM COUNTERS                                                00370017
      ******************************************************************00380000
       01  PC-PROGRAM-COUNTERS.                                         00390007
           05  PC-RETRY-COUNTER            PIC 9(01)  VALUE ZEROS.      00400007
           05  PC-RETRY-MAX                PIC 9(01)  VALUE 5.          00410007
      ******************************************************************00420010
      **PROGRAM CALCULATIONS                                            00430010
      ******************************************************************00440010
       01  PC-PROGRAM-CALCULATIONS.                                     00450010
           05  PC-10-DIGIT                 PIC 9(10)  VALUE ZEROS.      00460010
           05  PC-18-DIGIT                 PIC S9(18) COMP VALUE ZEROS. 00470010
           05  PC-09-DIGIT                 PIC S9(09) COMP VALUE ZEROS. 00480010
      ******************************************************************00490000
      **PROGRAM VARIABLES                                               00500017
      ******************************************************************00510000
       01  PV-PROGRAM-VARIABLES.                                        00520017
           05  PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACE.      00530017
      ******************************************************************00540000
      **COPYBOOKS                                                       00550000
      ******************************************************************00560000
      ***VSM ITEM COPYBOOK                                              00570005
           COPY IMRD008.                                                00580025
      ***ABEND COPYBOOK                                                 00590005
           COPY DPWS004.                                                00600025
      ***DB2 MESSAGE AREA                                               00610000
           COPY SQLCA2.                                                 00620025
      ******************************************************************00630000
      **TABLE INCLUSIONS                                                00640000
      ******************************************************************00650000
      ***DB2 COMMUNICATIONS AREA                                        00660000
           EXEC SQL                                                     00670000
                INCLUDE SQLCA                                           00680000
           END-EXEC.                                                    00690000
      ***XST_SKU TABLE                                                  00700009
           EXEC SQL                                                     00710000
               INCLUDE XST00010                                         00720009
           END-EXEC.                                                    00730000
                                                                        00740000
       LINKAGE SECTION.                                                 00750000
      ******************************************************************00760000
      **LINKAGE VARIABLES                                               00770000
      ******************************************************************00780000
       01  LV-IMRD008-RECORD               PIC X(450).                  00790005
       01  LV-RETURN-CODE                  PIC S9(04).                  00800005
       01  LV-ACTION-IND                   PIC 9(02).                   00810018
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00820018
                                                                        00830000
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      00840005
                                LV-RETURN-CODE,                         00850018
                                LV-ACTION-IND,                          00860018
                                LV-CURRENT-TIMESTAMP.                   00870018
      ******************************************************************00880000
       0000-MAINLINE.                                                   00890000
                                                                        00900000
            PERFORM 1000-INITIALIZATION.                                00910000
            PERFORM 2000-PROCESS-MESSAGE                                00920016
                    UNTIL PS-PROCESS-COMPLETE.                          00930005
            PERFORM 9999-WRAPUP.                                        00940000
                                                                        00950000
       EJECT.                                                           00960005
      ***************************************************************** 00970000
      **MOVES ALL COPYBOOK FIELDS TO THE DCLGEN FIELDS                  00980007
      ***************************************************************** 00990000
       1000-INITIALIZATION.                                             01000000
                                                                        01010000
           MOVE '1000-INITIALIZATION'    TO PV-PARAGRAPH-NAME.          01020017
                                                                        01030000
           INITIALIZE PS-PROCESS-CONTINUE-SW.                           01040008
           INITIALIZE PC-RETRY-COUNTER.                                 01050008
                                                                        01060008
           MOVE LV-IMRD008-RECORD          TO IMRD008-RECORD.           01070009
                                                                        01080016
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     01090010
      *     COMP FIELDS.                                                01100010
           MOVE IM008-ITEM-KEY-INTR-UPC-ID TO PC-10-DIGIT.              01110010
           MOVE PC-10-DIGIT                TO PC-18-DIGIT.              01120010
           MOVE PC-18-DIGIT                TO PC-09-DIGIT.              01130010
           MOVE PC-09-DIGIT                TO XST00010-INTR-UPC-ID.     01140010
                                                                        01150016
      *FIELD IS MAINTAINED BY THE XS DOMAIN                             01160009
121402     MOVE LV-CURRENT-TIMESTAMP       TO XST00010-LAST-UPD-TMST.   01170021
121402     MOVE IM008-CRE-TMST         TO XST00010-SOR-LAST-MSG-TMST.   01180021
                                                                        01190000
       EJECT.                                                           01200005
      ***************************************************************** 01210000
      **UPDATES E BUSINESS INDICATOR TO N ON XST_SKU                    01220023
      ***************************************************************** 01230000
       2000-PROCESS-MESSAGE.                                            01240016
                                                                        01250000
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01260017
                                                                        01270005
           EXEC SQL                                                     01280005
              UPDATE XST_SKU                                            01290009
                  SET   E_BUS_ITM_IND     = 'N'                         01300024
011403                , E_BUS_PRD_GP_NBR  = 0                           01310024
                      , LAST_UPD_TMST     = :XST00010-LAST-UPD-TMST     01320024
121402                , SOR_LAST_MSG_TMST = :XST00010-SOR-LAST-MSG-TMST 01330021
                  WHERE INTR_UPC_ID       = :XST00010-INTR-UPC-ID       01340024
           END-EXEC.                                                    01350005
                                                                        01360005
           EVALUATE TRUE                                                01370013
      ***SUCCESSFUL PROCESSING                                          01380023
              WHEN SQLCODE = ZERO                                       01390005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01400008
      ***ROW/TABLE LOCK                                                 01410023
              WHEN SQLCODE = -911                                       01420005
              WHEN SQLCODE = -913                                       01430007
                   ADD +1                      TO PC-RETRY-COUNTER      01440007
                   IF  PC-RETRY-COUNTER = PC-RETRY-MAX                  01450007
                       SET PS-PROCESS-COMPLETE TO TRUE                  01460007
                   ELSE                                                 01470007
                       ADD +1                  TO PC-RETRY-COUNTER      01480007
                   END-IF                                               01490007
              WHEN OTHER                                                01500005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01510005
           END-EVALUATE.                                                01520005
                                                                        01530000
       EJECT.                                                           01540005
      ******************************************************************01550000
      *MOVE RETURN CODE TO LINKAGE VARIABLE AND EXIT MODULE             01560000
      ******************************************************************01570000
       9999-WRAPUP.                                                     01580000
                                                                        01590000
           MOVE '9999-WRAPUP'         TO PV-PARAGRAPH-NAME              01600020
                                                                        01610000
           MOVE SQLCODE               TO LV-RETURN-CODE.                01620020
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 01630018
                                                                        01640018
           EXIT PROGRAM.                                                01650000
                                                                        01660000
       EJECT.                                                           01670005
