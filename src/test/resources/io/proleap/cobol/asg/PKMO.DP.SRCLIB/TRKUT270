000100*************************                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*************************                                                 
000400                                                                          
000500 PROGRAM-ID.      TRKUT270.                                               
000600 AUTHOR.          MARGARET MORRISON.                                      
000700 DATE-WRITTEN.    09/04.                                                  
000800                                                                          
000900******************************************************************        
001000* THIS PROGRAM WILL CREATE SEVERAL FILES OF LEGACY TRANSPORTATION         
001100* DATA.                                                                   
002400******************************************************************        
002500* CHANGE LOG                                                              
002500* DATE       CHANGED BY    DESC                                           
002500* ---------- ------------- --------------------------------------         
BARB  * 01/15/2018 BARB SCHULTZ  ADDED PO TYPE                                  
BARB  *                          'LE' - ECOMM LANDED                            
002400******************************************************************        
002500                                                                          
002600 ENVIRONMENT DIVISION.                                                    
002700 CONFIGURATION SECTION.                                                   
002800                                                                          
002900 SOURCE-COMPUTER. IBM-3090.                                               
003000 OBJECT-COMPUTER. IBM-3090.                                               
003100                                                                          
003200 INPUT-OUTPUT SECTION.                                                    
003300 FILE-CONTROL.                                                            
003400                                                                          
003500     SELECT PO-TABLE                                                      
003600         ASSIGN                  TO OUT01.                                
003700     SELECT FOB-TABLE                                                     
003800         ASSIGN                  TO OUT02.                                
004300                                                                          
004800***************                                                           
004900 DATA DIVISION.                                                           
005000***************                                                           
005100 FILE SECTION.                                                            
005200                                                                          
007502 FD  PO-TABLE                                                             
007503     RECORDING MODE F                                                     
007504     BLOCK CONTAINS 0 RECORDS                                             
007505     RECORD CONTAINS 59 CHARACTERS                                        
007506     LABEL RECORDS ARE STANDARD                                           
007507     DATA RECORD IS PO-TABLE-RECORD.                                      
007508 01  PO-TABLE-RECORD                     PIC X(59).                       
007509                                                                          
007510 FD  FOB-TABLE                                                            
007511     RECORDING MODE F                                                     
007512     BLOCK CONTAINS 0 RECORDS                                             
007513     RECORD CONTAINS 60 CHARACTERS                                        
007514     LABEL RECORDS ARE STANDARD                                           
007515     DATA RECORD IS FOB-TABLE-RECORD.                                     
007516 01  FOB-TABLE-RECORD                    PIC X(60).                       
007517                                                                          
007614************************                                                  
007615 WORKING-STORAGE SECTION.                                                 
007616************************                                                  
007617                                                                          
013000 01  PS-PROGRAM-SWITCHES.                                                 
033700     05  PS-END-OF-PO-CURSOR-SW         PIC  X(01)    VALUE 'N'.          
033800         88  PS-NOT-END-OF-PO-CURSOR                  VALUE 'N'.          
033801         88  PS-END-OF-PO-CURSOR                      VALUE 'Y'.          
033802     05  PS-END-OF-FOB-CURSOR-SW        PIC  X(01)    VALUE 'N'.          
033803         88  PS-NOT-END-OF-FOB-CURSOR                 VALUE 'N'.          
033804         88  PS-END-OF-FOB-CURSOR                     VALUE 'Y'.          
033805     05  PS-VALID-ENTNME-SW             PIC  X(01)    VALUE 'N'.          
033806         88  PS-INVALID-ENTNME                        VALUE 'N'.          
033807         88  PS-VALID-ENTNME                          VALUE 'Y'.          
033841                                                                          
033842 01  PC-PROGRAM-CONSTANTS.                                                
033843     05  PC-PGM-NAME                   PIC X(08)  VALUE                   
033844                                                 'TRKUT270'.              
033845     05  PC-NEG-1                      PIC S9(03) VALUE -1                
033846                                                  COMP-3.                 
033847     05  PC-MAX-RETRIES                PIC S9(03) VALUE +5                
033848                                                  COMP-3.                 
034200 01  PV-PROGRAM-VARIABLES.                                                
034201     05  PV-PO-NBR                     PIC S9(09) VALUE +0                
034202                                                  COMP.                   
034300     05  PV-CURRENT-TMST               PIC X(26)  VALUE SPACES.           
034400     05  PV-CURRENT-DATE               PIC X(10)  VALUE SPACES.           
034410     05  PV-STRT-FCTRY-MIN             PIC X(10)  VALUE SPACES.           
034430     05  PV-RETRY-COUNTER              PIC S9(04) VALUE +0                
034440                                                  COMP SYNC.              
036003     05  PV-MOM-DPRTR-TMST-NULL        PIC S9(04) VALUE +0                
036004                                                  COMP SYNC.              
036005     05  PV-MOM-POE-DELY-TMST-NULL     PIC S9(04) VALUE +0                
036006                                                  COMP SYNC.              
036007     05  PV-PO-BKG-DELY-TMST-NULL      PIC S9(04) VALUE +0                
036008                                                  COMP SYNC.              
036009     05  PV-PO-RCVD-DELY-TMST-NULL     PIC S9(04) VALUE +0                
036010                                                  COMP SYNC.              
036020     05  PV-FILD-TMST-NULL             PIC S9(04) VALUE +0                
036030                                                  COMP SYNC.              
041000                                                                          
041100 01  PV-REFORMATED-DATE.                                                  
041200     05  PV-REFORMAT-MONTH             PIC X(02)  VALUE SPACES.           
041300     05  FILLER                        PIC X(01)  VALUE '/'.              
041400     05  PV-REFORMAT-DAY               PIC X(02)  VALUE SPACES.           
041500     05  FILLER                        PIC X(01)  VALUE '/'.              
041600     05  PV-REFORMAT-YEAR              PIC X(04)  VALUE SPACES.           
041601                                                                          
041609 01  ABEND-CODE                        PIC S9(04) VALUE ZEROS             
041610                                                  COMP SYNC.              
041611     88  PC-CONTROL-CARD-ERROR                    VALUE +4001.            
041612     88  PC-DB2-ERROR                             VALUE +4013.            
041613                                                                          
102301*---------------------------------------------------------------*         
102302*    DB2 AREA FOR TPURCHS                                                 
102303*---------------------------------------------------------------*         
102304     EXEC SQL                                                             
102305          INCLUDE TPURCHS                                                 
102306     END-EXEC.                                                            
102331*---------------------------------------------------------------*         
102332*    DB2 AREA FOR TPLNSHP                                       *         
102333*---------------------------------------------------------------*         
102334     EXEC SQL                                                             
102335          INCLUDE TPLNSHP                                                 
102336     END-EXEC.                                                            
102391*---------------------------------------------------------------*         
102392*    DB2 AREA FOR TIMCNPO                                       *         
102393*---------------------------------------------------------------*         
102394     EXEC SQL                                                             
102395          INCLUDE TIMCNPO                                                 
102396     END-EXEC.                                                            
102403*---------------------------------------------------------------*         
102404*    DB2 AREA FOR TIMCNTK                                                 
102405*---------------------------------------------------------------*         
102406     EXEC SQL                                                             
102407          INCLUDE TIMCNTK                                                 
102408     END-EXEC.                                                            
102415*---------------------------------------------------------------*         
102416*    DB2 AREA FOR TIMFOB                                        *         
102417*---------------------------------------------------------------*         
102418     EXEC SQL                                                             
102419          INCLUDE TIMFOB                                                  
102420     END-EXEC.                                                            
102481*---------------------------------------------------------------*         
102482*    DB2 AREA FOR TIMBKPO                                       *         
102483*---------------------------------------------------------------*         
102484     EXEC SQL                                                             
102485          INCLUDE TIMBKPO                                                 
102486     END-EXEC.                                                            
102487*---------------------------------------------------------------*         
102488*    DB2 AREA FOR TIMRCPO                                       *         
102489*---------------------------------------------------------------*         
102490     EXEC SQL                                                             
102491          INCLUDE TIMRCPO                                                 
102492     END-EXEC.                                                            
102493*---------------------------------------------------------------*         
102494*    DB2 AREA FOR TIMCSPO                                       *         
102495*---------------------------------------------------------------*         
102496     EXEC SQL                                                             
102497          INCLUDE TIMCSPO                                                 
102498     END-EXEC.                                                            
102499*---------------------------------------------------------------*         
102500*    DB2 AREA FOR TIMCSEN                                                 
102501*---------------------------------------------------------------*         
102502     EXEC SQL                                                             
102503          INCLUDE TIMCSEN                                                 
102504     END-EXEC.                                                            
102505*---------------------------------------------------------------*         
102506*    DB2 AREA FOR TENTNME                                                 
102507*---------------------------------------------------------------*         
102508     EXEC SQL                                                             
102509          INCLUDE TENTNME                                                 
102510     END-EXEC.                                                            
102520*---------------------------------------------------------------*         
102530*    DB2 AREA FOR TEXTENT                                                 
102540*---------------------------------------------------------------*         
102550     EXEC SQL                                                             
102560          INCLUDE TEXTENT                                                 
102570     END-EXEC.                                                            
117600*---------------------------------------------------------------*         
117601*  COPYBOOK FOR IMPORT PO DATES TABLE DATA.                      *        
117602*---------------------------------------------------------------*         
117603     COPY TRRD270.                                                        
117604*---------------------------------------------------------------*         
117605*  COPYBOOK FOR FOB TABLE DATA.                                  *        
117606*---------------------------------------------------------------*         
117607     COPY TRRD271.                                                        
117656*---------------------------------------------------------------*         
117657*  ABEND PROCESSING COPYBOOK.                                    *        
117658*---------------------------------------------------------------*         
117659     COPY DPWS004.                                                        
117660*---------------------------------------------------------------*         
117700*  DB2 COMMUNICATION AREA.                                                
117800*---------------------------------------------------------------*         
117900     EXEC SQL                                                             
118000         INCLUDE SQLCA                                                    
118100     END-EXEC.                                                            
118200                                                                          
118300*---------------------------------------------------------------*         
118400* THE PURPOSE OF THIS CURSOR IS TO GET THE PO'S WHERE                     
118410* (START XFCTRY DATE + 180 DAYS) > OR = TO CURRENT DATE.                  
118800*---------------------------------------------------------------*         
118900     EXEC SQL                                                             
119000          DECLARE PO-TABLE-CSR CURSOR WITH HOLD FOR                       
119100           SELECT DISTINCT                                                
119200                  A.PO_NBR                                                
121000                 ,B.STRT_FCTRY_DTE                                        
121100           FROM   TPURCHS A                                               
121400                 ,TPLNSHP B                                               
121500           WHERE  (B.STRT_FCTRY_DTE >= :PV-STRT-FCTRY-MIN)                
121600             AND  A.VER_STATUS_CDE = 'A'                                  
BARB               AND  A.PO_TYP_CDE IN ('IM','IE','IF','LD', 'LE')             
121800             AND  A.PO_NBR = B.PO_NBR                                     
121900             AND  B.VER_STATUS_CDE = 'A'                                  
122000           ORDER BY A.PO_NBR                                              
122300             WITH UR                                                      
123900     END-EXEC.                                                            
124000                                                                          
124100*---------------------------------------------------------------*         
124200* THE PURPOSE OF THIS CURSOR IS TO GET ALL OF THE TIMFOB                  
124300* TABLE ROWS.                                                             
124400*---------------------------------------------------------------*         
124500     EXEC SQL                                                             
124600          DECLARE FOB-TABLE-CSR CURSOR WITH HOLD FOR                      
124700           SELECT A.FOB_CDE                                               
124800                , A.FOB_STD_EDI_CDE                                       
124900                , A.FOB_CTY_NM                                            
124910                , A.FOB_CNTRY_CDE                                         
124920                , A.CONSLR_ENT_ID                                         
124930                , A.POE_CDE                                               
125000           FROM TIMFOB A                                                  
125010          WHERE STAT_CDE = 'A'                                            
125100          ORDER BY A.FOB_CDE                                              
125800             WITH UR                                                      
125900     END-EXEC.                                                            
126000                                                                          
137104*****************************************************************         
137105 PROCEDURE DIVISION.                                                      
137106*****************************************************************         
137107                                                                          
137108*--------------------------------------------------------------*          
137109* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE  *          
137110* PROGRAM.                                                     *          
137111*--------------------------------------------------------------*          
137112 0000-PROGRAM-MAINLINE.                                                   
137113                                                                          
137114     PERFORM 1000-INITIALIZE.                                             
137115                                                                          
137116     PERFORM 2000-PERFORM-TABLE-EXTRACTS.                                 
137117                                                                          
137118     CLOSE PO-TABLE                                                       
137119           FOB-TABLE.                                                     
137303     STOP RUN.                                                            
137304                                                                          
137305*--------------------------------------------------------------*          
137306 1000-INITIALIZE.                                                         
137307                                                                          
137308     OPEN OUTPUT PO-TABLE                                                 
137309                 FOB-TABLE.                                               
137402                                                                          
137404     PERFORM 7100-SET-CURRENT-TMST.                                       
137405     PERFORM 7110-SET-CURRENT-DATE.                                       
137406     PERFORM 7120-SET-STRT-FCTRY-DTE-MIN.                                 
137410                                                                          
496494*----------------------------------------------------------------*        
496495* CREATE EXTRACT FILES                                                    
496496*----------------------------------------------------------------*        
496497 2000-PERFORM-TABLE-EXTRACTS.                                             
496498                                                                          
496500     PERFORM 2100-OPEN-PO-TABLE-CSR.                                      
496501     PERFORM 2110-PROCESS-PO-TABLE-CSR                                    
496502       UNTIL PS-END-OF-PO-CURSOR.                                         
496505                                                                          
496507     PERFORM 3100-OPEN-FOB-TABLE-CSR.                                     
496508     PERFORM 3110-PROCESS-FOB-TABLE-CSR                                   
496509       UNTIL PS-END-OF-FOB-CURSOR.                                        
496511                                                                          
496512*--------------------------------------------------------------*          
496513* OPEN PO TABLE CURSOR                                                    
496514*--------------------------------------------------------------*          
496515 2100-OPEN-PO-TABLE-CSR.                                                  
496516                                                                          
496517     EXEC SQL                                                             
496518          OPEN PO-TABLE-CSR                                               
496519     END-EXEC.                                                            
496520                                                                          
496521     EVALUATE TRUE                                                        
496522         WHEN SQLCODE = ZERO                                              
496523             CONTINUE                                                     
496524         WHEN SQLWARN0 NOT = SPACES                                       
496525         WHEN SQLCODE < ZERO                                              
496526         WHEN SQLCODE > ZERO                                              
496527             DISPLAY '****************************************'           
496528             DISPLAY '** ABEND                              **'           
496529             DISPLAY '** PROGRAM = TRKUT270                 **'           
496530             DISPLAY '** PARAGRAPH = 2100-OPEN-PO-TABLE-CSR **'           
496533             DISPLAY '****************************************'           
496534             PERFORM 9100-SQL-ABEND                                       
496541     END-EVALUATE.                                                        
496542                                                                          
496543*--------------------------------------------------------------*          
496544* PROCESS PO TABLE CURSOR                                                 
496545*--------------------------------------------------------------*          
496546 2110-PROCESS-PO-TABLE-CSR.                                               
496547                                                                          
496550     PERFORM 2115-FETCH-PO-TABLE-CSR.                                     
496570                                                                          
496580     IF PS-END-OF-PO-CURSOR                                               
496581         PERFORM 2120-CLOSE-PO-TABLE-CSR                                  
496590     ELSE                                                                 
496591        INITIALIZE DCLTIMCNTK                                             
496592                   DCLTIMCNPO                                             
496593                   DCLTIMBKPO                                             
496594                   DCLTIMRCPO                                             
496595                   DCLTIMCSEN                                             
496596                   DCLTIMCSPO                                             
496600        PERFORM 2200-SELECT-DPRTR-DTE                                     
496601        PERFORM 2205-SELECT-POE-DELY-DTE                                  
496602        PERFORM 2210-SELECT-BOOKING-DATES                                 
496800        PERFORM 2220-SELECT-RECEIVING-DATES                               
496801        PERFORM 2230-SELECT-CUSTOMS-DATES                                 
496803        PERFORM 8040-WRITE-PO-DATES-REC                                   
496804     END-IF.                                                              
496805                                                                          
496851*--------------------------------------------------------------*          
496852* FETCH PO TABLE CURSOR                                                   
496853*--------------------------------------------------------------*          
496854 2115-FETCH-PO-TABLE-CSR.                                                 
496855                                                                          
496858     EXEC SQL                                                             
496859         FETCH PO-TABLE-CSR                                               
496860          INTO :PURCHS-PO-NBR                                             
497901              ,:PLNSHP-STRT-FCTRY-DTE                                     
497902     END-EXEC.                                                            
497903                                                                          
497904     EVALUATE TRUE                                                        
497905         WHEN SQLCODE = ZERO                                              
497906             MOVE PURCHS-PO-NBR TO PV-PO-NBR                              
497907                                                                          
497908         WHEN SQLCODE = +100                                              
497909             SET PS-END-OF-PO-CURSOR TO TRUE                              
497910                                                                          
497911         WHEN SQLWARN0 NOT = SPACES                                       
497912         WHEN SQLCODE < ZERO                                              
497913         WHEN SQLCODE > ZERO                                              
497914             DISPLAY '****************************************'           
497915             DISPLAY '** ABEND                              **'           
497916             DISPLAY '** PROGRAM = TRKUT270                 **'           
497917             DISPLAY '** PARAGRAPH = 2115-FETCH-PO-TABLE-CSR**'           
497918             DISPLAY '****************************************'           
497919             PERFORM 9100-SQL-ABEND                                       
497926     END-EVALUATE.                                                        
497927                                                                          
497928     DISPLAY 'PURCHS-PO-NBR  = ' PURCHS-PO-NBR.                           
497929     DISPLAY 'PLNSHP-STRT-FCTRY-DTE = ' PLNSHP-STRT-FCTRY-DTE.            
497931                                                                          
497932 EJECT                                                                    
497933                                                                          
497934*--------------------------------------------------------------*          
497935* CLOSE PO TABLE CURSOR                                                   
497936*--------------------------------------------------------------*          
497937 2120-CLOSE-PO-TABLE-CSR.                                                 
497938                                                                          
497941         EXEC SQL                                                         
497942              CLOSE PO-TABLE-CSR                                          
497943         END-EXEC.                                                        
497944                                                                          
497945     EVALUATE TRUE                                                        
497946         WHEN SQLCODE = ZERO                                              
497947             CONTINUE                                                     
497948         WHEN SQLWARN0 NOT = SPACES                                       
497949         WHEN SQLCODE < ZERO                                              
497950         WHEN SQLCODE > ZERO                                              
497951             DISPLAY '****************************************'           
497952             DISPLAY '** ABEND                              **'           
497953             DISPLAY '** PROGRAM = TRKUT270                 **'           
497954             DISPLAY '** PARAGRAPH = 2120-CLOSE-PO-TABLE-CSR**'           
497955             DISPLAY '****************************************'           
497956             PERFORM 9100-SQL-ABEND                                       
497957     END-EVALUATE.                                                        
497958                                                                          
497959*--------------------------------------------------------------*          
497960* SELECT MINIMUM MOM DEPARTURE DATE                                       
497961*--------------------------------------------------------------*          
497962 2200-SELECT-DPRTR-DTE.                                                   
497963                                                                          
497966     PERFORM                                                              
497967         WITH TEST AFTER                                                  
497968         VARYING PV-RETRY-COUNTER FROM 1 BY 1                             
497969         UNTIL   PV-RETRY-COUNTER > PC-MAX-RETRIES  OR                    
497970                 SQLCODE = ZERO                     OR                    
497971                 SQLCODE = -305                     OR                    
497972                 SQLCODE = +100                                           
497973                                                                          
497974       EXEC SQL                                                           
497975         SELECT MIN(MOM_DPRTR_TMST)                                       
497990           INTO :IMCNTK-MOM-DPRTR-TMST:PV-MOM-DPRTR-TMST-NULL             
498100           FROM TIMCNTK A                                                 
498200              , TIMCNPO B                                                 
498300          WHERE B.PO_NBR = :PV-PO-NBR                                     
498400            AND B.CONTNR_ID = A.CONTNR_ID                                 
498500            AND B.CONTNR_SEQ_NBR = A.CONTNR_SEQ_NBR                       
498510       END-EXEC                                                           
498520                                                                          
498530       EVALUATE TRUE                                                      
498540           WHEN SQLCODE = ZERO                                            
498550               CONTINUE                                                   
498560                                                                          
498561           WHEN SQLCODE = +100                                            
498562           WHEN SQLCODE = -305                                            
498563               MOVE SPACES TO IMCNTK-MOM-DPRTR-TMST                       
498564                              IMCNTK-MOM-POE-DELY-TMST                    
498565                                                                          
498566           WHEN SQLWARN0 NOT EQUAL SPACE                                  
498567           WHEN SQLCODE NOT EQUAL ZERO                                    
498568               DISPLAY '****************************************'         
498569               DISPLAY '** ABEND                              **'         
498570               DISPLAY '** PROGRAM = TRKUT270                 **'         
498571               DISPLAY '** PARAGRAPH = 2200-SELECT-DPRTR-DTE  **'         
498572               DISPLAY '** SELECT TIMCNTK                     **'         
498573               DISPLAY '** PO NBR  = ' PV-PO-NBR                          
498577               DISPLAY '****************************************'         
498578               PERFORM 9100-SQL-ABEND                                     
498585       END-EVALUATE                                                       
498590     END-PERFORM.                                                         
498591                                                                          
498592     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
498593         (SQLCODE NOT = ZERO               AND                            
498594          SQLCODE NOT = -305               AND                            
498595          SQLCODE NOT = +100)                                             
498603         DISPLAY '****************************************'               
498604         DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'               
498605         DISPLAY '** SELECT FROM TIMCNTK                **'               
498606         DISPLAY '** PARAGRAPH = 2200-SELECT-DPRTR-DTE  **'               
498607         DISPLAY '** PV-PO-NBR  = ' PV-PO-NBR                             
498609         DISPLAY '****************************************'               
498610         PERFORM 9100-SQL-ABEND                                           
498617     END-IF.                                                              
498618                                                                          
498619*--------------------------------------------------------------*          
498620* SELECT MINIMUM MOM POE DELIVERY DATE                                    
498621*--------------------------------------------------------------*          
498622 2205-SELECT-POE-DELY-DTE.                                                
498623                                                                          
498626     PERFORM                                                              
498627         WITH TEST AFTER                                                  
498628         VARYING PV-RETRY-COUNTER FROM 1 BY 1                             
498629         UNTIL   PV-RETRY-COUNTER > PC-MAX-RETRIES  OR                    
498630                 SQLCODE = ZERO                     OR                    
498631                 SQLCODE = -305                     OR                    
498632                 SQLCODE = +100                                           
498633                                                                          
498634       EXEC SQL                                                           
498636         SELECT MIN(MOM_POE_DELY_TMST)                                    
498638           INTO :IMCNTK-MOM-POE-DELY-TMST                                 
498639                :PV-MOM-POE-DELY-TMST-NULL                                
498640           FROM TIMCNTK A                                                 
498641              , TIMCNPO B                                                 
498642          WHERE B.PO_NBR = :PV-PO-NBR                                     
498643            AND B.CONTNR_ID = A.CONTNR_ID                                 
498644            AND B.CONTNR_SEQ_NBR = A.CONTNR_SEQ_NBR                       
498645       END-EXEC                                                           
498646                                                                          
498647       EVALUATE TRUE                                                      
498648           WHEN SQLCODE = ZERO                                            
498649               CONTINUE                                                   
498650                                                                          
498651           WHEN SQLCODE = +100                                            
498652           WHEN SQLCODE = -305                                            
498653               MOVE SPACES TO IMCNTK-MOM-DPRTR-TMST                       
498654                              IMCNTK-MOM-POE-DELY-TMST                    
498655                                                                          
498656           WHEN SQLWARN0 NOT EQUAL SPACE                                  
498657           WHEN SQLCODE NOT EQUAL ZERO                                    
498658               DISPLAY '****************************************'         
498659               DISPLAY '** ABEND                              **'         
498660               DISPLAY '** PROGRAM = TRKUT270                 **'         
498661               DISPLAY '** PARAGRAPH = 2205-SELECT-POE-DELY-DTE*'         
498662               DISPLAY '** SELECT TIMCNTK                     **'         
498663               DISPLAY '** PO NBR  = ' PV-PO-NBR                          
498664               DISPLAY '****************************************'         
498665               PERFORM 9100-SQL-ABEND                                     
498666       END-EVALUATE                                                       
498667     END-PERFORM.                                                         
498668                                                                          
498669     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
498670         (SQLCODE NOT = ZERO               AND                            
498671          SQLCODE NOT = -305               AND                            
498672          SQLCODE NOT = +100)                                             
498673         DISPLAY '****************************************'               
498674         DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'               
498675         DISPLAY '** SELECT FROM TIMCNTK                **'               
498676         DISPLAY '** PARAGRAPH = 2205-SELECT-POE-DELY-DTE*'               
498677         DISPLAY '** PV-PO-NBR  = ' PV-PO-NBR                             
498678         DISPLAY '****************************************'               
498679         PERFORM 9100-SQL-ABEND                                           
498680     END-IF.                                                              
498681                                                                          
498682*--------------------------------------------------------------*          
498683* SELECT BOOKING MIN DATES                                                
498684*--------------------------------------------------------------*          
498685 2210-SELECT-BOOKING-DATES.                                               
498686                                                                          
498689     PERFORM                                                              
498690         WITH TEST AFTER                                                  
498691         VARYING PV-RETRY-COUNTER FROM 1 BY 1                             
498692         UNTIL   PV-RETRY-COUNTER > PC-MAX-RETRIES  OR                    
498693                 SQLCODE = ZERO                     OR                    
498694                 SQLCODE = -305                     OR                    
498695                 SQLCODE = +100                                           
498696                                                                          
498697       EXEC SQL                                                           
498700         SELECT MIN(PO_BKG_DELY_TMST)                                     
498800           INTO :IMBKPO-PO-BKG-DELY-TMST:PV-PO-BKG-DELY-TMST-NULL         
499000           FROM TIMBKPO A                                                 
499200          WHERE A.PO_NBR = :PV-PO-NBR                                     
499500       END-EXEC                                                           
499600                                                                          
499700       EVALUATE TRUE                                                      
499800         WHEN SQLCODE = ZERO                                              
499900             CONTINUE                                                     
500000                                                                          
500100         WHEN SQLCODE = +100                                              
500110         WHEN SQLCODE = -305                                              
500200             MOVE SPACES TO IMBKPO-PO-BKG-DELY-TMST                       
500400                                                                          
500710         WHEN SQLWARN0 NOT EQUAL SPACE                                    
500720         WHEN SQLCODE NOT EQUAL ZERO                                      
500730               DISPLAY '****************************************'         
500740               DISPLAY '** ABEND                              **'         
500750               DISPLAY '** PROGRAM = TRKUT270                 **'         
500760               DISPLAY '** PARAGRAPH = 2210-SELECT-BOOKING-DTES*'         
500770               DISPLAY '** SELECT TIMBKPO                     **'         
500780               DISPLAY '** PO NBR  = ' PV-PO-NBR                          
500790               DISPLAY '****************************************'         
500791               PERFORM 9100-SQL-ABEND                                     
501400       END-EVALUATE                                                       
501410     END-PERFORM.                                                         
501500                                                                          
501510     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
501520         (SQLCODE NOT = ZERO               AND                            
501530             SQLCODE NOT = -305            AND                            
501540             SQLCODE NOT = +100)                                          
501541         DISPLAY '****************************************'               
501542         DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'               
501543         DISPLAY '** SELECT FROM TIMBKPO                **'               
501544         DISPLAY '** PARAGRAPH = 2210-SELECT-BOOKING-DTES*'               
501545         DISPLAY '** PV-PO-NBR  = ' PV-PO-NBR                             
501546         DISPLAY '****************************************'               
501547         PERFORM 9100-SQL-ABEND                                           
501592     END-IF.                                                              
501593                                                                          
501600*--------------------------------------------------------------*          
501700* SELECT RECEIVING MIN DATES                                              
501800*--------------------------------------------------------------*          
501900 2220-SELECT-RECEIVING-DATES.                                             
502000                                                                          
502010     PERFORM                                                              
502020         WITH TEST AFTER                                                  
502030         VARYING PV-RETRY-COUNTER FROM 1 BY 1                             
502040         UNTIL   PV-RETRY-COUNTER > PC-MAX-RETRIES  OR                    
502050                 SQLCODE = ZERO                     OR                    
502060                 SQLCODE = -305                     OR                    
502070                 SQLCODE = +100                                           
502080                                                                          
502100      EXEC SQL                                                            
502200         SELECT MIN(PO_RCVD_DELY_TMST)                                    
502300           INTO :IMRCPO-PO-RCVD-DELY-TMST                                 
502310                :PV-PO-RCVD-DELY-TMST-NULL                                
502400           FROM TIMRCPO A                                                 
502500          WHERE A.PO_NBR = :PV-PO-NBR                                     
502600      END-EXEC                                                            
502700                                                                          
502800       EVALUATE TRUE                                                      
502900         WHEN SQLCODE = ZERO                                              
503000             CONTINUE                                                     
503100                                                                          
503200         WHEN SQLCODE = +100                                              
503210         WHEN SQLCODE = -305                                              
503300             MOVE SPACES TO IMRCPO-PO-RCVD-DELY-TMST                      
503400                                                                          
503710         WHEN SQLWARN0 NOT EQUAL SPACE                                    
503720         WHEN SQLCODE NOT EQUAL ZERO                                      
503730               DISPLAY '****************************************'         
503740               DISPLAY '** ABEND                              **'         
503750               DISPLAY '** PROGRAM = TRKUT270                 **'         
503760               DISPLAY '** PARAGRAPH = 2220-SELECT-RCVG-DATES **'         
503770               DISPLAY '** SELECT TIMRCPO                     **'         
503780               DISPLAY '** PO NBR  = ' PV-PO-NBR                          
503790               DISPLAY '****************************************'         
503791               PERFORM 9100-SQL-ABEND                                     
504400       END-EVALUATE                                                       
504410     END-PERFORM.                                                         
504500                                                                          
504510     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
504520         (SQLCODE NOT = ZERO               AND                            
504530             SQLCODE NOT = -305            AND                            
504540             SQLCODE NOT = +100)                                          
504541         DISPLAY '****************************************'               
504542         DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'               
504543         DISPLAY '** SELECT FROM TIMRCPO                **'               
504544         DISPLAY '** PARAGRAPH = 2220-SELECT-RECEIVING-DTE'               
504545         DISPLAY '** PV-PO-NBR  = ' PV-PO-NBR                             
504546         DISPLAY '****************************************'               
504547         PERFORM 9100-SQL-ABEND                                           
504592     END-IF.                                                              
504593                                                                          
504600*--------------------------------------------------------------*          
504700* SELECT CUSTOMS DATES                                                    
504800*--------------------------------------------------------------*          
504900 2230-SELECT-CUSTOMS-DATES.                                               
505000                                                                          
505010     PERFORM                                                              
505020         WITH TEST AFTER                                                  
505030         VARYING PV-RETRY-COUNTER FROM 1 BY 1                             
505040         UNTIL   PV-RETRY-COUNTER > PC-MAX-RETRIES  OR                    
505050                 SQLCODE = ZERO                     OR                    
505060                 SQLCODE = -305                     OR                    
505070                 SQLCODE = +100                                           
505100       EXEC SQL                                                           
505200         SELECT MIN(FILD_TMST)                                            
505300           INTO :IMCSEN-FILD-TMST:PV-FILD-TMST-NULL                       
505400           FROM TIMCSEN A                                                 
505410              , TIMCSPO B                                                 
505500          WHERE B.PO_NBR = :PV-PO-NBR                                     
505510            AND B.CSTM_ENTR_ID = A.CSTM_ENTR_ID                           
505600       END-EXEC                                                           
505700                                                                          
505800       EVALUATE TRUE                                                      
505900         WHEN SQLCODE = ZERO                                              
506000             CONTINUE                                                     
506100                                                                          
506200         WHEN SQLCODE = +100                                              
506210         WHEN SQLCODE = -305                                              
506300             MOVE SPACES TO IMCSEN-FILD-TMST                              
506400                                                                          
506710         WHEN SQLWARN0 NOT EQUAL SPACE                                    
506720         WHEN SQLCODE NOT EQUAL ZERO                                      
506730               DISPLAY '****************************************'         
506740               DISPLAY '** ABEND                              **'         
506750               DISPLAY '** PROGRAM = TRKUT270                 **'         
506760               DISPLAY '** PARAGRAPH = 2230-SELECT-CUSTOM-DTES**'         
506770               DISPLAY '** SELECT TIMCSEN                     **'         
506780               DISPLAY '** PO NBR  = ' PV-PO-NBR                          
506790               DISPLAY '****************************************'         
506791               PERFORM 9100-SQL-ABEND                                     
507220       END-EVALUATE                                                       
507221     END-PERFORM.                                                         
507230                                                                          
507240     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
507250         (SQLCODE NOT = ZERO               AND                            
507260             SQLCODE NOT = -305            AND                            
507261             SQLCODE NOT = +100)                                          
507262         DISPLAY '****************************************'               
507263         DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'               
507264         DISPLAY '** SELECT FROM TIMCSEN                **'               
507265         DISPLAY '** PARAGRAPH = 2230-SELECT-CUSTOMS-DATE*'               
507266         DISPLAY '** PV-PO-NBR  = ' PV-PO-NBR                             
507267         DISPLAY '****************************************'               
507268         PERFORM 9100-SQL-ABEND                                           
507275     END-IF.                                                              
507276                                                                          
507277*--------------------------------------------------------------*          
507278* OPEN FOB TABLE CURSOR                                                   
507279*--------------------------------------------------------------*          
507280 3100-OPEN-FOB-TABLE-CSR.                                                 
507281                                                                          
507282     EXEC SQL                                                             
507283          OPEN FOB-TABLE-CSR                                              
507284     END-EXEC.                                                            
507285                                                                          
507286     EVALUATE TRUE                                                        
507287         WHEN SQLCODE = ZERO                                              
507288             CONTINUE                                                     
507289         WHEN SQLWARN0 NOT = SPACES                                       
507290         WHEN SQLCODE < ZERO                                              
507291         WHEN SQLCODE > ZERO                                              
507292             DISPLAY '****************************************'           
507293             DISPLAY '** ABEND                              **'           
507294             DISPLAY '** PROGRAM = TRKUT270                 **'           
507295             DISPLAY '** PARAGRAPH = 3100-OPEN-FOB-TABLE-CSR**'           
507296             DISPLAY '****************************************'           
507297             PERFORM 9100-SQL-ABEND                                       
507298     END-EVALUATE.                                                        
507299                                                                          
507300*--------------------------------------------------------------*          
507301* PROCESS FOB TABLE CURSOR                                                
507302*--------------------------------------------------------------*          
507303 3110-PROCESS-FOB-TABLE-CSR.                                              
507304                                                                          
507305     INITIALIZE DCLTIMFOB.                                                
507306     PERFORM 3115-FETCH-FOB-TABLE-CSR.                                    
507307                                                                          
507308     IF PS-END-OF-FOB-CURSOR                                              
507309         PERFORM 3120-CLOSE-FOB-TABLE-CSR                                 
507310     ELSE                                                                 
507321        PERFORM 8050-WRITE-FOB-REC                                        
507322     END-IF.                                                              
507323                                                                          
507324*--------------------------------------------------------------*          
507325* FETCH FOB TABLE CURSOR                                                  
507326*--------------------------------------------------------------*          
507327 3115-FETCH-FOB-TABLE-CSR.                                                
507328                                                                          
507331     EXEC SQL                                                             
507332         FETCH FOB-TABLE-CSR                                              
507333          INTO :IMFOB-FOB-CDE                                             
507334             , :IMFOB-FOB-STD-EDI-CDE                                     
507335             , :IMFOB-FOB-CTY-NM                                          
507336             , :IMFOB-FOB-CNTRY-CDE                                       
507337             , :IMFOB-CONSLR-ENT-ID                                       
507338             , :IMFOB-POE-CDE                                             
507339     END-EXEC.                                                            
507340                                                                          
507341     EVALUATE TRUE                                                        
507342         WHEN SQLCODE = ZERO                                              
507344             PERFORM 3200-SELECT-CONSLR-SCAC                              
507345             IF PS-VALID-ENTNME                                           
507346                 MOVE EXTENT-SCAC-CDE TO TR271-CONSLR-SCAC                
507349             ELSE                                                         
507350                 MOVE SPACES TO TR271-CONSLR-SCAC                         
507351             END-IF                                                       
507354                                                                          
507355         WHEN SQLCODE = +100                                              
507356             SET PS-END-OF-FOB-CURSOR TO TRUE                             
507357                                                                          
507358         WHEN SQLWARN0 NOT = SPACES                                       
507359         WHEN SQLCODE < ZERO                                              
507360         WHEN SQLCODE > ZERO                                              
507361             DISPLAY '****************************************'           
507362             DISPLAY '** ABEND                              **'           
507363             DISPLAY '** PROGRAM = TRKUT270                 **'           
507364             DISPLAY '** PARAGRAPH = 3115-FETCH-FOB-TABLE-CSR*'           
507365             DISPLAY '****************************************'           
507366             PERFORM 9100-SQL-ABEND                                       
507367     END-EVALUATE.                                                        
507368                                                                          
507369 EJECT                                                                    
507370                                                                          
507371*--------------------------------------------------------------*          
507372* CLOSE FOB TABLE CURSOR                                                  
507373*--------------------------------------------------------------*          
507374 3120-CLOSE-FOB-TABLE-CSR.                                                
507375                                                                          
507378         EXEC SQL                                                         
507379              CLOSE FOB-TABLE-CSR                                         
507380         END-EXEC.                                                        
507381                                                                          
507382     EVALUATE TRUE                                                        
507383         WHEN SQLCODE = ZERO                                              
507384             CONTINUE                                                     
507385         WHEN SQLWARN0 NOT = SPACES                                       
507386         WHEN SQLCODE < ZERO                                              
507387         WHEN SQLCODE > ZERO                                              
507388             DISPLAY '****************************************'           
507389             DISPLAY '** ABEND                              **'           
507390             DISPLAY '** PROGRAM = TRKUT270                 **'           
507391             DISPLAY '** PARAGRAPH = 3120-CLOSE-FOB-TABLE-CSR*'           
507392             DISPLAY '****************************************'           
507393             PERFORM 9100-SQL-ABEND                                       
507394     END-EVALUATE.                                                        
507400                                                                          
507401*--------------------------------------------------------------*          
507402* SELECT CONSOLIDATOR SCAC CODE.                                          
507403*--------------------------------------------------------------*          
507410 3200-SELECT-CONSLR-SCAC.                                                 
507420                                                                          
507450     PERFORM                                                              
507460         WITH TEST AFTER                                                  
507470         VARYING PV-RETRY-COUNTER FROM 1 BY 1                             
507480         UNTIL   PV-RETRY-COUNTER > PC-MAX-RETRIES  OR                    
507490                 SQLCODE = ZERO                     OR                    
507491                 SQLCODE = +100                                           
507492                                                                          
507493         EXEC SQL                                                         
507494             SELECT DISTINCT                                              
507495                    C.SCAC_CDE                                            
507496              INTO :EXTENT-SCAC-CDE                                       
507498              FROM  TENTNME B                                             
507499                  , TEXTENT C                                             
507501              WHERE B.ENT_ID = :IMFOB-CONSLR-ENT-ID                       
507502                AND B.TYPE_CDE = '04'                                     
507503                AND B.ENT_ID = C.ENT_ID                                   
507504         END-EXEC                                                         
507505                                                                          
507506         EVALUATE TRUE                                                    
507507             WHEN SQLCODE = ZERO                                          
507508             WHEN SQLCODE = -811                                          
507509                 SET PS-VALID-ENTNME   TO TRUE                            
507510                                                                          
507511             WHEN SQLCODE = +100                                          
507512                 SET PS-INVALID-ENTNME TO TRUE                            
507513                                                                          
507514             WHEN SQLWARN0 NOT EQUAL SPACE                                
507515             WHEN SQLCODE NOT EQUAL ZERO                                  
507516                 DISPLAY '**************************************'         
507517                 DISPLAY '** ABEND                            **'         
507518                 DISPLAY '** PROGRAM = TRKUT270               **'         
507519                 DISPLAY '** PARAGRAPH = 3200-SELECT-CONSLR-SCAC'         
507520                 DISPLAY '** SELECT CONSOLIDATOR SCAC CODE    **'         
507522                 DISPLAY 'CONSLR ENT ID = ' IMFOB-CONSLR-ENT-ID           
507524                 DISPLAY '**************************************'         
507525                 PERFORM 9100-SQL-ABEND                                   
507526         END-EVALUATE                                                     
507527     END-PERFORM.                                                         
507528                                                                          
507529     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
507530         (SQLCODE NOT = ZERO    AND                                       
507531             SQLCODE NOT = +100 AND                                       
507532             SQLCODE NOT = -811)                                          
507533         DISPLAY '****************************************'               
507534         DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'               
507535         DISPLAY '** SELECT FROM TENTNME                **'               
507536         DISPLAY '** PARAGRAPH = 3200-SELECT-CONSLR-SCAC**'               
507537         DISPLAY '** CONSLR ENT ID = ' IMFOB-CONSLR-ENT-ID                
507539         DISPLAY '****************************************'               
507540         PERFORM 9100-SQL-ABEND                                           
507541     END-IF.                                                              
507542                                                                          
507550******************************************************************        
507600* SET THE CURRENT TIMESTAMP.                                     *        
507700******************************************************************        
507800 7100-SET-CURRENT-TMST.                                                   
507900                                                                          
508000          EXEC SQL                                                        
508100              SET :PV-CURRENT-TMST = CURRENT TIMESTAMP                    
508200          END-EXEC                                                        
508300                                                                          
508400         EVALUATE TRUE                                                    
508500             WHEN SQLCODE = ZERO                                          
508600                 CONTINUE                                                 
508700                                                                          
508800             WHEN SQLWARN0 NOT EQUAL SPACE                                
508900             WHEN SQLCODE NOT EQUAL ZERO                                  
509000                 DISPLAY '**************************************'         
509100                 DISPLAY '** ABEND                            **'         
509200                 DISPLAY '** PROGRAM = TRKUT270               **'         
509300                 DISPLAY '** PARAGRAPH = 7100-SET-CURRENT-TMST**'         
509400                 DISPLAY '** SET CURRENT TIMESTAMP            **'         
509500                 DISPLAY '** CURRENT TMST   = ' PV-CURRENT-TMST           
509600                 DISPLAY '**************************************'         
509700                 PERFORM 9100-SQL-ABEND                                   
509800         END-EVALUATE.                                                    
509900                                                                          
510000******************************************************************        
510100* SET THE CURRENT DATE.                                          *        
510200******************************************************************        
510300 7110-SET-CURRENT-DATE.                                                   
510400                                                                          
510500          EXEC SQL                                                        
510600              SET :PV-CURRENT-DATE = CURRENT DATE                         
510700          END-EXEC                                                        
510800                                                                          
510900         EVALUATE TRUE                                                    
511000             WHEN SQLCODE = ZERO                                          
511100                 CONTINUE                                                 
511200                                                                          
511300             WHEN SQLWARN0 NOT EQUAL SPACE                                
511400             WHEN SQLCODE NOT EQUAL ZERO                                  
511500                 DISPLAY '**************************************'         
511600                 DISPLAY '** ABEND                            **'         
511700                 DISPLAY '** PROGRAM = TRKUT270               **'         
511800                 DISPLAY '** PARAGRAPH = 7110-SET-CURRENT-DATE**'         
511900                 DISPLAY '** SET CURRENT DATE                 **'         
512000                 DISPLAY '** CURRENT DATE   = ' PV-CURRENT-DATE           
512100                 DISPLAY '**************************************'         
512200                 PERFORM 9100-SQL-ABEND                                   
512300         END-EVALUATE.                                                    
512400                                                                          
512500******************************************************************        
512600* SET THE MINIMUM START XFACTORY DATE                            *        
512700******************************************************************        
512800 7120-SET-STRT-FCTRY-DTE-MIN.                                             
512900                                                                          
513000       EXEC SQL                                                           
513100         SET :PV-STRT-FCTRY-MIN = (CURRENT DATE - 180 DAYS)               
513200       END-EXEC                                                           
513300                                                                          
513400         EVALUATE TRUE                                                    
513500             WHEN SQLCODE = ZERO                                          
513600                 CONTINUE                                                 
513700                                                                          
513800             WHEN SQLWARN0 NOT EQUAL SPACE                                
513900             WHEN SQLCODE NOT EQUAL ZERO                                  
514000                 DISPLAY '**************************************'         
514100                 DISPLAY '** ABEND                            **'         
514200                 DISPLAY '** PROGRAM = TRKUT270               **'         
514300                 DISPLAY '** PARAGRAPH = 7120-SET-STRT-XFCTRY-**'         
514400                 DISPLAY '** MIN-DTE                          **'         
514600                 DISPLAY '**************************************'         
514700                 PERFORM 9100-SQL-ABEND                                   
514800         END-EVALUATE.                                                    
514810                                                                          
514820      DISPLAY 'MINIMUM STRT XFCTRY DATE = ' PV-STRT-FCTRY-MIN.            
514900                                                                          
519904*----------------------------------------------------------------*        
519905* WRITE RECORD TO PO DATES FILE                                           
519906*----------------------------------------------------------------*        
519907 8040-WRITE-PO-DATES-REC.                                                 
519908                                                                          
519911     INITIALIZE PO-TABLE-RECORD.                                          
519912                                                                          
519930     MOVE PV-PO-NBR                TO TR270-PO-NBR.                       
519940     MOVE IMCNTK-MOM-DPRTR-TMST (1:10)                                    
521900                                   TO TR270-MOM-DPRTR-DTE.                
522200     MOVE IMCNTK-MOM-POE-DELY-TMST (1:10)                                 
522201                                   TO TR270-MOM-POE-DELY-DTE.             
522210     MOVE IMBKPO-PO-BKG-DELY-TMST (1:10)                                  
522211                                   TO TR270-PO-BKG-DELY-DTE.              
522220     MOVE IMRCPO-PO-RCVD-DELY-TMST (1:10)                                 
522221                                   TO TR270-PO-RCVD-DELY-DTE.             
522230     MOVE IMCSEN-FILD-TMST (1:10)  TO TR270-CSTMS-FILD-DTE.               
522400                                                                          
523502     WRITE PO-TABLE-RECORD                                                
523503        FROM TR270-CONTNR-PO-MIN-DATES.                                   
523504                                                                          
523505*----------------------------------------------------------------*        
523506* WRITE RECORD TO FOB FILE                                                
523507*----------------------------------------------------------------*        
523508 8050-WRITE-FOB-REC.                                                      
523509                                                                          
523530     INITIALIZE FOB-TABLE-RECORD.                                         
523540                                                                          
523580     MOVE IMFOB-FOB-CDE         TO TR271-FOB-CDE.                         
523590     MOVE IMFOB-FOB-STD-EDI-CDE TO TR271-FOB-STD-EDI-CDE.                 
523600     MOVE IMFOB-FOB-CTY-NM      TO TR271-FOB-CTY-NM.                      
523700     MOVE IMFOB-FOB-CNTRY-CDE   TO TR271-FOB-CNTRY-CDE.                   
523800*    MOVE IMFOB-CONSLR-ENT-ID   TO TR271-CONSLR-ENT-ID.                   
524000     MOVE IMFOB-POE-CDE         TO TR271-POE-CDE.                         
524400                                                                          
524510     WRITE FOB-TABLE-RECORD                                               
524600        FROM TR271-FOB-TABLE.                                             
524700                                                                          
529504*----------------------------------------------------------------*        
529505*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
529506*  ERROR OR WARNING CODE OCCURS.                                          
529507*----------------------------------------------------------------*        
529508 9100-SQL-ABEND.                                                          
529510******************************************************************        
529520* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
529521* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
529522* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
529523* FORMAT.                                                                 
529524******************************************************************        
529526     COPY DPPD004.                                                        
529527                                                                          
529529     MOVE +4013 TO ABEND-CODE.                                            
529530     CALL 'ILBOABN0' USING ABEND-CODE.                                    
529540*                                                                         
