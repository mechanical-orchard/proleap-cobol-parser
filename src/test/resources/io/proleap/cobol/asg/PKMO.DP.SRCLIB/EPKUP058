      ******************************************************************00010023
       IDENTIFICATION DIVISION.                                         00020023
      ******************************************************************00030023
                                                                        00040023
       PROGRAM-ID.             EPKUP058.                                00050023
       AUTHOR.                 JIM HUNTER.                              00060023
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070023
       DATE-WRITTEN.           JULY, 2012.                              00080023
       DATE-COMPILED.                                                   00090023
                                                                        00100023
      ******************************************************************00110023
      * THIS PROGRAM WILL CONTROL THE ASSIGNING OF BUSINESS DATES ON    00120023
      * THE COSA PARTITION TABLE (TEPPART).                             00130023
      * IT WAS MODELLED AFTER EPKUP048, WITH CODE ADDED FROM EPKUT051   00140023
      * AND EPKUT052 AS NOTED.  THE CALENDAR DATE ROUTINE WAS CALLED    00150023
      * TO PROVIDE ADDITIONAL DATE ERROR CHECKING.                      00160023
      * IT WILL BEGIN RUNNING AS PART OF THE FSH (FLEXIBLE STORE HOUR)  00170029
      * PROJECT, WHICH WILL ELIMINATE THE USE OF THE TEMPORARY AND      00180029
      * HISTORICAL PARTITIONS FROM THE COSA TABLES.  THE DAILY AND      00190029
      * WEEKLY PARTITION ROLL PROCESSES WILL ALSO BE ELIMINATED.        00200029
      *                                                                 00210023
      * INITIALLY, PARTNS 1-4 WILL REMAIN 'TEMPORARY' PARTNS AND WILL   00220023
      * NOT HAVE TRANSACTIONS STORED IN THEM.  THIS IS TO ALLOW FOR     00230023
      * THE UNLIKELY EVENT THAT A BACKOUT OF WRITING DIRECTLY TO THE    00240023
      * PERM PARTNS WOULD BE NEEDED.                                    00250023
      * THEY WILL CONTINUE TO HOLD THE CURRENT DATE'S BUSINESS DATE -   00260029
      * THE HUB AND FLASH WILL CONTINUE TO ACCESS PARTITION 1 TO GET    00270029
      * THE BUSINESS DATE.                                              00280029
      * OPERATIONAL PARTITIONS 96 THRU 500 WILL BE ADDED TO ACCOMODATE  00290023
      * THE LOSS OF THE HISTORICAL PARTITIONS.                          00300023
      *                                                                 00310023
      * THE HISTORICAL PARTITIONS WILL BE KEPT UNTIL THE 500            00320023
      * OPERATIONAL PARTITIONS HAVE BEEN FILLED (ROUGHLY 13 MONTHS).    00330023
      * AT THAT TIME, THE HISTORICAL TABLES WILL BE DROPPED BY THE      00340023
      * DBA'S AND ALL REFERENCES TO THEM WILL BE REMOVED FROM PROGRAMS. 00350023
      * IN THE MEAN TIME, THE HISTORICAL PARTITIONS WILL NOT ROLL OFF   00360023
      * OF THE HISTORICAL TABLES - PROGRAMS CAN CONTINUE TO USE THEM.   00370023
      *                                                                 00380023
      * THIS PROGRAM WILL RUN NIGHTLY AND WILL:                         00390023
      * 1) UPDATE THE TEPPART SALES DATES ON PARTITIONS 1 THRU 4        00400026
      *    TO REFLECT THE NEXT BUSINESS DAY (CTLCD DATE).               00410023
      *    IN 2013, A NEW TABLE WILL TAKE THE PLACE OF PARTN 1          00420023
      *    FOR HOLDING THE CURRENT BUSINESS DATE AND PARTNS 1 - 4       00430023
      *    WILL BE CONVERTED TO HOLD TRANSACTIONS LIKE THE REST OF      00440029
      *    THE PERMANENT PARTITIONS.                                    00450029
      * 2) FIND THE LOWEST UNASSIGNED PARTITION (SALES DATE =           00460023
      *    '9999-12-31') AND UPDATE IT TO REFLECT THE NEXT              00470023
      *    BUSINESS DAY + 1.  THIS WILL BE THE PARTITION WRITTEN TO     00480023
      *    WHEN 'FUTURE' DATED TRANSACTIONS ARE SENT (TYPICALLY,        00490023
      *    BY EAST COAST STORES WHEN THEY ARE OPEN PAST 1:00 A.M.,      00500029
      *    AND THEIR REGISTERS ARE ROLLED TO THE NEXT BUSINESS DAY).    00510023
      *    IF AN UNASSIGNED PARTITION IS FOUND, SKIP #3 (FINDING        00520023
      *    PARTITION WITH OLDEST DATE).                                 00530023
      * 3) IF AN UNASSIGNED PARTITION IS NOT FOUND IN STEP 2...         00540029
      *    FIND THE OPERATIONAL PARTITION WITH THE OLDEST SALES DATE    00550029
      *    AND UPDATE IT TO REFLECT THE NEXT BUSINESS DAY + 1.  THIS    00560023
      *    WILL BE THE PARTITION WRITTEN TO WHEN 'FUTURE' DATED         00570023
      *    TRANSACTIONS ARE SENT (TYPICALLY, BY EAST COAST STORES       00580023
      *    WHEN THEY ARE OPEN PAST 1:00 A.M., AND THEIR REGISTERS ARE   00590023
      *    ROLLED TO THE NEXT BUSINESS DAY).                            00600023
      * 4) PASS THE OPERATIONAL PARTITION WITH THE ASSIGNED FUTURE DATE 00610025
      *    VIA A CONTROL CARD TO EPK386 WHICH WILL CLEAR ALL OF         00620029
      *    THE COSA TABLES FOR THAT PARTITION.                          00630023
      *                                                                 00640023
      * THIS PROGRAM MUST ASSIGN PARTITIONS 2 DAYS IN ADVANCE TO        00650023
      * ALLOW FOR THE NEXT BUSINESS DATE'S FUTURE TRANSACTIONS.         00660023
      * WHEN THIS PROGRAM RUNS, THE NEW BUSINESS DATE'S PARTITION       00670023
      * MAY ALREADY CONTAIN FUTURE DATED TRANSACTIONS - IT CAN NOT      00680023
      * BE CLEARED!                                                     00690023
      *                                                                 00700023
      * BEFORE THIS PROGRAM RUNS FOR THE FIRST TIME, AN                 00710023
      * INITIALIZATION PROGRAM (EPKUP958) WILL NEED TO BE RUN THAT      00720026
      * WILL ASSIGN THE NEXT BUSINESS DATE'S PARTITION.                 00730026
      * THE INITIALIZATION PROGRAM WILL ALSO ASSIGN NEW PARTITIONS      00740023
      * 96 THRU 500 TO WITH SALES DATES OF '9999-12-31' SO THAT         00750023
      * THEY ARE THE NEXT TO BE ASSIGNED.  UNASSIGNED PARTITIONS        00760029
      * WILL HAVE THE STR_GP_CDE AND DB_STRUC_CDE WILL SET TO 'N'       00770029
      * SO THAT PROGRAMS WON'T ACCIDENTLY REFERENCE THEM.               00780029
      ******************************************************************00790023
      * CHANGE LOG: INITIAL PROGRAM.                                    00800028
      * DATE:  JULY, 2012                                               00810028
      * PRGMR: JIM HUNTER                                               00820028
      * WHY:   FSH (FLEXIBLE STORE HOUR) PROJECT - WR40932              00830028
      *                                                                 00840023
      ******************************************************************00850023
      * DATE:  FEBRUARY, 2013.                                          00860042
      * PRGMR: JIM ARENDT                                               00870041
      * WHY:   FSH13 (FLEXIBLE STORE HOUR) PROJECT - CRQ000000041382    00880041
      *                                                                 00890041
      ******************************************************************00900041
C62728* DATE:  OCTOBER, 2013.                                           00900143
C62728* PRGMR: COGNIZANT                                                00900243
C62728* WHY:   FSH13 (FLEXIBLE STORE HOUR) PROJECT - CHG0062728         00900343
C62728*                                                                 00900443
C62728******************************************************************00900543
                                                                        00910023
       ENVIRONMENT DIVISION.                                            00920023
      ******************************************************************00930023
                                                                        00940023
       CONFIGURATION SECTION.                                           00950023
       SOURCE-COMPUTER.        IBM-3090.                                00960023
       OBJECT-COMPUTER.        IMB-3090.                                00970023
                                                                        00980023
       INPUT-OUTPUT SECTION.                                            00990023
       FILE-CONTROL.                                                    01000023
                                                                        01010023
           SELECT DATE-CONTROL-FILE                                     01020023
               ASSIGN TO INP01.                                         01030023
                                                                        01040023
           SELECT FUTURE-PARTN-FILE-OUT                                 01050025
               ASSIGN TO OUT01.                                         01060023
                                                                        01070023
      ******************************************************************01080023
       DATA DIVISION.                                                   01090023
      ******************************************************************01100023
       FILE SECTION.                                                    01110023
                                                                        01120023
       FD  DATE-CONTROL-FILE                                            01130023
           RECORDING MODE IS F                                          01140023
           BLOCK CONTAINS 0 CHARACTERS                                  01150023
           LABEL RECORDS ARE STANDARD.                                  01160023
                                                                        01170023
       01  DATE-CONTROL-REC                 PIC  X(80).                 01180023
                                                                        01190023
                                                                        01200023
       FD  FUTURE-PARTN-FILE-OUT                                        01210025
           RECORDING MODE IS F                                          01220023
           LABEL RECORDS ARE STANDARD                                   01230023
           BLOCK CONTAINS 0 RECORDS.                                    01240023
                                                                        01250023
       01  FUTURE-PARTN-RECORD              PIC  X(80).                 01260025
                                                                        01270023
      ******************************************************************01280023
       WORKING-STORAGE SECTION.                                         01290023
      ******************************************************************01300023
                                                                        01310023
       01  ABEND-CODE                  PIC S9(04)   VALUE ZEROS         01320023
                                                    COMP SYNC.          01330023
           88  AC-MISSING-RECORD                    VALUE +4003.        01340023
           88  AC-DATA-ERROR                        VALUE +4008.        01350023
           88  AC-DB2-ERROR                         VALUE +4013.        01360023
           88  AC-CALENDAR-ROUTINE-ERROR            VALUE +4019.        01370023
                                                                        01380023
       01  PC-PROGRAM-CONSTANTS.                                        01390023
           05  PC-PROGRAM-NAME         PIC  X(08)   VALUE 'EPKUP058'.   01400023
           05  PC-OPERATIONAL          PIC  X(01)   VALUE 'O'.          01410023
           05  PC-A                    PIC  X(01)   VALUE 'A'.          01420023
           05  PC-NOT-FOUND            PIC S9(03)   VALUE +100.         01430023
           05  PC-COMP-1               PIC S9(04)   VALUE 1             01440023
                                                    COMP.               01450023
       01  PS-PROGRAM-SWITCHES.                                         01460026
           05  PS-SQL-SW                     PIC X(01)  VALUE ' '.      01470026
               88  PS-ROW-FOUND                         VALUE 'F'.      01480026
               88  PS-ROW-NOT-FOUND                     VALUE 'N'.      01490026
                                                                        01500027
      *----------------------------------------------------------       01510023
      * CONTROL CARD DATE - SCHEDULED DATE                              01520023
      *----------------------------------------------------------       01530023
       01 CD-CONTROL-DATE.                                              01540023
           05  FILLER                    PIC X(20).                     01550023
           05  CD-POS-DATE-CYMD          PIC X(8).                      01560023
           05  FILLER                    REDEFINES CD-POS-DATE-CYMD.    01570023
               10  CD-POS-CENTURY        PIC X(2).                      01580023
               10  CD-POS-YEAR           PIC X(2).                      01590023
               10  CD-POS-MONTH          PIC X(2).                      01600023
               10  CD-POS-DAY            PIC X(2).                      01610023
           05  FILLER                    PIC X(52).                     01620023
                                                                        01630023
                                                                        01640023
       01  PV-PROGRAM-VARIABLES.                                        01650023
           05  PV-PARAGRAPH-NAME       PIC  X(30)   VALUE SPACE.        01660023
           05  PV-OPERATION-MSG-1      PIC  X(40)   VALUE SPACE.        01670023
           05  PV-OPERATION-MSG-2      PIC  X(40)   VALUE SPACE.        01680023
           05  PV-DB2-OPERATION        PIC  X(30)   VALUE SPACE.        01690023
           05  PV-CURR-DATE.                                            01700023
              10 PV-CURR-DATE-CC       PIC  X(02)   VALUE SPACES.       01710023
              10 PV-CURR-DATE-YY       PIC  X(02)   VALUE SPACES.       01720023
              10 FILLER                PIC  X(01)   VALUE '-'.          01730023
              10 PV-CURR-DATE-MM       PIC  X(02)   VALUE SPACES.       01740023
              10 FILLER                PIC  X(01)   VALUE '-'.          01750023
              10 PV-CURR-DATE-DD       PIC  X(02)   VALUE SPACES.       01760023
                                                                        01770023
           05  PV-TODAYS-CTLCD-DATE REDEFINES                           01780023
                  PV-CURR-DATE         PIC  X(10).                      01790023
           05  PV-FUTURE-DATE          PIC  X(10)   VALUE SPACES.       01800023
           05  PV-YESTERDAYS-DATE      PIC  X(10)   VALUE SPACES.       01810023
           05  PV-OLDEST-PARTN         PIC  S9(4)  COMP  VALUE 0.       01820023
           05  PV-OLDEST-PARTN-DATE    PIC  X(10)   VALUE SPACES.       01830023
           05  PV-DAYS-DIFF            PIC  ZZZ,ZZZ,ZZ9-.               01840023
                                                                        01850023
                                                                        01860023
       01  PV-FUTURE-PARTN-RECORD.                                      01870025
           05  PV-REC-FUTURE-PARTN     PIC  9(04)   VALUE ZEROES.       01880025
           05  FILLER                  PIC  X(76)   VALUE SPACE.        01890025
                                                                        01900023
      ******************************************************************01910023
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        01920023
      ******************************************************************01930023
                                                                        01940023
           COPY DPWS004.                                                01950023
                                                                        01960023
      ******************************************************************01970023
      *  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)             01980023
      ******************************************************************01990023
                                                                        02000023
           COPY DPWS500.                                                02010023
                                                                        02020023
      ******************************************************************02030023
      *  SQL AND COBOL DECLARATIONS FOR THE PARTITION TABLE - TEPPART   02040023
      ******************************************************************02050023
                                                                        02060023
           EXEC SQL                                                     02070023
               INCLUDE TEPPART                                          02080023
           END-EXEC.                                                    02090023
                                                                        02100031
FSH13 ******************************************************************02110032
FSH13 *  SQL AND COBOL DECLARATIONS FOR THE EPT_POS_ACTV_SLS_DTE TABLE  02120032
FSH13 ******************************************************************02130032
FSH13                                                                   02140032
FSH13      EXEC SQL                                                     02150032
FSH13          INCLUDE EPT00069                                         02160032
FSH13      END-EXEC.                                                    02170032
                                                                        02180023
      ******************************************************************02190023
      *  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                02200023
      ******************************************************************02210023
                                                                        02220023
           EXEC SQL                                                     02230023
               INCLUDE SQLCA                                            02240023
           END-EXEC.                                                    02250023
                                                                        02260023
      ******************************************************************02270023
       PROCEDURE DIVISION.                                              02280023
      ******************************************************************02290023
                                                                        02300023
      ******************************************************************02310023
       0000-MAINLINE.                                                   02320023
      ******************************************************************02330023
                                                                        02340023
           PERFORM 1000-READ-DATE-CONTROL.                              02350023
                                                                        02360023
FSH13      PERFORM 2000-EPT-SLS-DTE-RTN.                                02370039
                                                                        02380031
FSH13      PERFORM 2200-UPDATE-PARTN1-4-SALE-DATE.                      02390039
           DISPLAY 'PARTN 1 SALES DATE SET TO ' PV-TODAYS-CTLCD-DATE.   02400036
                                                                        02410023
           PERFORM 3000-GET-NEXT-PARTN-TO-ASSIGN.                       02420024
                                                                        02430023
           PERFORM 4000-UPDATE-OLDEST-OPER-DATE.                        02440023
           DISPLAY 'FUTURE DATE PARTN NBR ' PV-OLDEST-PARTN             02450026
                                 ' SET TO ' PV-FUTURE-DATE              02460026
                                                                        02470023
           PERFORM 5000-WRITE-PART-NBR-CNTL-CARD.                       02480023
                                                                        02490023
           STOP RUN.                                                    02500023
                                                                        02510023
                                                                        02520023
      ******************************************************************02530023
       1000-READ-DATE-CONTROL.                                          02540023
      *                                                                 02550023
      ******************************************************************02560023
                                                                        02570023
           OPEN INPUT DATE-CONTROL-FILE.                                02580023
                                                                        02590023
           READ DATE-CONTROL-FILE                                       02600023
             AT END                                                     02610023
               SET AC-MISSING-RECORD TO TRUE                            02620023
               MOVE '1000-READ-DATE-CONTROL'  TO PV-PARAGRAPH-NAME      02630023
               MOVE 'DATE CONTROL RECORD IS MISSING'                    02640023
                                              TO PV-OPERATION-MSG-2     02650023
               PERFORM 9999-ABEND                                       02660023
           END-READ.                                                    02670023
                                                                        02680023
           MOVE DATE-CONTROL-REC TO CD-CONTROL-DATE.                    02690023
                                                                        02700023
           MOVE CD-POS-CENTURY   TO PV-CURR-DATE-CC.                    02710023
           MOVE CD-POS-YEAR      TO PV-CURR-DATE-YY.                    02720023
           MOVE CD-POS-MONTH     TO PV-CURR-DATE-MM.                    02730023
           MOVE CD-POS-DAY       TO PV-CURR-DATE-DD.                    02740023
                                                                        02750023
           CLOSE DATE-CONTROL-FILE.                                     02760023
                                                                        02770023
           DISPLAY 'EPKUP058 MESSAGES'.                                 02780023
           DISPLAY ' '                                                  02790023
           DISPLAY 'TODAYS CTLCD DATE       = ' PV-TODAYS-CTLCD-DATE.   02800036
                                                                        02810023
           PERFORM 1200-GET-CURRENT-PARTN1-DATE.                        02820023
           MOVE EPPART-SLS-DTE                TO PV-YESTERDAYS-DATE.    02830026
           DISPLAY ' '.                                                 02840026
           DISPLAY 'YESTERDAYS DATE WAS     = '  PV-YESTERDAYS-DATE.    02850036
                                                                        02860023
           PERFORM 1300-CTLCD-DATE-DAYS-DIFF.                           02870023
                                                                        02880023
           PERFORM 1500-GET-FUTURE-DATE.                                02890023
                                                                        02900023
                                                                        02910023
                                                                        02920023
      ******************************************************************02930023
       1200-GET-CURRENT-PARTN1-DATE.                                    02940023
      * READ THE CURRENT PROCESSING DATE (PART 1 DATE).                 02950023
      *                                                                 02960023
           EXEC SQL                                                     02970023
              SELECT SLS_DTE                                            02980023
                INTO :EPPART-SLS-DTE                                    02990023
                FROM TEPPART                                            03000023
               WHERE PARTN_NBR    = 1                                   03010023
                 AND DB_STRUC_CDE = 'O'                                 03020023
                 AND STR_GP_CDE  <> 'A'                                 03030023
           END-EXEC.                                                    03040023
                                                                        03050023
           IF SQLCODE = ZERO                                            03060023
              CONTINUE                                                  03070026
           ELSE                                                         03080023
              MOVE '1200-GET-CURRENT-PARTN1-DATE ' TO PV-PARAGRAPH-NAME 03090023
              MOVE SQLCODE                         TO PV-DB2-OPERATION  03100023
              PERFORM 9100-SQL-ABEND                                    03110023
           END-IF.                                                      03120023
                                                                        03130023
                                                                        03140023
                                                                        03150023
      ******************************************************************03160023
       1300-CTLCD-DATE-DAYS-DIFF.                                       03170023
      * COMPARE PART 1 DATE AND CTLCD DATE TO GET DAYS DIFFERANCE.      03180023
      *                                                                 03190023
      * IF CTLCD DATE (NEXT DATE) IS 1 DAY GREATER THAN YESTERDAY'S     03200030
      * PARTN 1 PROCESSING DATE, IT IS VALID.                           03210030
      *                                                                 03220023
      * OTHERWISE, IT SHOULD BE CONSIDERED INVALID AND PROCESSING       03230023
      * SHOULD NOT CONTINUE.                                            03240023
      ******************************************************************03250023
                                                                        03260023
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03270023
                                                                        03280023
           SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   03290023
           SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   03300023
           SET  DPG52-LK-DTE-ISO-FMT         TO TRUE.                   03310023
           SET  DPG53-LK-CMPR-DTE-ISO-FMT    TO TRUE.                   03320023
                                                                        03330023
           MOVE PV-TODAYS-CTLCD-DATE    TO DPG52-LK-DATE-INPUT.         03340023
           MOVE PV-YESTERDAYS-DATE      TO DPG53-LK-CMPR-DATE-INPUT.    03350023
                                                                        03360023
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    03370023
                                                   DPG54 DPG55 DPG56.   03380023
           EVALUATE TRUE                                                03390023
               WHEN DPG54-DATE-VALID                                    03400023
                   IF DPG54-CMPR-DATE-VALID-IND = 'Y' THEN              03410023
                      MOVE DPG55-DAYS-DIFFERENCE TO PV-DAYS-DIFF        03420023
                      IF DPG55-DAYS-DIFFERENCE = 1                      03430030
                         DISPLAY 'TODAYS CTLCD DATE IS 1 GREATER THAN'  03440030
                         ' YESTERDAYS DATE - IT IS VALID'               03450030
                      ELSE                                              03460023
                         MOVE '1300-CTLCD-DATE-DAYS-DIFF '              03470023
                                                  TO PV-PARAGRAPH-NAME  03480023
                         DISPLAY 'TODAYS CTLCD DATE IS NOT 1 GT YESTER' 03490030
                         'DAYS DATE - IT IS INVALID'                    03500030
                         DISPLAY 'PV-TODAYS-CTLCD-DATE    = '           03510036
                                  PV-TODAYS-CTLCD-DATE                  03520023
                         DISPLAY 'PV-YESTERDAYS-DATE      = '           03530036
                                  PV-YESTERDAYS-DATE                    03540023
                         DISPLAY 'DIFFERANCE = ' PV-DAYS-DIFF           03550023
                         SET AC-DATA-ERROR TO TRUE                      03560023
                         PERFORM 9999-ABEND                             03570023
                      END-IF                                            03580023
                   ELSE                                                 03590023
                      MOVE '1300-CTLCD-DATE-DAYS-DIFF '                 03600023
                                                  TO PV-PARAGRAPH-NAME  03610023
                      MOVE 'ERROR COMPARING DATES'                      03620023
                                                  TO PV-OPERATION-MSG-1 03630023
                      DISPLAY 'PV-TODAYS-CTLCD-DATE    = '              03640036
                               PV-TODAYS-CTLCD-DATE                     03650023
                      DISPLAY 'PV-YESTERDAYS-DATE      = '              03660036
                               PV-YESTERDAYS-DATE                       03670023
                      DISPLAY 'THE COMPARE DATE INDICATOR: '            03680023
                               DPG54-CMPR-DATE-VALID-IND                03690023
                              ' INDICATES AN INVALID DATE '             03700023
                      PERFORM 9200-CAL-ABEND                            03710023
                   END-IF                                               03720023
               WHEN OTHER                                               03730023
                   MOVE '1300-CTLCD-DATE-DAYS-DIFF '                    03740023
                                                 TO PV-PARAGRAPH-NAME   03750023
                   MOVE 'ERROR COMPARING DATES'  TO PV-OPERATION-MSG-1  03760023
                   DISPLAY 'PV-TODAYS-CTLCD-DATE    = '                 03770036
                            PV-TODAYS-CTLCD-DATE                        03780023
                   DISPLAY 'PV-YESTERDAYS-DATE      = '                 03790036
                            PV-YESTERDAYS-DATE                          03800023
                   PERFORM 9200-CAL-ABEND                               03810023
           END-EVALUATE.                                                03820023
                                                                        03830023
                                                                        03840023
      ******************************************************************03850023
       1500-GET-FUTURE-DATE.                                            03860023
      *                                                                 03870023
      *    CALL THE CALENDAR ROUTINE TO HELP INSURE THE VALIDITY OF THE 03880023
      *    THE CTLCD DATE AND TO CALCULATE THE DATE FOR THE NEXT        03890023
      *    PARTITION CYCLE TO BE ASSIGNED - THIS WILL BE 1 DAY INTO     03900023
      *    THE FUTURE.                                                  03910023
      ******************************************************************03920023
                                                                        03930023
           INITIALIZE DPG51                                             03940023
                      DPG52                                             03950023
                      DPG53                                             03960023
                      DPG54                                             03970023
                      DPG55                                             03980023
                      DPG56.                                            03990023
                                                                        04000023
           SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                      04010023
           SET DPG51-INCREMENT-DATE       TO TRUE.                      04020023
           SET DPG52-LK-DTE-ISO-FMT       TO TRUE.                      04030023
           MOVE PV-TODAYS-CTLCD-DATE      TO DPG52-LK-DATE-INPUT.       04040023
           MOVE 1                         TO DPG51-INCR-DECR-DAYS-9.    04050023
                                                                        04060023
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                04070023
                                          DPG52                         04080023
                                          DPG53                         04090023
                                          DPG54                         04100023
                                          DPG55                         04110023
                                          DPG56.                        04120023
                                                                        04130023
           EVALUATE TRUE                                                04140023
              WHEN DPG54-NO-ERROR                                       04150023
                 MOVE DPG55-DB2-ISO-DATE-FMT     TO PV-FUTURE-DATE      04160023
              WHEN OTHER                                                04170023
                   MOVE '1500-GET-FUTURE-DATE'   TO PV-PARAGRAPH-NAME   04180023
                   DISPLAY ' PV-TODAYS-CTLCD-DATE   = '                 04190036
                             PV-TODAYS-CTLCD-DATE                       04200023
                   PERFORM 9200-CAL-ABEND                               04210023
              END-EVALUATE.                                             04220023
                                                                        04230023
           DISPLAY ' '.                                                 04240023
           DISPLAY 'NEW FUTURE DATE         = ' PV-FUTURE-DATE.         04250036
                                                                        04260023
FSH13 ******************************************************************04270039
FSH13  2000-EPT-SLS-DTE-RTN.                                            04280039
FSH13 *     UPDATE THE ROW ON EPT_POS_ACTV_SLS_DTE.                     04290039
FSH13 *     IF ROW NOT FOUND, INSERT A ROW.                             04300039
FSH13 ******************************************************************04310039
FSH13                                                                   04320039
FSH13      PERFORM 2100-UPDATE-EPT-SLS-DTE.                             04330039
FSH13                                                                   04340039
FSH13      IF PS-ROW-FOUND                                              04350039
FSH13         DISPLAY                                                   04360039
FSH13            'SUCCESSFUL UPDATE OF EPT_POS_ACTV_SLS_DTE SET TO '    04370039
FSH13                                   PV-TODAYS-CTLCD-DATE            04380039
FSH13      ELSE                                                         04390039
FSH13         PERFORM 2150-INSERT-EPT-SLS-DTE                           04400039
FSH13         IF PS-ROW-FOUND                                           04410039
FSH13            DISPLAY                                                04420039
FSH13            'SUCCESSFUL INSERT OF EPT_POS_ACTV_SLS_DTE SET TO '    04430039
FSH13                                   PV-TODAYS-CTLCD-DATE            04440039
FSH13         END-IF                                                    04450039
FSH13      END-IF.                                                      04460039
FSH13                                                                   04470039
FSH13 ******************************************************************04480032
FSH13  2100-UPDATE-EPT-SLS-DTE.                                         04490039
FSH13 *     UPDATE THE ROW ON EPT_POS_ACTV_SLS_DTE.                     04500037
FSH13 ******************************************************************04510032
FSH13                                                                   04520032
FSH13      EXEC SQL                                                     04530032
FSH13         UPDATE EPT_POS_ACTV_SLS_DTE                               04540032
FSH13            SET POS_ACTV_SLS_DTE = :PV-TODAYS-CTLCD-DATE           04550034
FSH13               ,LAST_UPD_TMST    = CURRENT TIMESTAMP               04560033
FSH13      END-EXEC.                                                    04570032
FSH13                                                                   04580032
FSH13      EVALUATE TRUE                                                04590032
FSH13         WHEN SQLCODE = ZERO                                       04600032
FSH13            SET PS-ROW-FOUND     TO TRUE                           04610038
FSH13                                                                   04620032
FSH13         WHEN SQLCODE = +100                                       04630032
FSH13            SET PS-ROW-NOT-FOUND TO TRUE                           04640038
FSH13                                                                   04650032
FSH13         WHEN OTHER                                                04660032
FSH13            MOVE '2100-UPDATE-EPT-SLS-DTE'                         04670039
FSH13                                          TO PV-PARAGRAPH-NAME     04680032
FSH13            MOVE SQLCODE                  TO PV-DB2-OPERATION      04690032
FSH13            MOVE 'TRYING TO UPDATE SALES DATE '                    04700032
FSH13                                          TO PV-OPERATION-MSG-1    04710032
FSH13            PERFORM 9100-SQL-ABEND                                 04720032
FSH13      END-EVALUATE.                                                04730032
FSH13                                                                   04740032
FSH13 ******************************************************************04750032
FSH13  2150-INSERT-EPT-SLS-DTE.                                         04760039
FSH13 *     INSERT A ROW INTO EPT_POS_ACTV_SLS_DTE                      04770032
FSH13 ******************************************************************04780032
FSH13                                                                   04790032
FSH13      EXEC SQL                                                     04800032
FSH13         INSERT INTO EPT_POS_ACTV_SLS_DTE                          04810032
FSH13                  ( POS_ACTV_SLS_DTE                               04820035
FSH13                   ,LAST_UPD_TMST )                                04830035
FSH13                VALUES                                             04840032
FSH13                  ( :PV-TODAYS-CTLCD-DATE                          04850032
FSH13                   ,CURRENT TIMESTAMP )                            04860032
FSH13      END-EXEC.                                                    04870032
FSH13                                                                   04880032
FSH13      EVALUATE TRUE                                                04890038
FSH13         WHEN SQLCODE = ZERO                                       04900038
FSH13            SET PS-ROW-FOUND TO TRUE                               04910038
FSH13                                                                   04920038
FSH13         WHEN OTHER                                                04930038
FSH13            MOVE '2150-INSERT-EPT-SLS-DTE'                         04940039
FSH13                                       TO PV-PARAGRAPH-NAME        04950032
FSH13            MOVE SQLCODE               TO PV-DB2-OPERATION         04960038
FSH13            MOVE 'TRYING TO INSERT SALES DATE '                    04970038
FSH13                                       TO PV-OPERATION-MSG-1       04980032
FSH13            MOVE PV-TODAYS-CTLCD-DATE  TO PV-OPERATION-MSG-2       04990038
FSH13            PERFORM 9100-SQL-ABEND                                 05000038
FSH13      END-EVALUATE.                                                05010038
                                                                        05020031
      ******************************************************************05030031
FSH13  2200-UPDATE-PARTN1-4-SALE-DATE.                                  05040040
      *     UPDATE TEPPART TABLE CONTROL DATE, WHICH IS KEPT IN         05050031
      *     PARTITION 1.                                                05060031
      ******************************************************************05070031
                                                                        05080031
           EXEC SQL                                                     05090031
              UPDATE TEPPART                                            05100031
                 SET SLS_DTE      = :PV-TODAYS-CTLCD-DATE               05110031
               WHERE PARTN_NBR   IN (1,2,3,4)                           05120031
                 AND DB_STRUC_CDE = 'O'                                 05130031
                 AND STR_GP_CDE  <> 'A'                                 05140031
           END-EXEC.                                                    05150031
                                                                        05160031
           IF SQLCODE = ZERO                                            05170031
              CONTINUE                                                  05180031
           ELSE                                                         05190031
FSH13         MOVE '2200-UPDATE-PARTN1-4-SALE-DATE'                     05200040
                                            TO PV-PARAGRAPH-NAME        05210031
              MOVE SQLCODE                  TO PV-DB2-OPERATION         05220031
              MOVE 'TRYING TO SET PARTN 1-4 SALES DATES TO '            05230031
                                            TO PV-OPERATION-MSG-1       05240031
              MOVE PV-TODAYS-CTLCD-DATE     TO PV-OPERATION-MSG-2       05250031
              PERFORM 9100-SQL-ABEND                                    05260031
           END-IF.                                                      05270031
                                                                        05280023
      ******************************************************************05290023
       3000-GET-NEXT-PARTN-TO-ASSIGN.                                   05300024
      *     PERFORM PARA 3050-GET-UNASSIGNED-PARTN FIRST TO GET THE     05310026
      *     LOWEST UNASSIGNED OPERATIONAL PARTN NBR.                    05320026
      *     IF ALL NEW PARTNS HAVE BEEN ASSIGNED, PERFORM PARA          05330026
      *     3100-GET-OLDEST-DATE-PARTN TO FIND THE NEXT PARTN TO USE.   05340026
      *     THE DATE AND PARTITION NUMBER WILL BE USED IN PARA          05350023
      *     4000-UPDATE-OLDEST-OPER-DATE TO SET THE DATE ON THE PARTN   05360023
      *     TO THE FUTURE DATE,                                         05370023
      *     AND IN PARA 5000-WRITE-PART-NBR-CNTL-CARD TO CREATE THE     05380023
      *     FUTURE PARTITION CONTROL CARD (WHICH WILL BE USED TO CLEAR  05390025
      *     THE PARTITION).                                             05400023
      ******************************************************************05410023
                                                                        05420023
           PERFORM 3050-GET-UNASSIGNED-PARTN.                           05430026
                                                                        05440023
           IF PS-ROW-FOUND                                              05450026
              MOVE EPPART-PARTN-NBR          TO PV-OLDEST-PARTN         05460023
                                                PV-REC-FUTURE-PARTN     05470025
              MOVE EPPART-SLS-DTE            TO PV-OLDEST-PARTN-DATE    05480023
              DISPLAY ' '                                               05490023
              DISPLAY 'UNASSIGNED DATE WAS     = ' EPPART-SLS-DTE       05500036
              DISPLAY 'UNASSIGNED PARTN WAS    = ' EPPART-PARTN-NBR     05510036
           ELSE                                                         05520023
              PERFORM 3100-GET-OLDEST-DATE-PARTN                        05530026
              IF PS-ROW-FOUND                                           05540026
                 MOVE EPPART-PARTN-NBR          TO PV-OLDEST-PARTN      05550026
                                                   PV-REC-FUTURE-PARTN  05560026
                 MOVE EPPART-SLS-DTE            TO PV-OLDEST-PARTN-DATE 05570026
                 DISPLAY ' '                                            05580026
                 DISPLAY 'OLDEST DATE WAS         = ' EPPART-SLS-DTE    05590036
                 DISPLAY 'OLDEST DATE PARTN WAS   = ' EPPART-PARTN-NBR  05600036
                 PERFORM 3200-OLDEST-DATE-DAYS-DIFF                     05610026
              END-IF                                                    05620026
           END-IF.                                                      05630023
                                                                        05640023
                                                                        05650026
      ******************************************************************05660026
       3050-GET-UNASSIGNED-PARTN.                                       05670026
      *     SELECT THE LOWEST UNASSIGNED OPERATIONAL PARTN NBR.         05680026
      *     UNASSIGNED PARTN NBRS ARE INITIALIZED WITH SALES DATES      05690026
      *     = '9999-12-31'.                                             05700026
      ******************************************************************05710026
                                                                        05720026
           EXEC SQL                                                     05730026
              SELECT A.PARTN_NBR,                                       05740026
                     A.SLS_DTE                                          05750026
                INTO :EPPART-PARTN-NBR,                                 05760026
                     :EPPART-SLS-DTE                                    05770026
                FROM TEPPART A                                          05780026
               WHERE A.DB_STRUC_CDE = 'N'                               05790026
                 AND A.STR_GP_CDE   = 'N'                               05800026
                 AND A.SLS_DTE      = '9999-12-31'                      05810026
                 AND A.PARTN_NBR    =                                   05820026
                     (SELECT MIN(B.PARTN_NBR)                           05830026
                        FROM TEPPART B                                  05840026
                       WHERE B.DB_STRUC_CDE = 'N'                       05850026
                         AND B.STR_GP_CDE   = 'N'                       05860026
                         AND B.SLS_DTE      = '9999-12-31')             05870026
           END-EXEC.                                                    05880026
                                                                        05890026
           IF SQLCODE = ZERO                                            05900026
              SET PS-ROW-FOUND  TO TRUE                                 05910026
           ELSE                                                         05920026
              IF SQLCODE = +100                                         05930026
                 SET PS-ROW-NOT-FOUND  TO TRUE                          05940026
              ELSE                                                      05950026
                 MOVE '3050-GET-UNASSIGNED-PARTN' TO PV-PARAGRAPH-NAME  05960026
                 MOVE SQLCODE                     TO PV-DB2-OPERATION   05970026
                 PERFORM 9100-SQL-ABEND                                 05980026
              END-IF                                                    05990026
           END-IF.                                                      06000026
                                                                        06010026
                                                                        06020026
                                                                        06030023
      ******************************************************************06040023
       3100-GET-OLDEST-DATE-PARTN.                                      06050023
      *     (FROM EPKUT051 - PARA. 5000-)                               06060023
      *     SELECT THE OLDEST OPERATIONAL SLS_DTE AND THE PARTN_NBR     06070026
      *     ASSOCIATED WITH IT.                                         06080026
      ******************************************************************06090023
                                                                        06100023
           EXEC SQL                                                     06110023
              SELECT A.PARTN_NBR,                                       06120023
                     A.SLS_DTE                                          06130023
                INTO :EPPART-PARTN-NBR,                                 06140023
                     :EPPART-SLS-DTE                                    06150023
                FROM TEPPART A                                          06160023
               WHERE A.DB_STRUC_CDE = 'O'                               06170023
                 AND A.STR_GP_CDE   = 'A'                               06180023
                 AND A.SLS_DTE      =                                   06190023
                     (SELECT MIN(B.SLS_DTE)                             06200023
                        FROM TEPPART B                                  06210023
                       WHERE B.DB_STRUC_CDE = 'O'                       06220023
                         AND B.STR_GP_CDE   = 'A')                      06230023
           END-EXEC.                                                    06240023
                                                                        06250023
           IF SQLCODE = ZERO                                            06260023
              SET PS-ROW-FOUND  TO TRUE                                 06270026
           ELSE                                                         06280023
              MOVE '3100-GET-OLDEST-DATE-PARTN' TO PV-PARAGRAPH-NAME    06290023
              MOVE SQLCODE                      TO PV-DB2-OPERATION     06300023
              PERFORM 9100-SQL-ABEND                                    06310023
           END-IF.                                                      06320023
                                                                        06330023
                                                                        06340023
      ******************************************************************06350023
       3200-OLDEST-DATE-DAYS-DIFF.                                      06360023
      * COMPARE THE OLDEST PARTITION DATE FOUND TO THE PARTITION 1      06370023
C62728* CONTROL DATE.  IF THE DIFFERENCE IS NOT AT LEAST 493 DAYS,      06380043
      * THERE MAY BE A PROBLEM AND WE DO NOT WANT TO CLEAR THE          06390023
      * OLDEST DATE'S PARTITION - STOP PROCESSING.                      06400023
      * THIS COULD INDICATE THAT THE NEW OPERATIONAL PARTITIONS         06410023
      * WEREN'T INITIALIZED PROPERLY.                                   06420023
      * THIS PARA WILL BE REQUIRED WHEN AN OLD PARTN IS TO BE OVERLAID  06430023
      * (PARTN FOUND IN PARA 3100-GET-OLDEST-DATE-PARTN),               06440023
      * BUT IS NOT REQUIRED WHEN AN UNASSIGNED PARTN IS FOUND IN PARA   06450023
      * 3050-GET-UNASSIGNED-PARTN.                                      06460026
      ******************************************************************06470023
                                                                        06480023
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              06490023
                                                                        06500023
           SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   06510023
           SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   06520023
           SET  DPG52-LK-DTE-ISO-FMT         TO TRUE.                   06530023
           SET  DPG53-LK-CMPR-DTE-ISO-FMT    TO TRUE.                   06540023
                                                                        06550023
           MOVE PV-YESTERDAYS-DATE      TO DPG52-LK-DATE-INPUT.         06560023
           MOVE PV-OLDEST-PARTN-DATE    TO DPG53-LK-CMPR-DATE-INPUT.    06570023
                                                                        06580023
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    06590023
                                                   DPG54 DPG55 DPG56.   06600023
           EVALUATE TRUE                                                06610023
               WHEN DPG54-DATE-VALID                                    06620023
                   IF DPG54-CMPR-DATE-VALID-IND = 'Y' THEN              06630023
                      MOVE DPG55-DAYS-DIFFERENCE TO PV-DAYS-DIFF        06640023
C62728                IF  DPG55-DAYS-DIFFERENCE > 493                   06650043
C62728                   DISPLAY 'OLDEST PARTN DATE FOUND IS GT 493'    06660043
                            ' DAYS OLD - IT IS VALID'                   06670023
                      ELSE                                              06680023
                         MOVE '3200-OLDEST-DATE-DAYS-DIFF '             06690023
                                                  TO PV-PARAGRAPH-NAME  06700023
                         DISPLAY 'OLDEST PARTN DATE FOUND IS NOT GT '   06710023
C62728                      '493 DAYS OLD - IT IS INVALID'              06720043
                         DISPLAY 'PV-YESTERDAYS-DATE      = '           06730036
                                  PV-YESTERDAYS-DATE                    06740023
                         DISPLAY 'PV-OLDEST-PARTN-DATE    = '           06750036
                                  PV-OLDEST-PARTN-DATE                  06760023
                         DISPLAY 'DAYS DIFFERANCE         = '           06770036
                                  PV-DAYS-DIFF                          06780023
                         SET AC-DATA-ERROR TO TRUE                      06790023
                         PERFORM 9999-ABEND                             06800023
                      END-IF                                            06810023
                   ELSE                                                 06820023
                      MOVE '3200-OLDEST-DATE-DAYS-DIFF '                06830023
                                                  TO PV-PARAGRAPH-NAME  06840023
                      MOVE 'ERROR COMPARING DATES'                      06850023
                                                  TO PV-OPERATION-MSG-1 06860023
                      DISPLAY 'PV-YESTERDAYS-DATE      = '              06870036
                               PV-YESTERDAYS-DATE                       06880023
                      DISPLAY 'PV-OLDEST-PARTN-DATE    = '              06890036
                               PV-OLDEST-PARTN-DATE                     06900023
                      DISPLAY 'THE COMPARE DATE INDICATOR: '            06910023
                               DPG54-CMPR-DATE-VALID-IND                06920023
                              ' INDICATES AN INVALID DATE '             06930023
                      PERFORM 9200-CAL-ABEND                            06940023
                   END-IF                                               06950023
               WHEN OTHER                                               06960023
                   MOVE '3200-OLDEST-DATE-DAYS-DIFF '                   06970023
                                                 TO PV-PARAGRAPH-NAME   06980023
                   MOVE 'ERROR COMPARING DATES'  TO PV-OPERATION-MSG-1  06990023
                   DISPLAY 'PV-YESTERDAYS-DATE      = '                 07000036
                            PV-YESTERDAYS-DATE                          07010023
                   DISPLAY 'PV-OLDEST-PARTN-DATE    = '                 07020036
                            PV-OLDEST-PARTN-DATE                        07030023
                   PERFORM 9200-CAL-ABEND                               07040023
           END-EVALUATE.                                                07050023
                                                                        07060023
                                                                        07070023
      ******************************************************************07080023
       4000-UPDATE-OLDEST-OPER-DATE.                                    07090023
      *     (FROM EPKUT052 - PARA. 5000-)                               07100023
      *     UPDATE EITHER:                                              07110023
      *                                                                 07120023
      *     THE UNASSIGNED PARTN WITH THE LOWEST PARTN NBR (DETERMINED  07130023
      *     IN PARA 3050-GET-UNASSIGNED-PARTN)                          07140026
      *         O R                                                     07150023
      *     THE OLDEST OPERATIONAL SALES DATE (DETERMINED IN            07160023
      *     PARA 3100-GET-OLDEST-DATE-PARTN)                            07170024
      *                                                                 07180023
      *     WITH THE FUTURE PARTITION DATE.                             07190026
      ******************************************************************07200023
                                                                        07210023
           EXEC SQL                                                     07220023
              UPDATE TEPPART                                            07230023
                 SET SLS_DTE               = :PV-FUTURE-DATE            07240023
                   , STR_GP_CDE            = 'A'                        07250023
                   , DB_STRUC_CDE          = 'O'                        07260023
               WHERE PARTN_NBR             = :PV-OLDEST-PARTN           07270023
                 AND SLS_DTE               = :PV-OLDEST-PARTN-DATE      07280023
                 AND DB_STRUC_CDE         <> 'H'                        07290023
           END-EXEC.                                                    07300023
                                                                        07310023
           IF SQLCODE = ZERO                                            07320023
              SET PS-ROW-FOUND              TO TRUE                     07330026
           ELSE                                                         07340023
              MOVE '4000-UPDATE-OLDEST-OPER-DATE' TO PV-PARAGRAPH-NAME  07350023
              MOVE SQLCODE                  TO PV-DB2-OPERATION         07360023
              STRING 'UPDATING PARTN '                                  07370023
                      PV-REC-FUTURE-PARTN                               07380025
                     ' OLD SALES DATE'                                  07390023
                     DELIMITED BY SIZE                                  07400023
                     INTO PV-OPERATION-MSG-1                            07410023
              STRING PV-OLDEST-PARTN-DATE                               07420023
                     ' TO '                                             07430023
                     PV-FUTURE-DATE                                     07440023
                     DELIMITED BY SIZE                                  07450023
                     INTO PV-OPERATION-MSG-2                            07460023
              PERFORM 9100-SQL-ABEND                                    07470023
           END-IF.                                                      07480023
                                                                        07490023
                                                                        07500023
      ******************************************************************07510023
       5000-WRITE-PART-NBR-CNTL-CARD.                                   07520023
      *     (FROM EPKUT051 - PARA. 6000-)                               07530023
      *     WRITE THE PARTITION NUMBER CONTROL CARD WHICH CONTAINS      07540023
      *     THE PARTITION NUMBER TO BE CLEARED OUT AND USED FOR THE     07550023
      *     NEXT FUTURE DATE (TOMORROW).                                07560023
      ******************************************************************07570023
                                                                        07580023
           OPEN OUTPUT FUTURE-PARTN-FILE-OUT.                           07590025
                                                                        07600023
           WRITE FUTURE-PARTN-RECORD     FROM PV-FUTURE-PARTN-RECORD.   07610025
                                                                        07620023
           CLOSE FUTURE-PARTN-FILE-OUT.                                 07630025
                                                                        07640023
                                                                        07650023
      ******************************************************************07660023
       9100-SQL-ABEND.                                                  07670023
      *     SQL ABEND CODE.                                             07680023
      ******************************************************************07690023
                                                                        07700023
           DISPLAY '** ABEND     **'.                                   07710023
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                07720023
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              07730023
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               07740023
           DISPLAY '** DATA      ** = ' PV-OPERATION-MSG-1.             07750023
           DISPLAY '** DATA      ** = ' PV-OPERATION-MSG-2.             07760023
                                                                        07770023
      *************** SQLCA MESSAGE DISPLAY COPY BOOK ******************07780023
                                                                        07790023
           COPY DPPD004.                                                07800023
                                                                        07810023
           SET AC-DB2-ERROR TO TRUE.                                    07820023
           CALL 'ILBOABN0' USING ABEND-CODE.                            07830023
                                                                        07840023
                                                                        07850023
      ******************************************************************07860023
      * 9200-CAL-ABEND                                                  07870023
      *     ABEND PROGRAM WHEN A CALENDAR ROUTINE ERROR OCCURS.         07880023
      ******************************************************************07890023
       9200-CAL-ABEND.                                                  07900023
                                                                        07910023
           DISPLAY '** ABEND           **'.                             07920023
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          07930023
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        07940023
           DISPLAY '** DATA            ** = ' PV-OPERATION-MSG-1.       07950023
           DISPLAY '** DATA            ** = ' PV-OPERATION-MSG-2.       07960023
           DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'            07970023
           SET AC-CALENDAR-ROUTINE-ERROR  TO TRUE.                      07980023
           DISPLAY '** ERROR MESSAGE      = ' DPG54-ERROR-MESSAGE.      07990023
           DISPLAY '** DPG51 = ' DPG51.                                 08000023
           DISPLAY '** DPG52 = ' DPG52.                                 08010023
           DISPLAY '** DPG53 = ' DPG53.                                 08020023
           DISPLAY '** DPG54 = ' DPG54.                                 08030023
           DISPLAY '** DPG55 = ' DPG55.                                 08040023
           DISPLAY '** DPG56 = ' DPG56.                                 08050023
           CALL 'ILBOABN0' USING ABEND-CODE.                            08060023
                                                                        08070023
                                                                        08080023
      ******************************************************************08090023
      * 9999-ABEND                                                      08100023
      *     ABEND PROGRAM WHEN A NON-DB2 AND NON-CALENDAR ROUTINE       08110023
      *     ERROR OCCURS.                                               08120023
      ******************************************************************08130023
       9999-ABEND.                                                      08140023
                                                                        08150023
           DISPLAY '** ABEND           **'.                             08160023
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          08170023
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        08180023
           DISPLAY '** DATA            ** = ' PV-OPERATION-MSG-1.       08190023
           DISPLAY '** DATA            ** = ' PV-OPERATION-MSG-2.       08200023
           CALL 'ILBOABN0' USING ABEND-CODE.                            08210023
                                                                        08220023
      ******************* END OF EPKUP058 ******************************08230023
