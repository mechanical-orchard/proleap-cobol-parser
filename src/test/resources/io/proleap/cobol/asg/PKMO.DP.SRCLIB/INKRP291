       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKRP291.                                       
       AUTHOR.                  D. THEEL.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            11/20/2008.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  INVENTORY STORE-EXPECTED CUMULATIVE SUMMARY REPORT BY STORE   *        
      *  THIS PROGRAM WILL CREATE THE INK31125 REPORT THAT WAS BEING   *        
      *  CREATED IN INKRP191.  THE REPORT WILL BREAK AND TOTAL BY      *        
      *  CYCLE ID.  IT WILL ALSO CREATE A GRAND TOTAL PAGE WITH THE    *        
      *  CYCLE ID OF 99999.  THE INPUT FILE HAS BEEN SORTED/SUMMED BY  *        
      *  OPEN FISCAL MONTH END DATE, CYCLE ID, ADJUSTMENT DATE, STORE. *        
      *CHANGE HISTORY:                                                 *        
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES          *        
      * ------- ---------- ----------- --------------------------------*        
      * W33304  11-29-2008 D. THEEL    INITIAL IMPLEMENTATION FOR HILO.*        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT STORE-EXPECTED-IN-FILE                                        
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT STORE-EXPECTED-RPT                                            
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  STORE-EXPECTED-IN-FILE                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-EXPECTED-IN-REC           PIC X(75).                           
                                                                                
       FD  STORE-EXPECTED-RPT                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS REPORT-RECORD.                                        
       01  REPORT-RECORD                   PIC X(132).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      ******************************************************************        
      *  HEADER LINE                                                            
      ******************************************************************        
      *01  DP132O-STANDRD-HEADER-132-COLS.                                      
      *01  DP132O-CNFDL-HDR-132-COLS.                                           
           COPY DPWS132O.                                                       
                                                                                
       01  H1-HEADING.                                                          
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(37)       VALUE SPACES.        
           05  FILLER                      PIC X(24)       VALUE                
               'INVENTORY STORE-EXPECTED'.                                      
           05  FILLER                      PIC X(11)       VALUE                
                   ' CUMULATIVE'.                                               
           05  FILLER                      PIC X(24)       VALUE                
                   ' SUMMARY REPORT BY STORE'.                                  
           05  FILLER                      PIC X(35)       VALUE SPACES.        
                                                                                
       01  H2-HEADING.                                                          
           05  FILLER              PIC X(06)   VALUE ' CYCLE'.                  
           05  FILLER              PIC X(01)   VALUE SPACE.                     
           05  H2-INV-ID           PIC 9(05)   VALUE ZERO.                      
           05  FILLER              PIC X(03)   VALUE SPACE.                     
           05  FILLER              PIC X(40)   VALUE SPACE.                     
           05  H2-INV-NAME-TEXT    PIC X(20)   VALUE SPACE.                     
           05  FILLER              PIC X(57)   VALUE SPACE.                     
                                                                                
       01  H3-HEADING.                                                          
           05  FILLER                      PIC X(25)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE                
                   'LOCATION  '.                                                
           05  FILLER                      PIC X(20)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE                
               ' ADJ DATE '.                                                    
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(15)       VALUE                
               '      QUANTITY '.                                               
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(21)       VALUE                
               '     EXTENDED RETAIL '.                                         
           05  FILLER                      PIC X(25)       VALUE SPACES.        
                                                                                
       01  H4-HEADING.                                                          
           05  FILLER                      PIC X(25)      VALUE SPACES.         
           05  FILLER                      PIC X(30)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(10)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(15)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(21)      VALUE ALL '-'.        
           05  FILLER                      PIC X(25)      VALUE SPACES.         
                                                                                
       01  DETAIL-LINE.                                                         
           05  FILLER                      PIC X(25)       VALUE SPACES.        
           05  DT-LOC-NBR-9                PIC 9(04)       VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  DT-DESCRIPTION              PIC X(25)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  DT-ADJUST-DATE              PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  DT-ADJ-QTY                  PIC ZZ,ZZZ,ZZZ,ZZ9-                  
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  DT-EXTD-RTL-AMT             PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-            
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(25)       VALUE SPACES.        
                                                                                
       01  CYCLE-TOTAL-LINE.                                                    
           05  FILLER                      PIC X(26)       VALUE SPACES.        
           05  FILLER                      PIC X(20)       VALUE                
               '     CYCLE TOTAL:   '.                                          
           05  FILLER                      PIC X(09)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  CT-ADJ-QTY                  PIC ZZ,ZZZ,ZZZ,ZZ9-                  
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  CT-EXTD-RTL-AMT             PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-            
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(25)       VALUE SPACES.        
                                                                                
       01  GRAND-TOTAL-LINE.                                                    
           05  FILLER                      PIC X(26)       VALUE SPACES.        
           05  FILLER                      PIC X(20)       VALUE                
               '     GRAND TOTAL:   '.                                          
           05  FILLER                      PIC X(09)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  GT-ADJ-QTY                  PIC ZZ,ZZZ,ZZZ,ZZ9-                  
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  GT-EXTD-RTL-AMT             PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.99-            
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(25)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-END.                                                       
           05  FILLER                      PIC X(46)       VALUE SPACES.        
           05  FILLER                      PIC X(39)       VALUE                
               '*****  E N D   O F   R E P O R T  *****'.                       
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-PRINT-LINE-MAX           PIC S9(04)      VALUE +57            
                               COMP.                                            
           05  PC-MAX-RETRIES            PIC S9(04)   VALUE +3                  
                                                      COMP SYNC.                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-ADJ-FILE          PIC X           VALUE 'N'.           
               88  END-OF-ADJ-FILE                         VALUE 'Y'.           
               88  NOT-END-OF-ADJ-FILE                     VALUE 'N'.           
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-PAGE-COUNT               PIC S9(05)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PRINT-LINE-COUNT         PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-ADVANCE-LINE             PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-CC        PIC X(02).                           
               10  PV-PROCESSING-YY        PIC X(02).                           
               10  PV-PROC-DSH1            PIC X(01).                           
               10  PV-PROCESSING-MM        PIC X(02).                           
               10  PV-PROC-DSH2            PIC X(01).                           
               10  PV-PROCESSING-DD        PIC X(02).                           
           05  PV-DATE-HOLD-CCYY-MM-DD     PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES PV-DATE-HOLD-CCYY-MM-DD.                        
               10  PV-DATE-HOLD-CCYY       PIC X(04).                           
               10  FILLER REDEFINES PV-DATE-HOLD-CCYY.                          
                   15  PV-DATE-HOLD-CC     PIC X(02).                           
                   15  PV-DATE-HOLD-YY     PIC X(02).                           
               10  PV-DATE-HOLD-DASH-1     PIC X(01).                           
               10  PV-DATE-HOLD-MM         PIC X(02).                           
               10  PV-DATE-HOLD-DASH-2     PIC X(01).                           
               10  PV-DATE-HOLD-DD         PIC X(02).                           
           05  PV-DATE-RFMT-MM-DD-CCYY     PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES PV-DATE-RFMT-MM-DD-CCYY.                        
               10  PV-DATE-RFMT-MM         PIC X(02).                           
               10  PV-DATE-RFMT-DASH-1     PIC X(01).                           
               10  PV-DATE-RFMT-DD         PIC X(02).                           
               10  PV-DATE-RFMT-DASH-2     PIC X(01).                           
               10  PV-DATE-RFMT-CCYY       PIC X(04).                           
               10  FILLER REDEFINES PV-DATE-RFMT-CCYY.                          
                   15  PV-DATE-RFMT-CC     PIC X(02).                           
                   15  PV-DATE-RFMT-YY     PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
           05  PV-TODAYS-TIME.                                                  
               10  PV-TODAYS-TIME-HR       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-MIN      PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-SEC      PIC 9(02)       VALUE ZEROES.        
               10  FILLER                  PIC 9(02)       VALUE ZEROES.        
           05  PV-CURR-LOC-NBR             PIC X(5)        VALUE SPACES.        
           05  PV-CURR-LOC-NBR-9 REDEFINES PV-CURR-LOC-NBR                      
                                           PIC 9(5).                            
           05  PV-CYCLE-ADJ-QTY            PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-CYCLE-EXTD-RTL-AMT       PIC S9(13)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-GRD-TTL-ADJ-QTY          PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-GRD-TTL-EXTD-RTL-AMT     PIC S9(13)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-READ-COUNT               PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-RETRY-COUNT               PIC S9(04)   VALUE ZERO.            
           05  PV-HOLD-INV-ID          PIC S9(09)  COMP VALUE +0.               
           05  PV-TEMP-NBR-10-COMP     PIC S9(10) COMP VALUE ZERO.              
           05  PV-TEMP-NBR-10-NUM      PIC 9(10)   VALUE ZERO.                  
           05  FILLER REDEFINES  PV-TEMP-NBR-10-NUM.                            
               10  FILLER        PIC 9(05).                                     
               10  PV-REPORT-INV-ID                                             
                                 PIC 9(05).                                     
                                                                                
      *****************************************************************         
      *  INPUT STORE EXPECTED CUM FILE RECORD LAYOUT                            
      *****************************************************************         
           COPY INRD237.                                                        
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  SQL-ABEND-4013                              VALUE +4013.         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TINCNTL - PHYSICAL INVENTORY CONTROL MASTER TABLE                    
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    XMT_LOC - STORE LOCATION TABLE                             *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN RPOCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM B200-PROCESS-REPORT                                          
              UNTIL END-OF-ADJ-FILE.                                            
                                                                                
           PERFORM B900-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
                                                                                
           MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
           MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
           MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
                                                                                
           MOVE '-' TO PV-PROC-DSH1.                                            
           MOVE '-' TO PV-PROC-DSH2.                                            
                                                                                
           IF PV-TODAYS-DATE-YY > '80'                                          
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC                                    
           END-IF.                                                              
                                                                                
           MOVE PV-TODAYS-DATE-MM  TO DP132O-RUN-MONTH.                         
           MOVE PV-TODAYS-DATE-DD  TO DP132O-RUN-DAY.                           
           MOVE PV-TODAYS-DATE-YY  TO DP132O-RUN-YEAR.                          
           MOVE PV-TODAYS-TIME-HR  TO DP132O-RUN-HOUR.                          
           MOVE PV-TODAYS-TIME-MIN TO DP132O-RUN-MINUTE.                        
           MOVE 'INKRP291'         TO DP132O-PROGRAM-NAME.                      
           MOVE 1                  TO DP132O-REPORT-NUMBER.                     
                                                                                
           OPEN INPUT  STORE-EXPECTED-IN-FILE                                   
                OUTPUT STORE-EXPECTED-RPT.                                      
                                                                                
           PERFORM Z100-READ-ADJ-FILE.                                          
           PERFORM Z200-GET-CYCLE-NAME.                                         
           MOVE IN237-INV-ID             TO PV-HOLD-INV-ID.                     
           MOVE PV-HOLD-INV-ID           TO PV-TEMP-NBR-10-COMP.                
           MOVE PV-TEMP-NBR-10-COMP      TO PV-TEMP-NBR-10-NUM.                 
           MOVE PV-REPORT-INV-ID         TO H2-INV-ID.                          
           MOVE INCNTL-INV-NM-TEXT (1:INCNTL-INV-NM-LEN)                        
                                                 TO H2-INV-NAME-TEXT.           
           PERFORM Z450-PRINT-HEADINGS.                                         
                                                                                
      ******************************************************************        
      *    PROCESS REPORT                                                       
      ******************************************************************        
       B200-PROCESS-REPORT.                                                     
           IF END-OF-ADJ-FILE                                                   
               CONTINUE                                                         
           ELSE                                                                 
             IF IN237-INV-ID NOT EQUAL PV-HOLD-INV-ID                           
  ************PRINT OLD CYCLE TOTAL THEN NEW CYCLE HEADING                      
                 PERFORM Z650-PRINT-CYCLE-TOTAL                                 
                 PERFORM Z200-GET-CYCLE-NAME                                    
                 MOVE IN237-INV-ID        TO PV-HOLD-INV-ID                     
                 MOVE PV-HOLD-INV-ID      TO PV-TEMP-NBR-10-COMP                
                 MOVE PV-TEMP-NBR-10-COMP TO PV-TEMP-NBR-10-NUM                 
                 MOVE PV-REPORT-INV-ID    TO H2-INV-ID                          
                 MOVE INCNTL-INV-NM-TEXT (1:INCNTL-INV-NM-LEN)                  
                                          TO H2-INV-NAME-TEXT                   
                 PERFORM Z450-PRINT-HEADINGS                                    
                 PERFORM Z550-PRINT-DETAIL                                      
             ELSE                                                               
                 PERFORM Z550-PRINT-DETAIL                                      
             END-IF                                                             
             PERFORM Z100-READ-ADJ-FILE                                         
           END-IF.                                                              
                                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B900-FINALIZE.                                                           
           PERFORM Z650-PRINT-CYCLE-TOTAL.                                      
           PERFORM Z750-PRINT-GRAND-TOTAL.                                      
                                                                                
           CLOSE   STORE-EXPECTED-IN-FILE                                       
                   STORE-EXPECTED-RPT.                                          
                                                                                
           DISPLAY ' *** INPUT RECS READ COUNT: ' PV-READ-COUNT.                
                                                                                
      ******************************************************************        
      *    REFORMAT DATE FOR OUTPUT FROM CCYY-MM-DD TO MM-DD-CCYY               
      ******************************************************************        
       E100-REFORMAT-DATE.                                                      
                                                                                
           MOVE PV-DATE-HOLD-CCYY TO PV-DATE-RFMT-CCYY.                         
           MOVE PV-DATE-HOLD-MM   TO PV-DATE-RFMT-MM.                           
           MOVE PV-DATE-HOLD-DD   TO PV-DATE-RFMT-DD.                           
           MOVE PV-DATE-HOLD-DASH-1 TO PV-DATE-RFMT-DASH-1.                     
           MOVE PV-DATE-HOLD-DASH-2 TO PV-DATE-RFMT-DASH-2.                     
                                                                                
                                                                                
      ******************************************************************        
      *    READ THE NEXT RECORD FROM STORE-EXPECTED-ADJ-FILE                    
      ******************************************************************        
       Z100-READ-ADJ-FILE.                                                      
                                                                                
           READ STORE-EXPECTED-IN-FILE                                          
               INTO IN237-STR-EXPTED-RECORD                                     
               AT END SET END-OF-ADJ-FILE TO TRUE                               
               NOT AT END ADD +1          TO PV-READ-COUNT.                     
                                                                                
                                                                                
      ******************************************************************        
      *    GET CYCLE NAME                                                       
      ******************************************************************        
       Z200-GET-CYCLE-NAME.                                                     
           MOVE ZERO TO PV-RETRY-COUNT.                                         
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                          
                    OR SQLCODE = +0                                             
                                                                                
           EXEC SQL                                                             
                SELECT INV_NM                                                   
                  INTO :INCNTL-INV-NM                                           
                  FROM TINCNTL                                                  
                 WHERE INV_ID = :IN237-INV-ID                                   
                WITH UR                                                         
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
               WHEN -911                                                        
               WHEN -913                                                        
                    CONTINUE                                                    
               WHEN OTHER                                                       
                   MOVE IN237-INV-ID    TO PV-TEMP-NBR-10-COMP                  
                   MOVE PV-TEMP-NBR-10-COMP TO PV-TEMP-NBR-10-NUM               
                   DISPLAY '*****'                                              
                   DISPLAY 'INV ID ' PV-TEMP-NBR-10-NUM ' NOT FOUND'            
                   DISPLAY '*****'                                              
                   MOVE 'Z200-GET-CYCLE-NAME' TO PV-OPER-ATTEMPTED              
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE                                                         
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE 'TINCNTL MAX RETRIES EXCEEDED'                              
                                            TO PV-OPER-ATTEMPTED                
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *    GET LOCATION INFORMATION                                             
      ******************************************************************        
       Z300-GET-LOC-NAME.                                                       
           MOVE IN237-STORE-NBR-9 TO XMT00001-LOC-NBR.                          
           MOVE ZERO TO PV-RETRY-COUNT.                                         
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                          
                    OR SQLCODE = +0                                             
                                                                                
           EXEC SQL                                                             
                SELECT LOC_NM                                                   
                  INTO :XMT00001-LOC-NM                                         
                FROM   XMT_LOC                                                  
                WHERE  LOC_NBR = :XMT00001-LOC-NBR                              
                WITH UR                                                         
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
               WHEN -911                                                        
               WHEN -913                                                        
                    CONTINUE                                                    
               WHEN OTHER                                                       
                   MOVE XMT00001-LOC-NBR TO PV-CURR-LOC-NBR-9                   
                   DISPLAY ' *** INVALID STORE: ' PV-CURR-LOC-NBR               
                   MOVE 'Z300-GET-LOC-NAME' TO PV-OPER-ATTEMPTED                
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE                                                         
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE 'XMT_LOC MAX RETRIES EXCEEDED'                              
                                            TO PV-OPER-ATTEMPTED                
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *    PRINT THE HEADINGS                                                   
      ******************************************************************        
       Z450-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1 TO PV-PAGE-COUNT.                                              
           MOVE PV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                            
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE REPORT-RECORD FROM H1-HEADING.                                 
                                                                                
           WRITE REPORT-RECORD FROM H2-HEADING                                  
               AFTER ADVANCING 2 LINES.                                         
           WRITE REPORT-RECORD FROM H3-HEADING.                                 
           WRITE REPORT-RECORD FROM H4-HEADING.                                 
                                                                                
           MOVE 7 TO PV-PRINT-LINE-COUNT.                                       
           MOVE 2 TO PV-ADVANCE-LINE.                                           
                                                                                
                                                                                
      ******************************************************************        
      *    PRINT THE DETAIL LINE                                                
      ******************************************************************        
       Z550-PRINT-DETAIL.                                                       
                                                                                
           IF PV-PRINT-LINE-COUNT + PV-ADVANCE-LINE                             
                   > PC-PRINT-LINE-MAX                                          
               PERFORM Z450-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           PERFORM Z300-GET-LOC-NAME.                                           
           MOVE IN237-STORE-NBR-9 TO DT-LOC-NBR-9.                              
           MOVE XMT00001-LOC-NM   TO DT-DESCRIPTION.                            
           MOVE IN237-ADJ-DATE    TO PV-DATE-HOLD-CCYY-MM-DD.                   
           PERFORM E100-REFORMAT-DATE.                                          
           MOVE PV-DATE-RFMT-MM-DD-CCYY TO DT-ADJUST-DATE.                      
           MOVE IN237-ADJ-QTY     TO DT-ADJ-QTY.                                
           MOVE IN237-EXTD-RTL-AMT TO DT-EXTD-RTL-AMT.                          
           ADD IN237-ADJ-QTY      TO PV-CYCLE-ADJ-QTY                           
                                     PV-GRD-TTL-ADJ-QTY.                        
           ADD IN237-EXTD-RTL-AMT TO PV-CYCLE-EXTD-RTL-AMT                      
                                         PV-GRD-TTL-EXTD-RTL-AMT.               
           WRITE REPORT-RECORD FROM DETAIL-LINE                                 
               AFTER ADVANCING PV-ADVANCE-LINE LINES.                           
                                                                                
           ADD  PV-ADVANCE-LINE   TO PV-PRINT-LINE-COUNT.                       
           MOVE 1                 TO PV-ADVANCE-LINE.                           
           INITIALIZE DETAIL-LINE.                                              
                                                                                
                                                                                
      ******************************************************************        
      *    PRINT THE TOTAL FOR THIS CYCLE                                       
      ******************************************************************        
       Z650-PRINT-CYCLE-TOTAL.                                                  
           IF PV-PRINT-LINE-COUNT + PV-ADVANCE-LINE                             
                   > PC-PRINT-LINE-MAX                                          
               PERFORM Z450-PRINT-HEADINGS                                      
           END-IF.                                                              
           MOVE PV-CYCLE-ADJ-QTY           TO CT-ADJ-QTY.                       
           MOVE PV-CYCLE-EXTD-RTL-AMT      TO CT-EXTD-RTL-AMT.                  
                                                                                
           MOVE 2                 TO PV-ADVANCE-LINE.                           
           WRITE REPORT-RECORD FROM CYCLE-TOTAL-LINE                            
               AFTER ADVANCING PV-ADVANCE-LINE LINES.                           
                                                                                
           ADD  PV-ADVANCE-LINE TO PV-PRINT-LINE-COUNT.                         
           MOVE 1                 TO PV-ADVANCE-LINE.                           
           INITIALIZE CYCLE-TOTAL-LINE.                                         
           MOVE ZEROES TO PV-CYCLE-ADJ-QTY.                                     
           MOVE ZEROES TO PV-CYCLE-EXTD-RTL-AMT.                                
                                                                                
                                                                                
      ******************************************************************        
      *    PRINT THE GRAND TOTAL AFTER PAGE BREAK WITH CYCLE 99999              
      ******************************************************************        
       Z750-PRINT-GRAND-TOTAL.                                                  
           MOVE 99999               TO H2-INV-ID.                               
           MOVE 'GRAND TOTAL'       TO H2-INV-NAME-TEXT.                        
           PERFORM Z450-PRINT-HEADINGS.                                         
           MOVE PV-GRD-TTL-ADJ-QTY  TO GT-ADJ-QTY.                              
           MOVE PV-GRD-TTL-EXTD-RTL-AMT TO GT-EXTD-RTL-AMT.                     
           WRITE REPORT-RECORD FROM GRAND-TOTAL-LINE                            
               AFTER ADVANCING PV-ADVANCE-LINE LINES.                           
           MOVE 1                 TO PV-ADVANCE-LINE.                           
           WRITE REPORT-RECORD FROM RP-REPORT-END                               
               AFTER ADVANCING PV-ADVANCE-LINE LINES.                           
                                                                                
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                                    
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           SET SQL-ABEND-4013         TO TRUE.                                  
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKRP291'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM Z999-ABEND.                                                  
                                                                                
      ******************************************************************        
      *    FORCE ABEND                                                          
      ******************************************************************        
       Z999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
