      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    IMKCS314.                                                 
       AUTHOR.        JACK KRAMER.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  NOVEMBER 2005.                                            
       DATE-COMPILED.                                                           
      *----------------------------------------------------------------*        
      *    I314 - ADD BRAND VENDOR RELATIONSHIP                        *        
      *                                                                *        
      *    THIS PROGRAM ALLOWS THE USER TO ADD A BRAND VENDOR          *        
      *    RELATIONSHIP.                                               *        
      *----------------------------------------------------------------*        
      *    CHANGE LOG:                                                 *        
      *    DATE  01/16/2023                                            *        
      *               - CHANGE CALL OF 'DPKCS030' TO MAKE THE CALL     *        
      *                 DYNAMIC VS STATIC                              *        
      *        MADE BY: JEAN KURK               WR/IR #: CHG0276953    *        
      *----------------------------------------------------------------*        
                                                                                
       DATA DIVISION.                                                           
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  DK-DB2-KEYS.                                                         
           05  DK-DEPT                        PIC S9(4)V  COMP-3.               
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-MAP-NAME      PIC  X(08)  VALUE  'IM314A  '.          
           05  PC-CURRENT-MAPSET-NAME   PIC  X(08)  VALUE  'IMKM314 '.          
           05  PC-CURRENT-PROGRAM-NAME  PIC  X(08)  VALUE  'IMKCS314'.          
           05  PC-MAX-RETRIES           PIC S9(04)  VALUE  +3                   
                                                    COMP.                       
           05  PC-VENDOR-TYPE            PIC  X(01)  VALUE 'M'.                 
           05  PC-ENTNME-TYP-CDE         PIC  X(02)  VALUE '01'.                
           05  PC-OPERCO-NBR             PIC  X(02)  VALUE '03'.                
           05  PC-DPT-LITERAL            PIC  X(03)  VALUE 'DPT'.               
           05  PC-CHARTC-LITERAL         PIC S9(04)  VALUE +03                  
                                                     COMP SYNC.                 
           05  PC-UNKNOWN-BRAND-ID            PIC S9(9)   VALUE +2230           
                                                     COMP SYNC.                 
           05  PC-UNKNOWN-VENDOR-ID           PIC S9(9)   VALUE +5161           
                                                          COMP SYNC.            
           05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
           05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
           05  PC-DTL-QUEUE-SEQ               PIC  9(01)  VALUE 3.              
           05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.            
           05  PC-LIST-FORMAT                 PIC  X(01)  VALUE '2'.            
           05  PC-TSYMSG-NUMBERS.                                               
               10  PC-TSYMSG-00007      PIC  9(05)  VALUE  00007.               
               10  PC-TSYMSG-00009      PIC  9(05)  VALUE  00009.               
               10  PC-TSYMSG-00034      PIC  9(05)  VALUE  00034.               
               10  PC-TSYMSG-00069      PIC  9(05)  VALUE  00069.               
               10  PC-TSYMSG-00070      PIC  9(05)  VALUE  00070.               
               10  PC-TSYMSG-00148      PIC  9(05)  VALUE  00148.               
               10  PC-TSYMSG-00564      PIC  9(05)  VALUE  00564.               
               10  PC-TSYMSG-02156      PIC  9(05)  VALUE  02156.               
               10  PC-TSYMSG-03131      PIC  9(05)  VALUE  03131.               
               10  PC-TSYMSG-03449      PIC  9(05)  VALUE  03449.               
               10  PC-TSYMSG-03450      PIC  9(05)  VALUE  03450.               
               10  PC-TSYMSG-03452      PIC  9(05)  VALUE  03452.               
               10  PC-TSYMSG-03453      PIC  9(05)  VALUE  03453.               
               10  PC-TSYMSG-03454      PIC  9(05)  VALUE  03454.               
               10  PC-TSYMSG-03456      PIC  9(05)  VALUE  03456.               
               10  PC-TSYMSG-03560      PIC  9(05)  VALUE  03560.               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW              PIC  X(01)  VALUE  'N'.                 
               88  PS-ERROR                         VALUE  'Y'.                 
               88  PS-NO-ERROR                      VALUE  'N'.                 
           05  PS-COMMAREA-SW                 PIC X(01)    VALUE 'N'.           
               88  PS-COMMAREA-VALID                       VALUE 'Y'.           
               88  PS-COMMAREA-NOT-VALID                   VALUE 'N'.           
           05  PS-LIST-END-SW                 PIC X(01)    VALUE 'N'.           
               88  PS-LIST-END                             VALUE 'Y'.           
               88  PS-NO-LIST-END                          VALUE 'N'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DEPT-STATUS-CDE             PIC  X(01).                       
           05  PV-VEND-STATUS-CDE             PIC  X(01).                       
           05  PV-BRAND-STATUS-CDE            PIC  X(01).                       
           05  PV-DUMMY                       PIC X(01).                        
           05  PV-BEG-DATE                    PIC X(10).                        
           05  PV-END-DATE                    PIC X(10).                        
           05  PV-CURRENT-DATE                PIC X(10).                        
           05  PV-ISO-DATE REDEFINES PV-CURRENT-DATE.                           
               10  PV-ISO-CENTURY             PIC X(02).                        
               10  PV-ISO-YEAR                PIC X(02).                        
               10  PV-DASH1                   PIC X(01).                        
               10  PV-ISO-MONTH               PIC X(02).                        
               10  PV-DASH2                   PIC X(01).                        
               10  PV-ISO-DAY                 PIC X(02).                        
           05  PV-MMDDYY-DATE.                                                  
               10  PV-MMDDYY-MONTH            PIC X(02).                        
               10  PV-MMDDYY-DAY              PIC X(02).                        
               10  PV-MMDDYY-YEAR             PIC X(02).                        
           05  PV-ENT-ID                      PIC S9(09)   COMP.                
           05  PV-LBL-SEQ-NBR                 PIC S9(04)   COMP.                
           05  PV-NULL-IND                    PIC S9(04)   COMP.                
           05  PV-CICS-RESP             PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
                                                                                
      *----------------------------------------------------------------*        
      *    THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT   *        
      *    SUB-ROUTINE (DPKUT100).                                     *        
      *----------------------------------------------------------------*        
           COPY DPWS010I.                                                       
                                                                                
      *                                                                         
      *  COPY BOOK FOR THE CALENDAR ROUTINE                                     
      *                                                                         
           COPY DPWS500.                                                        
                                                                                
      **--------------------------------------------------------------**        
      ** THE LAYOUT FOR THE TEMPORARY STORAGE QUEUE CONTAINING        **        
      ** SELECTED LIST ITEMS TO BE PROCESSED BY THIS PROGRAM.         **        
      **--------------------------------------------------------------**        
           COPY IMWS009.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    MAP LAYOUT                                                  *        
      *----------------------------------------------------------------*        
                                                                                
           COPY IMKM314.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    ATTRIBUTE SETTINGS COPYBOOK.                                *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    FUNCTION KEYS COPYBOOK                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS016.                                                        
                                                                                
      *****************************************************************         
      * PARAMETERS FOR CALLING CICS ARCHITECTURE API                            
      *****************************************************************         
           COPY DPWS030.                                                        
           COPY DPWS930.                                                        
                                                                                
      *****************************************************************         
      *    STANDARD COMMAREA.                                                   
      *****************************************************************         
                                                                                
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
      *****************************************************************         
      *    INTER-APPLICATION COMMAREA.                                          
      *****************************************************************         
                                                                                
               10  INTER-APPL-COMM-AREA PIC X(200).                             
           COPY IMWS008.                                                        
      *****************************************************************         
      *    SPECIFIC COMMMAREA FOR IMKCS311.                                     
      *****************************************************************         
               10  ASC-SPECIFIC-COMMAREA.                                       
                   15  ASC-CENTURY                 PIC  X(02).                  
                   15  ASC-CURRENT-DATE            PIC  X(10).                  
                   15  ASC-DISPLAY-DATE            PIC  X(06).                  
                   15  ASC-DEPT                    PIC  X(03).                  
                   15  ASC-DEPT-N REDEFINES ASC-DEPT                            
                                                   PIC  9(03).                  
                   15  ASC-DEPT-DESC               PIC  X(25).                  
                   15  ASC-BRAND                   PIC  X(20).                  
                   15  ASC-VENDOR                  PIC  X(30).                  
                   15  ASC-BEG-DATE                PIC  X(06).                  
                   15  ASC-END-DATE                PIC  X(06).                  
                   15  ASC-NUMBER-OF-TS-ITEMS              PIC S9(05)           
                                                               COMP-3.          
                   15  ASC-DTL-QUEUE-NAME.                                      
                       20  ASC-DTL-TERM-ID                 PIC  X(04).          
                       20  ASC-DTL-TASK-NUMBER             PIC S9(05)           
                                                               COMP-3.          
                       20  ASC-DTL-SEQ                     PIC  9(01).          
                   15  ASC-SEL-QUEUE-NAME.                                      
                       20  ASC-SEL-TERM-ID                 PIC  X(04).          
                       20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-SEL-SEQ                     PIC  9(01).          
                   15  ASC-USE-SELECTION-QUEUE-IND         PIC  X(01).          
                       88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'.         
                   15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
                                                               COMP             
                                                               SYNC.            
                   15  ASC-SEL-SUB               PIC S9(04)  COMP.              
                   15  ASC-SEL-ITEM              PIC S9(04)  COMP.              
           EJECT                                                                
      *----------------------------------------------------------------*        
      *    ABEND PROCESSING COPYBOOK.                                  *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS013.                                                        
                                                                                
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR BRAND VENDOR RELATIONSHIP TABLE               *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE IMT00017                                                
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR BRAND VENDOR ID SEQUENCE TABLE                *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE IMT00020                                                
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR VENDOR DEPARTMENT                             *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TVNDDPT                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR EXTERNAL ENTERPRISE                           *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TEXTENT                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR ENTERPRISE NAME                               *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TENTNME                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MERCHANDISE AREA TABLE - TMDSEAR                                     
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR - TDCVSVL                                               
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
               INCLUDE TDCVSVL                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR - TVSCHVL                                               
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
               INCLUDE TVSCHVL                                                  
           END-EXEC.                                                            
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC  X(01).                       
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
      ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
      ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
      ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
      **                                                             **         
      ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
      ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
      *****************************************************************         
       0000-MAIN-MODULE.                                                        
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
           MOVE LENGTH OF IM314AI        TO DP030-MAP-LENGTH (1).               
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
           PERFORM 1000-CONTROL-PROCESSING                                      
                                                                                
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
       0001-CALL-CICS-ARCH-API.                                                 
      *    CALL 'DPKCS030' USING DFHEIBLK                                       
           CALL DP930-CICS-ARCH-API                                             
                           USING DFHEIBLK                                       
                                 DFHCOMMAREA                                    
                                 DP030-CICS-API-FIELDS                          
                                 DP020-STANDARD-COMMAREA                        
                                 IM314AI.                                       
                                                                                
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK                                            
                   DP013-XCTL-DISPLAY-RESTART                                   
                   DP013-CICS-ABEND      TO TRUE                                
               MOVE 'BEFORE 0000-MAIN-MODULE'                                   
                                         TO DP013-PARAGRAPH                     
      *        MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEX        
      *             'T LINE'             TO DP013-MESSAGE-TEXT (2)              
               MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
      -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE                                           
                                         TO DP013-MESSAGE-TEXT (3)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
      *----------------------------------------------------------------*        
                                                                                
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   INITIALIZE ASC-SPECIFIC-COMMAREA                             
                   EXEC SQL                                                     
                       SET  :PV-CURRENT-DATE = CURRENT DATE                     
                   END-EXEC                                                     
                   MOVE PV-CURRENT-DATE   TO ASC-CURRENT-DATE                   
                   MOVE PV-ISO-YEAR       TO PV-MMDDYY-YEAR                     
                   MOVE PV-ISO-MONTH      TO PV-MMDDYY-MONTH                    
                   MOVE PV-ISO-DAY        TO PV-MMDDYY-DAY                      
                   MOVE PV-ISO-CENTURY    TO ASC-CENTURY                        
                   MOVE PV-MMDDYY-DATE    TO ASC-DISPLAY-DATE                   
                   MOVE EIBTRMID          TO ASC-DTL-TERM-ID                    
                   MOVE DP020-SRC-TASK-NUMBER                                   
                                          TO ASC-DTL-TASK-NUMBER                
                   MOVE PC-DTL-QUEUE-SEQ  TO ASC-DTL-SEQ                        
                   PERFORM 1100-PROCESS-INTER-APPL-COMM                         
                   IF  PS-COMMAREA-NOT-VALID                                    
                       SET DP020-NEXT-ACT-APPL-ERROR                            
                                     TO  TRUE                                   
                   ELSE                                                         
                       PERFORM 2050-DELETE-DETAIL-QUEUE                         
                       PERFORM 4000-BUILD-INITIAL-PANEL                         
                   END-IF                                                       
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   PERFORM 2000-PROCESS-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   IF IM008-RETURN-BRAND                                        
                       MOVE IM008-RETURN-DATA     TO ASC-BRAND                  
                   ELSE                                                         
                       IF IM008-RETURN-VENDOR                                   
                           MOVE IM008-RETURN-DATA TO ASC-VENDOR                 
                       END-IF                                                   
                   END-IF                                                       
                   MOVE SPACES                TO IM008-RETURN-TYPE-IND          
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND                                        
                       DP013-NO-ROLLBACK TO TRUE                                
                   MOVE '1000-CONTROL-PROCESSING'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
                                         TO DP013-MESSAGE-TEXT(1)               
                   MOVE DP020-NEXT-APPL-ACTIVITY                                
                                         TO DP013-MESSAGE-TEXT(2)               
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
      * IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
      *----------------------------------------------------------------*        
                                                                                
       1100-PROCESS-INTER-APPL-COMM.                                            
                                                                                
           SET PS-COMMAREA-VALID              TO TRUE.                          
           MOVE IM008-DEPT                    TO ASC-DEPT.                      
           MOVE IM008-DEPT-DESC               TO ASC-DEPT-DESC.                 
           EVALUATE DP020-INT-APPL-FORMAT-IND                                   
               WHEN PC-CRIT-SPEC-FORMAT                                         
                   MOVE IM008-BRAND       TO ASC-BRAND                          
                   MOVE IM008-VENDOR      TO ASC-VENDOR                         
               WHEN PC-LIST-FORMAT                                              
                   SET ASC-USE-SELECTION-QUEUE TO TRUE                          
                   MOVE IM008-NUMBER-OF-TS-ITEMS                                
                                     TO ASC-NUMBER-OF-TS-ITEMS                  
                   MOVE IM008-SEL-QUEUE-NAME                                    
                                     TO ASC-SEL-QUEUE-NAME                      
                   PERFORM 1120-PREPARE-LIST-ENTRY                              
               WHEN OTHER                                                       
                   MOVE PC-TSYMSG-00148  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-FATAL TO TRUE                                  
                   SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                        
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    GET READY TO START AT THE FIRST ITEM ON THE LIST.           *        
      *----------------------------------------------------------------*        
                                                                                
       1120-PREPARE-LIST-ENTRY.                                                 
                                                                                
           MOVE IM009-MAX-ITEMS TO ASC-SEL-SUB.                                 
           MOVE ZERO            TO ASC-SEL-ITEM.                                
           PERFORM 2140-GET-NEXT-BV.                                            
      *----------------------------------------------------------------*        
      * FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*        
      * SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*        
      * EDITS.                                                         *        
      *----------------------------------------------------------------*        
       2000-PROCESS-PANEL.                                                      
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                                                                                
               WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
                   SET DP030-DONT-ERASE                                         
                       DP030-SEND-DATAONLY (1)                                  
                                         TO TRUE                                
                   MOVE SPACES           TO ASC-BRAND                           
                                            ASC-VENDOR                          
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3200-RESET-ATTRIBUTES                                
                                                                                
               WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                     
                   IF  ASC-USE-SELECTION-QUEUE                                  
                       PERFORM 2140-GET-NEXT-BV                                 
                       IF PS-LIST-END                                           
                           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
                           SET PS-NO-LIST-END TO TRUE                           
                       ELSE                                                     
                           PERFORM 2050-DELETE-DETAIL-QUEUE                     
                           PERFORM 4000-BUILD-INITIAL-PANEL                     
                       END-IF                                                   
                       PERFORM 3200-RESET-ATTRIBUTES                            
                   ELSE                                                         
                       MOVE PC-TSYMSG-00070                                     
                                        TO DP020-MSG-NUMBER                     
                       SET DP020-MSG-INFORMATIONAL TO TRUE                      
                   END-IF                                                       
                                                                                
               WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)                     
                   IF  ASC-USE-SELECTION-QUEUE                                  
                       PERFORM 2150-GET-PREV-BV                                 
                       IF PS-LIST-END                                           
                           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
                           SET PS-NO-LIST-END TO TRUE                           
                       ELSE                                                     
                           PERFORM 2050-DELETE-DETAIL-QUEUE                     
                           PERFORM 4000-BUILD-INITIAL-PANEL                     
                       END-IF                                                   
                       PERFORM 3200-RESET-ATTRIBUTES                            
                   ELSE                                                         
                       MOVE PC-TSYMSG-00069                                     
                                        TO DP020-MSG-NUMBER                     
                       SET DP020-MSG-INFORMATIONAL TO TRUE                      
                   END-IF                                                       
                                                                                
               WHEN OTHER                                                       
                   PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   IF  PS-NO-ERROR                                              
                       PERFORM 2100-CHECK-FUNCTION-KEY                          
                       SET DP030-CONTINUE-GO TO TRUE                            
                   ELSE                                                         
                       SET DP030-OVERRIDE-GO TO TRUE                            
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    DELETE THE TEMPORARY STORAGE QUEUE.                         *        
      *----------------------------------------------------------------*        
                                                                                
       2050-DELETE-DETAIL-QUEUE.                                                
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE (ASC-DTL-QUEUE-NAME)                                   
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
              (PV-CICS-RESP = DFHRESP(QIDERR))                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-NO-ROLLBACK TO TRUE                                    
                                                                                
               MOVE '2050-DELETE-DETAIL-QUEUE' TO DP013-PARAGRAPH               
               MOVE 'ERROR TRYING TO DELETE DETAIL TEMP STORAGE QUEUE'          
                              TO DP013-MESSAGE-TEXT (1)                         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           MOVE +0 TO ASC-HIGHEST-BLOCK-WRITTEN.                                
      ******************************************************************        
      ** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **        
      ** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **        
      ** FROM THE CICS ARCHITECTURE API.                              **        
      ******************************************************************        
       2100-CHECK-FUNCTION-KEY.                                                 
           EVALUATE TRUE                                                        
               WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                      
                   PERFORM 5300-ADD-RELATIONSHIP                                
      *            -- OBJECT WAS ADDED --                                       
                   MOVE PC-TSYMSG-00034  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-INFORMATIONAL                                  
                                         TO TRUE                                
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-02 (DP020-SRC-AID)                      
               WHEN DP020-FK-LOCAL-FUNC-03 (DP020-SRC-AID)                      
                   PERFORM 2110-PREPARE-INTER-APPL-COMM                         
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                   CONTINUE                                                     
                                                                                
               WHEN OTHER                                                       
                   SET DP013-NO-ROLLBACK                                        
                       DP013-XCTL-DISPLAY-RESTART                               
                       DP013-CICS-ABEND  TO TRUE                                
                   MOVE '2100-CHECK-FUNCTION-KEY'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
                                         TO DP013-MESSAGE-TEXT (1)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    GET THE NEXT MESSAGE IF A LIST OF MESSAGES IS BEING         *        
      *    PROCESSED.                                                  *        
      *----------------------------------------------------------------*        
                                                                                
       2140-GET-NEXT-BV.                                                        
                                                                                
           ADD +1                       TO ASC-SEL-SUB.                         
           IF  ASC-SEL-SUB > IM009-MAX-ITEMS                                    
               ADD +1                   TO ASC-SEL-ITEM                         
               MOVE +1                  TO ASC-SEL-SUB                          
           END-IF.                                                              
           IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                        
               PERFORM 9100-READ-SEL-QUEUE                                      
               IF  IM009-ITEM (ASC-SEL-SUB) > ZERO                              
                   MOVE IM009-BRAND (ASC-SEL-SUB)   TO ASC-BRAND                
                   MOVE IM009-VENDOR (ASC-SEL-SUB)  TO ASC-VENDOR               
                   SET IM009-INQ (ASC-SEL-SUB) TO TRUE                          
                   PERFORM 9200-REWRITE-SEL-QUEUE                               
               ELSE                                                             
                   SUBTRACT +1 FROM ASC-SEL-SUB                                 
                   MOVE PC-TSYMSG-00070 TO DP020-MSG-NUMBER                     
                   SET DP020-MSG-INFORMATIONAL TO TRUE                          
                   SET PS-LIST-END             TO TRUE                          
               END-IF                                                           
           ELSE                                                                 
               SUBTRACT +1 FROM ASC-SEL-ITEM                                    
               MOVE IM009-MAX-ITEMS     TO ASC-SEL-SUB                          
               MOVE PC-TSYMSG-00070     TO DP020-MSG-NUMBER                     
               SET DP020-MSG-INFORMATIONAL TO TRUE                              
               SET PS-LIST-END             TO TRUE                              
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    GET THE PREVIOUS MESSAGE IF A LIST OF MESSAGES IS BEING     *        
      *    PROCESSED.                                                  *        
      *----------------------------------------------------------------*        
                                                                                
       2150-GET-PREV-BV.                                                        
                                                                                
           SUBTRACT +1 FROM ASC-SEL-SUB.                                        
           IF  ASC-SEL-SUB < +1                                                 
               SUBTRACT +1 FROM ASC-SEL-ITEM                                    
               MOVE IM009-MAX-ITEMS     TO ASC-SEL-SUB                          
           END-IF.                                                              
                                                                                
           IF  ASC-SEL-ITEM > ZERO                                              
               PERFORM 9100-READ-SEL-QUEUE                                      
               MOVE IM009-BRAND (ASC-SEL-SUB)   TO ASC-BRAND                    
               MOVE IM009-VENDOR (ASC-SEL-SUB)  TO ASC-VENDOR                   
           ELSE                                                                 
               MOVE +1                  TO ASC-SEL-ITEM                         
                                           ASC-SEL-SUB                          
               MOVE PC-TSYMSG-00069     TO DP020-MSG-NUMBER                     
               SET DP020-MSG-INFORMATIONAL TO TRUE                              
               SET PS-LIST-END             TO TRUE                              
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **        
      ** INTER-APPLICATION COMMAREA.                                  **        
      ******************************************************************        
       2110-PREPARE-INTER-APPL-COMM.                                            
           INITIALIZE IM008-INTER-APPL-COMMAREA.                                
           MOVE ASC-DEPT                 TO IM008-DEPT.                         
           MOVE ASC-DEPT-DESC            TO IM008-DEPT-DESC.                    
           MOVE ASC-BRAND                TO IM008-BRAND.                        
           MOVE ASC-VENDOR               TO IM008-VENDOR.                       
      *----------------------------------------------------------------*        
      * MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*        
      * THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *        
      * APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *        
      *----------------------------------------------------------------*        
                                                                                
       2200-MOVE-SCREEN-TO-COMMAREA.                                            
           IF  ABRANDL > ZERO                                                   
               MOVE ABRANDI              TO ASC-BRAND                           
           ELSE                                                                 
               IF  ABRANDF = DP015-ERASE-EOF                                    
                   MOVE SPACE            TO ASC-BRAND                           
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  AVENDRL > ZERO                                                   
               MOVE AVENDRI              TO ASC-VENDOR                          
           ELSE                                                                 
               IF  AVENDRF = DP015-ERASE-EOF                                    
                   MOVE SPACE            TO ASC-VENDOR                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ABEGDTL > ZERO                                                   
               MOVE ABEGDTI              TO ASC-BEG-DATE                        
           ELSE                                                                 
               IF  ABEGDTF = DP015-ERASE-EOF                                    
                   MOVE SPACE            TO ASC-BEG-DATE                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  AENDDTL > ZERO                                                   
               MOVE AENDDTI              TO ASC-END-DATE                        
           ELSE                                                                 
               IF  AENDDTF = DP015-ERASE-EOF                                    
                   MOVE SPACE            TO ASC-END-DATE                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * PERFORM EDITS ON EACH FIELD ENTERED BY THE USER.  IF AN ERROR  *        
      * IS FOUND THE CURSOR IS POSITIONED AT THAT FIELD AND AN         *        
      * APPROPRIATE ERROR MESSAGE IS DISPLAYED.                        *        
      *----------------------------------------------------------------*        
                                                                                
       3000-EDIT-DATA-IN-COMMAREA.                                              
           PERFORM 3200-RESET-ATTRIBUTES.                                       
           EVALUATE TRUE                                                        
               WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
                   PERFORM 3010-ADD-EDITS                                       
                                                                                
               WHEN OTHER                                                       
                   CONTINUE                                                     
           END-EVALUATE.                                                        
                                                                                
           IF  PS-NO-ERROR                                                      
               MOVE -1                   TO ABRANDL                             
               SET DP030-SET-CURSOR-APPL-1                                      
                                         TO TRUE                                
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * ENSURE THAT A VALID BRAND AND VENDOR ARE ENTERED FOR THE ADD   *        
      * ALSO EDIT THE BEGIN AND END DATES                              *        
      *----------------------------------------------------------------*        
       3010-ADD-EDITS.                                                          
           IF ASC-BRAND = SPACES OR ASC-VENDOR = SPACES                         
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL                                              
                   DP030-SET-CURSOR-APPL-1 TO TRUE                              
               MOVE PC-TSYMSG-00007      TO DP020-MSG-NUMBER                    
               IF ASC-BRAND = SPACES                                            
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO ABRANDA                             
                   MOVE DP015-RED        TO ABRANDC                             
                   MOVE DP015-REVERSE    TO ABRANDH                             
                   MOVE -1               TO ABRANDL                             
               ELSE                                                             
                   MOVE -1               TO AVENDRL                             
               END-IF                                                           
               IF ASC-VENDOR = SPACES                                           
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO AVENDRA                             
                   MOVE DP015-RED        TO AVENDRC                             
                   MOVE DP015-REVERSE    TO AVENDRH                             
               END-IF                                                           
           END-IF.                                                              
           IF  PS-NO-ERROR                                                      
               PERFORM 5100-VALIDATE-BRAND                                      
           END-IF.                                                              
           IF  PS-NO-ERROR                                                      
               PERFORM 5200-VALIDATE-VENDOR                                     
           END-IF.                                                              
           IF  PS-NO-ERROR                                                      
               MOVE ASC-DEPT                 TO DK-DEPT                         
               PERFORM 5500-CHECK-EXISTING-RLTNSHIP                             
               IF PV-DUMMY = 'Y'                                                
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL                                          
                       DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE PC-TSYMSG-03449      TO DP020-MSG-NUMBER                
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                             TO ABRANDA                         
                   MOVE -1                   TO ABRANDL                         
               END-IF                                                           
           END-IF.                                                              
           IF  PS-NO-ERROR                                                      
               MOVE ASC-DEPT                 TO DK-DEPT                         
               PERFORM 5600-CHECK-EXPIRED-RLTNSHIP                              
               IF PV-DUMMY = 'Y'                                                
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL                                          
                       DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE PC-TSYMSG-03560      TO DP020-MSG-NUMBER                
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                             TO ABRANDA                         
                   MOVE -1                   TO ABRANDL                         
               END-IF                                                           
           END-IF.                                                              
           IF  PS-NO-ERROR                                                      
               IF ASC-BEG-DATE = SPACES                                         
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL                                          
                       DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE PC-TSYMSG-00007      TO DP020-MSG-NUMBER                
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                             TO ABEGDTA                         
                   MOVE DP015-RED            TO ABEGDTC                         
                   MOVE DP015-REVERSE        TO ABEGDTH                         
                   MOVE -1                   TO ABEGDTL                         
               ELSE                                                             
                   PERFORM 3020-VALIDATE-BEGIN-DATE                             
               END-IF                                                           
           END-IF.                                                              
           IF  PS-NO-ERROR                                                      
               IF ASC-END-DATE NOT = SPACES                                     
                   PERFORM 3030-VALIDATE-END-DATE                               
               ELSE                                                             
                   MOVE SPACES       TO PV-END-DATE                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * VALIDATE THAT THE BEGIN DATE IS VALID AND THAT IT IS GREATER   *        
      * THAN CURRENT DATE.                                             *        
      *----------------------------------------------------------------*        
       3020-VALIDATE-BEGIN-DATE.                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           MOVE SPACES                    TO PV-BEG-DATE.                       
           MOVE ASC-BEG-DATE              TO  DPG52-LK-DATE-INPUT.              
           MOVE ASC-DISPLAY-DATE          TO  DPG53-LK-CMPR-DATE-INPUT.         
           PERFORM 3100-COMPARE-DATES.                                          
           EVALUATE TRUE                                                        
               WHEN DPG54-NO-ERROR                                              
                   IF DPG55-DAYS-DIFFERENCE IS NEGATIVE                         
                       SET PS-ERROR                                             
                           DP020-MSG-FATAL                                      
                           DP030-SET-CURSOR-APPL-1 TO TRUE                      
                       MOVE PC-TSYMSG-03131      TO DP020-MSG-NUMBER            
                       MOVE DP015-UNP-ALP-BRT-OFF                               
                                                 TO ABEGDTA                     
                       MOVE DP015-RED            TO ABEGDTC                     
                       MOVE DP015-REVERSE        TO ABEGDTH                     
                       MOVE -1                   TO ABEGDTL                     
                   ELSE                                                         
                       MOVE DPG55-DB2-ISO-DATE-FMT                              
                                                 TO PV-BEG-DATE                 
                   END-IF                                                       
               WHEN OTHER                                                       
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL                                          
                       DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE PC-TSYMSG-00009      TO DP020-MSG-NUMBER                
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                             TO ABEGDTA                         
                   MOVE DP015-RED            TO ABEGDTC                         
                   MOVE DP015-REVERSE        TO ABEGDTH                         
                   MOVE -1                   TO ABEGDTL                         
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * VALIDATE THAT THE END DATE IS VALID AND THAT IT IS GREATER     *        
      * THAN THE BEGIN DATE (IF END DATE HAS BEEN ENTERED)             *        
      *----------------------------------------------------------------*        
       3030-VALIDATE-END-DATE.                                                  
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           MOVE SPACES                    TO PV-END-DATE.                       
           MOVE ASC-END-DATE              TO  DPG52-LK-DATE-INPUT.              
           MOVE ASC-BEG-DATE              TO  DPG53-LK-CMPR-DATE-INPUT.         
           PERFORM 3100-COMPARE-DATES.                                          
           EVALUATE TRUE                                                        
               WHEN DPG54-NO-ERROR                                              
                   IF DPG55-DAYS-DIFFERENCE < 1                                 
                       SET PS-ERROR                                             
                           DP020-MSG-FATAL                                      
                           DP030-SET-CURSOR-APPL-1 TO TRUE                      
                       MOVE PC-TSYMSG-03456      TO DP020-MSG-NUMBER            
                       MOVE DP015-UNP-ALP-BRT-OFF                               
                                                 TO AENDDTA                     
                       MOVE DP015-RED            TO AENDDTC                     
                       MOVE DP015-REVERSE        TO AENDDTH                     
                       MOVE -1                   TO AENDDTL                     
                   ELSE                                                         
                       MOVE DPG55-DB2-ISO-DATE-FMT                              
                                                 TO PV-END-DATE                 
                   END-IF                                                       
               WHEN OTHER                                                       
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL                                          
                       DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE PC-TSYMSG-00009      TO DP020-MSG-NUMBER                
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                             TO AENDDTA                         
                   MOVE DP015-RED            TO AENDDTC                         
                   MOVE DP015-REVERSE        TO AENDDTH                         
                   MOVE -1                   TO AENDDTL                         
           END-EVALUATE.                                                        
                                                                                
       3100-COMPARE-DATES.                                                      
           SET  DPG51-ACTUAL-CALENDAR-ONLY                                      
                DPG52-LK-DTE-GREG                                               
                DPG53-LK-CMPR-DTE-GREG    TO  TRUE.                             
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING                              
                     DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
                                                                                
                                                                                
       3200-RESET-ATTRIBUTES.                                                   
           MOVE DP015-UNP-ALP-NOR-OFF    TO ABRANDA                             
                                            AVENDRA                             
                                            ABEGDTA                             
                                            AENDDTA.                            
           MOVE DP015-GREEN              TO ABRANDC                             
                                            AVENDRC                             
                                            ABEGDTC                             
                                            AENDDTC.                            
           MOVE DP015-UNDERLINE          TO ABRANDH                             
                                            AVENDRH                             
                                            ABEGDTH                             
                                            AENDDTH.                            
                                                                                
      *----------------------------------------------------------------*        
      * GET THE NEXT AVAILABLE MESSAGE NUMBER, MOVE IT TO THE SCREEN   *        
      * AND POSITION THE CURSOR FOR THE INITIAL PANEL.                 *        
      *----------------------------------------------------------------*        
                                                                                
       4000-BUILD-INITIAL-PANEL.                                                
                                                                                
           SET DP030-SET-CURSOR-APPL-1   TO TRUE.                               
           MOVE -1                       TO ABRANDL.                            
           PERFORM 3200-RESET-ATTRIBUTES.                                       
           MOVE ASC-DISPLAY-DATE         TO ASC-BEG-DATE.                       
           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
                                                                                
      *----------------------------------------------------------------*        
      *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
      *----------------------------------------------------------------*        
                                                                                
       4400-MOVE-COMMAREA-TO-SCREEN.                                            
           MOVE ASC-DEPT                 TO ADEPTO.                             
           MOVE ASC-DEPT-DESC            TO ADESCO.                             
           MOVE ASC-BRAND                TO ABRANDO.                            
           MOVE ASC-VENDOR               TO AVENDRO.                            
           MOVE ASC-BEG-DATE             TO ABEGDTO.                            
           MOVE ASC-END-DATE             TO AENDDTO.                            
                                                                                
      *----------------------------------------------------------------*        
      *  READ BRAND STATUS FOR VALIDATION                                       
      *----------------------------------------------------------------*        
       5100-VALIDATE-BRAND.                                                     
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO)                                    
                         OR (SQLCODE = +100))                                   
                                                                                
               EXEC SQL                                                         
                 SELECT DISTINCT                                                
                        C.STAT_CDE                                              
                       ,C.CHARTC_SEQ_NBR                                        
                   INTO :PV-BRAND-STATUS-CDE                                    
                       ,:PV-LBL-SEQ-NBR                                         
                   FROM TMDSEAR A                                               
                       ,TDCVSVL B                                               
                       ,TVSCHVL C                                               
                  WHERE A.OPERCO_NBR      = :PC-OPERCO-NBR                      
                    AND A.MDSELV_CDE      = :PC-DPT-LITERAL                     
                    AND A.END_DTE         > :ASC-CURRENT-DATE                   
                    AND A.MDSEAR_DKEY     = B.MDSEAR_ID                         
                    AND C.CHARTC_CDE      = :PC-CHARTC-LITERAL                  
                    AND B.CHARTC_CDE      = C.CHARTC_CDE                        
                    AND A.DEPT_NBR        = :ASC-DEPT                           
                    AND B.CHARTC_SEQ_NBR  = C.CHARTC_SEQ_NBR                    
                    AND B.CHARTC_SEQ_NBR <> :PC-UNKNOWN-BRAND-ID                
                    AND C.CHARTC_VAL_DESC = :ASC-BRAND                          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '5100-VALIDATE-BRAND'                               
                                         TO DP013-PARAGRAPH                     
                       MOVE 'VALIDATE BRAND:'                                   
                                         TO DP013-MESSAGE-TEXT (1)              
                       MOVE ASC-BRAND    TO DP013-MESSAGE-TEXT (2)              
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TMDSEAR'    TO DP013-DB2-TABLE-NAME (1)            
                       MOVE 'TDCVSVL'    TO DP013-DB2-TABLE-NAME (2)            
                       MOVE 'TVSCHVL'    TO DP013-DB2-TABLE-NAME (3)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5100-VALIDATE-BRAND' TO DP013-PARAGRAPH                    
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE 'BRAND STATUS FOR'   TO DP013-MESSAGE-TEXT (2)              
               MOVE ASC-BRAND            TO DP013-MESSAGE-TEXT (3)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'TMDSEAR'            TO DP013-DB2-TABLE-NAME (1)            
               MOVE 'TDCVSVL'            TO DP013-DB2-TABLE-NAME (2)            
               MOVE 'TVSCHVL'            TO DP013-DB2-TABLE-NAME (3)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           ELSE                                                                 
               IF SQLCODE = +100                                                
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL                                          
                       DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE PC-TSYMSG-03452      TO DP020-MSG-NUMBER                
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO ABRANDA                             
                   MOVE DP015-RED        TO ABRANDC                             
                   MOVE DP015-REVERSE    TO ABRANDH                             
                   MOVE -1               TO ABRANDL                             
               ELSE                                                             
                   IF SQLCODE = ZERO                                            
                       IF PV-BRAND-STATUS-CDE NOT = 'A'                         
                           SET PS-ERROR                                         
                               DP020-MSG-FATAL                                  
                               DP030-SET-CURSOR-APPL-1 TO TRUE                  
                           MOVE PC-TSYMSG-03453  TO DP020-MSG-NUMBER            
                           MOVE DP015-UNP-ALP-BRT-OFF                           
                                                 TO ABRANDA                     
                           MOVE DP015-RED        TO ABRANDC                     
                           MOVE DP015-REVERSE    TO ABRANDH                     
                           MOVE -1               TO ABRANDL                     
                       END-IF                                                   
                   ELSE                                                         
                       MOVE '5100-VALIDATE-BRAND' TO DP013-PARAGRAPH            
                       MOVE 'INVALID RETURN ATTEMPTING TO SELECT'               
                                            TO DP013-MESSAGE-TEXT (1)           
                       MOVE 'BRAND STATUS FOR' TO DP013-MESSAGE-TEXT(2)         
                       MOVE ASC-BRAND      TO DP013-MESSAGE-TEXT(3)             
                       MOVE SQLCA          TO DP013-SQLCA                       
                       MOVE 'TMDSEAR'      TO DP013-DB2-TABLE-NAME(1)           
                       MOVE 'TDCVSVL'      TO DP013-DB2-TABLE-NAME(2)           
                       MOVE 'TVSCHVL'      TO DP013-DB2-TABLE-NAME(3)           
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART                           
                                           TO TRUE                              
                       PERFORM DP013-0000-PROCESS-ABEND                         
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *  READ VENDOR AND DEPARTMENT STATUS FOR VALIDATION                       
      *----------------------------------------------------------------*        
       5200-VALIDATE-VENDOR.                                                    
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO)                                    
                         OR (SQLCODE = +100))                                   
                                                                                
               EXEC SQL                                                         
                 SELECT A.VND_DEPT_STAT_CD                                      
                       ,B.STATUS_CDE                                            
                       ,C.ENT_ID                                                
                   INTO :PV-DEPT-STATUS-CDE                                     
                       ,:PV-VEND-STATUS-CDE                                     
                       ,:PV-ENT-ID                                              
                   FROM TVNDDPT A                                               
                       ,TEXTENT B                                               
                       ,TENTNME C                                               
                  WHERE A.DEPT_NBR        = :ASC-DEPT                           
                    AND A.VEND_TYP_IND    = :PC-VENDOR-TYPE                     
                    AND C.ENT_NAME_DESC   = :ASC-VENDOR                         
                    AND B.ENT_ID          = A.ENT_ID                            
                    AND C.ENT_ID          = A.ENT_ID                            
                    AND B.ENT_ID         <> :PC-UNKNOWN-VENDOR-ID               
                    AND C.TYPE_CDE        = :PC-ENTNME-TYP-CDE                  
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '5200-VALIDATE-VENDOR'                              
                                         TO DP013-PARAGRAPH                     
                       MOVE 'VALIDATE VENDOR:'                                  
                                         TO DP013-MESSAGE-TEXT (1)              
                       MOVE ASC-VENDOR   TO DP013-MESSAGE-TEXT (2)              
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TVNDDPT'    TO DP013-DB2-TABLE-NAME (1)            
                       MOVE 'TEXTENT'    TO DP013-DB2-TABLE-NAME (2)            
                       MOVE 'TENTNME'    TO DP013-DB2-TABLE-NAME (3)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5200-VALIDATE-VENDOR' TO DP013-PARAGRAPH                   
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE 'VENDOR STATUS FOR'  TO DP013-MESSAGE-TEXT (2)              
               MOVE ASC-VENDOR           TO DP013-MESSAGE-TEXT (3)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'TVNDDPT'            TO DP013-DB2-TABLE-NAME (1)            
               MOVE 'TEXTENT'            TO DP013-DB2-TABLE-NAME (2)            
               MOVE 'TENTNME'            TO DP013-DB2-TABLE-NAME (3)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           ELSE                                                                 
               IF SQLCODE = +100                                                
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL                                          
                       DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE PC-TSYMSG-03450      TO DP020-MSG-NUMBER                
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO AVENDRA                             
                   MOVE DP015-RED        TO AVENDRC                             
                   MOVE DP015-REVERSE    TO AVENDRH                             
                   MOVE -1               TO AVENDRL                             
               ELSE                                                             
                   IF SQLCODE = ZERO                                            
                       IF PV-VEND-STATUS-CDE NOT = 'A'                          
                           SET PS-ERROR                                         
                               DP020-MSG-FATAL                                  
                               DP030-SET-CURSOR-APPL-1 TO TRUE                  
                           MOVE PC-TSYMSG-00564  TO DP020-MSG-NUMBER            
                           MOVE DP015-UNP-ALP-BRT-OFF                           
                                                 TO AVENDRA                     
                           MOVE DP015-RED        TO AVENDRC                     
                           MOVE DP015-REVERSE    TO AVENDRH                     
                           MOVE -1               TO AVENDRL                     
                       ELSE                                                     
                           IF PV-DEPT-STATUS-CDE NOT = 'A'                      
                               SET PS-ERROR                                     
                                   DP020-MSG-FATAL                              
                                   DP030-SET-CURSOR-APPL-1 TO TRUE              
                               MOVE PC-TSYMSG-03454  TO DP020-MSG-NUMBER        
                               MOVE DP015-UNP-ALP-BRT-OFF                       
                                                     TO AVENDRA                 
                               MOVE DP015-RED        TO AVENDRC                 
                               MOVE DP015-REVERSE    TO AVENDRH                 
                               MOVE -1               TO AVENDRL                 
                           END-IF                                               
                       END-IF                                                   
                   ELSE                                                         
                       MOVE '5200-VALIDATE-VENDOR' TO DP013-PARAGRAPH           
                       MOVE 'INVALID RETURN ATTEMPTING TO SELECT'               
                                            TO DP013-MESSAGE-TEXT (1)           
                       MOVE 'VENDOR STATUS FOR' TO DP013-MESSAGE-TEXT(2)        
                       MOVE ASC-VENDOR     TO DP013-MESSAGE-TEXT(3)             
                       MOVE SQLCA          TO DP013-SQLCA                       
                       MOVE 'TVNDDPT'      TO DP013-DB2-TABLE-NAME(1)           
                       MOVE 'TEXTENT'      TO DP013-DB2-TABLE-NAME(2)           
                       MOVE 'TENTNME'      TO DP013-DB2-TABLE-NAME(3)           
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART                           
                                           TO TRUE                              
                       PERFORM DP013-0000-PROCESS-ABEND                         
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * CREATE THE BRAND VENDOR RELATIONSHIP                           *        
      *----------------------------------------------------------------*        
       5300-ADD-RELATIONSHIP.                                                   
           INITIALIZE DCLIMT00017.                                              
           MOVE ASC-DEPT-N               TO IMT00017-DEPT-NBR.                  
           MOVE PV-LBL-SEQ-NBR           TO IMT00017-LBL-SEQ-NBR.               
           MOVE PV-ENT-ID                TO IMT00017-ENT-ID.                    
           MOVE PV-BEG-DATE              TO IMT00017-EFF-BEG-DTE.               
           IF PV-END-DATE NOT = SPACES                                          
               MOVE PV-END-DATE          TO IMT00017-EFF-END-DTE                
               MOVE +1                   TO PV-NULL-IND                         
           ELSE                                                                 
               MOVE -1                   TO PV-NULL-IND                         
           END-IF.                                                              
           PERFORM 6000-GET-BRND-VND-ID.                                        
           PERFORM 5400-INSERT-RELATIONSHIP.                                    
           IF  ASC-USE-SELECTION-QUEUE                                          
               PERFORM 5350-UPDATE-SELECTION-QUEUE                              
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * RECORD IN THE SELECTION TEMP STORAGE QUEUE THE FACT THAT THIS  *        
      * OBJECT WAS ADDED.                                              *        
      *----------------------------------------------------------------*        
       5350-UPDATE-SELECTION-QUEUE.                                             
           PERFORM 9100-READ-SEL-QUEUE.                                         
           SET IM009-ADD (ASC-SEL-SUB) TO TRUE.                                 
           PERFORM 9200-REWRITE-SEL-QUEUE.                                      
      *----------------------------------------------------------------*        
      * INSERT A ROW INTO IMT_DEPT_BRND_VND                            *        
      *----------------------------------------------------------------*        
       5400-INSERT-RELATIONSHIP.                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO))                                   
               EXEC SQL                                                         
                   INSERT INTO IMT_DEPT_BRND_VND                                
                      (DEPT_BRND_VND_ID                                         
                      ,DEPT_NBR                                                 
                      ,LBL_SEQ_NBR                                              
                      ,ENT_ID                                                   
                      ,EFF_BEG_DTE                                              
                      ,EFF_END_DTE)                                             
                   VALUES                                                       
                      (IDENTITY_VAL_LOCAL()                                     
                      ,:IMT00017-DEPT-NBR                                       
                      ,:IMT00017-LBL-SEQ-NBR                                    
                      ,:IMT00017-ENT-ID                                         
                      ,:IMT00017-EFF-BEG-DTE                                    
                      ,:IMT00017-EFF-END-DTE :PV-NULL-IND)                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '5400-INSERT-RELATIONSHIP'                          
                                         TO DP013-PARAGRAPH                     
                       MOVE 'INSERT A ROW INTO THE BRAND VENDOR TABLE'          
                                         TO DP013-MESSAGE-TEXT(1)               
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'IMT_DEPT_BRND_VND'                                 
                                         TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5400-INSERT-RELATIONSHIP'                                  
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO INSERT A NEW        
      -             ' BRAND VENDOR'      TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'IMT_DEPT_BRND_VND'  TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *  READ FOR AN EXISTING FUTURE ACTIVE RELATIONSHIP WHEN END DATE          
      *  WAS NOT ENTERED                                                        
      *----------------------------------------------------------------*        
       5500-CHECK-EXISTING-RLTNSHIP.                                            
           MOVE SPACES                 TO PV-DUMMY.                             
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO)                                    
                         OR (SQLCODE = +100))                                   
                                                                                
               EXEC SQL                                                         
                 SELECT 'Y'                                                     
                   INTO :PV-DUMMY                                               
                   FROM IMT_DEPT_BRND_VND                                       
                  WHERE DEPT_NBR          = :DK-DEPT                            
                    AND LBL_SEQ_NBR       = :PV-LBL-SEQ-NBR                     
                    AND ENT_ID            = :PV-ENT-ID                          
                    AND (EFF_BEG_DTE     >= :ASC-CURRENT-DATE                   
                     OR  EFF_END_DTE     >= :ASC-CURRENT-DATE                   
                     OR  EFF_END_DTE     IS NULL)                               
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '5500-CHECK-EXISTING-RLTNSHIP'                      
                                         TO DP013-PARAGRAPH                     
                       MOVE 'READ FOR FUTURE ACTIVE RELATIONSHIP'               
                                         TO DP013-MESSAGE-TEXT (1)              
                       MOVE ASC-BRAND    TO DP013-MESSAGE-TEXT (2)              
                       MOVE ASC-VENDOR   TO DP013-MESSAGE-TEXT (3)              
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'IMT_DEPT_BRND_VND'                                 
                                         TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5500-CHECK-EXISTING-RLTNSHIP'                              
                                          TO DP013-PARAGRAPH                    
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE 'RELATIONSHIP'       TO DP013-MESSAGE-TEXT (2)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'IMT_DEPT_BRND_VND'  TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *  READ FOR AN EXPIRED RELATIONSHIP                                       
      *----------------------------------------------------------------*        
       5600-CHECK-EXPIRED-RLTNSHIP.                                             
           MOVE SPACES                 TO PV-DUMMY.                             
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO)                                    
                         OR (SQLCODE = +100))                                   
                                                                                
               EXEC SQL                                                         
                 SELECT 'Y'                                                     
                   INTO :PV-DUMMY                                               
                   FROM IMT_DEPT_BRND_VND                                       
                  WHERE DEPT_NBR          = :DK-DEPT                            
                    AND LBL_SEQ_NBR       = :PV-LBL-SEQ-NBR                     
                    AND ENT_ID            = :PV-ENT-ID                          
                    AND (EFF_BEG_DTE     <= :ASC-CURRENT-DATE                   
                    AND  EFF_END_DTE     <= :ASC-CURRENT-DATE)                  
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '5600-CHECK-EXPIRED-RLTNSHIP'                       
                                         TO DP013-PARAGRAPH                     
                       MOVE 'READ FOR AN INACTIVE RELATIONSHIP'                 
                                         TO DP013-MESSAGE-TEXT (1)              
                       MOVE ASC-BRAND    TO DP013-MESSAGE-TEXT (2)              
                       MOVE ASC-VENDOR   TO DP013-MESSAGE-TEXT (3)              
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'IMT_DEPT_BRND_VND'                                 
                                         TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5600-CHECK-EXPIRED-RLTNSHIP'                               
                                          TO DP013-PARAGRAPH                    
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE 'RELATIONSHIP'       TO DP013-MESSAGE-TEXT (2)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'IMT_DEPT_BRND_VND'  TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * INSERT INTO IMT_BRND_VND_ID TO GET THE NEXT DATALESS KEY.      *        
      *----------------------------------------------------------------*        
                                                                                
       6000-GET-BRND-VND-ID.                                                    
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO))                                   
               EXEC SQL                                                         
      *            INSERT INTO IMT_DEPT_BRND_ID()                               
                 INSERT INTO IMT_BRND_VND_ID                                    
                        ( ROW_SEQ_NBR )                                         
                 VALUES ( 0 )                                                   
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '6000-GET-BRND-VND-ID'                              
                                         TO DP013-PARAGRAPH                     
                       MOVE 'INSERT INTO THE BRAND VENDOR ID TABLE'             
                                         TO DP013-MESSAGE-TEXT(1)               
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'IMT_BRND_VND_ID'                                   
                                         TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '6000-GET-BRND-VND-ID'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO INSERT A NEW        
      -             ' BRAND VENDOR ID'   TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'IMT_BRND_VND_ID'    TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
      *----------------------------------------------------------------*        
                                                                                
       9100-READ-SEL-QUEUE.                                                     
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-SEL-QUEUE-NAME)                                   
                   INTO  (IM009-BRAND-VENDOR-REC)                               
                   ITEM  (ASC-SEL-ITEM)                                         
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-LOGIC-ABEND                                            
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9100-READ-SEL-QUEUE'                                       
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'           
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
      *----------------------------------------------------------------*        
       9200-REWRITE-SEL-QUEUE.                                                  
           EXEC CICS                                                            
                WRITEQ TS                                                       
                    QUEUE  (ASC-SEL-QUEUE-NAME)                                 
                    FROM   (IM009-BRAND-VENDOR-REC)                             
                    ITEM   (ASC-SEL-ITEM)                                       
                    RESP   (PV-CICS-RESP)                                       
                    REWRITE                                                     
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                           
               SET DP013-CICS-ABEND                                             
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9200-REWRITE-SEL-QUEUE'                                    
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'           
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
