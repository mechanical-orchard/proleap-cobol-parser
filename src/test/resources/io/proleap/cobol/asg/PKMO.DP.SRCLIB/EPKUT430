       IDENTIFICATION DIVISION.                                         00010073
       PROGRAM-ID.      EPKUT430.                                       00020073
       AUTHOR.          DAVID WELLER.                                   00030073
       DATE-WRITTEN.    APRIL 2010.                                     00040073
                                                                        00050073
      *---------------------------------------------------------------  00060073
      * UPDATE E-BUS-ITM-IND IN PLU FULL REFRESH FILE                   00070073
      *---------------------------------------------------------------  00080073
      *  THIS PROGRAM READS THE FULL FILES OF PLU DATA                  00090073
      *  AND UPDATES THE E-BUS-ITM-IND FIELD WITH THE VALUE FROM        00100073
      *  THE TWEBITM TABLE BEFORE WRITING THE FULL FILE TO OUTPUT.      00110073
      *--------------------------------------------------------------*  00120073
      * DATE:  APRIL 2010                                               00130073
      * WR/IR#: WR38172                                                 00140073
      * PROGRAMMER: DAVE WELLER                                         00150073
      * DESCRIPTION: - E_BUS_ITM_IND SENT TO STORES SET TO              00160073
      *                TWEBITM COLUMN WEB_EXCLV_IND                     00170073
      *--------------------------------------------------------------*  00180073
       ENVIRONMENT DIVISION.                                            00190073
      *----------------------------------------------------------       00200073
       CONFIGURATION SECTION.                                           00210073
       SOURCE-COMPUTER.        IBM-3090.                                00220073
       OBJECT-COMPUTER.        IBM-3090.                                00230073
                                                                        00240073
       INPUT-OUTPUT SECTION.                                            00250073
                                                                        00260073
       FILE-CONTROL.                                                    00270073
                                                                        00280073
           SELECT FULL-SKU-GLOBAL-FILE-IN                               00290073
                  ASSIGN TO INP01.                                      00300073
           SELECT FULL-SKU-GLOBAL-FILE-OUT                              00310073
                  ASSIGN TO OUT01.                                      00320073
                                                                        00330073
       DATA DIVISION.                                                   00340073
       FILE SECTION.                                                    00350073
                                                                        00360073
       FD  FULL-SKU-GLOBAL-FILE-IN                                      00370073
           RECORDING MODE F                                             00380073
           BLOCK CONTAINS 0 RECORDS                                     00390073
           LABEL RECORDS ARE OMITTED                                    00400073
           DATA RECORD IS FULL-SKU-GLOBAL-REC-IN.                       00410073
       01  FULL-SKU-GLOBAL-REC-IN          PIC X(170).                  00420073
                                                                        00430073
       FD  FULL-SKU-GLOBAL-FILE-OUT                                     00440073
           RECORDING MODE F                                             00450073
           BLOCK CONTAINS 0 RECORDS                                     00460073
           LABEL RECORDS ARE OMITTED                                    00470073
           DATA RECORD IS FULL-SKU-GLOBAL-REC-OUT.                      00480073
       01  FULL-SKU-GLOBAL-REC-OUT         PIC X(170).                  00490073
                                                                        00500073
       WORKING-STORAGE SECTION.                                         00510073
      *-----------------------------------------------------------      00520073
      * PS- PROGRAM SWITCHES                                            00530073
      *-----------------------------------------------------------      00540073
       01  PS-PROGRAM-SWITCHES.                                         00550073
           05  PS-END-OF-INPUT-FILE-SW        PIC X(01) VALUE 'N'.      00560073
               88  PS-END-OF-INPUT-FILE                 VALUE 'Y'.      00570073
               88  PS-NOT-END-OF-INPUT-FILE             VALUE 'N'.      00580073
           05  PS-TWEBITM-FOUND-SW            PIC  X(001).              00590073
               88  PS-TWEBITM-NOT-FOUND                   VALUE 'N'.    00600073
               88  PS-TWEBITM-FOUND                       VALUE 'Y'.    00610073
                                                                        00620073
      *-----------------------------------------------------------      00630073
      * PC- PROGRAM CONSTANTS                                           00640073
      *-----------------------------------------------------------      00650073
       01  PC-PROGRAM-CONSTANTS.                                        00660073
           05  PC-CURRENT-PROGRAM-NAME   PIC X(08)  VALUE 'EPKUT430'.   00670073
           05  PC-MAX-RETRIES            PIC S9(04) VALUE +5 COMP.      00680073
           05  PC-START-TIME             PIC X(15)                      00690073
                                         VALUE '04.00.00.000000'.       00700073
           05  PC-SQLS-START-TIME        PIC X(08)  VALUE '04:00:00'.   00710073
           05  PC-START-TIME-TRUNC       PIC X(10)  VALUE '03.00.00'.   00720073
           05  PC-PRIC-EFF-TIME-STD      PIC X(08)  VALUE '03:00:00'.   00730073
           05  PC-NULL-CHAR              PIC X(01)  VALUE '~'.          00740073
           05  PC-ZERO                   PIC S9(8)V VALUE +0 COMP-3.    00750073
                                                                        00760073
      *-----------------------------------------------------------      00770073
      * PV- PROGRAM VARIABLES                                           00780073
      *-----------------------------------------------------------      00790073
       01  PV-PROGRAM-VARAIBLES.                                        00800073
           05  PV-SQL-SUB                PIC S9(4)  VALUE +0 COMP.      00810073
           05  PV-INTR-UPC-ID            PIC S9(11) VALUE +0 COMP.      00820073
           05  PV-STRT-TMST              PIC X(26)  VALUE SPACES.       00830073
           05  PV-STRT-DTE               PIC X(10)  VALUE SPACES.       00840073
           05  PV-TODAY-DTE              PIC X(10)  VALUE SPACES.       00850073
C95983     05  PV-DB2-INTR-UPC-ID        PIC S9(12)  VALUE +0 COMP.     00860085
                                                                        00870073
           05  PV-DISPLAY-FIELDS.                                       00880073
               10  PV-DISPLAY-STR-NBR    PIC  9(8)  VALUE  0.           00890073
               10  PV-DISPLAY-AD-VERSION PIC  9(8)  VALUE  0.           00900073
                                                                        00910073
           05  PV-DB2-FIELDS.                                           00920073
               10  PV-DB2-STR-NBR        PIC S9(4)  VALUE +0 COMP.      00930073
               10  PV-DB2-SKU-NBR        PIC S9(8)V VALUE +0 COMP-3.    00940073
               10  PV-DB2-SKU-DESC       PIC X(30)  VALUE SPACES.       00950073
               10  PV-DB2-SIZE-DESC      PIC X(10)  VALUE SPACES.       00960073
               10  PV-DB2-E-BUS-ITM-IND  PIC X(01)  VALUE SPACES.       00970073
                                                                        00980073
           05  PV-RETRY-COUNT           PIC S9(04)  VALUE ZERO          00990073
                                                    COMP SYNC.          01000073
           05  PV-DISPLAY-INTERNAL      PIC  9(10) VALUE ZERO.          01010073
                                                                        01020073
      *----------------------------------------------------------       01030073
      * FULL SKU GLOBAL FILE LAYOUT - INPUT/OUTPUT                      01040073
      *----------------------------------------------------------       01050073
           COPY EPRD219.                                                01060073
                                                                        01070073
      *-----------------------------------------------------------      01080073
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004         01090073
      *-----------------------------------------------------------      01100073
           COPY DPWS004.                                                01110073
                                                                        01120073
      *-----------------------------------------------------------      01130073
      * DB2 ABEND COPYBOOK                                              01140073
      *-----------------------------------------------------------      01150073
           COPY DPWS900.                                                01160073
                                                                        01170073
      *-----------------------------------------------------------      01180073
      * DB2 MESSAGE AREA                                                01190073
      *-----------------------------------------------------------      01200073
           COPY SQLCA2.                                                 01210073
                                                                        01220073
      *-----------------------------------------------------------      01230073
      * PC- PROGRAM COUNTERS                                            01240073
      *-----------------------------------------------------------      01250073
       01  PC-PROGRAM-COUNTERS.                                         01260073
           05  PC-CNT-READ-RECS             PIC 9(09) VALUE 0.          01270073
           05  PC-CNT-GLBL-SKU-RECS         PIC 9(09) VALUE 0.          01280073
           05  PC-CNT-DISPLAY-RECS          PIC 9(09) VALUE 0.          01290073
                                                                        01300073
      *-----------------------------------------------------------      01310073
      * ERROR HANDLING                                                  01320073
      *-----------------------------------------------------------      01330073
       01  PV-MISC-ABEND-FIELDS.                                        01340073
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.      01350073
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.      01360073
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.      01370073
           05  PV-DB2-OPERATION             PIC X(30) VALUE SPACE.      01380073
       01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 01390073
           88  ABEND-4003-CTRL-CARD-ERROR        VALUE +4003.           01400073
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           01410073
           88  ABEND-4019-CAL-SUB-ERROR          VALUE +4019.           01420073
           88  ABEND-4020-MQ-ERROR               VALUE +4020.           01430073
           88  ABEND-4444-FORCED-ABEND           VALUE +4444.           01440073
                                                                        01450073
      *-----------------------------------------------------------      01460073
      * DB2 COMMUNICATIONS AREA                                         01470073
      *-----------------------------------------------------------      01480073
           EXEC SQL                                                     01490073
                INCLUDE SQLCA                                           01500073
           END-EXEC.                                                    01510073
                                                                        01520073
      *--------------------------------------------------               01530073
      *  WEB ITEM ATTRIBUTES TABLE                                      01540073
      *--------------------------------------------------               01550073
           EXEC SQL                                                     01560073
               INCLUDE TWEBITM                                          01570073
           END-EXEC.                                                    01580073
                                                                        01590073
                                                                        01600073
       LINKAGE SECTION.                                                 01610073
                                                                        01620073
      *----------------------------------------------------------       01630073
       PROCEDURE DIVISION.                                              01640073
      *----------------------------------------------------------       01650073
       0000-MAINLINE.                                                   01660073
                                                                        01670073
           PERFORM 1000-INITIALIZATION.                                 01680073
                                                                        01690073
           PERFORM 2000-PROCESS-INPUT                                   01700073
               UNTIL PS-END-OF-INPUT-FILE.                              01710073
                                                                        01720073
           PERFORM 2400-CLOSE-FILES.                                    01730073
                                                                        01740073
           PERFORM 9999-WRAPUP.                                         01750073
                                                                        01760073
           GOBACK.                                                      01770073
                                                                        01780073
      *----------------------------------------------------------       01790073
      * 1000-INITIALIZATION.                                            01800073
      *     INITIALIZATION AREA - INITIALIZES ALL VARIABLES, OPENS      01810073
      *     FILES, READS INPUT FILES                                    01820073
      *----------------------------------------------------------       01830073
       1000-INITIALIZATION.                                             01840073
                                                                        01850073
           OPEN INPUT  FULL-SKU-GLOBAL-FILE-IN.                         01860073
           OPEN OUTPUT FULL-SKU-GLOBAL-FILE-OUT.                        01870073
                                                                        01880073
           PERFORM 1500-READ-INPUT-FILE.                                01890073
                                                                        01900073
      *----------------------------------------------------------       01910073
      * 1500-READ-INPUT-FILE.                                           01920073
      *----------------------------------------------------------       01930073
       1500-READ-INPUT-FILE.                                            01940073
                                                                        01950073
           READ FULL-SKU-GLOBAL-FILE-IN INTO EP219-XST-SKU-RECORD       01960073
               AT END                                                   01970073
               SET PS-END-OF-INPUT-FILE TO TRUE.                        01980073
           IF PS-NOT-END-OF-INPUT-FILE                                  01990073
               ADD +1             TO PC-CNT-READ-RECS                   02000073
           END-IF.                                                      02010073
                                                                        02020073
      *----------------------------------------------------------       02030073
      * 2000-PROCESS-INPUT.                                             02040073
      *     FETCHES THE MERCHANDISE CURSOR AND THEN FETCHES ANY         02050073
      *     STYLE ID'S THAT ARE WITHIN THOSE DEPARTMENT AND SUB         02060073
      *     CLASSES.  CREATES DUMMY SKU RECORDS.                        02070073
      *----------------------------------------------------------       02080073
       2000-PROCESS-INPUT.                                              02090073
                                                                        02100073
           PERFORM 5900-SELECT-TWEBITM.                                 02110073
           IF PS-TWEBITM-FOUND                                          02120073
               MOVE WEBITM-WEB-EXCLV-IND  TO EP219-E-BUS-ITM-IND        02140073
           ELSE                                                         02160073
               MOVE 'N'                   TO EP219-E-BUS-ITM-IND        02170073
           END-IF.                                                      02180073
           SET EP219-E-BUS-ITM-IND-YES    TO TRUE.                      02190073
           PERFORM 8300-WRITE-SKU-GLBL-REC.                             02200073
                                                                        02210073
           ADD  1 TO PC-CNT-DISPLAY-RECS.                               02220073
           IF PC-CNT-DISPLAY-RECS > 100000                              02230073
               DISPLAY '                       '                        02240073
               DISPLAY '---  INTERIM COUNTS ---'                        02250073
               DISPLAY 'NUMBER OF GLBL SKU RECS WRITTEN: '              02260073
                       PC-CNT-GLBL-SKU-RECS                             02270073
                                                                        02280073
               MOVE ZEROES TO PC-CNT-DISPLAY-RECS                       02290073
           END-IF.                                                      02300073
                                                                        02310073
           PERFORM 1500-READ-INPUT-FILE.                                02320073
                                                                        02330073
      *-----------------------------------------------------------      02340073
      * 2400-CLOSE-FILES.                                               02350073
      *     CLOSE THE MERCHANDISE CURSOR.                               02360073
      *-----------------------------------------------------------      02370073
       2400-CLOSE-FILES.                                                02380073
                                                                        02390073
           CLOSE       FULL-SKU-GLOBAL-FILE-IN.                         02400073
           CLOSE       FULL-SKU-GLOBAL-FILE-OUT.                        02410073
                                                                        02420073
      *----------------------------------------------------------       02430073
      * 5900-SELECT-TWEBITM.                                            02440073
      *     RETRIEVE TWEBITM.                                           02450073
      *----------------------------------------------------------       02460073
       5900-SELECT-TWEBITM.                                             02470073
                                                                        02480073
C95983* WEBITM-INTR-UPC-ID WILL DISPLAY ONLY LAST 9 BYTES EVEN THOUGH   02481085
C95983* IT PROCESS 10 BYTE VALUE WHILE FETCHING RECORD FROM TABLE       02482085
                                                                        02483085
           MOVE EP219-INTR-UPC-ID  TO PV-DB2-INTR-UPC-ID.               02490080
           MOVE PV-DB2-INTR-UPC-ID TO WEBITM-INTR-UPC-ID.               02500080
                                                                        02541082
           PERFORM WITH TEST AFTER                                      02550073
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       02560073
                 UNTIL SQLCODE = +0                                     02570073
                    OR SQLCODE = +100                                   02580073
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  02590073
               EXEC SQL                                                 02600073
                   SELECT W.WEB_EXCLV_IND                               02610073
                     INTO :WEBITM-WEB-EXCLV-IND                         02620073
                   FROM TWEBITM W                                       02630073
                   WHERE W.INTR_UPC_ID = :WEBITM-INTR-UPC-ID            02640073
                     AND W.EFF_BEG_TMST <= CURRENT TIMESTAMP            02650073
                     AND W.EFF_END_TMST IS NULL                         02660073
                   WITH UR                                              02670073
               END-EXEC                                                 02680073
               EVALUATE TRUE                                            02700073
                   WHEN SQLCODE = -904                                  02710073
                   WHEN SQLCODE = -913                                  02720073
                       CONTINUE                                         02740073
                   WHEN SQLCODE = ZERO                                  02750073
                       SET PS-TWEBITM-FOUND       TO TRUE               02770073
                   WHEN SQLCODE = +100                                  02780073
                       SET PS-TWEBITM-NOT-FOUND   TO TRUE               02790073
                   WHEN OTHER                                           02800073
                       MOVE WEBITM-INTR-UPC-ID    TO PV-DISPLAY-INTERNAL02810073
                       DISPLAY '** '                                    02820073
                               PC-CURRENT-PROGRAM-NAME                  02830073
                               ' - INTR UPC ID: '                       02840073
                               PV-DISPLAY-INTERNAL                      02850073
                       MOVE '5900-SELECT-TWEBITM' TO PV-PARAGRAPH-NAME  02860073
                       MOVE 'SELECT FROM TWEBITM' TO PV-DB2-OPERATION   02870073
                       PERFORM 9900-SQL-ABEND-ROUTINE                   02880073
               END-EVALUATE                                             02890073
           END-PERFORM.                                                 02900073
                                                                        02910073
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          02920073
               MOVE WEBITM-INTR-UPC-ID            TO PV-DISPLAY-INTERNAL02930073
               DISPLAY '** '                                            02940073
                       PC-CURRENT-PROGRAM-NAME                          02950073
                       ' - INTR UPC ID: '                               02960073
                       PV-DISPLAY-INTERNAL                              02970073
               MOVE '5900-SELECT-TWEBITM'         TO PV-PARAGRAPH-NAME  02980073
               MOVE 'SELECT FROM TWEBITM'         TO PV-DB2-OPERATION   02990073
               PERFORM 9900-SQL-ABEND-ROUTINE                           03000073
           END-IF.                                                      03010073
                                                                        03020073
      *----------------------------------------------------------       03030073
      * 8300-WRITE-SKU-GLBL-REC.                                        03040073
      *     WRITES THE FULL SKU GLOBAL RECORD TO THE FULL SKU           03050073
      *     GLOBAL FILE.                                                03060073
      *----------------------------------------------------------       03070073
       8300-WRITE-SKU-GLBL-REC.                                         03080073
                                                                        03090073
           WRITE FULL-SKU-GLOBAL-REC-OUT                                03100073
               FROM EP219-XST-SKU-RECORD.                               03110073
                                                                        03120073
           ADD 1 TO PC-CNT-GLBL-SKU-RECS.                               03130073
                                                                        03140073
      *----------------------------------------------------------       03150073
      * 9800-ABEND.                                                     03160073
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  03170073
      *----------------------------------------------------------       03180073
       9800-ABEND.                                                      03190073
                                                                        03200073
           DISPLAY '** ABEND           **'.                             03210073
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  03220073
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        03230073
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       03240073
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       03250073
           CALL 'ILBOABN0' USING ABEND-CODE.                            03260073
                                                                        03270073
      *----------------------------------------------------------       03280073
      * 9900-SQL-ABEND-ROUTINE.                                         03290073
      *     SQL ABEND ROUTINE.                                          03300073
      *----------------------------------------------------------       03310073
       9900-SQL-ABEND-ROUTINE.                                          03320073
                                                                        03330073
           SET ABEND-4013-DB2-ERROR TO TRUE.                            03340073
           DISPLAY '** ABEND     **'.                                   03350073
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        03360073
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              03370073
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               03380073
                                                                        03390073
           COPY DPPD004.                                                03400073
           CALL 'ILBOABN0' USING ABEND-CODE.                            03410073
                                                                        03420073
      *9900-EXIT.                                                       03430073
                                                                        03440073
      *----------------------------------------------------------       03450073
      * 9999-WRAPUP.                                                    03460073
      *     END OF PROGRAM ROUTINE.  CLOSES ALL FILES AND DISPLAYS      03470073
      *     ANY DATA FROM THE PROGRAM.                                  03480073
      *----------------------------------------------------------       03490073
       9999-WRAPUP.                                                     03500073
                                                                        03510073
           DISPLAY ' '.                                                 03520073
           DISPLAY '*********************************************'.     03530073
           DISPLAY 'PROGRAM NAME: ' PC-CURRENT-PROGRAM-NAME.            03540073
           DISPLAY '*********************************************'.     03550073
           DISPLAY ' '.                                                 03560073
           DISPLAY '              NUMBER GLOBAL SKU RECS READ:    '     03570073
                    PC-CNT-READ-RECS.                                   03580073
           DISPLAY '              NUMBER GLOBAL SKU RECS WRITTEN: '     03590073
                    PC-CNT-GLBL-SKU-RECS.                               03600073
           DISPLAY ' '.                                                 03610073
           DISPLAY '*********************************************'.     03620073
                                                                        03630073
