      ******************************************************************        
      *              SVPD006 - KMC APPROVAL NUMBER PROCEDURE           *        
      *                                                                *        
      *   THIS PROCEDURE DIVISION WILL BE USED WITH SVWS006 TO         *        
      *   CALCULATE THE FORCE COMPLIANCE NUMBER OR THE APPROVAL NUMBER *        
      *   FOR KOHL'S MERCHANDISE CARD TRANSACTIONS.                    *        
      *                                                                *        
      *   THE PROGRAMS WHICH USE THIS PROCEDURE MUST SUPPLY THE        *        
      *   FOLLOWING:                                                   *        
      *   1)  MOVE KMC CARD NUMBER           TO SV006-KMC-ID.          *        
      *   2)  MOVE TENDER AMOUNT             TO SV006-TENDER-AMT.      *        
      *   3)  MOVE TERMINAL NUMBER           TO SV006-TERMINAL-NBR.    *        
      *   4)  MOVE TRANSACTION NUMBER        TO SV006-TRANSACTION-NBR. *        
      *   5)  THE PROGRAM MUST INCLUDE THE FOLLOWING COPYBOOKS/        *        
      *       WORKING STORAGE AREAS:                                   *        
      *                SVWS006  - (FOR THIS KMC APPROVAL NBR PROCEDURE)*        
208819*                DPWS036  - (FOR CHECK DIGIT ROUTINE)            *        
      *                                                                *        
      *   THIS PROCEDURE WILL POPULATE THE FOLLOWING FIELD:            *        
      *   1)  SV006-KMC-APPROVAL-NUMBER                                *        
      ******************************************************************        
208819*  09/18/2018 - CHG0208819 - MAINFRAME REFACTORING               *        
208819*                            RENAMING CGU040MC TO DPPD036        *        
208819*                            RENAMING CGU042MC TO DPWS036        *        
      ******************************************************************        
                                                                                
      ******************************************************************        
      * SV006-A100-MAINLINE                                            *        
      ******************************************************************        
       SV006-A100-MAINLINE.                                                     
                                                                                
           PERFORM SV006-B100-INITIALIZE.                                       
                                                                                
           PERFORM SV006-B200-CALC-ADD-POSITIONS                                
               VARYING SV006-PV-SUB FROM 1 BY 1                                 
               UNTIL SV006-PV-SUB > SV006-PC-LENGTH-OF-TABLES.                  
                                                                                
           PERFORM SV006-B300-CALCULATE-OFFSET                                  
               VARYING SV006-PV-SUB FROM 1 BY 1                                 
               UNTIL SV006-PV-SUB > SV006-PC-LENGTH-OF-TABLES.                  
                                                                                
           PERFORM SV006-B400-APPROVAL-NUMBER                                   
               VARYING SV006-PV-SUB FROM 1 BY 1                                 
               UNTIL SV006-PV-SUB > SV006-PC-LENGTH-OF-TABLES.                  
                                                                                
           MOVE SV006-PV-APPROVAL-NUMBER TO SV006-KMC-APPROVAL-NBR.             
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * SV006-B100-INITIALIZE.                                         *        
      *                                                                *        
      *      INITIALIZES THE HIGH AND LOW ORDER TABLES BY PULLING      *        
      *      SPECIFIC DIGITS FROM THE KMC ID, TENDER AMOUNT, TERMINAL, *        
      *      AND TRANSACTION NUMBER.                                   *        
      *                                                                *        
208819*      USES MODULE DPPD036  TO CREATE THE CHECK DIGIT USED IN    *        
      *      THE LOW ORDER TABLE.                                      *        
      *                                                                *        
      *      PERFORMED BY SV006-A100-MAINLINE.                         *        
      ******************************************************************        
                                                                                
       SV006-B100-INITIALIZE.                                                   
                                                                                
           MOVE SV006-KMC-ID           TO SV006-PV-KMC-ID.                      
           MOVE SV006-TENDER-AMT       TO SV006-PV-TENDER-AMT.                  
                                                                                
           MOVE SV006-PV-TEND-DIGIT-3  TO SV006-PV-HIGH-ORDER-DIGIT(1).         
           MOVE SV006-PV-KMC-DIGIT-2   TO SV006-PV-HIGH-ORDER-DIGIT(2).         
           MOVE SV006-PV-TEND-DIGIT-5  TO SV006-PV-HIGH-ORDER-DIGIT(3).         
           MOVE SV006-TERMINAL-DIGIT-4 TO SV006-PV-HIGH-ORDER-DIGIT(4).         
           MOVE SV006-PV-KMC-DIGIT-6   TO SV006-PV-HIGH-ORDER-DIGIT(5).         
           MOVE SV006-PV-TEND-DIGIT-4  TO SV006-PV-HIGH-ORDER-DIGIT(6).         
                                                                                
           MOVE SV006-PV-KMC-DIGIT-9   TO SV006-PV-LOW-ORDER-DIGIT(1).          
           MOVE SV006-PV-TEND-DIGIT-2  TO SV006-PV-LOW-ORDER-DIGIT(2).          
           MOVE SV006-TRANS-DIGIT-3    TO SV006-PV-LOW-ORDER-DIGIT(3).          
           MOVE SV006-PV-KMC-DIGIT-8   TO SV006-PV-LOW-ORDER-DIGIT(4).          
           MOVE SV006-PV-TEND-DIGIT-6  TO SV006-PV-LOW-ORDER-DIGIT(5).          
                                                                                
           MOVE SV006-PV-LOW-ORDER-TABLE TO SV006-PV-BASE-LAST-6.               
           MOVE SV006-PV-BASE-NUMBER   TO BASE-NUMBER.                          
           PERFORM CREATE-CHECK-DIGIT-NUMBER.                                   
           MOVE BASE-NUMBER            TO SV006-PV-BASE-NUMBER.                 
           MOVE SV006-PV-BASE-LAST-1   TO SV006-PV-LOW-ORDER-DIGIT(6).          
                                                                                
      ******************************************************************        
      * SV006-B200-CALC-ADD-POSITIONS.                                 *        
      *                                                                *        
      *      ADDS THE HIGH ORDER AND THE LOW ORDER "COLUMNS" TOGETHER, *        
      *      AND THEN ADDS 13 TO THAT NUMBER.                          *        
      *                                                                *        
      *      FOR EXAMPLE:                                              *        
      *       HIGH ORDER TABLE:   0   2   9   5   3   5                *        
      *        LOW ORDER TABLE:  +6  +1  +5  +5  +8  +3                *        
      *                          --  --  --  --  --  --                *        
      *                           6   3  14  10  11   8                *        
      *                         +13 +13 +13 +13 +13 +13                *        
      *                          --  --  --  --  --  --                *        
      *          ADD POSITIONS:  19  16  27  23  24  21                *        
      *                                                                *        
      *                                                                *        
      *      PERFORMED BY SV006-A100-MAINLINE.                         *        
      ******************************************************************        
                                                                                
       SV006-B200-CALC-ADD-POSITIONS.                                           
                                                                                
           COMPUTE SV006-PV-ADD-POSITION(SV006-PV-SUB) =                        
               SV006-PV-HIGH-ORDER-DIGIT(SV006-PV-SUB) +                        
               SV006-PV-LOW-ORDER-DIGIT(SV006-PV-SUB) + 13.                     
                                                                                
      ******************************************************************        
      * SV006-B300-CALCULATE-OFFSET.                                   *        
      *                                                                *        
      *      THE OFFSET IS CALCULATED FOR EACH HIGH ORDER/LOW ORDER    *        
      *      COLUMN BY ADDING THE ADD POSITIONS TO THE OFFSET OF THE   *        
      *      PREVIOUS NUMBER (STARTING WITH ZERO ON THE FIRST) AND     *        
      *      SUBTRACTING THE NUMBER OF RELATIVE CHARACTERS IF THE      *        
      *      OFFSET IS GREATER THAN THE NUMBER OF RELATIVE CHARACTERS. *        
      *      THE RESULTING NUMBER IS THEN USED TO RETRIEVE THE RELATIVE*        
      *      CHARACTER NUMBER ASSOCIATED WITH IT.                      *        
      *                                                                *        
      *      FOR EXAMPLE:                                              *        
      *          THE ADD POSITIONS USED BELOW ARE FROM THE EXAMPLE     *        
      *          USED IN THE COMMENTS FOR SV006-B200-CALC-ADD-POSITIONS*        
      *                                                                *        
      *          IN THIS EXAMPLE, THE NUMBER OF RELATIVE CHARACTERS    *        
      *          IS 32.                                                *        
      *                                                                *        
      *          ADD POSITIONS:  19  16  27  23  24  21                *        
      *                 OFFSET:   0  19  03  30  21  13                *        
      *                          --  --  --  --  --  --                *        
      *                          19  35  30  53  45  34                *        
      *                             -32     -32 -32 -32                *        
      *                              --      --  --  --                *        
      *                               3      21  13   2                *        
      *                                                                *        
      *          THIS CALCULATION STARTS ON THE LEFT.  THE FIRST ADD   *        
      *          POSITION IS ADDED TO ZERO.  IN THIS CASE, THE RESULT  *        
      *          OF THE FIRST CALCULATION IS 19.  19 IS THEN ADDED TO  *        
      *          THE SECOND ADD POSITION.  THE RESULT OF THE SECOND    *        
      *          CALCULATION, 35, IS > 32, SO 32 IS SUBTRACTED FROM    *        
      *          35.  EACH "ADD POSITION" IS ADDED TO THE RESULT OF    *        
      *          THE CALCULATION TO ITS LEFT UNTIL THE END.            *        
      *                                                                *        
      *      PERFORMED BY SV006-A100-MAINLINE.                         *        
      ******************************************************************        
                                                                                
       SV006-B300-CALCULATE-OFFSET.                                             
                                                                                
           IF SV006-PV-SUB = 1                                                  
               MOVE SV006-PV-ADD-POSITION(SV006-PV-SUB)                         
                                        TO SV006-PV-OFFSET(SV006-PV-SUB)        
           ELSE                                                                 
               COMPUTE SV006-PV-OFFSET(SV006-PV-SUB) =                          
                   SV006-PV-OFFSET(SV006-PV-SUB - 1) +                          
                   SV006-PV-ADD-POSITION(SV006-PV-SUB)                          
           END-IF.                                                              
                                                                                
           IF SV006-PV-OFFSET(SV006-PV-SUB) >                                   
                                            SV006-PC-NUMBER-OF-REL-CHARS        
               SUBTRACT SV006-PC-NUMBER-OF-REL-CHARS FROM                       
                        SV006-PV-OFFSET(SV006-PV-SUB)                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * SV006-B400-APPROVAL-NUMBER.                                    *        
      *                                                                *        
      *      THE APPROVAL NUMBER IS DERIVED BY TAKING THE OFFSET       *        
      *      NUMBER CALCULATED ABOVE AND MATCHING IT TO A LIST OF      *        
      *      RELATIVE CHARACTERS.                                      *        
      *                                                                *        
      *      THE RELATIVE CHARACTER LIST CONTAINS THE LETTERS A THRU Z *        
      *      EXCLUDING THE LETTER "O" AND THEN THE NUMBERS 1 THRU 9,   *        
      *      ZERO IS NOT USED EITHER.  THE LETTERS "U" AND "V" HAVE    *        
      *      ALSO BEEN EXCLUDED FROM THIS LIST.                        *        
      *                                                                *        
      *      IF THE RESULT OF THE OFFSET CALCULATION WAS 19, THE       *        
      *      RELATIVE CHARACTER WOULD BE "T".  IF THE RESULT WAS 26,   *        
      *      THE CHARACTER WOULD BE THE NUMBER THREE.                  *        
      *                                                                *        
      *      FOR EXAMPLE:                                              *        
      *          THE APPROVAL NUMBER FOR THE EXAMPLE BEING USED        *        
      *          WOULD BE:                                             *        
      *                                                                *        
      *         RESULT OF CALCULATION:  19  03  30  21  13  02         *        
      *            RELATIVE CHARACTER:  T   C   7   X   M   B          *        
      *                                                                *        
      *      PERFORMED BY SV006-A100-MAINLINE.                         *        
      ******************************************************************        
                                                                                
       SV006-B400-APPROVAL-NUMBER.                                              
                                                                                
           MOVE SV006-PV-OFFSET(SV006-PV-SUB) TO SV006-PV-CHAR-SUB.             
           MOVE SV006-PC-RELATIVE-CHARACTER(SV006-PV-CHAR-SUB)                  
                               TO SV006-PV-APPROVAL-DIGIT(SV006-PV-SUB).        
                                                                                
      ******************************************************************        
      * CREATE-CHECK-DIGIT-NUMBER.                                     *        
      *                                                                *        
      *      CREATE THE CHECK DIGIT FOR THE LOW ORDER TABLE.           *        
      *                                                                *        
      *      PERFORMED BY SV006-B100-INITIALIZE.                       *        
      ******************************************************************        
208819     COPY DPPD036.                                                        
                                                                                
       EJECT                                                                    
