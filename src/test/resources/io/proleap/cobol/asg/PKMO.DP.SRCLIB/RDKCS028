000100*-----------------------*                                         03/19/93
000200 IDENTIFICATION DIVISION.                                         RDKCS028
000300*-----------------------*                                            LV013
000400 PROGRAM-ID.    RDKCS028.                                         RDKCS028
000500 AUTHOR.        ANGIE COLE.                                       RDKCS028
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                          RDKCS028
000700 DATE-WRITTEN.  01-22-97.                                            CL*12
000800 DATE-COMPILED.                                                   RDKCS028
000900*----------------------------------------------------------------*RDKCS028
001000*    U028 - KMRC DRIVERS LICENSE CRITERIA SPECIFICATION          *   CL**2
001100*                                                                *RDKCS028
001200*    THIS PROGRAM ALLOWS THE USER TO ENTER DATA TO BE USED       *   CL**2
001300*    SUBSEQUENT PROGRAMS TO CREATE A LIST OF KMRC WHICH ARE      *   CL**2
001400*    ASSOCIATED WITH THE ENTERED DRIVERS LICENSE NUMBER.         *   CL**2
001700*----------------------------------------------------------------*RDKCS028
124679* 9/19/15        JIM ARENDT   CHG0124679                                  
124679*  AS PART OF THE LP REFUND REMEDIATION PROJECT COMMENTED OUT THE         
124679*  PERFORM OF PARA 3500-CHECK-FOR-FIVE-DIGITS.  USERS MUST NOW            
124679*  ENTER EITHER THE FULL ENCRYPTED OR UN-ENCRYPTED DRIVERS LIC NBR        
001700*----------------------------------------------------------------*RDKCS028
CICS  * CHANGED 10/27/2022    BY JIM HUNTER      WR: CHG0277293                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  ******************************************************************        
001800                                                                  RDKCS028
001900                                                                  RDKCS028
002000                                                                  RDKCS028
002100 ENVIRONMENT DIVISION.                                            RDKCS028
002200                                                                  RDKCS028
002300                                                                  RDKCS028
002400                                                                  RDKCS028
002500 DATA DIVISION.                                                   RDKCS028
002600                                                                  RDKCS028
002700                                                                  RDKCS028
002800 WORKING-STORAGE SECTION.                                         RDKCS028
002900*                                                                 RDKCS028
003000 01  PC-PROGRAM-CONSTANTS.                                        RDKCS028
003100     05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'RD028A  '.    CL**7
003200     05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'RDKM028 '.    CL**7
003300     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'RDKCS028'.    CL**7
003400     05  PC-TSYMSG-NUMBERS.                                       RDKCS028
003600         10  PC-TSYMSG-01532       PIC  9(05)  VALUE  01532.         CL**7
003601         10  PC-TSYMSG-01533       PIC  9(05)  VALUE  01533.         CL**7
003602         10  PC-TSYMSG-01534       PIC  9(05)  VALUE  01534.         CL**7
003603     05  PC-MIN-DIGITS             PIC  9(01)  VALUE  6.             CL**7
003604     05  PC-MIN-DIGITS-W-ASTERIX   PIC  9(01)  VALUE  7.             CL**7
003800*                                                                 RDKCS028
003900 01  PS-PROGRAM-SWITCHES.                                         RDKCS028
004000     05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.           CL**7
004100         88  PS-ERROR                          VALUE  'Y'.           CL**7
004200         88  PS-NO-ERROR                       VALUE  'N'.           CL**7
004201     05  PS-VALID-DRV-LIC-NBR-SW   PIC  X(01)  VALUE  'N'.           CL**7
004202         88  PS-VALID-DRV-LIC-NBR              VALUE  'Y'.           CL**7
004203         88  PS-NO-VALID-DRV-LIC-NBR           VALUE  'N'.           CL**7
004204     05  PS-DONE-SW                PIC  X(01)  VALUE  'N'.           CL**7
004205         88  PS-DONE                           VALUE  'Y'.           CL**7
004206         88  PS-NOT-DONE                       VALUE  'N'.           CL**7
004300                                                                     CL**7
004301 01  PM-PROGRAM-MESSAGES.                                                 
004303     05  PM-ABEND-INFO-A.                                                 
004304         10  FILLER                PIC X(15)   VALUE                      
004305                                   'DP020-SRC-AID: '.                     
004306         10  PM-A-SRC-AID          PIC X(02)   VALUE  SPACE.              
004307         10  FILLER                PIC X(19)   VALUE                      
004308                                   '. DP020-FK-ACTION: '.                 
004309         10  PM-A-FK-ACTION        PIC X(01)   VALUE  SPACE.              
004310         10  FILLER                PIC X(22)   VALUE                      
004311                                   '. DP020-FK-DISPN-IND: '.              
004312         10  PM-A-FK-DISPN         PIC X(01)   VALUE  SPACE.              
004313         10  FILLER                PIC X(01)   VALUE '.'.                 
004314         10  FILLER                PIC X(19)   VALUE SPACE.               
004315                                                                          
004316 01  PV-PROGRAM-VARIABLES.                                        RDKCS028
124679**   05  PV-DRV-LIC-NBR                 PIC  X(21)  VALUE SPACE.  RDKCS029
124679     05  PV-DRV-LIC-NBR                 PIC  X(50)  VALUE SPACE.  RDKCS029
004318     05  PV-DRV-LIC-NBR-BYTE            REDEFINES PV-DRV-LIC-NBR  DPKCS036
124679**                                      OCCURS 21 TIMES           DPKCS036
124679                                        OCCURS 50 TIMES           DPKCS036
004320                                        PIC  X(01).               DPKCS036
004330     05  PV-SUB                         PIC S9(04)  VALUE ZEROS           
004340                                                    COMP.                 
004341     05  PV-MIN-DIGITS                  PIC  9(01)  VALUE ZEROS.          
004342                                                                          
004400*----------------------------------------------------------------*   CL**7
004500*    MAP LAYOUT                                                  *   CL**7
004600*----------------------------------------------------------------*   CL**7
004700                                                                     CL**7
004800     COPY RDKM028.                                                   CL**7
004900     EJECT                                                        RDKCS028
005000*----------------------------------------------------------------*RDKCS028
005100*    ATTRIBUTE SETTINGS COPYBOOK.                                *RDKCS028
005200*----------------------------------------------------------------*RDKCS028
005300                                                                  RDKCS028
005400     COPY DPWS015.                                                RDKCS028
005500                                                                  RDKCS028
005600*----------------------------------------------------------------*RDKCS028
005700*    FUNCTION KEYS COPYBOOK                                      *RDKCS028
005800*----------------------------------------------------------------*RDKCS028
005900                                                                  RDKCS028
006000     COPY DPWS016.                                                RDKCS028
006100 EJECT                                                               CL**7
006200******************************************************************   CL**7
006300** THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT    **   CL**7
006400** SUB-ROUTINE (DPKUT100).                                      **   CL**7
006500******************************************************************   CL**7
006600     COPY DPWS010I.                                                  CL**7
006700 EJECT                                                               CL**7
006800******************************************************************   CL**7
006900** THE CALLING PARAMETER LIST (DPWS030) FOR THE CICS            **   CL**7
007000** ARCHITECTURE API (BLACK BOX) SUB-ROUTINE.                    **   CL**7
007100******************************************************************   CL**7
007200     COPY DPWS030.                                                   CL*13
CICS       COPY DPWS930.                                                   CL*13
007300 EJECT                                                               CL**7
007400*****************************************************************    CL**7
007500*    STANDARD COMMAREA.                                              CL**7
007600*****************************************************************    CL**7
007700                                                                     CL**7
007800     COPY DPWS020.                                                   CL**7
007900     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                   CL**7
008000                                                                     CL**7
008100*****************************************************************    CL**7
008200*    INTER-APPLICATION COMMAREA.                                     CL**7
008300*****************************************************************    CL**7
008400                                                                     CL**7
008500         10  INTER-APPL-COMM-AREA PIC X(200).                        CL**7
008600     COPY RDWS024.                                                   CL*13
008700*****************************************************************    CL**7
008800*    SPECIFIC COMMMAREA FOR RDKCS028.                                CL**7
008900*****************************************************************    CL**7
009000         10  ASC-SPECIFIC-COMMAREA.                                  CL**7
009100             15  ASC-DRV-LIC-NBR             PIC  X(21).             CL**7
124679             15  ASC-DRV-LIC-NBR-LONG        PIC  X(50).             CL**7
009700     EJECT                                                           CL**7
009800*----------------------------------------------------------------*   CL**7
009900*    ABEND PROCESSING COPYBOOK.                                  *   CL**7
010000*----------------------------------------------------------------*   CL**7
010100                                                                     CL**7
010200     COPY DPWS013.                                                   CL**7
010300                                                                     CL**7
024400                                                                  02440021
010400                                                                  RDKCS028
010500 EJECT                                                            RDKCS028
010600 LINKAGE SECTION.                                                 RDKCS028
010700                                                                  RDKCS028
010800 01  DFHCOMMAREA.                                                 RDKCS028
010900     05  FILLER                         OCCURS  1 TO 4072 TIMES   RDKCS028
011000                                        DEPENDING ON EIBCALEN.    RDKCS028
011100         10  FILLER                     PIC X.                    RDKCS028
011200                                                                  RDKCS028
011300                                                                  RDKCS028
011400 PROCEDURE DIVISION.                                              RDKCS028
011500***************************************************************** RDKCS028
011600** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. ** RDKCS028
011700** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  ** RDKCS028
011800** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            ** RDKCS028
011900**                                                             ** RDKCS028
012000** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  ** RDKCS028
012100** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       ** RDKCS028
012200***************************************************************** RDKCS028
012300 0000-MAIN-MODULE.                                                RDKCS028
012400     INITIALIZE DP030-CICS-API-FIELDS.                            RDKCS028
012500     MOVE +1                       TO DP030-NUMBER-OF-MAPS.          CL*11
012600     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.             CL*11
012700     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).            CL*11
012800     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                          CL*11
012900     MOVE LENGTH OF RD028AI        TO DP030-MAP-LENGTH (1).          CL*11
013000     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).        CL*11
124679                                                                          
124679*  THIS ENSURES THAT IF THE DATA COMING IN IS MIXED CASE IT               
124679*  DOESN'T GET CONVERTED TO ALL UPPER CASE.                               
124679     EXEC CICS                                                            
124679          SET TERMINAL(EIBTRMID) UCTRAN                                   
124679     END-EXEC.                                                            
124679                                                                          
013100     PERFORM 0001-CALL-CICS-ARCH-API.                             RDKCS028
013200*                                                                 RDKCS028
013300     PERFORM 1000-CONTROL-PROCESSING                              RDKCS028
013400*                                                                 RDKCS028
124679*  THIS TURNS OFF THE MIXED CASE CONVERSION                               
124679     EXEC CICS                                                            
124679          SET TERMINAL(EIBTRMID) NOUCTRAN                                 
124679     END-EXEC.                                                            
013400*                                                                 RDKCS028
013500     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).        CL*11
013600     PERFORM 0001-CALL-CICS-ARCH-API.                             RDKCS028
013700                                                                  RDKCS028
013800 0001-CALL-CICS-ARCH-API.                                         RDKCS028
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
014000                                    DFHCOMMAREA                           
014100                                    DP030-CICS-API-FIELDS                 
014200                                    DP020-STANDARD-COMMAREA               
014300                                    RD028AI.                              
014400*                                                                 RDKCS028
014500     IF  DP030-RC-CALL-SUCCESSFUL                                 RDKCS028
014600         CONTINUE                                                 RDKCS028
014700     ELSE                                                         RDKCS028
014800         SET DP013-NO-ROLLBACK                                       CL*11
014900             DP013-XCTL-DISPLAY-RESTART                              CL*11
015000             DP013-CICS-ABEND      TO TRUE                           CL*11
015100         MOVE 'BEFORE 0000-MAIN-MODULE'                              CL*11
015200                                   TO DP013-PARAGRAPH                CL*11
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
015500         MOVE DP030-RETURN-CODE                                   RDKCS028
015600                                   TO DP013-MESSAGE-TEXT (3)         CL*11
015700         PERFORM DP013-0000-PROCESS-ABEND                         RDKCS028
015800     END-IF.                                                      RDKCS028
015900 EJECT                                                            RDKCS028
107100                                                                          
016000*----------------------------------------------------------------*RDKCS028
016100*    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *RDKCS028
016200*    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *RDKCS028
016300*----------------------------------------------------------------*RDKCS028
016400                                                                  RDKCS028
016500 1000-CONTROL-PROCESSING.                                         RDKCS028
016600                                                                  RDKCS028
016700     EVALUATE TRUE                                                RDKCS028
016800         WHEN DP020-NEXT-ACT-INITIAL                              RDKCS028
016900             INITIALIZE ASC-SPECIFIC-COMMAREA                     RDKCS028
017000             PERFORM 4000-BUILD-INITIAL-PANEL                        CL**7
017100                                                                  RDKCS028
017200         WHEN DP020-NEXT-ACT-READ-MAP                             RDKCS028
017300             PERFORM 2000-FKEY-PROCESSING                         RDKCS028
017400                                                                  RDKCS028
017500         WHEN DP020-NEXT-ACT-RETURN                               RDKCS028
017600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL**9
017700             PERFORM 3000-EDIT-DATA-IN-COMMAREA                      CL**7
017800                                                                  RDKCS028
017900         WHEN OTHER                                               RDKCS028
018000             SET DP013-LOGIC-ABEND                                   CL*11
018100                 DP013-NO-ROLLBACK TO TRUE                           CL*11
018200             MOVE '1000-CONTROL-PROCESSING'                       RDKCS028
018300                                   TO  DP013-PARAGRAPH               CL*11
018400             MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'   RDKCS028
018500                                   TO  DP013-MESSAGE-TEXT(1)         CL*11
018600             MOVE DP020-NEXT-APPL-ACTIVITY                        RDKCS028
018700                                   TO DP013-MESSAGE-TEXT(2)          CL*11
018800             PERFORM DP013-0000-PROCESS-ABEND                     RDKCS028
018900     END-EVALUATE.                                                RDKCS028
019000                                                                  RDKCS028
019100*----------------------------------------------------------------*RDKCS028
019200* FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*   CL**7
019300* SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*   CL**7
019400* EDITS.                                                         *   CL**7
019500*----------------------------------------------------------------*RDKCS028
019600                                                                  RDKCS028
019700 2000-FKEY-PROCESSING.                                            RDKCS028
019800                                                                  RDKCS028
019900     EVALUATE TRUE                                                RDKCS028
020000                                                                  RDKCS028
020100         WHEN DP020-SRC-AID = DP016-CLEAR                         RDKCS028
020200             PERFORM 3000-EDIT-DATA-IN-COMMAREA                      CL**7
020300             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL**9
020400                                                                  RDKCS028
020500         WHEN DP020-FK-REFRESH(DP020-SRC-AID)                     RDKCS028
020600             INITIALIZE ASC-SPECIFIC-COMMAREA                     RDKCS028
020700             PERFORM 4000-BUILD-INITIAL-PANEL                        CL**7
020800             PERFORM 3200-RESET-ATTRIBUTES                           CL*11
023010                                                                          
023011         WHEN OTHER                                                  CL**7
023012             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                    CL**7
023013             PERFORM 3000-EDIT-DATA-IN-COMMAREA                      CL**7
023014             IF  PS-NO-ERROR                                         CL**7
023015                 SET DP030-CONTINUE-GO TO TRUE                       CL*11
023016                 PERFORM 2100-CHECK-FUNCTION-KEY                     CL**7
023017             ELSE                                                    CL*11
023018                 SET DP030-OVERRIDE-GO TO TRUE                       CL*11
023019             END-IF                                                  CL*11
023020     END-EVALUATE.                                                RDKCS028
023021******************************************************************   CL**7
023022** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **   CL**7
023023** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **   CL**7
023024** FROM THE CICS ARCHITECTURE API.                              **   CL**7
023025******************************************************************   CL**7
023026 2100-CHECK-FUNCTION-KEY.                                            CL**7
023027     EVALUATE TRUE                                                   CL**7
023028*        WHEN DP020-FK-LOCAL-GOTO (DP020-SRC-AID)                    CL**7
023029         WHEN DP020-FK-DISPN-GO (DP020-SRC-AID)                           
023030             PERFORM 2110-PREPARE-INTER-APPL-COMM                    CL*11
023031                                                                     CL*12
023040         WHEN DP020-SRC-AID = DP016-ENTER                            CL*12
023100             CONTINUE                                                CL*12
023200                                                                     CL**7
023201         WHEN (DP020-FK-GO-TO-TRAN(DP020-SRC-AID)) OR             RDKCS028
023202              (DP020-FK-RETURN(DP020-SRC-AID))                    RDKCS028
023203             CONTINUE                                             RDKCS028
023204                                                                          
023205                                                                        RD
023300         WHEN OTHER                                                  CL**7
023400             SET DP013-NO-ROLLBACK                                   CL*11
023500                 DP013-XCTL-DISPLAY-RESTART                          CL*11
023600                 DP013-CICS-ABEND  TO TRUE                           CL*11
023700             MOVE '2100-CHECK-FUNCTION-KEY'                          CL**7
023800                                   TO DP013-PARAGRAPH                CL*11
023900             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'         CL**7
024000                                   TO DP013-MESSAGE-TEXT (1)         CL*11
024010             MOVE 'OR CODE MISSING FROM THIS PROGRAM TO PROCESS:'         
024020                                   TO DP013-MESSAGE-TEXT (2)              
024021             MOVE DP020-SRC-AID    TO PM-A-SRC-AID                        
024022             MOVE DP020-FK-ACTION (DP020-SRC-AID)                         
024023                                   TO PM-A-FK-ACTION                      
024024             MOVE DP020-FK-DISPN-IND (DP020-SRC-AID)                      
024025                                   TO PM-A-FK-DISPN                       
024030             MOVE PM-ABEND-INFO-A  TO DP013-MESSAGE-TEXT (3)              
024070             MOVE 'REVIEW PFKEYS SETUP FOR THIS MNEMONIC.'                
024080                                   TO DP013-MESSAGE-TEXT (4)              
024100             PERFORM DP013-0000-PROCESS-ABEND                        CL**7
024200     END-EVALUATE.                                                   CL**7
024300******************************************************************   CL**9
024400** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **   CL**9
024500** INTER-APPLICATION COMMAREA.                                  **   CL**9
024600******************************************************************   CL**9
024700 2110-PREPARE-INTER-APPL-COMM.                                       CL**9
024800     INITIALIZE RD024-INTER-APPL-COMMAREA.                           CL**9
024900     MOVE ASC-DRV-LIC-NBR          TO RD024-DRV-LIC-NBR.             CL*11
124679     MOVE ASC-DRV-LIC-NBR-LONG     TO RD024-DRV-LIC-NBR-LONG.        CL*11
025300                                                                  RDKCS028
025300 EJECT                                                            RDKCS028
025400*----------------------------------------------------------------*RDKCS028
025500* MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*   CL**7
025600* THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *   CL**7
025700* APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *   CL**7
025800*----------------------------------------------------------------*RDKCS028
025900                                                                  RDKCS028
026000 2200-MOVE-SCREEN-TO-COMMAREA.                                       CL**7
026000                                                                          
026100     IF  ADRLCNL > ZERO                                              CL**2
124679         MOVE ADRLCNI             TO ASC-DRV-LIC-NBR                 CL*11
124679                                     ASC-DRV-LIC-NBR-LONG            CL*11
026300     ELSE                                                         RDKCS028
026400         IF  ADRLCNF = DP015-ERASE-EOF                               CL**2
026500             MOVE SPACES           TO ASC-DRV-LIC-NBR                CL*11
124679                                      ASC-DRV-LIC-NBR-LONG           CL*11
026600         END-IF                                                   RDKCS028
026700     END-IF.                                                      RDKCS028
026800                                                                  RDKCS028
029200*----------------------------------------------------------------*RDKCS028
029300* ENSURE THE DATA IS CORRECT.  NOTE THAT IN A CRITERIA SPECIFI-  *   CL**7
029400* CATION SCREEN ONLY MINIMAL EDITS ARE DONE.  EDITS RELATED TO   *   CL**7
029500* A SPECIFIC SCREEN THAT THE USER IS GOING TO (LOCAL-GO) ARE DONE*   CL**7
029600* IN THE DESTINATION PROGRAM, NOT HERE.                          *   CL**7
029601* 1)  VERIFY DRIVERS LICENSE IS NOT SPACES.                      *   CL**7
029602* 2)  VERIFY THE NUMBER OF DIGITS ENTERED IS AT LEAST 5.         *   CL**7
029603*     IF AN ASTERIX IS ENTERED FIRST, VERIFY THERE IS AT LEAST   *   CL**7
029604*     5 MORE DIGITS AFTER THAT.                                  *   CL**7
029605*     VERIFY THAT THERE ARE LEAST 5 DIGITS BEFORE AN ASTERISK.   *   CL**7
029700*----------------------------------------------------------------*   CL**7
029800                                                                  RDKCS028
029900 3000-EDIT-DATA-IN-COMMAREA.                                         CL**7
030000     PERFORM 3200-RESET-ATTRIBUTES.                                  CL*11
030001                                                                          
124679     IF  ASC-DRV-LIC-NBR      = SPACES                            RDKCS028
124679     AND ASC-DRV-LIC-NBR-LONG = SPACES                            RDKCS028
030003*   ---- DRIVERS LICENSE NUMBER REQUIRED ---                              
030004        MOVE PC-TSYMSG-01532 TO DP020-MSG-NUMBER                  RDKCS028
030005        MOVE -1              TO ADRLCNL                           RDKCS028
030006        MOVE DP015-RED       TO ADRLCNC                           RDKCS028
030007        MOVE DP015-REVERSE   TO ADRLCNH                           RDKCS028
031600        SET DP030-SET-CURSOR-APPL-1 TO TRUE                          CL**2
031601        SET DP020-MSG-FATAL TO TRUE                               RDKCS028
031602        SET PS-ERROR        TO TRUE                               RDKCS028
031603     ELSE                                                                 
124679**      PERFORM 3500-CHECK-FOR-FIVE-DIGITS                                
124679        PERFORM 3600-DL-ROUTINE                                           
031605     END-IF.                                                              
031606                                                                          
033000******************************************************************   CL**7
033100** MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE **   CL**7
033200** MAP.                                                         **   CL**7
033300******************************************************************   CL**7
033400 3200-RESET-ATTRIBUTES.                                              CL*11
033500     MOVE DP015-UNP-ALP-NOR-OFF    TO ADRLCNA.                       CL*11
033900     MOVE DP015-GREEN              TO ADRLCNC.                       CL*11
034300     MOVE DP015-UNDERLINE          TO ADRLCNH.                       CL*11
034700*                                                                    CL**9
034701*----------------------------------------------------------------*RDKCS028
034702* 1)  VERIFY THE NUMBER OF DIGITS ENTERED IS AT LEAST 5.         *   CL**7
034703*     IF AN ASTERIX IS ENTERED FIRST, VERIFY THERE IS AT LEAST   *   CL**7
034704*     5 MORE DIGITS AFTER THAT.                                  *   CL**7
034705* 2)  VERIFY THAT THERE ARE LEAST 5 DIGITS BEFORE AN ASTERISK.   *   CL**7
034706*----------------------------------------------------------------*   CL**7
034707                                                                  RDKCS028
034708 3500-CHECK-FOR-FIVE-DIGITS.                                         CL**7
034709                                                                          
034710     MOVE ASC-DRV-LIC-NBR TO PV-DRV-LIC-NBR.                              
034720                                                                          
034730     IF PV-DRV-LIC-NBR-BYTE (1) = '*'                                     
034740        MOVE PC-MIN-DIGITS-W-ASTERIX TO PV-MIN-DIGITS                     
034750     ELSE                                                                 
034760        MOVE PC-MIN-DIGITS           TO PV-MIN-DIGITS                     
034770     END-IF.                                                              
034780                                                                          
034790     SET PS-NOT-DONE      TO TRUE.                                        
034800     PERFORM VARYING PV-SUB                                               
034900        FROM 1 BY 1                                                       
035000        UNTIL PV-SUB EQUAL PV-MIN-DIGITS                                  
035100           OR PS-DONE                                                     
035200              IF PV-DRV-LIC-NBR-BYTE (PV-SUB) = SPACE                     
035300                 SET PS-DONE TO TRUE                                      
035400              END-IF                                                      
035500     END-PERFORM.                                                         
035501                                                                          
035502     IF PS-DONE AND (PV-DRV-LIC-NBR-BYTE (1) = '*')               RDKCS028
035503*       ---- AT LEAST 5 DIGITS MUST BE ENTERED AFTER ASTERIX ---          
035504        MOVE PC-TSYMSG-01533 TO DP020-MSG-NUMBER                  RDKCS028
035505        MOVE -1              TO ADRLCNL                           RDKCS028
035506        MOVE DP015-RED       TO ADRLCNC                           RDKCS028
035507        MOVE DP015-REVERSE   TO ADRLCNH                           RDKCS028
035508        SET DP030-SET-CURSOR-APPL-1 TO TRUE                          CL**2
035509        SET DP020-MSG-FATAL  TO TRUE                              RDKCS028
035510        SET PS-ERROR         TO TRUE                              RDKCS028
035511     ELSE                                                                 
035512     IF (PV-DRV-LIC-NBR-BYTE (1) = '*') AND                       RDKCS028
035513        (PV-DRV-LIC-NBR-BYTE (6) = '*') AND                       RDKCS028
035514        (PV-DRV-LIC-NBR-BYTE (7) = ' ')                           RDKCS028
035515*       ---- AT LEAST 5 DIGITS MUST BE ENTERED AFTER ASTERIX ---          
035516        MOVE PC-TSYMSG-01533 TO DP020-MSG-NUMBER                  RDKCS028
035517        MOVE -1              TO ADRLCNL                           RDKCS028
035518        MOVE DP015-RED       TO ADRLCNC                           RDKCS028
035519        MOVE DP015-REVERSE   TO ADRLCNH                           RDKCS028
035520        SET DP030-SET-CURSOR-APPL-1 TO TRUE                          CL**2
035521        SET DP020-MSG-FATAL  TO TRUE                              RDKCS028
035522        SET PS-ERROR         TO TRUE                              RDKCS028
035523     ELSE                                                                 
035524     IF PS-DONE                                                   RDKCS028
035525*       ---- AT LEAST 5 DIGITS MUST BE ENTERED ----                       
035526        MOVE PC-TSYMSG-01534 TO DP020-MSG-NUMBER                  RDKCS028
035527        MOVE -1              TO ADRLCNL                           RDKCS028
035528        MOVE DP015-RED       TO ADRLCNC                           RDKCS028
035529        MOVE DP015-REVERSE   TO ADRLCNH                           RDKCS028
035530        SET DP030-SET-CURSOR-APPL-1 TO TRUE                          CL**2
035531        SET DP020-MSG-FATAL  TO TRUE                              RDKCS028
035532        SET PS-ERROR         TO TRUE                              RDKCS028
035533     ELSE                                                                 
035534     IF (PV-DRV-LIC-NBR-BYTE (5) = '*') AND                       RDKCS028
035535        (PV-DRV-LIC-NBR-BYTE (6) = ' ')                           RDKCS028
035536*       ---- AT LEAST 5 DIGITS MUST BE ENTERED ----                       
035537        MOVE PC-TSYMSG-01534 TO DP020-MSG-NUMBER                  RDKCS028
035538        MOVE -1              TO ADRLCNL                           RDKCS028
035539        MOVE DP015-RED       TO ADRLCNC                           RDKCS028
035540        MOVE DP015-REVERSE   TO ADRLCNH                           RDKCS028
035541        SET DP030-SET-CURSOR-APPL-1 TO TRUE                          CL**2
035542        SET DP020-MSG-FATAL  TO TRUE                              RDKCS028
035543        SET PS-ERROR         TO TRUE.                             RDKCS028
035544                                                                          
124679*----------------------------------------------------------------*RDKCS028
124679*  DETERMINE WHICH VERSION OF THE DRIVERS LICENSE WAS ENTERED    *RDKCS028
124679*----------------------------------------------------------------*RDKCS028
124679 3600-DL-ROUTINE.                                                         
124679                                                                          
124679     IF ASC-DRV-LIC-NBR-LONG > SPACES                                     
124679        MOVE ASC-DRV-LIC-NBR-LONG  TO PV-DRV-LIC-NBR                      
124679     ELSE                                                                 
124679        MOVE ASC-DRV-LIC-NBR       TO PV-DRV-LIC-NBR                      
124679     END-IF.                                                              
124679                                                                          
124679     IF PV-DRV-LIC-NBR-BYTE (30) > ' '                            RDKCS028
124679        MOVE SPACES             TO ASC-DRV-LIC-NBR                        
124679     ELSE                                                                 
124679        MOVE SPACES             TO ASC-DRV-LIC-NBR-LONG                   
124679     END-IF.                                                              
124679                                                                          
035545*----------------------------------------------------------------*RDKCS028
035546*  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *RDKCS028
035547*  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *RDKCS028
035548*  IT HERE.                                                      *RDKCS028
035549*----------------------------------------------------------------*RDKCS028
035550 4000-BUILD-INITIAL-PANEL.                                           CL**7
035551     SET DP030-SET-CURSOR-APPL-1   TO TRUE                           CL*11
035552     MOVE -1                       TO ADRLCNL.                       CL*11
035600*                                                                    CL**9
035700*----------------------------------------------------------------*   CL**9
035800*    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *   CL**9
035900*----------------------------------------------------------------*   CL**9
036000 4400-MOVE-COMMAREA-TO-SCREEN.                                       CL**9
124679*    MOVE ASC-DRV-LIC-NBR          TO ADRLCNO.                       CL*11
124679     IF ASC-DRV-LIC-NBR > SPACES                                     CL*11
124679        MOVE ASC-DRV-LIC-NBR       TO ADRLCNO                        CL*11
124679     ELSE                                                            CL*11
124679        MOVE ASC-DRV-LIC-NBR-LONG  TO ADRLCNO                        CL*11
124679     END-IF.                                                         CL*11
035600*                                                                    CL**9
036600*----------------------------------------------------------------*RDKCS028
036700*    ABEND PROCESSOR MODULE                                      *RDKCS028
036800*----------------------------------------------------------------*RDKCS028
036900                                                                  RDKCS028
037000     COPY DPPD013.                                                RDKCS028
