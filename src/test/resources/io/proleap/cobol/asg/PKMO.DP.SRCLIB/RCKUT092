000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    RCKUT092.                                                 
000600 AUTHOR.        DENISE HAHN                                               
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   SEPTEMBER, 2018.                                          
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM WILL CREATE AN EXTRACT FILE OF EFC FREE ASTRAY            
001310*  RECEIVERS THAT ARE NOT VERIFIED.  THE EXTRACT FILE WILL BE             
001320*  USED BY RCKUP067 TO VERIFY THE RECEIVERS.                              
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
002900                                                                          
002901     SELECT EFC-FA-RCVR-FILE                                              
002902         ASSIGN TO OUT01.                                                 
002910                                                                          
003200******************************************************************        
003300 DATA DIVISION.                                                           
003400******************************************************************        
003500                                                                          
003600 FILE SECTION.                                                            
003700                                                                          
003800                                                                          
003801 FD  EFC-FA-RCVR-FILE                                                     
003802     RECORDING MODE IS F                                                  
003803     LABEL RECORDS ARE STANDARD                                           
003804     BLOCK CONTAINS 0 RECORDS.                                            
003805                                                                          
003810 01  EFC-FA-RCVR-REC            PIC  X(200).                              
003900                                                                          
003901******************************************************************        
003910 WORKING-STORAGE SECTION.                                                 
004000******************************************************************        
004010*-----------------------------------------------------------------        
004020*                                                                         
004030*-----------------------------------------------------------------        
004040 01  ABEND-CODE                     PIC S9(04) VALUE +0                   
004050                                               COMP SYNC.                 
004060                                                                          
004061*-----------------------------------------------------------------        
004062* PROGRAM CONSTANTS                                                       
004063*-----------------------------------------------------------------        
004070 01  PC-PROGRAM-CONSTANTS.                                                
004080     05  PC-CURRENT-PROGRAM-NAME    PIC  X(08) VALUE                      
004090         'RCKUT092'.                                                      
004091     05  PC-USER-ID-LABEL           PIC  X(03) VALUE 'UID'.               
004092                                                                          
004093*-----------------------------------------------------------------        
004094* PROGRAM SWITCHES                                                        
004095*-----------------------------------------------------------------        
004096 01  PS-PROGRAM-SWITCHES.                                                 
004097     05  PS-END-OF-CSR-SW           PIC  X(01) VALUE 'N'.                 
004098         88  PS-END-OF-CSR                     VALUE 'Y'.                 
004099         88  PS-END-OF-CSR-NO                  VALUE 'N'.                 
004100     05  PS-ERROR-SW                PIC  X(01) VALUE 'N'.                 
004110         88  PS-ERROR                          VALUE 'Y'.                 
004120         88  PS-ERROR-NO                       VALUE 'N'.                 
004121     05  PS-STORE-INV-CYCLE-SW      PIC  X(01) VALUE 'N'.                 
004122         88  PS-STORE-INV-CYCLE                VALUE 'Y'.                 
004123         88  PS-STORE-INV-CYCLE-NO             VALUE 'N'.                 
004124     05  PS-DC-FLUSH-SW             PIC  X(01) VALUE 'N'.                 
004125         88  PS-DC-FLUSH-YES                   VALUE 'Y'.                 
004126         88  PS-DC-FLUSH-NO                    VALUE 'N'.                 
004127     05  PS-RCVR-IN-TRBL-SW         PIC  X(01) VALUE 'N'.                 
004128         88  PS-RCVR-IN-TRBL-YES               VALUE 'Y'.                 
004129         88  PS-RCVR-IN-TRBL-NO                VALUE 'N'.                 
004130                                                                          
004131*-----------------------------------------------------------------        
004132* PROGRAM VARIABLES                                                       
004133*-----------------------------------------------------------------        
004134                                                                          
004140 01  PV-PROGRAM-VARIABLES.                                                
004141     05  PV-FETCH-COUNT             PIC 9(09).                            
004150     05  PV-INV-COUNT               PIC 9(09).                            
004151     05  PV-RCVR-IN-TRBL-CNT        PIC 9(09).                            
004152     05  PV-RCVR-COUNT              PIC 9(09).                            
004282     05  PV-WORK-DATE-PLUS-7        PIC  X(10)  VALUE ZEROS.              
004283                                                                          
004284     05  PV-CURRENT-PARAGRAPH       PIC X(30)  VALUE SPACES.              
004285     05  PV-PARAGRAPH-NAME          PIC  X(30) VALUE SPACES.              
004286     05  PV-DB2-OPERATION-ATTEMPTED PIC  X(30) VALUE SPACES.              
004287                                                                          
004288     05  PV-DISP-OPER               PIC  9(04) VALUE ZEROES.              
004289     05  PV-DISP-PO-NBR             PIC  9(09) VALUE ZEROES.              
004290     05  PV-DISP-RCVR               PIC  9(04) VALUE ZEROES.              
004291     05  PV-DISP-SQLCODE            PIC  -999  VALUE ZEROES.              
004292     05  PV-DISP-COUNT              PIC -ZZZ,ZZZ,ZZ9.                     
004293     05  PV-TRBL-COUNT              PIC S9(09)  COMP.                     
004294                                                                          
004295 01  DK-DB2-KEYS.                                                         
004296     05  DK-OPER-UNIT-ID              PIC S9(04) COMP VALUE +0.           
004297     05  DK-ORD-NBR                   PIC S9(09) COMP VALUE +0.           
004298     05  DK-RCVR-SEQ                  PIC S9(09) COMP VALUE +0.           
004300                                                                          
005800*-----------------------------------------------------------------        
005900*  OUTPUT RECORD LAYOUT                                                   
005910*-----------------------------------------------------------------        
006100     COPY RCRD100.                                                        
006200                                                                          
006300*-----------------------------------------------------------------        
006600*  DB2 FORMATTED MESSAGE AREA                                             
006610*-----------------------------------------------------------------        
006800     COPY DPWS004.                                                        
007101                                                                          
007102*-----------------------------------------------------------------        
007103*  TRECEIV                                                                
007105*-----------------------------------------------------------------        
007106     EXEC SQL                                                             
007107          INCLUDE TRECEIV                                                 
007108     END-EXEC.                                                            
007109                                                                          
007110*-----------------------------------------------------------------        
007111*  TSHPTPO                                                                
007112*-----------------------------------------------------------------        
007113     EXEC SQL                                                             
007114          INCLUDE TSHPTPO                                                 
007115     END-EXEC.                                                            
007116                                                                          
007117*-----------------------------------------------------------------        
007118*  TSHIPMT                                                                
007119*-----------------------------------------------------------------        
007120     EXEC SQL                                                             
007121          INCLUDE TSHIPMT                                                 
007122     END-EXEC.                                                            
007123                                                                          
007124*-----------------------------------------------------------------        
007125*  TVHTRIP                                                                
007126*-----------------------------------------------------------------        
007127     EXEC SQL                                                             
007128          INCLUDE TVHTRIP                                                 
007129     END-EXEC.                                                            
007130                                                                          
007131*-----------------------------------------------------------------        
007132*  TTRIPST                                                                
007133*-----------------------------------------------------------------        
007134     EXEC SQL                                                             
007135          INCLUDE TTRIPST                                                 
007136     END-EXEC.                                                            
007137                                                                          
007138*-----------------------------------------------------------------        
007139*  WST_SHPMT_XREF_HDR                                                     
007140*-----------------------------------------------------------------        
007141     EXEC SQL                                                             
007142          INCLUDE WST00001                                                
007143     END-EXEC.                                                            
007144                                                                          
007145*-----------------------------------------------------------------        
007146*  XLT_LOC                                                                
007147*-----------------------------------------------------------------        
007149     EXEC SQL                                                             
007150          INCLUDE XLT00010                                                
007151     END-EXEC.                                                            
007152                                                                          
007153*-----------------------------------------------------------------        
007154*  TTRSTYP                                                                
007155*-----------------------------------------------------------------        
007156     EXEC SQL                                                             
007157          INCLUDE TTRSTYP                                                 
007158     END-EXEC.                                                            
007159                                                                          
007160*-----------------------------------------------------------------        
007170*  TKRPRPM                                                                
007180*-----------------------------------------------------------------        
007181     EXEC SQL                                                             
007182          INCLUDE TKRPRPM                                                 
007183     END-EXEC.                                                            
007184                                                                          
007192******************************************************************        
007193*    XLT_MDSE_INV_PARM                                                    
007194******************************************************************        
007195     EXEC SQL                                                             
007196          INCLUDE XLT00025                                                
007197     END-EXEC.                                                            
007198                                                                          
007199*-----------------------------------------------------------------        
007200*  TTRBNTF                                                                
007201*-----------------------------------------------------------------        
007202     EXEC SQL                                                             
007203          INCLUDE TTRBNTF                                                 
007204     END-EXEC.                                                            
007205                                                                          
007206*-----------------------------------------------------------------        
007207*  TTRBRSN                                                                
007208*-----------------------------------------------------------------        
007209     EXEC SQL                                                             
007210          INCLUDE TTRBRSN                                                 
007211     END-EXEC.                                                            
007212                                                                          
007213*-----------------------------------------------------------------        
007214*    EFC-IP-FREE-ASTRAY-CSR                                               
007215*-----------------------------------------------------------------        
007216     EXEC SQL                                                             
007217          DECLARE EFC-IP-FREE-ASTRAY-CSR CURSOR FOR                       
007218          SELECT A.CURR_OPER_UNT_ID                                       
007219               , A.ORD_NBR                                                
007220               , A.REC_SEQ_NBR                                            
007221               , A.STATUS_CDE                                             
007222               , A.CRE_UID                                                
007223               , A.UPD_UID                                                
007224               , A.UPD_PGM_ID                                             
007225               , A.CRE_TMST                                               
007226               , D.OWNER_SCAC_ID                                          
007227               , D.VEH_NBR                                                
007228               , D.TRIP_ID                                                
007229               , E.TRIP_STATUS_CDE                                        
007230               , E.STATUS_DTE                                             
007231               , E.STATUS_TME                                             
007232               , E.STATUS_UID                                             
007233               , F.EXT_SHP_ID                                             
007234               , K.DESC                                                   
007235            FROM TRECEIV A                                                
007236               , TSHPTPO B                                                
007237               , TSHIPMT C                                                
007238               , TVHTRIP D                                                
007239               , TTRIPST E                                                
007240               , WST_SHPMT_XREF_HDR F                                     
007241               , XLT_LOC G                                                
007242               , TTRSTYP K                                                
007243               , TKRPRPM L                                                
007244          WHERE A.CURR_OPER_UNT_ID  =  G.LOC_NBR                          
007245            AND G.E_BUS_IND         = 'Y'                                 
007246            AND A.STATUS_CDE        = 'IP'                                
007247            AND A.VER_STATUS_CDE    = 'A'                                 
007248            AND A.FRE_ASTRA_IND     = 'Y'                                 
007249            AND A.ORD_NBR           = B.PO_NBR                            
007250            AND A.REC_SEQ_NBR       = B.REC_SEQ_NBR                       
007251            AND B.SHIPT_ID          = C.SHIPT_ID                          
007252            AND C.TRIP_ID           = D.TRIP_ID                           
007253            AND D.TRIP_ID           = E.TRIP_ID                           
007254            AND E.STATUS_DTE IN                                           
007255                (SELECT MAX(H.STATUS_DTE)                                 
007256                   FROM TTRIPST H                                         
007257                  WHERE E.TRIP_ID  = H.TRIP_ID)                           
007258            AND E.STATUS_TME IN                                           
007259                (SELECT MAX(I.STATUS_TME)                                 
007260                   FROM TTRIPST I                                         
007261                  WHERE E.TRIP_ID  =  I.TRIP_ID                           
007262                    AND I.STATUS_DTE IN                                   
007263                        (SELECT MAX(J.STATUS_DTE)                         
007264                           FROM TTRIPST J                                 
007265                          WHERE I.TRIP_ID  = J.TRIP_ID)                   
007266                 )                                                        
007267            AND E.TRIP_STATUS_CDE IN ('ET','UN','DP')                     
007268            AND D.TRIP_ID           =  F.INTR_TRIP_ID                     
007269            AND E.TRIP_STATUS_CDE   =  K.TRIP_STATUS_CDE                  
007270            AND L.LOC_ID            = 90                                  
007271            AND L.INTFC_SYS_CDE     = 'KHL'                               
007272            AND L.SYS_PRC_FUNC_CDE  = 'VEEFFA'                            
007273            AND L.FUNC_PRC_STAT_CDE = 'AC'                                
007274            AND A.CRE_TMST         <=                                     
007275                (CURRENT TIMESTAMP - L.FUNC_QTY MINUTES)                  
007276          ORDER BY A.CURR_OPER_UNT_ID                                     
007277                 , A.ORD_NBR                                              
007278                 , A.REC_SEQ_NBR                                          
007279           WITH UR                                                        
007280     END-EXEC.                                                            
007290                                                                          
007293*-----------------------------------------------------------------        
007300*  COMMUNICATIONS AREA FOR DB2                                            
007310*-----------------------------------------------------------------        
007500     EXEC SQL                                                             
007600          INCLUDE SQLCA                                                   
007700     END-EXEC.                                                            
007800                                                                          
012994                                                                          
013700******************************************************************        
013800 PROCEDURE DIVISION.                                                      
013900******************************************************************        
014000                                                                          
014010                                                                          
014020*-----------------------------------------------------------------        
014030* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
014040*      PROGRAM.                                                           
014050*-----------------------------------------------------------------        
014100 0000-PROGRAM-MAINLINE.                                                   
014600                                                                          
014610     DISPLAY ' '.                                                         
014620     DISPLAY ' RCKUT092 - START'                                          
014630     DISPLAY ' '.                                                         
014710                                                                          
014720     SET PS-END-OF-CSR-NO TO TRUE.                                        
014760                                                                          
014770     OPEN OUTPUT EFC-FA-RCVR-FILE.                                        
014780                                                                          
014790     PERFORM 8000-OPEN-RCVR-CSR.                                          
014800                                                                          
014900     PERFORM 8010-FETCH-RCVR-CSR.                                         
015000                                                                          
015100     IF PS-END-OF-CSR                                                     
015200        DISPLAY '*                                            *'          
015210        DISPLAY '* NO EFC IP FREE ASTRAY RECEIVERS TO VERIFY *'           
015220        DISPLAY '*                                            *'          
015230     ELSE                                                                 
015240         PERFORM 2000-PROCESS-RCVR-CSR                                    
015250             UNTIL PS-END-OF-CSR                                          
015260                OR PS-ERROR                                               
015270     END-IF.                                                              
015280                                                                          
015290     PERFORM 9000-EOJ-ROUTINE.                                            
015300                                                                          
015301     DISPLAY ' '                                                          
015302     MOVE PV-RCVR-COUNT  TO  PV-DISP-COUNT                                
015303     DISPLAY ' TOTAL RCVR: ' PV-DISP-COUNT                                
015304                                                                          
015305     MOVE PV-INV-COUNT   TO  PV-DISP-COUNT                                
015306     DISPLAY ' INVENTORY:  ' PV-DISP-COUNT                                
015307     DISPLAY ' '                                                          
015308                                                                          
015309     MOVE PV-RCVR-IN-TRBL-CNT TO PV-DISP-COUNT                            
015310     DISPLAY ' IN TROUBLE: '     PV-DISP-COUNT                            
015311     DISPLAY ' '                                                          
015312                                                                          
015313     DISPLAY ' RCKUT092 - END'.                                           
015314     DISPLAY ' '.                                                         
015320                                                                          
015400     STOP RUN.                                                            
015500                                                                          
015501                                                                          
016826*-----------------------------------------------------------------        
016827*  PROCESS CURSOR OF EFC IP FREE ASTRAY RECEIVERS                         
016828*-----------------------------------------------------------------        
016829 2000-PROCESS-RCVR-CSR.                                                   
016830                                                                          
016840     ADD  1  TO  PV-FETCH-COUNT.                                          
016850                                                                          
016858     SET PS-STORE-INV-CYCLE-NO TO TRUE.                                   
016859                                                                          
016860     MOVE RECEIV-CURR-OPER-UNT-ID TO DK-OPER-UNIT-ID.                     
016861     MOVE RECEIV-ORD-NBR          TO DK-ORD-NBR.                          
016862     MOVE RECEIV-REC-SEQ-NBR      TO DK-RCVR-SEQ                          
016863                                                                          
016864     PERFORM 2250-CHECK-DC-FLUSH-PARM.                                    
016865                                                                          
016866     IF  PS-DC-FLUSH-YES                                                  
016867         PERFORM 2275-CHECK-INV-IN-PROCESS                                
016868     END-IF.                                                              
016870                                                                          
016871     IF  PS-STORE-INV-CYCLE                                               
016872         ADD 1  TO  PV-INV-COUNT                                          
016873                                                                          
016874         MOVE DK-OPER-UNIT-ID TO PV-DISP-OPER                             
016875         MOVE DK-ORD-NBR      TO PV-DISP-PO-NBR                           
016876         MOVE DK-RCVR-SEQ     TO PV-DISP-RCVR                             
016877         DISPLAY 'RCVR:' PV-DISP-OPER   '-'                               
016878                         PV-DISP-PO-NBR '-'                               
016879                         PV-DISP-RCVR                                     
016880                 ' NOT VERIFIED - INVENTORY'                              
016881     END-IF.                                                              
016882                                                                          
016883     SET PS-RCVR-IN-TRBL-NO     TO  TRUE.                                 
016884     IF  PS-STORE-INV-CYCLE-NO  AND                                       
016885         PS-ERROR-NO                                                      
016886         PERFORM 3000-CHECK-FOR-TROUBLE                                   
016887     END-IF.                                                              
016888                                                                          
016889     IF  PS-STORE-INV-CYCLE-NO  AND                                       
016890         PS-RCVR-IN-TRBL-NO     AND                                       
016891         PS-ERROR-NO                                                      
016892         PERFORM 7000-CREATE-OUTPUT                                       
016894     END-IF.                                                              
016895                                                                          
016896     IF PS-ERROR-NO                                                       
016897         PERFORM 8010-FETCH-RCVR-CSR                                      
016898     END-IF.                                                              
016899                                                                          
016900                                                                          
016901*---------------------------------------------------------------*         
016902* CHECK TO SEE IF THE DC SHOULD FLUSH THEIR BUILDING.                     
016903*---------------------------------------------------------------*         
016904 2250-CHECK-DC-FLUSH-PARM.                                                
016905                                                                          
016906     EXEC SQL                                                             
016907          SELECT  FUNC_TXT                                                
016908          INTO   :KRPRPM-FUNC-TXT                                         
016909          FROM  TKRPRPM                                                   
016910          WHERE LOC_ID            =  90                                   
016911            AND SYS_PRC_FUNC_CDE  =  'FLUSH'                              
016912            AND FUNC_PRC_STAT_CDE =  'AC'                                 
016913     END-EXEC.                                                            
016914                                                                          
016915     EVALUATE TRUE                                                        
016916         WHEN SQLCODE = +0                                                
016917            IF KRPRPM-FUNC-TXT = 'N'                                      
016918                SET PS-DC-FLUSH-NO  TO TRUE                               
016919            ELSE                                                          
016920                SET PS-DC-FLUSH-YES TO TRUE                               
016921            END-IF                                                        
016922         WHEN OTHER                                                       
016923                SET PS-DC-FLUSH-NO  TO TRUE                               
016924     END-EVALUATE.                                                        
016925                                                                          
016926*---------------------------------------------------------------*         
016927* CHECK TO SEE IF STORE IS IN INVENTORY CYCLE                             
016928*---------------------------------------------------------------*         
016929 2275-CHECK-INV-IN-PROCESS.                                               
016930                                                                          
016931     EXEC SQL                                                             
016932          SELECT                                                          
016933                 A.DC_CTOFF_DTE                                           
016934             ,   (A.DC_CTOFF_DTE + 7 DAYS)                                
016935          INTO                                                            
016936                 :XLT00025-DC-CTOFF-DTE                                   
016937             ,   :PV-WORK-DATE-PLUS-7                                     
016938          FROM                                                            
016939                XLT_MDSE_INV_PARM A                                       
016940          WHERE A.LOC_NBR           =  :DK-OPER-UNIT-ID                   
016941           AND  A.DC_CTOFF_DTE >= (CURRENT_DATE - 7 DAYS)                 
016942           AND  A.DC_CTOFF_DTE <= (CURRENT_DATE + 7 DAYS)                 
016943           AND  A.ACTV_IND         = 'Y'                                  
016944           AND  A.LOC_INV_STAT_CDE = 'IN'                                 
016945     END-EXEC.                                                            
016946                                                                          
016947     EVALUATE TRUE                                                        
016948         WHEN SQLCODE = +0                                                
016949             SET PS-STORE-INV-CYCLE TO TRUE                               
016950             MOVE RECEIV-CURR-OPER-UNT-ID TO PV-DISP-OPER                 
016951             MOVE RECEIV-ORD-NBR          TO PV-DISP-PO-NBR               
016952             MOVE RECEIV-REC-SEQ-NBR      TO PV-DISP-RCVR                 
016953             DISPLAY ' '                                                  
016954             DISPLAY '* INVENTORY CYCLE - RECEIVER NOT VERIFIED'          
016955                     '   '         PV-DISP-OPER                           
016956                     '-'           PV-DISP-PO-NBR                         
016957                     '-'           PV-DISP-RCVR                           
016958             ADD  1  TO  PV-INV-COUNT                                     
016959                                                                          
016960         WHEN SQLCODE = +100                                              
016961             CONTINUE                                                     
016962         WHEN SQLWARN0 NOT = SPACES                                       
016963         WHEN SQLCODE < +0                                                
016964         WHEN SQLCODE > +0                                                
016965             MOVE RECEIV-CURR-OPER-UNT-ID TO PV-DISP-OPER                 
016966             MOVE RECEIV-ORD-NBR          TO PV-DISP-PO-NBR               
016967             MOVE RECEIV-REC-SEQ-NBR      TO PV-DISP-RCVR                 
016968             MOVE SQLCODE     TO PV-DISP-SQLCODE                          
016969             SET PS-ERROR        TO TRUE                                  
016970             DISPLAY '******************************************'         
016971             DISPLAY '*ABEND                                   *'         
016972             DISPLAY '*PROGRAM   = RCKUP067                    *'         
016973             DISPLAY '*DB2 OPER  = SELECT ON XLT_MDSE_INV_PARM *'         
016974             DISPLAY '*PARAGRAPH = 2275-CHECK-INV-IN-PROCESS   *'         
016975             DISPLAY '*SQLCODE = ' PV-DISP-SQLCODE                        
016976             DISPLAY '*RECEIVER= ' PV-DISP-OPER                           
016977                     '-'           PV-DISP-PO-NBR                         
016978                     '-'           PV-DISP-RCVR                           
016979             DISPLAY '******************************************'         
016980     END-EVALUATE.                                                        
016981                                                                          
016982                                                                          
016983*---------------------------------------------------------------*         
016984* CHECK TO SEE IF RECEIVER HAS UNRESOLVED TROUBLE                         
016985*---------------------------------------------------------------*         
016986 3000-CHECK-FOR-TROUBLE.                                                  
016987                                                                          
016988     SET PS-RCVR-IN-TRBL-NO  TO TRUE.                                     
016989     MOVE RECEIV-ORD-NBR     TO TRBNTF-PO-NBR.                            
016990     MOVE RECEIV-REC-SEQ-NBR TO TRBNTF-REC-SEQ-NBR.                       
016991                                                                          
016992     EXEC SQL                                                             
016993          SELECT COUNT(*)                                                 
016994          INTO  :PV-TRBL-COUNT                                            
016995          FROM  TTRBNTF A                                                 
016996             ,  TTRBRSN B                                                 
016997          WHERE A.PO_NBR        = :TRBNTF-PO-NBR                          
016998            AND A.REC_SEQ_NBR   = :TRBNTF-REC-SEQ-NBR                     
016999            AND A.RESOLVED_IND  = 'N'                                     
017000            AND A.TRBL_RSN_CDE  = B.TRBL_RSN_CDE                          
017001            AND B.NOTIF_IND     = 'T'                                     
017002          FETCH FIRST 1 ROW ONLY                                          
017003           WITH UR                                                        
017004     END-EXEC.                                                            
017005                                                                          
017006     EVALUATE TRUE                                                        
017007         WHEN SQLCODE = +0                                                
017008              IF  PV-TRBL-COUNT > 0                                       
017009                  SET PS-RCVR-IN-TRBL-YES TO TRUE                         
017010                  MOVE RECEIV-CURR-OPER-UNT-ID TO PV-DISP-OPER            
017011                  MOVE RECEIV-ORD-NBR          TO PV-DISP-PO-NBR          
017012                  MOVE RECEIV-REC-SEQ-NBR      TO PV-DISP-RCVR            
017013                  DISPLAY ' '                                             
017014                  DISPLAY '* RCVR TROUBLE   '                             
017015                          ' - RECEIVER NOT VERIFIED'                      
017016                          '   '         PV-DISP-OPER                      
017017                          '-'           PV-DISP-PO-NBR                    
017018                          '-'           PV-DISP-RCVR                      
017019                  ADD  1  TO  PV-RCVR-IN-TRBL-CNT                         
017020              END-IF                                                      
017021                                                                          
017022         WHEN SQLCODE = +100                                              
017023             CONTINUE                                                     
017024                                                                          
017025         WHEN SQLWARN0 NOT = SPACES                                       
017026         WHEN SQLCODE < +0                                                
017027         WHEN SQLCODE > +0                                                
017028             MOVE RECEIV-CURR-OPER-UNT-ID TO PV-DISP-OPER                 
017029             MOVE RECEIV-ORD-NBR          TO PV-DISP-PO-NBR               
017030             MOVE RECEIV-REC-SEQ-NBR      TO PV-DISP-RCVR                 
017031             MOVE SQLCODE     TO PV-DISP-SQLCODE                          
017032             SET PS-ERROR        TO TRUE                                  
017033             DISPLAY '******************************************'         
017034             DISPLAY '*ABEND                                   *'         
017035             DISPLAY '*PROGRAM   = RCKUT092                    *'         
017036             DISPLAY '*DB2 OPER  = CHECK RCVR IN TRBL          *'         
017037             DISPLAY '*PARAGRAPH = 3000-CHECK-FOR-TROUBLE      *'         
017038             DISPLAY '*SQLCODE = ' PV-DISP-SQLCODE                        
017039             DISPLAY '*RECEIVER= ' PV-DISP-OPER                           
017040                     '-'           PV-DISP-PO-NBR                         
017041                     '-'           PV-DISP-RCVR                           
017042             DISPLAY '******************************************'         
017043     END-EVALUATE.                                                        
017044                                                                          
017045                                                                          
017046*----------------------------------------------------------------*        
017047*                                                                *        
017048*----------------------------------------------------------------*        
017049 7000-CREATE-OUTPUT.                                                      
017050     MOVE RECEIV-CURR-OPER-UNT-ID TO RC100-CURR-OPER-UNIT.                
017051     MOVE RECEIV-ORD-NBR          TO RC100-ORD-NBR.                       
017052     MOVE RECEIV-REC-SEQ-NBR      TO RC100-REC-SEQ-NBR.                   
017053     MOVE RECEIV-STATUS-CDE       TO RC100-RCVR-STATUS.                   
017054     MOVE RECEIV-CRE-UID          TO RC100-CREATE-USERID.                 
017055     MOVE RECEIV-UPD-UID          TO RC100-UPDATE-USERID.                 
017056     MOVE RECEIV-UPD-PGM-ID       TO RC100-UPDATE-PROGRAM.                
017057     MOVE RECEIV-CRE-TMST         TO RC100-CREATE-TMST.                   
017058     MOVE VHTRIP-OWNER-SCAC-ID    TO RC100-SCAC.                          
017059     MOVE VHTRIP-VEH-NBR          TO RC100-VEHICL.                        
017060     MOVE VHTRIP-TRIP-ID          TO RC100-TRIP.                          
017061     MOVE TRIPST-TRIP-STATUS-CDE  TO RC100-TRIP-STATUS.                   
017062     MOVE TRIPST-STATUS-DTE       TO RC100-TRIP-STATUS-DATE.              
017063     MOVE TRIPST-STATUS-TME       TO RC100-TRIP-STATUS-TIME.              
017064     MOVE TRIPST-STATUS-UID       TO RC100-TRIP-STATUS-UID.               
017065     MOVE WST00001-EXT-SHP-ID     TO RC100-EFC-SHIPMT-NBR.                
017066     MOVE TRSTYP-DESC             TO RC100-TRIP-STATUS-DESC.              
017067                                                                          
017068     WRITE EFC-FA-RCVR-REC                                                
017069         FROM RC100-EFC-RCVR-REC.                                         
017070                                                                          
017071     ADD  1  TO PV-RCVR-COUNT.                                            
017072                                                                          
017073     MOVE RECEIV-CURR-OPER-UNT-ID TO PV-DISP-OPER.                        
017074     MOVE RECEIV-ORD-NBR          TO PV-DISP-PO-NBR.                      
017075     MOVE RECEIV-REC-SEQ-NBR      TO PV-DISP-RCVR.                        
017076                                                                          
017077     DISPLAY 'RCVR:' PV-DISP-OPER   '-'                                   
017078                     PV-DISP-PO-NBR '-'                                   
017079                     PV-DISP-RCVR.                                        
017080                                                                          
017081                                                                          
017082*----------------------------------------------------------------*        
017083*                                                                *        
017084*----------------------------------------------------------------*        
017085 8000-OPEN-RCVR-CSR.                                                      
017086                                                                          
017087     EXEC SQL                                                             
017088          OPEN EFC-IP-FREE-ASTRAY-CSR                                     
017089     END-EXEC.                                                            
017090                                                                          
017091     EVALUATE TRUE                                                        
017100         WHEN SQLCODE = +0                                                
017200              CONTINUE                                                    
017201         WHEN SQLWARN0 NOT = SPACE                                        
017202         WHEN SQLCODE  NOT = +0                                           
017203             MOVE SQLCODE TO PV-DISP-SQLCODE                              
017206             SET PS-ERROR        TO TRUE                                  
017207             DISPLAY '******************************************'         
017208             DISPLAY '*ABEND                                   *'         
017209             DISPLAY '*PROGRAM   = RCKUP067                    *'         
017210             DISPLAY '*DB2 OPER  = OPEN EFC-IP-FREE-ASTRAY-CSR *'         
017211             DISPLAY '*PARAGRAPH = 8000-OPEN-RCVR-CSR          *'         
017212             DISPLAY '*SQLCODE = ' PV-DISP-SQLCODE                        
017213             DISPLAY '******************************************'         
017214     END-EVALUATE.                                                        
017215                                                                          
017216                                                                          
017217*----------------------------------------------------------------*        
017219*                                                                *        
017220*----------------------------------------------------------------*        
017222 8010-FETCH-RCVR-CSR.                                                     
017223                                                                          
017224     EXEC SQL                                                             
017225          FETCH EFC-IP-FREE-ASTRAY-CSR                                    
017226           INTO :RECEIV-CURR-OPER-UNT-ID                                  
017227               ,:RECEIV-ORD-NBR                                           
017228               ,:RECEIV-REC-SEQ-NBR                                       
017229               ,:RECEIV-STATUS-CDE                                        
017230               ,:RECEIV-CRE-UID                                           
017231               ,:RECEIV-UPD-UID                                           
017232               ,:RECEIV-UPD-PGM-ID                                        
017233               ,:RECEIV-CRE-TMST                                          
017234               ,:VHTRIP-OWNER-SCAC-ID                                     
017235               ,:VHTRIP-VEH-NBR                                           
017236               ,:VHTRIP-TRIP-ID                                           
017237               ,:TRIPST-TRIP-STATUS-CDE                                   
017238               ,:TRIPST-STATUS-DTE                                        
017239               ,:TRIPST-STATUS-TME                                        
017240               ,:TRIPST-STATUS-UID                                        
017241               ,:WST00001-EXT-SHP-ID                                      
017242               ,:TRSTYP-DESC                                              
017243     END-EXEC.                                                            
017244                                                                          
017245     EVALUATE TRUE                                                        
017246         WHEN SQLCODE = +0                                                
017247              CONTINUE                                                    
017248                                                                          
017249         WHEN SQLCODE = +100                                              
017250              SET PS-END-OF-CSR TO TRUE                                   
017251                                                                          
017252         WHEN SQLWARN0 NOT = SPACE                                        
017253         WHEN SQLCODE < +0                                                
017254         WHEN SQLCODE > +0                                                
017255             MOVE SQLCODE TO PV-DISP-SQLCODE                              
017258             SET PS-ERROR        TO TRUE                                  
017259             DISPLAY '******************************************'         
017260             DISPLAY '*ABEND                                   *'         
017261             DISPLAY '*PROGRAM   = RCKUP067                    *'         
017262             DISPLAY '*DB2 OPER  = FETCH EFC-IP-FREE-ASTRAY-CSR*'         
017263             DISPLAY '*PARAGRAPH = 8010-FETCH-RCVR-CSR         *'         
017264             DISPLAY '*SQLCODE = ' PV-DISP-SQLCODE                        
017265             DISPLAY '******************************************'         
017266     END-EVALUATE.                                                        
017267                                                                          
017268                                                                          
017269*----------------------------------------------------------------*        
017271*                                                                *        
017272*----------------------------------------------------------------*        
017274 8020-CLOSE-RCVR-CSR.                                                     
017275                                                                          
017276     EXEC SQL                                                             
017277          CLOSE EFC-IP-FREE-ASTRAY-CSR                                    
017278     END-EXEC.                                                            
017279                                                                          
017280     EVALUATE TRUE                                                        
017281         WHEN SQLCODE = +0                                                
017282              CONTINUE                                                    
017283         WHEN SQLWARN0 NOT = SPACE                                        
017284         WHEN SQLCODE  NOT = +0                                           
017285             MOVE SQLCODE TO PV-DISP-SQLCODE                              
017286             SET PS-ERROR        TO TRUE                                  
017287             DISPLAY '******************************************'         
017288             DISPLAY '*ABEND                                   *'         
017289             DISPLAY '*PROGRAM   = RCKUP067                    *'         
017290             DISPLAY '*DB2 OPER  = OPEN EFC-IP-FREE-ASTRAY-CSR *'         
017291             DISPLAY '*PARAGRAPH = 8000-OPEN-RCVR-CSR          *'         
017292             DISPLAY '*SQLCODE = ' PV-DISP-SQLCODE                        
017293             DISPLAY '******************************************'         
017294     END-EVALUATE.                                                        
017295                                                                          
017296                                                                          
022710                                                                          
022800 9000-EOJ-ROUTINE.                                                        
022810*-----------------------------------------------------------------        
023000*  CLOSE OUTPUT FILE.                                                     
023010*-----------------------------------------------------------------        
023200     CLOSE EFC-FA-RCVR-FILE.                                              
023800                                                                          
023900                                                                          
023910*-----------------------------------------------------------------        
023920*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
023930*  ERROR OR WARNING CODE OCCURS.                                          
023940*-----------------------------------------------------------------        
024000 9100-SQL-ABEND.                                                          
024100                                                                          
024500     DISPLAY '9100-SQL-ABEND'.                                            
024600     DISPLAY '** ABEND     **'.                                           
024700     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
024800     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
024900     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
025000                                                                          
025100******************************************************************        
025200* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
025300* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
025400* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
025500* FORMAT.                                                                 
025600******************************************************************        
025700     COPY DPPD004.                                                        
025800                                                                          
025900     MOVE +4013                  TO ABEND-CODE.                           
026000     CALL 'ILBOABN0' USING ABEND-CODE.                                    
