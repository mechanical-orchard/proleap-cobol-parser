       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.             IMKUP064.                                        
       AUTHOR.                 KRISTIN PETERSON.                                
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           12/26/2006.                                      
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  IMKUP064 - FOR EACH NEW OR MODIFIED BUSINESS RULE ON TABLE             
      *             IMT_CHARTC_RULDESC, REBUILD THE CUSTOMER INTERFACE          
      *             DESCRIPTION AND UPDATE IT ON TABLE TMDARVS.                 
      ******************************************************************        
      *  CHANGE LOG:                                                            
      *                                                                         
      *  12/26/06 - KRISTIN PETERSON                                            
      *             (WR #31557) NEW PROGRAM FOR THE CUSTOMER INTERFACE          
      *             DESCRIPTION PROJECT.                                        
W35681******************************************************************        
W35681*  09/28/10 - BRUCE GRAHAM - KFORCE                                       
W35681*             WR35681, WR37044, WR37184, WR38861 ETL                      
W35681*             CUSTOMER DESCRIPTION CHANGE NEW TMDARVS ATTRIBUTES          
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.                        IBM-3090.                        
       OBJECT-COMPUTER.                        IBM-3090.                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                 
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      * PROGRAM VARIABLES                                              *        
      ******************************************************************        
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-CURR-DEPT                  PIC  X(03).                        
           05  PV-CURR-DEPT-9                REDEFINES                          
               PV-CURR-DEPT                  PIC  9(03).                        
           05  PV-CURR-DEPT-PACKED           PIC S9(04)V  VALUE ZEROES          
                                                          COMP-3.               
                                                                                
           05  PV-CURR-MCL                   PIC  X(02).                        
           05  PV-CURR-MCL-9                 REDEFINES                          
               PV-CURR-MCL                   PIC  9(02).                        
           05  PV-CURR-MCL-PACKED            PIC S9(04)V  VALUE ZEROES          
                                                          COMP-3.               
                                                                                
           05  PV-CURR-SCL                   PIC  X(03).                        
           05  PV-CURR-SCL-9                 REDEFINES                          
               PV-CURR-SCL                   PIC  9(03).                        
           05  PV-CURR-SCL-PACKED            PIC S9(04)V  VALUE ZEROES          
                                                          COMP-3.               
                                                                                
           05  PV-PREV-LBL-SEQ-NBR           PIC S9(04)   COMP.                 
           05  PV-PREV-RPT-HIER-SEQ-NBR      PIC S9(04)   COMP.                 
           05  PV-PREV-BDY-STYL-SEQ-NBR      PIC S9(04)   COMP.                 
           05  PV-PREV-SLHTT-SEQ-NBR         PIC S9(04)   COMP.                 
           05  PV-PREV-FUNC-SEQ-NBR          PIC S9(04)   COMP.                 
           05  PV-PREV-FBRCN-SEQ-NBR         PIC S9(04)   COMP.                 
           05  PV-PREV-PATRN-SEQ-NBR         PIC S9(04)   COMP.                 
           05  PV-PREV-GNDR-SEQ-NBR          PIC S9(04)   COMP.                 
W35681     05  PV-PREV-CTG-SEQ-NBR           PIC S9(04)   VALUE +0 COMP.        
W35681     05  PV-PREV-MAJ-CTG-SEQ-NBR       PIC S9(04)   VALUE +0 COMP.        
W35681     05  PV-PREV-MTRL-SEQ-NBR          PIC S9(04)   VALUE +0 COMP.        
W35681     05  PV-PREV-ATTR-1-SEQ-NBR        PIC S9(04)   VALUE +0 COMP.        
W35681     05  PV-PREV-ATTR-2-SEQ-NBR        PIC S9(04)   VALUE +0 COMP.        
W35681     05  PV-PREV-NW-SLHTT-SEQ-NBR      PIC S9(04)   VALUE +0 COMP.        
           05  PV-SAVED-BUS-GRP-DESC         PIC X(8)     VALUE SPACES.         
           05  PV-CHARTC-CDE                 PIC S9(4)    COMP.                 
               88  IM-CHARTC-CDE                          VALUES                
                                                           +1, +3,              
                                                           +7, +21,             
                                                          +22, +23,             
W35681                                                    +24, +26,             
W35681                                                    +36, +37,             
W35681                                                    +38, +39,             
W35681                                                    +42, +43.             
               88  BG-CHARTC-CDE                          VALUE +5.             
               88  NULL-CHARTC-CDE                        VALUE +0.             
           05  PV-CHARTC-CDE-TYPE            PIC S9(4)    COMP.                 
               88  PV-ATTR-FBRCN                          VALUE +1.             
               88  PV-ATTR-BRAND                          VALUE +3.             
               88  PV-ATTR-PATRN                          VALUE +7.             
               88  PV-ATTR-HIER                           VALUE +21.            
               88  PV-ATTR-FUNC                           VALUE +22.            
               88  PV-ATTR-SILH                           VALUE +23.            
               88  PV-ATTR-BDYSTL                         VALUE +24.            
               88  PV-ATTR-GNDR                           VALUE +26.            
               88  PV-ATTR-BUSGRP                         VALUE +5.             
W35681         88  PV-ATTR-CTG                            VALUE +36.            
W35681         88  PV-ATTR-MAJ-CTG                        VALUE +37.            
W35681         88  PV-ATTR-NW-SILH                        VALUE +38.            
W35681         88  PV-ATTR-MTRL                           VALUE +39.            
W35681         88  PV-ATTR-ATTR-1                         VALUE +42.            
W35681         88  PV-ATTR-ATTR-2                         VALUE +43.            
           05  PV-SHORT-DESC                 PIC X(8)     VALUE SPACES.         
           05  PV-CUST-DESC                  PIC X(26)    VALUE SPACES.         
           05  PV-CUST-DESC-PART-1           PIC X(8)     VALUE SPACES.         
           05  PV-CUST-DESC-PART-2           PIC X(8)     VALUE SPACES.         
           05  PV-CUST-DESC-PART-3           PIC X(8)     VALUE SPACES.         
           05  PV-CUST-DESC-DFLT-BRND        PIC X(8)     VALUE SPACES.         
           05  PV-CUST-DESC-DFLT-HIER        PIC X(8)     VALUE SPACES.         
W35681     05  PV-CUST-DESC-DFLT-CTG         PIC X(8)     VALUE SPACES.         
           05  PV-CUST-DESC-EDITED           PIC X(26)    VALUE SPACES.         
           05  PV-NBR-BLANK-ATTRS            PIC S9(1)    VALUE ZEROES.         
           05  PV-END-SPACE-CNT              PIC S9(04)   VALUE +0              
                                                          COMP SYNC.            
           05  PV-END-SPACE-CNT-1            PIC S9(04)   VALUE +0              
                                                          COMP SYNC.            
           05  PV-END-SPACE-CNT-2            PIC S9(04)   VALUE +0              
                                                          COMP SYNC.            
           05  PV-END-SPACE-CNT-3            PIC S9(04)   VALUE +0              
                                                          COMP SYNC.            
           05  PV-CNT-TRAILING-SPACES        PIC X(8)     VALUE SPACES.         
           05  PV-7-TRAILING-SPCS  REDEFINES                                    
               PV-CNT-TRAILING-SPACES.                                          
               10  FILLER                    PIC  X(1).                         
               10  PV-7-TRLNG-SPCS           PIC  X(7).                         
           05  PV-6-TRAILING-SPCS  REDEFINES                                    
               PV-CNT-TRAILING-SPACES.                                          
               10  FILLER                    PIC  X(2).                         
               10  PV-6-TRLNG-SPCS           PIC  X(6).                         
           05  PV-5-TRAILING-SPCS  REDEFINES                                    
               PV-CNT-TRAILING-SPACES.                                          
               10  FILLER                    PIC  X(3).                         
               10  PV-5-TRLNG-SPCS           PIC  X(5).                         
           05  PV-4-TRAILING-SPCS  REDEFINES                                    
               PV-CNT-TRAILING-SPACES.                                          
               10  FILLER                    PIC  X(4).                         
               10  PV-4-TRLNG-SPCS           PIC  X(4).                         
           05  PV-3-TRAILING-SPCS  REDEFINES                                    
               PV-CNT-TRAILING-SPACES.                                          
               10  FILLER                    PIC  X(5).                         
               10  PV-3-TRLNG-SPCS           PIC  X(3).                         
           05  PV-2-TRAILING-SPCS  REDEFINES                                    
               PV-CNT-TRAILING-SPACES.                                          
               10  FILLER                    PIC  X(6).                         
               10  PV-2-TRLNG-SPCS           PIC  X(2).                         
           05  PV-1-TRAILING-SPCS  REDEFINES                                    
               PV-CNT-TRAILING-SPACES.                                          
               10  FILLER                    PIC  X(7).                         
               10  PV-1-TRLNG-SPCS           PIC  X(1).                         
                                                                                
           05  PV-ABEND-MESSAGE                PIC X(30)  VALUE SPACE.          
           05  PV-DB2-OPERATION-ATTEMPTED      PIC X(30)  VALUE SPACE.          
           05  PV-PARAGRAPH-NAME               PIC X(30)  VALUE SPACE.          
           05  PV-RETRY-COUNT                  PIC S9(04) VALUE ZERO            
                                                          COMP SYNC.            
           05  PV-DISPLAY-COUNT                PIC ZZZ,ZZZ,ZZ9                  
                                                          VALUE ZEROES.         
                                                                                
      ******************************************************************        
      * PROGRAM SWITCHES                                               *        
      ******************************************************************        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-BUS-RULE-CSR-SW     PIC X(1)     VALUE 'N'.            
               88  PS-END-OF-BUS-RULE-CSR                 VALUE 'Y'.            
           05  PS-DEPT-LVL-RULE-SW           PIC X(1)     VALUE 'N'.            
               88  PS-DEPT-LVL-RULE                       VALUE 'Y'.            
           05  PS-MCL-LVL-RULE-SW            PIC X(1)     VALUE 'N'.            
               88  PS-MCL-LVL-RULE                        VALUE 'Y'.            
           05  PS-SCL-LVL-RULE-SW            PIC X(1)     VALUE 'N'.            
               88  PS-SCL-LVL-RULE                        VALUE 'Y'.            
           05  PS-END-OF-STYLE-CSR-SW        PIC X(1)     VALUE 'N'.            
               88  PS-END-OF-STYLE-CSR                    VALUE 'Y'.            
           05  PS-DFLT-BRND-IS-BLANK-SW      PIC X(1)     VALUE 'N'.            
               88  PS-DFLT-BRND-IS-BLANK                  VALUE 'Y'.            
           05  PS-DFLT-HIER-IS-BLANK-SW      PIC X(1)     VALUE 'N'.            
               88  PS-DFLT-HIER-IS-BLANK                  VALUE 'Y'.            
           05  PS-DFLT-CTG-IS-BLANK-SW       PIC X(1)     VALUE 'N'.            
               88  PS-DFLT-CTG-IS-BLANK                   VALUE 'Y'.            
                                                                                
      ******************************************************************        
      * PROGRAM ACCUMULATORS                                           *        
      ******************************************************************        
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-NBR-BUS-RULES-PROCESSED    PIC 9(09)    VALUE ZERO.           
           05  PA-NBR-STYLES-PROCESSED       PIC 9(09)    VALUE ZERO.           
           05  PA-NBR-TMDARVS-UPDATES        PIC 9(09)    VALUE ZERO.           
           05  PA-NBR-BUS-RULE-UPDATES       PIC 9(09)    VALUE ZERO.           
           05  PA-NBR-UPDTS-SINCE-COMMIT     PIC 9(09)    VALUE ZERO.           
           05  PA-NBR-COMMITS-TAKEN          PIC 9(09)    VALUE ZERO.           
                                                                                
      ******************************************************************        
      * PROGRAM CONSTANTS                                              *        
      ******************************************************************        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME               PIC  X(08)    VALUE                
                                                           'IMKUP064'.          
           05  PC-MAXIMUM-RETRY-COUNT        PIC S9(04)    VALUE +3             
                                                           COMP SYNC.           
           05  PC-ATTR-BRAND                 PIC S9(04)    VALUE +3             
                                                           COMP.                
           05  PC-ATTR-HIER                  PIC S9(04)    VALUE +21            
                                                           COMP.                
           05  PC-ATTR-CTG                   PIC S9(04)    VALUE +36            
                                                           COMP.                
           05  PC-BUS-RULE-PRC-CPLT          PIC  X(01)    VALUE 'C'.           
                                                                                
       01  ABEND-CODE                        PIC S9(04)    VALUE 0              
                                                           COMP SYNC.           
           88  ABEND-4001-INVALID-ATTR                     VALUE +4001.         
                                                                                
      *****************************************************************         
      *  TABLE DCLGEN - IMT_CHARTC_RULDESC                                      
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE IMT00022                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TABLE DCLGEN - TMDSEAR                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TABLE DCLGEN - TMDARVS                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TMDARVS                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TABLE DCLGEN - TVSCHVL                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TVSCHVL                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TABLE DCLGEN - TMDSEDP                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TMDSEDP                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TABLE DCLGEN - TMDSEBG                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TMDSEBG                                                  
           END-EXEC.                                                            
                                                                                
      *  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
      ******************************************************************        
      *  CURSOR TO RETRIEVE THE NEW OR MODIFIED CUSTOMER INTERFACE              
      *  DESCRIPTION BUSINESS RULES.  THE RULES WILL BE RETRIEVED AND           
      *  PROCESSED IN THE FOLLOWING ORDER:                                      
      *       1ST - SUBCLASS-LEVEL RULES                                        
      *       2ND - MAJOR CLASS-LEVEL RULES                                     
      *       3RD - DEPARTMENT-LEVEL RULES                                      
      ******************************************************************        
           EXEC SQL                                                             
                                                                                
           DECLARE BUS_RULE_CSR CURSOR WITH HOLD FOR                            
                                                                                
               SELECT A.MDSEAR_ID                                               
                    , B.MDSELV_CDE                                              
                    , B.DEPT_NBR                                                
                    , B.MAJ_CL_NBR                                              
                    , B.SUB_CL_NBR                                              
                    , A.POS1_VS_CHARTC_CDE                                      
                    , A.POS2_VS_CHARTC_CDE                                      
                    , A.POS3_VS_CHARTC_CDE                                      
                 FROM IMT_CHARTC_RULDESC A                                      
                    , TMDSEAR B                                                 
                WHERE A.MDSEAR_ID = B.MDSEAR_DKEY                               
                  AND A.CHARTC_RUL_PRC_CDE = 'U'                                
                  AND ((B.MDSELV_CDE = 'MCL')                                   
                   OR  (B.MDSELV_CDE = 'SCL')                                   
                   OR  (B.MDSELV_CDE = 'DPT'))                                  
                  AND CURRENT DATE BETWEEN B.STRT_DTE AND B.END_DTE             
             ORDER BY B.MDSELV_CDE DESC                                         
                    , B.DEPT_NBR   ASC                                          
                    , B.MAJ_CL_NBR ASC                                          
                    , B.SUB_CL_NBR ASC                                          
                                                                                
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * SELECT ALL ACTIVE STYLES FOR A SUBCLASS-LEVEL BUSINESS RULE.            
      ******************************************************************        
           EXEC SQL                                                             
                                                                                
           DECLARE STYLES_BY_SCL_CSR CURSOR WITH HOLD FOR                       
                                                                                
                SELECT SUB_CL_MDSEAR_ID                                         
                     , VS_ID                                                    
                     , LBL_SEQ_NBR                                              
                     , RPT_HIER_SEQ_NBR                                         
                     , BDY_STYL_SEQ_NBR                                         
                     , SLHTT_SEQ_NBR                                            
                     , FUNC_SEQ_NBR                                             
                     , GNDR_SEQ_NBR                                             
                     , FBRCN_SEQ_NBR                                            
                     , PATRN_SEQ_NBR                                            
                     , CUST_INTFC_DESC                                          
W35681               , CTG_SEQ_NBR                                              
W35681               , MAJ_CTG_SEQ_NBR                                          
W35681               , MTRL_SEQ_NBR                                             
W35681               , ATTR_1_SEQ_NBR                                           
W35681               , ATTR_2_SEQ_NBR                                           
W35681               , NW_SLHTT_SEQ_NBR                                         
                  FROM TMDARVS                                                  
                 WHERE DEPT_NBR    = :PV-CURR-DEPT-PACKED                       
                   AND MAJ_CL_NBR  = :PV-CURR-MCL-PACKED                        
                   AND SUB_CL_NBR  = :PV-CURR-SCL-PACKED                        
                   AND ACTV_IND    = 'Y'                                        
              ORDER BY LBL_SEQ_NBR                                              
                     , RPT_HIER_SEQ_NBR                                         
                     , BDY_STYL_SEQ_NBR                                         
                     , SLHTT_SEQ_NBR                                            
                     , FUNC_SEQ_NBR                                             
                     , GNDR_SEQ_NBR                                             
                     , FBRCN_SEQ_NBR                                            
                     , PATRN_SEQ_NBR                                            
W35681               , CTG_SEQ_NBR                                              
W35681               , MAJ_CTG_SEQ_NBR                                          
W35681               , MTRL_SEQ_NBR                                             
W35681               , ATTR_1_SEQ_NBR                                           
W35681               , ATTR_2_SEQ_NBR                                           
W35681               , NW_SLHTT_SEQ_NBR                                         
                                                                                
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * SELECT ALL ACTIVE STYLES FOR A MAJOR CLASS-LEVEL BUSINESS RULE.         
      *    ** EXCLUDE STYLES THAT ARE PART OF ANY SUBCLASS-LEVEL                
      *       BUSINESS RULE FOR THIS DEPT/MCL.                                  
      ******************************************************************        
           EXEC SQL                                                             
                                                                                
           DECLARE STYLES_BY_MCL_CSR CURSOR WITH HOLD FOR                       
                                                                                
                SELECT SUB_CL_MDSEAR_ID                                         
                     , VS_ID                                                    
                     , LBL_SEQ_NBR                                              
                     , RPT_HIER_SEQ_NBR                                         
                     , BDY_STYL_SEQ_NBR                                         
                     , SLHTT_SEQ_NBR                                            
                     , FUNC_SEQ_NBR                                             
                     , GNDR_SEQ_NBR                                             
                     , FBRCN_SEQ_NBR                                            
                     , PATRN_SEQ_NBR                                            
                     , CUST_INTFC_DESC                                          
W35681               , CTG_SEQ_NBR                                              
W35681               , MAJ_CTG_SEQ_NBR                                          
W35681               , MTRL_SEQ_NBR                                             
W35681               , ATTR_1_SEQ_NBR                                           
W35681               , ATTR_2_SEQ_NBR                                           
W35681               , NW_SLHTT_SEQ_NBR                                         
                  FROM TMDARVS A                                                
                 WHERE A.DEPT_NBR    = :PV-CURR-DEPT-PACKED                     
                   AND A.MAJ_CL_NBR  = :PV-CURR-MCL-PACKED                      
                   AND A.ACTV_IND    = 'Y'                                      
                   AND NOT EXISTS                                               
                          (SELECT 1                                             
                             FROM IMT_CHARTC_RULDESC B                          
                            WHERE B.MDSEAR_ID = A.SUB_CL_MDSEAR_ID)             
              ORDER BY LBL_SEQ_NBR                                              
                     , RPT_HIER_SEQ_NBR                                         
                     , BDY_STYL_SEQ_NBR                                         
                     , SLHTT_SEQ_NBR                                            
                     , FUNC_SEQ_NBR                                             
                     , GNDR_SEQ_NBR                                             
                     , FBRCN_SEQ_NBR                                            
                     , PATRN_SEQ_NBR                                            
W35681               , CTG_SEQ_NBR                                              
W35681               , MAJ_CTG_SEQ_NBR                                          
W35681               , MTRL_SEQ_NBR                                             
W35681               , ATTR_1_SEQ_NBR                                           
W35681               , ATTR_2_SEQ_NBR                                           
W35681               , NW_SLHTT_SEQ_NBR                                         
                                                                                
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * SELECT ALL ACTIVE STYLES FOR A DEPARTMENT-LEVEL BUSINESS RULE.          
      *    ** EXCLUDE STYLES THAT ARE PART OF ANY MAJOR CLASS-LEVEL OR          
      *       SUBCLASS-LEVEL BUSINESS RULE FOR THIS DEPARTMENT.                 
      ******************************************************************        
           EXEC SQL                                                             
                                                                                
           DECLARE STYLES_BY_DEPT_CSR CURSOR WITH HOLD FOR                      
                                                                                
                SELECT SUB_CL_MDSEAR_ID                                         
                     , VS_ID                                                    
                     , LBL_SEQ_NBR                                              
                     , RPT_HIER_SEQ_NBR                                         
                     , BDY_STYL_SEQ_NBR                                         
                     , SLHTT_SEQ_NBR                                            
                     , FUNC_SEQ_NBR                                             
                     , GNDR_SEQ_NBR                                             
                     , FBRCN_SEQ_NBR                                            
                     , PATRN_SEQ_NBR                                            
                     , CUST_INTFC_DESC                                          
W35681               , CTG_SEQ_NBR                                              
W35681               , MAJ_CTG_SEQ_NBR                                          
W35681               , MTRL_SEQ_NBR                                             
W35681               , ATTR_1_SEQ_NBR                                           
W35681               , ATTR_2_SEQ_NBR                                           
W35681               , NW_SLHTT_SEQ_NBR                                         
                  FROM TMDARVS A                                                
                 WHERE A.DEPT_NBR  = :PV-CURR-DEPT-PACKED                       
                   AND A.ACTV_IND  = 'Y'                                        
                   AND NOT EXISTS                                               
                      (SELECT 1                                                 
                         FROM   IMT_CHARTC_RULDESC B                            
                            ,   TMDSEAR C                                       
                        WHERE   B.MDSEAR_ID   = C.MDSEAR_DKEY                   
                          AND   C.OPERCO_NBR  = '03'                            
                          AND ((C.MDSELV_CDE  = 'MCL'                           
                          AND   C.DEPT_NBR    =                                 
                                  SUBSTR(DIGITS(A.DEPT_NBR),2,3)                
                          AND   C.MAJ_CL_NBR  =                                 
                                  SUBSTR(DIGITS(A.MAJ_CL_NBR),3,2))             
                           OR  (C.MDSELV_CDE  = 'SCL'                           
                          AND   C.DEPT_NBR    =                                 
                                  SUBSTR(DIGITS(A.DEPT_NBR),2,3)                
                          AND   C.MAJ_CL_NBR  =                                 
                                  SUBSTR(DIGITS(A.MAJ_CL_NBR),3,2)              
                          AND   C.SUB_CL_NBR  =                                 
                                  SUBSTR(DIGITS(A.SUB_CL_NBR),2,3))))           
              ORDER BY LBL_SEQ_NBR                                              
                     , RPT_HIER_SEQ_NBR                                         
                     , BDY_STYL_SEQ_NBR                                         
                     , SLHTT_SEQ_NBR                                            
                     , FUNC_SEQ_NBR                                             
                     , GNDR_SEQ_NBR                                             
                     , FBRCN_SEQ_NBR                                            
                     , PATRN_SEQ_NBR                                            
W35681               , CTG_SEQ_NBR                                              
W35681               , MAJ_CTG_SEQ_NBR                                          
W35681               , MTRL_SEQ_NBR                                             
W35681               , ATTR_1_SEQ_NBR                                           
W35681               , ATTR_2_SEQ_NBR                                           
W35681               , NW_SLHTT_SEQ_NBR                                         
                                                                                
           END-EXEC.                                                            
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
      ******************************************************************        
      * MAINLINE.                                                               
      ******************************************************************        
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           IF  PS-END-OF-BUS-RULE-CSR                                           
               DISPLAY ' '                                                      
               DISPLAY '***************************************'                
               DISPLAY ' NO BUSINESS RULE UPDATES PROCESSED'                    
               DISPLAY '***************************************'                
               DISPLAY ' '                                                      
           ELSE                                                                 
               PERFORM 2000-PROCESS-BUS-RULE-UPDATES                            
                   UNTIL PS-END-OF-BUS-RULE-CSR                                 
               PERFORM 8000-COMMIT                                              
           END-IF.                                                              
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      * INITIALIZATION                                                          
      ******************************************************************        
                                                                                
       1000-INITIALIZATION.                                                     
                                                                                
      **** INITIALIZE THE DCLGEN FIELDS FOR TABLES ACCESSED                     
           INITIALIZE DCLIMT00022                                               
                      DCLTMDSEAR                                                
                      DCLTMDARVS                                                
                      DCLTVSCHVL                                                
                      DCLTMDSEDP                                                
                      DCLTMDSEBG.                                               
                                                                                
           PERFORM 1100-OPEN-BUS-RULE-CSR.                                      
                                                                                
           PERFORM 2400-FETCH-BUS-RULE-CSR.                                     
                                                                                
                                                                                
      ******************************************************************        
      *  OPEN THE BUSINESS RULES CURSOR                                         
      ******************************************************************        
       1100-OPEN-BUS-RULE-CSR.                                                  
                                                                                
           EXEC SQL                                                             
               OPEN BUS_RULE_CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '1100-OPEN-BUS-RULE-CSR'                                
                     TO  PV-PARAGRAPH-NAME                                      
                   MOVE 'OPEN ON BUSINESS RULE CURSOR'                          
                     TO  PV-DB2-OPERATION-ATTEMPTED                             
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  FOR EACH NEW OR MODIFIED BUSINESS RULE IN TABLE                        
      *  IMT_CHARTC_RULDESC, FIND ALL OF THE IMPACTED STYLES AND                
      *  UPDATE THE CUSTOMER INTERFACE DESCRIPTION.                             
      ******************************************************************        
                                                                                
       2000-PROCESS-BUS-RULE-UPDATES.                                           
                                                                                
           INITIALIZE DCLTMDARVS.                                               
                                                                                
           MOVE 'N' TO PS-END-OF-STYLE-CSR-SW.                                  
                                                                                
           PERFORM 2010-SELECT-BUS-GROUP.                                       
                                                                                
           PERFORM 2020-SAVE-STYLE-ATTRS.                                       
                                                                                
           IF  PS-DEPT-LVL-RULE                                                 
               PERFORM 2030-OPEN-STYLES-BY-DEPT-CSR                             
               PERFORM 2130-FETCH-STYLES-BY-DEPT                                
           ELSE                                                                 
               IF  PS-MCL-LVL-RULE                                              
                   PERFORM 2040-OPEN-STYLES-BY-MCL-CSR                          
                   PERFORM 2140-FETCH-STYLES-BY-MCL                             
               ELSE                                                             
                   IF  PS-SCL-LVL-RULE                                          
                       PERFORM 2050-OPEN-STYLES-BY-SCL-CSR                      
                       PERFORM 2150-FETCH-STYLES-BY-SCL                         
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 2100-PROCESS-STYLES                                          
               UNTIL PS-END-OF-STYLE-CSR.                                       
                                                                                
           IF  PS-DEPT-LVL-RULE                                                 
               PERFORM 2040-CLOSE-STYLES-BY-DEPT-CSR                            
           ELSE                                                                 
               IF  PS-MCL-LVL-RULE                                              
                   PERFORM 2050-CLOSE-STYLES-BY-MCL-CSR                         
               ELSE                                                             
                   IF  PS-SCL-LVL-RULE                                          
                       PERFORM 2060-CLOSE-STYLES-BY-SCL-CSR                     
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 2300-UPDATE-BUS-RULE.                                        
                                                                                
           PERFORM 2400-FETCH-BUS-RULE-CSR.                                     
                                                                                
                                                                                
      ******************************************************************        
      *  SELECT THE BUSINESS GROUP DESCRIPTION FOR THE DEPARTMENT               
      *  BEING PROCESSED.                                                       
      *    - USED IN SOME BUSINESS RULES                                        
      *    - ALSO USED AS A DEFAULT DESCRIPTION WHEN TWO OF THE THREE           
      *      SHORT DESCRIPTIONS ARE BLANK, AND THE BRAND/HIERARCHY              
      *      DEFAULT IS NOT COMPLETE.                                           
      ******************************************************************        
       2010-SELECT-BUS-GROUP.                                                   
                                                                                
           PERFORM  WITH TEST AFTER                                             
             VARYING PV-RETRY-COUNT FROM 1 BY 1                                 
               UNTIL SQLCODE = +0                                               
                  OR SQLCODE = +100                                             
                  OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                    
                                                                                
               EXEC SQL                                                         
                  SELECT C.BUS_GP_SHRT_DESC                                     
                    INTO :MDSEBG-BUS-GP-SHRT-DESC                               
                    FROM TMDSEAR A                                              
                       , TMDSEDP B                                              
                       , TMDSEBG C                                              
                   WHERE A.OPERCO_NBR       = '03'                              
                     AND A.MDSELV_CDE       = 'DPT'                             
                     AND A.DEPT_NBR         = :MDSEAR-DEPT-NBR                  
                     AND CURRENT DATE BETWEEN A.STRT_DTE AND A.END_DTE          
                     AND A.MDSEAR_DKEY      = B.MDSEAR_DKEY                     
                     AND B.MDSEBG_NBR       = C.MDSEBG_NBR                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                        MOVE MDSEBG-BUS-GP-SHRT-DESC                            
                          TO PV-SAVED-BUS-GRP-DESC                              
                   WHEN SQLCODE = +100                                          
                        MOVE 'MISC'                                             
                          TO PV-SAVED-BUS-GRP-DESC                              
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        ADD 1        TO PV-RETRY-COUNT                          
                   WHEN OTHER                                                   
                        MOVE '2010-SELECT-BUS-GROUP'                            
                                     TO PV-PARAGRAPH-NAME                       
                        MOVE 'SELECT BUS GRP FROM TMDSEBG'                      
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                        PERFORM 9999-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '2010-SELECT-BUS-GROUP'                                     
                                     TO PV-PARAGRAPH-NAME                       
               MOVE 'SELECT BUS GRP FROM TMDSEBG'                               
                                     TO PV-DB2-OPERATION-ATTEMPTED              
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  SAVE THE CURRENT STYLE ATTRIBUTES BEFORE FETCHING THE NEXT             
      *  STYLE.  THE SAVED VALUES WILL BE USED WHEN PROCESSING THE NEXT         
      *  STYLE TO DETERMINE IF THE CUSTOMER INTERFACE DESCRIPTION NEEDS         
      *  TO BE REBUILT.                                                         
      ******************************************************************        
       2020-SAVE-STYLE-ATTRS.                                                   
                                                                                
           MOVE MDARVS-LBL-SEQ-NBR      TO PV-PREV-LBL-SEQ-NBR.                 
           MOVE MDARVS-RPT-HIER-SEQ-NBR TO PV-PREV-RPT-HIER-SEQ-NBR.            
           MOVE MDARVS-BDY-STYL-SEQ-NBR TO PV-PREV-BDY-STYL-SEQ-NBR.            
           MOVE MDARVS-SLHTT-SEQ-NBR    TO PV-PREV-SLHTT-SEQ-NBR.               
           MOVE MDARVS-FUNC-SEQ-NBR     TO PV-PREV-FUNC-SEQ-NBR.                
           MOVE MDARVS-FBRCN-SEQ-NBR    TO PV-PREV-FBRCN-SEQ-NBR.               
           MOVE MDARVS-PATRN-SEQ-NBR    TO PV-PREV-PATRN-SEQ-NBR.               
           MOVE MDARVS-GNDR-SEQ-NBR     TO PV-PREV-GNDR-SEQ-NBR.                
W35681     MOVE MDARVS-CTG-SEQ-NBR      TO PV-PREV-CTG-SEQ-NBR.                 
W35681     MOVE MDARVS-MAJ-CTG-SEQ-NBR  TO PV-PREV-MAJ-CTG-SEQ-NBR.             
W35681     MOVE MDARVS-MTRL-SEQ-NBR     TO PV-PREV-MTRL-SEQ-NBR.                
W35681     MOVE MDARVS-ATTR-1-SEQ-NBR   TO PV-PREV-ATTR-1-SEQ-NBR.              
W35681     MOVE MDARVS-ATTR-2-SEQ-NBR   TO PV-PREV-ATTR-2-SEQ-NBR.              
W35681     MOVE MDARVS-NW-SLHTT-SEQ-NBR TO PV-PREV-NW-SLHTT-SEQ-NBR.            
                                                                                
                                                                                
      ******************************************************************        
      *  OPEN THE STYLES-BY-DEPT CURSOR                                         
      ******************************************************************        
       2030-OPEN-STYLES-BY-DEPT-CSR.                                            
                                                                                
           EXEC SQL                                                             
               OPEN STYLES_BY_DEPT_CSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '2030-OPEN-STYLES-BY-DEPT-CSR'                          
                     TO  PV-PARAGRAPH-NAME                                      
                   MOVE 'OPEN ON STYLES-BY-DEPT CURSOR'                         
                     TO  PV-DB2-OPERATION-ATTEMPTED                             
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  OPEN THE STYLES-BY-MCL CURSOR                                          
      ******************************************************************        
       2040-OPEN-STYLES-BY-MCL-CSR.                                             
                                                                                
           EXEC SQL                                                             
               OPEN STYLES_BY_MCL_CSR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '2040-OPEN-STYLES-BY-MCL-CSR'                           
                     TO  PV-PARAGRAPH-NAME                                      
                   MOVE 'OPEN ON STYLES-BY-MCL CURSOR'                          
                     TO  PV-DB2-OPERATION-ATTEMPTED                             
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  OPEN THE STYLES-BY-SCL CURSOR                                          
      *********************X********************************************        
       2050-OPEN-STYLES-BY-SCL-CSR.                                             
                                                                                
           EXEC SQL                                                             
               OPEN STYLES_BY_SCL_CSR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '2050-OPEN-STYLES-BY-SCL-CSR'                           
                     TO  PV-PARAGRAPH-NAME                                      
                   MOVE 'OPEN ON STYLES-BY-SCL CURSOR'                          
                     TO  PV-DB2-OPERATION-ATTEMPTED                             
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  CLOSE THE STYLES-BY-DEPT CURSOR                                        
      ******************************************************************        
       2040-CLOSE-STYLES-BY-DEPT-CSR.                                           
                                                                                
           EXEC SQL                                                             
               CLOSE STYLES_BY_DEPT_CSR                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '2040-CLOSE-STYLES-BY-DEPT-CSR'                         
                     TO  PV-PARAGRAPH-NAME                                      
                   MOVE 'CLOSE ON STYLES-BY-DEPT CURSOR'                        
                     TO  PV-DB2-OPERATION-ATTEMPTED                             
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  CLOSE THE STYLES-BY-MCL CURSOR                                         
      ******************************************************************        
       2050-CLOSE-STYLES-BY-MCL-CSR.                                            
                                                                                
           EXEC SQL                                                             
               CLOSE STYLES_BY_MCL_CSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '2050-CLOSE-STYLES-BY-MCL-CSR'                          
                     TO  PV-PARAGRAPH-NAME                                      
                   MOVE 'CLOSE ON STYLES-BY-MCL CURSOR'                         
                     TO  PV-DB2-OPERATION-ATTEMPTED                             
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  CLOSE THE STYLES-BY-SCL CURSOR                                         
      ******************************************************************        
       2060-CLOSE-STYLES-BY-SCL-CSR.                                            
                                                                                
           EXEC SQL                                                             
               CLOSE STYLES_BY_SCL_CSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '2060-CLOSE-STYLES-BY-SCL-CSR'                          
                     TO  PV-PARAGRAPH-NAME                                      
                   MOVE 'CLOSE ON STYLES-BY-SCL CURSOR'                         
                     TO  PV-DB2-OPERATION-ATTEMPTED                             
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  BUILD THE NEW CUSTOMER INTERFACE DESCRIPTION FOR EACH STYLE            
      *  IMPACTED BY THE BUSINESS RULE CHANGE, AND UPDATE THE CUSTOMER          
      *  INTERFACE DESCRIPTION IN TABLE TMDARVS.                                
      ******************************************************************        
       2100-PROCESS-STYLES.                                                     
                                                                                
           IF  MDARVS-LBL-SEQ-NBR      = PV-PREV-LBL-SEQ-NBR                    
           AND MDARVS-RPT-HIER-SEQ-NBR = PV-PREV-RPT-HIER-SEQ-NBR               
           AND MDARVS-BDY-STYL-SEQ-NBR = PV-PREV-BDY-STYL-SEQ-NBR               
           AND MDARVS-SLHTT-SEQ-NBR    = PV-PREV-SLHTT-SEQ-NBR                  
           AND MDARVS-FUNC-SEQ-NBR     = PV-PREV-FUNC-SEQ-NBR                   
           AND MDARVS-GNDR-SEQ-NBR     = PV-PREV-GNDR-SEQ-NBR                   
           AND MDARVS-FBRCN-SEQ-NBR    = PV-PREV-FBRCN-SEQ-NBR                  
           AND MDARVS-PATRN-SEQ-NBR    = PV-PREV-PATRN-SEQ-NBR                  
W35681     AND MDARVS-CTG-SEQ-NBR      = PV-PREV-CTG-SEQ-NBR                    
W35681     AND MDARVS-MAJ-CTG-SEQ-NBR  = PV-PREV-MAJ-CTG-SEQ-NBR                
W35681     AND MDARVS-MTRL-SEQ-NBR     = PV-PREV-MTRL-SEQ-NBR                   
W35681     AND MDARVS-ATTR-1-SEQ-NBR   = PV-PREV-ATTR-1-SEQ-NBR                 
W35681     AND MDARVS-ATTR-2-SEQ-NBR   = PV-PREV-ATTR-2-SEQ-NBR                 
W35681     AND MDARVS-NW-SLHTT-SEQ-NBR = PV-PREV-NW-SLHTT-SEQ-NBR               
      ****     IF ALL OF THE ATTRIBUTES ARE THE SAME, USE THE SAME              
      ****     CUSTOMER INTERFACE DESCRIPTION THAT WAS JUST BUILT               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 2140-BUILD-CUST-DESC                                     
           END-IF.                                                              
                                                                                
           IF  PV-CUST-DESC-EDITED = MDARVS-CUST-INTFC-DESC                     
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 2170-UPDATE-CUST-DESC                                    
           END-IF.                                                              
                                                                                
           IF  PA-NBR-UPDTS-SINCE-COMMIT >= +1000                               
               INITIALIZE PA-NBR-UPDTS-SINCE-COMMIT                             
               PERFORM 8000-COMMIT                                              
           END-IF.                                                              
                                                                                
           PERFORM 2020-SAVE-STYLE-ATTRS.                                       
                                                                                
           IF  PS-DEPT-LVL-RULE                                                 
               PERFORM 2130-FETCH-STYLES-BY-DEPT                                
           ELSE                                                                 
               IF  PS-MCL-LVL-RULE                                              
                   PERFORM 2140-FETCH-STYLES-BY-MCL                             
               ELSE                                                             
                   IF  PS-SCL-LVL-RULE                                          
                       PERFORM 2150-FETCH-STYLES-BY-SCL                         
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  FETCH THE STYLES FOR THE DEPT-LEVEL BUSINESS RULE BEING                
      *  PROCESSED                                                              
      ******************************************************************        
       2130-FETCH-STYLES-BY-DEPT.                                               
                                                                                
           EXEC SQL                                                             
               FETCH STYLES_BY_DEPT_CSR                                         
               INTO :MDARVS-SUB-CL-MDSEAR-ID                                    
                  , :MDARVS-VS-ID                                               
                  , :MDARVS-LBL-SEQ-NBR                                         
                  , :MDARVS-RPT-HIER-SEQ-NBR                                    
                  , :MDARVS-BDY-STYL-SEQ-NBR                                    
                  , :MDARVS-SLHTT-SEQ-NBR                                       
                  , :MDARVS-FUNC-SEQ-NBR                                        
                  , :MDARVS-GNDR-SEQ-NBR                                        
                  , :MDARVS-FBRCN-SEQ-NBR                                       
                  , :MDARVS-PATRN-SEQ-NBR                                       
                  , :MDARVS-CUST-INTFC-DESC                                     
W35681            , :MDARVS-CTG-SEQ-NBR                                         
W35681            , :MDARVS-MAJ-CTG-SEQ-NBR                                     
W35681            , :MDARVS-MTRL-SEQ-NBR                                        
W35681            , :MDARVS-ATTR-1-SEQ-NBR                                      
W35681            , :MDARVS-ATTR-2-SEQ-NBR                                      
W35681            , :MDARVS-NW-SLHTT-SEQ-NBR                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN +0                                                            
                  ADD +1 TO PA-NBR-STYLES-PROCESSED                             
             WHEN +100                                                          
                  SET PS-END-OF-STYLE-CSR TO TRUE                               
             WHEN OTHER                                                         
                  MOVE '2130-FETCH-STYLES-BY-DEPT'                              
                    TO  PV-PARAGRAPH-NAME                                       
                  MOVE 'FETCH ON STYLES-BY-DEPT CURSOR'                         
                    TO  PV-DB2-OPERATION-ATTEMPTED                              
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  FETCH THE STYLES FOR THE MAJOR CLASS-LEVEL BUSINESS RULE BEING         
      *  PROCESSED                                                              
      ******************************************************************        
       2140-FETCH-STYLES-BY-MCL.                                                
                                                                                
           EXEC SQL                                                             
               FETCH STYLES_BY_MCL_CSR                                          
               INTO :MDARVS-SUB-CL-MDSEAR-ID                                    
                  , :MDARVS-VS-ID                                               
                  , :MDARVS-LBL-SEQ-NBR                                         
                  , :MDARVS-RPT-HIER-SEQ-NBR                                    
                  , :MDARVS-BDY-STYL-SEQ-NBR                                    
                  , :MDARVS-SLHTT-SEQ-NBR                                       
                  , :MDARVS-FUNC-SEQ-NBR                                        
                  , :MDARVS-GNDR-SEQ-NBR                                        
                  , :MDARVS-FBRCN-SEQ-NBR                                       
                  , :MDARVS-PATRN-SEQ-NBR                                       
                  , :MDARVS-CUST-INTFC-DESC                                     
W35681            , :MDARVS-CTG-SEQ-NBR                                         
W35681            , :MDARVS-MAJ-CTG-SEQ-NBR                                     
W35681            , :MDARVS-MTRL-SEQ-NBR                                        
W35681            , :MDARVS-ATTR-1-SEQ-NBR                                      
W35681            , :MDARVS-ATTR-2-SEQ-NBR                                      
W35681            , :MDARVS-NW-SLHTT-SEQ-NBR                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN +0                                                            
                  ADD +1 TO PA-NBR-STYLES-PROCESSED                             
             WHEN +100                                                          
                  SET PS-END-OF-STYLE-CSR TO TRUE                               
             WHEN OTHER                                                         
                  MOVE '2140-FETCH-STYLES-BY-MCL'                               
                    TO  PV-PARAGRAPH-NAME                                       
                  MOVE 'FETCH ON STYLES-BY-MCL CURSOR'                          
                    TO  PV-DB2-OPERATION-ATTEMPTED                              
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  FETCH THE STYLES FOR THE SUBCLASS-LEVEL BUSINESS RULE BEING            
      *  PROCESSED                                                              
      ******************************************************************        
       2150-FETCH-STYLES-BY-SCL.                                                
                                                                                
           EXEC SQL                                                             
               FETCH STYLES_BY_SCL_CSR                                          
               INTO :MDARVS-SUB-CL-MDSEAR-ID                                    
                  , :MDARVS-VS-ID                                               
                  , :MDARVS-LBL-SEQ-NBR                                         
                  , :MDARVS-RPT-HIER-SEQ-NBR                                    
                  , :MDARVS-BDY-STYL-SEQ-NBR                                    
                  , :MDARVS-SLHTT-SEQ-NBR                                       
                  , :MDARVS-FUNC-SEQ-NBR                                        
                  , :MDARVS-GNDR-SEQ-NBR                                        
                  , :MDARVS-FBRCN-SEQ-NBR                                       
                  , :MDARVS-PATRN-SEQ-NBR                                       
                  , :MDARVS-CUST-INTFC-DESC                                     
W35681            , :MDARVS-CTG-SEQ-NBR                                         
W35681            , :MDARVS-MAJ-CTG-SEQ-NBR                                     
W35681            , :MDARVS-MTRL-SEQ-NBR                                        
W35681            , :MDARVS-ATTR-1-SEQ-NBR                                      
W35681            , :MDARVS-ATTR-2-SEQ-NBR                                      
W35681            , :MDARVS-NW-SLHTT-SEQ-NBR                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN +0                                                            
                  ADD +1 TO PA-NBR-STYLES-PROCESSED                             
             WHEN +100                                                          
                  SET PS-END-OF-STYLE-CSR TO TRUE                               
             WHEN OTHER                                                         
                  MOVE '2150-FETCH-STYLES-BY-SCL'                               
                    TO  PV-PARAGRAPH-NAME                                       
                  MOVE 'FETCH ON STYLES-BY-SCL CURSOR'                          
                    TO  PV-DB2-OPERATION-ATTEMPTED                              
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  BUILD THE 26-CHARACTER CUSTOMER INTERFACE DESCRIPTION                  
      ******************************************************************        
       2140-BUILD-CUST-DESC.                                                    
                                                                                
           MOVE SPACES TO  PV-CUST-DESC                                         
                           PV-CUST-DESC-PART-1                                  
                           PV-CUST-DESC-PART-2                                  
                           PV-CUST-DESC-PART-3                                  
                           PV-CUST-DESC-DFLT-BRND                               
W35681                     PV-CUST-DESC-DFLT-HIER                               
W35681                     PV-CUST-DESC-DFLT-CTG.                               
           MOVE ZERO   TO  PV-NBR-BLANK-ATTRS.                                  
           MOVE 'N'    TO  PS-DFLT-BRND-IS-BLANK-SW                             
W35681                     PS-DFLT-HIER-IS-BLANK-SW                             
W35681                     PS-DFLT-CTG-IS-BLANK-SW.                             
                                                                                
           PERFORM 2141-GET-CUST-DESC-PART-1.                                   
           PERFORM 2142-GET-CUST-DESC-PART-2.                                   
                                                                                
W35681***  IF TWO OF THREE PARTS OF THE CUST DESC ARE                           
W35681***  BLANK AND ONE OF THEM IS BRAND OR CATAGORY,                          
W35681***  TRY TO USE BRAND + CATAGORY DEFAULT                                  
W35681***  AS THE DEFAULT DESCRIPTION .                                         
W35681***  IF THAT DOESN'T WORK, TRY BRAND + HIERARCHY                          
W35681***  AS THE DEFAULT DESCRIPTION .                                         
W35681***  IF THAT DOESN'T WORK, DEFAULT TO                                     
W35681***  BUSINESS GROUP AS THE DEFAULT DESCRIPTION                            
                                                                                
W35681     IF  PV-NBR-BLANK-ATTRS = 2                                           
W35681         PERFORM 2160-APPLY-DEFAULTS                                      
W35681     ELSE                                                                 
W35681***      IF AT LEAST ONE OF THE FIRST TWO PARTS OF THE CUST DESC          
W35681***      IS NOT BLANK, GO AHEAD AND RETRIEVE THE THIRD PART               
W35681         PERFORM 2143-GET-CUST-DESC-PART-3                                
W35681         IF  PV-NBR-BLANK-ATTRS = 2                                       
W35681             PERFORM 2160-APPLY-DEFAULTS                                  
W35681         END-IF                                                           
W35681     END-IF.                                                              
                                                                                
           PERFORM 2150-EDIT-CUST-DESC.                                         
                                                                                
W35681************************************************************              
W35681*     ALL OF THE COMMENTED OUT CODE BELOW WAS REPLACED     *              
W35681*     BY THE LOGIC ABOVE AND THE PERFORMED ROUTINES        *              
W35681************************************************************              
                                                                                
      ***  IF TWO OF THREE PARTS OF THE CUST DESC ARE                           
      ***  BLANK AND ONE OF THEM IS BRAND OR HIERARCHY,                         
      ***  USE BUSINESS GROUP AS THE DEFAULT DESCRIPTION                        
      ***                                                                       
      ***  IF TWO OF THREE PARTS OF THE CUST DESC ARE BLANK                     
      ***  AND NEITHER OF THEM IS BRAND OR HIERARCHY, USE THE                   
      ***  DEFAULT RULE OF BRAND + HIERARCHY                                    
      ***                                                                       
      ***  IF  PV-NBR-BLANK-ATTRS = 2                                           
      ***      IF  PS-DFLT-BRND-IS-BLANK                                        
      ***      OR  PS-DFLT-HIER-IS-BLANK                                        
      ***          MOVE PV-SAVED-BUS-GRP-DESC TO PV-CUST-DESC-PART-1            
      ***          MOVE SPACES                TO PV-CUST-DESC-PART-2            
      ***                                        PV-CUST-DESC-PART-3            
      ***      ELSE                                                             
      ***          PERFORM 2160-DEFAULT-BRND-HIER                               
      ***      END-IF                                                           
      ***  ELSE                                                                 
      ***      IF AT LEAST ONE OF THE FIRST TWO PARTS OF THE CUST DESC          
      ***      IS NOT BLANK, GO AHEAD AND RETRIEVE THE THIRD PART               
      ***      PERFORM 2143-GET-CUST-DESC-PART-3                                
      ***      IF  PV-NBR-BLANK-ATTRS = 2                                       
      ***          IF  PS-DFLT-BRND-IS-BLANK                                    
      ***          OR  PS-DFLT-HIER-IS-BLANK                                    
      ***              MOVE PV-SAVED-BUS-GRP-DESC                               
      ***                                     TO PV-CUST-DESC-PART-1            
      ***              MOVE SPACES            TO PV-CUST-DESC-PART-2            
      ***                                        PV-CUST-DESC-PART-3            
      ***          ELSE                                                         
      ***              PERFORM 2160-DEFAULT-BRND-HIER                           
      ***          END-IF                                                       
      ***      END-IF                                                           
      ***  END-IF.                                                              
                                                                                
      ******************************************************************        
      *  GET THE FIRST OF THREE 8-CHAR SHORT DESCRIPTIONS THAT WILL             
      *  BE USED TO BUILD THE CUSTOMER INTERFACE DESCRIPTION.                   
      ******************************************************************        
       2141-GET-CUST-DESC-PART-1.                                               
                                                                                
           MOVE IMT00022-POS1-VS-CHARTC-CDE TO PV-CHARTC-CDE                    
                                               PV-CHARTC-CDE-TYPE.              
           IF  IM-CHARTC-CDE                                                    
               PERFORM 2200-GET-ITEM-ATTR                                       
           ELSE                                                                 
               IF  BG-CHARTC-CDE                                                
                   MOVE PV-SAVED-BUS-GRP-DESC TO PV-SHORT-DESC                  
               ELSE                                                             
                   IF  NULL-CHARTC-CDE                                          
                       NEXT SENTENCE                                            
                   ELSE                                                         
                       MOVE +4001 TO ABEND-CODE                                 
                       MOVE '2141-GET-CUST-DESC-PART-1'                         
                         TO PV-PARAGRAPH-NAME                                   
                       MOVE 'INVALID ATTR USED IN BUS RULE'                     
                         TO PV-ABEND-MESSAGE                                    
                       PERFORM 9998-ABEND                                       
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE PV-SHORT-DESC TO PV-CUST-DESC-PART-1.                           
                                                                                
           IF  PV-CUST-DESC-PART-1 = SPACES                                     
               ADD +1 TO PV-NBR-BLANK-ATTRS                                     
               IF  PV-ATTR-BRAND                                                
                   SET PS-DFLT-BRND-IS-BLANK TO TRUE                            
               ELSE                                                             
W35681             IF  PV-ATTR-CTG                                              
W35681                 SET PS-DFLT-CTG-IS-BLANK TO TRUE                         
W35681             ELSE                                                         
W35681                 IF  PV-ATTR-HIER                                         
W35681                     SET PS-DFLT-HIER-IS-BLANK TO TRUE                    
W35681                 END-IF                                                   
W35681             END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               IF  PV-ATTR-BRAND                                                
                   MOVE PV-CUST-DESC-PART-1 TO PV-CUST-DESC-DFLT-BRND           
               ELSE                                                             
W35681             IF  PV-ATTR-CTG                                              
W35681                 MOVE PV-CUST-DESC-PART-1                                 
W35681                                      TO PV-CUST-DESC-DFLT-CTG            
W35681             ELSE                                                         
W35681                 IF  PV-ATTR-HIER                                         
W35681                     MOVE PV-CUST-DESC-PART-1                             
W35681                                      TO PV-CUST-DESC-DFLT-HIER           
W35681                 END-IF                                                   
W35681             END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  GET THE SECOND OF THREE 8-CHAR SHORT DESCRIPTIONS THAT WILL            
      *  BE USED TO BUILD THE CUSTOMER INTERFACE DESCRIPTION.                   
      ******************************************************************        
       2142-GET-CUST-DESC-PART-2.                                               
                                                                                
           MOVE IMT00022-POS2-VS-CHARTC-CDE TO PV-CHARTC-CDE                    
                                               PV-CHARTC-CDE-TYPE.              
                                                                                
           IF  IM-CHARTC-CDE                                                    
               PERFORM 2200-GET-ITEM-ATTR                                       
           ELSE                                                                 
               IF  BG-CHARTC-CDE                                                
                   MOVE PV-SAVED-BUS-GRP-DESC TO PV-SHORT-DESC                  
               ELSE                                                             
                   IF  NULL-CHARTC-CDE                                          
                       NEXT SENTENCE                                            
                   ELSE                                                         
                       MOVE +4001 TO ABEND-CODE                                 
                       MOVE '2142-GET-CUST-DESC-PART-2'                         
                         TO PV-PARAGRAPH-NAME                                   
                       MOVE 'INVALID ATTR USED IN BUS RULE'                     
                         TO PV-ABEND-MESSAGE                                    
                       PERFORM 9998-ABEND                                       
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE PV-SHORT-DESC TO PV-CUST-DESC-PART-2.                           
                                                                                
           IF  PV-CUST-DESC-PART-2 = SPACES                                     
               ADD +1 TO PV-NBR-BLANK-ATTRS                                     
               IF  PV-ATTR-BRAND                                                
                   SET PS-DFLT-BRND-IS-BLANK TO TRUE                            
               ELSE                                                             
W35681             IF  PV-ATTR-CTG                                              
W35681                 SET PS-DFLT-CTG-IS-BLANK TO TRUE                         
W35681             ELSE                                                         
W35681                 IF  PV-ATTR-CTG                                          
W35681                     SET PS-DFLT-HIER-IS-BLANK TO TRUE                    
W35681                 END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               IF  PV-ATTR-BRAND                                                
                   MOVE PV-CUST-DESC-PART-2 TO PV-CUST-DESC-DFLT-BRND           
               ELSE                                                             
W35681             IF  PV-ATTR-CTG                                              
W35681                 MOVE PV-CUST-DESC-PART-2                                 
W35681                                      TO PV-CUST-DESC-DFLT-CTG            
W35681             ELSE                                                         
W35681                 IF  PV-ATTR-HIER                                         
W35681                     MOVE PV-CUST-DESC-PART-2                             
W35681                                      TO PV-CUST-DESC-DFLT-HIER           
W35681                 END-IF                                                   
W35681             END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  GET THE THIRD OF THREE 8-CHAR SHORT DESCRIPTIONS THAT WILL             
      *  BE USED TO BUILD THE CUSTOMER INTERFACE DESCRIPTION.                   
      ******************************************************************        
       2143-GET-CUST-DESC-PART-3.                                               
                                                                                
           MOVE IMT00022-POS3-VS-CHARTC-CDE TO PV-CHARTC-CDE                    
                                               PV-CHARTC-CDE-TYPE.              
                                                                                
           IF  IM-CHARTC-CDE                                                    
               PERFORM 2200-GET-ITEM-ATTR                                       
           ELSE                                                                 
               IF  BG-CHARTC-CDE                                                
                   MOVE PV-SAVED-BUS-GRP-DESC TO PV-SHORT-DESC                  
               ELSE                                                             
                   IF  NULL-CHARTC-CDE                                          
                       NEXT SENTENCE                                            
                   ELSE                                                         
                       MOVE +4001 TO ABEND-CODE                                 
                       MOVE '2143-GET-CUST-DESC-PART-3'                         
                         TO PV-PARAGRAPH-NAME                                   
                       MOVE 'INVALID ATTR USED IN BUS RULE'                     
                         TO PV-ABEND-MESSAGE                                    
                       PERFORM 9998-ABEND                                       
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE PV-SHORT-DESC TO PV-CUST-DESC-PART-3.                           
                                                                                
           IF  PV-CUST-DESC-PART-3 = SPACES                                     
               ADD +1 TO PV-NBR-BLANK-ATTRS                                     
               IF  PV-ATTR-BRAND                                                
                   SET PS-DFLT-BRND-IS-BLANK TO TRUE                            
               ELSE                                                             
W35681             IF  PV-ATTR-CTG                                              
W35681                 SET PS-DFLT-CTG-IS-BLANK TO TRUE                         
W35681             ELSE                                                         
W35681                 IF  PV-ATTR-HIER                                         
W35681                     SET PS-DFLT-HIER-IS-BLANK TO TRUE                    
W35681             END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               IF  PV-ATTR-BRAND                                                
                   MOVE PV-CUST-DESC-PART-3 TO PV-CUST-DESC-DFLT-BRND           
               ELSE                                                             
W35681             IF  PV-ATTR-CTG                                              
W35681                 MOVE PV-CUST-DESC-PART-3                                 
W35681                                      TO PV-CUST-DESC-DFLT-CTG            
W35681             ELSE                                                         
W35681                 IF  PV-ATTR-HIER                                         
W35681                     MOVE PV-CUST-DESC-PART-3                             
W35681                                      TO PV-CUST-DESC-DFLT-HIER           
W35681                 END-IF                                                   
W35681         END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  EDIT THE CUSTOMER INTERFACE DESCRIPTION.                               
      ******************************************************************        
       2150-EDIT-CUST-DESC.                                                     
                                                                                
      **** ELIMINATE DUPLICATE SHORT DESCRIPTIONS WITHIN THE 26-CHAR            
      **** CUSTOMER INTERFACE DESCRIPTION.                                      
                                                                                
           IF  PV-CUST-DESC-PART-2 = PV-CUST-DESC-PART-3                        
               MOVE SPACES TO PV-CUST-DESC-PART-3                               
           END-IF.                                                              
                                                                                
           IF  PV-CUST-DESC-PART-1 = PV-CUST-DESC-PART-3                        
               MOVE SPACES TO PV-CUST-DESC-PART-3                               
           END-IF.                                                              
                                                                                
           IF  PV-CUST-DESC-PART-1 = PV-CUST-DESC-PART-2                        
               MOVE SPACES TO PV-CUST-DESC-PART-2                               
           END-IF.                                                              
                                                                                
      **** REFORMAT THE CUSTOMER DESCRIPTION TO ELIMINATE SPACES IN             
      **** PARTS 1 AND 2 OF THE 26-CHAR CUSTOMER INTERFACE DESCRIPTION.         
                                                                                
           IF  PV-CUST-DESC-PART-2 = SPACES                                     
               MOVE PV-CUST-DESC-PART-3 TO PV-CUST-DESC-PART-2                  
               MOVE SPACES              TO PV-CUST-DESC-PART-3                  
           END-IF.                                                              
                                                                                
           IF  PV-CUST-DESC-PART-1 = SPACES                                     
               MOVE PV-CUST-DESC-PART-2 TO PV-CUST-DESC-PART-1                  
               MOVE PV-CUST-DESC-PART-3 TO PV-CUST-DESC-PART-2                  
               MOVE SPACES              TO PV-CUST-DESC-PART-3                  
           END-IF.                                                              
                                                                                
      **** ELIMINATE EXTRA SPACES WITHIN THE 26-CHAR CUSTOMER INTERFACE         
      **** DESCRIPTION.                                                         
                                                                                
           IF  PV-CUST-DESC-PART-2 = SPACES                                     
           AND PV-CUST-DESC-PART-3 = SPACES                                     
               MOVE PV-SAVED-BUS-GRP-DESC TO PV-CUST-DESC-EDITED                
           ELSE                                                                 
               PERFORM 2155-ELIM-SPACES                                         
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  DELETE EXTRA SPACES FROM THE 26-CHAR CUSTOMER INTERFACE DESC           
      ******************************************************************        
       2155-ELIM-SPACES.                                                        
                                                                                
           INITIALIZE PV-END-SPACE-CNT                                          
                      PV-END-SPACE-CNT-1                                        
                      PV-END-SPACE-CNT-2                                        
                      PV-END-SPACE-CNT-3                                        
                      PV-CNT-TRAILING-SPACES                                    
                      PV-CUST-DESC-EDITED.                                      
                                                                                
           MOVE PV-CUST-DESC-PART-1 TO PV-CNT-TRAILING-SPACES.                  
           PERFORM 2157-COUNT-SPACES.                                           
           MOVE PV-END-SPACE-CNT    TO PV-END-SPACE-CNT-1.                      
                                                                                
           INITIALIZE PV-END-SPACE-CNT                                          
                      PV-CNT-TRAILING-SPACES.                                   
                                                                                
           MOVE PV-CUST-DESC-PART-2 TO PV-CNT-TRAILING-SPACES.                  
           PERFORM 2157-COUNT-SPACES.                                           
           MOVE PV-END-SPACE-CNT    TO PV-END-SPACE-CNT-2.                      
                                                                                
           INITIALIZE PV-END-SPACE-CNT                                          
                      PV-CNT-TRAILING-SPACES.                                   
                                                                                
           MOVE PV-CUST-DESC-PART-3 TO PV-CNT-TRAILING-SPACES.                  
           PERFORM 2157-COUNT-SPACES.                                           
           MOVE PV-END-SPACE-CNT    TO PV-END-SPACE-CNT-3.                      
                                                                                
           STRING PV-CUST-DESC-PART-1  (1 : 8 - PV-END-SPACE-CNT-1)             
                                       DELIMITED BY SIZE                        
                  SPACE                DELIMITED BY SIZE                        
                  PV-CUST-DESC-PART-2  (1 : 8 - PV-END-SPACE-CNT-2)             
                                       DELIMITED BY SIZE                        
                  SPACE                DELIMITED BY SIZE                        
                  PV-CUST-DESC-PART-3  (1 : 8 - PV-END-SPACE-CNT-3)             
                                       DELIMITED BY SIZE                        
                  INTO PV-CUST-DESC-EDITED                                      
           END-STRING.                                                          
                                                                                
                                                                                
      ******************************************************************        
      *  COUNT THE TRAILING SPACES IN EACH 8-CHAR SHORT DESCRIPTION             
      *  (PARTS 1, 2, AND 3 OF THE CUSTOMER INTERFACE DESCRIPTION).             
      ******************************************************************        
       2157-COUNT-SPACES.                                                       
                                                                                
           IF  PV-CNT-TRAILING-SPACES = SPACES                                  
               MOVE +8 TO PV-END-SPACE-CNT                                      
           ELSE                                                                 
               IF  PV-7-TRLNG-SPCS = SPACES                                     
                   MOVE +7 TO PV-END-SPACE-CNT                                  
               ELSE                                                             
                   IF  PV-6-TRLNG-SPCS = SPACES                                 
                       MOVE +6 TO PV-END-SPACE-CNT                              
                   ELSE                                                         
                       IF  PV-5-TRLNG-SPCS = SPACES                             
                           MOVE +5 TO PV-END-SPACE-CNT                          
                       ELSE                                                     
                           IF  PV-4-TRLNG-SPCS = SPACES                         
                               MOVE +4 TO PV-END-SPACE-CNT                      
                           ELSE                                                 
                               IF  PV-3-TRLNG-SPCS = SPACES                     
                                   MOVE +3 TO PV-END-SPACE-CNT                  
                               ELSE                                             
                                   IF  PV-2-TRLNG-SPCS = SPACES                 
                                       MOVE +2 TO PV-END-SPACE-CNT              
                                   ELSE                                         
                                       IF  PV-1-TRLNG-SPCS = SPACES             
                                           MOVE +1 TO PV-END-SPACE-CNT          
                                       END-IF                                   
                                   END-IF                                       
                               END-IF                                           
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
W35681******************************************************************        
W35681*  TWO OF THE THREE PARTS THAT MAKE UP THE 26-CHAR CUSTOMER               
W35681*  INTERFACE DESCRIPTION ARE BLANK - USE BRAND/CATAGORY DEFAULT           
W35681*  DESCRIPTION OR BRAND/HIERARCHY DEFAULT OR BUSINESS GROUP               
W35681******************************************************************        
W35681 2160-APPLY-DEFAULTS.                                                     
W35681                                                                          
W35681** BRAND MUST BE PRESENT OR BUS GRP DEFAULT IS USED                       
W35681** IF CATAGORY IS PRESENT IT WILL BE USED WITH BRAND                      
W35681** IF CATAGORY IS NOT PRESENT, HIERARCHY WILL BE USED WITH BRAND          
W35681** IF NEITHER CATAGORY NOR HIERARCHY ARE PRESENT, BUS GRP DEFAULT         
W35681** WILL BE USED.                                                          
W35681                                                                          
W35681     PERFORM 2161-TRY-BRAND.                                              
W35681                                                                          
W35681     IF  PV-CUST-DESC-PART-1    = SPACES                                  
W35681         MOVE PV-SAVED-BUS-GRP-DESC TO PV-CUST-DESC-PART-1                
W35681         MOVE SPACES                TO PV-CUST-DESC-PART-2                
W35681                                       PV-CUST-DESC-PART-3                
W35681     ELSE                                                                 
W35681         PERFORM 2162-TRY-CTG                                             
W35681         IF  PV-CUST-DESC-PART-2    = SPACES                              
W35681             PERFORM 2163-TRY-HIER                                        
W35681             IF  PV-CUST-DESC-PART-2    = SPACES                          
W35681                 MOVE PV-SAVED-BUS-GRP-DESC                               
W35681                                    TO PV-CUST-DESC-PART-1                
W35681                 MOVE SPACES        TO PV-CUST-DESC-PART-2                
W35681                                       PV-CUST-DESC-PART-3                
W35681              END-IF                                                      
W35681         END-IF                                                           
W35681     END-IF.                                                              
W35681                                                                          
W35681                                                                          
W35681                                                                          
W35681 2161-TRY-BRAND.                                                          
W35681                                                                          
W35681     IF PV-CUST-DESC-DFLT-BRND = SPACES                                   
W35681         MOVE PC-ATTR-BRAND TO PV-CHARTC-CDE                              
W35681                               PV-CHARTC-CDE-TYPE                         
W35681         PERFORM 2200-GET-ITEM-ATTR                                       
W35681         MOVE PV-SHORT-DESC TO PV-CUST-DESC-PART-1                        
W35681     ELSE                                                                 
W35681         MOVE PV-CUST-DESC-DFLT-BRND TO PV-CUST-DESC-PART-1               
W35681                                                                          
W35681     END-IF.                                                              
W35681                                                                          
W35681                                                                          
W35681 2162-TRY-CTG.                                                            
W35681                                                                          
W35681     IF PV-CUST-DESC-DFLT-CTG  = SPACES                                   
W35681         MOVE PC-ATTR-CTG   TO PV-CHARTC-CDE                              
W35681                               PV-CHARTC-CDE-TYPE                         
W35681         PERFORM 2200-GET-ITEM-ATTR                                       
W35681         MOVE PV-SHORT-DESC TO PV-CUST-DESC-PART-2                        
W35861         MOVE SPACES                 TO PV-CUST-DESC-PART-3               
W35681     ELSE                                                                 
W35681         MOVE PV-CUST-DESC-DFLT-CTG  TO PV-CUST-DESC-PART-2               
W35861         MOVE SPACES                 TO PV-CUST-DESC-PART-3               
W35681     END-IF.                                                              
W35681                                                                          
W35681 2163-TRY-HIER.                                                           
W35681                                                                          
W35681     IF PV-CUST-DESC-DFLT-HIER = SPACES                                   
W35681         MOVE PC-ATTR-HIER  TO PV-CHARTC-CDE                              
W35681                               PV-CHARTC-CDE-TYPE                         
W35681         PERFORM 2200-GET-ITEM-ATTR                                       
W35681         MOVE PV-SHORT-DESC TO PV-CUST-DESC-PART-2                        
W35861         MOVE SPACES                 TO PV-CUST-DESC-PART-3               
W35681     ELSE                                                                 
W35681         MOVE PV-CUST-DESC-DFLT-HIER TO PV-CUST-DESC-PART-2               
W35861         MOVE SPACES                 TO PV-CUST-DESC-PART-3               
W35681     END-IF.                                                              
W35681                                                                          
W35681                                                                          
      ******************************************************************        
      *  UPDATE THE CUSTOMER INTERFACE DESCRIPTION ON TMDARVS                   
      ******************************************************************        
       2170-UPDATE-CUST-DESC.                                                   
                                                                                
           MOVE PV-CUST-DESC-EDITED TO MDARVS-CUST-INTFC-DESC.                  
                                                                                
           PERFORM  WITH TEST AFTER                                             
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
                                                                                
               EXEC SQL                                                         
                   UPDATE TMDARVS                                               
                      SET CUST_INTFC_DESC  = :MDARVS-CUST-INTFC-DESC            
                    WHERE SUB_CL_MDSEAR_ID = :MDARVS-SUB-CL-MDSEAR-ID           
                      AND VS_ID            = :MDARVS-VS-ID                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                        ADD +1 TO PA-NBR-TMDARVS-UPDATES                        
                                  PA-NBR-UPDTS-SINCE-COMMIT                     
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        ADD 1          TO PV-RETRY-COUNT                        
                   WHEN OTHER                                                   
                        MOVE '2170-UPDATE-CUST-DESC'                            
                                       TO PV-PARAGRAPH-NAME                     
                        MOVE 'UPDATE CUST DESC ON TMDARVS'                      
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                        PERFORM 9999-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '2170-UPDATE-CUST-DESC'                                     
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'UPDATE CUST DESC ON TMDARVS'                               
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  GET THE ITEM ATTRIBUTE 8-CHAR SHORT DESCRIPTION NEEDED TO              
      *  BUILD THE 26-CHARACTER CUSTOMER INTERFACE DESCRIPTION.                 
      ******************************************************************        
       2200-GET-ITEM-ATTR.                                                      
                                                                                
           MOVE PV-CHARTC-CDE TO VSCHVL-CHARTC-CDE.                             
                                                                                
W35681*   CHANGED IF - ELSE TO AN EVALUATE                                      
W35681                                                                          
W35681     EVALUATE TRUE                                                        
W35681                                                                          
W35681         WHEN PV-ATTR-BRAND                                               
W35681                  MOVE MDARVS-LBL-SEQ-NBR                                 
W35681                             TO VSCHVL-CHARTC-SEQ-NBR                     
W35681         WHEN PV-ATTR-HIER                                                
W35681                  MOVE MDARVS-RPT-HIER-SEQ-NBR                            
W35681                             TO VSCHVL-CHARTC-SEQ-NBR                     
W35681         WHEN PV-ATTR-FUNC                                                
W35681                  MOVE MDARVS-FUNC-SEQ-NBR                                
W35681                             TO VSCHVL-CHARTC-SEQ-NBR                     
W35681         WHEN PV-ATTR-SILH                                                
W35681                  MOVE MDARVS-SLHTT-SEQ-NBR                               
W35681                             TO VSCHVL-CHARTC-SEQ-NBR                     
W35681         WHEN PV-ATTR-BDYSTL                                              
W35681                  MOVE MDARVS-BDY-STYL-SEQ-NBR                            
W35681                             TO VSCHVL-CHARTC-SEQ-NBR                     
W35681         WHEN PV-ATTR-GNDR                                                
W35681                  MOVE MDARVS-GNDR-SEQ-NBR                                
W35681                             TO VSCHVL-CHARTC-SEQ-NBR                     
W35681         WHEN PV-ATTR-FBRCN                                               
W35681                  MOVE MDARVS-FBRCN-SEQ-NBR                               
W35681                             TO VSCHVL-CHARTC-SEQ-NBR                     
W35681         WHEN PV-ATTR-PATRN                                               
W35681                  MOVE MDARVS-PATRN-SEQ-NBR                               
W35681                             TO VSCHVL-CHARTC-SEQ-NBR                     
W35681         WHEN PV-ATTR-CTG                                                 
W35681                  MOVE MDARVS-CTG-SEQ-NBR                                 
W35681                             TO VSCHVL-CHARTC-SEQ-NBR                     
W35681         WHEN PV-ATTR-MAJ-CTG                                             
W35681                  MOVE MDARVS-MAJ-CTG-SEQ-NBR                             
W35681                             TO VSCHVL-CHARTC-SEQ-NBR                     
W35681         WHEN PV-ATTR-NW-SILH                                             
W35681                  MOVE MDARVS-NW-SLHTT-SEQ-NBR                            
W35681                             TO VSCHVL-CHARTC-SEQ-NBR                     
W35681         WHEN PV-ATTR-MTRL                                                
W35681                  MOVE MDARVS-MTRL-SEQ-NBR                                
W35681                             TO VSCHVL-CHARTC-SEQ-NBR                     
W35681         WHEN PV-ATTR-ATTR-1                                              
W35681                  MOVE MDARVS-ATTR-1-SEQ-NBR                              
W35681                             TO VSCHVL-CHARTC-SEQ-NBR                     
W35681         WHEN PV-ATTR-ATTR-2                                              
W35681                  MOVE MDARVS-ATTR-2-SEQ-NBR                              
W35681                             TO VSCHVL-CHARTC-SEQ-NBR                     
W35681     END-EVALUATE.                                                        
                                                                                
           PERFORM 2210-GET-ITEM-ATTR-TVSCHVL.                                  
                                                                                
                                                                                
      ******************************************************************        
      *  FINISH GETTING THE ITEM ATTRIBUTE FOR THIS STYLE                       
      ******************************************************************        
       2210-GET-ITEM-ATTR-TVSCHVL.                                              
                                                                                
           MOVE SPACES TO VSCHVL-SHRT-CHARTC-DESC.                              
                                                                                
           PERFORM  WITH TEST AFTER                                             
             VARYING PV-RETRY-COUNT FROM 1 BY 1                                 
               UNTIL SQLCODE = +0                                               
                  OR SQLCODE = +100                                             
                  OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                    
                                                                                
               EXEC SQL                                                         
                  SELECT SHRT_CHARTC_DESC                                       
                    INTO :VSCHVL-SHRT-CHARTC-DESC                               
                    FROM TVSCHVL                                                
                   WHERE CHARTC_CDE     = :VSCHVL-CHARTC-CDE                    
                     AND CHARTC_SEQ_NBR = :VSCHVL-CHARTC-SEQ-NBR                
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                        CONTINUE                                                
                   WHEN SQLCODE = +100                                          
                        MOVE SPACES TO VSCHVL-SHRT-CHARTC-DESC                  
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        ADD 1        TO PV-RETRY-COUNT                          
                   WHEN OTHER                                                   
                        MOVE '2210-GET-ITEM-ATTR-TVSCHVL'                       
                                     TO PV-PARAGRAPH-NAME                       
                        MOVE 'SELECT ON TVSCHVL'                                
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                        PERFORM 9999-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '2210-GET-ITEM-ATTR-TVSCHVL'                                
                                     TO PV-PARAGRAPH-NAME                       
               MOVE 'SELECT ON TVSCHVL'                                         
                                     TO PV-DB2-OPERATION-ATTEMPTED              
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           IF  VSCHVL-SHRT-CHARTC-DESC = SPACES                                 
           OR  VSCHVL-SHRT-CHARTC-DESC = 'NONE'                                 
           OR  VSCHVL-SHRT-CHARTC-DESC = 'OTHER'                                
               MOVE SPACES TO PV-SHORT-DESC                                     
           ELSE                                                                 
               MOVE VSCHVL-SHRT-CHARTC-DESC TO PV-SHORT-DESC                    
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  UPDATE THE PROCESSING CODE ON BUSINESS RULE TABLE                      
      *  IMT_CHARTC_RULDESC.                                                    
      ******************************************************************        
       2300-UPDATE-BUS-RULE.                                                    
                                                                                
           MOVE PC-BUS-RULE-PRC-CPLT TO IMT00022-CHARTC-RUL-PRC-CDE.            
                                                                                
           PERFORM  WITH TEST AFTER                                             
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
                                                                                
               EXEC SQL                                                         
                   UPDATE IMT_CHARTC_RULDESC                                    
                      SET CHARTC_RUL_PRC_CDE =                                  
                                         :IMT00022-CHARTC-RUL-PRC-CDE           
                    WHERE MDSEAR_ID    = :IMT00022-MDSEAR-ID                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                        ADD +1 TO PA-NBR-BUS-RULE-UPDATES                       
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                        ADD 1          TO PV-RETRY-COUNT                        
                   WHEN OTHER                                                   
                        MOVE '2300-UPDATE-BUS-RULE'                             
                                       TO PV-PARAGRAPH-NAME                     
                        MOVE 'UPDT CUST DESC BUS RULE TABLE'                    
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                        PERFORM 9999-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '2300-UPDATE-BUS-RULE'                                      
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'UPDT CUST DESC BUS RULE TABLE'                             
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  FETCH THE BUSINESS RULES CURSOR                                        
      ******************************************************************        
       2400-FETCH-BUS-RULE-CSR.                                                 
                                                                                
           MOVE 'N' TO PS-DEPT-LVL-RULE-SW                                      
                       PS-MCL-LVL-RULE-SW                                       
                       PS-SCL-LVL-RULE-SW.                                      
                                                                                
           INITIALIZE PV-CURR-DEPT-9                                            
                      PV-CURR-DEPT-PACKED                                       
                      PV-CURR-MCL-9                                             
                      PV-CURR-MCL                                               
                      PV-CURR-SCL-9                                             
                      PV-CURR-SCL.                                              
                                                                                
           EXEC SQL                                                             
               FETCH BUS_RULE_CSR                                               
               INTO :IMT00022-MDSEAR-ID                                         
                  , :MDSEAR-MDSELV-CDE                                          
                  , :MDSEAR-DEPT-NBR                                            
                  , :MDSEAR-MAJ-CL-NBR                                          
                  , :MDSEAR-SUB-CL-NBR                                          
                  , :IMT00022-POS1-VS-CHARTC-CDE                                
                  , :IMT00022-POS2-VS-CHARTC-CDE                                
                  , :IMT00022-POS3-VS-CHARTC-CDE                                
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN +0                                                            
                  ADD +1 TO PA-NBR-BUS-RULES-PROCESSED                          
                  MOVE MDSEAR-DEPT-NBR   TO PV-CURR-DEPT                        
                  MOVE PV-CURR-DEPT-9    TO PV-CURR-DEPT-PACKED                 
                  MOVE MDSEAR-MAJ-CL-NBR TO PV-CURR-MCL                         
                  MOVE PV-CURR-MCL-9     TO PV-CURR-MCL-PACKED                  
                  MOVE MDSEAR-SUB-CL-NBR TO PV-CURR-SCL                         
                  MOVE PV-CURR-SCL-9     TO PV-CURR-SCL-PACKED                  
                  IF  MDSEAR-MDSELV-CDE = 'DPT'                                 
                      SET PS-DEPT-LVL-RULE TO TRUE                              
                  ELSE                                                          
                      IF  MDSEAR-MDSELV-CDE = 'MCL'                             
                          SET PS-MCL-LVL-RULE TO TRUE                           
                      ELSE                                                      
                          IF  MDSEAR-MDSELV-CDE = 'SCL'                         
                              SET PS-SCL-LVL-RULE TO TRUE                       
                          END-IF                                                
                      END-IF                                                    
                  END-IF                                                        
             WHEN +100                                                          
                  SET PS-END-OF-BUS-RULE-CSR TO TRUE                            
             WHEN OTHER                                                         
                  MOVE '2400-FETCH-BUS-RULE-CSR' TO PV-PARAGRAPH-NAME           
                  MOVE 'FETCH ON BUSINESS RULE CURSOR' TO                       
                        PV-DB2-OPERATION-ATTEMPTED                              
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  COMMIT DB2 UPDATES                                                     
      ******************************************************************        
                                                                                
       8000-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
               COMMIT WORK                                                      
           END-EXEC.                                                            
           ADD +1 TO PA-NBR-COMMITS-TAKEN.                                      
                                                                                
                                                                                
      ******************************************************************        
      *  PERFORM END-OF-JOB PROCESSING                                          
      ******************************************************************        
                                                                                
       9000-EOJ-ROUTINE.                                                        
                                                                                
           PERFORM 9100-CLOSE-BUS-RULE-CSR.                                     
                                                                                
           PERFORM 9200-DISPLAY-TOTALS.                                         
                                                                                
                                                                                
      ******************************************************************        
      *  CLOSE THE BUSINESS RULES CURSOR                                        
      ******************************************************************        
       9100-CLOSE-BUS-RULE-CSR.                                                 
                                                                                
           EXEC SQL                                                             
               CLOSE BUS_RULE_CSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '9100-CLOSE-BUS-RULE-CSR'                               
                     TO  PV-PARAGRAPH-NAME                                      
                   MOVE 'CLOSE ON BUSINESS RULE CURSOR '                        
                     TO  PV-DB2-OPERATION-ATTEMPTED                             
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  DISPLAYS PROGRAM TOTALS                                                
      ******************************************************************        
                                                                                
       9200-DISPLAY-TOTALS.                                                     
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*********************************************'.             
           DISPLAY '***** TOTAL COUNTS FOR PROGRAM IMKUP064 *****'.             
           DISPLAY '*********************************************'.             
                                                                                
           MOVE PA-NBR-BUS-RULES-PROCESSED   TO PV-DISPLAY-COUNT.               
           DISPLAY 'TOT NBR OF BUS RULES PROCESSED = '                          
                                                     PV-DISPLAY-COUNT.          
                                                                                
           MOVE PA-NBR-BUS-RULE-UPDATES      TO PV-DISPLAY-COUNT.               
           DISPLAY 'TOT NBR OF BUS RULES UPDATED   = '                          
                                                     PV-DISPLAY-COUNT.          
                                                                                
           MOVE PA-NBR-STYLES-PROCESSED      TO PV-DISPLAY-COUNT.               
           DISPLAY 'TOT NBR OF STYLES PROCESSED    = '                          
                                                     PV-DISPLAY-COUNT.          
                                                                                
           MOVE PA-NBR-TMDARVS-UPDATES       TO PV-DISPLAY-COUNT.               
           DISPLAY 'TOT NBR OF UPDATES TO TMDARVS  = '                          
                                                     PV-DISPLAY-COUNT.          
                                                                                
           MOVE PA-NBR-COMMITS-TAKEN         TO PV-DISPLAY-COUNT.               
           DISPLAY 'TOT NBR OF COMMITS             = '                          
                                                     PV-DISPLAY-COUNT.          
           DISPLAY SPACES.                                                      
                                                                                
      ******************************************************************04850020
      * 9998-ABEND.                                                     04860020
      *    - ABEND ROUTINE MISCELLANEOUS ERRORS                         04870020
      ******************************************************************04880020
       9998-ABEND.                                                      07730094
                                                                                
           PERFORM 9200-DISPLAY-TOTALS.                                         
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '** ABEND **'.                                       04910020
           DISPLAY '** IMKUP064 - ABEND'.                               04920020
           DISPLAY '** PARAGRAPH **      = ' PV-PARAGRAPH-NAME.         07780094
           DISPLAY '** DEPARTMENT **     = ' MDSEAR-DEPT-NBR.           07780094
           DISPLAY '** ABEND MESSAGE **  = ' PV-ABEND-MESSAGE.                  
           DISPLAY ' '.                                                         
           CALL 'ILBOABN0' USING ABEND-CODE.                            07800094
                                                                                
                                                                                
      ******************************************************************        
      *  PERFORMS ABEND PROCESSING FOR SQL ERRORS.                              
      ******************************************************************        
                                                                                
       9999-SQL-ABEND.                                                          
                                                                                
           PERFORM 9200-DISPLAY-TOTALS.                                         
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '9999-SQL-ABEND'                                             
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = IMKUP064'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
           DISPLAY ' '.                                                         
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
