000100*-----------------------*                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*-----------------------*                                                 
000400 PROGRAM-ID.    IQKCS409.                                                 
000500 AUTHOR.        PAUL OMAN.                                                
000600 INSTALLATION.  KOHLS.                                                    
000700 DATE-WRITTEN.  07-16-03.                                                 
000800 DATE-COMPILED.                                                           
000900                                                                          
001000*---------------------------------------------------------------*         
001100*    I409 - STYLE LIST.                                         *         
001200*                                                               *         
001210*    THIS SCREEN IS ONLY ACCESSED FROM IQLDEPT (IQKCS404)       *         
001220*        (DEPARTMENT LIST).  IT CAN DISPLAY THE DATA IN TWO     *         
001300*        DIFFERENT WAYS.                                        *         
001301*                                                               *         
001302*        1 - DISPLAY A LIST FOR A SPECIFIC VENDOR LABEL /       *         
001303*            VENDOR NAME AND BUSINESS GROUP THAT WAS SELECTED   *         
001304*            ON IQKCS402 (IQLVEND).                             *         
001305*                                                               *         
001306*        2 - DISPLAY A LIST FOR ALL VENDORS.                    *         
001307*                                                               *         
001308*    ALLOWS THE USER TO SELECT A STYLE AND PASS IT ALONG WITH   *         
001309*    THE VENDOR NAME (IF SUPPLIED) TO ONE OF THE FOLLOWING      *         
001310*    SCREENS.                                                   *         
001311*                                                               *         
001312*        F17 - IQLSKU   (IQKCS408)                              *         
001313*              FORMAT CODE IS '5'                               *         
001314*        F19 - IQLCOLOR (IQKCS406) - SELECT A STYLE             *         
001315*              FORMAT CODE IS '2'                               *         
001320*                                                               *         
002200*---------------------------------------------------------------*         
002210*---------------------------------------------------------------*         
002220* DATE:        09/14/2021                                                 
002230* WR/IR#:      CHG0261678                                                 
002240* PROGRAMMER:  GERMAN BANDA                                               
002250* DESCRIPTION: CHANGE CALL OF DPKUT100 TO                                 
002260*              CALL DP010I-NUMERIC-EDIT-ROUTINE                           
002270*              MAKING THE CALL DYNAMIC VS STATIC                          
002290*---------------------------------------------------------------*         
CICS  * CHANGED 11/01/2022    BY JIM HUNTER      WR: CHG0277565                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  ******************************************************************        
002300                                                                          
002400 ENVIRONMENT DIVISION.                                                    
002500                                                                          
002600 DATA DIVISION.                                                           
002700                                                                          
002800 WORKING-STORAGE SECTION.                                                 
002900 01  DK-DB2-KEYS.                                                         
003000     15  DK-GP-NBR               PIC S9(03) COMP-3  VALUE 0.              
003100     15  DK-DEPT-NBR             PIC S9(04) COMP-3  VALUE 0.              
003300                                                                          
003400 01  PC-PROGRAM-CONSTANTS.                                                
003500     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
003600                                                    'IQ409A  '.           
003700     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
003800                                                    'IQKM409 '.           
003900     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
004000                                                    'IQKCS409'.           
004100     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
004200     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
004300     05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE ' '.            
004400     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +11             
004500                                                    COMP SYNC.            
004600     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
004700                                                    COMP SYNC.            
004800     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
004900                                                    +1000000              
005000                                                    COMP-3.               
005100     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
005200                                                    COMP SYNC.            
005300     05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
005400                                                    COMP-3.               
005500                                                                          
005600     05  PC-TSYMSG-NUMBERS.                                               
005700         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
005800         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
005900         10  PC-TSYMSG-00047            PIC  9(05)  VALUE 00047.          
006000         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
006100         10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
006200         10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
006300         10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
006400         10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
006500         10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
006600         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
006700         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
006800         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
006900         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
007000         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
007100                                                                          
007200 01  PS-PROGRAM-SWITCHES.                                                 
007300     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
007400         88  PS-NO-ERROR                            VALUE 'Y'.            
007500         88  PS-ERROR                               VALUE 'N'.            
007600     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
007700         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
007800         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
007900     05  PS-CURSOR-SW                   PIC  X(01)  VALUE ' '.            
008000         88  PS-CSR-STYLE-VENDOR                    VALUE 'A'.            
008100         88  PS-CSR-STYLE-NO-VENDOR                 VALUE 'B'.            
008200     05  PS-COUNT-CURSOR-SW             PIC  X(01)  VALUE ' '.            
008300         88  PS-CNT-STYLE-VENDOR                    VALUE 'A'.            
008400         88  PS-CNT-STYLE-LABEL                     VALUE 'B'.            
008500         88  PS-CNT-STYLE-NO-VENDOR                 VALUE 'C'.            
008600                                                                          
008700 01  PV-PROGRAM-VARIABLES.                                                
008800     05  PV-CURSOR-NAME                 PIC X(18)   VALUE SPACES.         
008810     05  PV-SUB                         PIC S9(04)  VALUE +0              
008900                                                    COMP SYNC.            
009000     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
009100                                                    COMP SYNC.            
009200     05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
009300                                                    COMP-3.               
009400     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
009500                                                    COMP-3.               
009600     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
009700                                                    COMP-3.               
009800     05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
009900                                                    COMP-3.               
010000     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
010100                                                    COMP-3.               
010200     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
010300                                                    COMP-3.               
010400     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
010500                                                    COMP SYNC.            
010600     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
010700                                                    COMP SYNC.            
010800     05  PV-ITEM                        PIC S9(04)  VALUE +0              
010900                                                    COMP SYNC.            
011000     05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
011100                                                    COMP SYNC.            
011200     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
011300                                                    COMP SYNC.            
011400     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
011500                                                    COMP SYNC.            
011600     05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
011700                                                    COMP SYNC.            
011800     05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
011900                                                    COMP SYNC.            
012000     05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
012100                                                    COMP SYNC.            
012200     05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
012300                                                    COMP SYNC.            
012400     05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
012500                                                    COMP SYNC.            
012600     05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
012700                                                    COMP SYNC.            
012800     05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
012900                                                    COMP SYNC.            
013000     05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
013100                                                    COMP SYNC.            
013200     05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
013300                                                    COMP SYNC.            
013400     05  PV-SKU-CNT                     PIC S9(09)  VALUE +0              
013500                                                    COMP-3.               
013600     05  PV-BOX                         PIC S9(09)  VALUE +0              
013700                                                    COMP-3.               
013800******************************************************************        
013900**  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
014000**  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **        
014100**  APPLICATION-SPECIFIC COMMAREA.                              **        
014200******************************************************************        
014300 01  LQW-LIST-QUEUE-WORK-AREA.                                            
014400     05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
014500     05  LQW-ITEM-ENTRIES               OCCURS 11 TIMES                   
014600                                        INDEXED BY LQW-IDX.               
014700         10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
014800         10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
014900         10  LQW-SELECTED-SW            PIC  X(01).                       
015000             88  LQW-SELECT                         VALUE 'S'.            
015100             88  LQW-DESELECT                       VALUE ' '.            
015200         10  LQW-STYLE                  PIC  X(20).                       
015300         10  LQW-STYLE-DESC             PIC  X(20).                       
015400         10  LQW-STYLE-BOX              PIC  X(04).                       
015410         10  LQW-STYLE-ID               PIC  9(08).                       
015500                                                                          
015600                                                                          
015700*---------------------------------------------------------------*         
015800*  RECORD LAYOUT FOR THE SELECTION TEMP STORAGE QUEUE.          *         
015900*---------------------------------------------------------------*         
016000     COPY IQWS014.                                                        
016100                                                                          
016200*---------------------------------------------------------------*         
016300*    MAP LAYOUT                                                 *         
016400*---------------------------------------------------------------*         
016500                                                                          
016600     COPY IQKM409.                                                        
016700                                                                          
016800 01  MR-OCCURS-LAYOUT                   REDEFINES  IQ409AO.               
016900     05  FILLER                         PIC X(161).                       
017000     05  MR-SCROLL-AREA                 OCCURS 11 TIMES.                  
017100         10  MR-SELECTL                 PIC S9(04) COMP.                  
017200         10  MR-SELECTA                 PIC  X(01).                       
017300         10  MR-SELECTC                 PIC  X(01).                       
017400         10  MR-SELECTH                 PIC  X(01).                       
017500         10  MR-SELECTI                 PIC  X(01).                       
017600                                                                          
017700         10  MR-STYLE-NBRL              PIC S9(04) COMP.                  
017800         10  MR-STYLE-NBRA              PIC  X(01).                       
017900         10  MR-STYLE-NBRC              PIC  X(01).                       
018000         10  MR-STYLE-NBRH              PIC  X(01).                       
018100         10  MR-STYLE-NBR               PIC  X(20).                       
018200                                                                          
018300         10  MR-STYLE-DESCL             PIC S9(04) COMP.                  
018400         10  MR-STYLE-DESCA             PIC  X(01).                       
018500         10  MR-STYLE-DESCC             PIC  X(01).                       
018600         10  MR-STYLE-DESCH             PIC  X(01).                       
018700         10  MR-STYLE-DESC              PIC  X(20).                       
018800                                                                          
018900         10  MR-STYLE-BOXL              PIC S9(04) COMP.                  
019000         10  MR-STYLE-BOXA              PIC  X(01).                       
019100         10  MR-STYLE-BOXC              PIC  X(01).                       
019200         10  MR-STYLE-BOXH              PIC  X(01).                       
019300         10  MR-STYLE-BOX               PIC  X(04).                       
019400                                                                          
019500*---------------------------------------------------------------*         
019600*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
019700*---------------------------------------------------------------*         
019800                                                                          
019900     COPY DPWS015.                                                        
020000                                                                          
020100*---------------------------------------------------------------*         
020200*    FUNCTION KEYS COPYBOOK.                                    *         
020300*---------------------------------------------------------------*         
020400                                                                          
020500     COPY DPWS016.                                                        
020600                                                                          
020700*---------------------------------------------------------------*         
020800*    NUMERIC EDIT LAYOUT.                                       *         
020900*---------------------------------------------------------------*         
021000                                                                          
021100     COPY DPWS010I.                                                       
021200                                                                          
021300*---------------------------------------------------------------*         
021400*    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
021500*---------------------------------------------------------------*         
021600                                                                          
021700     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
021800                                                                          
021900*---------------------------------------------------------------*         
022000*    STANDARD COMMAREA.                                         *         
022100*---------------------------------------------------------------*         
022200                                                                          
022300     COPY DPWS020.                                                        
022400     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
022500*---------------------------------------------------------------*         
022600* INTER-APPLICATION COMMAREA.                                   *         
022700*   IQWS016 - IS THE INPUT LAYOUT WHEN COMING FROM THE PREVIOUS *         
022800*             PROGARM.                                          *         
022900*   IQWS016 - IS THE OUTPUT LAYOUT THAT WILL BE PASSED FROM     *         
023000*             THIS LIST PROGRAM.                                *         
023100*---------------------------------------------------------------*         
023200                                                                          
023300         10  INTER-APPL-COMM-AREA       PIC X(200).                       
023400     COPY IQWS016.                                                        
023500*---------------------------------------------------------------*         
023600*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
023700*        CURRENT LENGTH IS 1300 FOR IQLSTYLE MNEMONIC.                    
023800*        YOU MUST CHECK THE COMMAREA LENGTH FOR THE MNEMONIC              
023900*            IF YOU ADD ANY MORE FIELDS.                                  
024000*---------------------------------------------------------------*         
024100         10  ASC-SPECIFIC-COMMAREA.                                       
024200             15  ASC-LIST-KEYS.                                           
024300                 20  ASC-LBL-NM                      PIC  X(30).          
024400                 20  ASC-GP-NBR                      PIC  9(03).          
024500                 20  ASC-GP-DESC                     PIC  X(25).          
024600                 20  ASC-DEPT-NBR                    PIC  9(04).          
024700                 20  ASC-DEPT-DESC                   PIC  X(30).          
024800                 20  ASC-SKU-CNT                     PIC  9(9).           
024900             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
025000                 20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
025100                     88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
025200                     88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
025300                                                                          
025400                 20  ASC-ITEM-ARRAY.                                      
025500                     25  ASC-ITEM-ENTRIES OCCURS 11 TIMES.                
025600                         30  ASC-LIST-ITEM-NUMBER                         
025700                                                     PIC S9(09)           
025800                                                     COMP-3.              
025900                         30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
026000                             88  ASC-VALID-SELECT    VALUE ' '            
026100                                                           'S'            
026200                                                           'B'            
026300                                                           'E'.           
026400                             88  ASC-TEMP-NO-SELECT  VALUE ' '.           
026500                             88  ASC-TEMP-SELECT     VALUE 'S'.           
026600                             88  ASC-TEMP-BEGIN      VALUE 'B'.           
026700                             88  ASC-TEMP-END        VALUE 'E'.           
026800                         30  ASC-SELECTED-SW         PIC  X(01).          
026900                             88  ASC-NO-SELECT       VALUE ' '.           
027000                             88  ASC-SELECT          VALUE 'S'.           
027100                             88  ASC-BEGIN           VALUE 'B'.           
027200                             88  ASC-END             VALUE 'E'.           
027300                         30  ASC-STYLE-NBR           PIC  X(20).          
027400                         30  ASC-STYLE-DESC          PIC  X(20).          
027500                         30  ASC-STYLE-BOX           PIC  9(04).          
027510                         30  ASC-STYLE-ID            PIC  9(08).          
027600                                                                          
027700             15  ASC-BLK-SUB                         PIC S9(04)           
027800                                                          COMP            
027900                                                          SYNC.           
028000             15  ASC-ITEM-SUB                        PIC S9(04)           
028100                                                          COMP            
028200                                                          SYNC.           
028300             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
028400                                                          COMP-3.         
028500             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
028600                                                          COMP-3.         
028700             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
028800                                                          COMP-3.         
028900             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
029000                                                          COMP-3.         
029100             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
029200                                                          COMP-3.         
029300             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
029400                                                          COMP            
029500                                                          SYNC.           
029600             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
029700                                                          COMP-3.         
029800             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
029900                                                          COMP-3.         
030000             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
030100                 88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
030200                 88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
030300             15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
030400                 88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
030500                 88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
030600             15  ASC-LIST-QUEUE-NAME.                                     
030700                 20  ASC-LIST-TERM-ID                PIC  X(04).          
030800                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
030900                                                          COMP-3.         
031000                 20  ASC-LIST-SEQ                    PIC  9(01).          
031100             15  ASC-SEL-QUEUE-NAME.                                      
031200                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
031300                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
031400                                                          COMP-3.         
031500                 20  ASC-SEL-SEQ                     PIC  9(01).          
031600             15  ASC-START-ITEM-REQUEST.                                  
031700                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
031800             15  ASC-FILLER                          PIC  X(14).          
031900         10  FILLER                                  PIC  X(1389).        
032000                                                                          
032100*---------------------------------------------------------------*         
032200*    ABEND PROCESSING COPYBOOK.                                 *         
032300*---------------------------------------------------------------*         
032400                                                                          
032500     COPY DPWS013.                                                        
032600                                                                          
032700                                                                          
032800*---------------------------------------------------------------*         
032900*    DB2 AREA FOR XST_SKU                                       *         
033000*---------------------------------------------------------------*         
033100     EXEC SQL                                                             
033200          INCLUDE XST00010                                                
033300     END-EXEC.                                                            
033310*---------------------------------------------------------------*         
033320*    DB2 AREA FOR XST_VND_STYL                                  *         
033330*---------------------------------------------------------------*         
033340     EXEC SQL                                                             
033350          INCLUDE XST00013                                                
033360     END-EXEC.                                                            
033400*---------------------------------------------------------------*         
033500*    DB2 AREA FOR XST_DEPT                                      *         
033600*---------------------------------------------------------------*         
033700     EXEC SQL                                                             
033800          INCLUDE XST00014                                                
033900     END-EXEC.                                                            
034000*---------------------------------------------------------------*         
034100*    DB2 AREA FOR XST_DEPT                                      *         
034200*---------------------------------------------------------------*         
034300     EXEC SQL                                                             
034400          INCLUDE XST00015                                                
034500     END-EXEC.                                                            
034600*---------------------------------------------------------------*         
034700*    DB2 AREA FOR XST_BUS_GP                                    *         
034800*---------------------------------------------------------------*         
034900     EXEC SQL                                                             
035000          INCLUDE XST00016                                                
035100     END-EXEC.                                                            
035200*---------------------------------------------------------------*         
035300*    DB2 AREA FOR XST_VND                                       *         
035400*---------------------------------------------------------------*         
035500     EXEC SQL                                                             
035600          INCLUDE XST00018                                                
035700     END-EXEC.                                                            
035800*---------------------------------------------------------------*         
035900*    DB2 AREA FOR COMMUNICATIONS                                *         
036000*---------------------------------------------------------------*         
036100     EXEC SQL                                                             
036200          INCLUDE SQLCA                                                   
036300     END-EXEC.                                                            
036400                                                                          
036500*---------------------------------------------------------------*         
036700*    FETCH THE STYLE INFORMATION FOR A SPECIFIC VENDOR                    
036900*---------------------------------------------------------------*         
037000                                                                          
037100     EXEC SQL                                                             
037200          DECLARE CSR-STYLE-VENDOR CURSOR FOR                             
037300                                                                          
037400            SELECT                                                        
037500                   VNDSTYL.DEPT_NBR                                       
037600                  ,VNDSTYL.STYL_ID                                        
037610                  ,VNDSTYL.VS_NBR                                         
037700                  ,VNDSTYL.STKR_NBR                                       
037800                  ,SUBCL.SUB_CL_NM                                        
037900                                                                          
038000              FROM XST_VND               VND                              
038100                  ,XST_VND_STYL          VNDSTYL                          
038200                  ,XST_SUB_CL            SUBCL                            
038300                                                                          
038400             WHERE  VND.ENT_NM_DESC     = :ASC-LBL-NM                     
038500               AND  VNDSTYL.DEPT_NBR    = :DK-DEPT-NBR                    
038600               AND  VNDSTYL.DEPT_NBR    = SUBCL.DEPT_NBR                  
038700               AND  VNDSTYL.MAJ_CL_NBR  = SUBCL.MAJ_CL_NBR                
038800               AND  VNDSTYL.SUB_CL_NBR  = SUBCL.SUB_CL_NBR                
038900               AND  VND.ENT_ID          = VNDSTYL.ENT_ID                  
039000       UNION                                                              
039100            SELECT                                                        
039200                   VNDSTYL.DEPT_NBR                                       
039210                  ,VNDSTYL.STYL_ID                                        
039300                  ,VNDSTYL.VS_NBR                                         
039400                  ,VNDSTYL.STKR_NBR                                       
039500                  ,SUBCL.SUB_CL_NM                                        
039600                                                                          
039700              FROM XST_VND_STYL          VNDSTYL                          
039800                  ,XST_SUB_CL            SUBCL                            
039900                                                                          
040000             WHERE  VNDSTYL.LBL_NM      = :ASC-LBL-NM                     
040100               AND  VNDSTYL.DEPT_NBR    = :DK-DEPT-NBR                    
040200               AND  VNDSTYL.DEPT_NBR    = SUBCL.DEPT_NBR                  
040300               AND  VNDSTYL.MAJ_CL_NBR  = SUBCL.MAJ_CL_NBR                
040400               AND  VNDSTYL.SUB_CL_NBR  = SUBCL.SUB_CL_NBR                
040500                                                                          
040600          ORDER BY  3, 2                                                  
040700                                                                          
040800     END-EXEC.                                                            
040810                                                                          
040820*---------------------------------------------------------------*         
040830*    FETCH THE STYLE INFORMATION FOR ALL VENDORS                          
040840*---------------------------------------------------------------*         
040900                                                                          
041000     EXEC SQL                                                             
041100          DECLARE STYLE-NOVND CURSOR FOR                                  
041200                                                                          
041300            SELECT                                                        
041400                   VNDSTYL.DEPT_NBR                                       
041500                  ,VNDSTYL.STYL_ID                                        
041510                  ,VNDSTYL.VS_NBR                                         
041600                  ,VNDSTYL.STKR_NBR                                       
041700                  ,SUBCL.SUB_CL_NM                                        
041800                                                                          
041900              FROM XST_VND_STYL          VNDSTYL                          
042000                  ,XST_SUB_CL            SUBCL                            
042100                                                                          
042200             WHERE  VNDSTYL.DEPT_NBR    = :DK-DEPT-NBR                    
042300               AND  VNDSTYL.DEPT_NBR    = SUBCL.DEPT_NBR                  
042400               AND  VNDSTYL.MAJ_CL_NBR  = SUBCL.MAJ_CL_NBR                
042500               AND  VNDSTYL.SUB_CL_NBR  = SUBCL.SUB_CL_NBR                
042600          ORDER BY  3, 2                                                  
042700                                                                          
042800     END-EXEC.                                                            
042900                                                                          
043000*   COUNT AVAILABLE SKU'S IF KNOW THE VENDOR NAME AND DEPT                
043100     EXEC SQL                                                             
043200        DECLARE CNT-STYLE-VND CURSOR FOR                                  
043300           SELECT  COUNT(*)                                               
043400             FROM  XST_VND_STYL  D,                                       
043500                   XST_VND       E,                                       
043600                   XST_SKU       F                                        
043700            WHERE  E.ENT_NM_DESC = :ASC-LBL-NM                            
043800              AND  D.DEPT_NBR    = :DK-DEPT-NBR                           
043900              AND  D.STYL_ID     = F.STYL_ID                              
044000              AND  D.ENT_ID      = E.ENT_ID                               
044100     END-EXEC.                                                            
044200                                                                          
044300*   COUNT AVAILABLE SKU'S IF KNOW THE LABEL NAME AND DEPT                 
044400     EXEC SQL                                                             
044500        DECLARE CNT-STYLE-LBL CURSOR FOR                                  
044600           SELECT  COUNT(*)                                               
044700             FROM  XST_VND_STYL  D,                                       
044800                   XST_SKU       F                                        
044900            WHERE  D.LBL_NM     = :ASC-LBL-NM                             
045000              AND  D.DEPT_NBR   = :DK-DEPT-NBR                            
045100              AND  D.STYL_ID    =  F.STYL_ID                              
045200     END-EXEC.                                                            
045300                                                                          
045310*   COUNT AVAILABLE SKU'S FOR A DEPARTMENT                                
045400     EXEC SQL                                                             
045500        DECLARE CNT-STYLE-NOVND CURSOR FOR                                
045600           SELECT  COUNT(*)                                               
045700             FROM  XST_VND_STYL  D,                                       
045800                   XST_SKU       F                                        
045900            WHERE  D.DEPT_NBR    = :DK-DEPT-NBR                           
046000              AND  D.STYL_ID    =  F.STYL_ID                              
046100     END-EXEC.                                                            
046200                                                                          
046300                                                                          
046400 LINKAGE SECTION.                                                         
046500                                                                          
046600 01  DFHCOMMAREA.                                                         
046700     05  FILLER                         OCCURS  1 TO 4072 TIMES           
046800                                        DEPENDING ON EIBCALEN.            
046900         10  FILLER                     PIC X.                            
047000                                                                          
047100                                                                          
047200 PROCEDURE DIVISION.                                                      
047300******************************************************************        
047400**  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
047500**  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
047600**  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
047700******************************************************************        
047800 0000-MAIN-MODULE.                                                        
047900     INITIALIZE DP030-CICS-API-FIELDS.                                    
048000     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
048100     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
048200     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
048300     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
048400     MOVE LENGTH OF IQ409AI        TO DP030-MAP-LENGTH (1).               
048500     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
048600     PERFORM 0001-CALL-CICS-ARCH-API.                                     
048700*                                                                         
048800     PERFORM 1000-CONTROL-PROCESSING                                      
048900*                                                                         
049000     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
049100     PERFORM 0001-CALL-CICS-ARCH-API.                                     
049200                                                                          
049300******************************************************************        
049400**  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
049500**  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
049600******************************************************************        
049700 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
049900                                    DFHCOMMAREA                           
050000                                    DP030-CICS-API-FIELDS                 
050100                                    DP020-STANDARD-COMMAREA               
050200                                    IQ409AI.                              
050300*                                                                         
050400     IF  DP030-RC-CALL-SUCCESSFUL                                         
050500         CONTINUE                                                         
050600     ELSE                                                                 
050700         SET DP013-NO-ROLLBACK                                            
050800             DP013-XCTL-DISPLAY-RESTART                                   
050900             DP013-CICS-ABEND      TO TRUE                                
051000         MOVE '0001-CALL-CICS-ARCH-API'                                   
051100                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
051400         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
051500         PERFORM DP013-0000-PROCESS-ABEND                                 
051600     END-IF.                                                              
051700*----------------------------------------------------------------*        
051800* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
051900* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
052000*                                                                *        
052100* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
052200*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
052300*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
052400*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
052500*----------------------------------------------------------------*        
052600                                                                          
052700 1000-CONTROL-PROCESSING.                                                 
052800                                                                          
052900     EVALUATE TRUE                                                        
053000         WHEN DP020-NEXT-ACT-INITIAL                                      
053100             INITIALIZE ASC-SPECIFIC-COMMAREA                             
053200             MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
053300                                      ASC-SEL-TERM-ID                     
053400             MOVE DP020-SRC-TASK-NUMBER                                   
053500                                   TO ASC-LIST-TASK-NUMBER                
053600                                      ASC-SEL-TASK-NUMBER                 
053700             MOVE PC-LIST-QUEUE-SEQ                                       
053800                                   TO ASC-LIST-SEQ                        
053900             MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
054000             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
054100***-----$TEMPLATE NOTE$-------------------------------------------        
054200*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
054300*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
054400*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
054500***---------------------------------------------------------------        
054600             IF  PS-COMMAREA-NOT-VALID                                    
054700                 SET DP020-NEXT-ACT-APPL-ERROR                            
054800                                   TO TRUE                                
054900             ELSE                                                         
055000                 PERFORM 4000-BUILD-INITIAL-PANEL                         
055100             END-IF                                                       
055200                                                                          
055300         WHEN DP020-NEXT-ACT-READ-MAP                                     
055400             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
055500                                   TO PV-TOP-ITEM                         
055600             PERFORM 2000-PROCESS-PANEL                                   
055700                                                                          
055800         WHEN DP020-NEXT-ACT-RETURN                                       
055900             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
056000                                   TO PV-TOP-ITEM                         
056100             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
056200             PERFORM 6500-REINIT-ASC-FLDS                                 
056300             PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
056500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
056600                                                                          
056700         WHEN OTHER                                                       
056800             SET DP013-LOGIC-ABEND TO TRUE                                
056900             SET DP013-NO-ROLLBACK TO TRUE                                
057000             MOVE '1000-CONTROL-PROCESSING'                               
057100                                   TO DP013-PARAGRAPH                     
057200             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
057300                                   TO DP013-MESSAGE-TEXT(1)               
057400             PERFORM DP013-0000-PROCESS-ABEND                             
057500     END-EVALUATE.                                                        
057600                                                                          
057700                                                                          
057800*----------------------------------------------------------------*        
057900* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
058000* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
058100*----------------------------------------------------------------*        
058200                                                                          
058300 1100-PROCESS-INTER-APPL-COMM.                                            
058400     SET PS-COMMAREA-VALID         TO TRUE.                               
058500     MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
058600     IF  DP020-INT-APPL-FORMAT-IND = PC-CRIT-SPEC-FORMAT                  
058700         MOVE IQ016-GP-NBR       TO ASC-GP-NBR                            
058800                                     DK-GP-NBR                            
058900         MOVE IQ016-GP-DESC      TO ASC-GP-DESC                           
059000         MOVE IQ016-LBL-NM       TO ASC-LBL-NM                            
059200         MOVE IQ016-DEPT-NBR     TO ASC-DEPT-NBR                          
059300                                     DK-DEPT-NBR                          
059400         MOVE IQ016-DEPT-DESC    TO ASC-DEPT-DESC                         
059500     ELSE                                                                 
059600         IF  DP020-NAV-KEY-DATA > SPACE                                   
059700             MOVE DP020-NAV-KEY-DATA                                      
059800                                   TO ASC-GP-NBR                          
059900                                      DK-GP-NBR                           
060000                                      ASC-LBL-NM                          
060200         END-IF                                                           
060300     END-IF.                                                              
060400                                                                          
060500                                                                          
060600*----------------------------------------------------------------*        
060700* DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
060800* ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
060900* USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
061000* OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
061100* FUNCTION KEYS CAN GET THIS FAR.                                *        
061200*----------------------------------------------------------------*        
061300                                                                          
061400 2000-PROCESS-PANEL.                                                      
061500     EVALUATE TRUE                                                        
061600         WHEN DP020-SRC-AID = DP016-CLEAR                                 
061700             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
061800             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
061900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
062000                                                                          
062100         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
062200             MOVE ASC-GP-NBR TO DK-GP-NBR                                 
062400             PERFORM 2050-DELETE-LIST-QUEUE                               
062500             PERFORM 4000-BUILD-INITIAL-PANEL                             
062600                                                                          
062700         WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
062800             PERFORM 2140-DESELECT-ALL-ITEMS                              
062900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
063000                                                                          
063100         WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
063200             PERFORM 2150-SELECT-ALL-ITEMS                                
063300             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
063400                                                                          
063500         WHEN OTHER                                                       
063600             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
063700             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
063800             IF  PS-NO-ERROR                                              
063900                 SET DP030-CONTINUE-GO                                    
064000                                 TO TRUE                                  
064100                 PERFORM 2100-CHECK-FUNCTION-KEY                          
064200             ELSE                                                         
064300                 SET DP030-OVERRIDE-GO                                    
064400                                 TO TRUE                                  
064500             END-IF                                                       
064600     END-EVALUATE.                                                        
064700                                                                          
064800*----------------------------------------------------------------*        
064900*  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
065000*----------------------------------------------------------------*        
065100 2050-DELETE-LIST-QUEUE.                                                  
065200                                                                          
065300     EXEC CICS                                                            
065400         DELETEQ TS                                                       
065500             QUEUE (ASC-LIST-QUEUE-NAME)                                  
065600             RESP  (PV-CICS-RESP)                                         
065700     END-EXEC.                                                            
065800                                                                          
065900     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
066000        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
066100         CONTINUE                                                         
066200     ELSE                                                                 
066300         SET DP013-CICS-ABEND      TO TRUE                                
066400         SET DP013-NO-ROLLBACK     TO TRUE                                
066500                                                                          
066600         MOVE '2050-DELETE-LIST-QUEUE'                                    
066700                                   TO DP013-PARAGRAPH                     
066800         MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
066900                                   TO DP013-MESSAGE-TEXT (1)              
067000         PERFORM DP013-0000-PROCESS-ABEND                                 
067100     END-IF.                                                              
067200                                                                          
067300     MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
067400                                                                          
067500*----------------------------------------------------------------*        
067600* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
067700* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
067800* CICS ARCHITECTURE API.                                         *        
067900*----------------------------------------------------------------*        
068000 2100-CHECK-FUNCTION-KEY.                                                 
068100     EVALUATE TRUE                                                        
068200*        LOCAL-01   CLASSLST     IQLCLASS                                 
068300*        LOCAL-02   COLORLST     IQLCOLOR                                 
068400         WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
068500         WHEN DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID)                       
068600             IF  ASC-NUMBER-OF-SELECTED-ITEMS = ZERO                      
068700*                --  A LIST ITEM MUST BE SELECTED --                      
068800                 MOVE PC-TSYMSG-00057                                     
068900                                   TO DP020-MSG-NUMBER                    
069000                 SET DP020-MSG-FATAL                                      
069100                     DP030-OVERRIDE-GO                                    
069200                                   TO TRUE                                
069300             ELSE                                                         
069400               IF  ASC-NUMBER-OF-SELECTED-ITEMS > 1                       
069500*                --  ONLY ONE LIST ITEM MAY BE SELECTED --                
069600                   MOVE PC-TSYMSG-00047                                   
069700                                   TO DP020-MSG-NUMBER                    
069800                   SET DP020-MSG-FATAL                                    
069900                       DP030-OVERRIDE-GO                                  
070000                                   TO TRUE                                
070100               ELSE                                                       
070200                 MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
070300                                TO IQ016-NUMBER-OF-SELECTED-ITEMS         
070400                 PERFORM 5000-PREPARE-SEL-QUEUE                           
070500               END-IF                                                     
070600             END-IF                                                       
070700                                                                          
070800         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
070900             MOVE +1               TO PV-TOP-ITEM                         
071000             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
071100                                                                          
071200         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
071300             PERFORM 2110-BROWSE-BACKWARD                                 
071400             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
071500                                                                          
071600         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
071700             PERFORM 2120-BROWSE-FORWARD                                  
071800             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
071900                                                                          
072000         WHEN DP020-SRC-AID = DP016-ENTER                                 
072100             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
072200             IF  PS-ERROR                                                 
072300                 CONTINUE                                                 
072400             ELSE                                                         
072500                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
072600             END-IF                                                       
072700                                                                          
072800         WHEN OTHER                                                       
072900             SET DP013-NO-ROLLBACK                                        
073000                 DP013-XCTL-DISPLAY-RESTART                               
073100                 DP013-CICS-ABEND  TO TRUE                                
073200             MOVE '2100-CHECK-FUNCTION-KEY'                               
073300                                   TO DP013-PARAGRAPH                     
073400             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
073500                                   TO DP013-MESSAGE-TEXT (1)              
073600             PERFORM DP013-0000-PROCESS-ABEND                             
073700     END-EVALUATE.                                                        
073800*                                                                         
073900*----------------------------------------------------------------*        
074000*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
074100*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
074200*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
074300*----------------------------------------------------------------*        
074400                                                                          
074500 2110-BROWSE-BACKWARD.                                                    
074600     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
074700         GIVING PV-TOP-ITEM.                                              
074800*                                                                         
074900     IF  ((PV-TOP-ITEM < +1)                                              
075000       OR (PV-TOP-ITEM = +1))                                             
075100         MOVE +1                   TO PV-TOP-ITEM                         
075200*        -- TOP OF LIST --                                                
075300         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
075400         SET DP020-MSG-INFORMATIONAL TO TRUE                              
075500     END-IF.                                                              
075600                                                                          
075700                                                                          
075800*----------------------------------------------------------------*        
075900*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
076000*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
076100*    PAST THE END, ISSUE THE BOTTOM OF LIST                      *        
076200*----------------------------------------------------------------*        
076300                                                                          
076400 2120-BROWSE-FORWARD.                                                     
076500                                                                          
076600     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
076700         GIVING PV-TOP-ITEM.                                              
076800     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
076900         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
077000                                   TO PV-TOP-ITEM                         
077100*        -- BOTTOM OF LIST --                                             
077200         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
077300         SET DP020-MSG-INFORMATIONAL                                      
077400                                   TO TRUE                                
077500     ELSE                                                                 
077600         COMPUTE PV-BOTTOM-ITEM =                                         
077700             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
077800         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
077900*            -- BOTTOM OF LIST --                                         
078000             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
078100             SET DP020-MSG-INFORMATIONAL                                  
078200                                   TO TRUE                                
078300         END-IF                                                           
078400     END-IF.                                                              
078500                                                                          
078600*----------------------------------------------------------------*        
078700*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
078800*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
078900*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
079000*    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
079100*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
079200*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
079300*----------------------------------------------------------------*        
079400                                                                          
079500 2130-CHECK-FOR-JUMP-BROWSE.                                              
079600     SET PS-NO-ERROR               TO TRUE.                               
079700     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
079800         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
079900                                   TO PV-TOP-ITEM                         
080000     ELSE                                                                 
080100         MOVE ASC-START-ITEM-REQUEST                                      
080200                                   TO DP010I-UNEDITED-FIELD               
080300         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
080400         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
080500         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
080600                                   TO TRUE                                
080710         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
080720                         USING DP010I-NUMERIC-EDIT-AREA                   
080800                                                                          
080900         IF  NOT DP010I-ERROR-DETECTED                                    
081000             MOVE DP010I-NUMERIC-FIELD                                    
081100                                   TO  ASC-START-ITEM-REQUEST-N           
081200             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
081300                 IF  ASC-START-ITEM-REQUEST-N >                           
081400                                       ASC-NUMBER-OF-ITEMS-READ           
081500                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
081600                     COMPUTE PV-TOP-ITEM =                                
081700                             ASC-NUMBER-OF-ITEMS-READ                     
081800                             - PC-ITEMS-PER-PANEL + 1                     
081900                     IF  PV-TOP-ITEM < +1                                 
082000                         MOVE +1   TO PV-TOP-ITEM                         
082100                         MOVE PC-TSYMSG-00070                             
082200                                   TO DP020-MSG-NUMBER                    
082300                         SET DP020-MSG-INFORMATIONAL                      
082400                                   TO TRUE                                
082500                     END-IF                                               
082600                 ELSE                                                     
082700                     MOVE ASC-START-ITEM-REQUEST-N                        
082800                                   TO PV-TOP-ITEM                         
082900                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
083000                     COMPUTE PV-BOTTOM-ITEM =                             
083100                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
083200                     IF  PV-BOTTOM-ITEM >                                 
083300                                   ASC-NUMBER-OF-ITEMS-READ               
083400*                        -- BOTTOM OF LIST --                             
083500                         MOVE PC-TSYMSG-00070                             
083600                                   TO DP020-MSG-NUMBER                    
083700                         SET DP020-MSG-INFORMATIONAL                      
083800                                   TO TRUE                                
083900                     END-IF                                               
084000                 END-IF                                                   
084100             ELSE                                                         
084200*                --- VALUE OUT OF RANGE ----                              
084300                 MOVE PC-TSYMSG-00101                                     
084400                                   TO DP020-MSG-NUMBER                    
084500                 MOVE -1           TO ASTRTITL                            
084600                 SET DP030-SET-CURSOR-APPL-1                              
084700                                   TO TRUE                                
084800                 MOVE DP015-REVERSE                                       
084900                                   TO ASTRTITH                            
085000                 MOVE DP015-RED    TO ASTRTITC                            
085100                 MOVE DP015-UNP-NUM-BRT-MDT                               
085200                                   TO ASTRTITA                            
085300                 SET DP020-MSG-FATAL                                      
085400                                   TO TRUE                                
085500                 SET PS-ERROR      TO TRUE                                
085600             END-IF                                                       
085700                                                                          
085800         ELSE                                                             
085900             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
086000             MOVE -1               TO ASTRTITL                            
086100             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
086200             MOVE DP015-REVERSE    TO ASTRTITH                            
086300             MOVE DP015-RED        TO ASTRTITC                            
086400             MOVE DP015-UNP-NUM-BRT-MDT                                   
086500                                   TO ASTRTITA                            
086600             SET DP020-MSG-FATAL   TO TRUE                                
086700             SET PS-ERROR          TO TRUE                                
086800         END-IF                                                           
086900     END-IF.                                                              
087000                                                                          
087100*----------------------------------------------------------------*        
087200*    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
087300*    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
087400*----------------------------------------------------------------*        
087500 2140-DESELECT-ALL-ITEMS.                                                 
087600     MOVE +1                       TO ASC-BLK-SUB.                        
087700                                                                          
087800     PERFORM 2141-CLEAR-SELECTED-INDS                                     
087900         VARYING ASC-ITEM-SUB                                             
088000         FROM 1 BY 1                                                      
088100         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
088200                                                                          
088300     ADD +1                        TO ASC-BLK-SUB.                        
088400                                                                          
088500     PERFORM 2141-CLEAR-SELECTED-INDS                                     
088600         VARYING ASC-ITEM-SUB                                             
088700         FROM 1 BY 1                                                      
088800         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
088900                                                                          
089000     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
089100                                   TO PV-LIST-ITEM-NUMBER.                
089200                                                                          
089300     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
089400         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
089500                                                                          
089600     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
089700        (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
089800         MOVE +1                   TO ASC-BLK-SUB                         
089900                                                                          
090000         PERFORM 5100-WRITE-TEMP-STORAGE                                  
090100                                                                          
090200         MOVE +1                   TO PV-BLOCK-NUMBER                     
090300                                                                          
090400         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
090500             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
090600                                                                          
090700         PERFORM                                                          
090800             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
090900                                                                          
091000             PERFORM 4410-READ-TEMP-STORAGE                               
091100                                                                          
091200             PERFORM 2141-CLEAR-SELECTED-INDS                             
091300                 VARYING ASC-ITEM-SUB FROM 1 BY 1                         
091400                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
091500                                                                          
091600             PERFORM 5100-WRITE-TEMP-STORAGE                              
091700                                                                          
091800             ADD +1                TO PV-BLOCK-NUMBER                     
091900                                                                          
092000         END-PERFORM                                                      
092100                                                                          
092200         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
092300             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
092400                                                                          
092500         PERFORM                                                          
092600             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
092700                   ASC-HIGHEST-BLOCK-WRITTEN                              
092800                                                                          
092900             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
093000                                   TO PV-BLOCK-NUMBER                     
093100                                                                          
093200             PERFORM 4410-READ-TEMP-STORAGE                               
093300                                                                          
093400             PERFORM 2141-CLEAR-SELECTED-INDS                             
093500                 VARYING ASC-ITEM-SUB                                     
093600                 FROM 1 BY 1                                              
093700                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
093800                                                                          
093900             PERFORM 5100-WRITE-TEMP-STORAGE                              
094000                                                                          
094100             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
094200                                                                          
094300         END-PERFORM                                                      
094400                                                                          
094500         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
094600                                   TO PV-BLOCK-NUMBER                     
094700                                                                          
094800         PERFORM 4410-READ-TEMP-STORAGE                                   
094900     END-IF.                                                              
095000                                                                          
095100                                                                          
095200     MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS.         
095300     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.        
095400     MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.          
095500                                                                          
095600     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
095700                                                                          
095800                                                                          
095900*----------------------------------------------------------------*        
096000*    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
096100*----------------------------------------------------------------*        
096200                                                                          
096300 2141-CLEAR-SELECTED-INDS.                                                
096400                                                                          
096500     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
096600             NOT EQUAL ZERO                                               
096700         IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
096800             SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
096900                 ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
097000                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
097100                                   TO TRUE                                
097200         END-IF                                                           
097300     END-IF.                                                              
097400                                                                          
097500                                                                          
097600*----------------------------------------------------------------*        
097700*  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECT INDS      *        
097800*  SWITCH FOR ALL ITEMS.                                         *        
097900*----------------------------------------------------------------*        
098000                                                                          
098100 2150-SELECT-ALL-ITEMS.                                                   
098200                                                                          
098300     MOVE +1                       TO ASC-BLK-SUB.                        
098400                                                                          
098500     PERFORM 2151-SET-SELECTED-INDS                                       
098600         VARYING ASC-ITEM-SUB                                             
098700         FROM 1 BY 1                                                      
098800         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
098900                                                                          
099000     ADD +1                        TO ASC-BLK-SUB.                        
099100                                                                          
099200     PERFORM 2151-SET-SELECTED-INDS                                       
099300         VARYING ASC-ITEM-SUB                                             
099400         FROM 1 BY 1                                                      
099500         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
099600                                                                          
099700     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
099800                                   TO PV-LIST-ITEM-NUMBER.                
099900                                                                          
100000     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
100100         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
100200                                                                          
100300     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
100400        (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
100500         MOVE +1                   TO ASC-BLK-SUB                         
100600                                                                          
100700         PERFORM 5100-WRITE-TEMP-STORAGE                                  
100800                                                                          
100900         MOVE +1                   TO PV-BLOCK-NUMBER                     
101000                                                                          
101100         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
101200             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
101300                                                                          
101400         PERFORM                                                          
101500             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
101600                                                                          
101700             PERFORM 4410-READ-TEMP-STORAGE                               
101800                                                                          
101900             PERFORM 2151-SET-SELECTED-INDS                               
102000                 VARYING ASC-ITEM-SUB                                     
102100                 FROM 1 BY 1                                              
102200                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
102300                                                                          
102400             PERFORM 5100-WRITE-TEMP-STORAGE                              
102500                                                                          
102600             ADD +1                TO PV-BLOCK-NUMBER                     
102700                                                                          
102800         END-PERFORM                                                      
102900                                                                          
103000         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
103100             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
103200                                                                          
103300         PERFORM                                                          
103400             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
103500                   ASC-HIGHEST-BLOCK-WRITTEN                              
103600                                                                          
103700             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
103800                                   TO PV-BLOCK-NUMBER                     
103900                                                                          
104000             PERFORM 4410-READ-TEMP-STORAGE                               
104100                                                                          
104200             PERFORM 2151-SET-SELECTED-INDS                               
104300                 VARYING ASC-ITEM-SUB                                     
104400                 FROM 1 BY 1                                              
104500                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
104600                                                                          
104700             PERFORM 5100-WRITE-TEMP-STORAGE                              
104800                                                                          
104900             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
105000                                                                          
105100         END-PERFORM                                                      
105200                                                                          
105300         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
105400                                   TO PV-BLOCK-NUMBER                     
105500                                                                          
105600         PERFORM 4410-READ-TEMP-STORAGE                                   
105700     END-IF.                                                              
105800                                                                          
105900                                                                          
106000     MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
106100                       ASC-NUMBER-OF-SELECTED-ITEMS.                      
106200                                                                          
106300     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
106400                                                                          
106500                                                                          
106600*----------------------------------------------------------------*        
106700*    MARK EACH ITEM AS BEING SELECTED.                           *        
106800*----------------------------------------------------------------*        
106900                                                                          
107000 2151-SET-SELECTED-INDS.                                                  
107100                                                                          
107200     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
107300             NOT EQUAL ZERO                                               
107400         IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
107500             SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
107600                 ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
107700                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
107800                               TO  TRUE                                   
107900         END-IF                                                           
108000     END-IF.                                                              
108100******************************************************************        
108200** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
108300** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
108400******************************************************************        
108500 2200-MOVE-SCREEN-TO-COMMAREA.                                            
108600                                                                          
108700     MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
108800     MOVE +1                       TO PV-SUB.                             
108900*                                                                         
109000     PERFORM 3100-SET-SUBSCRIPTS.                                         
109100     IF  ASTRTITL > ZERO                                                  
109200         MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
109300     ELSE                                                                 
109400         IF  ASTRTITF = DP015-ERASE-EOF                                   
109500             INITIALIZE ASC-START-ITEM-REQUEST                            
109600         END-IF                                                           
109700     END-IF.                                                              
109800     PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
109900         VARYING PV-SUB                                                   
110000         FROM 1 BY 1                                                      
110100         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
110200******************************************************************        
110300** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
110400******************************************************************        
110500 2210-MOVE-LINES-TO-COMMAREA.                                             
110600     IF  MR-SELECTL (PV-SUB) > ZERO                                       
110700         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
110800         MOVE MR-SELECTI (PV-SUB)                                         
110900                                   TO ASC-TEMP-SELECTED-SW                
111000                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
111100     ELSE                                                                 
111200         IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
111300                 SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
111400                 MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
111500                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
111600         END-IF                                                           
111700     END-IF.                                                              
111800     ADD +1                        TO ASC-ITEM-SUB.                       
111900     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
112000         ADD +1                    TO ASC-BLK-SUB                         
112100         MOVE +1                   TO ASC-ITEM-SUB                        
112200     END-IF.                                                              
112300******************************************************************        
112400** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
112500** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
112600******************************************************************        
112700 3000-EDIT-DATA-IN-COMMAREA.                                              
112800     SET PS-NO-ERROR               TO TRUE.                               
112900     PERFORM 3200-RESET-ATTRIBUTES.                                       
113000     MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
113100**                                                                        
113200* CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
113300* EDITTING IS DONE FROM THE BOTTOM UP.                                    
113400**                                                                        
113500     COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
113600         ASC-ITEMS-DISPLAYED - 1.                                         
113700     PERFORM 3100-SET-SUBSCRIPTS.                                         
113800     PERFORM 3300-EDIT-SELECTIONS                                         
113900         VARYING PV-SUB                                                   
114000         FROM PV-SUB BY -1                                                
114100         UNTIL PV-SUB < +1                                                
114200*                                                                         
114300     IF  PS-NO-ERROR                                                      
114400         PERFORM 3400-CHECK-RANGE-SEQUENCE                                
114500     END-IF.                                                              
114600*                                                                         
114700     IF  ((PS-NO-ERROR)                                                   
114800       AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
114900       AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
115000             PERFORM 3500-MARK-RANGE-AS-SELECTED                          
115100     END-IF.                                                              
115200                                                                          
115300*----------------------------------------------------------------*        
115400*    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
115500*    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
115600*    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
115700*                                                                *        
115800*    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
115900*    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
116000*    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
116100*    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
116200*----------------------------------------------------------------*        
116300                                                                          
116400 3100-SET-SUBSCRIPTS.                                                     
116500                                                                          
116600     COMPUTE ASC-ITEM-SUB =                                               
116700         (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
116800                                                                          
116900     IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
117000         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
117100                                                                          
117200         MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
117300     ELSE                                                                 
117400         MOVE +1                     TO ASC-BLK-SUB                       
117500     END-IF.                                                              
117600******************************************************************        
117700** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
117800******************************************************************        
117900                                                                          
118000 3200-RESET-ATTRIBUTES.                                                   
118100     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
118200     MOVE DP015-GREEN              TO ASTRTITC.                           
118300     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
118400*                                                                         
118500     PERFORM                                                              
118600         VARYING PV-SUB                                                   
118700         FROM 1 BY 1                                                      
118800         UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
118900         MOVE DP015-UNP-ALP-NOR-OFF                                       
119000                                   TO MR-SELECTA (PV-SUB)                 
119100         MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
119200         MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
119300                                                                          
120700     END-PERFORM.                                                         
120800******************************************************************        
120900** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **        
121000** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **        
121100** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **        
121200** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **        
121300******************************************************************        
121400 3300-EDIT-SELECTIONS.                                                    
121500     EVALUATE TRUE                                                        
121600         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
121700             = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
121800                 CONTINUE                                                 
121900         WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
122000             IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
122100                 ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
122200                 MOVE ASC-TEMP-SELECTED-SW                                
122300                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
122400                                TO ASC-SELECTED-SW                        
122500                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
122600             END-IF                                                       
122700         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
122800                 = SPACE                                                  
122900             EVALUATE TRUE                                                
123000                 WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
123100                     SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
123200                     MOVE SPACE TO ASC-SELECTED-SW                        
123300                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
123400                 WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
123500                     MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
123600                     MOVE SPACE TO ASC-SELECTED-SW                        
123700                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
123800                 WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
123900                     MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
124000                     MOVE SPACE TO ASC-SELECTED-SW                        
124100                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
124200             END-EVALUATE                                                 
124300         WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
124400             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
124500                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
124600             END-IF                                                       
124700             PERFORM 3310-EDIT-RANGE                                      
124800         WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
124900             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
125000                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
125100             END-IF                                                       
125200             PERFORM 3310-EDIT-RANGE                                      
125300         WHEN OTHER                                                       
125400             MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
125500             SET DP020-MSG-FATAL    TO TRUE                               
125600             MOVE -1                TO MR-SELECTL (PV-SUB)                
125700             SET DP030-SET-CURSOR-APPL-1                                  
125800                                    TO TRUE                               
125900             MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
126000             MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
126100             SET PS-ERROR TO TRUE                                         
126200             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
126300                 SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
126400                 MOVE SPACE         TO ASC-SELECTED-SW                    
126500                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
126600             END-IF                                                       
126700     END-EVALUATE.                                                        
126800                                                                          
126900     SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
127000     SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
127100     IF  ASC-ITEM-SUB < 1                                                 
127200         SUBTRACT +1 FROM ASC-BLK-SUB                                     
127300         MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
127400     END-IF.                                                              
127500******************************************************************        
127600** IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A      **        
127700** RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1    **        
127800** ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN)**        
127900** THE ENDING.                                                  **        
128000******************************************************************        
128100 3310-EDIT-RANGE.                                                         
128200     IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
128300         IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
128400             MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
128500                                   TO ASC-SELECTED-SW                     
128600                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
128700             MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
128800         ELSE                                                             
128900             IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
129000                 CONTINUE                                                 
129100             ELSE                                                         
129200*                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
129300                 MOVE PC-TSYMSG-00054                                     
129400                                   TO DP020-MSG-NUMBER                    
129500                 SET DP020-MSG-FATAL                                      
129600                                   TO TRUE                                
129700                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
129800                 SET DP030-SET-CURSOR-APPL-1                              
129900                                   TO TRUE                                
130000                 MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
130100                 MOVE DP015-UNDERLINE                                     
130200                                   TO MR-SELECTH (PV-SUB)                 
130300                 SET PS-ERROR      TO TRUE                                
130400             END-IF                                                       
130500         END-IF                                                           
130600     ELSE                                                                 
130700         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
130800             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
130900                                   TO ASC-SELECTED-SW                     
131000                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
131100             MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
131200         ELSE                                                             
131300             IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
131400                 CONTINUE                                                 
131500             ELSE                                                         
131600*                -- ONLY ONE END-OF-RANGE ALLOWED --                      
131700                 MOVE PC-TSYMSG-00055                                     
131800                                   TO DP020-MSG-NUMBER                    
131900                 SET DP020-MSG-FATAL                                      
132000                                   TO TRUE                                
132100                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
132200                 SET DP030-SET-CURSOR-APPL-1                              
132300                                   TO TRUE                                
132400                 SET PS-ERROR      TO TRUE                                
132500             END-IF                                                       
132600         END-IF                                                           
132700     END-IF.                                                              
132800*                                                                         
132900******************************************************************        
133000** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **        
133100** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **        
133200******************************************************************        
133300 3400-CHECK-RANGE-SEQUENCE.                                               
133400     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
133500       AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
133600       AND  ASC-START-OF-SELECTED-RANGE >                                 
133700                                         ASC-END-OF-SELECTED-RANGE        
133800*        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
133900         MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
134000         SET DP020-MSG-FATAL TO TRUE                                      
134100         SET PS-ERROR TO TRUE                                             
134200         PERFORM 3410-POSITION-AT-ERROR                                   
134300     END-IF.                                                              
134400******************************************************************        
134500** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **        
134600** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **        
134700** SHOULD BE POSITIONED ON A RANGE ERROR.                       **        
134800******************************************************************        
134900 3410-POSITION-AT-ERROR.                                                  
135000     SET DP030-SET-CURSOR-APPL-1                                          
135100                                   TO TRUE.                               
135200     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
135300                    - PV-TOP-ITEM + 1.                                    
135400     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
135500         CONTINUE                                                         
135600     ELSE                                                                 
135700         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
135800         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
135900         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
136000     END-IF.                                                              
136100*                                                                         
136200     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
136300                    - PV-TOP-ITEM + 1.                                    
136400     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
136500         CONTINUE                                                         
136600     ELSE                                                                 
136700         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
136800         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
136900         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
137000     END-IF.                                                              
137100                                                                          
137200*----------------------------------------------------------------*        
137300*    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
137400*    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
137500*    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
137600*----------------------------------------------------------------*        
137700                                                                          
137800 3500-MARK-RANGE-AS-SELECTED.                                             
137900     MOVE +1                       TO  ASC-BLK-SUB.                       
138000     PERFORM 3510-MARK-SELECTED-INDS                                      
138100         VARYING ASC-ITEM-SUB                                             
138200         FROM 1 BY 1                                                      
138300         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
138400                                                                          
138500     ADD +1                        TO ASC-BLK-SUB.                        
138600     PERFORM 3510-MARK-SELECTED-INDS                                      
138700         VARYING ASC-ITEM-SUB                                             
138800         FROM 1 BY 1                                                      
138900         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
139000                                                                          
139100     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
139200                                   TO  PV-LIST-ITEM-NUMBER.               
139300     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
139400         1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
139500               PC-ITEMS-PER-PANEL).                                       
139600                                                                          
139700     MOVE ASC-START-OF-SELECTED-RANGE                                     
139800                                   TO PV-START-OF-SELECTED-RANGE.         
139900     COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
140000         1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
140100               PC-ITEMS-PER-PANEL).                                       
140200                                                                          
140300     MOVE ASC-END-OF-SELECTED-RANGE                                       
140400                                   TO  PV-END-OF-SELECTED-RANGE.          
140500     COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
140600         1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
140700               PC-ITEMS-PER-PANEL).                                       
140800     COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
140900         PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
141000                                                                          
141100     IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
141200        (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
141300         MOVE +1                   TO ASC-BLK-SUB                         
141400         PERFORM 5100-WRITE-TEMP-STORAGE                                  
141500         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
141600             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
141700                                                                          
141800         PERFORM                                                          
141900             UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
142000                   PV-PREV-BLOCK-IN-ARRAY                                 
142100             MOVE PV-FIRST-BLOCK-IN-RANGE                                 
142200                                   TO PV-BLOCK-NUMBER                     
142300             PERFORM 4410-READ-TEMP-STORAGE                               
142400             PERFORM 3510-MARK-SELECTED-INDS                              
142500                 VARYING ASC-ITEM-SUB                                     
142600                 FROM 1 BY 1                                              
142700                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
142800             PERFORM 5100-WRITE-TEMP-STORAGE                              
142900             ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
143000         END-PERFORM                                                      
143100                                                                          
143200         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
143300             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
143400         PERFORM                                                          
143500             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
143600                   PV-LAST-BLOCK-IN-RANGE                                 
143700             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
143800                                   TO PV-BLOCK-NUMBER                     
143900             PERFORM 4410-READ-TEMP-STORAGE                               
144000             PERFORM 3510-MARK-SELECTED-INDS                              
144100                 VARYING ASC-ITEM-SUB                                     
144200                 FROM 1 BY 1                                              
144300                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
144400             PERFORM 5100-WRITE-TEMP-STORAGE                              
144500             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
144600         END-PERFORM                                                      
144700                                                                          
144800         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
144900                                   TO PV-BLOCK-NUMBER                     
145000         PERFORM 4410-READ-TEMP-STORAGE                                   
145100     END-IF.                                                              
145200                                                                          
145300     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
145400                                      ASC-END-OF-SELECTED-RANGE.          
145500                                                                          
145600                                                                          
145700*----------------------------------------------------------------*        
145800* SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
145900*----------------------------------------------------------------*        
146000                                                                          
146100 3510-MARK-SELECTED-INDS.                                                 
146200                                                                          
146300     IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
146400                                ASC-ITEM-SUB) >                           
146500         ASC-START-OF-SELECTED-RANGE)                                     
146600     OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
146700                                ASC-ITEM-SUB) =                           
146800         ASC-START-OF-SELECTED-RANGE)                                     
146900                                                                          
147000         IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
147100                                    ASC-ITEM-SUB) <                       
147200             ASC-END-OF-SELECTED-RANGE)                                   
147300         OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
147400                                    ASC-ITEM-SUB) =                       
147500             ASC-END-OF-SELECTED-RANGE)                                   
147600                                                                          
147700             IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
147800                 SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
147900                     ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
148000                               TO TRUE                                    
148100                 SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      
148200                               TO TRUE                                    
148300                 ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS            
148400             END-IF                                                       
148500         END-IF                                                           
148600     END-IF.                                                              
148700                                                                          
148800                                                                          
148900*----------------------------------------------------------------*        
149000*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
149100*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
149200*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
149300*    DISPLAY THE PANEL TO USER.                                  *        
149400*----------------------------------------------------------------*        
149500                                                                          
149600 4000-BUILD-INITIAL-PANEL.                                                
149700                                                                          
149800     INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
149900                                 ASC-BLOCK-ENTRIES (2).                   
150000     MOVE +1                 TO ASC-BLK-SUB.                              
150100     SET ASC-ITEMS-LEFT-ON-DB                                             
150200                             TO TRUE.                                     
150300                                                                          
150400     MOVE +0                 TO ASC-ITEM-SUB                              
150500                                ASC-NUMBER-OF-ITEMS-READ                  
150600                                ASC-NUMBER-OF-SELECTED-ITEMS              
150700                                ASC-HIGHEST-BLOCK-WRITTEN                 
150800                                ASC-START-OF-SELECTED-RANGE               
150900                                ASC-END-OF-SELECTED-RANGE                 
151000                                ASC-ITEM-AT-TOP-OF-PANEL.                 
151100                                                                          
151200*--------------------------------------------------------------*          
151300*    FOLLOWING LOGIC DETERMINES WHAT CURSOR TO USE TO RETRIEVE *          
151400*        THE DATA.  THIS SCREEN CAN ONLY BE GOTTEN TO FROM     *          
151500*        IQLDEPT - IQKCS404 - DEPARTMENT LIST SCREEN.          *          
151600*                                                              *          
151700*    THE VENDOR NAME IS EITHER SPACES OR HAS A VALUE AND THAT  *          
151800*        IS USED BY THE FOLLOWING LOGIC TO DETERMINE WHAT      *          
151900*        CURSOR TO USE.                                        *          
152000*                                                              *          
152100*--------------------------------------------------------------*          
152200                                                                          
152300     EVALUATE ASC-LBL-NM                                                  
152400         WHEN SPACES                                                      
152700              SET PS-CNT-STYLE-NO-VENDOR TO TRUE                          
152710              MOVE 'CNT-STYLE-NOVND   ' TO PV-CURSOR-NAME                 
152800              PERFORM 4011-OPEN-COUNT-CURSOR                              
152900              PERFORM 4350-FETCH-COUNTS                                   
153000              PERFORM 4021-CLOSE-COUNT-CURSOR                             
153100                                                                          
153200              MOVE PV-SKU-CNT TO ASC-SKU-CNT                              
153300              MOVE ZEROES     TO PV-SKU-CNT                               
153400                                                                          
153410              SET PS-CSR-STYLE-NO-VENDOR TO TRUE                          
153411              MOVE 'STYLE-NOVND       ' TO PV-CURSOR-NAME                 
153420                                                                          
153500         WHEN OTHER                                                       
153800              SET PS-CNT-STYLE-VENDOR TO TRUE                             
153810              MOVE 'CNT-STYLE-VND     ' TO PV-CURSOR-NAME                 
153900              PERFORM 4011-OPEN-COUNT-CURSOR                              
154000              PERFORM 4350-FETCH-COUNTS                                   
154100              PERFORM 4021-CLOSE-COUNT-CURSOR                             
154200                                                                          
154300              MOVE PV-SKU-CNT TO ASC-SKU-CNT                              
154400              MOVE ZEROES     TO PV-SKU-CNT                               
154500                                                                          
154600              SET PS-CNT-STYLE-LABEL TO TRUE                              
154610              MOVE 'CNT-STYLE-LBL     ' TO PV-CURSOR-NAME                 
154700              PERFORM 4011-OPEN-COUNT-CURSOR                              
154800              PERFORM 4350-FETCH-COUNTS                                   
154900              PERFORM 4021-CLOSE-COUNT-CURSOR                             
155000                                                                          
155100              ADD  PV-SKU-CNT TO ASC-SKU-CNT                              
155200                                                                          
155210              SET PS-CSR-STYLE-VENDOR TO TRUE                             
155211              MOVE 'CSR-STYLE-VENDOR  ' TO PV-CURSOR-NAME                 
155220                                                                          
155300     END-EVALUATE                                                         
155400                                                                          
155500     PERFORM 4010-OPEN-DATA-CURSOR.                                       
155600                                                                          
155700     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
155800                                                                          
155900     PERFORM 4020-CLOSE-CURSOR.                                           
156000                                                                          
156100     MOVE +1                   TO  PV-TOP-ITEM.                           
156200     IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
156300         SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
156400         SET DP020-MSG-FATAL   TO  TRUE                                   
156500         MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
156600     ELSE                                                                 
156700         MOVE -1               TO MR-SELECTL (1)                          
156800         SET DP030-SET-CURSOR-APPL-1                                      
156900                               TO TRUE                                    
157000         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
157100     END-IF.                                                              
157200                                                                          
157300*----------------------------------------------------------------*        
157400* OPEN ONE OF THE DATA CURSORS.                                           
157500*----------------------------------------------------------------*        
157600                                                                          
157700 4010-OPEN-DATA-CURSOR.                                                   
157800                                                                          
157900     MOVE '4010-OPEN-DATA-CURSOR'  TO DP013-PARAGRAPH.                    
158000                                                                          
158100     PERFORM                                                              
158200         WITH TEST AFTER                                                  
158300         VARYING PV-RETRY-COUNTER                                         
158400         FROM 1 BY 1                                                      
158500         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
158600                   OR (SQLCODE = ZERO))                                   
158700                                                                          
158800         EVALUATE TRUE                                                    
158900             WHEN PS-CSR-STYLE-NO-VENDOR                                  
159000                  EXEC SQL                                                
159100                       OPEN STYLE-NOVND                                   
159200                  END-EXEC                                                
159300                                                                          
159400             WHEN PS-CSR-STYLE-VENDOR                                     
159500                  EXEC SQL                                                
159600                       OPEN CSR-STYLE-VENDOR                              
159700                  END-EXEC                                                
159800                                                                          
159900         END-EVALUATE                                                     
160000                                                                          
160100         EVALUATE TRUE                                                    
160200             WHEN SQLCODE = ZERO                                          
160300             WHEN SQLCODE = -904                                          
160400             WHEN SQLCODE = -913                                          
160500                 CONTINUE                                                 
160600             WHEN SQLWARN0 NOT = SPACES                                   
160700             WHEN SQLCODE < ZERO                                          
160800             WHEN SQLCODE > ZERO                                          
161010                 STRING 'OPEN '                                           
161020                         PV-CURSOR-NAME                                   
161030                        ' CURSOR'                                         
161040                         DELIMITED BY SIZE                                
161050                         INTO DP013-MESSAGE-TEXT (1)                      
161100                 MOVE SQLCA        TO DP013-SQLCA                         
161200                 MOVE 'SUB_CL, VND'                                       
161300                                   TO DP013-DB2-TABLE-NAME (1)            
161400                 MOVE 'VND_STYL'   TO DP013-DB2-TABLE-NAME (2)            
161500                 SET DP013-DB2-ABEND                                      
161600                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
161700                 PERFORM DP013-0000-PROCESS-ABEND                         
161800         END-EVALUATE                                                     
161900                                                                          
162000     END-PERFORM.                                                         
162100*                                                                         
162200     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
162410         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN '            
162420                 PV-CURSOR-NAME                                           
162430                ' CURSOR'                                                 
162440                 DELIMITED BY SIZE                                        
162450                 INTO DP013-MESSAGE-TEXT (1)                              
162500         MOVE SQLCA                TO DP013-SQLCA                         
162600         MOVE 'SUB_CL, VND' TO DP013-DB2-TABLE-NAME (1)                   
162700         MOVE 'VND_STYL'    TO DP013-DB2-TABLE-NAME (2)                   
162800         SET DP013-DB2-ABEND                                              
162900             DP013-XCTL-DISPLAY-RESTART                                   
163000                                   TO TRUE                                
163100         PERFORM DP013-0000-PROCESS-ABEND                                 
163200     END-IF.                                                              
163210                                                                          
163220*----------------------------------------------------------------*        
163230*    OPEN ONE OF THE COUNT CURSORS THAT PROVIDES THE NUMBER OF            
163231*    AVAILABLE UPCS.                                                      
163240*----------------------------------------------------------------*        
163300                                                                          
163400 4011-OPEN-COUNT-CURSOR.                                                  
163500                                                                          
163600     MOVE '4011-OPEN-COUNT-CURSOR'   TO DP013-PARAGRAPH.                  
163700                                                                          
163800     PERFORM                                                              
163900         WITH TEST AFTER                                                  
164000         VARYING PV-RETRY-COUNTER                                         
164100         FROM 1 BY 1                                                      
164200         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
164300                   OR (SQLCODE = ZERO))                                   
164400                                                                          
164500         EVALUATE TRUE                                                    
164600             WHEN PS-CNT-STYLE-NO-VENDOR                                  
164700                  EXEC SQL                                                
164800                       OPEN CNT-STYLE-NOVND                               
164900                  END-EXEC                                                
165000                                                                          
165100             WHEN PS-CNT-STYLE-VENDOR                                     
165200                  EXEC SQL                                                
165300                       OPEN CNT-STYLE-VND                                 
165400                  END-EXEC                                                
165500                                                                          
165600             WHEN PS-CNT-STYLE-LABEL                                      
165700                  EXEC SQL                                                
165800                       OPEN CNT-STYLE-LBL                                 
165900                  END-EXEC                                                
166000                                                                          
166100             WHEN OTHER                                                   
166200                  CONTINUE                                                
166300                                                                          
166400         END-EVALUATE                                                     
166500                                                                          
166600         EVALUATE TRUE                                                    
166700             WHEN SQLCODE = ZERO                                          
166800             WHEN SQLCODE = -904                                          
166900             WHEN SQLCODE = -913                                          
167000                 CONTINUE                                                 
167100             WHEN SQLWARN0 NOT = SPACES                                   
167200             WHEN SQLCODE < ZERO                                          
167300             WHEN SQLCODE > ZERO                                          
167510                 STRING 'OPEN '                                           
167520                         PV-CURSOR-NAME                                   
167530                        ' CURSOR'                                         
167540                         DELIMITED BY SIZE                                
167550                         INTO DP013-MESSAGE-TEXT (1)                      
167600                 MOVE SQLCA        TO DP013-SQLCA                         
167700                 MOVE 'SKU, VND' TO DP013-DB2-TABLE-NAME (1)              
167800                 MOVE 'VND_STYL' TO DP013-DB2-TABLE-NAME (2)              
167900                 SET DP013-DB2-ABEND                                      
168000                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
168100                 PERFORM DP013-0000-PROCESS-ABEND                         
168200         END-EVALUATE                                                     
168300                                                                          
168400     END-PERFORM.                                                         
168500*                                                                         
168600     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
168810         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN '            
168820                 PV-CURSOR-NAME                                           
168830                ' CURSOR'                                                 
168840                 DELIMITED BY SIZE                                        
168850                 INTO DP013-MESSAGE-TEXT (1)                              
168900         MOVE SQLCA                TO DP013-SQLCA                         
169000         MOVE 'SKU, VND ' TO DP013-DB2-TABLE-NAME (1)                     
169100         MOVE 'VND_STYL ' TO DP013-DB2-TABLE-NAME (2)                     
169200         SET DP013-DB2-ABEND                                              
169300             DP013-XCTL-DISPLAY-RESTART                                   
169400                                   TO TRUE                                
169500         PERFORM DP013-0000-PROCESS-ABEND                                 
169600     END-IF.                                                              
169700                                                                          
169800                                                                          
169900                                                                          
170000*----------------------------------------------------------------*        
170100* CLOSE ONE OF THE DATA CURSORS.                                          
170200*----------------------------------------------------------------*        
170300                                                                          
170400 4020-CLOSE-CURSOR.                                                       
170500                                                                          
170600     EVALUATE TRUE                                                        
170700         WHEN PS-CSR-STYLE-NO-VENDOR                                      
170800              EXEC SQL                                                    
170900                   CLOSE STYLE-NOVND                                      
171000              END-EXEC                                                    
171100                                                                          
171210         WHEN PS-CSR-STYLE-VENDOR                                         
171300              EXEC SQL                                                    
171400                   CLOSE CSR-STYLE-VENDOR                                 
171500              END-EXEC                                                    
171600                                                                          
171700     END-EVALUATE.                                                        
171800                                                                          
171900     EVALUATE TRUE                                                        
172000         WHEN SQLCODE = ZERO                                              
172100             CONTINUE                                                     
172200         WHEN SQLWARN0 NOT = SPACES                                       
172300         WHEN SQLCODE < ZERO                                              
172400         WHEN SQLCODE > ZERO                                              
172500             MOVE '4020-CLOSE-CURSOR'                                     
172600                               TO DP013-PARAGRAPH                         
172700             MOVE 'CLOSE CLASS CURSOR '                                   
172800                               TO DP013-MESSAGE-TEXT (1)                  
172810             STRING 'CLOSE '                                              
172820                     PV-CURSOR-NAME                                       
172830                    ' CURSOR'                                             
172840                     DELIMITED BY SIZE                                    
172850                     INTO DP013-MESSAGE-TEXT (1)                          
172900             MOVE SQLCA        TO DP013-SQLCA                             
173000             MOVE 'SUB_CL, VND'                                           
173100                               TO DP013-DB2-TABLE-NAME (1)                
173200             MOVE 'VND_STYL'   TO DP013-DB2-TABLE-NAME (2)                
173300             SET DP013-DB2-ABEND                                          
173400                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
173500             PERFORM DP013-0000-PROCESS-ABEND                             
173600     END-EVALUATE.                                                        
173700                                                                          
173720*----------------------------------------------------------------*        
173730*    CLOSE ONE OF THE COUNT CURSORS THAT PROVIDES THE NUMBER OF           
173740*    AVAILABLE UPCS.                                                      
173750*----------------------------------------------------------------*        
174100                                                                          
174200 4021-CLOSE-COUNT-CURSOR.                                                 
174300                                                                          
174400     EVALUATE TRUE                                                        
174500         WHEN PS-CNT-STYLE-NO-VENDOR                                      
174600              EXEC SQL                                                    
174700                   CLOSE CNT-STYLE-NOVND                                  
174800              END-EXEC                                                    
174900                                                                          
175000         WHEN PS-CNT-STYLE-VENDOR                                         
175100              EXEC SQL                                                    
175200                   CLOSE CNT-STYLE-VND                                    
175300              END-EXEC                                                    
175400                                                                          
175500         WHEN PS-CNT-STYLE-LABEL                                          
175600              EXEC SQL                                                    
175700                   CLOSE CNT-STYLE-LBL                                    
175800              END-EXEC                                                    
175900                                                                          
176000         WHEN OTHER                                                       
176100              CONTINUE                                                    
176200                                                                          
176300     END-EVALUATE.                                                        
176400                                                                          
176500     EVALUATE TRUE                                                        
176600         WHEN SQLCODE = ZERO                                              
176700             CONTINUE                                                     
176800         WHEN SQLWARN0 NOT = SPACES                                       
176900         WHEN SQLCODE < ZERO                                              
177000         WHEN SQLCODE > ZERO                                              
177100             MOVE '4020-CLOSE-CURSOR'                                     
177200                               TO DP013-PARAGRAPH                         
177410             STRING 'CLOSE '                                              
177420                     PV-CURSOR-NAME                                       
177430                    ' CURSOR'                                             
177440                     DELIMITED BY SIZE                                    
177450                     INTO DP013-MESSAGE-TEXT (1)                          
177500             MOVE SQLCA        TO DP013-SQLCA                             
177600             MOVE 'SUB_CL, VND'                                           
177700                               TO DP013-DB2-TABLE-NAME (1)                
177800             MOVE 'VND_STYL'   TO DP013-DB2-TABLE-NAME (2)                
177900             SET DP013-DB2-ABEND                                          
178000                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
178100             PERFORM DP013-0000-PROCESS-ABEND                             
178200     END-EVALUATE.                                                        
178300                                                                          
178400*----------------------------------------------------------------*        
178500*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
178600*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
178700*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
178800*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
178900*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
179000*                                                                *        
179100*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
179200*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
179300*    UPON INITIALIZATION.                                        *        
179400*----------------------------------------------------------------*        
179500                                                                          
179600 4300-READ-ITEMS-FROM-DB.                                                 
179700                                                                          
179800     PERFORM                                                              
179900         VARYING PV-READ-COUNT                                            
180000         FROM 1 BY 1                                                      
180100         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
180200            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
180300                                                                          
180400         PERFORM 4310-FETCH                                               
180500                                                                          
180600         IF  ASC-ITEMS-LEFT-ON-DB                                         
180700             IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
180800                 ADD +1            TO ASC-ITEM-SUB                        
180900             ELSE                                                         
181000                 ADD +1            TO ASC-BLK-SUB                         
181100                 MOVE +1           TO ASC-ITEM-SUB                        
181200                                                                          
181300                 IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
181400                     MOVE +1       TO ASC-BLK-SUB                         
181500                     SET ASC-BLOCK-MODIFIED (1)                           
181600                                   TO TRUE                                
181700                     PERFORM 5100-WRITE-TEMP-STORAGE                      
181800                     MOVE ASC-BLOCK-ENTRIES                               
181900                               (PC-MAX-BLOCKS-IN-ARRAY)                   
182000                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
182100                     ADD +1        TO ASC-BLK-SUB                         
182200                     INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
182300                     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
182400                                   TO TRUE                                
182500                 END-IF                                                   
182600             END-IF                                                       
182700             ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
182800             MOVE ASC-NUMBER-OF-ITEMS-READ                                
182900                                   TO ASC-LIST-ITEM-NUMBER                
183000                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
183100             MOVE XST00013-STYL-ID    TO ASC-STYLE-ID                     
183200                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
183210             MOVE XST00013-VS-NBR      TO ASC-STYLE-NBR                   
183220                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
183300             MOVE XST00014-SUB-CL-NM TO ASC-STYLE-DESC                    
183400                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
183500             MOVE XST00013-STKR-NBR  TO PV-BOX                            
183600             MOVE PV-BOX             TO ASC-STYLE-BOX                     
183700                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
183800         END-IF                                                           
183900     END-PERFORM.                                                         
184000                                                                          
184100     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
184200         MOVE +1               TO ASC-BLK-SUB                             
184300         PERFORM 5100-WRITE-TEMP-STORAGE                                  
184400         ADD +1                TO ASC-BLK-SUB                             
184500         PERFORM 5100-WRITE-TEMP-STORAGE                                  
184600                                                                          
184700         MOVE 1                TO ASC-BLK-SUB                             
184800                                  PV-BLOCK-NUMBER                         
184900         PERFORM 4410-READ-TEMP-STORAGE                                   
185000         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
185100             MOVE 2            TO ASC-BLK-SUB                             
185200                                  PV-BLOCK-NUMBER                         
185300             PERFORM 4410-READ-TEMP-STORAGE                               
185400         END-IF                                                           
185500     END-IF.                                                              
185600                                                                          
185700*----------------------------------------------------------------*        
185800*    FETCH DATA TO DISPLAY USING ONE OF THE DATA CURSORS.        *        
185900*----------------------------------------------------------------*        
186000                                                                          
186100 4310-FETCH.                                                              
186400                                                                          
186410     PERFORM                                                              
186420         WITH TEST AFTER                                                  
186430         VARYING PV-RETRY-COUNTER                                         
186440         FROM 1 BY 1                                                      
186450         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
186460                   OR  SQLCODE = ZERO                                     
186461                   OR  SQLCODE = +100)                                    
186470                                                                          
186500         EVALUATE TRUE                                                    
186600             WHEN PS-CSR-STYLE-NO-VENDOR                                  
186700                  EXEC SQL                                                
186800                       FETCH STYLE-NOVND                                  
186900                             INTO                                         
187000                               :XST00013-DEPT-NBR                         
187100                              ,:XST00013-STYL-ID                          
187110                              ,:XST00013-VS-NBR                           
187200                              ,:XST00013-STKR-NBR                         
187300                              ,:XST00014-SUB-CL-NM                        
187400                                                                          
187500                  END-EXEC                                                
187600                                                                          
187710             WHEN PS-CSR-STYLE-VENDOR                                     
187800                  EXEC SQL                                                
187900                       FETCH CSR-STYLE-VENDOR                             
188000                             INTO                                         
188100                               :XST00013-DEPT-NBR                         
188200                              ,:XST00013-STYL-ID                          
188210                              ,:XST00013-VS-NBR                           
188300                              ,:XST00013-STKR-NBR                         
188400                              ,:XST00014-SUB-CL-NM                        
188500                                                                          
188600                  END-EXEC                                                
188700                                                                          
188800         END-EVALUATE                                                     
188900                                                                          
189000         EVALUATE TRUE                                                    
189100             WHEN SQLCODE = ZERO                                          
189200             WHEN SQLCODE = +100                                          
189300                  CONTINUE                                                
189400             WHEN SQLWARN0 NOT = SPACE                                    
189500             WHEN SQLCODE NOT = ZERO                                      
189600                  MOVE '4310-FETCH'                                       
189700                                   TO DP013-PARAGRAPH                     
189910                  STRING 'FETCH '                                         
189920                          PV-CURSOR-NAME                                  
189930                         ' CURSOR'                                        
189940                          DELIMITED BY SIZE                               
189950                          INTO DP013-MESSAGE-TEXT (1)                     
190000                  MOVE SQLCA       TO DP013-SQLCA                         
190100                  SET DP013-DB2-ABEND                                     
190200                                   TO TRUE                                
190300                  PERFORM DP013-0000-PROCESS-ABEND                        
190400         END-EVALUATE                                                     
190410                                                                          
190420     END-PERFORM.                                                         
190430                                                                          
190440     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
190471         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO FETCH '           
190472                 PV-CURSOR-NAME                                           
190473                ' CURSOR'                                                 
190474                 DELIMITED BY SIZE                                        
190475                 INTO DP013-MESSAGE-TEXT (1)                              
190480         MOVE SQLCA                TO DP013-SQLCA                         
190491         SET DP013-DB2-ABEND                                              
190492             DP013-XCTL-DISPLAY-RESTART                                   
190493                                   TO TRUE                                
190494         PERFORM DP013-0000-PROCESS-ABEND                                 
190495     END-IF.                                                              
190496                                                                          
190500                                                                          
190600     IF  SQLCODE EQUAL +100                                               
190700         SET ASC-NO-ITEMS-LEFT-ON-DB                                      
190800                                   TO TRUE                                
190900     END-IF.                                                              
191000                                                                          
191100*----------------------------------------------------------------*        
191210*    COUNT NUMBER OF AVAILABLE UPCS AND DISPLAY ON SCREEN.       *        
191300*----------------------------------------------------------------*        
191400                                                                          
191500 4350-FETCH-COUNTS.                                                       
191700                                                                          
191710     PERFORM                                                              
191720         WITH TEST AFTER                                                  
191730         VARYING PV-RETRY-COUNTER                                         
191740         FROM 1 BY 1                                                      
191750         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
191760                   OR (SQLCODE = ZERO))                                   
191800         EVALUATE TRUE                                                    
191900             WHEN PS-CNT-STYLE-NO-VENDOR                                  
192000                  EXEC SQL                                                
192100                      FETCH CNT-STYLE-NOVND                               
192200                             INTO                                         
192300                              :PV-SKU-CNT                                 
192400                  END-EXEC                                                
192500                                                                          
192600             WHEN PS-CNT-STYLE-VENDOR                                     
192700                  EXEC SQL                                                
192800                      FETCH CNT-STYLE-VND                                 
192900                             INTO                                         
193000                              :PV-SKU-CNT                                 
193100                  END-EXEC                                                
193200                                                                          
193300             WHEN PS-CNT-STYLE-LABEL                                      
193400                  EXEC SQL                                                
193500                      FETCH CNT-STYLE-LBL                                 
193600                             INTO                                         
193700                              :PV-SKU-CNT                                 
193800                  END-EXEC                                                
193900                                                                          
194000             WHEN OTHER                                                   
194100                  CONTINUE                                                
194200                                                                          
194300         END-EVALUATE                                                     
194400                                                                          
194500         EVALUATE TRUE                                                    
194600             WHEN SQLCODE = ZERO                                          
194700             WHEN SQLCODE = +100                                          
194800                  CONTINUE                                                
194900                                                                          
195000             WHEN SQLWARN0 NOT = SPACE                                    
195100             WHEN SQLCODE NOT = ZERO                                      
195310                  STRING 'FETCH '                                         
195320                          PV-CURSOR-NAME                                  
195330                         ' CURSOR'                                        
195340                          DELIMITED BY SIZE                               
195350                          INTO DP013-MESSAGE-TEXT (1)                     
195400                  MOVE SQLCA       TO DP013-SQLCA                         
195500                  SET DP013-DB2-ABEND                                     
195600                                   TO TRUE                                
195700                  PERFORM DP013-0000-PROCESS-ABEND                        
195800         END-EVALUATE                                                     
195810                                                                          
195820     END-PERFORM.                                                         
195830                                                                          
195840     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
195871         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO FETCH '           
195872                 PV-CURSOR-NAME                                           
195873                ' CURSOR'                                                 
195874                 DELIMITED BY SIZE                                        
195875                 INTO DP013-MESSAGE-TEXT (1)                              
195880         MOVE SQLCA                TO DP013-SQLCA                         
195891         SET DP013-DB2-ABEND                                              
195892             DP013-XCTL-DISPLAY-RESTART                                   
195893                                   TO TRUE                                
195894         PERFORM DP013-0000-PROCESS-ABEND                                 
195895     END-IF.                                                              
195896                                                                          
195900                                                                          
196000*----------------------------------------------------------------*        
196100* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
196200*----------------------------------------------------------------*        
196300                                                                          
196400 4400-MOVE-COMMAREA-TO-SCREEN.                                            
196500                                                                          
196600     IF ASC-LBL-NM = SPACES                                               
196700        MOVE 'ALL' TO AVENDNMO                                            
196800     ELSE                                                                 
196900        MOVE ASC-LBL-NM TO AVENDNMO                                       
197000     END-IF.                                                              
197100                                                                          
197200     MOVE ASC-GP-NBR          TO ABUSGRPO.                                
197300     MOVE ASC-GP-DESC         TO ABUSDESO.                                
197400     MOVE ASC-DEPT-NBR        TO ADEPTNOO.                                
197500     MOVE ASC-DEPT-DESC       TO ADPTDESO.                                
197600     MOVE ASC-SKU-CNT         TO ASKUO.                                   
197700                                                                          
197800     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
197900         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
198000         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
198100             MOVE +1    TO ASC-BLK-SUB                                    
198200             PERFORM 5100-WRITE-TEMP-STORAGE                              
198300             ADD +1     TO ASC-BLK-SUB                                    
198400             PERFORM 5100-WRITE-TEMP-STORAGE                              
198500             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
198600                 GIVING PV-BLOCK-NUMBER                                   
198700                 REMAINDER PV-REMAINDER                                   
198800             IF  PV-REMAINDER > ZERO                                      
198900                 ADD +1 TO PV-BLOCK-NUMBER                                
199000             END-IF                                                       
199100             MOVE +1    TO ASC-BLK-SUB                                    
199200             PERFORM 4410-READ-TEMP-STORAGE                               
199300             ADD +1     TO ASC-BLK-SUB                                    
199400                           PV-BLOCK-NUMBER                                
199500             PERFORM 4410-READ-TEMP-STORAGE                               
199600     END-IF.                                                              
199700     MOVE +1            TO ASC-BLK-SUB.                                   
199800     COMPUTE ASC-ITEM-SUB =                                               
199900         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
200000         + PC-ITEMS-PER-PANEL).                                           
200100*                                                                         
200200     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
200300         ADD +1         TO ASC-BLK-SUB                                    
200400         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
200500     END-IF.                                                              
200600*                                                                         
200700     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
200800         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
200900     PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
201000     PERFORM 4420-FORMAT-LIST-LINES                                       
201100         VARYING PV-SUB                                                   
201200         FROM PC-ITEMS-PER-PANEL BY -1                                    
201300         UNTIL PV-SUB < +1.                                               
201400*                                                                         
201500     COMPUTE ASC-ITEMS-DISPLAYED =                                        
201600         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
201700     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
201800                                      ASTRTITO.                           
201900     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
202000     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
202100     IF  DP020-MSG-NUMBER-X = SPACE                                       
202200         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
202300*            --- MORE ITEMS TO DISPLAY ----                               
202400             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
202500             SET DP020-MSG-INFORMATIONAL TO TRUE                          
202600         END-IF                                                           
202700     END-IF.                                                              
202800*----------------------------------------------------------------*        
202900*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
203000*----------------------------------------------------------------*        
203100                                                                          
203200 4410-READ-TEMP-STORAGE.                                                  
203300                                                                          
203400     EXEC CICS                                                            
203500         READQ TS                                                         
203600             QUEUE (ASC-LIST-QUEUE-NAME)                                  
203700             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
203800             ITEM  (PV-BLOCK-NUMBER)                                      
203900             RESP  (PV-CICS-RESP)                                         
204000     END-EXEC.                                                            
204100                                                                          
204200     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
204300       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
204400         CONTINUE                                                         
204500     ELSE                                                                 
204600         SET DP013-CICS-ABEND     TO TRUE                                 
204700         SET DP013-NO-ROLLBACK    TO TRUE                                 
204800         MOVE '4410-READ-TEMP-STORAGE'                                    
204900                                  TO DP013-PARAGRAPH                      
205000         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
205100                                  TO DP013-MESSAGE-TEXT(1)                
205200         PERFORM DP013-0000-PROCESS-ABEND                                 
205300     END-IF.                                                              
205400                                                                          
205500     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
205600*                                                                         
205700     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
205800         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
205900     END-IF.                                                              
206000                                                                          
206100*----------------------------------------------------------------*        
206200*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
206300*----------------------------------------------------------------*        
206400                                                                          
206500 4420-FORMAT-LIST-LINES.                                                  
206600     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
206700         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
206800         MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
207000         MOVE DP015-HL-OFF       TO MR-SELECTH (PV-SUB)                   
207100         MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
207200                                    MR-STYLE-NBR (PV-SUB)                 
207300                                    MR-STYLE-DESC (PV-SUB)                
207400                                    MR-STYLE-BOX  (PV-SUB)                
207500     ELSE                                                                 
207600         IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
207700                                                   > SPACE                
207800             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
207900                                 TO MR-SELECTI (PV-SUB)                   
208000         ELSE                                                             
208100             MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
208200                                 TO MR-SELECTI (PV-SUB)                   
208300         END-IF                                                           
208400*                                                                         
208500         IF  DP030-SET-CURSOR-TRAILER                                     
208600             IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
208700                 > SPACE)                                                 
208800               OR (PV-SUB = 1))                                           
208900                SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
209000                MOVE -1          TO MR-SELECTL (PV-SUB)                   
209100             END-IF                                                       
209200         END-IF                                                           
209300*                                                                         
209400         IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
209500                                                                          
209600             MOVE ASC-STYLE-NBR (ASC-BLK-SUB ASC-ITEM-SUB)                
209700                                 TO MR-STYLE-NBR (PV-SUB)                 
209800             MOVE ASC-STYLE-DESC (ASC-BLK-SUB ASC-ITEM-SUB)               
209900                                 TO MR-STYLE-DESC (PV-SUB)                
209910             IF ASC-STYLE-BOX (ASC-BLK-SUB ASC-ITEM-SUB) = 0              
209920                MOVE SPACES TO MR-STYLE-BOX (PV-SUB)                      
209930             ELSE                                                         
210000                MOVE ASC-STYLE-BOX  (ASC-BLK-SUB ASC-ITEM-SUB)            
210100                                 TO MR-STYLE-BOX  (PV-SUB)                
210110             END-IF                                                       
210200         END-IF                                                           
210300     END-IF.                                                              
210400     SUBTRACT +1 FROM ASC-ITEM-SUB                                        
210500     IF  ASC-ITEM-SUB < + 1                                               
210600         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
210700         MOVE +1                 TO ASC-BLK-SUB                           
210800     END-IF.                                                              
210900******************************************************************        
211000** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
211100** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
211200** ISSUE A WARNING.                                             **        
211300******************************************************************        
211400 4430-CHECK-FOR-BEGIN-AND-END.                                            
211500     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
211600         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
211700*            -- NO END-OF-RANGE SPECFIED ---                              
211800             MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
211900             SET DP020-MSG-WARNING TO TRUE                                
212000             PERFORM 4431-POSITION-AT-WARNING                             
212100         END-IF                                                           
212200     ELSE                                                                 
212300         IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
212400*            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
212500             MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
212600             SET DP020-MSG-WARNING TO TRUE                                
212700             PERFORM 4431-POSITION-AT-WARNING                             
212800         END-IF                                                           
212900     END-IF.                                                              
213000******************************************************************        
213100** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
213200** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
213300** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
213400** POSITION THE CURSOR THERE.                                   **        
213500******************************************************************        
213600 4431-POSITION-AT-WARNING.                                                
213700     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
213800                    - PV-TOP-ITEM + 1.                                    
213900     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
214000         CONTINUE                                                         
214100     ELSE                                                                 
214200         SET DP030-SET-CURSOR-APPL-1                                      
214300                                   TO TRUE                                
214400         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
214500         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
214600         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
214700     END-IF.                                                              
214800*                                                                         
214900     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
215000                    - PV-TOP-ITEM + 1.                                    
215100     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
215200         CONTINUE                                                         
215300     ELSE                                                                 
215400         SET DP030-SET-CURSOR-APPL-1                                      
215500                                   TO TRUE                                
215600         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
215700         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
215800         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
215900     END-IF.                                                              
216000                                                                          
216100*----------------------------------------------------------------*        
216200* PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
216300* ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
216400* FOR THE SELECTED ITEMS QUEUE.                                  *        
216500*                                                                *        
216600* FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
216700* QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
216800* THE ARRAY IS FULL.                                             *        
216900*----------------------------------------------------------------*        
217000                                                                          
217100 5000-PREPARE-SEL-QUEUE.                                                  
217200                                                                          
217300     MOVE +1                   TO  ASC-BLK-SUB.                           
217400     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
217500     ADD +1                    TO  ASC-BLK-SUB.                           
217600     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
217700                                                                          
217800     MOVE ASC-SEL-QUEUE-NAME   TO  IQ016-SEL-QUEUE-NAME.                  
217900     PERFORM 6010-DELETE-SEL-QUEUE.                                       
218000                                                                          
218100     SET ASC-UPDATE-TSQ        TO  TRUE.                                  
218200                                                                          
218300     SET IQ014-IDX             TO  1.                                     
218400     MOVE ZERO                 TO IQ016-NUMBER-OF-TS-ITEMS.               
218500     INITIALIZE IQ014-SELECTED-ITEM-ARRAY.                                
218600                                                                          
218700     PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
218800             VARYING PV-ITEM                                              
218900             FROM 1 BY 1                                                  
219000             UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
219100                                                                          
219200     IF  IQ014-IDX > 1                                                    
219300         PERFORM 5205-WRITE-SEL-QUEUE                                     
219400         ADD +1                TO IQ016-NUMBER-OF-TS-ITEMS                
219500     END-IF.                                                              
219600                                                                          
219700*----------------------------------------------------------------*        
219800*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
219900*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
220000*----------------------------------------------------------------*        
220100                                                                          
220200 5100-WRITE-TEMP-STORAGE.                                                 
220300     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
220400                                   TO PV-LIST-ITEM-NUMBER.                
220500     COMPUTE PV-BLOCK-NUMBER =                                            
220600         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
220700                                                                          
220800     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
220900     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
221000         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
221100             PERFORM 5105-REWRITE-ASC-LISTQ                               
221200         END-IF                                                           
221300     ELSE                                                                 
221400         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
221500         ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
221600         PERFORM 5110-WRITE-ASC-LISTQ                                     
221700         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
221800     END-IF.                                                              
221900                                                                          
222000     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
222100******************************************************************        
222200** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
222300** SPECIFIC COMMAREA.                                           **        
222400******************************************************************        
222500 5105-REWRITE-ASC-LISTQ.                                                  
222600     EXEC CICS                                                            
222700         WRITEQ TS                                                        
222800             QUEUE (ASC-LIST-QUEUE-NAME)                                  
222900             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
223000             ITEM  (PV-BLOCK-NUMBER)                                      
223100             REWRITE                                                      
223200             RESP  (PV-CICS-RESP)                                         
223300     END-EXEC.                                                            
223400                                                                          
223500     IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
223600         CONTINUE                                                         
223700     ELSE                                                                 
223800         SET DP013-CICS-ABEND            TO TRUE                          
223900         SET DP013-NO-ROLLBACK           TO TRUE                          
224000         MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
224100         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
224200                                         TO DP013-MESSAGE-TEXT(1)         
224300         PERFORM DP013-0000-PROCESS-ABEND                                 
224400     END-IF.                                                              
224500                                                                          
224600******************************************************************        
224700** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
224800** SPECIFIC COMMAREA.                                           **        
224900******************************************************************        
225000 5110-WRITE-ASC-LISTQ.                                                    
225100     EXEC CICS                                                            
225200         WRITEQ TS                                                        
225300             QUEUE (ASC-LIST-QUEUE-NAME)                                  
225400             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
225500             ITEM  (PV-BLOCK-NUMBER)                                      
225600             RESP  (PV-CICS-RESP)                                         
225700     END-EXEC.                                                            
225800     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
225900         SET DP013-CICS-ABEND      TO TRUE                                
226000         SET DP013-NO-ROLLBACK     TO TRUE                                
226100         MOVE '5110-WRITE-ASC-LISTQ'                                      
226200                                   TO DP013-PARAGRAPH                     
226300         MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
226400                                   TO DP013-MESSAGE-TEXT(1)               
226500         PERFORM DP013-0000-PROCESS-ABEND                                 
226600     END-IF.                                                              
226700                                                                          
226800*----------------------------------------------------------------*        
226900* READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
227000* TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
227100*----------------------------------------------------------------*        
227200                                                                          
227300 5200-FILL-SEL-QUEUE-RECORD.                                              
227400                                                                          
227500     PERFORM 6112-READ-LIST-QUEUE.                                        
227600                                                                          
227700     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
227800         PERFORM                                                          
227900         VARYING LQW-IDX                                                  
228000         FROM 1 BY 1                                                      
228100         UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
228200           OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
228300                                                                          
228400             IF  LQW-SELECT (LQW-IDX)                                     
228500                 MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
228600                                   TO IQ014-ITEM (IQ014-IDX)              
228700                 MOVE LQW-STYLE-ID  (LQW-IDX)                             
228800                             TO IQ016-STYLE-ID                            
228900                 MOVE 'STYLE SCREEN USED' TO IQ016-SUB-CL-NM              
229000                 SET IQ014-IDX UP BY 1                                    
229100                 IF  IQ014-IDX > IQ014-MAX-ITEMS                          
229200                     PERFORM 5205-WRITE-SEL-QUEUE                         
229300                     ADD +1        TO IQ016-NUMBER-OF-TS-ITEMS            
229400                     INITIALIZE IQ014-SELECTED-ITEM-ARRAY                 
229500                     SET IQ014-IDX TO  1                                  
229600                 END-IF                                                   
229700             END-IF                                                       
229800         END-PERFORM                                                      
229900     END-IF.                                                              
230000                                                                          
230100*----------------------------------------------------------------*        
230200*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
230300*----------------------------------------------------------------*        
230400                                                                          
230500 5205-WRITE-SEL-QUEUE.                                                    
230600                                                                          
230700     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
230800                                   TO IQ014-SELECTED-ITEMS.               
230900                                                                          
231000     EXEC CICS                                                            
231100          WRITEQ TS                                                       
231200              QUEUE (ASC-SEL-QUEUE-NAME)                                  
231300              FROM  (IQ014-MESSAGE-SEL-REC)                               
231400              ITEM  (PV-SEL-QUEUE-ITEM)                                   
231500              RESP  (PV-CICS-RESP)                                        
231600     END-EXEC.                                                            
231700                                                                          
231800     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
231900         CONTINUE                                                         
232000     ELSE                                                                 
232100         SET DP013-CICS-ABEND      TO TRUE                                
232200         SET DP013-NO-ROLLBACK     TO TRUE                                
232300         MOVE '5205-WRITE-SEL-QUEUE'                                      
232400                                   TO DP013-PARAGRAPH                     
232500         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
232600                                   TO DP013-MESSAGE-TEXT(1)               
232700         PERFORM DP013-0000-PROCESS-ABEND                                 
232800     END-IF.                                                              
232900                                                                          
233000******************************************************************        
233100** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **        
233200** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **        
233300** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **        
233400** THE USER.                                                    **        
233500******************************************************************        
233600 6000-REFRESH-PANEL-FROM-TEMP.                                            
233700     IF  ASC-UPDATE-TSQ                                                   
233800         SET ASC-NO-TSQ-UPDATE TO TRUE                                    
233900         MOVE ZERO                 TO PV-SAVE-ITEM                        
234000                                      PV-SEL-QUEUE-ITEM                   
234100         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
234200                                   TO PV-HOLD-SELECTED-ITEMS              
234300         MOVE +1                   TO PV-ITEMS-PROCESSED                  
234400         PERFORM 6100-UPDATE-TSQ                                          
234500             UNTIL PV-ITEMS-PROCESSED >                                   
234600                                     PV-HOLD-SELECTED-ITEMS               
234700         PERFORM 6010-DELETE-SEL-QUEUE                                    
234800         INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
234900                    ASC-BLOCK-ENTRIES (2)                                 
235000     END-IF.                                                              
235100                                                                          
235200*----------------------------------------------------------------*        
235300*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
235400*----------------------------------------------------------------*        
235500 6010-DELETE-SEL-QUEUE.                                                   
235600                                                                          
235700     EXEC CICS                                                            
235800         DELETEQ TS                                                       
235900             QUEUE (ASC-SEL-QUEUE-NAME)                                   
236000             RESP  (PV-CICS-RESP)                                         
236100     END-EXEC.                                                            
236200*                                                                         
236300     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
236400        (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
236500         CONTINUE                                                         
236600     ELSE                                                                 
236700         SET DP013-CICS-ABEND TO TRUE                                     
236800         SET DP013-NO-ROLLBACK TO TRUE                                    
236900         MOVE '6010-DELETE-SEL-QUEUE'                                     
237000                        TO DP013-PARAGRAPH                                
237100         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
237200                        TO DP013-MESSAGE-TEXT (1)                         
237300         PERFORM DP013-0000-PROCESS-ABEND                                 
237400     END-IF.                                                              
237500                                                                          
237600*----------------------------------------------------------------*        
237700*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
237800*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
237900*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
238000*    THE TEMP QUEUE.                                             *        
238100*----------------------------------------------------------------*        
238200                                                                          
238300 6100-UPDATE-TSQ.                                                         
238400                                                                          
238500     ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
238600     PERFORM 6105-READ-SEL-QUEUE.                                         
238700     PERFORM                                                              
238800         VARYING IQ014-IDX                                                
238900         FROM 1 BY 1                                                      
239000         UNTIL ((PV-ITEMS-PROCESSED >                                     
239100                              PV-HOLD-SELECTED-ITEMS)                     
239200         OR (IQ014-IDX GREATER THAN IQ014-MAX-ITEMS))                     
239300         PERFORM 6110-PROCESS-ITEMS                                       
239400         ADD +1                     TO PV-ITEMS-PROCESSED                 
239500     END-PERFORM.                                                         
239600     PERFORM 6111-WRITE-LIST-QUEUE.                                       
239700*----------------------------------------------------------------*        
239800*    READ THE LIST QUEUE RECORD.                                 *        
239900*----------------------------------------------------------------*        
240000                                                                          
240100 6105-READ-SEL-QUEUE.                                                     
240200                                                                          
240300     EXEC CICS                                                            
240400          READQ TS                                                        
240500              QUEUE (ASC-SEL-QUEUE-NAME)                                  
240600              INTO  (IQ014-MESSAGE-SEL-REC)                               
240700              ITEM  (PV-SEL-QUEUE-ITEM)                                   
240800              RESP  (PV-CICS-RESP)                                        
240900     END-EXEC.                                                            
241000*                                                                         
241100     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
241200         CONTINUE                                                         
241300     ELSE                                                                 
241400         SET DP013-CICS-ABEND      TO  TRUE                               
241500         SET DP013-NO-ROLLBACK     TO  TRUE                               
241600         MOVE '6105-READ-SEL-QUEUE'                                       
241700                                   TO  DP013-PARAGRAPH                    
241800         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
241900                                   TO  DP013-MESSAGE-TEXT(1)              
242000         PERFORM DP013-0000-PROCESS-ABEND                                 
242100     END-IF.                                                              
242200                                                                          
242300*----------------------------------------------------------------*        
242400*    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
242500*    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
242600*    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
242700*----------------------------------------------------------------*        
242800                                                                          
242900 6110-PROCESS-ITEMS.                                                      
243000                                                                          
243100     COMPUTE PV-ITEM = 1 + ((IQ014-ITEM (IQ014-IDX) - 1)                  
243200                     / PC-ITEMS-PER-PANEL).                               
243300                                                                          
243400     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
243500         IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
243600             PERFORM 6111-WRITE-LIST-QUEUE                                
243700         END-IF                                                           
243800         MOVE PV-ITEM          TO PV-SAVE-ITEM                            
243900                                                                          
244000         PERFORM 6112-READ-LIST-QUEUE                                     
244100         IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
244200             CONTINUE                                                     
244300         END-IF                                                           
244400     END-IF.                                                              
244500                                                                          
244600     COMPUTE PV-SUB = (IQ014-ITEM (IQ014-IDX)                             
244700                    - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
244800     SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
244900     MOVE LQW-SELECTED-SW (PV-SUB)                                        
245000                               TO LQW-TEMP-SELECTED-SW (PV-SUB).          
245100                                                                          
245200     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
245300                                                                          
245400*----------------------------------------------------------------*        
245500*    WRITE THE TS QUEUE RECORD JUST UPDATED.                     *        
245600*----------------------------------------------------------------*        
245700                                                                          
245800 6111-WRITE-LIST-QUEUE.                                                   
245900                                                                          
246000     EXEC CICS                                                            
246100         WRITEQ TS                                                        
246200             QUEUE (ASC-LIST-QUEUE-NAME)                                  
246300             FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
246400             ITEM  (PV-SAVE-ITEM)                                         
246500             REWRITE                                                      
246600             RESP  (PV-CICS-RESP)                                         
246700     END-EXEC.                                                            
246800*                                                                         
246900     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
247000         CONTINUE                                                         
247100     ELSE                                                                 
247200         SET DP013-CICS-ABEND  TO  TRUE                                   
247300         SET DP013-NO-ROLLBACK TO  TRUE                                   
247400         MOVE '6111-WRITE-LIST-QUEUE'                                     
247500                               TO  DP013-PARAGRAPH                        
247600         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
247700                               TO  DP013-MESSAGE-TEXT(1)                  
247800         PERFORM DP013-0000-PROCESS-ABEND                                 
247900     END-IF.                                                              
248000                                                                          
248100                                                                          
248200*----------------------------------------------------------------*        
248300*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
248400*----------------------------------------------------------------*        
248500                                                                          
248600 6112-READ-LIST-QUEUE.                                                    
248700                                                                          
248800     EXEC CICS                                                            
248900         READQ TS                                                         
249000             QUEUE (ASC-LIST-QUEUE-NAME)                                  
249100             INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
249200             ITEM  (PV-ITEM)                                              
249300             RESP  (PV-CICS-RESP)                                         
249400     END-EXEC.                                                            
249500*                                                                         
249600     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
249700         CONTINUE                                                         
249800     ELSE                                                                 
249900         SET DP013-CICS-ABEND        TO TRUE                              
250000         SET DP013-NO-ROLLBACK       TO TRUE                              
250100         MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
250200         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
250300                                     TO DP013-MESSAGE-TEXT(1)             
250400         PERFORM DP013-0000-PROCESS-ABEND                                 
250500     END-IF.                                                              
250600                                                                          
250700                                                                          
250800*----------------------------------------------------------------*        
250900*    RE-INITIALIZE ASC- FIELDS AFTER F3 RETURN                   *        
251000*----------------------------------------------------------------*        
251100 6500-REINIT-ASC-FLDS.                                                    
251200                                                                          
251300     MOVE ASC-LBL-NM       TO   IQ016-LBL-NM.                             
251400     MOVE ASC-GP-NBR       TO   IQ016-GP-NBR.                             
251500     MOVE ASC-GP-DESC      TO   IQ016-GP-DESC.                            
251600                                                                          
251700*----------------------------------------------------------------*        
251800*    ABEND PROCESSOR MODULE                                      *        
251900*----------------------------------------------------------------*        
252000                                                                          
252100     COPY DPPD013.                                                        
