       IDENTIFICATION DIVISION.                                         00010001
                                                                        00020001
       PROGRAM-ID.    MLKUP357.                                         00030001
       AUTHOR.        RONNA MCDONALD.                                   00040001
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050001
       DATE-WRITTEN.  8-02-07.                                          00060001
       DATE-COMPILED.                                                   00070001
                                                                        00080001
      ******************************************************************00090001
      *           MLKUP357 - WEEKLY DETAILED VENDOR LEDGER             *00100001
      *                      TABLE UPDATE (MLT_WKLY_VND)               *00110001
      ******************************************************************00120001
                                                                        00130001
       ENVIRONMENT DIVISION.                                            00140001
                                                                        00150001
       CONFIGURATION SECTION.                                           00160001
       SOURCE-COMPUTER.        IBM-3090.                                00170001
       OBJECT-COMPUTER.        IBM-3090.                                00180001
       INPUT-OUTPUT SECTION.                                            00190001
       FILE-CONTROL.                                                    00200001
           SELECT CONDENSED-FILE                                        00210001
               ASSIGN TO INP01.                                         00220001
                                                                        00230001
                                                                        00240001
       DATA DIVISION.                                                   00250001
       FILE SECTION.                                                    00260001
                                                                        00270001
       FD  CONDENSED-FILE                                               00280001
           LABEL RECORDS ARE STANDARD                                   00290001
           RECORDING MODE IS F                                          00300001
           BLOCK CONTAINS 0 RECORDS.                                    00310001
       01  CONDENSED-RECORD            PIC X(400).                      00320001
      *                                                                 00330001
                                                                        00340001
       WORKING-STORAGE SECTION.                                         00350001
                                                                        00360001
       01  FILLER                      PIC X(28)   VALUE                00370001
                'BEGINNING OF WORKING STORAGE'.                         00380001
                                                                        00390001
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00400001
                                                                        00410001
       01  PC-PROGRAM-CONSTANTS.                                        00420001
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK111'.    00430003
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP357'.  00440001
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00450001
                                                                        00460001
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00470001
           05  PE-MSG-01                       PIC X(50) VALUE          00480001
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00490001
           05  PE-MSG-02                       PIC X(50) VALUE          00500001
                'ATTEMPT TO UPDATE ROW ON TABLE MLT_WKLY_VND FAILED'.   00510001
           05  PE-MSG-03                       PIC X(50) VALUE          00520001
                'ATTEMPT TO INSERT ROW ON TABLE MLT_WKLY_VND FAILED'.   00530001
           05  PE-MSG-04                       PIC X(50) VALUE          00540001
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00550001
           05  PE-MSG-05                       PIC X(50) VALUE          00560001
                'UPDATE TO TCKRSTINF FAILED                      '.     00570001
           05  PE-MSG-06                       PIC X(50) VALUE          00580001
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00590001
           05  PE-MSG-07                       PIC X(50) VALUE          00600001
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00610001
      *                                                                 00620001
       01  PS-PROGRAM-SWITCHES.                                         00630001
           05  PS-EOF-CONDENSED-FILE-SW     PIC X(01)     VALUE 'N'.    00640001
               88  PS-EOF-CONDENSED-FILE                  VALUE 'Y'.    00650001
           05  PS-MLT00015-CURSOR-EOF-SW    PIC X(01)     VALUE 'N'.    00660001
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00670001
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00680001
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00690001
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00700001
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00710001
                                                                        00720001
       01  PI-PROGRAM-INDICATORS.                                       00730001
           05  PI-PROCESSING-OPTIONS-IND    PIC X(01)     VALUE 'P'.    00740001
               88  PI-UPDATE-MLT00015-ROW                 VALUE 'U'.    00750001
               88  PI-INSERT-MLT00015-ROW                 VALUE 'I'.    00760001
      *                                                                 00770001
       01  PROGRAM-VARIABLES.                                           00780001
           05  PV-RETRY-COUNT            PIC S9(04) VALUE +0.           00790001
           05  PV-DEADLOCK-COUNT         PIC S9(04) VALUE +0.           00800001
           05  PV-ENT-ID                 PIC 9(09)  VALUE 0.            00810001
      *                                                                 00820001
           05  PV-PARAGRAPH-NAME         PIC  X(30) VALUE SPACES.       00830001
           05  PV-ABEND-MESSAGE          PIC  X(75) VALUE SPACES.       00840001
           05  PV-OPERATION-ATTEMPTED    PIC  X(50) VALUE SPACES.       00850001
           05  PV-CURRENT-TIMESTAMP      PIC  X(26) VALUE SPACES.       00860001
           05  PV-COMMIT-TIMESTAMP       PIC  X(26) VALUE SPACES.       00870001
           05  PV-MKUP-NET-AMT           PIC S9(11)V99 VALUE +0 COMP-3. 00880001
           05  PV-MKUP-INIT-AMT          PIC S9(11)V99 VALUE +0 COMP-3. 00890001
           05  PV-NET-COST               PIC S9(11)V99 VALUE +0 COMP-3. 00900001
           05  PV-NET-RETAIL             PIC S9(11)V99 VALUE +0 COMP-3. 00901001
                                                                        00902001
       01  PA-PROGRAM-ACCUMULATORS.                                     00903001
           05  PA-RECORD-COUNTS.                                        00904001
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      00905001
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      00906001
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      00907001
               10  PA-MAJ-CL-PROCESSED   PIC  9(09)  VALUE ZEROES.      00908001
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      00909001
                                                                        00910001
       01  PD-PROGRAM-DISPLAYS.                                         00920001
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            00930001
       01  SAVE-AREA.                                                   00940001
           05  SV-PRIMARY-KEY.                                          00950001
               10  SV-PARTN-NBR          PIC S9(4)  COMP    VALUE +0.   00960001
               10  SV-DEPT-NBR           PIC S9(4)V COMP-3  VALUE +0.   00970001
               10  SV-MAJ-CL-NBR         PIC S9(4)V COMP-3  VALUE +0.   00980001
               10  SV-SUB-CL-NBR         PIC S9(4)V COMP-3  VALUE +0.   00990001
               10  SV-LOC-NBR            PIC S9(4)  COMP    VALUE +0.   01000001
               10  SV-ENT-ID             PIC S9(9)  COMP    VALUE +0.   01010001
           05  SV-UPDATE-FIELDS.                                        01020001
               10  SV-SLS-REG-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01030001
               10  SV-SLS-PROMO-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01040001
               10  SV-SLS-PROCLR-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01050001
               10  SV-SLS-CLR-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01060001
               10  SV-SLS-OTH-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01070001
               10  SV-MKDN-PLU-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01080001
               10  SV-MKDN-PROMO-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01090001
               10  SV-MKDN-RGST-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01100001
               10  SV-MKDN-GLDST-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01110001
               10  SV-MKDN-UMTCH-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01120001
               10  SV-MKDN-BYR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01130001
               10  SV-MKDN-STR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01140001
               10  SV-MKDN-PRPT-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01150001
               10  SV-MKDN-OTH-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01160001
               10  SV-MKDN-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01170001
               10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01180001
               10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01190001
               10  SV-XFER-OUT-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01200001
               10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01210001
               10  SV-RCPT-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01220001
               10  SV-RCPT-GRO-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01230001
               10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01240001
               10  SV-PADJ-FRGT-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01250001
               10  SV-PADJ-DISC-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01260001
               10  SV-PADJ-RTV-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01270001
               10  SV-PADJ-RTV-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01280001
               10  SV-PADJ-DA-COST-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01290001
               10  SV-PADJ-DA-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01300001
               10  SV-PADJ-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01310001
               10  SV-PADJ-OTH-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01320001
               10  SV-PADJ-OTH-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01330001
               10  SV-DR-ALW-RTL-AMT     PIC S9(11)V99  COMP-3 VALUE +0.01340001
               10  SV-RTV-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01350001
               10  SV-MKUP-INIT-AMT      PIC S9(11)V99  COMP-3 VALUE +0.01351001
               10  SV-MKUP-NET-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01352001
               10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01353001
               10  SV-ONORD-RTL-AMT      PIC S9(11)V99  COMP-3 VALUE +0.01354001
               10  SV-ONORD-COST-AMT     PIC S9(11)V99  COMP-3 VALUE +0.01355001
               10  SV-SHNK-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01356001
               10  SV-END-INV-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01357001
      *                                                                 01358001
      ******************************************************************01359001
      * MLRD155 - WK DETAILED MERCH LEDGER UPDATE TXN LAYOUT            01360001
      ******************************************************************01370001
           COPY MLRD157.                                                01380002
      *                                                                 01390001
      ******************************************************************01400001
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01410001
      ******************************************************************01420001
           COPY DPWS004.                                                01430001
      *                                                                 01440001
      ******************************************************************01450001
      *  USED FOR DB2 ERRORS                                            01460001
      ******************************************************************01470001
           EXEC SQL                                                     01480001
               INCLUDE SQLCA                                            01490001
           END-EXEC.                                                    01500001
      *                                                                 01510001
      ******************************************************************01520001
      *  MERCHANDISE LEDGER WEEKLY DETAIL TABLE                         01530001
      ******************************************************************01540001
           EXEC SQL                                                     01550001
               INCLUDE MLT00015                                         01560001
           END-EXEC.                                                    01570001
      *                                                                 01580001
      ***************************************************************** 01590001
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01600001
      ***************************************************************** 01610001
           EXEC SQL                                                     01620001
               INCLUDE TCKRSTCN                                         01630001
           END-EXEC.                                                    01640001
      *                                                                 01650001
           EXEC SQL                                                     01660001
               INCLUDE TCKRSTIN                                         01670001
           END-EXEC.                                                    01680001
      *                                                                 01690001
      ******************************************************************01700001
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01710001
      ******************************************************************01720001
       01  CR-CHECKPOINT-RESTART-AREA.                                  01730001
           05  CR-AREA-LEN                  PIC S9(04) VALUE +35  COMP. 01740001
           05  CR-CHECKPOINT-RESTART-VAR.                               01750001
               10  CR-PREV-DTL-KEY.                                     01760001
                   15  CR-PARTN-NBR         PIC  S9(4)  COMP            01770001
                                                       VALUE ZEROES.    01780001
                   15  CR-DEPT-NBR          PIC  S9(4)V COMP-3          01790001
                                                       VALUE ZEROES.    01800001
                   15  CR-MAJ-CL-NBR        PIC  S9(4)V COMP-3          01810001
                                                       VALUE ZEROES.    01820001
                   15  CR-SUB-CL-NBR        PIC  S9(4)V COMP-3          01830001
                                                       VALUE ZEROES.    01831001
                   15  CR-LOC-NBR           PIC  S9(4)  COMP            01831101
                                                       VALUE ZEROES.    01831201
                   15  CR-ENT-ID            PIC  S9(9)  COMP            01831301
                                                       VALUE ZEROES.    01831401
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01831501
      *                                                                 01831601
       01  CK-CHECKPOINT-SAVE-AREA.                                     01831701
           05  CK-SAVE-PREV-KEY.                                        01831801
               10  CK-PARTN-NBR         PIC  S9(4)  COMP VALUE ZEROES.  01831901
               10  CK-DEPT-NBR          PIC  S9(4)V COMP-3              01832001
                                                         VALUE ZEROES.  01833001
               10  CK-MAJ-CL-NBR        PIC  S9(4)V COMP-3              01834001
                                                         VALUE ZEROES.  01835001
               10  CK-SUB-CL-NBR        PIC  S9(4)V COMP-3              01836001
                                                         VALUE ZEROES.  01837001
               10  CK-LOC-NBR           PIC  S9(4)  COMP VALUE ZEROES.  01838001
               10  CK-ENT-ID            PIC  S9(9)  COMP VALUE ZEROES.  01839001
                                                                        01840001
                                                                        01850001
      *-------------------*                                             01860001
       PROCEDURE DIVISION.                                              01870001
      *-------------------*                                             01880001
                                                                        01890001
       0000-MLKUP357.                                                   01900001
                                                                        01910001
           PERFORM 1000-INITIALIZATION.                                 01920001
      *                                                                 01930001
           PERFORM 2000-PROCESS-INPUT                                   01940001
                UNTIL PS-EOF-CONDENSED-FILE.                            01950001
      *                                                                 01960001
           PERFORM 8000-EOJ-ROUTINE.                                    01970001
                                                                        01980001
           STOP RUN.                                                    01990001
                                                                        02000001
      ******************************************************************02010001
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02020001
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02030001
      ******************************************************************02040001
      *                                                                 02050001
       1000-INITIALIZATION.                                             02060001
      *                                                                 02070001
           OPEN INPUT CONDENSED-FILE.                                   02080001
                                                                        02090001
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02100001
                                                                        02110001
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02120001
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02130001
               PERFORM 7500-SELECT-RESTART-INFO                         02140001
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02150001
                                            CR-CHECKPOINT-RESTART-VAR   02160001
               MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY        02170001
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                02180001
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   02190001
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 02200001
               DISPLAY 'SUB CLASS NBR   ' CR-SUB-CL-NBR                 02210001
               DISPLAY 'STORE NBR       ' CR-LOC-NBR                    02220001
               DISPLAY 'ENT-ID          ' CR-ENT-ID                     02230001
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           02240001
           ELSE                                                         02250001
               SET PS-FIRST-COMMIT TO TRUE                              02260001
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02270001
           END-IF.                                                      02280001
      *                                                                 02290001
           PERFORM 4000-READ-CONDENSED-FILE UNTIL                       02300001
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02310001
            OR PS-EOF-CONDENSED-FILE.                                   02320001
      *                                                                 02330001
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02340001
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                02350001
                        PA-INPUT-COUNT                                  02360001
               DISPLAY 'PARTITION NUMBER      ' ML157-WK-PARTN-NBR      02370001
               DISPLAY 'DEPARTMENT NBR        ' ML157-DEPT-NBR          02380001
               DISPLAY 'MAJOR CLASS NBR       ' ML157-MAJ-CL-NBR        02390001
               DISPLAY 'SUB CLASS NBR         ' ML157-SUB-CL-NBR        02400001
               DISPLAY 'LOCATION NBR          ' ML157-LOC-NBR           02410001
               MOVE ML157-ENT-ID   TO PV-ENT-ID                         02420001
               DISPLAY 'ENT-ID                ' PV-ENT-ID               02430001
           END-IF.                                                      02440001
                                                                        02450001
           IF PS-EOF-CONDENSED-FILE                                     02460001
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02470001
                   CONTINUE                                             02480001
               ELSE                                                     02490001
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02500001
           ELSE                                                         02510001
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02520001
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02530001
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02540001
           END-IF.                                                      02550001
                                                                        02560001
      ******************************************************************02570001
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02580001
      ******************************************************************02590001
       2000-PROCESS-INPUT.                                              02600001
      *                                                                 02610001
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02620001
      *                                                                 02630001
           PERFORM 7000-SELECT-DB-ROW.                                  02640001
      *                                                                 02650001
           IF PS-PROGRAM-DEADLOCKED                                     02660001
               CONTINUE                                                 02670001
           ELSE                                                         02680001
               EVALUATE TRUE                                            02690001
                   WHEN PI-UPDATE-MLT00015-ROW                          02700001
                       PERFORM 7100-UPDATE-MLT00015-ROW                 02710001
                   WHEN PI-INSERT-MLT00015-ROW                          02720001
                       PERFORM 7200-INSERT-MLT00015-ROW                 02730001
                   WHEN OTHER                                           02740001
                       MOVE '7000-SELECT-DB-ROW' TO PV-PARAGRAPH-NAME   02750001
                       MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED         02760001
                       PERFORM 9999-SQL-ABEND                           02770001
               END-EVALUATE                                             02780001
           END-IF.                                                      02790001
      *                                                                 02800001
           IF PS-PROGRAM-DEADLOCKED                                     02810001
               CONTINUE                                                 02820001
           ELSE                                                         02830001
               PERFORM 7700-COMMIT-PROCESSING                           02840001
               PERFORM 4000-READ-CONDENSED-FILE                         02850001
           END-IF.                                                      02860001
      *                                                                 02870001
      *                                                                 02880001
      ******************************************************************02890001
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         02900001
      ******************************************************************02910001
       2100-CALCULATE-UPDATES.                                          02920001
      * ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES     02930001
      *                                                                 02940001
           COMPUTE SV-SLS-REG-RTL-AMT =                                 02950001
                   MLT00015-SLS-REG-RTL-AMT + ML157-SLS-REG-RTL-AMT.    02960001
           COMPUTE SV-SLS-PROMO-RTL-AMT =                               02970001
                   MLT00015-SLS-PROMO-RTL-AMT + ML157-SLS-PROMO-RTL-AMT.02980001
           COMPUTE SV-SLS-PROCLR-RTL-AMT =                              02990001
                 MLT00015-SLS-PROCLR-RTL-AMT + ML157-SLS-PROCLR-RTL-AMT.03000001
           COMPUTE SV-SLS-CLR-RTL-AMT =                                 03010001
                   MLT00015-SLS-CLR-RTL-AMT + ML157-SLS-CLR-RTL-AMT.    03020001
           COMPUTE SV-SLS-OTH-RTL-AMT =                                 03030001
                   MLT00015-SLS-OTH-RTL-AMT + ML157-SLS-OTH-RTL-AMT.    03040001
           COMPUTE SV-MKDN-PLU-RTL-AMT  =                               03050001
                   MLT00015-MKDN-PLU-RTL-AMT + ML157-MKDN-PLU-RTL-AMT.  03060001
           COMPUTE SV-MKDN-RGST-RTL-AMT =                               03070001
                   MLT00015-MKDN-RGST-RTL-AMT + ML157-MKDN-RGST-RTL-AMT.03080001
           COMPUTE SV-MKDN-UMTCH-RTL-AMT =                              03090001
                 MLT00015-MKDN-UMTCH-RTL-AMT + ML157-MKDN-UMTCH-RTL-AMT.03100001
           COMPUTE SV-MKDN-BYR-RTL-AMT  =                               03110001
                   MLT00015-MKDN-BYR-RTL-AMT + ML157-MKDN-BYR-RTL-AMT.  03120001
           COMPUTE SV-MKDN-STR-RTL-AMT  =                               03130001
                   MLT00015-MKDN-STR-RTL-AMT + ML157-MKDN-STR-RTL-AMT.  03140001
           COMPUTE SV-MKDN-RTL-AMT      =                               03150001
                   MLT00015-MKDN-RTL-AMT    + ML157-MKDN-RTL-AMT.       03160001
           COMPUTE SV-MKUP-RTL-AMT      =                               03170001
                   MLT00015-MKUP-RTL-AMT    + ML157-MKUP-RTL-AMT.       03180001
           COMPUTE SV-XFER-IN-RTL-AMT   =                               03190001
                   MLT00015-XFER-IN-RTL-AMT + ML157-XFER-IN-RTL-AMT.    03200001
           COMPUTE SV-XFER-OUT-RTL-AMT  =                               03210001
                   MLT00015-XFER-OUT-RTL-AMT + ML157-XFER-OUT-RTL-AMT.  03220001
           COMPUTE SV-RCPT-RTL-AMT      =                               03230001
                   MLT00015-RCPT-RTL-AMT    + ML157-RCPT-RTL-AMT.       03240001
           COMPUTE SV-RCPT-NET-COST-AMT =                               03250001
                   MLT00015-RCPT-NET-COST-AMT + ML157-RCPT-NET-COST-AMT.03260001
           COMPUTE SV-RCPT-GRO-COST-AMT =                               03270001
                   MLT00015-RCPT-GRO-COST-AMT + ML157-RCPT-GRO-COST-AMT.03280001
           COMPUTE SV-PADJ-FRGT-COST-AMT =                              03290001
                 MLT00015-PADJ-FRGT-COST-AMT + ML157-PADJ-FRGT-COST-AMT.03300001
           COMPUTE SV-PADJ-DISC-COST-AMT =                              03310001
                 MLT00015-PADJ-DISC-COST-AMT + ML157-PADJ-DISC-COST-AMT.03320001
           COMPUTE SV-PADJ-RTL-AMT      =                               03330001
                   MLT00015-PADJ-RTL-AMT    + ML157-PADJ-RTL-AMT.       03340001
           COMPUTE SV-PADJ-NET-COST-AMT =                               03350001
                   MLT00015-PADJ-NET-COST-AMT + ML157-PADJ-NET-COST-AMT.03360001
           COMPUTE SV-PADJ-FRGT-COST-AMT =                              03370001
                 MLT00015-PADJ-FRGT-COST-AMT + ML157-PADJ-FRGT-COST-AMT.03380001
           COMPUTE SV-DR-ALW-RTL-AMT    =                               03390001
                   MLT00015-DR-ALW-RTL-AMT  + ML157-DR-ALW-RTL-AMT.     03400001
           COMPUTE SV-RTV-RTL-AMT       =                               03410001
                   MLT00015-RTV-RTL-AMT     + ML157-RTV-RTL-AMT.        03420001
           COMPUTE SV-INV-ADJ-RTL-AMT   =                               03430001
                   MLT00015-INV-ADJ-RTL-AMT + ML157-INV-ADJ-RTL-AMT.    03440001
           COMPUTE SV-PADJ-RTV-COST-AMT  = MLT00015-PADJ-RTV-COST-AMT   03450001
                                         + ML157-PADJ-RTV-COST-AMT.     03460001
           COMPUTE SV-PADJ-RTV-RTL-AMT   = MLT00015-PADJ-RTV-RTL-AMT    03470001
                                         + ML157-PADJ-RTV-RTL-AMT.      03480001
           COMPUTE SV-PADJ-DA-COST-AMT   = MLT00015-PADJ-DA-COST-AMT    03490001
                                         + ML157-PADJ-DA-COST-AMT.      03500001
           COMPUTE SV-PADJ-DA-RTL-AMT    = MLT00015-PADJ-DA-RTL-AMT     03510001
                                         + ML157-PADJ-DA-RTL-AMT.       03520001
           COMPUTE SV-PADJ-OTH-COST-AMT  = MLT00015-PADJ-OTH-COST-AMT   03521001
                                         + ML157-PADJ-OTH-COST-AMT.     03521101
           COMPUTE SV-PADJ-OTH-RTL-AMT   = MLT00015-PADJ-OTH-RTL-AMT    03521201
                                         + ML157-PADJ-OTH-RTL-AMT.      03521301
           COMPUTE SV-MKDN-PROMO-RTL-AMT = MLT00015-MKDN-PROMO-RTL-AMT  03521401
                                         + ML157-MKDN-PROMO-RTL-AMT.    03521501
           COMPUTE SV-MKDN-PRPT-RTL-AMT  = MLT00015-MKDN-PRPT-RTL-AMT   03521601
                                         + ML157-MKDN-PRPT-RTL-AMT.     03521701
           COMPUTE SV-MKDN-GLDST-RTL-AMT = MLT00015-MKDN-GLDST-RTL-AMT  03521801
                                         + ML157-MKDN-GLDST-RTL-AMT.    03521901
           COMPUTE SV-MKDN-OTH-RTL-AMT   = MLT00015-MKDN-OTH-RTL-AMT    03522001
                                         + ML157-MKDN-OTH-RTL-AMT.      03522101
                                                                        03522201
      *--- INITIAL MARKUP AMOUNT                                        03522301
           COMPUTE PV-MKUP-INIT-AMT =                                   03522401
                   ML157-RCPT-RTL-AMT - ML157-RCPT-NET-COST-AMT.        03522501
      *            ML157-RCPT-RTL-AMT - ML157-RCPT-GRO-COST-AMT.        03522601
           COMPUTE SV-MKUP-INIT-AMT   =                                 03522701
                   MLT00015-MKUP-INIT-AMT + PV-MKUP-INIT-AMT.           03522801
                                                                        03522901
      *--- NET MARKUP AMOUNT                                            03523001
      *    COMPUTE PV-MKUP-NET-AMT =                                    03524001
      *           (ML157-RCPT-RTL-AMT -                                 03525001
      *           (ML157-RCPT-GRO-COST-AMT +                            03526001
      *            ML157-PADJ-FRGT-COST-AMT) -                          03527001
      *            ML157-PADJ-DISC-COST-AMT).                           03528001
           COMPUTE PV-NET-RETAIL =                                      03529001
                  (ML157-RCPT-RTL-AMT +                                 03530001
                   ML157-PADJ-RTL-AMT +                                 03540001
                   ML157-MKUP-RTL-AMT +                                 03550001
                  (ML157-XFER-IN-RTL-AMT -                              03560001
                   ML157-XFER-OUT-RTL-AMT))                             03570001
           END-COMPUTE.                                                 03580001
           COMPUTE PV-NET-COST = ML157-RCPT-NET-COST-AMT +              03590001
                                 ML157-PADJ-NET-COST-AMT                03600001
           END-COMPUTE.                                                 03610001
                                                                        03620001
           COMPUTE PV-MKUP-NET-AMT = PV-NET-RETAIL - PV-NET-COST        03630001
           END-COMPUTE.                                                 03631001
                                                                        03632001
           COMPUTE SV-MKUP-NET-AMT     =                                03633001
                   MLT00015-MKUP-NET-AMT + PV-MKUP-NET-AMT.             03634001
                                                                        03635001
           COMPUTE SV-INV-ADJ-RTL-AMT  =                                03636001
                   MLT00015-INV-ADJ-RTL-AMT + ML157-INV-ADJ-RTL-AMT.    03637001
      *                                                                 03638001
      ******************************************************************03639001
      * READS THE CONDENSED FILE INTO THE MLT_WKLY_VND LAYOUT           03640001
      ******************************************************************03650001
       4000-READ-CONDENSED-FILE.                                        03660001
           READ CONDENSED-FILE INTO ML157-UPDATE-REC                    03670001
              AT END                                                    03680001
                MOVE 'Y' TO PS-EOF-CONDENSED-FILE-SW.                   03690001
     *                                                                  03700001
           IF PS-EOF-CONDENSED-FILE                                     03710001
               MOVE ML157-WK-PARTN-NBR        TO CR-PARTN-NBR           03720001
               MOVE ML157-DEPT-NBR            TO CR-DEPT-NBR            03730001
               MOVE ML157-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          03740001
               MOVE ML157-SUB-CL-NBR          TO CR-SUB-CL-NBR          03750001
               MOVE ML157-LOC-NBR             TO CR-LOC-NBR             03760001
               MOVE ML157-ENT-ID              TO PV-ENT-ID              03770001
               MOVE PV-ENT-ID                 TO CR-ENT-ID              03780001
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    03790001
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03800001
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03810001
                                             TCKRSTINF-WORK-STRG-DESC   03820001
               EXEC SQL                                                 03830001
                 UPDATE TCKRSTINF                                       03840001
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03850001
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03860001
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03870001
               END-EXEC                                                 03880001
               IF SQLCODE = 0                                           03890001
                   CONTINUE                                             03900001
               ELSE                                                     03910001
                   MOVE PE-MSG-05          TO PV-OPERATION-ATTEMPTED    03920001
                   MOVE '* 4000-READ-CONDENSED *' TO PV-PARAGRAPH-NAME  03930001
                   PERFORM 9999-SQL-ABEND                               03940001
               END-IF                                                   03950001
            ELSE                                                        03960001
                ADD 1                        TO PA-INPUT-COUNT          03970001
            END-IF.                                                     03980001
      *                                                                 03990001
      ******************************************************************04000001
      * SELECT A ROW FROM THE WEEKLY DTL TABLE THAT MATCHES INPUT KEY   04010001
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      04020001
      ******************************************************************04030001
       7000-SELECT-DB-ROW.                                              04040001
      *                                                                 04050001
           EXEC SQL                                                     04060001
               SELECT SLS_REG_RTL_AMT                                   04070001
                     ,SLS_PROMO_RTL_AMT                                 04080001
                     ,SLS_PROCLR_RTL_AMT                                04090001
                     ,SLS_CLR_RTL_AMT                                   04100001
                     ,SLS_OTH_RTL_AMT                                   04110001
                     ,MKDN_PLU_RTL_AMT                                  04111001
                     ,MKDN_PROMO_RTL_AMT                                04112001
                     ,MKDN_RGST_RTL_AMT                                 04113001
                     ,MKDN_GLDST_RTL_AMT                                04114001
                     ,MKDN_UMTCH_RTL_AMT                                04115001
                     ,MKDN_BYR_RTL_AMT                                  04116001
                     ,MKDN_STR_RTL_AMT                                  04117001
                     ,MKDN_PRPT_RTL_AMT                                 04118001
                     ,MKDN_OTH_RTL_AMT                                  04119001
                     ,MKDN_RTL_AMT                                      04119101
                     ,MKUP_RTL_AMT                                      04119201
                     ,XFER_IN_RTL_AMT                                   04119301
                     ,XFER_OUT_RTL_AMT                                  04119401
                     ,RCPT_RTL_AMT                                      04119501
                     ,RCPT_NET_COST_AMT                                 04119601
                     ,RCPT_GRO_COST_AMT                                 04119701
                     ,PADJ_RTL_AMT                                      04119801
                     ,PADJ_FRGT_COST_AMT                                04119901
                     ,PADJ_DISC_COST_AMT                                04120001
                     ,PADJ_RTV_COST_AMT                                 04120101
                     ,PADJ_RTV_RTL_AMT                                  04120201
                     ,PADJ_DA_COST_AMT                                  04120301
                     ,PADJ_DA_RTL_AMT                                   04120401
                     ,PADJ_NET_COST_AMT                                 04120501
                     ,PADJ_OTH_COST_AMT                                 04120601
                     ,PADJ_OTH_RTL_AMT                                  04120701
                     ,DR_ALW_RTL_AMT                                    04120801
                     ,RTV_RTL_AMT                                       04120901
                     ,MKUP_INIT_AMT                                     04121001
                     ,MKUP_NET_AMT                                      04121101
                     ,INV_ADJ_RTL_AMT                                   04121201
                 INTO :MLT00015-SLS-REG-RTL-AMT                         04121301
                     ,:MLT00015-SLS-PROMO-RTL-AMT                       04121401
                     ,:MLT00015-SLS-PROCLR-RTL-AMT                      04121501
                     ,:MLT00015-SLS-CLR-RTL-AMT                         04121601
                     ,:MLT00015-SLS-OTH-RTL-AMT                         04121701
                     ,:MLT00015-MKDN-PLU-RTL-AMT                        04121801
                     ,:MLT00015-MKDN-PROMO-RTL-AMT                      04121901
                     ,:MLT00015-MKDN-RGST-RTL-AMT                       04122001
                     ,:MLT00015-MKDN-GLDST-RTL-AMT                      04123001
                     ,:MLT00015-MKDN-UMTCH-RTL-AMT                      04123101
                     ,:MLT00015-MKDN-BYR-RTL-AMT                        04123201
                     ,:MLT00015-MKDN-STR-RTL-AMT                        04123301
                     ,:MLT00015-MKDN-PRPT-RTL-AMT                       04123401
                     ,:MLT00015-MKDN-OTH-RTL-AMT                        04123501
                     ,:MLT00015-MKDN-RTL-AMT                            04123601
                     ,:MLT00015-MKUP-RTL-AMT                            04123701
                     ,:MLT00015-XFER-IN-RTL-AMT                         04123801
                     ,:MLT00015-XFER-OUT-RTL-AMT                        04123901
                     ,:MLT00015-RCPT-RTL-AMT                            04124001
                     ,:MLT00015-RCPT-NET-COST-AMT                       04124101
                     ,:MLT00015-RCPT-GRO-COST-AMT                       04124201
                     ,:MLT00015-PADJ-RTL-AMT                            04124301
                     ,:MLT00015-PADJ-FRGT-COST-AMT                      04124401
                     ,:MLT00015-PADJ-DISC-COST-AMT                      04124501
                     ,:MLT00015-PADJ-RTV-COST-AMT                       04124601
                     ,:MLT00015-PADJ-RTV-RTL-AMT                        04124701
                     ,:MLT00015-PADJ-DA-COST-AMT                        04124801
                     ,:MLT00015-PADJ-DA-RTL-AMT                         04124901
                     ,:MLT00015-PADJ-NET-COST-AMT                       04125001
                     ,:MLT00015-PADJ-OTH-COST-AMT                       04125101
                     ,:MLT00015-PADJ-OTH-RTL-AMT                        04125201
                     ,:MLT00015-DR-ALW-RTL-AMT                          04125301
                     ,:MLT00015-RTV-RTL-AMT                             04125401
                     ,:MLT00015-MKUP-INIT-AMT                           04125501
                     ,:MLT00015-MKUP-NET-AMT                            04125601
                     ,:MLT00015-INV-ADJ-RTL-AMT                         04125701
               FROM MLT_WKLY_VND                                        04125801
              WHERE   PARTN_NBR           = :ML157-WK-PARTN-NBR         04125901
                AND   DEPT_NBR            = :ML157-DEPT-NBR             04126001
                AND   MAJ_CL_NBR          = :ML157-MAJ-CL-NBR           04127001
                AND   SUB_CL_NBR          = :ML157-SUB-CL-NBR           04128001
                AND   LOC_NBR             = :ML157-LOC-NBR              04129001
                AND   ENT_ID              = :ML157-ENT-ID               04130001
           END-EXEC.                                                    04140001
      *                                                                 04150001
           EVALUATE SQLCODE                                             04160001
               WHEN 0                                                   04170001
                   SET PI-UPDATE-MLT00015-ROW     TO TRUE               04180001
               WHEN +100                                                04190001
                   SET PI-INSERT-MLT00015-ROW     TO TRUE               04200001
               WHEN -911                                                04210001
                   DISPLAY '*** -911 OCCURRED ON SELECT ***'            04220001
                   ADD +1             TO PV-DEADLOCK-COUNT              04230001
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04240001
                       MOVE '7000-SELECT-DB-ROW' TO                     04250001
                                                    PV-PARAGRAPH-NAME   04260001
                       PERFORM 9000-DEADLOCK-ERROR                      04270001
                   ELSE                                                 04280001
                       PERFORM 7999-RESTART-PROCESS                     04290001
                   END-IF                                               04300001
               WHEN OTHER                                               04310001
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     04320001
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             04330001
                   PERFORM 9999-SQL-ABEND                               04340001
           END-EVALUATE.                                                04350001
      *                                                                 04360001
      *                                                                 04370001
      ***************************************************************** 04380001
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON PARTITION NBR,   04390001
      * DEPT, MAJ CL, SUB CL, LOC NBR, AND ENT ID.  CURRENT TABLE       04400001
      * VALUES UPDATED TO INCLUDE VALUES FROM INPUT REC MLRD154         04410001
      ***************************************************************** 04420001
       7100-UPDATE-MLT00015-ROW.                                        04430001
      *                                                                 04440001
           PERFORM 2100-CALCULATE-UPDATES.                              04450001
      *                                                                 04460001
           EXEC SQL                                                     04470001
               UPDATE MLT_WKLY_VND                                      04480001
                   SET SLS_REG_RTL_AMT     = :SV-SLS-REG-RTL-AMT        04490001
                     , SLS_PROMO_RTL_AMT   = :SV-SLS-PROMO-RTL-AMT      04500001
                     , SLS_PROCLR_RTL_AMT  = :SV-SLS-PROCLR-RTL-AMT     04510001
                     , SLS_CLR_RTL_AMT     = :SV-SLS-CLR-RTL-AMT        04520001
                     , SLS_OTH_RTL_AMT     = :SV-SLS-OTH-RTL-AMT        04530001
                     , MKDN_PLU_RTL_AMT    = :SV-MKDN-PLU-RTL-AMT       04540001
                     , MKDN_PROMO_RTL_AMT  = :SV-MKDN-PROMO-RTL-AMT     04550001
                     , MKDN_RGST_RTL_AMT   = :SV-MKDN-RGST-RTL-AMT      04560001
                     , MKDN_GLDST_RTL_AMT  = :SV-MKDN-GLDST-RTL-AMT     04570001
                     , MKDN_UMTCH_RTL_AMT  = :SV-MKDN-UMTCH-RTL-AMT     04580001
                     , MKDN_BYR_RTL_AMT    = :SV-MKDN-BYR-RTL-AMT       04590001
                     , MKDN_STR_RTL_AMT    = :SV-MKDN-STR-RTL-AMT       04600001
                     , MKDN_PRPT_RTL_AMT   = :SV-MKDN-PRPT-RTL-AMT      04610001
                     , MKDN_OTH_RTL_AMT    = :SV-MKDN-OTH-RTL-AMT       04620001
                     , MKDN_RTL_AMT        = :SV-MKDN-RTL-AMT           04630001
                     , MKUP_RTL_AMT        = :SV-MKUP-RTL-AMT           04640001
                     , XFER_IN_RTL_AMT     = :SV-XFER-IN-RTL-AMT        04650001
                     , XFER_OUT_RTL_AMT    = :SV-XFER-OUT-RTL-AMT       04660001
                     , RCPT_RTL_AMT        = :SV-RCPT-RTL-AMT           04670001
                     , RCPT_NET_COST_AMT   = :SV-RCPT-NET-COST-AMT      04680001
                     , RCPT_GRO_COST_AMT   = :SV-RCPT-GRO-COST-AMT      04690001
                     , PADJ_RTL_AMT        = :SV-PADJ-RTL-AMT           04700001
                     , PADJ_FRGT_COST_AMT  = :SV-PADJ-FRGT-COST-AMT     04710001
                     , PADJ_DISC_COST_AMT  = :SV-PADJ-DISC-COST-AMT     04720001
                     , PADJ_RTV_COST_AMT   = :SV-PADJ-RTV-COST-AMT      04730001
                     , PADJ_RTV_RTL_AMT    = :SV-PADJ-RTV-RTL-AMT       04740001
                     , PADJ_DA_COST_AMT    = :SV-PADJ-DA-COST-AMT       04750001
                     , PADJ_DA_RTL_AMT     = :SV-PADJ-DA-RTL-AMT        04760001
                     , PADJ_NET_COST_AMT   = :SV-PADJ-NET-COST-AMT      04770001
                     , PADJ_OTH_COST_AMT   = :SV-PADJ-OTH-COST-AMT      04780001
                     , PADJ_OTH_RTL_AMT    = :SV-PADJ-OTH-RTL-AMT       04790001
                     , DR_ALW_RTL_AMT      = :SV-DR-ALW-RTL-AMT         04800001
                     , RTV_RTL_AMT         = :SV-RTV-RTL-AMT            04810001
                     , MKUP_INIT_AMT       = :SV-MKUP-INIT-AMT          04820001
                     , MKUP_NET_AMT        = :SV-MKUP-NET-AMT           04830001
                     , INV_ADJ_RTL_AMT     = :SV-INV-ADJ-RTL-AMT        04840001
              WHERE   PARTN_NBR            = :ML157-WK-PARTN-NBR        04850001
                AND   DEPT_NBR             = :ML157-DEPT-NBR            04860001
                AND   MAJ_CL_NBR           = :ML157-MAJ-CL-NBR          04870001
                AND   SUB_CL_NBR           = :ML157-SUB-CL-NBR          04880001
                AND   LOC_NBR              = :ML157-LOC-NBR             04890001
                AND   ENT_ID               = :ML157-ENT-ID              04900001
           END-EXEC                                                     04910001
      *                                                                 04920001
           EVALUATE SQLCODE                                             04930001
               WHEN 0                                                   04940001
                   CONTINUE                                             04950001
               WHEN -911                                                04960001
                   ADD +1             TO PV-DEADLOCK-COUNT              04970001
                   DISPLAY '*** -911 OCCURRED ON UPDATE ***'            04980001
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04990001
                       MOVE '7100-UPDATE-MLT00015-ROW' TO               05000001
                                                    PV-PARAGRAPH-NAME   05010001
                       PERFORM 9000-DEADLOCK-ERROR                      05020001
                   ELSE                                                 05030001
                       PERFORM 7999-RESTART-PROCESS                     05040001
                   END-IF                                               05050001
               WHEN OTHER                                               05060001
                   MOVE '7100-UPDATE-MLT00015-ROW' TO PV-PARAGRAPH-NAME 05070001
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED05080001
                   PERFORM 9999-SQL-ABEND                               05090001
           END-EVALUATE.                                                05100001
      *                                                                 05110001
           ADD 1 TO PA-UPDATE-COUNT.                                    05120001
      *                                                                 05130001
      ***************************************************************** 05140001
      * THERE WAS NO MATCHING ROW ON THE WEEKLY DTL TABLE. INITIAL    * 05150001
      * ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW.             * 05160001
      * CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN RECALC* 05170001
      ***************************************************************** 05180001
       7200-INSERT-MLT00015-ROW.                                        05190001
      *                                                                 05200001
           COMPUTE PV-MKUP-INIT-AMT =                                   05210001
                   ML157-RCPT-RTL-AMT - ML157-RCPT-NET-COST-AMT.        05220001
      *            ML157-RCPT-RTL-AMT - ML157-RCPT-GRO-COST-AMT.        05230001
                                                                        05240001
      *    COMPUTE PV-MKUP-NET-AMT =                                    05250001
      *           (ML157-RCPT-RTL-AMT -                                 05260001
      *           (ML157-RCPT-GRO-COST-AMT +                            05270001
      *            ML157-PADJ-FRGT-COST-AMT) -                          05280001
      *            ML157-PADJ-DISC-COST-AMT).                           05290001
      *                                                                 05300001
           COMPUTE PV-NET-RETAIL =                                      05310001
                  (ML157-RCPT-RTL-AMT +                                 05320001
                   ML157-PADJ-RTL-AMT +                                 05330001
                   ML157-MKUP-RTL-AMT +                                 05340001
                  (ML157-XFER-IN-RTL-AMT -                              05350001
                   ML157-XFER-OUT-RTL-AMT))                             05360001
           END-COMPUTE.                                                 05370001
           COMPUTE PV-NET-COST = ML157-RCPT-NET-COST-AMT +              05380001
                                 ML157-PADJ-NET-COST-AMT                05390001
           END-COMPUTE.                                                 05400001
                                                                        05410001
           COMPUTE PV-MKUP-NET-AMT = PV-NET-RETAIL - PV-NET-COST        05420001
           END-COMPUTE.                                                 05430001
           EXEC SQL                                                     05440001
               INSERT INTO MLT_WKLY_VND                                 05450001
                    (  PARTN_NBR                                        05460001
                     , DEPT_NBR                                         05470001
                     , MAJ_CL_NBR                                       05480001
                     , SUB_CL_NBR                                       05490001
                     , LOC_NBR                                          05500001
                     , ENT_ID                                           05510001
                     , FSCL_WK_END_DTE                                  05520001
                     , SLS_REG_RTL_AMT                                  05530001
                     , SLS_PROMO_RTL_AMT                                05540001
                     , SLS_PROCLR_RTL_AMT                               05550001
                     , SLS_CLR_RTL_AMT                                  05560001
                     , SLS_OTH_RTL_AMT                                  05570001
                     , MKDN_PLU_RTL_AMT                                 05580001
                     , MKDN_PROMO_RTL_AMT                               05590001
                     , MKDN_RGST_RTL_AMT                                05600001
                     , MKDN_GLDST_RTL_AMT                               05610001
                     , MKDN_UMTCH_RTL_AMT                               05620001
                     , MKDN_BYR_RTL_AMT                                 05630001
                     , MKDN_STR_RTL_AMT                                 05640001
                     , MKDN_PRPT_RTL_AMT                                05650001
                     , MKDN_OTH_RTL_AMT                                 05660001
                     , MKDN_RTL_AMT                                     05670001
                     , MKUP_RTL_AMT                                     05680001
                     , XFER_IN_RTL_AMT                                  05690001
                     , XFER_OUT_RTL_AMT                                 05700001
                     , RCPT_RTL_AMT                                     05710001
                     , RCPT_NET_COST_AMT                                05720001
                     , RCPT_GRO_COST_AMT                                05730001
                     , PADJ_RTL_AMT                                     05740001
                     , PADJ_FRGT_COST_AMT                               05750001
                     , PADJ_DISC_COST_AMT                               05760001
                     , PADJ_RTV_COST_AMT                                05770001
                     , PADJ_RTV_RTL_AMT                                 05780001
                     , PADJ_DA_COST_AMT                                 05790001
                     , PADJ_DA_RTL_AMT                                  05800001
                     , PADJ_NET_COST_AMT                                05810001
                     , PADJ_OTH_COST_AMT                                05820001
                     , PADJ_OTH_RTL_AMT                                 05821001
                     , DR_ALW_RTL_AMT                                   05822001
                     , RTV_RTL_AMT                                      05823001
                     , MKUP_INIT_AMT                                    05824001
                     , MKUP_NET_AMT                                     05825001
                     , INV_ADJ_RTL_AMT                                  05826001
                     , ONORD_RTL_AMT                                    05827001
                     , ONORD_COST_AMT                                   05828001
                     , SHNK_RTL_AMT                                     05829001
                     , END_INV_RTL_AMT   )                              05830001
               VALUES                                                   05840001
                   (   :ML157-WK-PARTN-NBR                              05850001
                     , :ML157-DEPT-NBR                                  05860001
                     , :ML157-MAJ-CL-NBR                                05870001
                     , :ML157-SUB-CL-NBR                                05880001
                     , :ML157-LOC-NBR                                   05890001
                     , :ML157-ENT-ID                                    05900001
                     , :ML157-FSCL-WK-END-DTE                           05910001
                     , :ML157-SLS-REG-RTL-AMT                           05920001
                     , :ML157-SLS-PROMO-RTL-AMT                         05930001
                     , :ML157-SLS-PROCLR-RTL-AMT                        05940001
                     , :ML157-SLS-CLR-RTL-AMT                           05950001
                     , :ML157-SLS-OTH-RTL-AMT                           05960001
                     , :ML157-MKDN-PLU-RTL-AMT                          05970001
                     , :ML157-MKDN-PROMO-RTL-AMT                        05980001
                     , :ML157-MKDN-RGST-RTL-AMT                         05990001
                     , :ML157-MKDN-GLDST-RTL-AMT                        06000001
                     , :ML157-MKDN-UMTCH-RTL-AMT                        06010001
                     , :ML157-MKDN-BYR-RTL-AMT                          06020001
                     , :ML157-MKDN-STR-RTL-AMT                          06030001
                     , :ML157-MKDN-PRPT-RTL-AMT                         06040001
                     , :ML157-MKDN-OTH-RTL-AMT                          06050001
                     , :ML157-MKDN-RTL-AMT                              06060001
                     , :ML157-MKUP-RTL-AMT                              06070001
                     , :ML157-XFER-IN-RTL-AMT                           06080001
                     , :ML157-XFER-OUT-RTL-AMT                          06090001
                     , :ML157-RCPT-RTL-AMT                              06100001
                     , :ML157-RCPT-NET-COST-AMT                         06110001
                     , :ML157-RCPT-GRO-COST-AMT                         06120001
                     , :ML157-PADJ-RTL-AMT                              06130001
                     , :ML157-PADJ-FRGT-COST-AMT                        06140001
                     , :ML157-PADJ-DISC-COST-AMT                        06150001
                     , :ML157-PADJ-RTV-COST-AMT                         06160001
                     , :ML157-PADJ-RTV-RTL-AMT                          06170001
                     , :ML157-PADJ-DA-COST-AMT                          06171001
                     , :ML157-PADJ-DA-RTL-AMT                           06172001
                     , :ML157-PADJ-NET-COST-AMT                         06173001
                     , :ML157-PADJ-OTH-COST-AMT                         06174001
                     , :ML157-PADJ-OTH-RTL-AMT                          06175001
                     , :ML157-DR-ALW-RTL-AMT                            06176001
                     , :ML157-RTV-RTL-AMT                               06177001
                     , :PV-MKUP-INIT-AMT                                06178001
                     , :PV-MKUP-NET-AMT                                 06179001
                     , :ML157-INV-ADJ-RTL-AMT                           06180001
                     , 0                                                06190001
                     , 0                                                06200001
                     , 0                                                06210001
                     , 0                       )                        06220001
           END-EXEC                                                     06230001
      *                                                                 06240001
           EVALUATE SQLCODE                                             06250001
               WHEN 0                                                   06260001
                   CONTINUE                                             06270001
               WHEN -911                                                06280001
                   ADD +1             TO PV-DEADLOCK-COUNT              06290001
                   DISPLAY '*** -911 OCCURRED ON INSERT ***'            06300001
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              06310001
                       MOVE '7200-INSERT-MLT00015-ROW' TO               06320001
                                                    PV-PARAGRAPH-NAME   06330001
                       PERFORM 9000-DEADLOCK-ERROR                      06340001
                   ELSE                                                 06350001
                       PERFORM 7999-RESTART-PROCESS                     06360001
                   END-IF                                               06370001
               WHEN OTHER                                               06380001
                   MOVE '7200-INSERT-MLT00015-ROW' TO PV-PARAGRAPH-NAME 06390001
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED06400001
                   PERFORM 9999-SQL-ABEND                               06410001
           END-EVALUATE.                                                06420001
                                                                        06430001
           ADD 1 TO PA-INSERT-COUNT.                                    06440001
                                                                        06450001
      ******************************************************************06460001
      * 7300-GET-COMMIT-TIMESTAMP.                                     *06470001
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *06480001
      *            CONTROL TABLE                                       *06490001
      ******************************************************************06500001
       7300-GET-COMMIT-TIMESTAMP.                                       06510001
                                                                        06520001
           EXEC SQL                                                     06530001
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        06540001
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       06550001
               INTO :PV-COMMIT-TIMESTAMP                                06560001
               FROM TCKRSTCNTL                                          06570001
              WHERE JOB_NAME  = :PC-JOB-NAME                            06580001
                AND PLAN_NAME = :PC-PROGRAM-NAME                        06590001
           END-EXEC.                                                    06600001
                                                                        06610001
           EVALUATE TRUE                                                06620001
               WHEN SQLCODE = 0                                         06630001
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  06640001
                   CONTINUE                                             06650001
               WHEN SQLCODE NOT EQUAL ZERO                              06660001
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME06670001
                   PERFORM 9999-SQL-ABEND                               06680001
           END-EVALUATE.                                                06690001
      *                                                                 06700001
      *                                                                 06710001
      ******************************************************************06720001
      * 7400-INIT-CHECKPOINT-SELECT                                    *06730001
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *06740001
      *            IF WE ARE IN A RESTART CONDITION.                   *06750001
      ******************************************************************06760001
       7400-INIT-CHECKPOINT-SELECT.                                     06770001
                                                                        06780001
           EXEC SQL                                                     06790001
             SELECT  CKPT_STAT_CODE                                     06800001
                    ,CKPT_FRQNCY_QTY                                    06810001
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         06820001
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        06830001
               FROM  TCKRSTCNTL                                         06840001
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06850001
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06860001
           END-EXEC.                                                    06870001
                                                                        06880001
           IF SQLCODE = 0                                               06890001
               CONTINUE                                                 06900001
           ELSE                                                         06910001
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  06920001
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     06930001
               PERFORM 9999-SQL-ABEND                                   06940001
           END-IF.                                                      06950001
      *                                                                 06960001
      *                                                                 06970001
      ******************************************************************06980001
      * 7500-SELECT-RESTART-INFO                                       *06990001
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *07000001
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *07010001
      ******************************************************************07020001
       7500-SELECT-RESTART-INFO.                                        07030001
                                                                        07040001
           EXEC SQL                                                     07050001
             SELECT  WORK_STRG_DESC                                     07060001
               INTO  :TCKRSTINF-WORK-STRG-DESC                          07070001
               FROM  TCKRSTINF                                          07080001
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07090001
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07100001
           END-EXEC.                                                    07110001
                                                                        07120001
           IF SQLCODE = 0                                               07130001
               CONTINUE                                                 07140001
           ELSE                                                         07150001
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   07160001
               PERFORM 9999-SQL-ABEND                                   07170001
           END-IF.                                                      07180001
                                                                        07190001
      ******************************************************************07200001
      * 7600-SET-CURRENT-TIMESTAMP.                                    *07210001
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *07220001
      ******************************************************************07230001
       7600-SET-CURRENT-TIMESTAMP.                                      07240001
                                                                        07250001
           EXEC SQL                                                     07260001
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           07270001
           END-EXEC.                                                    07280001
                                                                        07290001
           IF SQLCODE = 0                                               07300001
               CONTINUE                                                 07310001
           ELSE                                                         07320001
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 07330001
               PERFORM 9999-SQL-ABEND                                   07340001
           END-IF.                                                      07350001
      *                                                                 07360001
      *                                                                 07370001
      ******************************************************************07380001
      * 7700-COMMIT-PROCESSING.                                        *07390001
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *07400001
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*07410001
      ******************************************************************07420001
       7700-COMMIT-PROCESSING.                                          07430001
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     07440001
                                                                        07450001
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          07460001
                                                                        07470001
      * PREPARE COMMIT RECORD FOR UPDATE                                07480001
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                07490001
               MOVE ML157-WK-PARTN-NBR        TO CR-PARTN-NBR           07500001
               MOVE ML157-DEPT-NBR            TO CR-DEPT-NBR            07510001
               MOVE ML157-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          07520001
               MOVE ML157-SUB-CL-NBR          TO CR-SUB-CL-NBR          07530001
               MOVE ML157-LOC-NBR             TO CR-LOC-NBR             07540001
               MOVE ML157-ENT-ID              TO PV-ENT-ID              07550001
               MOVE PV-ENT-ID                 TO CR-ENT-ID              07560001
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    07570001
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  07580001
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       07590001
                                             TCKRSTINF-WORK-STRG-DESC   07600001
               IF PS-FIRST-COMMIT                                       07610001
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                07620001
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                07630001
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       07640001
               END-IF                                                   07650001
               PERFORM 7800-COMMIT-CHANGES                              07660001
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        07670001
           END-IF.                                                      07680001
      *                                                                 07690001
      *                                                                 07700001
      ******************************************************************07710001
      * 7800-COMMIT-CHANGES.                                            07720001
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      07730001
      *            TAKE A COMMIT.                                       07740001
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    07750001
      ******************************************************************07760001
       7800-COMMIT-CHANGES.                                             07770001
                                                                        07780001
           EXEC SQL                                                     07790001
             UPDATE TCKRSTINF                                           07800001
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          07810001
              WHERE JOB_NAME       = :PC-JOB-NAME                       07820001
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   07830001
           END-EXEC.                                                    07840001
                                                                        07850001
           IF SQLCODE = 0                                               07860001
               CONTINUE                                                 07870001
           ELSE                                                         07880001
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED07890001
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07900001
               PERFORM 9999-SQL-ABEND                                   07910001
           END-IF.                                                      07920001
                                                                        07930001
           EXEC SQL                                                     07940001
               COMMIT                                                   07950001
           END-EXEC.                                                    07960001
                                                                        07970001
           IF SQLCODE = 0                                               07980001
               ADD 1 TO PA-COMMIT-COUNT                                 07990001
           ELSE                                                         08000001
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED08010001
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     08020001
               PERFORM 9999-SQL-ABEND                                   08030001
           END-IF.                                                      08040001
      *                                                                 08050001
      ******************************************************************08060001
      * 7900-UPDATE-CHECKPOINT-STATUS                                   08070001
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   08080001
      ******************************************************************08090001
       7900-UPDATE-CHECKPOINT-STATUS.                                   08100001
                                                                        08110001
           EXEC SQL                                                     08120001
             UPDATE  TCKRSTCNTL                                         08130001
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        08140001
              WHERE  JOB_NAME  = :PC-JOB-NAME                           08150001
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       08160001
           END-EXEC.                                                    08170001
                                                                        08180001
           IF SQLCODE = 0                                               08190001
               CONTINUE                                                 08200001
           ELSE                                                         08210001
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME08220001
               PERFORM 9999-SQL-ABEND                                   08230001
           END-IF.                                                      08240001
                                                                        08250001
           EJECT                                                        08260001
      *                                                                 08270001
      *                                                                 08280001
      ******************************************************************08290001
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST MLT_WKLY_VND ROW FOR  08300001
      * UPDATE.  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST  08310001
      * COMMIT.                                                         08320001
      ******************************************************************08330001
       7999-RESTART-PROCESS.                                            08340001
      *                                                                 08350001
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           08360001
      *                                                                 08370001
           CLOSE CONDENSED-FILE.                                        08380001
      *                                                                 08390001
           PERFORM 7500-SELECT-RESTART-INFO.                            08400001
      *                                                                 08410001
           DISPLAY '**** RESTART **** '.                                08420001
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 08430001
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   08440001
                                                                        08450001
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      08460001
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        08470001
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       08480001
           IF PS-FIRST-COMMIT                                           08490001
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)               08500001
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     08510001
                      ' BEGINNING'                                      08520001
           ELSE                                                         08530001
               DISPLAY 'TCKRSTINF-LAST CKPT '                           08540001
                        TCKRSTINF-WORK-STRG-DESC (1:35)                 08550001
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 08560001
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         08570001
                    CR-CHECKPOINT-RESTART-AREA                          08580001
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                08590001
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   08600001
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 08610001
               DISPLAY 'SUB CLASS NBR   ' CR-SUB-CL-NBR                 08620001
               DISPLAY 'LOCATION NBR    ' CR-LOC-NBR                    08630001
               DISPLAY 'ENT-ID          ' CR-ENT-ID                     08640001
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           08650001
           END-IF.                                                      08660001
                                                                        08670001
           OPEN INPUT CONDENSED-FILE.                                   08680001
           MOVE ZEROES                          TO PA-INPUT-COUNT.      08690001
      *                                                                 08700001
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          08710001
           PERFORM 4000-READ-CONDENSED-FILE                             08720001
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              08730001
           DISPLAY 'INPUT RECORD RESTARTED ON RECORD '                  08740001
                    PA-INPUT-COUNT                                      08750001
           DISPLAY 'PARTITION NBR         ' ML157-WK-PARTN-NBR.         08760001
           DISPLAY 'DEPARTMENT NBR        ' ML157-DEPT-NBR.             08770001
           DISPLAY 'MAJOR CLASS NBR       ' ML157-MAJ-CL-NBR.           08780001
           DISPLAY 'SUB CLASS NBR         ' ML157-SUB-CL-NBR.           08790001
           DISPLAY 'LOCATION NBR          ' ML157-LOC-NBR.              08800001
           MOVE ML157-ENT-ID TO PV-ENT-ID.                              08810001
           DISPLAY 'ENT-ID                ' PV-ENT-ID.                  08820001
      ******************************************************************08830001
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *08840001
      ******************************************************************08850001
       8000-EOJ-ROUTINE.                                                08860001
      *                                                                 08870001
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       08880001
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       08890001
                                                                        08900001
      *                                                                 08910001
           DISPLAY SPACES.                                              08920001
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         08930001
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        08940001
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        08950001
           DISPLAY SPACES.                                              08960001
      *                                                                 08970001
           CLOSE CONDENSED-FILE.                                        08980001
      *                                                                 08990001
      *                                                                 09000001
      ******************************************************************09010001
      *  PERFORMED BY 7100-UPDATE AND 7200-INSERT                       09020001
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   09030001
      ******************************************************************09040001
       9000-DEADLOCK-ERROR.                                             09050001
      *                                                                 09060001
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     09070001
               PERFORM 9999-SQL-ABEND.                                  09080001
      *                                                                 09090001
      ******************************************************************09100001
      *  STANDARD DB2 ERROR ABEND ROUTINE                               09110001
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             09120001
      ******************************************************************09130001
       9999-SQL-ABEND.                                                  09140001
                                                                        09150001
           MOVE +4013                 TO ABEND-CODE.                    09160001
           DISPLAY '**  ABEND     **'.                                  09170001
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              09180001
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            09190001
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       09200001
                                                                        09210001
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         09220001
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        09230001
                                                                        09240001
           COPY DPPD004.                                                09250001
           CALL 'ILBOABN0'  USING ABEND-CODE.                           09260001
