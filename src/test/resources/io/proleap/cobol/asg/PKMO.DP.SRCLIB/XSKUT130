      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.         XSKUT130.                                            
       AUTHOR.             RICH KELLEN.                                         
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       MAR, 2009.                                           
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM PREPARES THE XST-SKU-LOC PURGE FILE TO BE         *        
      * PROCESSED BY XSKUP130 UNDER THE DB2 CHECKPOINT RESTART SYSTEM. *        
      *                                                                *        
      * THE PROGRAM READS THE XST-SKU-LOC PURGE RECS, REFORMATS THE    *        
      * RECORDS AND SETS A CHECKPOINT INDICATOR ON THE RECORD BASED    *        
      * ON HOW CHECKPOINTS WILL BE TAKEN BY THE MAINTENANCE PROGRAM.   *        
      * AFTER A RECORD HAS BEEN REFORMATTED IT IS PASSED TO THE        *        
      * TRANSACTION PREPARATION MODULE (DPKUT900) WHICH DOES SOME MORE *        
      * PROCESSING ON THE RECORDS AND WRITES THE REFORMATTED RECORDS   *        
      * OUT TO A SEQUENTIAL FILE.                                      *        
      *                                                                *        
      * THE PROGRAM COMMUNICATES WITH THE TRANSACTION PREPARATION      *        
      * MODULE USING FUNCTION CODES (OPEN, WRITE, & CLOSE) AND CHECKS  *        
      * THE RETURN CODE AFTER EACH REQUEST.  IF AN INVALID RETURN CODE *        
      * IS RECEIVED, ANY INFORMATION THAT MAY BE HELPFUL IN RESOLVING  *        
      * THE BAD RETURN CODE IS DISPLAYED ON SYSOUT AND THE PROGRAM     *        
      * ABENDS.                                                        *        
      *                                                                *        
      * INPUT FILES:   XST-SKU-LOC-PRGE-FL            DDNAME = INP01   *        
      *                                                                *        
      * OUTPUT FILES:  N/A                                             *        
      *                                                                *        
      ******************************************************************        
260107*    JIM HUNTER 08-19-21     ADD COPYBOOK DPWS990 TO MAKE        *        
260107*    CHG0260107              DPKUT900 CALL DYNAMIC.              *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT XST-SKU-LOC-PRGE-FL ASSIGN TO INP01.                          
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  XST-SKU-LOC-PRGE-FL                                                  
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
                                                                                
       01  XST-SKU-LOC-PRGE-UPD-REC PIC X(058).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04) VALUE +0  COMP.          
                                                                                
      ******************************************************************        
      *PC - PROGRAM CONSTANTS                                                   
      ******************************************************************        
       01  PROGRAM-CONSTANTS.                                                   
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'XSKUT130'.        
           05  PC-TRAN-PREP-FUNC-CODES.                                         
               10  PC-TRAN-PREP-FUNC-OPEN   PIC X(05)  VALUE 'OPEN '.           
               10  PC-TRAN-PREP-FUNC-WRITE  PIC X(05)  VALUE 'WRITE'.           
               10  PC-TRAN-PREP-FUNC-CLOSE  PIC X(05)  VALUE 'CLOSE'.           
           05  PC-JOB-NAME                  PIC X(08)  VALUE 'XSK111  '.        
           05  PC-PLAN-NAME                 PIC X(08)  VALUE 'XSKUP130'.        
           05  PC-TRANS-RECORD-LENGTH       PIC S9(04) VALUE +058 COMP.         
                                                                                
      ******************************************************************        
      *PV - PROGRAM VARIABLES                                                   
      ******************************************************************        
       01  PROGRAM-VARIABLES.                                                   
           05  PV-CHECKPOINT-TYPE           PIC X(01)  VALUE SPACE.             
               88  CONTROL-BREAK                       VALUE 'C'.               
               88  LOGICAL-TRANSACTION                 VALUE 'L'.               
               88  TIME-INTERVAL                       VALUE 'T'.               
               88  REQUESTED-BY-PROGRAM                VALUE 'R'.               
           05  PV-HOLD-UPC-ID          PIC  S9(9) COMP VALUE ZERO.              
                                                                                
      ******************************************************************        
      *PA - PROGRAM ACCUMULATORS                                                
      ******************************************************************        
       01  PROGRAM-ACCUMULATORS.                                                
           05  PA-INPUT-RECORDS-READ        PIC S9(11) VALUE 0  COMP-3.         
                                                                                
           05  PA-OUTPUT-RECS-WRITTEN       PIC S9(11) VALUE 0  COMP-3.         
                                                                                
                                                                                
      ******************************************************************        
      *PS - PROGRAM SWITCHES                                                    
      ******************************************************************        
       01  PROGRAM-SWITCHES.                                                    
           05  PS-INPUT-UPDATE-EOF-SW       PIC X(01)  VALUE 'N'.               
               88  PS-INPUT-UPDATE-EOF                 VALUE 'Y'.               
           05  PS-FIRST-TRANS-READ-SW       PIC X(01)  VALUE 'Y'.               
               88  FIRST-TRANS-READ                    VALUE 'Y'.               
                                                                                
      ******************************************************************        
      *  XST-SKU-LOC-PRGE-UPD-REC LAYOUT                               *        
      ******************************************************************        
           COPY XSRD130.                                                        
                                                                                
      ******************************************************************        
      *  TRANSACTION PREPARATION MODULE LINKAGE SECTION                *        
      ******************************************************************        
260107     COPY DPWS990.                                                        
                                                                                
           COPY DPLS000.                                                        
                                                                                
      ******************************************************************        
      *  SQL COMMUNICATIONS WORK AREA                                  *        
      ******************************************************************        
           COPY SQLCA2.                                                         
                                                                                
       EJECT                                                                    
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      * A100-MAINLINE-PROCESSING                                                
      *    CONTROLS FLOW OF THE PROGRAM                                *        
      ******************************************************************        
       A100-MAINLINE-PROCESSING.                                                
                                                                                
           PERFORM B100-INITIALIZE.                                             
                                                                                
           PERFORM B200-PROCESS-UPDATE-INPUT-FILE                               
               UNTIL PS-INPUT-UPDATE-EOF.                                       
                                                                                
           PERFORM B300-END-OF-JOB-ROUTINE.                                     
                                                                                
           GOBACK.                                                              
       EJECT                                                                    
      ******************************************************************        
      * B100-INITIALIZE                                                *        
      ******************************************************************        
       B100-INITIALIZE.                                                         
                                                                                
           PERFORM C100-ISSUE-OPEN-REQUEST.                                     
                                                                                
           OPEN INPUT XST-SKU-LOC-PRGE-FL.                                      
                                                                                
           PERFORM C200-READ-UPDATE-INPUT-FILE.                                 
           IF PS-INPUT-UPDATE-EOF                                               
               DISPLAY ' '                                                      
               DISPLAY 'MESSAGES FOR PROGRAM ' PC-PROGRAM-NAME                  
               DISPLAY 'UPDATE INPUT FILE IS EMPTY'                             
               DISPLAY ' '                                                      
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * B200-PROCESS-UPDATE-INPUT-FILE.                                *        
      *     READ AN UPDATE INPUT RECORD.                               *        
      *     IF END-OF-FILE, STOP PROCESSING.                           *        
      *     IF A RECORD IS READ, GATHER WHATEVER OTHER DATA            *        
      *     IS NECESSARY TO TAKE CHECKPOINTS BASED ON THE TYPE OF      *        
      *     CHECKPOINTS BEING TAKEN BY XSKUP130 AND ISSUE A WRITE      *        
      *     REQUEST TO THE TRANSACTION PREPARATION PROGRAM.            *        
      ******************************************************************        
       B200-PROCESS-UPDATE-INPUT-FILE.                                          
                                                                                
           IF FIRST-TRANS-READ                                                  
               MOVE 'N'                        TO PS-FIRST-TRANS-READ-SW        
           END-IF.                                                              
                                                                                
           IF CONTROL-BREAK                                                     
               PERFORM C300-CKPT-BY-CNTL-BREAK                                  
           ELSE                                                                 
               IF LOGICAL-TRANSACTION                                           
                   PERFORM C400-CKPT-BY-LOGICAL-TRANS                           
               ELSE                                                             
                   PERFORM C500-CKPT-BY-TIME-OR-REQUEST                         
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
           PERFORM C200-READ-UPDATE-INPUT-FILE.                                 
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * B300-END-OF-JOB-ROUTINE                                        *        
      ******************************************************************        
       B300-END-OF-JOB-ROUTINE.                                                 
                                                                                
           CLOSE XST-SKU-LOC-PRGE-FL.                                           
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'MESSAGES FOR PROGRAM ' PC-PROGRAM-NAME.                     
           DISPLAY 'NUMBER OF INPUT RECORDS READ:          '                    
                   PA-INPUT-RECORDS-READ.                                       
           DISPLAY 'NUMBER OF TRANS/PREP RECORDS WRITTEN:  '                    
                   PA-OUTPUT-RECS-WRITTEN.                                      
           DISPLAY ' '.                                                         
                                                                                
           PERFORM C600-ISSUE-CLOSE-REQUEST.                                    
                                                                                
      ******************************************************************        
      * C100-ISSUE-OPEN-REQUEST                                        *        
      *     ISSUE AN 'OPEN' REQUEST TO THE TRANSACTION PREPARATION     *        
      *     MODULE.                                                    *        
      ******************************************************************        
       C100-ISSUE-OPEN-REQUEST.                                                 
                                                                                
           MOVE PC-TRAN-PREP-FUNC-OPEN TO DP000-FUNC-CODE.                      
           MOVE PC-JOB-NAME            TO DP000-JOB-NAME.                       
           MOVE PC-PLAN-NAME           TO DP000-PLAN-NAME.                      
                                                                                
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
                                                                                
           MOVE DP000-CKPT-TYPE-CODE   TO PV-CHECKPOINT-TYPE.                   
                                                                                
      ******************************************************************        
      * C200-READ-UPDATE-INPUT-FILE                                    *        
      ******************************************************************        
       C200-READ-UPDATE-INPUT-FILE.                                             
                                                                                
           READ XST-SKU-LOC-PRGE-FL                                             
                INTO XS130-XST-SKU-LOC-PRGE-REC                                 
               AT END                                                           
                   SET PS-INPUT-UPDATE-EOF TO TRUE.                             
                                                                                
           IF PS-INPUT-UPDATE-EOF                                               
               CONTINUE                                                         
           ELSE                                                                 
               ADD +1 TO PA-INPUT-RECORDS-READ                                  
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * C300-CKPT-BY-CNTL-BREAK                                        *        
      *     THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSAC- *        
      *     TION RECORD "ON" IF THERE IS A CHANGE IN THE STORE         *        
      *     NUMBER.  AFTER THE TRANSACTION RECORD HAS BEEN FORMATTED,  *        
      *     A WRITE REQUEST IS ISSUED TO THE TRANSACTION PREPARATION   *        
      *     MODULE.                                                    *        
      * NOTE: AT THIS TIME CONTROL/LOGIC BREAK PROCESSING IS IDENTICAL.*        
      ******************************************************************        
       C300-CKPT-BY-CNTL-BREAK.                                                 
                                                                                
           IF XS130-SKU-LOC-UPC-ID = PV-HOLD-UPC-ID                             
               MOVE 'N' TO DP000-CHECKPOINT-IND                                 
           ELSE                                                                 
               MOVE 'Y' TO DP000-CHECKPOINT-IND                                 
               MOVE XS130-SKU-LOC-UPC-ID TO PV-HOLD-UPC-ID                      
           END-IF.                                                              
                                                                                
           MOVE PC-TRANS-RECORD-LENGTH  TO DP000-TRANS-REC-LENGTH.              
           MOVE XS130-XST-SKU-LOC-PRGE-REC TO DP000-TRANS-REC.                  
           MOVE PC-TRAN-PREP-FUNC-WRITE TO DP000-FUNC-CODE.                     
           MOVE PC-JOB-NAME             TO DP000-JOB-NAME.                      
           MOVE PC-PLAN-NAME            TO DP000-PLAN-NAME.                     
                                                                                
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
           ADD +1 TO PA-OUTPUT-RECS-WRITTEN.                                    
                                                                                
      ******************************************************************        
      * C400-CKPT-BY-LOGICAL-TRANS.                                    *        
      *     THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSAC- *        
      *     TION RECORD "ON".  THE UPDATE PROGRAM WILL CHECK THE       *        
      *     CHECKPOINT FREQUENCY TO KNOW WHETHER A CHECKPOINT SHOULD BE*        
      *     TAKEN.   AFTER THE TRANSACTION RECORD HAS BEEN FORMATTED,  *        
      *     A WRITE REQUEST IS ISSUED TO THE TRANSACTION PREPARATION   *        
      *     MODULE.                                                    *        
      ******************************************************************        
       C400-CKPT-BY-LOGICAL-TRANS.                                              
                                                                                
           MOVE 'Y'                     TO DP000-CHECKPOINT-IND.                
                                                                                
           MOVE PC-TRANS-RECORD-LENGTH  TO DP000-TRANS-REC-LENGTH.              
           MOVE XS130-XST-SKU-LOC-PRGE-REC TO DP000-TRANS-REC.                  
           MOVE PC-TRAN-PREP-FUNC-WRITE TO DP000-FUNC-CODE.                     
           MOVE PC-JOB-NAME             TO DP000-JOB-NAME.                      
           MOVE PC-PLAN-NAME            TO DP000-PLAN-NAME.                     
                                                                                
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
           ADD +1 TO PA-OUTPUT-RECS-WRITTEN.                                    
                                                                                
                                                                                
      ******************************************************************        
      * C500-CKPT-BY-TIME-OR-REQUEST.                                  *        
      *     THIS ROUTINE BUILDS THE TRANSACTION RECORD AND ISSUES A    *        
      *     WRITE REQUEST TO THE TRANSACTION PREPARATION MODULE IF     *        
      *     CHECKPOINTS ARE TAKEN BY TIME INTERVAL (SECONDS) OR        *        
      *     REQUESTED BY THE MAINTENANCE PROGRAM.                      *        
      ******************************************************************        
       C500-CKPT-BY-TIME-OR-REQUEST.                                            
                                                                                
           MOVE 'N'                     TO DP000-CHECKPOINT-IND.                
           MOVE PC-TRANS-RECORD-LENGTH     TO DP000-TRANS-REC-LENGTH.           
           MOVE XS130-XST-SKU-LOC-PRGE-REC TO DP000-TRANS-REC.                  
           MOVE PC-TRAN-PREP-FUNC-WRITE    TO DP000-FUNC-CODE.                  
           MOVE PC-JOB-NAME                TO DP000-JOB-NAME.                   
           MOVE PC-PLAN-NAME               TO DP000-PLAN-NAME.                  
                                                                                
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
           ADD +1 TO PA-OUTPUT-RECS-WRITTEN.                                    
                                                                                
      ******************************************************************        
      * C600-ISSUE-CLOSE-REQUEST                                       *        
      *     ISSUE A 'CLOSE' REQUEST TO THE TRANSACTION PREPARATION     *        
      *     MODULE.                                                    *        
      ******************************************************************        
       C600-ISSUE-CLOSE-REQUEST.                                                
                                                                                
           MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.                     
           MOVE PC-JOB-NAME             TO DP000-JOB-NAME.                      
           MOVE PC-PLAN-NAME            TO DP000-PLAN-NAME.                     
                                                                                
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
                                                                                
      ******************************************************************        
      *  TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES        *        
      ******************************************************************        
           COPY DPPD000.                                                        
                                                                                
