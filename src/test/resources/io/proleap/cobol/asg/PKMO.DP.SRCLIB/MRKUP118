000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000430                                                                          
000500 PROGRAM-ID.             MRKUP118.                                        
000600 AUTHOR.                 A KINTOPF.                                       
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000800 DATE-WRITTEN            03-29-01.                                        
000900 DATE-COMPILED.                                                           
001000                                                                          
001100*****************************************************************         
001200*  UPDATE FT CODE ON TMRITST TABLE FOR REGIONAL CLEARANCE ITEMS *         
001300*                                                               *         
001400*  THIS PROGRAM WILL UPDATE THE FT CODE ON THE REPLENISHMENT    *         
001500*  TO 'X' (OFF) WHEN AN E-COMMERCE SKU/STORE GOES INTO CLEARANCE*         
001600*  STATUS (30) ON THE UPC/STORE TABLE.                          *         
001800*                                                               *         
001900*****************************************************************         
002000* 03/29/01 - AOK               NEW PROGRAM - COPIED MRKUP097    *         
002400*****************************************************************         
002410* 05/21/2002 - DON TOMLINSON  NI-SKU MR PHASE 4 CHANGES         *         
002420* -  TSKXREF ADDED.                                             *         
002430* -  ADDED ISWS006 COPYBOOK TO HANDLE DIFFERENT FORMATS / SIZES *         
002440*    IN FIELDS.                                                 *         
002450* -  SQL CHANGED FOR WHERE STATEMENTS TO REFERENCE DCLGEN       *         
002460*    FIELDS INSTEAD OF PROGRAM VARAIABLES IN WORKING STORAGE    *         
002470* -  REPORT FORMAT CHANGED TO ZERO SUPPRESS FOR DEPT AND SKU.   *         
002480* -  REPORT NOW HAS SUBCLASS AND SKU ON DETAIL LINE.            *         
002400*****************************************************************         
002410* 07/30/2004 - BOB FORD  STORE NUMBER EXPANSION  WR#26363       *         
002420* -  CHANGED REFERENCES FROM TSTR000 TO XMT_LOC                 *         
002430* -  CHANGED DL-STORE-NO TO 9(5).                               *         
002400*****************************************************************         
002410* 03/06/2005 - MARIA KLAMIK                 PM: PM00857302      *         
002410*              ADDED CODE TO RESEARCH ERROR.                    *         
002410*              TOOK OUT REFERENCE TO XMT_LOC IN PARA            *         
002410*              D400-FETCH-STORE-DATA.                           *         
002410*              CODE WOULD RETURN MULTIPLE ROWS - CARTESIAN JOIN *         
002490*****************************************************************         
002500 ENVIRONMENT DIVISION.                                                    
002600*****************************************************************         
002700                                                                          
002800 CONFIGURATION SECTION.                                                   
002900                                                                          
003000 SOURCE-COMPUTER.        IBM-3090.                                        
003100 OBJECT-COMPUTER.        IBM-3090.                                        
003200                                                                          
003300 INPUT-OUTPUT SECTION.                                                    
003400                                                                          
003500 FILE-CONTROL.                                                            
003600                                                                          
003700     SELECT PRINT-REPORT          ASSIGN TO RPT01.                        
003800*                                                                         
003900*****************************************************************         
004000 DATA DIVISION.                                                           
004100*****************************************************************         
004200*                                                                         
004300 FILE SECTION.                                                            
004400                                                                          
004500 FD  PRINT-REPORT                                                         
004600     RECORDING MODE F                                                     
004700     BLOCK CONTAINS 0 RECORDS                                             
004800     RECORD CONTAINS 132 CHARACTERS                                       
004900     LABEL RECORDS ARE OMITTED                                            
005000     DATA RECORD IS PRINT-LINE.                                           
005100                                                                          
005200 01  PRINT-LINE                           PIC X(132).                     
005300*                                                                         
005400******************************************                                
005500 WORKING-STORAGE SECTION.                                                 
005600******************************************                                
005700*                                                                         
005800*    COPY MRRD001I.                                                       
005900*                                                                         
006000*01 SYSTEM-RECORD.                                                        
006100*    COPY MRRD002I.                                                       
006200******************************************                                
006300* STANDARD HEADER LINE - KOHLS 132 COL                                    
006400******************************************                                
006500     COPY DPWS132.                                                        
007100                                                                          
007200 01  FILLER.                                                              
007300     05  FILLER                       PIC  X(36)   VALUE                  
007400         ' ***** WORKING STORAGE PGM=MRKUP118 '.                          
007500     05  FILLER                       PIC  X(10)   VALUE                  
007600         ' LOCATION='.                                                    
007700     05  PGM-LOCATION                 PIC  X(04)   VALUE SPACE.           
007800     05  FILLER                       PIC  X(06)   VALUE                  
007900         ' *****'.                                                        
008000                                                                          
008100 01  PROGRAM-SWITCHES.                                                    
008200     05  PS-EOC-ITEM-CSR              PIC  X(01)   VALUE 'N'.             
008300         88  EOC-ITEM-CSR                          VALUE 'Y'.             
008400                                                                          
009100     05  PS-STORE-FOUND               PIC  X(01)   VALUE 'Y'.             
009200         88  STORE-FOUND                           VALUE 'Y'.             
009210         88  NO-STORE-FOUND                        VALUE 'N'.             
009300                                                                          
009400 01  PC-CONSTANTS.                                                        
009700     05  PC-PLUS-1                    PIC S9(04)   VALUE +1               
009800                                                   COMP SYNC.             
009900     05  PC-ROW-NOT-FOUND             PIC S9(04)   VALUE +100             
010000                                                   COMP SYNC.             
010500     05  PC-FT-X                      PIC  X(01)   VALUE 'X'.             
010600     05  PC-OP-QTY-0                  PIC S9(9)    VALUE ZEROS            
010700                                                   COMP-3.                
010800     05  PC-OUTL-QTY-0                PIC S9(9)    VALUE ZEROS            
010900                                                   COMP-3.                
011000                                                                          
011100     05  PC-MAX-LINES                 PIC S9(3)    VALUE +57.             
011200                                                                          
011300 01  PV-ACCUMULATOR-FIELDS.                                               
011400     05  PV-CHANGE-CNT                PIC S9(07)   VALUE ZERO             
011500                                                   COMP-3.                
011600     05  PV-CURR-TMST                 PIC  X(26)   VALUE SPACES.          
011700     05  PV-COMMIT-TMST               PIC  X(26)   VALUE SPACES.          
012300*                                                                         
012800 01  PV-SCHED-DATE.                                                       
012900     05  PV-SCHED-CCYY                PIC  9(4)    VALUE ZEROS.           
013000     05  PV-SCHED-FILLER              PIC  X(1)    VALUE SPACE.           
013100     05  PV-SCHED-MM                  PIC  9(2)    VALUE ZEROS.           
013200     05  PV-SCHED-FILLER              PIC  X(1)    VALUE SPACE.           
013201     05  PV-SCHED-DD                  PIC  9(2)    VALUE ZEROS.           
013202*                                                                         
013600 01  CT-CPU-TIME.                                                         
013700     05  CT-CPU-HR                    PIC  9(2)    VALUE ZEROS.           
013800     05  CT-CPU-MIN                   PIC  9(2)    VALUE ZEROS.           
013900     05  CT-CPU-SEC                   PIC  9(2)    VALUE ZEROS.           
014000     05  CT-CPU-HSEC                  PIC  9(2)    VALUE ZEROS.           
014100*                                                                         
014600 01  PV-MISC-FIELDS.                                                      
014700     05  PV-ABEND-CODE                PIC S9(04)   VALUE ZERO             
014800                                                   COMP SYNC.             
014900     05  PV-ABEND-PARAGRAPH           PIC  X(30)   VALUE SPACES.          
015000                                                                          
015500     05  PV-CLASS-NBR                 PIC  X(02)   VALUE SPACES.          
015600                                                                          
015900     05  PV-DEPT-DESC                 PIC  X(25)   VALUE SPACES.          
016000                                                                          
016100     05  PV-DEPT-NBR                  PIC  X(03)   VALUE SPACES.          
016200                                                                          
016300     05  PV-DEPT-NBR-HOLD             PIC  X(03)   VALUE SPACES.          
016400                                                                          
017600     05  PV-LINE-CNT                  PIC S9(04)   VALUE ZEROS            
017700                                                   COMP-3.                
018400     05  PV-PAGE-CNT                  PIC  9(04)   VALUE ZEROS            
018500                                                   COMP-3.                
019400     05  PV-SKU                       PIC  9(08)   VALUE ZERO.            
019500                                                                          
019600     05  PV-SKU-DESC                  PIC  X(25)   VALUE SPACES.          
019700                                                                          
020201     05  PV-STAT-CDE                  PIC  9(03)   VALUE ZERO.            
020202         88 MIXED                                  VALUE 25.              
020203         88 CLEARANCE                              VALUE 30.              
020204         88 DISCONTINUED                           VALUE 90.              
020205                                                                          
020700 01  HL-HEADING-LINE1.                                                    
020800     05  FILLER                       PIC  X(39)   VALUE SPACE.           
020900     05  FILLER                       PIC  X(55)   VALUE                  
021000     'ACTIVE TO CLEARANCE OR DISCONTINUED SKUS INFOREM UPDATE'.           
021100     05  FILLER                       PIC  X(01)   VALUE SPACE.           
021200                                                                          
021300 01  HL-HEADING-LINE2.                                                    
021400     05  FILLER                       PIC  X(22)   VALUE SPACE.           
021500     05  FILLER                       PIC  X(40)   VALUE                  
021600         'INFOREM ECOMMERCE SKUS WITH CLEARANCE OR'.                      
021700     05  FILLER                       PIC  X(29)   VALUE                  
021800         ' DISCONTINUED STATUS UPDATED '.                                 
021900     05  FILLER                       PIC  X(21)   VALUE                  
022000         'OP, OUTL = 0 AND FT=X'.                                         
022100     05  FILLER                       PIC  X(06)   VALUE SPACE.           
022200                                                                          
022300 01  HL-HEADING-LINE3.                                                    
022400     05  FILLER                       PIC  X(36)   VALUE SPACE.           
022500     05  FILLER                       PIC  X(32)   VALUE                  
022600         'INFOREM SKUS WITH MIXED STATUS '.                               
022700     05  FILLER                       PIC  X(29)   VALUE                  
022800         'UPDATED CLEARANCE STORES FT=X'.                                 
022900     05  FILLER                       PIC  X(01)   VALUE SPACE.           
023000                                                                          
023100 01  HL-HEADING-LINE4.                                                    
023200     05  FILLER                       PIC  X(03)   VALUE SPACE.           
023300     05  FILLER                       PIC  X(12)   VALUE                  
023400         'DEPARTMENT: '.                                                  
023500     05  HL3-DEPT-NO                  PIC  Z(03).                         
023600     05  FILLER                       PIC  X(03)   VALUE SPACE.           
023700     05  HL3-DEPT-NAME                PIC  X(25).                         
023800     05  FILLER                       PIC  X(02)   VALUE SPACE.           
023900                                                                          
024000 01  HL-HEADING-LINE5.                                                    
024100     05  FILLER                       PIC  X(47)   VALUE SPACE.           
024200     05  FILLER                       PIC  X(5)    VALUE SPACE.           
024400     05  FILLER                       PIC  X(80)   VALUE SPACE.           
024500                                                                          
024600 01  HL-HEADING-LINE6.                                                    
024700     05  FILLER                       PIC  X(02)   VALUE SPACE.           
024800     05  FILLER                       PIC  X(02)   VALUE                  
024900         'CL'.                                                            
024901     05  FILLER                       PIC  X(01)   VALUE SPACE.           
024910     05  FILLER                       PIC  X(08)   VALUE                  
024920         'SKU'.                                                           
025000     05  FILLER                       PIC  X(06)   VALUE SPACE.           
025100     05  FILLER                       PIC  X(25)   VALUE                  
025200         'SKU DESCRIPTION'.                                               
025300     05  FILLER                       PIC  X(33)   VALUE SPACE.           
026000     05  FILLER                       PIC  X(5)    VALUE                  
026100         'STORE'.                                                         
026200     05  FILLER                       PIC  X(4)    VALUE SPACE.           
026300     05  FILLER                       PIC  X(25)   VALUE                  
026400         'STORE NAME'.                                                    
026500     05  FILLER                       PIC  X(4)    VALUE SPACE.           
026600     05  FILLER                       PIC  X(12)   VALUE                  
026700         'STATUS'.                                                        
026710     05  FILLER                       PIC  X(05)   VALUE SPACE.           
026800                                                                          
026900 01  HL-HEADING-LINE7.                                                    
026910     05  FILLER                       PIC X(02)    VALUE SPACE.           
026920     05  FILLER                       PIC X(02)    VALUE ALL '-'.         
026940     05  FILLER                       PIC X(01)    VALUE SPACE.           
027100     05  FILLER                       PIC X(08)    VALUE ALL '-'.         
027200     05  FILLER                       PIC X(06)    VALUE SPACE.           
027300     05  FILLER                       PIC X(25)    VALUE ALL '-'.         
027400     05  FILLER                       PIC X(33)    VALUE SPACE.           
027700     05  FILLER                       PIC X(05)    VALUE ALL '-'.         
027800     05  FILLER                       PIC X(04)    VALUE SPACE.           
027900     05  FILLER                       PIC X(25)    VALUE ALL '-'.         
028000     05  FILLER                       PIC X(04)    VALUE SPACE.           
028100     05  FILLER                       PIC X(12)    VALUE ALL '-'.         
028200     05  FILLER                       PIC X(05)    VALUE SPACE.           
028400                                                                          
028800 01  DETAIL-LINE.                                                         
028900     05  FILLER                       PIC X(02)    VALUE SPACE.           
029000     05  DL-CL                        PIC X(02).                          
029100     05  FILLER                       PIC X(1)     VALUE SPACE.           
029200     05  DL-SKU                       PIC Z(08).                          
029600     05  FILLER                       PIC X(06)    VALUE SPACE.           
029700     05  DL-DESCRIPTION               PIC X(25).                          
029800     05  FILLER                       PIC X(33)    VALUE SPACE.           
030300     05  DL-STORE-NO                  PIC ZZZZ9.                          
030400     05  FILLER                       PIC X(04)    VALUE SPACE.           
030500     05  DL-STORE-NAME                PIC X(25).                          
030600     05  FILLER                       PIC X(04)    VALUE SPACE.           
030700     05  DL-STATUS                    PIC X(12).                          
030800     05  FILLER                       PIC X(05)    VALUE SPACE.           
031000                                                                          
031100 01  NO-REPORT-LINE.                                                      
031200     05  FILLER                       PIC X(20)    VALUE                  
031300         '*NO RECORDS UPDATED*'.                                          
031400                                                                          
031500 01  END-OF-REPORT-LINE.                                                  
031600     05  FILLER                       PIC X(20)    VALUE                  
031700         '** END OF REPORT ** '.                                          
031800                                                                          
031810******************************************************************        
031820*    NI SKU WORK AREA FIELDS                                              
031830******************************************************************        
031840     COPY ISWS006.                                                        
031850******************************************************************        
031860                                                                          
031900     EXEC SQL                                                             
032000         INCLUDE TSKXREF                                                  
032100     END-EXEC.                                                            
032110                                                                          
032120     EXEC SQL                                                             
032130         INCLUDE TMRITEM                                                  
032140     END-EXEC.                                                            
032200                                                                          
032300     EXEC SQL                                                             
032400         INCLUDE TMRITST                                                  
032500     END-EXEC.                                                            
032600                                                                          
032700     EXEC SQL                                                             
032800         INCLUDE TSK                                                      
032900     END-EXEC.                                                            
033000                                                                          
033100     EXEC SQL                                                             
033200         INCLUDE TUPCPLS                                                  
033300     END-EXEC.                                                            
033400                                                                          
033500     EXEC SQL                                                             
033600         INCLUDE XMT00001                                                 
033700     END-EXEC.                                                            
033800                                                                          
033900     EXEC SQL                                                             
034000         INCLUDE TCKRSTCN                                                 
034100     END-EXEC.                                                            
034200                                                                          
034300     EXEC SQL                                                             
034400         INCLUDE TMDSEAR                                                  
034500     END-EXEC.                                                            
034600                                                                          
034700* DB2 COMMUNICATION AREA                                                  
034800                                                                          
034900     EXEC SQL                                                             
035000         INCLUDE SQLCA                                                    
035100     END-EXEC.                                                            
035200                                                                          
035300* DB2 COMMUNICATION AREA2                                                 
035400                                                                          
035500     COPY SQLCA2.                                                         
035600                                                                          
035800     EXEC SQL                                                             
035900       DECLARE CHG_CSR CURSOR WITH HOLD FOR                               
036000     SELECT  A.ITEM_NBR                                                   
036100            ,B.STR_NBR                                                    
036110            ,C.STATUS_CODE                                                
036200            ,E.DEPT                                                       
036300            ,E.SUBCLASS                                                   
036400            ,E.SKU                                                        
036500            ,C.SKU_DESC                                                   
036510            ,D.LOC_NM                                                     
036600     FROM TMRITEM A,                                                      
036700          TMRITST B,                                                      
036800          TSK     C,                                                      
036810          XMT_LOC D,                                                      
036820          TSKXREF E                                                       
036900     WHERE  A.ITEM_NBR      =  C.ITEM_NMBR                                
037010       AND  A.ITEM_NBR      =  B.ITEM_NBR                                 
037011       AND  A.ITEM_NBR      =  E.ITM_NBR                                  
037020       AND  B.STR_NBR       =  D.LOC_NBR                                  
037030       AND  D.E_BUS_IND     =  'Y'                                        
037040       AND  D.LOC_TYP_CDE   =  'DS'                                       
037050       AND  B.FT_CDE        <> 'X'                                        
037100       AND  C.STATUS_CODE   IN (25,30,90)                                 
037400     ORDER BY E.DEPT, C.STATUS_CODE                                       
037500     END-EXEC.                                                            
037600*                                                                         
041800*****************************************************************         
041900 PROCEDURE DIVISION.                                                      
042000*****************************************************************         
042100 A000-MAINLINE-PROCESSING.                                                
042200                                                                          
042300     PERFORM A100-INITIALIZATION.                                         
042600                                                                          
042900     PERFORM B100-PROCESS-CHANGE-RECORD                                   
043000       UNTIL EOC-ITEM-CSR.                                                
043100                                                                          
043200     PERFORM Z100-EOJ-PROCESSING.                                         
049632                                                                          
049633     GOBACK.                                                              
049634*                                                                         
049635******************************************************************        
049636** OPEN THE SYSTEM FILE AND ACCEPT CONTROL CARDS.                         
049637** CALL CALENDAR ROUTINES TO RETRIEVE VALUES NEEDED IN PROGRAM            
049638******************************************************************        
049639 A100-INITIALIZATION.                                                     
049640                                                                          
049641     OPEN OUTPUT PRINT-REPORT.                                            
049642                                                                          
049643     ACCEPT CT-CPU-TIME FROM TIME.                                        
049647     ACCEPT PV-SCHED-DATE.                                                
049648     MOVE CT-CPU-HR                   TO DP132-RUN-HOUR.                  
049649     MOVE CT-CPU-MIN                  TO DP132-RUN-MINUTE.                
049650                                                                          
049651     MOVE PV-SCHED-MM                 TO DP132-RUN-MONTH.                 
049652     MOVE PV-SCHED-DD                 TO DP132-RUN-DAY.                   
049653     MOVE PV-SCHED-CCYY               TO DP132-RUN-YEAR.                  
049657     MOVE 1                           TO DP132-REPORT-NUMBER.             
049658     MOVE 'MRKUP118'                  TO DP132-PROGRAM-NAME.              
049659                                                                          
049660     MOVE ZEROS                       TO PV-LINE-CNT                      
049661     MOVE ZEROS                       TO PV-PAGE-CNT                      
049662     PERFORM N100-REPORT-HEADINGS.                                        
049663*                                                                         
049664*  SET UP FIRST SCHEDULED COMMIT TIME                                     
049665*                                                                         
049666     EXEC SQL                                                             
049667         SELECT  (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)            
049668           INTO  :PV-COMMIT-TMST                                          
049669           FROM  TCKRSTCNTL                                               
049670          WHERE  PLAN_NAME = 'MRKUP118'                                   
049671     END-EXEC.                                                            
049672                                                                          
049673     IF SQLCODE = +0                                                      
049674         CONTINUE                                                         
049675     ELSE                                                                 
049676         DISPLAY 'PARAGRAPH A100, BAD SELECT FOR TIMESTAMP'               
049677         DISPLAY 'SQLCODE ' SQLCODE                                       
049678         DISPLAY ' '                                                      
049679         PERFORM Z999-SQL-ERROR                                           
049700     END-IF.                                                              
050100                                                                          
050101     PERFORM S100-OPEN-CHG-CSR.                                           
050102*                                                                         
050200******************************************************************        
050300*  PROCESS CHANGE RECORD UNTIL EOC                               *        
050400******************************************************************        
050500 B100-PROCESS-CHANGE-RECORD.                                              
050600                                                                          
050700     PERFORM D100-FETCH-CHG-ITEM-CURSOR.                                  
050701                                                                          
050710     IF NOT EOC-ITEM-CSR                                                  
050720         MOVE SKXREF-DEPT          TO NI-DEPT-9-3                         
050721         MOVE NI-DEPT-X-3          TO PV-DEPT-NBR                         
050730         MOVE TSK-STATUS-CODE      TO PV-STAT-CDE                         
050740         MOVE SKXREF-SUBCLASS      TO NI-SUB-CLASS-9-2                    
050741         MOVE NI-SUB-CLASS-X-2     TO PV-CLASS-NBR                        
050743         MOVE SKXREF-SKU           TO PV-SKU                              
050744         MOVE TSK-SKU-DESC         TO PV-SKU-DESC                         
050750         IF PV-DEPT-NBR NOT = PV-DEPT-NBR-HOLD                            
050751             PERFORM D300-GET-DEPT-NAME                                   
050760             MOVE PV-DEPT-NBR      TO PV-DEPT-NBR-HOLD                    
050761                                      NI-DEPT-X-3                         
050770             MOVE NI-DEPT-9-3      TO HL3-DEPT-NO                         
050780             MOVE PV-DEPT-DESC     TO HL3-DEPT-NAME                       
050790             PERFORM N200-DETAIL-HEADINGS                                 
050793         END-IF                                                           
050795         PERFORM D200-CONTINUE-MOVE                                       
050797     END-IF.                                                              
051500*                                                                         
053000******************************************************************        
053100* THIS PARAGRAPH FETCHES THE ITEM CURSOR FOR CHANGE RECORDS      *        
053200******************************************************************        
053300 D100-FETCH-CHG-ITEM-CURSOR.                                              
053400                                                                          
053500     MOVE 'D100'                   TO PGM-LOCATION.                       
053600     MOVE 'D100-FETCH-CHG-ITEM-CURSOR'                                    
053700                                   TO PV-ABEND-PARAGRAPH.                 
053800     EXEC SQL                                                             
053900       FETCH CHG_CSR                                                      
054000        INTO                                                              
054100             :MRITEM-ITEM-NBR,                                            
054200             :MRITST-STR-NBR,                                             
054210             :TSK-STATUS-CODE,                                            
054300             :SKXREF-DEPT,                                                
054400             :SKXREF-SUBCLASS,                                            
054500             :SKXREF-SKU,                                                 
054600             :TSK-SKU-DESC,                                               
054610             :XMT00001-LOC-NM                                             
054700     END-EXEC.                                                            
054800                                                                          
054900     EVALUATE TRUE                                                        
055000       WHEN SQLCODE         = ZERO                                        
055100         CONTINUE                                                         
055200       WHEN SQLCODE         = PC-ROW-NOT-FOUND                            
055300         PERFORM U100-CLOSE-CHG-ITEM-CSR                                  
055400         SET EOC-ITEM-CSR TO TRUE                                         
055500       WHEN OTHER                                                         
055600         DISPLAY PV-ABEND-PARAGRAPH                                       
054100         DISPLAY 'MRITEM-ITEM-NBR = ' MRITEM-ITEM-NBR                     
054200         DISPLAY 'MRITST-STR-NBR= ' MRITST-STR-NBR                        
054210         DISPLAY 'TSK-STATUS-CODE= ' TSK-STATUS-CODE                      
055700         PERFORM Z999-SQL-ERROR                                           
055800     END-EVALUATE.                                                        
055900                                                                          
062300                                                                          
062400******************************************************************        
062500* THIS PARAGRAPH MOVES THE FETCHED FIELDS TO THE DETAIL LINE     *        
062600******************************************************************        
062700 D200-CONTINUE-MOVE.                                                      
062701                                                                          
062710     MOVE SPACES                     TO DETAIL-LINE.                      
062720     MOVE PV-CLASS-NBR               TO DL-CL.                            
062760     MOVE PV-SKU                     TO DL-SKU.                           
062770     MOVE PV-SKU-DESC                TO DL-DESCRIPTION.                   
062780     MOVE MRITST-STR-NBR             TO DL-STORE-NO.                      
062790     MOVE XMT00001-LOC-NM            TO DL-STORE-NAME.                    
062800                                                                          
063800     IF MIXED                                                             
063810         MOVE 'Y'                    TO PS-STORE-FOUND                    
063900         PERFORM D400-FETCH-STORE-DATA                                    
064000         IF STORE-FOUND                                                   
064100             PERFORM M200-CHG-SKUS-STORES                                 
064200             PERFORM Z900-COMMIT-TEST                                     
064900             MOVE 'MIXED'            TO DL-STATUS                         
064910             PERFORM P100-PRINT-DETAIL                                    
064920         END-IF                                                           
066900     ELSE                                                                 
067100         PERFORM M100-CHG-SKUS-TMRITST                                    
067200         PERFORM Z900-COMMIT-TEST                                         
068300         IF DISCONTINUED                                                  
068400             MOVE 'DISCONTINUED'   TO DL-STATUS                           
068500         ELSE                                                             
068600             MOVE 'CLEARANCE'      TO DL-STATUS                           
068700         END-IF                                                           
068800         PERFORM P100-PRINT-DETAIL                                        
068900     END-IF.                                                              
069000*                                                                         
069100******************************************************************        
069200*  GET DEPARTMENT NAME                                           *        
069300******************************************************************        
069400 D300-GET-DEPT-NAME.                                                      
069500                                                                          
069600     MOVE 'D300-GET-DEPT-NAME' TO PV-ABEND-PARAGRAPH.                     
069700                                                                          
069800     EXEC SQL                                                             
069900     SELECT   MDSEAR_DESC                                                 
070000        INTO  :PV-DEPT-DESC                                               
070100        FROM  TMDSEAR                                                     
070200        WHERE DEPT_NBR = :PV-DEPT-NBR                                     
070300          AND MDSELV_CDE = 'DPT'                                          
070400          AND CURRENT DATE BETWEEN STRT_DTE AND END_DTE                   
070500     END-EXEC.                                                            
070600                                                                          
070700     EVALUATE TRUE                                                        
070800       WHEN SQLCODE         = ZERO                                        
070900         CONTINUE                                                         
071000       WHEN SQLCODE         = PC-ROW-NOT-FOUND                            
071100         CONTINUE                                                         
071200       WHEN OTHER                                                         
071300         DISPLAY PV-ABEND-PARAGRAPH                                       
071300         DISPLAY 'PV-DEPT-NBR = ' PV-DEPT-NBR                             
071400         PERFORM Z999-SQL-ERROR                                           
071500     END-EVALUATE.                                                        
071600                                                                          
071700******************************************************************        
071800* THIS PARAGRAPH FETCHES THE STORE CURSOR FOR STORE NUMBER, CITY *        
MK    * TOOK OUT REFERNCE TO XMT_LOC                                   *        
071900******************************************************************        
072000 D400-FETCH-STORE-DATA.                                                   
072100                                                                          
MK                                                                              
MK         MOVE 'D400-FETCH-STORE-DATA' TO PV-ABEND-PARAGRAPH.                  
MK                                                                              
072101     EXEC SQL                                                             
072104     SELECT  A.UPC_STAT_CDE                                               
072108     INTO   :UPCPLS-UPC-STAT-CDE                                          
072109     FROM TUPCPLS A,                                                      
072111          TMRITST B                                                       
072112     WHERE A.STR_NBR       = B.STR_NBR                                    
072114        AND A.ITM_NBR      = B.ITEM_NBR                                   
072115        AND ((CURRENT TIMESTAMP                                           
072116               BETWEEN A.EFF_BEG_TMST AND A.EFF_END_TMST)                 
072117        OR  ((CURRENT TIMESTAMP   >   A.EFF_BEG_TMST)                     
072118        AND (A.EFF_END_TMST IS NULL)))                                    
072119        AND A.ITM_NBR      = :MRITEM-ITEM-NBR                             
072120        AND B.STR_NBR      = :MRITST-STR-NBR                              
072122        AND A.UPC_STAT_CDE = '30'                                         
072123     END-EXEC.                                                            
072130*                                                                         
073500     EVALUATE TRUE                                                        
073600       WHEN SQLCODE         = ZERO                                        
073700         CONTINUE                                                         
073800       WHEN SQLCODE         = PC-ROW-NOT-FOUND                            
074100         MOVE 'N' TO PS-STORE-FOUND                                       
074500       WHEN OTHER                                                         
074600         DISPLAY PV-ABEND-PARAGRAPH                                       
071300         DISPLAY 'PV-DEPT-NBR = ' PV-DEPT-NBR                             
074600         DISPLAY 'MRITEM-ITEM-NBR= ' MRITEM-ITEM-NBR                      
074600         DISPLAY 'MRITST-STR-NBR = ' MRITST-STR-NBR                       
074700         PERFORM Z999-SQL-ERROR                                           
074800     END-EVALUATE.                                                        
078201*                                                                         
081100******************************************************************        
081200** CHANGE STATUS ON TMRITST TABLE FOR CHANGE SKUS PROCESS       **        
081300******************************************************************        
081400 M100-CHG-SKUS-TMRITST.                                                   
081500                                                                          
081600     MOVE 'M100'                   TO PGM-LOCATION.                       
081700     MOVE 'M100-CHG-SKUS-TMRITST'  TO PV-ABEND-PARAGRAPH.                 
081800                                                                          
081900     EXEC SQL                                                             
082000         UPDATE TMRITST                                                   
082100                                                                          
082200         SET    FT_CDE      =  :PC-FT-X,                                  
082300                OP_QTY      =  :PC-OP-QTY-0,                              
082400                OUTL_QTY    =  :PC-OUTL-QTY-0                             
082500                                                                          
082600         WHERE ITEM_NBR     =  :MRITEM-ITEM-NBR                           
085200           AND STR_NBR      =  :MRITST-STR-NBR                            
085201     END-EXEC.                                                            
085202                                                                          
085203     EVALUATE TRUE                                                        
085204       WHEN SQLCODE         = ZERO                                        
085205       WHEN SQLCODE         = PC-ROW-NOT-FOUND                            
085206         ADD PC-PLUS-1 TO PV-CHANGE-CNT                                   
085207       WHEN OTHER                                                         
085208         DISPLAY PV-ABEND-PARAGRAPH                                       
074600         DISPLAY 'MRITEM-ITEM-NBR= ' MRITEM-ITEM-NBR                      
074600         DISPLAY 'MRITST-STR-NBR = ' MRITST-STR-NBR                       
085209         PERFORM Z999-SQL-ERROR                                           
085210     END-EVALUATE.                                                        
085211*                                                                         
085212******************************************************************        
085213** CHANGE STATUS ON TMRITST TABLE FOR STORES                    **        
085214******************************************************************        
085215 M200-CHG-SKUS-STORES.                                                    
085216                                                                          
085217     MOVE 'M200'                   TO PGM-LOCATION.                       
085218     MOVE 'M200-CHG-SKUS-STORES '  TO PV-ABEND-PARAGRAPH.                 
085219                                                                          
085220     EXEC SQL                                                             
085221         UPDATE TMRITST                                                   
085222                                                                          
085223         SET    FT_CDE      =  :PC-FT-X                                   
085224                                                                          
085225         WHERE ITEM_NBR     =  :MRITEM-ITEM-NBR                           
085226           AND STR_NBR      =  :MRITST-STR-NBR                            
085300     END-EXEC.                                                            
085400                                                                          
085500     EVALUATE TRUE                                                        
085600       WHEN SQLCODE         = ZERO                                        
085700       WHEN SQLCODE         = PC-ROW-NOT-FOUND                            
086400         ADD PC-PLUS-1 TO PV-CHANGE-CNT                                   
086401       WHEN OTHER                                                         
086402         DISPLAY PV-ABEND-PARAGRAPH                                       
074600         DISPLAY 'MRITEM-ITEM-NBR= ' MRITEM-ITEM-NBR                      
074600         DISPLAY 'MRITST-STR-NBR = ' MRITST-STR-NBR                       
086403         PERFORM Z999-SQL-ERROR                                           
086404     END-EVALUATE.                                                        
086405                                                                          
086600*                                                                         
086700****************************************************************          
086800* WRITES THE HEADING LINES FOR THE REPORT WHEN THERE IS A      *          
086900* FULL PAGE.                                                   *          
087000****************************************************************          
087100 N100-REPORT-HEADINGS.                                                    
087200                                                                          
087300     MOVE ZEROS                       TO PV-LINE-CNT.                     
087400     ADD +1                           TO PV-PAGE-CNT.                     
087500     MOVE PV-PAGE-CNT                 TO DP132-PAGE-NUMBER.               
087600     WRITE PRINT-LINE FROM DP132-STANDRD-HEADER-132-COLS                  
087700         AFTER ADVANCING PAGE.                                            
087800     WRITE PRINT-LINE FROM HL-HEADING-LINE1                               
087900         AFTER ADVANCING 1.                                               
088000     WRITE PRINT-LINE FROM HL-HEADING-LINE2                               
088100         AFTER ADVANCING 1.                                               
088200     WRITE PRINT-LINE FROM HL-HEADING-LINE3                               
088300         AFTER ADVANCING 1.                                               
088400     MOVE 4                           TO PV-LINE-CNT.                     
088500*                                                                         
088600****************************************************************          
088700* WRITES THE DETAIL HEADINGS FOR THE REPORT WHEN THERE IS A    *          
088800* CHANGE IN DEPARTMENT NUMBERS AND WHEN THE PAGE IS FULL.      *          
088900****************************************************************          
089000 N200-DETAIL-HEADINGS.                                                    
089100                                                                          
089200     IF PV-LINE-CNT > +52                                                 
089300         PERFORM N100-REPORT-HEADINGS                                     
089400     END-IF.                                                              
089500                                                                          
089900     WRITE PRINT-LINE FROM HL-HEADING-LINE5                               
090000         AFTER ADVANCING 1.                                               
090001                                                                          
090002     WRITE PRINT-LINE FROM HL-HEADING-LINE4                               
090003         AFTER ADVANCING 1.                                               
090004                                                                          
090005     WRITE PRINT-LINE FROM HL-HEADING-LINE5                               
090006         AFTER ADVANCING 1.                                               
090100                                                                          
090200     WRITE PRINT-LINE FROM HL-HEADING-LINE6                               
090300         AFTER ADVANCING 1.                                               
090400                                                                          
090500     WRITE PRINT-LINE FROM HL-HEADING-LINE7                               
090600         AFTER ADVANCING 1.                                               
090700                                                                          
090800     ADD 5                            TO PV-LINE-CNT.                     
090900*                                                                         
091000*****************************************************************         
091100* P100-PRINT-DETAIL - PRINTS OUT EACH DETAIL LINE OF THE SKUS   *         
091200* THAT WERE UPDATED.                                            *         
091300*****************************************************************         
091400 P100-PRINT-DETAIL.                                                       
091500                                                                          
091600     IF PV-LINE-CNT > PC-MAX-LINES                                        
091700         PERFORM N100-REPORT-HEADINGS                                     
091800         PERFORM N200-DETAIL-HEADINGS                                     
091900     END-IF.                                                              
092000                                                                          
092100     MOVE SPACES TO PRINT-LINE.                                           
092200                                                                          
092300     WRITE PRINT-LINE FROM DETAIL-LINE AFTER ADVANCING 1.                 
092400                                                                          
092500     ADD 1 TO PV-LINE-CNT.                                                
092600*                                                                         
096400******************************************************************        
096500* THIS PARAGRAPH OPENS THE CHG CURSOR                                     
096600******************************************************************        
096700 S100-OPEN-CHG-CSR.                                                       
096800                                                                          
096900     MOVE 'S100'                   TO PGM-LOCATION.                       
097000     MOVE 'S100-OPEN-CHG-CSR'      TO PV-ABEND-PARAGRAPH.                 
097100                                                                          
097200     EXEC SQL                                                             
097300         OPEN CHG_CSR                                                     
097400     END-EXEC.                                                            
097500                                                                          
097600     EVALUATE TRUE                                                        
097700       WHEN SQLCODE         = ZERO                                        
097800       WHEN SQLCODE         = PC-ROW-NOT-FOUND                            
097900         CONTINUE                                                         
098000       WHEN OTHER                                                         
098100         DISPLAY PV-ABEND-PARAGRAPH                                       
098200         PERFORM Z999-SQL-ERROR                                           
098300     END-EVALUATE.                                                        
100500*                                                                         
102800******************************************************************        
102900*  CLOSE CHANGE CURSOR                                           *        
103000******************************************************************        
103100 U100-CLOSE-CHG-ITEM-CSR.                                                 
103200                                                                          
103300     MOVE 'U100'                   TO PGM-LOCATION.                       
103400     MOVE 'U001-CLOSE-CHG-ITEM-CSR'                                       
103500                                   TO PV-ABEND-PARAGRAPH.                 
103600     EXEC SQL                                                             
103700         CLOSE CHG_CSR                                                    
103800     END-EXEC.                                                            
103900                                                                          
104000     EVALUATE TRUE                                                        
104100       WHEN SQLCODE         = ZERO                                        
104200       WHEN SQLCODE         = PC-ROW-NOT-FOUND                            
104300         CONTINUE                                                         
104400       WHEN OTHER                                                         
104500         DISPLAY PV-ABEND-PARAGRAPH                                       
104600         PERFORM Z999-SQL-ERROR                                           
104700     END-EVALUATE.                                                        
104800*                                                                         
108820******************************************************************        
108830*     END OF JOB PROCESSING                                      *        
108840******************************************************************        
108850 Z100-EOJ-PROCESSING.                                                     
108851                                                                          
108860     IF PV-CHANGE-CNT = 0                                                 
108870        WRITE PRINT-LINE FROM NO-REPORT-LINE                              
108880             AFTER ADVANCING 2                                            
108890     END-IF.                                                              
108900                                                                          
109000     WRITE PRINT-LINE FROM END-OF-REPORT-LINE                             
109010             AFTER ADVANCING 2.                                           
109020                                                                          
109030     CLOSE  PRINT-REPORT.                                                 
109100*                                                                         
109300******************************************************************        
109400*     DB2 COMMIT TEST                                            *        
109500******************************************************************        
109700 Z900-COMMIT-TEST.                                                        
109900                                                                          
110000     MOVE 'Z900'                   TO PGM-LOCATION.                       
110100     MOVE 'Z900-COMMIT-TEST     '  TO PV-ABEND-PARAGRAPH.                 
110200                                                                          
110300*  GET THE CURRENT TIMESTAMP                                              
110400                                                                          
110500     EXEC SQL                                                             
110600         SET :PV-CURR-TMST = CURRENT TIMESTAMP                            
110700     END-EXEC.                                                            
110800                                                                          
110900     IF SQLCODE = +0                                                      
111000         CONTINUE                                                         
111100     ELSE                                                                 
111200         DISPLAY 'PARAGRAPH Z900  BAD SET FOR CURRENT TIMESTAMP'          
111300         DISPLAY ' '                                                      
111400         PERFORM Z999-SQL-ERROR                                           
111410     END-IF.                                                              
111500                                                                          
111600*  COMPARE TO THE NEXT SCHEDULED COMMIT TIME                              
111700                                                                          
111800     IF PV-CURR-TMST > PV-COMMIT-TMST                                     
111900         PERFORM Z910-COMMIT                                              
111910     END-IF.                                                              
112000                                                                          
112200******************************************************************        
112300*     DB2 COMMIT PROCESSING                                      *        
112500******************************************************************        
112700 Z910-COMMIT.                                                             
113100                                                                          
113200     EXEC SQL                                                             
113300         COMMIT                                                           
113400     END-EXEC.                                                            
113500                                                                          
113600     IF SQLCODE = +0                                                      
113700         CONTINUE                                                         
113800     ELSE                                                                 
113900         DISPLAY 'PARAGRAPH Z910  BAD COMMIT'                             
114000         DISPLAY ' '                                                      
114100         PERFORM Z999-SQL-ERROR                                           
114110     END-IF.                                                              
114200                                                                          
114300*  GET THE NEXT SCHEDULED COMMIT TIME                                     
114400                                                                          
114500     EXEC SQL                                                             
114600         SELECT  (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)            
114700           INTO  :PV-COMMIT-TMST                                          
114800           FROM  TCKRSTCNTL                                               
114900          WHERE  PLAN_NAME = 'MRKUP118'                                   
115000     END-EXEC.                                                            
115100                                                                          
115200     IF SQLCODE = +0                                                      
115300         CONTINUE                                                         
115400     ELSE                                                                 
115500         DISPLAY 'PARAGRAPH Z910  SELECT FOR NEXT TIMESTAMP'              
115600         DISPLAY ' '                                                      
115700         PERFORM Z999-SQL-ERROR                                           
115710     END-IF.                                                              
115800*                                                                         
115900******************************************************************        
116000*   ERROR MESSAGE ROUTINE                                                 
116100******************************************************************        
116200 Z999-SQL-ERROR.                                                          
116300                                                                          
116400     MOVE SQLCODE    TO SQLCODE2.                                         
116500     MOVE SQLERRD(3) TO SQLERRD3.                                         
116600     DISPLAY '** ABEND **       ** = SQLERROR'.                           
116700     DISPLAY '** PROGRAM        ** = MRKUP118'.                           
116800     DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
116900     DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
117000     DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
117100     DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
117200     MOVE +4013 TO PV-ABEND-CODE.                                         
117300     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
117400******************************************************************        
