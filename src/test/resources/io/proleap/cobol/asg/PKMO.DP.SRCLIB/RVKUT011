000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    RVKUT011.                                                 
000300 AUTHOR.        VIRGENE LENNEMAN.                                         
000400 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000500 DATE-WRITTEN.  MAY 2007.                                                 
000600 DATE-COMPILED.                                                           
000700******************************************************************        
000800*  RVKUT011 WILL CREATE A FILE CONTAINING PHYSICAL INVENTORY     *        
000900*  COUNTS AT AN ITEM LEVEL.                                      *        
001000******************************************************************        
001100*                                                                         
001200 ENVIRONMENT DIVISION.                                                    
001300*                                                                         
001400 CONFIGURATION SECTION.                                                   
001500*                                                                         
001600 SOURCE-COMPUTER.        IBM-3090.                                        
001700 OBJECT-COMPUTER.        IBM-3090.                                        
001800*                                                                         
001900                                                                          
002000 INPUT-OUTPUT SECTION.                                                    
002100                                                                          
002200 FILE-CONTROL.                                                            
002300                                                                          
002400     SELECT RTV-REPORT-EXTRACT                                            
002500         ASSIGN TO IN01.                                                  
002600                                                                          
002700     SELECT INVENTORY-CNT-FILE                                            
002800         ASSIGN TO OUT01.                                                 
002900                                                                          
003000******************************************************************        
003100 DATA DIVISION.                                                           
003200******************************************************************        
003300                                                                          
003400 FILE SECTION.                                                            
003500                                                                          
003600 FD  RTV-REPORT-EXTRACT                                                   
003700     RECORDING MODE IS F                                                  
003800     LABEL RECORDS ARE STANDARD                                           
003900     BLOCK CONTAINS 0 RECORDS.                                            
004000                                                                          
004100 01  RTV-REPORT-EXTRACT-RECORD       PIC  X(200).                         
004200                                                                          
004300 FD  INVENTORY-CNT-FILE                                                   
004400     RECORDING MODE IS F                                                  
004500     LABEL RECORDS ARE STANDARD                                           
004600     BLOCK CONTAINS 0 RECORDS.                                            
004700                                                                          
004800 01  INVENTORY-CNT-RECORD                PIC  X(175).                     
004900                                                                          
005000******************************************************************        
005100 WORKING-STORAGE SECTION.                                                 
005200******************************************************************        
005300                                                                          
005400******************************************************************        
005500* PC PROGRAM CONSTANTS                                           *        
005600******************************************************************        
005700 01  PC-PROGRAM-CONSTANTS.                                                
005800     05  PC-CURRENT-PROGRAM-NAME      PIC  X(08) VALUE 'RVKUT011'.        
005900                                                                          
006000******************************************************************        
006100* PS PROGRAM SWITCHES                                            *        
006200******************************************************************        
006300 01  PS-PROGRAM-SWITCHES.                                                 
006400     05  PS-END-OF-EXTRACT-FILE-IND      PIC  X(01)  VALUE 'N'.           
006500         88  PS-END-OF-EXTRACT-FILE                  VALUE 'Y'.           
006600         88  PS-NOT-END-OF-EXTRACT-FILE              VALUE 'N'.           
006700                                                                          
006800******************************************************************        
006900* PV PROGRAM VARIABLES                                           *        
007000******************************************************************        
007100 01  PV-PROGRAM-VARIABLES.                                                
007200     05  PV-SKU-COUNT                    PIC S9(09)  COMP.                
007300     05  PV-RECS-READ                    PIC  9(07)  VALUE ZERO.          
007400     05  PV-RECS-WRITTEN                 PIC  9(07)  VALUE ZERO.          
007500     05  PV-HOLD-FIELDS.                                                  
007600         10  PV-HOLD-LOC                 PIC  X(04).                      
007600         10  PV-HOLD-DEPT                PIC  9(03).                      
007700         10  PV-HOLD-SUB-CL              PIC  9(02).                      
007800         10  PV-HOLD-SKU                 PIC  9(08).                      
007900         10  PV-HOLD-RETAIL              PIC  9(09)V99.                   
008000                                                                          
008100*  INPUT FILE                                                             
008200*                                                                         
008300     COPY RVRD023.                                                        
008400                                                                          
008500                                                                          
008600*  RTV SKU INFO                                                           
008700*                                                                         
008800     COPY RVRD024.                                                        
008900                                                                          
009000******************************************************************        
009100 PROCEDURE DIVISION.                                                      
009200******************************************************************        
009300*                                                                         
009400******************************************************************        
009500* 1000-MAIN-MODULE  - HIGHEST LEVEL CONTROL ROUTINE.             *        
009600******************************************************************        
009700 1000-MAIN-MODULE.                                                        
009800     PERFORM 1100-INITIALIZE                                              
009900     PERFORM 2000-PROCESS-INPUT                                           
010000             UNTIL PS-END-OF-EXTRACT-FILE.                                
010100                                                                          
010200     PERFORM 9000-CLOSE-FILES.                                            
010300                                                                          
010400     GOBACK.                                                              
010500                                                                          
010600******************************************************************        
010700* 1100-INITIALIZE.                                                        
010800******************************************************************        
010900 1100-INITIALIZE.                                                         
011000     OPEN INPUT RTV-REPORT-EXTRACT                                        
011100          OUTPUT INVENTORY-CNT-FILE.                                      
011200                                                                          
011300     SET PS-NOT-END-OF-EXTRACT-FILE TO TRUE.                              
011400                                                                          
011500     PERFORM 7000-READ-EXTRACT-FILE.                                      
011600                                                                          
011700     IF PS-NOT-END-OF-EXTRACT-FILE                                        
011800         MOVE RV023-RTV-LOC       TO PV-HOLD-LOC                          
011800         MOVE RV023-DEPT-NBR      TO PV-HOLD-DEPT                         
011900         MOVE RV023-SUB-CL-NBR    TO PV-HOLD-SUB-CL                       
012000         MOVE RV023-SKU           TO PV-HOLD-SKU                          
012100         MOVE RV023-RTL-AMT       TO PV-HOLD-RETAIL                       
012200     ELSE                                                                 
012300         DISPLAY 'INPUT FILE IS EMPTY, NO OUTPUT TO CREATE'               
012400     END-IF.                                                              
012500                                                                          
012600******************************************************************        
012700* 2000-PROCESS-INPUT                                                      
012800*   - READ DETAIL INFORMATION                                             
012900*   - SUMMARIZE DETAILS BY DEPARTMENT, CLASS, SKU, RETAIL                 
013000*   - WRITE INVENTORY RECORD                                              
013100******************************************************************        
013200 2000-PROCESS-INPUT.                                                      
013400     IF RV023-DEPT-NBR    =  PV-HOLD-DEPT   AND                           
013500         RV023-SUB-CL-NBR =  PV-HOLD-SUB-CL AND                           
013600         RV023-SKU        =  PV-HOLD-SKU    AND                           
013700         RV023-RTL-AMT    =  PV-HOLD-RETAIL                               
013800         ADD 1          TO PV-SKU-COUNT                                   
013900     ELSE                                                                 
014000         PERFORM 2100-SETUP-OUTPUT                                        
014100         PERFORM 8000-WRITE-INVNTRY-CNT-RECORD                            
014200         MOVE 0                TO PV-SKU-COUNT                            
014300         MOVE RV023-RTV-LOC    TO PV-HOLD-LOC                             
014300         MOVE RV023-DEPT-NBR   TO PV-HOLD-DEPT                            
014400         MOVE RV023-SUB-CL-NBR TO PV-HOLD-SUB-CL                          
014500         MOVE RV023-SKU        TO PV-HOLD-SKU                             
014600         MOVE RV023-RTL-AMT    TO PV-HOLD-RETAIL                          
014700         ADD 1                 TO PV-SKU-COUNT                            
014800     END-IF.                                                              
014900                                                                          
015000     PERFORM 7000-READ-EXTRACT-FILE.                                      
015100                                                                          
015200******************************************************************        
015300* 2100-SETUP-OUTPUT                                                       
015400*   - READ DETAIL INFORMATION                                             
015500*   - SUMMARIZE DETAILS BY DEPARTMENT, CLASS, SKU, RETAIL                 
015600*   - WRITE INVENTORY RECORD                                              
015700******************************************************************        
015800 2100-SETUP-OUTPUT.                                                       
013310     INITIALIZE RV024-SKU-RECORD.                                         
015920     MOVE PV-HOLD-LOC          TO RV024-CNTRL-STK-ID                      
016000     MOVE PV-HOLD-DEPT         TO RV024-DEPT.                             
016100     MOVE PV-HOLD-SUB-CL       TO RV024-CLASS.                            
016200     MOVE PV-HOLD-SKU          TO RV024-SKU-NBR.                          
016300     MOVE PV-HOLD-RETAIL       TO RV024-CUR-UNIT-RTL-AMT.                 
016400     MOVE PV-SKU-COUNT         TO RV024-AVAILABLE.                        
016500                                                                          
016600******************************************************************        
016700* 7000-READ-EXTRACT-FILE.                                        *        
016800*  THIS PROCEDURE WILL READ ONE EXTRACT RECORD FROM THE EXTRACT  *        
016900*  FLAT FILE.                                                    *        
017000******************************************************************        
017100 7000-READ-EXTRACT-FILE.                                                  
017200     READ RTV-REPORT-EXTRACT                                              
017300         INTO RV023-INVENTORY-RECORD                                      
017400         AT END SET PS-END-OF-EXTRACT-FILE TO TRUE.                       
017500                                                                          
011700     IF PS-NOT-END-OF-EXTRACT-FILE                                        
017600         ADD 1 TO PV-RECS-READ                                            
011700     END-IF.                                                              
017700                                                                          
017800******************************************************************        
017900* 8000-WRITE-INVNTRY-CNT-RECORD                                  *        
018000*  THIS PROCEDURE WILL WRITE ONE RECORD TO INVENTORY COUNT FILE. *        
018100******************************************************************        
018200 8000-WRITE-INVNTRY-CNT-RECORD.                                           
018300     WRITE INVENTORY-CNT-RECORD                                           
018400         FROM RV024-SKU-RECORD.                                           
018500                                                                          
018600     ADD 1 TO PV-RECS-WRITTEN.                                            
018700                                                                          
018800******************************************************************        
018900*9000-CLOSE-FILES.                                                        
019000******************************************************************        
019100 9000-CLOSE-FILES.                                                        
019200     PERFORM 2100-SETUP-OUTPUT.                                           
019200     PERFORM 8000-WRITE-INVNTRY-CNT-RECORD.                               
019200     CLOSE INVENTORY-CNT-FILE                                             
019300           RTV-REPORT-EXTRACT.                                            
019400                                                                          
019400     DISPLAY '*************  RVKUT011 **************'.                    
019500     DISPLAY 'INPUT RECORDS READ ==>  ' PV-RECS-READ.                     
019600     DISPLAY 'REPORT RECORDS WRITTEN  ==>  ' PV-RECS-WRITTEN.             
019400     DISPLAY '*************  RVKUT011 **************'.                    
019700                                                                          
019800*************   END OF RVKUT011  *********************************        
