       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         ETKUT112.                                            
       DATE-WRITTEN.       03-03-2000.                                          
       DATE-COMPILED.                                                           
                                                                                
************************************************************************        
      *PURPOSE:                                                                 
      * THIS PROGRAM WILL CREATE AN EXTRACT FILE THAT WILL BE USED TO           
      * UPDATE DOCUMENT TRACKING WITH THE TIMESTAMP WHEN 810S ARE               
      * PASSED TO THE APPLICATION.                                              
      *                     810 - INVOICES.                                     
************************************************************************        
0228JF* 02-28-2001 JON FIEDLER 0228JF                                           
0228JF* ADDED THE TRN-TXT-DTL AND THE INCHG-CNTL-NBR TO THE SYSOUT              
0228JF* ERROR MESSAGE.                                                          
************************************************************************        
JF1202* 12-05-2002 JON FIEDLER        WR/IR #: PM00511884                       
JF1202* REMOVED WS-SUB VARIABLE AND CODE ASSOCIATED WITH VENDOR ID              
JF1202* HAVING MORE THAN 5 VENDORS SHARING THAT VENDOR ID.                      
JF1202* THIS IS BEING REMOVED BECAUSE WE HAVE A GROUP OF 7 VENDORS              
JF1202* SHARING AN ID AND 2 ARE NOT HAVING THEIR LIFE CYCLE STATUS              
JF1202* UPDATED BECAUSE OF THIS CONDITIONAL STATEMENT.                          
***********************************************************************         
C94517*  CHANGED 11/06/2014    BY COGNIZANT       WR: CHG0094517                
C94517*                                                                         
C94517* EXTENDED PO# FROM 7 TO 8 DIGITS WHERE NEEDED.                           
C94517* RECOMPILED FOR APRD050 8 DIGITS PO# EXPANDED.                           
C94517*-----------------------------------------------------------------        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO IN01.                                                  
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 350 CHARACTERS                                       
           DATA RECORD IS INPUT-INV-RECORD.                                     
                                                                                
       01  INPUT-INV-RECORD          PIC X(350).                                
                                                                                
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS OUTPUT-RECORD.                                        
                                                                                
           COPY ETRD023.                                                        
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  WS-AP050-RECORD.                                                     
           COPY APRD050.                                                        
                                                                                
       01  WS-TALLY                      PIC S9(09) COMP VALUE ZEROS.           
       01  WS-DB2-TIMESTAMP              PIC X(26) VALUE SPACES.                
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  END-OF-FILE-SW            PIC X(1)  VALUE 'N'.                   
               88  END-OF-FILE                     VALUE 'Y'.                   
           05  END-OF-TRDPRT-SW          PIC X(1)  VALUE 'N'.                   
               88  END-OF-TRDPRT                   VALUE 'Y'.                   
           05  ERRORS-FOUND-SW           PIC X(1)  VALUE 'N'.                   
               88  ERRORS-FOUND                    VALUE 'Y'.                   
                                                                                
       01  PV-PARAGRAPH-NAME             PIC X(30).                             
       01  PV-DB2-OPER-ATTEMPTED         PIC X(30).                             
                                                                                
       01  ABEND-CODE                PIC S9(04)  VALUE +0 COMP SYNC.            
           88 ABEND-4013                         VALUE +4013.                   
           88 ABEND-4095                         VALUE +4095.                   
                                                                                
      ******************************************************************        
      * SQLCA MESSAGE MODULE DSNTIAR ARGUMENTS......                            
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      * DB2 ERROR HANDLING ARGUMENTS....                                        
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * TRADING PARTNER TABLE ARGUMENTS.....                                    
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * TABLE CURSOR FOR THE TRADING PARTNER FINCTIONAL CONTROL GROUP.          
      ******************************************************************        
           EXEC SQL                                                             
             DECLARE TRDPRT_CURSOR CURSOR FOR                                   
               SELECT TP_DKEY                                                   
               FROM TTRDPRT                                                     
               WHERE VEND_INCHG_ID_DESC = :AP050-EDI-SENDER-ID-QUAL             
               AND VEND_INCHG_ID = :AP050-EDI-SENDER-ID                         
DUPREC         AND INACTV_DTE = '9999-09-09'                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *FLOW:    THE MODULE SEQUENTIALLY READS THE INVOICING DATASET             
      *      LOOKING FOR ALL HEADER RECORDS. THE TRADING PARTNER TABLE          
      *      IS THEN READ TO CAPTURE THE TP-DKEY KEY UNIQUE TO THE ISA          
      *      NUMBER AND QUALIFIER.                                              
      *                                                                         
      *         WHEN WE HAVE A INVOICING HEADER RECORD, PLUG THE                
      *      DOCUMENT TRACKING EXTRACT DATASET WITH INFORMATION NEEDED          
      *      BY THE DOCUMENT TRACKING ENVIRONMENT.                              
      *                                                                         
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE-DRIVER-SECTION.                                            
                                                                                
           PERFORM 0100-INITIALIZATION                                          
              THRU 0100-INITIALIZATION-EXIT.                                    
                                                                                
           PERFORM 1000-PROCESS-INPUT-FILE                                      
              THRU 1000-PROCESS-INPUT-FILE-EXIT                                 
                 UNTIL END-OF-FILE.                                             
                                                                                
           PERFORM 9000-END-OF-JOB-TASKS                                        
              THRU 9000-END-OF-JOB-TASKS-EXIT.                                  
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 0100-INITIALIZATION.....                                                
      ******************************************************************        
       0100-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  INPUT-FILE                                               
                OUTPUT OUTPUT-FILE.                                             
                                                                                
           EXEC SQL                                                             
              SET :WS-DB2-TIMESTAMP = CURRENT TIMESTAMP                         
           END-EXEC.                                                            
                                                                                
           MOVE 'N'  TO END-OF-FILE-SW.                                         
           MOVE 'N'  TO END-OF-TRDPRT-SW.                                       
           MOVE 'N'  TO ERRORS-FOUND-SW.                                        
                                                                                
           INITIALIZE INPUT-INV-RECORD.                                         
           INITIALIZE WS-AP050-RECORD.                                          
           INITIALIZE ET023-EXTRACT-RECORD.                                     
                                                                                
           PERFORM 1100-READ-INPUT-FILE                                         
              THRU 1100-READ-INPUT-FILE-EXIT.                                   
                                                                                
       0100-INITIALIZATION-EXIT.                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 1000-PROCESS THE INPUT FILE..                                           
      *  READ THE DATASET SEQUENTIALLY. ONLY PROCESS THE HEADER RECORDS.        
      * PLUG THE DOCUMENT TRACKING DATASET WITH INFORMATION NEEDED FOR          
      * DOCUMENT TRACKING..                                                     
      ******************************************************************        
       1000-PROCESS-INPUT-FILE.                                                 
                                                                                
           MOVE INPUT-INV-RECORD TO WS-AP050-RECORD.                            
                                                                                
           IF AP050-INV-INTRCHG                                                 
               NEXT SENTENCE                                                    
           ELSE                                                                 
              PERFORM 1100-READ-INPUT-FILE                                      
                 THRU 1100-READ-INPUT-FILE-EXIT                                 
              GO TO 1000-PROCESS-INPUT-FILE-EXIT.                               
                                                                                
           EXEC SQL                                                             
              OPEN TRDPRT_CURSOR                                                
           END-EXEC.                                                            
           IF SQLCODE NOT = +000                                                
              MOVE '1000-PROCESS-INPUT-FILE' TO PV-PARAGRAPH-NAME               
              MOVE 'OPEN CURSOR' TO PV-DB2-OPER-ATTEMPTED                       
              PERFORM 9900-SQL-ERROR.                                           
                                                                                
           MOVE 'N' TO END-OF-TRDPRT-SW.                                        
           PERFORM 1200-GET-TP-DKEY                                             
              THRU 1200-GET-TP-DKEY-EXIT                                        
                 UNTIL END-OF-TRDPRT.                                           
                                                                                
           EXEC SQL                                                             
              CLOSE TRDPRT_CURSOR                                               
           END-EXEC.                                                            
           IF SQLCODE NOT = +000                                                
              MOVE '1000-PROCESS-INPUT-FILE' TO PV-PARAGRAPH-NAME               
              MOVE 'CLOSE CURSOR' TO PV-DB2-OPER-ATTEMPTED                      
              PERFORM 9900-SQL-ERROR.                                           
                                                                                
           PERFORM 1100-READ-INPUT-FILE                                         
              THRU 1100-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-PROCESS-INPUT-FILE-EXIT.                                            
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 1100-READ THE INPUT FILE.                                               
      ******************************************************************        
       1100-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE                                                      
              AT END                                                            
                 SET END-OF-FILE TO TRUE.                                       
                                                                                
       1100-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 1200-GET-TP-KEY..                                                       
      *   READ THE TRADING PARTNER TABLE TO GET THE TP-DKEY. LOAD THE           
      * EXTRACT DATASET WITH HEADER INFORMATION SPECIFICALLY NEEDED FOR         
      * DOCUMENT TRACKING.                                                      
      ******************************************************************        
       1200-GET-TP-DKEY.                                                        
                                                                                
           EXEC SQL                                                             
               FETCH TRDPRT_CURSOR                                              
               INTO :TRDPRT-TP-DKEY                                             
           END-EXEC.                                                            
                                                                                
                                                                                
           IF SQLCODE IS EQUAL TO ZEROS                                         
              MOVE TRDPRT-TP-DKEY      TO ET023-TP-DKEY                         
              MOVE AP050-EDI-TMST-KEY  TO ET023-TP-DKEY-TMST                    
              MOVE AP050-GS-CNTL-NBR   TO ET023-FUNC-CDE-CNTL-NBR               
              MOVE AP050-TS-CNTL-NBR   TO ET023-TRN-SET-CNTL-NBR                
              MOVE WS-DB2-TIMESTAMP    TO ET023-TRN-RLSE-TMST                   
              MOVE '810'               TO ET023-TRN-SET-CDE                     
              MOVE ZEROS               TO WS-TALLY                              
              INSPECT AP050-VENDOR-INVOICE-NBR                                  
                 TALLYING WS-TALLY FOR LEADING ZEROS                            
              MOVE AP050-VENDOR-INVOICE-NBR                                     
                    (1 + WS-TALLY:22 - WS-TALLY) TO ET023-TRN-DTL-TXT           
              WRITE ET023-EXTRACT-RECORD                                        
           ELSE                                                                 
JF1202     IF SQLCODE IS EQUAL TO +100                                          
              MOVE 'Y' TO END-OF-TRDPRT-SW                                      
           ELSE                                                                 
              DISPLAY SPACES                                                    
              DISPLAY 'UNABLE TO LOCATE TTRDPRT TABLE ENTRY'                    
              DISPLAY 'VEND_INCHG_ID_DESC = ' AP050-EDI-SENDER-ID-QUAL          
              DISPLAY 'VEND_INCHG_ID = ' AP050-EDI-SENDER-ID                    
              DISPLAY 'FUNC-CDE-CNTL-NBR = ' AP050-GS-CNTL-NBR                  
0228JF        DISPLAY 'TRN-DTL-TXT = ' AP050-VENDOR-INVOICE-NBR                 
              DISPLAY 'TRN-SET-CNTL-NBR = ' AP050-TS-CNTL-NBR                   
0228JF        DISPLAY 'INCHG-CNTL-NBR = ' AP050-INTRCHG-CNTL-NBR                
              DISPLAY 'TRN-SET-CDE = 810'                                       
              DISPLAY 'SQLCODE = ' SQLCODE                                      
              DISPLAY 'DATASET = ' WS-AP050-RECORD                              
              MOVE 'Y' TO END-OF-TRDPRT-SW                                      
              MOVE 'Y' TO ERRORS-FOUND-SW.                                      
                                                                                
       1200-GET-TP-DKEY-EXIT.                                                   
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 9000-END OF JOB TASKS.                                                  
      ******************************************************************        
       9000-END-OF-JOB-TASKS.                                                   
                                                                                
           IF ERRORS-FOUND AND                                                  
              ABEND-CODE IS NOT EQUAL TO +4013                                  
               SET ABEND-4095 TO TRUE.                                          
                                                                                
           CLOSE INPUT-FILE                                                     
                 OUTPUT-FILE.                                                   
                                                                                
       9000-END-OF-JOB-TASKS-EXIT.                                              
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 9900-SEQ ERROR HANDLING ROUTINE.                                        
      ******************************************************************        
       9900-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
