000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    SUKUT182.                                                 
000600 AUTHOR.        DENISE HAHN                                               
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   MAY, 2016.                                                
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM CREATES AN EXTRACT FILE OF SHIPPED TRAILERS FOR           
001300*  BRICK AND MORTAR STORES BASED ON A GIVEN TIME FRAME.                   
001301*                                                                         
001302*  TIME FRAME IS DETERMINED BY PRIOR STEP CREATING DATE RANGE.            
001303*  M-MONTHLY - SUKUT065 CALCULATES FISCAL DATE RANGE.                     
001304*  DATE RANGE INPUT FILE - SURD032 FORMAT                                 
001305*                                                                         
001306*  CURSOR WILL BRING BACK OUTBOUND TRIPS SHIPPED FOR THE GIVEN            
001307*  TIME FRAME.                                                            
001308*                                                                         
001309*  FOR EACH TRIP, PROGRAM WILL COUNT THE NUMBER OF DIVERTED               
001310*  CARTONS AND SCANNED ON CARTONS                                         
001311*                                                                         
001317*  EXTRACT FILE WILL BE CREATED WITH THE FOLLOWING                        
001318*  TRIP #, # SCANNED ON,  # DIVERTED                                      
001319*                                                                         
001320*  EXTRACT FILE WILL BE USED BY PROGRAM SUKRP073                          
001330*                                                                         
001332*  OUTPUT FILE IS IN SURD133 FORMAT                                       
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
003300     SELECT INPUT-FILE                                                    
003400            ASSIGN TO INP01.                                              
003401                                                                          
003402     SELECT OUTPUT-FILE                                                   
003403            ASSIGN TO OUT01.                                              
003410                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
003910                                                                          
004000 FD  INPUT-FILE                                                           
004100     RECORDING MODE IS F                                                  
004200     LABEL RECORDS ARE STANDARD                                           
004400     BLOCK CONTAINS 0 RECORDS.                                            
004600 01  INPUT-RECORD                     PIC X(80).                          
004700                                                                          
004800 FD  OUTPUT-FILE                                                          
004900     RECORDING MODE IS F                                                  
005000     LABEL RECORDS ARE STANDARD                                           
005100     BLOCK CONTAINS 0 RECORDS.                                            
005300 01  OUTPUT-RECORD              PIC X(75).                                
005470                                                                          
005500                                                                          
005600******************************************************************        
005700 WORKING-STORAGE SECTION.                                                 
005800******************************************************************        
005801                                                                          
005810 01  DK-FIELDS.                                                           
005820     05  DK-TRIP-ID                         PIC S9(09)  COMP.             
005821     05  DK-DEST-OPER                       PIC S9(04)  COMP.             
005830                                                                          
005840 01  PROGRAM-VARIABLES.                                                   
005841     05  PV-WORK-TIMESTAMP.                                               
005842         10  PV-WORK-TMST-DATE              PIC X(10).                    
005843         10  PV-WORK-TMST-TIME              PIC X(16).                    
005844             88 PV-WORK-TIME-BEGIN VALUE '-00.00.00.000000'.              
005845             88 PV-WORK-TIME-END   VALUE '-23.59.59.999999'.              
005846*                                                                         
005847     05  PV-FROM-TIMESTAMP                  PIC  X(26).                   
005848     05  PV-TO-TIMESTAMP                    PIC  X(26).                   
005849*                                                                         
005850     05  PV-PARAGRAPH-NAME                  PIC  X(40).                   
005851     05  PV-DB2-OPERATION-ATTEMPTED         PIC  X(40).                   
005852*                                                                         
005853     05  PV-MESSAGE-1                       PIC  X(100).                  
005854     05  PV-MESSAGE-2                       PIC  X(100).                  
005855     05  PV-SQLCODE                         PIC  -999.                    
005860*                                                                         
005866*                                                                         
005867     05  PV-DISP-OPER                       PIC  Z(04).                   
005868     05  PV-DISP-STORE                      PIC  Z(04).                   
005869     05  PV-DISP-TRIP-ID                    PIC  Z(09).                   
005870*                                                                         
005890     05  PV-DISPLAY-COUNT                   PIC ZZZ,ZZZ,ZZZ.              
005891     05  PV-DIVERT-COUNT                    PIC S9(09)  COMP.             
005892     05  PV-SCANON-COUNT                    PIC S9(09)  COMP.             
005893     05  PV-WRITE-COUNTER                   PIC  9(09).                   
005894     05  PV-FETCH-COUNT                     PIC  9(09).                   
005896     05  PV-FETCH-COUNT-DISP                PIC  9(09).                   
005897                                                                          
005898 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
005899                                                   COMP SYNC.             
005900 01  PROGRAM-SWITCHES.                                                    
005901     05  PS-EOF-CURSOR-SW                   PIC X(01) VALUE 'N'.          
005902         88  PS-EOF-CURSOR                            VALUE 'Y'.          
005903         88  PS-EOF-CURSOR-NO                         VALUE 'N'.          
005909                                                                          
005910******************************************************************        
005911* OUTPUT RECORD LAYOUT - EXTRACT DATE RANGE                               
005912******************************************************************        
005920     COPY SURD032.                                                        
005930*                                                                         
005931******************************************************************        
005932* OUTPUT RECORD LAYOUT                                                    
005933******************************************************************        
005934     COPY SURD133.                                                        
005935*                                                                         
005936* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
005937* TSUOBTR - OUTBOUND TRAILER TRIP TABLE                                   
005938* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
005939     EXEC SQL                                                             
005940          INCLUDE TSUOBTR                                                 
005941     END-EXEC.                                                            
005942                                                                          
005943* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
005944* TSUOBTS - OUTBOUND TRAILER TRIP STATUS TABLE                            
005945* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
005946     EXEC SQL                                                             
005947          INCLUDE TSUOBTS                                                 
005948     END-EXEC.                                                            
005949                                                                          
005950* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
005951* TSUCDOT - CONVEYOR CUTOFF                                               
005952* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
005953     EXEC SQL                                                             
005954          INCLUDE TSUCDOT                                                 
005955     END-EXEC.                                                            
005956                                                                          
005957* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
005958* TSUCRDV - CONVEYOR DIVERT                                               
005959* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
005960     EXEC SQL                                                             
005961          INCLUDE TSUCRDV                                                 
005962     END-EXEC.                                                            
005963                                                                          
005964* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
005965* SUT00024 - OUTBOUND CARTON - SUT_OTBND_CART                             
005966* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
005967     EXEC SQL                                                             
005968          INCLUDE SUT00024                                                
005969     END-EXEC.                                                            
005970                                                                          
005971* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
005972* SUT00027 - OUTBOUND LOCATION - SUT_OTBND_LOC                            
005973* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
005974     EXEC SQL                                                             
005975          INCLUDE SUT00027                                                
005976     END-EXEC.                                                            
005977                                                                          
005978* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
005979* XLT_LOC - LOGISTIC DOMAIN LOCATION DATABASE                             
005980* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
005981     EXEC SQL                                                             
005982          INCLUDE XLT00010                                                
005983     END-EXEC.                                                            
005990                                                                          
006210* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006211*                                                                         
006212* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006213     EXEC SQL                                                             
006214          INCLUDE SQLCA                                                   
006215     END-EXEC.                                                            
006220                                                                          
006230                                                                          
006240* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006250*                                                                         
006260* SHIPPING DOOR CURSOR - FOR BRICK & MORTAR STORES                        
006270*                                                                         
006280* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006290     EXEC SQL                                                             
006291         DECLARE DC-SHIP-TRLR-CSR CURSOR FOR                              
006292         SELECT A.ORGN_OPER_UNT_ID                                        
006293              , A.DEST_OPER_UNT_ID                                        
006294              , A.OTBND_TRIP_ID                                           
006298           FROM TSUOBTR A                                                 
006299              , TSUOBTS B                                                 
006300              , XLT_LOC C                                                 
006301         WHERE A.DEST_OPER_UNT_ID  =  C.LOC_NBR                           
006302           AND A.DOOR_TYP_CDE      = 'SH'                                 
006303           AND C.LOC_TYP_CDE       = 'DS'                                 
006310           AND C.E_BUS_IND         = 'N'                                  
006320           AND A.OTBND_TRIP_ID     =  B.OTBND_TRIP_ID                     
006330           AND B.TRIP_STAT_CDE     = 'DS'                                 
006340           AND B.OTBND_STAT_TMST  >= :PV-FROM-TIMESTAMP                   
006350           AND B.OTBND_STAT_TMST  <= :PV-TO-TIMESTAMP                     
006351         ORDER BY A.ORGN_OPER_UNT_ID                                      
006352                , A.DEST_OPER_UNT_ID                                      
006353                , A.OTBND_TRIP_ID                                         
006360         WITH UR                                                          
006395     END-EXEC.                                                            
006396                                                                          
006400                                                                          
007300*-----------------------------------------------------------------        
007500*  DB2 FORMATTED MESSAGE AREA                                             
007510*-----------------------------------------------------------------        
007700     COPY DPWS004.                                                        
007800                                                                          
018300                                                                          
018400                                                                          
018600 PROCEDURE DIVISION.                                                      
018801                                                                          
018810*-----------------------------------------------------------------        
018820*                                                                         
018840*-----------------------------------------------------------------        
018900 0000-PROGRAM-MAINLINE.                                                   
019400                                                                          
019500     PERFORM 1000-INITIALIZE.                                             
019510                                                                          
019520     DISPLAY '********************************************'.              
019530     DISPLAY '*                                          *'.              
019540     DISPLAY '*                                          *'.              
019600     IF  PS-EOF-CURSOR-NO                                                 
019700         PERFORM 2000-PROCESS-CURSOR                                      
019800           UNTIL PS-EOF-CURSOR                                            
019900     END-IF.                                                              
019910                                                                          
020000     PERFORM 9000-EOJ-ROUTINE.                                            
020113                                                                          
020120     DISPLAY '*                                          *'.              
020130     DISPLAY '*                                          *'.              
020131     DISPLAY '********************************************'.              
020140                                                                          
020200     STOP RUN.                                                            
020300                                                                          
020310                                                                          
020320*-----------------------------------------------------------------        
020330*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
020340*    -- OPEN INPUT FILE                                                   
020341*    -- OPEN OUTPUT FILE                                                  
020350*    -- OPEN CURSOR                                                       
020360*    -- FETCH CURSOR                                                      
020361*    -- IF NO TRAILERS SHIPPED - JOB FINISHES - NOTHING TO REPORT         
020370*-----------------------------------------------------------------        
020400 1000-INITIALIZE.                                                         
021100                                                                          
021500     OPEN INPUT  INPUT-FILE                                               
021510          OUTPUT OUTPUT-FILE.                                             
021600                                                                          
021700     PERFORM 8000-READ-CONTROL-REC.                                       
021710                                                                          
021711     IF  PS-EOF-CURSOR-NO                                                 
021712         MOVE SU032-FROM-DATE    TO PV-WORK-TMST-DATE                     
021713         SET PV-WORK-TIME-BEGIN  TO TRUE                                  
021714         MOVE  PV-WORK-TIMESTAMP TO PV-FROM-TIMESTAMP                     
021717         MOVE SU032-TO-DATE      TO PV-WORK-TMST-DATE                     
021718         SET PV-WORK-TIME-END    TO TRUE                                  
021719         MOVE  PV-WORK-TIMESTAMP TO PV-TO-TIMESTAMP                       
021720         DISPLAY ' SUKUT182'                                              
021721                 ' FROM:' PV-FROM-TIMESTAMP                               
021722                 ' TO:'   PV-TO-TIMESTAMP                                 
021723                                                                          
021800         PERFORM 7000-OPEN-CURSOR                                         
021900         PERFORM 7005-FETCH-CURSOR                                        
021910     END-IF.                                                              
022000                                                                          
022001     IF  PS-EOF-CURSOR                                                    
022002         MOVE 4095  TO  ABEND-CODE                                        
022003     END-IF.                                                              
022004                                                                          
022010                                                                          
025449*-----------------------------------------------------------------        
025450*  LOOP THROUGH THE RESULTS TABLE.                                        
025451*  FORMAT THE OUTPUT RECORD.  WRITE THE RECORDS THAT MEET CRITERIA.       
025452*-----------------------------------------------------------------        
025460 2000-PROCESS-CURSOR.                                                     
026000                                                                          
026001     PERFORM 3000-COUNT-DIVERT.                                           
026010     PERFORM 3010-COUNT-SCANON.                                           
026095                                                                          
026096     PERFORM 5000-FORMAT-OUTPUT-RECORD.                                   
026152                                                                          
026153     PERFORM 8100-WRITE-OUTPUT-RECORD.                                    
026160                                                                          
026200     PERFORM 7005-FETCH-CURSOR.                                           
026300                                                                          
026310                                                                          
026312*-----------------------------------------------------------------        
026313*  COUNT THE NUMBER OF CARTONS THAT WERE DIVERTED TO A TRAILER.           
026316*-----------------------------------------------------------------        
026319 3000-COUNT-DIVERT.                                                       
026320                                                                          
026321     MOVE  SUOBTR-OTBND-TRIP-ID     TO  DK-TRIP-ID.                       
026322     MOVE  SUOBTR-DEST-OPER-UNT-ID  TO  DK-DEST-OPER.                     
026323     MOVE  ZEROES                   TO  PV-DIVERT-COUNT.                  
026324                                                                          
026325     EXEC SQL                                                             
026326         SELECT COUNT(*)                                                  
026327           INTO :PV-DIVERT-COUNT                                          
026328           FROM TSUCDOT A                                                 
026329              , TSUCRDV B                                                 
026330              , SUT_OTBND_CART C                                          
026331          WHERE A.OTBND_TRIP_ID         = :DK-TRIP-ID                     
026332            AND A.OPER_UNT_ID           =  B.OPER_UNT_ID                  
026333            AND A.DOOR_ID               =  B.DOOR_ID                      
026334            AND A.STRT_DIVERT_TMST     <=  B.DIVERT_TMST                  
026335            AND A.END_DIVERT_TMST      >=  B.DIVERT_TMST                  
026336            AND B.DIVERT_TMST  IN                                         
026337                (SELECT MAX(E.DIVERT_TMST)                                
026338                   FROM TSUCRDV E                                         
026339                  WHERE B.SHIPT_UNT_ID  =  E.SHIPT_UNT_ID                 
026340                    AND E.ACTV_CDE      = 'A')                            
026341            AND B.ACTV_CDE              =  'A'                            
026342            AND B.SHIPT_UNT_ID          =  C.SHIPT_UNT_ID                 
026343            AND C.DEST_LOC_NBR          = :DK-DEST-OPER                   
026370           WITH UR                                                        
026371                                                                          
026372     END-EXEC.                                                            
026373                                                                          
026374     EVALUATE TRUE                                                        
026375         WHEN SQLCODE = ZERO                                              
026376              CONTINUE                                                    
026379                                                                          
026380         WHEN SQLCODE = +100                                              
026381              MOVE  ZEROES  TO  PV-DIVERT-COUNT                           
026382                                                                          
026383         WHEN SQLWARN0 NOT = SPACES                                       
026384         WHEN SQLCODE < ZERO                                              
026385         WHEN SQLCODE > ZERO                                              
026386             MOVE SQLCODE              TO PV-SQLCODE                      
026387             MOVE SUOBTR-OTBND-TRIP-ID    TO PV-DISP-OPER                 
026388             MOVE SUOBTR-ORGN-OPER-UNT-ID TO PV-DISP-STORE                
026389             MOVE SUOBTR-DEST-OPER-UNT-ID TO PV-DISP-TRIP-ID              
026391*                                                                         
026392              STRING '3000-'                                              
026393                     ' SQL:'   PV-SQLCODE                                 
026394                     ' OPER:'  PV-DISP-OPER                               
026395                     ' DEST:'  PV-DISP-STORE                              
026396                     ' TRIP:'  PV-DISP-TRIP-ID                            
026401                               DELIMITED BY SIZE                          
026402                          INTO PV-MESSAGE-1                               
026403                                                                          
026418             DISPLAY PV-MESSAGE-1                                         
026420                                                                          
026421             MOVE '3000-DIVERT-COUNT          '                           
026422                                 TO PV-PARAGRAPH-NAME                     
026423             MOVE 'COUNT DIVERTED CARTONS         '                       
026424                                 TO PV-DB2-OPERATION-ATTEMPTED            
026425             PERFORM 9100-SQL-ABEND                                       
026426     END-EVALUATE.                                                        
026427                                                                          
026428                                                                          
026429*-----------------------------------------------------------------        
026430*  COUNT THE NUMBER OF CARTONS THAT WERE SCANNED ONTO THE TRAILER.        
026433*-----------------------------------------------------------------        
026434 3010-COUNT-SCANON.                                                       
026435                                                                          
026436     MOVE  SUOBTR-OTBND-TRIP-ID     TO  DK-TRIP-ID.                       
026437     MOVE  SUOBTR-DEST-OPER-UNT-ID  TO  DK-DEST-OPER.                     
026438     MOVE  ZEROES                   TO  PV-SCANON-COUNT.                  
026439                                                                          
026440     EXEC SQL                                                             
026441         SELECT COUNT(*)                                                  
026442           INTO :PV-SCANON-COUNT                                          
026443           FROM SUT_OTBND_LOC                                             
026444          WHERE OTBND_TRIP_ID = :DK-TRIP-ID                               
026445            AND SCN_CDE       = 'SHON'                                    
026446            AND ACTV_CDE      = 'A'                                       
026466           WITH UR                                                        
026467                                                                          
026468     END-EXEC.                                                            
026469                                                                          
026470     EVALUATE TRUE                                                        
026471         WHEN SQLCODE = ZERO                                              
026472              CONTINUE                                                    
026473                                                                          
026474     EVALUATE TRUE                                                        
026475         WHEN SQLCODE = +100                                              
026476              MOVE ZEROES  TO  PV-SCANON-COUNT                            
026477                                                                          
026478         WHEN SQLWARN0 NOT = SPACES                                       
026479         WHEN SQLCODE < ZERO                                              
026480         WHEN SQLCODE > ZERO                                              
026481             MOVE SQLCODE              TO PV-SQLCODE                      
026482             MOVE SUOBTR-OTBND-TRIP-ID    TO PV-DISP-OPER                 
026483             MOVE SUOBTR-ORGN-OPER-UNT-ID TO PV-DISP-STORE                
026484             MOVE SUOBTR-DEST-OPER-UNT-ID TO PV-DISP-TRIP-ID              
026485*                                                                         
026486              STRING '3010-'                                              
026487                     ' SQL:'   PV-SQLCODE                                 
026488                     ' OPER:'  PV-DISP-OPER                               
026489                     ' DEST:'  PV-DISP-STORE                              
026490                     ' TRIP:'  PV-DISP-TRIP-ID                            
026491                               DELIMITED BY SIZE                          
026492                          INTO PV-MESSAGE-1                               
026493                                                                          
026494             DISPLAY PV-MESSAGE-1                                         
026495                                                                          
026496             MOVE '3010-SCANON-COUNT          '                           
026497                                 TO PV-PARAGRAPH-NAME                     
026498             MOVE 'COUNT SCANNED ON CARTONS       '                       
026499                                 TO PV-DB2-OPERATION-ATTEMPTED            
026500             PERFORM 9100-SQL-ABEND                                       
026501     END-EVALUATE.                                                        
026502                                                                          
026510                                                                          
026600                                                                          
026860*-----------------------------------------------------------------        
026861*  FORMAT OUTPUT RECORD                                                   
026863*-----------------------------------------------------------------        
026864 5000-FORMAT-OUTPUT-RECORD.                                               
026865                                                                          
026866     INITIALIZE  SU133-EXTRACT-RECORD.                                    
026966*                                                                         
026967     MOVE SUOBTR-ORGN-OPER-UNT-ID  TO  SU133-OPER-UNT-ID.                 
026969     MOVE SUOBTR-DEST-OPER-UNT-ID  TO  SU133-STORE-DEST.                  
026971     MOVE SUOBTR-OTBND-TRIP-ID     TO  SU133-TRIP-ID.                     
026973     MOVE 1                        TO  SU133-TRAILER-COUNT.               
026975     MOVE PV-DIVERT-COUNT          TO  SU133-DIVERT-COUNT.                
026977     MOVE PV-SCANON-COUNT          TO  SU133-SCANON-COUNT.                
026982                                                                          
027032                                                                          
027033*-----------------------------------------------------------------        
027034*  OPEN THE CURSOR.                                                       
027040*-----------------------------------------------------------------        
027100 7000-OPEN-CURSOR.                                                        
027300                                                                          
027400     EXEC SQL                                                             
027500          OPEN DC-SHIP-TRLR-CSR                                           
027600     END-EXEC                                                             
027700                                                                          
027800     EVALUATE TRUE                                                        
027810         WHEN SQLCODE = ZERO                                              
027820              CONTINUE                                                    
027900         WHEN SQLCODE = -904                                              
028000         WHEN SQLCODE = -911                                              
028100              CONTINUE                                                    
028200         WHEN SQLWARN0 NOT = SPACES                                       
028300         WHEN SQLCODE < ZERO                                              
028400         WHEN SQLCODE > ZERO                                              
028410             MOVE SQLCODE              TO PV-SQLCODE                      
028472*                                                                         
028473              STRING '7000-'                                              
028474                     ' SQL:'   PV-SQLCODE                                 
028475                     ' FROM:'  PV-FROM-TIMESTAMP                          
028476                     ' TO:'    PV-TO-TIMESTAMP                            
028482                               DELIMITED BY SIZE                          
028483                          INTO PV-MESSAGE-1                               
028484                                                                          
028506                                                                          
028510             MOVE '7000-OPEN-CURSOR            '                          
028600                                 TO PV-PARAGRAPH-NAME                     
028700             MOVE 'OPEN THE DC-SHIP-TRLR-CSR   '                          
028800                                 TO PV-DB2-OPERATION-ATTEMPTED            
028900             PERFORM 9100-SQL-ABEND                                       
029200     END-EVALUATE.                                                        
029400                                                                          
030200                                                                          
030210*-----------------------------------------------------------------        
030220*  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.                 
030230*-----------------------------------------------------------------        
030300 7005-FETCH-CURSOR.                                                       
030500                                                                          
030700     EXEC SQL                                                             
030920          FETCH DC-SHIP-TRLR-CSR                                          
031000           INTO :SUOBTR-ORGN-OPER-UNT-ID                                  
031100               ,:SUOBTR-DEST-OPER-UNT-ID                                  
031101               ,:SUOBTR-OTBND-TRIP-ID                                     
032300     END-EXEC.                                                            
032400                                                                          
032500     EVALUATE TRUE                                                        
032600         WHEN SQLCODE = ZERO                                              
032610              MOVE SUOBTR-ORGN-OPER-UNT-ID  TO  PV-DISP-OPER              
032611              MOVE SUOBTR-DEST-OPER-UNT-ID  TO  PV-DISP-STORE             
032612              MOVE SUOBTR-OTBND-TRIP-ID     TO  PV-DISP-TRIP-ID           
032613                                                                          
032614              ADD  1  TO  PV-FETCH-COUNT                                  
032620                          PV-FETCH-COUNT-DISP                             
032630              IF  PV-FETCH-COUNT-DISP >= 10                               
032640                  DISPLAY PV-DISP-OPER '-'                                
032641                          PV-DISP-STORE '-'                               
032642                          PV-DISP-TRIP-ID '-'                             
032643                          ' FETCH: '  PV-FETCH-COUNT                      
032650                  MOVE  ZEROES  TO  PV-FETCH-COUNT-DISP                   
032660              END-IF                                                      
032700             CONTINUE                                                     
032710                                                                          
032800         WHEN SQLCODE = +100                                              
032900             SET PS-EOF-CURSOR          TO  TRUE                          
033000                                                                          
033100         WHEN SQLWARN0 NOT = SPACES                                       
033200         WHEN SQLCODE < ZERO                                              
033300         WHEN SQLCODE > ZERO                                              
033301             MOVE SQLCODE              TO PV-SQLCODE                      
033394              STRING '7005-'                                              
033395                     ' SQL:'   PV-SQLCODE                                 
033396                     ' FROM:'  PV-FROM-TIMESTAMP                          
033397                     ' TO:'    PV-TO-TIMESTAMP                            
033403                               DELIMITED BY SIZE                          
033404                          INTO PV-MESSAGE-1                               
033405             DISPLAY PV-MESSAGE-1                                         
033406                                                                          
033410             MOVE '7005-FETCH-CURSOR          '                           
033500                                 TO PV-PARAGRAPH-NAME                     
033600             MOVE 'FETCH THE DC-SHIP-TRLR-CSR     '                       
033700                                 TO PV-DB2-OPERATION-ATTEMPTED            
033800             PERFORM 9100-SQL-ABEND                                       
033900     END-EVALUATE.                                                        
034000                                                                          
034120                                                                          
034121*-----------------------------------------------------------------        
034122*  CLOSE THE CURSOR.                                                      
034123*-----------------------------------------------------------------        
034124 7010-CLOSE-CURSOR.                                                       
034125                                                                          
034126     EXEC SQL                                                             
034127          CLOSE DC-SHIP-TRLR-CSR                                          
034128     END-EXEC                                                             
034129                                                                          
034130     EVALUATE TRUE                                                        
034131         WHEN SQLCODE = ZERO                                              
034132              CONTINUE                                                    
034133         WHEN SQLCODE = -904                                              
034134         WHEN SQLCODE = -911                                              
034135              CONTINUE                                                    
034136         WHEN SQLWARN0 NOT = SPACES                                       
034137         WHEN SQLCODE < ZERO                                              
034138         WHEN SQLCODE > ZERO                                              
034139             MOVE SQLCODE              TO PV-SQLCODE                      
034147*                                                                         
034148              STRING '7010-'                                              
034149                     ' SQL:'   PV-SQLCODE                                 
034150                     ' FROM:'  PV-FROM-TIMESTAMP                          
034151                     ' TO:'    PV-TO-TIMESTAMP                            
034157                               DELIMITED BY SIZE                          
034158                          INTO PV-MESSAGE-1                               
034159                                                                          
034160             MOVE '7010-CLOSE-CURSOR           '                          
034161                                 TO PV-PARAGRAPH-NAME                     
034162             MOVE 'CLOSE THE DC-SHIP-TRLR-CSR   '                         
034163                                 TO PV-DB2-OPERATION-ATTEMPTED            
034164             PERFORM 9100-SQL-ABEND                                       
034165     END-EVALUATE.                                                        
034166                                                                          
034168                                                                          
034169*-----------------------------------------------------------------        
034170*  READ DATE CONTROL RECORD                                               
034171*-----------------------------------------------------------------        
034172 8000-READ-CONTROL-REC.                                                   
034173                                                                          
034174     READ INPUT-FILE       INTO                                           
034175           SU032-DATE-CONTROL-REC                                         
034176     AT END                                                               
034177         SET PS-EOF-CURSOR            TO TRUE                             
034178         DISPLAY 'PROGRAM:  SUKUT182'                                     
034179         DISPLAY 'NO DATE CONTROL CARD'                                   
034181         DISPLAY SPACES                                                   
034182     NOT AT END                                                           
034183         DISPLAY 'PROGRAM:  SUKUT182'                                     
034184         DISPLAY 'DATE CONTROL CARD:'                                     
034185                 SU032-DATE-CONTROL-REC                                   
034186         DISPLAY SPACES                                                   
034187     END-READ.                                                            
034188                                                                          
034189*-----------------------------------------------------------------        
034190*  WRITE THE SHIPMENT-UNIT-RECORD                                         
034191*-----------------------------------------------------------------        
034192 8100-WRITE-OUTPUT-RECORD.                                                
034200                                                                          
034600     WRITE OUTPUT-RECORD                                                  
034700      FROM SU133-EXTRACT-RECORD.                                          
034800     ADD +1 TO PV-WRITE-COUNTER.                                          
035690                                                                          
035700                                                                          
035710*-----------------------------------------------------------------        
035711*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
035720*-----------------------------------------------------------------        
035800 9000-EOJ-ROUTINE.                                                        
035900                                                                          
036000     PERFORM 7010-CLOSE-CURSOR.                                           
036100                                                                          
036300     CLOSE INPUT-FILE                                                     
036310           OUTPUT-FILE.                                                   
036400                                                                          
036500     MOVE PV-WRITE-COUNTER TO PV-DISPLAY-COUNT.                           
036600     DISPLAY 'TOTAL OUTPUT RECORDS:  ' PV-DISPLAY-COUNT.                  
036700                                                                          
040200                                                                          
040210*-----------------------------------------------------------------        
040220*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
040230*  ERROR OR WARNING CODE OCCURS.                                          
040240*-----------------------------------------------------------------        
040300 9100-SQL-ABEND.                                                          
040800     DISPLAY '9100-SQL-ABEND'.                                            
040900     DISPLAY '** ABEND     **'.                                           
041000     DISPLAY '** PROGRAM   ** =  SUKUT182'.                               
041100     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
041200     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
041300                                                                          
041400******************************************************************        
041500* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
041600* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
041700* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
041800* FORMAT.                                                                 
041900******************************************************************        
042000     COPY DPPD004.                                                        
042100                                                                          
042200     MOVE +4013                  TO ABEND-CODE.                           
042300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
042310/                                                                         
