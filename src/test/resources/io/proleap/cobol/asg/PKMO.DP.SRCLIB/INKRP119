       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         INKRP119.                                            
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       04-05-2001.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *PURPOSE:  UNIT BOOKING INVALID SKU REPORT....                            
      *                                                                         
      *CHANGE HISTORY:                                                          
W04061* DATE     04/06/2001             CHANGE ID=W04061                        
W04061*   INK365 - UNIT BOOKING PROCESS EFFICIENCIES (CREATION...)              
W04061*    MADE BY: ROD JANSSEN            WR/IR #:99999                        
      ******************************************************************        
RB0401*  CHANGES:                                                               
RB0401*     04/30/01 WR# 21591 RONNA BUTZ (LOOK FOR RB0401 TO IDENTIFY          
RB0401*                                                                         
RB0401*     CHANGES ARE FOR MANIFESTING.                                        
RB0401*     COPYBOOK INWS016 INCREASED IN SIZE                                  
RB0401*                                                                         
RB0401******************************************************************        
W21732******************************************************************        
W21732*CHANGES:                                                                 
W21732* DATE     01/20/2003                                                     
W21732*   8 DIGIT SKU IS ADDED IN PLACE OF DEPT, CLASS, SEQUENCE NUMBER         
W21732*   COPY BOOK INWS016 IS CHANGED FOR NI-SKU                               
W21732*    MADE BY: SUKUMAR,IGSI           WR/IR #:WR21732                      
W26600*                                                                         
W26600*  W26600  08/  /2004  PHYSICAL INVENTORY STORE EXPANSION                 
W26600*                      M JOHNSON                                          
W26600*                                                                         
W26600******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT MISSING-SKU-FILE                                              
           ASSIGN TO INP01.                                                     
                                                                                
           SELECT MISSING-SKU-REPORT-FILE                                       
           ASSIGN TO RPT01.                                                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  MISSING-SKU-FILE                                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W21732 01  MISSING-SKU-RECORD          PIC  X(80).                              
                                                                                
       FD  MISSING-SKU-REPORT-FILE                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  MISSING-SKU-REPORT-RECORD   PIC  X(132).                             
                                                                                
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  H1-HEADING-LINE.                                                     
           05  FILLER          PIC X(50)        VALUE SPACES.                   
           05  FILLER          PIC X(31)        VALUE                           
           'UNIT BOOKING INVALID SKU REPORT'.                                   
           05  FILLER          PIC X(51)        VALUE SPACES.                   
                                                                                
       01  H2-HEADING-LINE.                                                     
           05  FILLER                  PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'STORE'.                 
W21732     05  FILLER                  PIC X(01) VALUE SPACES.                  
W21732     05  FILLER                  PIC X(04) VALUE 'DEPT'.                  
W21732     05  FILLER                  PIC X(01) VALUE SPACES.                  
W21732     05  FILLER                  PIC X(05) VALUE 'CLASS'.                 
W21732     05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'SKU     '.              
           05  FILLER                  PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE 'QUANTITY  '.            
W21732     05  FILLER                  PIC X(77) VALUE SPACES.                  
                                                                                
       01  DL1-DETAIL-LINE.                                                     
           05  FILLER                  PIC X(10)    VALUE SPACES.               
W26600     05  DL1-STORE               PIC 9(04)    VALUE ZEROS.                
W26600     05  FILLER                  PIC X(02)    VALUE SPACES.               
W21732     05  DL1-DEPT                PIC X(04)    VALUE SPACES.               
W21732     05  FILLER                  PIC X(01)    VALUE SPACES.               
W21732     05  DL1-CLASS               PIC X(04)    VALUE SPACES.               
W21732     05  FILLER                  PIC X(02)    VALUE SPACES.               
           05  DL1-SKU                 PIC X(08)    VALUE SPACES.               
           05  FILLER                  PIC X(10)    VALUE SPACES.               
           05  DL1-QTY                 PIC Z,ZZZ,ZZ9-.                          
W21732     05  FILLER                  PIC X(77)    VALUE SPACES.               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-MORE-INPUT-RECS-SW     PIC X  VALUE 'Y'.              INKUT116
               88  PS-MORE-INPUT-RECS           VALUE 'Y'.              INKUT116
               88  PS-NO-MORE-INPUT-RECS        VALUE 'N'.              INKUT116
                                                                                
       01  PC-PRINT-CONTROL.                                                    
           05  PC-LINES-PER-PAGE       PIC S9(03)  VALUE +57  COMP-3.           
           05  PC-ADVANCE              PIC S9(03)  VALUE ZERO COMP-3.           
           05  PC-PAGE-COUNT           PIC S9(05)  VALUE ZERO COMP-3.           
           05  PC-LINE-COUNT           PIC S9(03)  VALUE +100 COMP-3.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-TODAYS-DATE.                                                  
               10  PV-YY               PIC 9(02)     VALUE ZERO.                
               10  PV-MM               PIC 9(02)     VALUE ZERO.                
               10  PV-DD               PIC 9(02)     VALUE ZERO.                
           05  PV-TODAYS-TIME.                                                  
               10  PV-HR               PIC 9(02)     VALUE ZERO.                
               10  PV-MIN              PIC 9(02)     VALUE ZERO.                
               10  PV-SEC              PIC 9(02)     VALUE ZERO.                
               10  FILLER              PIC 9(02)     VALUE ZERO.                
                                                                                
      ******************************************************************        
      *    KOHLS STANDARD REPORT HEADER - 132 CHARACTERS                        
      ******************************************************************        
           COPY DPWS132O.                                                       
                                                                                
      *****************************************************************         
      * INPUT/OUTPUT FILE RECORD LAYOUT                                         
      *****************************************************************         
           COPY INWS016.                                                        
                                                                                
      /*****************************************************************        
      *FLOW:   IT DOES FLOW......                                               
      *                                                                         
      ******************************************************************        
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       A000-MAINLINE-PROCESSING.                                                
                                                                                
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B500-INITIALIZE.                                             
                                                                                
           PERFORM Z100-READ-INPUT-REC.                                         
                                                                                
           PERFORM B300-PROCESS-RECS                                            
               UNTIL PS-NO-MORE-INPUT-RECS.                                     
                                                                                
           PERFORM B400-CLOSE-FILES.                                            
                                                                                
       Z999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      /*****************************************************************        
      *    OPEN ALL FILES.                                                      
      ******************************************************************        
       B100-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  MISSING-SKU-FILE                                         
                OUTPUT MISSING-SKU-REPORT-FILE.                                 
                                                                                
      ****************************************************************          
      *B300-PROCESS MISSING SKUS....                                            
      ****************************************************************          
       B300-PROCESS-RECS.                                                       
                                                                                
           IF PC-LINE-COUNT > PC-LINES-PER-PAGE                                 
               PERFORM Z500-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           MOVE 1              TO PC-ADVANCE.                                   
           MOVE IN016-STORE    TO DL1-STORE.                                    
           MOVE IN016-SKU      TO DL1-SKU.                                      
           MOVE IN016-QTY      TO DL1-QTY.                                      
W21732     MOVE IN016-DEPT     TO DL1-DEPT.                                     
W21732     MOVE IN016-CLASS    TO DL1-CLASS.                                    
                                                                                
           MOVE DL1-DETAIL-LINE     TO MISSING-SKU-REPORT-RECORD.               
           PERFORM Z400-WRITE-INVALID-SKU-REC.                                  
                                                                                
           PERFORM Z100-READ-INPUT-REC.                                         
                                                                                
      ******************************************************************        
      * B400-CLOSE-FILES.                                                       
      ******************************************************************        
       B400-CLOSE-FILES.                                                        
                                                                                
           CLOSE MISSING-SKU-FILE                                               
                 MISSING-SKU-REPORT-FILE.                                       
                                                                                
      ******************************************************************        
      * B500-INITIALIZE.                                                        
      ******************************************************************        
       B500-INITIALIZE.                                                         
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
           MOVE PV-MM           TO DP132O-RUN-MONTH.                            
           MOVE PV-DD           TO DP132O-RUN-DAY.                              
           MOVE PV-YY           TO DP132O-RUN-YEAR.                             
           MOVE PV-HR           TO DP132O-RUN-HOUR.                             
           MOVE PV-MIN          TO DP132O-RUN-MINUTE.                           
           MOVE 'INKRP119'      TO DP132O-PROGRAM-NAME.                         
           MOVE 1               TO DP132O-REPORT-NUMBER.                        
                                                                                
      ****************************************************************          
      * Z100-READ-INPUT-REC.                                                    
      ****************************************************************          
                                                                                
       Z100-READ-INPUT-REC.                                                     
                                                                                
           READ MISSING-SKU-FILE                                                
               INTO IN016-PHYSICAL-COUNT-RECORD                                 
                 AT END  SET PS-NO-MORE-INPUT-RECS TO TRUE.                     
                                                                                
      ******************************************************************        
      * Z400-WRITE-INVALID-SKU-REC.                                             
      ******************************************************************        
       Z400-WRITE-INVALID-SKU-REC.                                              
                                                                                
           WRITE MISSING-SKU-REPORT-RECORD AFTER ADVANCING PC-ADVANCE.          
                                                                                
           ADD PC-ADVANCE TO PC-LINE-COUNT.                                     
                                                                                
      ******************************************************************        
      * Z500-PRINT-HEADINGS.                                                    
      ******************************************************************        
       Z500-PRINT-HEADINGS.                                                     
                                                                                
           MOVE 1 TO PC-LINE-COUNT.                                             
           ADD 1  TO PC-PAGE-COUNT.                                             
           MOVE PC-PAGE-COUNT    TO DP132O-PAGE-NUMBER.                         
           WRITE MISSING-SKU-REPORT-RECORD                                      
               FROM DP132O-STANDRD-HEADER-132-COLS AFTER ADVANCING PAGE.        
           MOVE 2 TO PC-ADVANCE.                                                
                                                                                
           MOVE H1-HEADING-LINE TO MISSING-SKU-REPORT-RECORD.                   
           PERFORM Z400-WRITE-INVALID-SKU-REC.                                  
                                                                                
           MOVE H2-HEADING-LINE TO MISSING-SKU-REPORT-RECORD.                   
           PERFORM Z400-WRITE-INVALID-SKU-REC.                                  
                                                                                
           INITIALIZE MISSING-SKU-REPORT-RECORD.                                
           PERFORM Z400-WRITE-INVALID-SKU-REC.                                  
                                                                                
