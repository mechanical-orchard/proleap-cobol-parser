       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         INKRP016                                             
       AUTHOR.             PAUL JORGENSEN.                                      
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       8/06.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  THIS IS A REPORT SHOWING RETAIL SHORTAGES GREATER THAN WHAT            
      *  IS DEFINED BY THE CONTROL CARD, BY STORE.                              
      *                                                                         
      ******************************************************************        
      *                     PROGRAM LOG                                         
      ******************************************************************        
      * 08/31   P JORGENSEN                                                     
      *        INITIAL IMPLEMENTATION                                           
W33304* W33304  09-23-2008 VIGNESH SUBRAMANIAN                                  
W33304*                    HOLDING INVENTORY LEDGER OPEN (HILO).                
      ******************************************************************        
      ******************************************************************        
                                                                        INKRP016
       ENVIRONMENT DIVISION.                                            INKRP016
       CONFIGURATION SECTION.                                           INKRP016
       INPUT-OUTPUT SECTION.                                            INKRP016
       FILE-CONTROL.                                                    INKRP016
                                                                        INKRP016
           SELECT CONTROL-CARD-FILE                                     INKRP016
               ASSIGN TO INP01.                                         INKRP016
                                                                        INKRP016
           SELECT SHORTAGE-INPUT-FILE                                   INKRP016
               ASSIGN TO INP02.                                         INKRP016
                                                                        INKRP016
           SELECT SHORTAGE-REPORT-FILE                                  INKRP016
               ASSIGN TO RPT01.                                         INKRP016
                                                                        INKRP016
       DATA DIVISION.                                                   INKRP016
       FILE SECTION.                                                    INKRP016
                                                                        INKRP016
       FD  CONTROL-CARD-FILE                                            INKRP016
           LABEL RECORDS ARE OMITTED                                    INKRP016
           RECORDING MODE IS F                                          INKRP016
           BLOCK CONTAINS 0 RECORDS.                                    INKRP016
       01  CONTROL-CARD-REC        PIC X(080).                          INKRP016
                                                                        INKRP016
       FD  SHORTAGE-INPUT-FILE                                          INKRP016
           LABEL RECORDS ARE OMITTED                                    INKRP016
           RECORDING MODE IS F                                          INKRP016
           BLOCK CONTAINS 0 RECORDS.                                    INKRP016
W33304 01  SHORTAGE-INPUT-REC      PIC X(170).                          INKRP016
                                                                        INKRP016
       FD  SHORTAGE-REPORT-FILE                                         INKRP016
           LABEL RECORDS ARE OMITTED                                    INKRP016
           RECORD CONTAINS 132 CHARACTERS                               INKRP016
           RECORDING  MODE IS F                                         INKRP016
           DATA RECORD IS SHORTAGE-REPORT-REC.                          INKRP016
       01  SHORTAGE-REPORT-REC    PIC X(132).                           INKRP016
                                                                        INKRP016
       WORKING-STORAGE SECTION.                                         INKRP016
                                                                        INKRP016
       01  CC-CONTROL-CARD.                                             INKRP016
           05  FILLER                      PIC X(01)       VALUE SPACE. INKRP016
               88  CC-COMMENT-RECORD                       VALUE '*'.           
               88  CC-VALID-RECORD                         VALUE 'V'.           
               88  CC-NOT-COMMENT-RECORD                   VALUE SPACE.         
           05  CC-SHORTAGE-THRESHOLD-X     PIC X(06)       VALUE SPACES.INKRP016
           05  CC-SHORTAGE-THRESHOLD-N REDEFINES CC-SHORTAGE-THRESHOLD-X        
                                           PIC 9(06).                   INKRP016
           05  FILLER                      PIC X(73)       VALUE SPACES.        
                                                                        INKRP016
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.INKRP016
           88  ABEND-4003                          VALUE +4003.         INKRP016
                                                                        INKRP016
       01  PC-PROGRAM-CONSTANTS.                                        00770000
W33304     05  PC-MAX-LINE-COUNT       PIC S9(3) COMP-3 VALUE +58.      00780023
           05  PC-BLANK-LINE           PIC X(132) VALUE SPACES.         INKRP016
                                                                        INKRP016
       01  PV-PROGRAM-VARIABLES.                                        INKRP016
           05  PV-PAGE-COUNT           PIC S9(05)  VALUE ZERO.          INKRP016
           05  PV-NBR-OF-LINES         PIC S9(03)  VALUE ZERO.          INKRP016
           05  PV-LINE-COUNT           PIC S9(03)  VALUE ZERO COMP-3.   INKRP016
           05  PV-SHORTAGE-THRESHOLD   PIC S9(13)V99 COMP-3.                    
           05  PV-OVERAGE-THRESHOLD    PIC S9(13)V99 COMP-3.                    
           05  PV-PREV-STORE-NBR       PIC S9(05) COMP VALUE ZERO.              
           05  PV-OPER-ATTEMPTED       PIC X(30)    VALUE SPACES.       INKRP016
           05  PV-PARA                 PIC X(30)    VALUE SPACES.       INKRP016
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)    VALUE SPACES.       INKRP016
           05  PV-TIMESTAMP.                                            INKRP016
               10  PV-CC               PIC X(2).                        INKRP016
               10  PV-YY               PIC X(2).                        INKRP016
               10  FILLER              PIC X     VALUE '-'.             INKRP016
               10  PV-MM               PIC X(2).                        INKRP016
               10  FILLER              PIC X     VALUE '-'.             INKRP016
               10  PV-DD               PIC X(2).                        INKRP016
                                                                        INKRP016
           05  PV-MDY-DATE.                                             INKRP016
               10  PV-MDY-MM           PIC X(2).                        INKRP016
               10  FILLER              PIC X     VALUE '/'.             INKRP016
               10  PV-MDY-DD           PIC X(2).                        INKRP016
               10  FILLER              PIC X     VALUE '/'.             INKRP016
               10  PV-MDY-YY           PIC X(2).                        INKRP016
                                                                        INKRP016
           05  PV-COMPUTED-FIELDS.                                      INKRP016
               10  PV-RECORD-FETCHED       PIC S9(15)      VALUE ZEROES         
                               COMP-3.                                          
               10  PV-TOT-QTY              PIC S9(15)      VALUE ZEROES         
                               COMP-3.                                          
W33304     05  PV-INV-ID                   PIC S9(9) COMP  VALUE ZERO.          
W33304     05  PV-SHORTAGE-CYCLE-TOTAL     PIC S9(13)V99  COMP-3                
W33304                                                     VALUE ZERO.          
W33304     05  PV-SHORTAGE-GRAND-TOTAL     PIC S9(13)V99  COMP-3                
W33304                                                     VALUE ZERO.  INKRP016
       01  SWITCHES.                                                    INKRP016
           05  END-OF-INPUT-SW         PIC X       VALUE 'N'.           INKRP016
               88  END-OF-INPUT                    VALUE 'Y'.           INKRP016
                                                                        INKRP016
           05  END-OF-CONTROL-CARD-SW  PIC X       VALUE 'N'.           INKRP016
               88  END-OF-CONTROL-CARD             VALUE 'Y'.           INKRP016
                                                                        INKRP016
       01  CPU-DATE.                                                    INKRP016
           05  CPU-YEAR                    PIC X(02)     VALUE SPACES.  INKRP016
           05  CPU-MONTH                   PIC X(02)     VALUE SPACES.  INKRP016
           05  CPU-DAY                     PIC X(02)     VALUE SPACES.  INKRP016
                                                                        INKRP016
       01  CPU-TIME.                                                    INKRP016
           05  CPU-HOUR                    PIC X(02)     VALUE SPACES.  INKRP016
           05  CPU-MINUTE                  PIC X(02)     VALUE SPACES.  INKRP016
           05  FILLER                      PIC X(02)     VALUE SPACES.  INKRP016
                                                                        INKRP016
       01  H1-HEADING.                                                  INKRP016
           05  FILLER              PIC X(39)   VALUE SPACES.            INKRP016
           05  FILLER              PIC X(30)   VALUE                    INKRP016
              'DEPARTMENTAL OVERAGE/SHORTAGE '.                         INKRP016
           05  FILLER              PIC X(25)   VALUE                    INKRP016
              'EXCEPTION REPORT BY STORE'.                              INKRP016
           05  FILLER              PIC X(38)   VALUE SPACES.            INKRP016
                                                                        INKRP016
       01  H2-HEADING.                                                  INKRP016
W33304     05  FILLER              PIC X(06)   VALUE 'CYCLE '.          INKRP016
W33304     05  H2-CYCLE            PIC 9(05)   VALUE ZEROES.            INKRP016
W33304     05  FILLER              PIC X(35)   VALUE SPACES.            INKRP016
           05  FILLER              PIC X(32)   VALUE                    INKRP016
               'OVERAGES/SHORTAGES IN EXCESS OF '.                      INKRP016
           05  H2-SHRTG-THRESHOLD  PIC $$$$,$$9.                        INKRP016
W33304     05  FILLER              PIC X(05)   VALUE ' FOR '.           INKRP016
W33304     05  H2-CYCLE-NAME       PIC X(20)   VALUE SPACE.             INKRP016
W33304     05  FILLER              PIC X(23)   VALUE SPACE.             INKRP016
                                                                        INKRP016
       01  H3-HEADING.                                                          
           05  FILLER              PIC X(18)   VALUE SPACES.                    
           05  FILLER              PIC X(40)   VALUE                            
               'STORE   STORE                 OVER/SHORT'.                      
           05  FILLER              PIC X(74)   VALUE SPACES.                    
                                                                                
       01  H4-HEADING.                                                  INKRP016
           05  FILLER              PIC X(18)   VALUE SPACES.            INKRP016
           05  FILLER              PIC X(40)   VALUE                    INKRP016
               '  NBR   NAME          DEPT    RETAIL AMT'.                      
           05  FILLER              PIC X(74)   VALUE SPACE.             INKRP016
                                                                        INKRP016
       01  D1-DETAIL-LINE.                                              INKRP016
           05  FILLER                   PIC X(19).                      INKRP016
           05  D1-STR-NBR               PIC ZZZ9.                       INKRP016
           05  D1-STR-NBR-X REDEFINES D1-STR-NBR                        INKRP016
                                        PIC X(4).                       INKRP016
           05  FILLER                   PIC X(03) VALUE SPACES.         INKRP016
           05  D1-STR-NAME              PIC X(10).                      INKRP016
           05  FILLER                   PIC X(04) VALUE SPACES.         INKRP016
           05  D1-DEPT-NBR              PIC X(04).                      INKRP016
           05  FILLER                   PIC X(02) VALUE SPACES.         INKRP016
           05  D1-SHORT-RETAIL-AMT      PIC Z,ZZZ,ZZ9.99-.              INKRP016
           05  FILLER                   PIC X(73) VALUE SPACES.         INKRP016
                                                                                
W33304 01  C1-DETAIL-LINE.                                                      
W33304     05  FILLER                   PIC X(11) VALUE 'CYCLE TOTAL'.          
W33304     05  FILLER                   PIC X(33) VALUE SPACES.                 
W33304     05  C1-SHORT-RETAIL-AMT      PIC ZZZ,ZZZ,ZZ9.99-.                    
W33304     05  FILLER                   PIC X(73) VALUE SPACES.                 
W33304                                                                          
W33304 01  C1-DETAIL-LINE2.                                                     
W33304     05  FILLER                   PIC X(11) VALUE 'GRAND TOTAL'.          
W33304     05  FILLER                   PIC X(33) VALUE SPACES.                 
W33304     05  C1-SHORT-RETAIL-AMT2     PIC ZZZ,ZZZ,ZZ9.99-.                    
W33304     05  FILLER                   PIC X(73) VALUE SPACES.                 
W33304                                                                          
      ******************************************************************02620000
      ***  REPORT NO DATA LINE                                          02630000
      ******************************************************************02640000
       01  ER-REPORT-NO-DATA-LINE.                                      02650000
           05  FILLER                  PIC X(34)   VALUE                02660000
                                  '*** NO DATA ON WHICH TO REPORT ***'. 02670000
      ******************************************************************02620000
      ***  REPORT END OF REPORT LINE                                    02630000
      ******************************************************************02640000
       01  ER-END-OF-REPORT-LINE.                                       02650000
           05  FILLER                  PIC X(21)   VALUE                02660000
                                          '*** END OF REPORT ***'.      02670000
           05  FILLER                  PIC X(112)  VALUE SPACES.        02660000
                                                                        INKRP016
      * COPY FOR STANDARD HEADING                                       INKRP016
           COPY DPWS132O.                                               INKRP016
                                                                        INKRP016
      *************************************************************             
      *    COPYBOOK FOR INVENTORY EXTRACT FILE                                  
      *************************************************************             
           COPY INRD014.                                                        
                                                                        INKRP016
      ***************************************************************** INKRP104
      *  SQL ABEND ROUTINE WORKING STORAGE                              INKRP104
      ***************************************************************** INKRP104
                                                                        INKRP104
           COPY DPWS004.                                                INKRP104
                                                                        INKRP104
      ***************************************************************** INKRP104
      * DB2 XMT_LOC STORE TABLE DCLGEN.                                 INKRP104
      ***************************************************************** INKRP104
           EXEC SQL                                                     INKRP104
               INCLUDE XMT00001                                         INKRP104
           END-EXEC.                                                    INKRP104
                                                                                
W33304                                                                          
W33304******************************************************************        
W33304*  DCLGEN FOR THE INVENTORY CONTROL TABLE.                                
W33304******************************************************************        
W33304     EXEC SQL                                                             
W33304         INCLUDE TINCNTL                                                  
W33304     END-EXEC.                                                            
W33304                                                                          
      *****************************************************************         
      * SQL COMMON AREA                                                         
      *****************************************************************         
           EXEC SQL                                                     INKRP104
               INCLUDE SQLCA                                            INKRP104
           END-EXEC.                                                    INKRP104
                                                                        INKRP104
           COPY SQLCA2.                                                 INKRP104
                                                                        INKRP016
       LINKAGE SECTION.                                                 INKRP016
                                                                        INKRP016
       PROCEDURE DIVISION.                                              INKRP016
                                                                        INKRP016
       0000-MAIN-MODULE.                                                INKRP016
                                                                        INKRP016
           PERFORM 1000-INITIALIZATION.                                 INKRP016
                                                                        INKRP016
           PERFORM 7100-READ-INPUT-FILE                                         
           IF NOT END-OF-INPUT                                                  
               PERFORM 2000-PROCESS-INPUT                               INKRP016
                   UNTIL END-OF-INPUT                                   INKRP016
           ELSE                                                                 
               WRITE SHORTAGE-REPORT-REC FROM ER-REPORT-NO-DATA-LINE            
               AFTER ADVANCING 3 LINES                                          
           END-IF.                                                              
W33304     MOVE PV-SHORTAGE-CYCLE-TOTAL                                         
W33304                         TO C1-SHORT-RETAIL-AMT.                          
W33304     WRITE SHORTAGE-REPORT-REC FROM C1-DETAIL-LINE                        
W33304         AFTER ADVANCING 1 LINES.                                         
W33304     MOVE 99999  TO H2-CYCLE.                                             
W33304     MOVE 'ALL ACTIVE CYCLES' TO H2-CYCLE-NAME.                           
W33304     PERFORM 8000-PRINT-HEADINGS.                                         
W33304     MOVE PV-SHORTAGE-GRAND-TOTAL                                         
W33304                         TO C1-SHORT-RETAIL-AMT2.                         
W33304     WRITE SHORTAGE-REPORT-REC FROM C1-DETAIL-LINE2                       
W33304         AFTER ADVANCING 1 LINES.                                         
           WRITE SHORTAGE-REPORT-REC FROM ER-END-OF-REPORT-LINE                 
               AFTER ADVANCING 1 LINES.                                         
                                                                        INKRP016
           CLOSE CONTROL-CARD-FILE                                      INKRP016
                 SHORTAGE-INPUT-FILE                                    INKRP016
                 SHORTAGE-REPORT-FILE.                                  INKRP016
                                                                        INKRP016
           STOP RUN.                                                    INKRP016
      *                                                                 INKRP016
       1000-INITIALIZATION.                                             INKRP016
           MOVE '*1000-INITIALIZATION             *' TO PV-PARA         INKRP016
                                                                        INKRP016
           OPEN INPUT  CONTROL-CARD-FILE                                INKRP016
                       SHORTAGE-INPUT-FILE                              INKRP016
                OUTPUT SHORTAGE-REPORT-FILE.                            INKRP016
                                                                        INKRP016
           MOVE 1          TO DP132O-REPORT-NUMBER.                     INKRP016
           ACCEPT CPU-DATE FROM DATE.                                   INKRP016
           MOVE CPU-YEAR   TO DP132O-RUN-YEAR                           INKRP016
           MOVE CPU-MONTH  TO DP132O-RUN-MONTH                          INKRP016
           MOVE CPU-DAY    TO DP132O-RUN-DAY                            INKRP016
           ACCEPT CPU-TIME FROM TIME.                                   INKRP016
           MOVE CPU-HOUR   TO DP132O-RUN-HOUR.                          INKRP016
           MOVE CPU-MINUTE TO DP132O-RUN-MINUTE.                        INKRP016
           MOVE 'INKRP016' TO DP132O-PROGRAM-NAME.                      INKRP016
W33304     INITIALIZE PV-SHORTAGE-GRAND-TOTAL.                                  
                                                                        INKRP016
           PERFORM                                                              
               UNTIL END-OF-CONTROL-CARD                                        
                  OR CC-VALID-RECORD                                            
                  PERFORM 7000-READ-CONTROL-CARD                                
           END-PERFORM.                                                         
           IF CC-VALID-RECORD                                                   
               PERFORM 1100-EDIT-CONTROL-CARDS                                  
           END-IF.                                                              
           IF END-OF-CONTROL-CARD                                               
               MOVE 'NO VALID CONTROL CARD'                             INKRP016
                                        TO PV-DB2-OPER-ATTEMPTED                
               PERFORM 9900-ABEND                                               
           END-IF.                                                              
                                                                        INKRP016
       1100-EDIT-CONTROL-CARDS.                                         INKRP016
                                                                        INKRP016
           DISPLAY 'SHORTAGE CONTROL CARD:  ' CC-CONTROL-CARD           INKRP016
           IF CC-SHORTAGE-THRESHOLD-X NUMERIC                           INKRP016
               MOVE CC-SHORTAGE-THRESHOLD-N                                     
                                        TO H2-SHRTG-THRESHOLD                   
                                           PV-OVERAGE-THRESHOLD                 
               MULTIPLY PV-OVERAGE-THRESHOLD BY -1                              
                                    GIVING PV-SHORTAGE-THRESHOLD                
           ELSE                                                                 
               MOVE '*1100-EDIT-CONTROL-CARDS      *'                   INKRP016
                                        TO PV-PARA                      INKRP016
               MOVE 'NO VALID CONTROL CARD VALUE'                       INKRP016
                                        TO PV-DB2-OPER-ATTEMPTED                
               PERFORM 9900-ABEND                                               
           END-IF.                                                              
                                                                                
       2000-PROCESS-INPUT.                                              INKRP016
           IF IN014-SHORTAGE-RTL-AMT > PV-OVERAGE-THRESHOLD                     
           OR IN014-SHORTAGE-RTL-AMT < PV-SHORTAGE-THRESHOLD                    
               PERFORM 2100-MOVE-AND-PRINT                                      
           END-IF.                                                              
           PERFORM 7100-READ-INPUT-FILE.                                        
                                                                                
       2100-MOVE-AND-PRINT.                                                     
W33304     IF IN014-INV-ID NOT = PV-INV-ID                                      
W33304         MOVE IN014-INV-ID       TO H2-CYCLE                              
W33304         PERFORM 4000-FIND-CYCLE-NAME                                     
W33304         IF PV-INV-ID NOT = 0                                             
W33304             MOVE PV-SHORTAGE-CYCLE-TOTAL                                 
W33304                                 TO C1-SHORT-RETAIL-AMT                   
W33304             WRITE SHORTAGE-REPORT-REC FROM C1-DETAIL-LINE                
W33304                 AFTER ADVANCING PV-NBR-OF-LINES                          
W33304             ADD PV-NBR-OF-LINES TO PV-LINE-COUNT                         
W33304             INITIALIZE PV-SHORTAGE-CYCLE-TOTAL                           
W33304         END-IF                                                           
W33304     END-IF.                                                              
           IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                                 
W33304        OR IN014-INV-ID NOT = PV-INV-ID                                   
               PERFORM 8000-PRINT-HEADINGS                                      
               MOVE ZERO           TO PV-PREV-STORE-NBR                         
           END-IF.                                                              
                                                                                
           IF IN014-STR-NBR = PV-PREV-STORE-NBR                                 
               MOVE +1             TO PV-NBR-OF-LINES                           
               MOVE SPACES         TO D1-STR-NBR-X                              
                                      D1-STR-NAME                               
           ELSE                                                                 
               MOVE +3             TO PV-NBR-OF-LINES                           
               MOVE IN014-STR-NBR  TO PV-PREV-STORE-NBR                         
                                      D1-STR-NBR                                
               IF IN014-STR-NAME NOT = SPACES                                   
                   MOVE IN014-STR-NAME                                          
                                   TO D1-STR-NAME                               
               ELSE                                                             
                   PERFORM 3000-GET-STR-NAME                                    
               END-IF                                                           
           END-IF.                                                              
           MOVE IN014-DEPT-NBR     TO D1-DEPT-NBR                               
           MOVE IN014-SHORTAGE-RTL-AMT                                          
                                   TO D1-SHORT-RETAIL-AMT.                      
W33304     ADD  IN014-SHORTAGE-RTL-AMT                                          
W33304                             TO PV-SHORTAGE-CYCLE-TOTAL                   
W33304                                PV-SHORTAGE-GRAND-TOTAL.                  
           WRITE SHORTAGE-REPORT-REC FROM D1-DETAIL-LINE                        
               AFTER ADVANCING PV-NBR-OF-LINES.                                 
           ADD PV-NBR-OF-LINES     TO PV-LINE-COUNT.                            
                                                                                
W33304     MOVE IN014-INV-ID TO PV-INV-ID.                                      
       3000-GET-STR-NAME.                                                       
           INITIALIZE DCLXMT00001.                                              
      *                                                                         
           EXEC SQL                                                             
             SELECT LOC_10_ABBR_NM                                              
               INTO :XMT00001-LOC-10-ABBR-NM                                    
               FROM XMT_LOC                                                     
              WHERE LOC_NBR = :IN014-STR-NBR                                    
              WITH UR                                                           
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE XMT00001-LOC-10-ABBR-NM TO D1-STR-NAME                  
               WHEN OTHER                                                       
                   MOVE 'NOT FOUND '            TO D1-STR-NAME                  
           END-EVALUATE.                                                        
                                                                        INKRP016
W33304******************************************************************        
W33304* 4000-FIND-CYCLE-NAME - FIND CYCLE NAME IN TABLE                *        
W33304******************************************************************        
W33304                                                                          
W33304 4000-FIND-CYCLE-NAME.                                                    
W33304                                                                          
W33304     INITIALIZE INCNTL-INV-NM-TEXT                                        
W33304                                                                          
W33304     EXEC SQL                                                             
W33304          SELECT  INV_NM                                                  
W33304          INTO   :INCNTL-INV-NM                                           
W33304          FROM    TINCNTL                                                 
W33304          WHERE INV_ID = :IN014-INV-ID                                    
W33304     END-EXEC.                                                            
W33304                                                                          
W33304     EVALUATE TRUE                                                        
W33304         WHEN SQLCODE        = +000                                       
W33304             MOVE INCNTL-INV-NM-TEXT TO H2-CYCLE-NAME                     
W33304         WHEN OTHER                                                       
W33304             MOVE    '4000-FIND-CYCLE-NAME'                               
W33304                                      TO PV-PARA                          
W33304             MOVE    'SELECT ROW FROM TINCNTL'                            
W33304                                 TO PV-DB2-OPER-ATTEMPTED                 
W33304             PERFORM 9900-ABEND                                           
W33304     END-EVALUATE.                                                        
W33304                                                                          
       7000-READ-CONTROL-CARD.                                          INKRP016
                                                                        INKRP016
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                  INKR    
               AT END                                                   INKR    
                   SET END-OF-CONTROL-CARD TO TRUE.                     INKR    
                                                                        INKRP016
       7100-READ-INPUT-FILE.                                            INKRP016
                                                                        INKRP016
           READ SHORTAGE-INPUT-FILE INTO IN014-SHORTAGE-EXTR-REC        INKR    
               AT END                                                   INKR    
                   SET END-OF-INPUT TO TRUE.                            INKR    
                                                                                
       8000-PRINT-HEADINGS.                                             INKRP016
                                                                        INKRP016
           ADD 1              TO PV-PAGE-COUNT.                         INKRP016
                                                                        INKRP016
           MOVE PV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                    INKRP016
           WRITE SHORTAGE-REPORT-REC FROM                                       
                                        DP132O-STANDRD-HEADER-132-COLS  INKRP016
               AFTER ADVANCING PAGE.                                            
           WRITE SHORTAGE-REPORT-REC FROM H1-HEADING                            
               AFTER ADVANCING 2 LINES.                                         
           WRITE SHORTAGE-REPORT-REC FROM H2-HEADING                            
               AFTER ADVANCING 1 LINES.                                         
           WRITE SHORTAGE-REPORT-REC FROM H3-HEADING                            
               AFTER ADVANCING 2 LINES.                                         
           WRITE SHORTAGE-REPORT-REC FROM H4-HEADING                            
               AFTER ADVANCING 1 LINES.                                         
      *    WRITE SHORTAGE-REPORT-REC FROM PC-BLANK-LINE                         
      *        AFTER ADVANCING 1 LINES.                                         
                                                                        INKRP016
      *    MOVE +8         TO PV-LINE-COUNT.                            INKRP016
           MOVE +7         TO PV-LINE-COUNT.                            INKRP016
                                                                        INKRP016
      ******************************************************************INKRP016
      *  THIS ROUTINE FORCES AN ABEND                                   INKRP016
      ******************************************************************INKRP016
       9900-ABEND.                                                      INKRP016
                                                                        INKRP016
           MOVE +4013                 TO ABEND-CODE.                    INKRP016
           DISPLAY '**  ABEND     **'.                                  INKRP016
           DISPLAY '**  PROGRAM   **  =  INKRP016'.                     INKRP016
           DISPLAY '**  PARAGRAPH **  = ' PV-PARA.                      INKRP016
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.        INKRP016
                                                                        INKRP016
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         INKRP016
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        INKRP016
                                                                        INKRP016
           COPY DPPD004.                                                INKRP016
                                                                        INKRP016
           CALL 'ILBOABN0'  USING ABEND-CODE.                           INKRP016
