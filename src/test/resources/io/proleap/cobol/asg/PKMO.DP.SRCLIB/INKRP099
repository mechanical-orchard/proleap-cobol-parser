       ID DIVISION.                                                             
       PROGRAM-ID.      INKRP099.                                               
       AUTHOR.          B. GRAHAM - STRATAGEM.                                  
       DATE-WRITTEN     MAY, 25,2007.                                           
       DATE-COMPILED.                                                           
      ******************************************************************        
      *    INKRP099                                                     00070036
      *  - PRODUCE PHYSICAL INVENTORY SUMMARY REPORT                    00080036
      *    USING A GIVEN FILE OF STORES (LOCATIONS)FROM INK300/INK301   00090036
      *        1.  PRELOAD BLOCKED TRANSACTION SKIP TABLE (MLSI013)     00100036
      *        2.  ACCESS TINCNTL/TINVPAR TO GET INV DATE               00100036
      *            (ACTUAL OR PLANNED?)(USE ON REPORT AND FOR COMMON    00100036
      *            PRICING PROCESS.                                     00100036
      *        3.  RETRIEVE CORRESPONDING INFORMATION FROM TSUMINV      00100036
      *            FOR THE NUMBER OF UNITS AND RETAIL DOLLARS.                  
      *        4.  RETRIEVE CORRESPONDING INFORMATION FROM TINENTY      00100036
      *            FOR THE NUMBER OF UNITS AND RETAIL DOLLARS.                  
      *            INVOKE THE COMMON PRICING PROCESS TO GET DEPT AND            
      *            SUB CLASS ATTRIBUTES.                                        
      *        5.  RETRIEVE CORRESPONDING INFORMATION FROM TINVDTL      00100036
      *            FOR INVALID TRANSACTION PROCESSING                   00100036
      *        6.  ACCESS BLOCKED TRANSACTION PROCESS USING MLSI013     00110036
      *        7.  PRODUCE REPORT LINE                                  00130036
      *        8.  FORMAT AND WRITE A RECORD FOR CUM REPORT             00130036
      *                                                                 00190036
      ******************************************************************00200036
      *  CHANGE LOG                                                     00210036
      *  DATE       CHANGED BY    DESC                                  00220036
      *  ---------- ------------- --------------------------------------00230036
      *  05-25-2007 B. GRAHAM - STRATAGEM                               00240036
      *                         W28545 PHYSICAL INVENTORY               00250036
      *                         ORIGINAL CODE                           00260036
      *                                                                 00270036
W28545*  06-26-2007 S. JACKSON  ENHANCE INITIAL 'IF' LOGIC FOR DATE     00240036
      *                         CARD AND STORE FILE VALIDATION.         00250036
      *                                                                 00270036
S28545*  06-27-2007 S. JACKSON  DON'T ADD SHEET UNITS TO THE PHYSICAL   00240036
      *                         COUNT TOTALS. DETERMINE IF SHEETS HAVE  00250036
      *                         ALREADY BEEN ADDED TO TSUMINV (IF YES   00270036
      *                         SUBTRACT SHEET UNITS/$ FROM PHYSICAL)   00270036
W33304*  10-03-2008 NIJESH /    HOLDING INVENTORY LEDGER OPEN (HILO)            
W33304*              INFOSYS                                                    
      ******************************************************************00280036
                                                                        00290036
       ENVIRONMENT DIVISION.                                            00300036
       CONFIGURATION SECTION.                                           00310036
       SOURCE-COMPUTER.        IBM-3090.                                00320036
       OBJECT-COMPUTER.        IBM-3090.                                00330036
       INPUT-OUTPUT SECTION.                                            00340036
       FILE-CONTROL.                                                    00350036
                                                                        00360036
           SELECT  IN-DATE-FILE            ASSIGN TO INP01.             00370036
                                                                        00380036
           SELECT  IN-LOCATION-FILE        ASSIGN TO INP02.             00390036
                                                                        00400036
           SELECT  SKIP-CLASS-FILE         ASSIGN TO INP03.             00410036
                                                                        00420036
           SELECT  SUMMARY-RPT-FILE        ASSIGN TO RPT01.             00430036
                                                                        00440036
           SELECT  CUM-FILE                ASSIGN TO OUT01.             00450036
                                                                        00440036
                                                                        00460036
       DATA DIVISION.                                                   00510036
                                                                        00520036
       FILE SECTION.                                                    00530036
                                                                        00540036
       FD  IN-DATE-FILE                                                 00550036
           RECORDING MODE F                                             00560036
           LABEL RECORDS ARE STANDARD                                   00570036
           BLOCK CONTAINS  0 RECORDS.                                   00580036
                                                                        00590036
       01  IN-DATE-RECORD                  PIC X(80).                   00600036
                                                                        00610036
       FD  IN-LOCATION-FILE                                             00620036
           RECORDING MODE F                                             00630036
           LABEL RECORDS ARE STANDARD                                   00640036
           BLOCK CONTAINS  0 RECORDS.                                   00650036
                                                                        00660036
       01  IN-LOCATION-REC                 PIC X(80).                   00670036
                                                                        00680036
       FD  SKIP-CLASS-FILE                                              00690036
           RECORDING  MODE IS F                                         00700036
           LABEL RECORDS ARE STANDARD                                   00710036
           BLOCK CONTAINS 0 RECORDS.                                    00720036
       01  SKIP-CLASS-REC                  PIC X(80).                   00730036
                                                                        00740036
       FD  SUMMARY-RPT-FILE                                             00750036
           RECORDING MODE F                                             00760036
           LABEL RECORDS ARE STANDARD                                   00770036
           BLOCK CONTAINS  0 RECORDS.                                   00780036
                                                                        00790036
       01  SUM-REPORT-REC                  PIC X(132).                  00800036
                                                                        00810036
       FD  CUM-FILE                                                     00820036
           RECORDING MODE F                                             00830036
           LABEL RECORDS ARE STANDARD                                   00840036
           BLOCK CONTAINS  0 RECORDS.                                   00850036
                                                                        00860036
       01  CUM-REC                         PIC X(100).                  00870036
                                                                        00880036
                                                                        01020036
       WORKING-STORAGE SECTION.                                         01030036
                                                                        01040036
       01  PA-PROGRAM-ACCUMULATORS.                                     01050036
           05  PA-INPUT-DATE-CNT     PIC S9(9) COMP-3 VALUE +0.         01060036
           05  PA-INPUT-LOC-CNT      PIC S9(9) COMP-3 VALUE +0.         01070036
           05  PA-CUM-CNT            PIC S9(9) COMP-3 VALUE +0.         01080036
           05  PA-RPT-LINES          PIC S9(3) COMP-3 VALUE +52.        01080036
           05  PA-PAGE-CNT           PIC S9(3) COMP-3 VALUE +0.         01080036
           05  PA-SHEET-TOTAL-QTY    PIC S9(9) COMP-3 VALUE +0.         01060036
           05  PA-SHEET-EXTD-RTL-TOT PIC S9(9)V99 COMP VALUE +0.        01060036
                                                                        01120036
       01  PC-PROGRAM-CONSTANTS.                                        01130036
           05  PC-PROGRAM-ID         PIC X(08)     VALUE 'INKRP099'.    01140036
           05  PC-1                  PIC X(01)     VALUE '1'.           01150036
           05  PC-T                  PIC X(01)     VALUE 'T'.           01150036
           05  PC-4096               PIC S9(08) COMP SYNC VALUE +4096.  01150036
           05  PC-4097               PIC S9(08) COMP SYNC VALUE +4097.  01150036
           05  PC-4098               PIC S9(08) COMP SYNC VALUE +4098.  01150036
           05  PC-4099               PIC S9(08) COMP SYNC VALUE +4099.  01150036
                                                                        01160036
       01  PV-PROGRAM-VARIABLES.                                        01170036
           05  PV-CURRENT-TMST       PIC  X(26)  VALUE SPACES.                  
           05  PV-DB2-OPER-ATTEMPTED PIC  X(30)          VALUE SPACES.  01210036
           05  PV-EDIT-DEPT-NBR      PIC  9(4)           VALUE ZEROS.   01280036
           05  PV-EDIT-DEPT-NBR-R REDEFINES PV-EDIT-DEPT-NBR.           01290036
               10  FILLER            PIC  X(01).                        01300036
               10  PV-EDIT-DEPT-2-4  PIC  9(03).                        01310036
           05  PV-EDIT-SCL-NBR       PIC  9(4)           VALUE ZEROS.   01390036
           05  PV-EDIT-SCL-NBR-R REDEFINES PV-EDIT-SCL-NBR.             01400036
               10  FILLER            PIC  X(02).                        01410036
               10  PV-EDIT-SCL-3-4   PIC  9(02).                        01420036
           05  PV-EDIT-STR-NBR       PIC  9(5)           VALUE ZEROS.   01440036
           05  PV-EDIT-STR-NBR-R REDEFINES PV-EDIT-STR-NBR              01450036
                                     PIC  X(05).                        01460036
           05  PV-LOC-NBR            PIC  S9(04) COMP    VALUE ZEROS.   01230036
           05  PV-PARAGRAPH-NAME     PIC  X(30)          VALUE SPACES.  01230036
                                                                        01470036
       01  PS-PROGRAM-SWITCHES.                                         01480036
           05  PS-DATE-IS-EMPTY-SW         PIC X(01)     VALUE 'N'.     01490036
               88  PS-DATE-IS-EMPTY-NO                   VALUE 'N'.     01500036
               88  PS-DATE-IS-EMPTY-YES                  VALUE 'Y'.     01510036
           05  PS-DATE-IS-VALID-SW         PIC X(01)     VALUE 'N'.     01490036
               88  PS-DATE-IS-VALID-NO                   VALUE 'N'.     01500036
               88  PS-DATE-IS-VALID-YES                  VALUE 'Y'.     01510036
           05  PS-DATE-FILE-AT-END-SW      PIC X(01)     VALUE 'N'.     01490036
               88  PS-DATE-FILE-AT-END-NO                VALUE 'N'.     01500036
               88  PS-DATE-FILE-AT-END-YES               VALUE 'Y'.     01510036
           05  PS-LOCATION-FILE-AT-END-SW  PIC X(01)     VALUE 'N'.     01490036
               88  PS-LOCATION-FILE-AT-END-NO            VALUE 'N'.     01500036
               88  PS-LOCATION-FILE-AT-END-YES           VALUE 'Y'.     01510036
           05  PS-BY-DATE-FINISHED-SW      PIC X(01)     VALUE 'N'.     01490036
               88  PS-BY-DATE-FINISHED-NO                VALUE 'N'.     01500036
               88  PS-BY-DATE-FINISHED-YES               VALUE 'Y'.     01510036
           05  PS-END-OF-SKIP-CLASS-IND    PIC X(01)     VALUE 'N'.     01600036
               88  PS-END-OF-SKIP-CLASS                  VALUE 'Y'.     01610036
           05  PS-TSUMINV-AT-END-SW        PIC X(01)     VALUE 'N'.     01600036
               88  PS-TSUMINV-AT-END-NO                  VALUE 'N'.     01610036
               88  PS-TSUMINV-AT-END-YES                 VALUE 'Y'.     01610036
           05  PS-TINENTY-AT-END-SW        PIC X(01)     VALUE 'N'.     01600036
               88  PS-TINENTY-AT-END-NO                  VALUE 'N'.     01610036
               88  PS-TINENTY-AT-END-YES                 VALUE 'Y'.     01610036
           05  PS-TINVDTL-AT-END-SW        PIC X(01)     VALUE 'N'.     01600036
               88  PS-TINVDTL-AT-END-NO                  VALUE 'N'.     01610036
               88  PS-TINVDTL-AT-END-YES                 VALUE 'Y'.     01610036
           05  PS-BLOCKED-TRANS-FOUND-SW   PIC X(01)     VALUE 'N'.     01600036
               88  PS-BLOCKED-TRANS-FOUND-NO             VALUE 'N'.     01610036
               88  PS-BLOCKED-TRANS-FOUND-YES            VALUE 'Y'.     01610036
W33304     05  PS-STORE-FOUND-SW           PIC X(01)     VALUE 'N'.             
W33304         88  PS-STORE-FOUND-YES                    VALUE 'Y'.             
W33304         88  PS-STORE-FOUND-NO                     VALUE 'N'.             
           05  PS-TINENTY-SHEET-TYPE       PIC X(01)     VALUE ' '.     01600036
S28545         88  PS-SHEET-KEYED-ENTRY                  VALUE 'K'.     01610036
S28545         88  PS-SHEET-TRANSFER-ENTRY               VALUE 'T'.     01610036
S28545         88  PS-SHEET-MANIFEST-ENTRY               VALUE 'M'.     01610036
                                                                        01620036
       01  PT-PROGRAM-TABLE-AREA.                                       01630036
           05  PT-SKIP-CLASS.                                           01640036
               10  PT-SKIP-CLASS-INDX      PIC S9(04)      VALUE ZEROS  01650036
                               COMP-3.                                  01660036
               10  PT-SKIP-CLASS-COUNT     PIC S9(04)      VALUE ZEROS  01670036
                               COMP-3.                                  01680036
               10  PT-SKIP-CLASS-MAX       PIC S9(04)      VALUE +999   01690036
                               COMP-3.                                  01700036
               10  PT-SKIP-CLASS-TBL OCCURS 999 TIMES.                  01710036
                   15  PT-SKIP-DEPT-CLASS  PIC X(05)       VALUE SPACES.01720036
                   15  FILLER REDEFINES PT-SKIP-DEPT-CLASS.             01730036
                       20  PT-SKIP-DEPT    PIC 9(03).                   01740036
                       20  PT-SKIP-DEPT-X REDEFINES PT-SKIP-DEPT        01750036
                                           PIC X(03).                   01760036
                       20  PT-SKIP-CLASS   PIC 9(02).                   01770036
                       20  PT-SKIP-CLASS-X REDEFINES PT-SKIP-CLASS      01780036
                                           PIC X(02).                   01790036
       01  PRINT-HEAD-2.                                                01790036
           05  FILLER VALUE SPACES                 PIC X(44).                   
           05  FILLER VALUE 'PHYSICAL INVENTORY '  PIC X(19).                   
           05  FILLER VALUE '- SUMMARY BALANCING'  PIC X(19).                   
           05  FILLER VALUE ' REPORT            '  PIC X(19).                   
           05  FILLER VALUE SPACES                 PIC X(31).                   
                                                                        01800036
       01  PRINT-HEAD-3.                                                01790036
           05  FILLER VALUE SPACES                 PIC X(30).                   
           05  FILLER VALUE 'ACTL  AVERAGE'        PIC X(13).                   
           05  FILLER VALUE SPACES                 PIC X(37).                   
           05  FILLER VALUE 'TRANSFER'             PIC X(08).                   
           05  FILLER VALUE SPACES                 PIC X(44).                   
                                                                        01800036
       01  PRINT-HEAD-4.                                                01790036
           05  FILLER VALUE SPACES                 PIC X(31).                   
           05  FILLER VALUE 'INV    PRICE'         PIC X(12).                   
           05  FILLER VALUE SPACES                 PIC X(08).                   
           05  FILLER VALUE 'PHYSICAL     '        PIC X(13).                   
           05  FILLER VALUE 'PHYSICAL'             PIC X(08).                   
           05  FILLER VALUE SPACES                 PIC X(11).                   
           05  FILLER VALUE 'SHEET'                PIC X(05).                   
           05  FILLER VALUE SPACES                 PIC X(09).                   
           05  FILLER VALUE 'BLOCKED'              PIC X(07).                   
           05  FILLER VALUE SPACES                 PIC X(06).                   
           05  FILLER VALUE 'INVALID'              PIC X(07).                   
           05  FILLER VALUE SPACES                 PIC X(08).                   
           05  FILLER VALUE 'TOTAL  '              PIC X(07).                   
                                                                        01800036
       01  PRINT-HEAD-5.                                                01790036
           05  FILLER VALUE SPACES                 PIC X(07).                   
           05  FILLER VALUE 'STORE'                PIC X(05).                   
           05  FILLER VALUE SPACES                 PIC X(18).                   
           05  FILLER VALUE 'DATE    POINT'        PIC X(13).                   
           05  FILLER VALUE SPACES                 PIC X(07).                   
           05  FILLER VALUE 'INVENTORY'            PIC X(09).                   
           05  FILLER VALUE SPACES                 PIC X(08).                   
           05  FILLER VALUE 'UNITS'                PIC X(05).                   
           05  FILLER VALUE SPACES                 PIC X(07).                   
           05  FILLER VALUE 'INVENTORY'            PIC X(09).                   
           05  FILLER VALUE SPACES                 PIC X(07).                   
           05  FILLER VALUE 'INVENTORY'            PIC X(09).                   
           05  FILLER VALUE SPACES                 PIC X(08).                   
           05  FILLER VALUE 'UNITS'                PIC X(05).                   
           05  FILLER VALUE SPACES                 PIC X(08).                   
           05  FILLER VALUE 'UNITS'                PIC X(05).                   
           05  FILLER VALUE SPACES                 PIC X(02).                   
                                                                        01800036
       01  PRINT-DETAIL-LINE.                                           01790036
           05  FILLER VALUE SPACES                 PIC X(01).                   
           05  PDL-STORE-NBR                       PIC ZZZ9.                    
           05  FILLER VALUE SPACES                 PIC X(02).                   
           05  PDL-STORE-NAME                      PIC X(20).                   
           05  FILLER VALUE SPACES                 PIC X(02).                   
           05  PDL-INV-DATE                        PIC X(05).                   
           05  FILLER VALUE SPACES                 PIC X(02).                   
           05  PDL-AVE-PRICE-POINT                 PIC ZZZ9.99.                 
           05  FILLER VALUE SPACES                 PIC X(02).                   
           05  PDL-PHY-INV-RTL                     PIC ZZZ,ZZZ,ZZ9.99.          
           05  FILLER VALUE SPACES                 PIC X(02).                   
           05  PDL-PHY-INV-UNITS                   PIC ZZZ,ZZZ,ZZ9.             
           05  FILLER VALUE SPACES                 PIC X(02).                   
           05  PDL-TRANSFER-SHEET-RTL              PIC ZZZ,ZZZ,ZZ9.99.          
           05  FILLER VALUE SPACES                 PIC X(02).                   
           05  PDL-BLOCKED-TRANS-RTL               PIC ZZZ,ZZZ,ZZ9.99.          
           05  FILLER VALUE SPACES                 PIC X(02).                   
           05  PDL-INVALID-UNITS                   PIC ZZZ,ZZZ,ZZ9.             
           05  FILLER VALUE SPACES                 PIC X(02).                   
           05  PDL-TOTAL-UNITS                     PIC ZZZ,ZZZ,ZZ9.             
           05  FILLER VALUE SPACES                 PIC X(02).                   
                                                                        01800036
       01  PRINT-NOTHING-LINE.                                          01790036
           05  FILLER VALUE SPACES                 PIC X(01).                   
           05  FILLER VALUE 'NOTHING TO BE PROCESSED'                           
                                                   PIC X(23).                   
           05  FILLER VALUE SPACES                 PIC X(108).                  
                                                                        01800036
       01  ABEND-CODE                      PIC S9(08)                   01810036
                                                   VALUE +0 COMP SYNC.  01820036
           88  ABEND-4098-DATE-ERROR               VALUE +4097.         01840036
           88  ABEND-4097-BOTH-TO-PROC             VALUE +4097.         01840036
           88  ABEND-4096-CLASS-TABLE              VALUE +4096.         01840036
                                                                        01920036
                                                                        02090036
       01  SK-SKIP-CLASS-RECORD.                                        02100036
           05  SK-SKIP-DEPT-CLASS          PIC X(05)       VALUE SPACES.02110036
           05  FILLER                      PIC X(75)       VALUE SPACES.02120036
                                                                        02090036
       01  IN-DATE-REC.                                                 02130036
           05  IN-DATE                     PIC X(10).                   02120036
           05  FILLER                      PIC X(70).                   02120036
                                                                        02180036
      *************************************************************     02190036
      *    COPYBOOK FOR CUM FILE RECORD DESCRIPTION                     02200036
      *************************************************************     02210036
           COPY INRD100.                                                02220036
                                                                        02180036
      *************************************************************     02190036
      *    COPYBOOK FOR INVENTORY LOCATION FILE                         02200036
      *************************************************************     02210036
           COPY INWS004.                                                02220036
                                                                        02230036
                                                                        02240036
      *************************************************************     02250036
      *    COPYBOOK FOR COMMON PRICING PROCESS WORKING STORAGE          02260036
      *************************************************************     02270036
           COPY INWS400.                                                02280036
                                                                        02290036
                                                                        02380036
      ***************************************************************** 02390036
      * ERROR MESSAGE AREA FOR DB2                                      02400036
      ***************************************************************** 02410036
           COPY DPWS004.                                                02420036
                                                                        02380036
      ***************************************************************** 02390036
      * PRINT HEADING AREAS FOR 132 PRINT                               02400036
      ***************************************************************** 02410036
           COPY DPWS132.                                                02420036
                                                                        02430036
      *****************************************************************         
      * XMT00001 DCLGEN                                                         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                        01380000
                                                                                
      *****************************************************************         
      * TINCNTL DCLGEN                                                          
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 INVENTORY PARMS                                                     
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 SUMMARY INVENTORY                                                   
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TSUMINV                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 SHEET  INVENTORY                                                    
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TINENTY                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 INVALID INVENTORY                                                   
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TINVDTL                                                  
           END-EXEC.                                                            
                                                                                
      *************************************************************     02440036
      *    DB2 COMMUNICATION AREA                                 *     02450036
      *************************************************************     02460036
                                                                                
           EXEC SQL                                                     02470036
               INCLUDE SQLCA                                            02480036
           END-EXEC.                                                    02490036
                                                                        02500036
      *****************************************************************         
      * TINVPAR CURSOR                                                          
      *****************************************************************         
                                                                        02500036
           EXEC SQL DECLARE TINVPAR_LOC_CSR CURSOR FOR                          
              SELECT LOC_NBR                                                    
                    ,ACTL_INV_DTE                                               
S28545              ,SHEET_INV_CPLD_IND                                         
              FROM TINVPAR A                                                    
                  ,TINCNTL B                                                    
              WHERE  ACTL_INV_DTE = :IN-DATE                                    
W33304          AND SCHD_PHY_CNT_CDE <> 'EI'                                    
W33304          AND LOC_INV_STAT_CDE = 'IN'                                     
                AND A.INV_ID = B.INV_ID                                         
                AND B.ACTV_IND = 'Y'                                            
              ORDER BY LOC_NBR                                                  
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      * TSUMINV LOC CURSOR                                                      
      *****************************************************************         
           EXEC SQL DECLARE TSUMINV_LOC_CSR CURSOR FOR                          
              SELECT DEPT_NBR                                                   
                    ,SUB_CL_NBR                                                 
                    ,INV_QTY                                                    
                    ,EXTD_AMT                                                   
                                                                                
              FROM TSUMINV                                                      
              WHERE LOC_NBR = :PV-LOC-NBR                                       
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      * TINENTY LOC CURSOR                                                      
      *****************************************************************         
           EXEC SQL DECLARE TINENTY_LOC_CSR CURSOR FOR                          
              SELECT INV_QTY                                                    
                    ,EXTD_RTL_AMT                                               
                    ,SKU_NBR                                                    
                    ,SHEET_TYP_CDE                                              
              FROM TINENTY                                                      
              WHERE STR_NBR = :PV-LOC-NBR                                       
S28545*         AND SHEET_TYP_CDE = :PC-T                                       
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TINVDTL LOC CURSOR                                                      
      *****************************************************************         
           EXEC SQL DECLARE TINVDTL_LOC_CSR CURSOR FOR                          
              SELECT REC_QTY                                                    
              FROM TINVDTL                                                      
              WHERE LOC_NBR = :PV-LOC-NBR                                       
           END-EXEC.                                                            
                                                                                
                                                                        02510036
       PROCEDURE DIVISION.                                              02520036
                                                                        02530036
      *************************************************************     02540036
      * MAIN PROCESSING PARAGRAPH                                 *     02550036
      *************************************************************     02560036
       0000-MAINLINE.                                                   02570036
                                                                        02580036
            PERFORM 1000-INITIALIZATION.                                02590036
                                                                        02600036
            IF PS-DATE-IS-EMPTY-YES                                     02600036
                AND  PS-LOCATION-FILE-AT-END-YES                        02600036
                PERFORM 2000-NOTHING-TO-PROCESS                         02600036
            ELSE                                                        02600036
                IF PS-DATE-IS-EMPTY-YES                                 02600036
                    PERFORM 2100-PROCESS-LOCATION-FILE                  02600036
                        UNTIL PS-LOCATION-FILE-AT-END-YES                       
                ELSE                                                    02600036
                    IF PS-DATE-IS-VALID-YES                             02600036
W28545                  PERFORM 2500-PROCESS-BY-DATE                    02600036
W28545                      UNTIL PS-BY-DATE-FINISHED-YES               02600036
W28545              END-IF                                              02600036
W28545          END-IF                                                  02600036
            END-IF.                                                     02600036
                                                                        02600036
      *AN INVALID DATE ERROR CONDITION WAS ALREADY HANDLED.             02600036
                                                                        02630036
            PERFORM 8000-END-PROCESSING.                                02640036
                                                                        02650036
            STOP RUN.                                                   02660036
                                                                        02670036
      ***************************************************************   02680036
      * OPEN FILES                                                      02690036
      ***************************************************************   02700036
       1000-INITIALIZATION.                                             02710036
                                                                        02720036
           EXEC SQL                                                             
              SET :PV-CURRENT-TMST = CURRENT TIMESTAMP                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = +0                                                 
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 PERFORM 9900-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                        02720036
           OPEN INPUT  IN-DATE-FILE                                             
                       IN-LOCATION-FILE                                         
                       SKIP-CLASS-FILE                                          
                OUTPUT SUMMARY-RPT-FILE                                         
                       CUM-FILE.                                        02770036
                                                                        02800036
           MOVE PV-CURRENT-TMST(6:2)  TO DP132-RUN-MONTH.                       
           MOVE PV-CURRENT-TMST(9:2)  TO DP132-RUN-DAY.                         
           MOVE PV-CURRENT-TMST(1:4)  TO DP132-RUN-YEAR.                        
           MOVE PV-CURRENT-TMST(12:2) TO DP132-RUN-HOUR.                        
           MOVE PV-CURRENT-TMST(15:2) TO DP132-RUN-MINUTE.                      
           MOVE PC-PROGRAM-ID         TO DP132-PROGRAM-NAME.                    
           MOVE PC-1                  TO DP132-REPORT-NUMBER.                   
           MOVE 0                     TO DP132-PAGE-NUMBER.                     
                                                                        02800036
           PERFORM 1050-READ-DATE-FILE.                                 02810036
                                                                        02800036
           PERFORM 6000-READ-LOCATIONS.                                 02810036
                                                                        02800036
      *********************************************************         02820036
      *    DATE FILE AND LOC FILE ARE MUTUALLY EXCLUSIVE      *         02820036
      *                                                       *         02820036
      *********************************************************         02820036
                                                                        02800036
           IF PA-INPUT-LOC-CNT > ZEROS                                          
               AND PA-INPUT-DATE-CNT > ZEROS                                    
               DISPLAY 'BOTH DATE AND LOCATION RECORDS ARE PRESENT'             
               DISPLAY 'ONLY ONE CAN BE PRESENT'                                
               MOVE PC-4097 TO ABEND-CODE                               03040036
               MOVE PC-4097 TO RETURN-CODE                              03040036
               PERFORM 9800-ABEND                                               
           END-IF.                                                              
                                                                        02840036
           PERFORM 1100-LOAD-SKIP-CLASS.                                02830036
                                                                        02840036
       1050-READ-DATE-FILE.                                             02850036
                                                                        02860036
           READ IN-DATE-FILE INTO IN-DATE-REC                           02870036
               AT END                                                   02880036
                   DISPLAY 'DATE FILE IS EMPTY '                        02890036
                   SET PS-DATE-FILE-AT-END-YES TO TRUE.                 02950036
                                                                        02960036
           IF PS-DATE-FILE-AT-END-NO                                    02970036
               IF IN-DATE = SPACES                                      02980036
                   SET PS-DATE-IS-EMPTY-YES TO TRUE                     02990036
               ELSE                                                     03000036
                   SET PS-DATE-IS-EMPTY-NO TO TRUE                      03010036
                   ADD +1 TO PA-INPUT-DATE-CNT                                  
                   PERFORM 1075-VALIDATE-DATE                           03010036
                   IF PS-DATE-IS-VALID-NO                               03030036
                       DISPLAY 'DATE IS NOT VALID '                     03040036
                       MOVE PC-4098 TO ABEND-CODE                       03040036
                       MOVE PC-4098 TO RETURN-CODE                      03040036
                       PERFORM 9800-ABEND                                       
                   END-IF                                                       
               END-IF                                                   03050036
W28545     ELSE                                                                 
W28545         SET PS-DATE-IS-EMPTY-YES TO TRUE                                 
           END-IF.                                                      03060036
                                                                        03070036
       1075-VALIDATE-DATE.                                              03010036
                                                                        03070036
           SET PS-DATE-IS-VALID-YES TO TRUE.                            03070036
                                                                        03070036
           EXEC SQL                                                             
              SELECT DISTINCT ACTL_INV_DTE                                      
                INTO :INVPAR-ACTL-INV-DTE                                       
              FROM TINVPAR A                                                    
                  ,TINCNTL B                                                    
              WHERE  ACTL_INV_DTE = :IN-DATE                                    
W33304          AND SCHD_PHY_CNT_CDE <> 'EI'                                    
W33304          AND LOC_INV_STAT_CDE = 'IN'                                     
                AND A.INV_ID = B.INV_ID                                         
                AND B.ACTV_IND = 'Y'                                            
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   SET PS-DATE-IS-VALID-YES TO TRUE                             
               WHEN SQLCODE = +100                                              
                   SET PS-DATE-IS-VALID-NO  TO TRUE                             
               WHEN OTHER                                                       
                   MOVE '1075-VALIDATE-DATE ' TO PV-PARAGRAPH-NAME              
                   MOVE 'SELECT TINVPAR DATE     '                              
                                 TO  PV-DB2-OPER-ATTEMPTED                      
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                        03090036
       1100-LOAD-SKIP-CLASS.                                            03080036
                                                                        03090036
           MOVE ZEROS           TO PT-SKIP-CLASS-INDX.                  03100036
           PERFORM 1175-READ-SKIP-CLASS.                                03110036
           PERFORM                                                      03120036
               UNTIL PS-END-OF-SKIP-CLASS                               03130036
                   OR PT-SKIP-CLASS-INDX > PT-SKIP-CLASS-MAX            03140036
               ADD +1                     TO PT-SKIP-CLASS-INDX         03150036
               MOVE SK-SKIP-DEPT-CLASS                                  03160036
                   TO PT-SKIP-DEPT-CLASS(PT-SKIP-CLASS-INDX)            03170036
               PERFORM 1175-READ-SKIP-CLASS                             03180036
           END-PERFORM.                                                 03190036
                                                                        03200036
           IF PT-SKIP-CLASS-INDX > PT-SKIP-CLASS-MAX                    03210036
               DISPLAY 'MORE THAN ' PT-SKIP-CLASS-MAX ' SKIP CLASS '    03220036
               DISPLAY 'MUST EXPAND SKIP-CLASS TABLE  '                 03230036
               MOVE PC-4096           TO ABEND-CODE                     03240036
                                         RETURN-CODE                    03240036
               PERFORM 9800-ABEND                                       03250036
           ELSE                                                         03260036
               MOVE PT-SKIP-CLASS-INDX    TO PT-SKIP-CLASS-COUNT        03270036
           END-IF.                                                      03280036
                                                                        03290036
                                                                        03300036
       1175-READ-SKIP-CLASS.                                            03310036
                                                                        03320036
           READ SKIP-CLASS-FILE      INTO SK-SKIP-CLASS-RECORD          03330036
             AT END                                                     03340036
               SET PS-END-OF-SKIP-CLASS     TO TRUE.                    03350036
                                                                        03360036
                                                                        03360036
       2000-NOTHING-TO-PROCESS.                                         02600036
                                                                        03360036
           PERFORM 5100-PRINT-HEADINGS                                  04640036
                                                                        04800036
           WRITE SUM-REPORT-REC FROM PRINT-NOTHING-LINE                 04800036
                                         AFTER ADVANCING 1 LINE.        04800036
                                                                        03360036
      **************************************************************    02600036
      *2100-PROCESS-LOCATION-FILE.                                      02600036
      * FIRST READ WAS DONE IN INITIALIZATION PARA                      03360036
      * IN100-SUMMARY-REPORT-RECORD IS USED AS A WORKING STORAGE        03360036
      * AREA TO ACCUMULATE VARIOUS AMOUNTS BEFORE THE DETAIL REPORT     03360036
      * IS PRODUCED AND BEFORE CUM RECORD IS WRITTEN.                   03360036
      **************************************************************    02600036
                                                                        03360036
       2100-PROCESS-LOCATION-FILE.                                      02600036
                                                                        03360036
           INITIALIZE IN100-SUMMARY-REPORT-RECORD.                      03370036
                                                                        03360036
           SET PS-TSUMINV-AT-END-NO TO TRUE.                            03360036
           SET PS-TINENTY-AT-END-NO TO TRUE.                            03360036
           SET PS-TINVDTL-AT-END-NO TO TRUE.                            03370036
                                                                        03360036
           PERFORM 2150-SELECT-INVPAR-DATE.                             03360036
                                                                        03360036
W33304     IF  PS-STORE-FOUND-YES                                               
W33304         PERFORM 2200-TSUMINV-PROCESS-ROWS                                
W33304             UNTIL PS-TSUMINV-AT-END-YES                                  
                                                                        03360036
W33304         PERFORM 2300-TINENTY-PROCESS-ROWS                                
W33304             UNTIL PS-TINENTY-AT-END-YES                                  
                                                                        03370036
W33304         PERFORM 2400-TINVDTL-PROCESS-ROWS                                
W33304             UNTIL PS-TINVDTL-AT-END-YES                                  
                                                                        03370036
W33304         PERFORM 5000-FORMAT-REPORT-AND-CUM                               
W33304     END-IF.                                                              
                                                                        03370036
           PERFORM 6000-READ-LOCATIONS.                                 03370036
                                                                        03370036
       2150-SELECT-INVPAR-DATE.                                         03360036
                                                                        03370036
           EXEC SQL                                                             
              SELECT ACTL_INV_DTE                                               
S28545              ,SHEET_INV_CPLD_IND                                         
                INTO :INVPAR-ACTL-INV-DTE                                       
S28545              ,:INVPAR-SHEET-INV-CPLD-IND                                 
              FROM TINVPAR A                                                    
                  ,TINCNTL B                                                    
              WHERE A.LOC_NBR = :PV-LOC-NBR                                     
W33304          AND SCHD_PHY_CNT_CDE <> 'EI'                                    
W33304          AND LOC_INV_STAT_CDE = 'IN'                                     
W33304          AND A.ACTL_FIN_BK_DTE = '9999-09-09'                            
                AND A.INV_ID = B.INV_ID                                         
                AND B.ACTV_IND = 'Y'                                            
              WITH UR                                                           
           END-EXEC.                                                            
                                                                        03370036
W33304     EVALUATE TRUE                                                        
W33304         WHEN SQLCODE = +0                                                
W33304              SET PS-STORE-FOUND-YES  TO TRUE                             
W33304         WHEN SQLCODE = +100                                              
W33304              SET PS-STORE-FOUND-NO   TO TRUE                             
W33304              DISPLAY ' STORE SKIPPED : ' PV-LOC-NBR                      
W33304         WHEN OTHER                                                       
W33304              MOVE '2150-SELECT-INVPAR-DATE'                              
W33304                                      TO PV-PARAGRAPH-NAME                
W33304              MOVE 'SELECT TINVPAR      '                                 
W33304                                      TO PV-DB2-OPER-ATTEMPTED            
W33304              PERFORM 9900-SQL-ABEND                                      
W33304     END-EVALUATE.                                                        
                                                                        03370036
       2200-TSUMINV-PROCESS-ROWS.                                       03360036
                                                                        03370036
           PERFORM 2210-OPEN-TSUMINV-CUR.                               03370036
                                                                        03370036
           PERFORM 2230-FETCH-TSUMINV-CUR                               03370036
               UNTIL PS-TSUMINV-AT-END-YES.                             03370036
                                                                        03370036
           PERFORM 2220-CLOSE-TSUMINV-CUR.                              03370036
                                                                        03370036
       2210-OPEN-TSUMINV-CUR.                                           03370036
                                                                        03370036
           EXEC SQL                                                             
               OPEN TSUMINV_LOC_CSR                                             
           END-EXEC.                                                            
                                                                        03370036
           IF SQLCODE NOT = +0                                                  
               MOVE '2210-OPEN-TSUMINV-CUR  ' TO PV-PARAGRAPH-NAME              
               MOVE 'OPEN TSUMINV_LOC_CSR' TO PV-DB2-OPER-ATTEMPTED             
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                        03370036
       2220-CLOSE-TSUMINV-CUR.                                          03370036
                                                                                
           EXEC SQL                                                             
              CLOSE TSUMINV_LOC_CSR                                             
           END-EXEC.                                                            
                                                                        03370036
           IF SQLCODE NOT = +0                                                  
               MOVE '2220-CLOSE-TSUMINV-CUR ' TO PV-PARAGRAPH-NAME              
               MOVE 'CLOSE TSUMINV_LOC_CSR' TO PV-DB2-OPER-ATTEMPTED            
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                        03370036
       2230-FETCH-TSUMINV-CUR.                                          03360036
                                                                        03370036
           EXEC SQL                                                             
              FETCH TSUMINV_LOC_CSR                                             
               INTO  :SUMINV-DEPT-NBR                                           
                    ,:SUMINV-SUB-CL-NBR                                         
                    ,:SUMINV-INV-QTY                                            
                    ,:SUMINV-EXTD-AMT                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   ADD SUMINV-INV-QTY       TO IN100-PHY-INV-UNITS              
                   ADD SUMINV-EXTD-AMT      TO IN100-PHY-INV-RTL                
                   MOVE SUMINV-SUB-CL-NBR   TO PV-EDIT-SCL-NBR                  
                   MOVE SUMINV-DEPT-NBR     TO PV-EDIT-DEPT-NBR                 
                   PERFORM 3000-BLOCKED-TRANS-CHECK                             
                   IF PS-BLOCKED-TRANS-FOUND-YES                                
                       ADD SUMINV-EXTD-AMT  TO IN100-BLOCKED-TRANS-RTL          
                   END-IF                                                       
               WHEN SQLCODE = +100                                              
                   SET PS-TSUMINV-AT-END-YES TO TRUE                            
               WHEN OTHER                                                       
                   MOVE '2230-FETCH-TSUMINV-CUR ' TO PV-PARAGRAPH-NAME          
                   MOVE 'FETCH TSUMINV LOC CURSOR'                              
                                 TO  PV-DB2-OPER-ATTEMPTED                      
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                        03370036
       2300-TINENTY-PROCESS-ROWS.                                       03370036
                                                                        03370036
           MOVE ZEROES TO PA-SHEET-TOTAL-QTY                            03370036
                          PA-SHEET-EXTD-RTL-TOT.                        03370036
                                                                        03370036
           PERFORM 2310-OPEN-TINENTY-CUR                                03370036
                                                                        03370036
           PERFORM 2330-FETCH-TINENTY-CUR                               03370036
               UNTIL PS-TINENTY-AT-END-YES.                             03370036
                                                                        03370036
           PERFORM 2320-CLOSE-TINENTY-CUR.                              03370036
                                                                        03370036
       2310-OPEN-TINENTY-CUR.                                           03370036
                                                                        03370036
           EXEC SQL                                                             
               OPEN TINENTY_LOC_CSR                                             
           END-EXEC.                                                            
                                                                        03370036
           IF SQLCODE NOT = +0                                                  
               MOVE '2310-OPEN-TINENTY-CUR  ' TO PV-PARAGRAPH-NAME              
               MOVE 'OPEN TINENTY_LOC_CSR' TO PV-DB2-OPER-ATTEMPTED             
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                        03370036
       2320-CLOSE-TINENTY-CUR.                                          03370036
                                                                                
           EXEC SQL                                                             
              CLOSE TINENTY_LOC_CSR                                             
           END-EXEC.                                                            
                                                                        03370036
           IF SQLCODE NOT = +0                                                  
               MOVE '2320-CLOSE-TINENTY-CUR ' TO PV-PARAGRAPH-NAME              
               MOVE 'CLOSE TINENTY_LOC_CSR' TO PV-DB2-OPER-ATTEMPTED            
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                        03370036
       2330-FETCH-TINENTY-CUR.                                          03360036
                                                                        03370036
           EXEC SQL                                                             
              FETCH TINENTY_LOC_CSR                                             
               INTO  :INENTY-INV-QTY                                            
                    ,:INENTY-EXTD-RTL-AMT                                       
                    ,:INENTY-SKU-NBR                                            
S28545              ,:INENTY-SHEET-TYP-CDE                                      
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
S28545             PERFORM 2340-SHEET-PROCESSING                                
               WHEN SQLCODE = +100                                              
                   SET PS-TINENTY-AT-END-YES TO TRUE                            
               WHEN OTHER                                                       
                   MOVE '2330-FETCH-TINENTY-CUR ' TO PV-PARAGRAPH-NAME          
                   MOVE 'FETCH TINENTY LOC CURSOR'                              
                                 TO  PV-DB2-OPER-ATTEMPTED                      
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                 03370036
      ***************************************************************** 03370036
      *  SHEET TYPE CODE TEST. TRANSFER SHEET $ ARE LISTED ON REPORT.   03370036
      *  ALL OTHER QTY AND RETAIL ARE NOT INCLUDED IN THE 'PHYSICAL'    03370036
      *  TOTALS. TINVPAR NEEDS TO BE CHECKED TO DETERMINE IF THE SHEET  03370036
      *  DATA HAS ALREADY BEEN ADDED TO THE PHYSICAL DATABASE (TSUMINV) 03370036
      ***************************************************************** 03370036
       2340-SHEET-PROCESSING.                                                   
                                                                                
           MOVE INENTY-SHEET-TYP-CDE TO PS-TINENTY-SHEET-TYPE.                  
                                                                        01610036
           IF  PS-SHEET-TRANSFER-ENTRY                                  01610036
               ADD INENTY-INV-QTY      TO PA-SHEET-TOTAL-QTY            01610036
               ADD INENTY-EXTD-RTL-AMT TO IN100-TRANSFER-SHEET-RTL      01610036
                                          PA-SHEET-EXTD-RTL-TOT         01610036
           ELSE                                                         01610036
               ADD INENTY-INV-QTY      TO PA-SHEET-TOTAL-QTY            01610036
               ADD INENTY-EXTD-RTL-AMT TO PA-SHEET-EXTD-RTL-TOT         01610036
           END-IF.                                                      01610036
                                                                        01610036
S28545*****    ADD INENTY-INV-QTY      TO IN100-PHY-INV-UNITS                   
      *        ADD INENTY-EXTD-RTL-AMT TO IN100-TRANSFER-SHEET-RTL              
      *                                                                         
           PERFORM 4000-LOOK-UP-ATTRIBUTES                                      
           PERFORM 3000-BLOCKED-TRANS-CHECK                                     
           IF PS-BLOCKED-TRANS-FOUND-YES                                        
               ADD INENTY-EXTD-RTL-AMT TO IN100-BLOCKED-TRANS-RTL               
           END-IF.                                                              
                                                                        03370036
       2400-TINVDTL-PROCESS-ROWS.                                       03370036
                                                                        03370036
           PERFORM 2410-OPEN-TINVDTL-CUR.                               03370036
                                                                        03370036
           PERFORM 2430-FETCH-TINVDTL-CUR                               03370036
               UNTIL PS-TINVDTL-AT-END-YES.                             03370036
                                                                        03370036
           PERFORM 2420-CLOSE-TINVDTL-CUR.                              03370036
                                                                        03370036
       2410-OPEN-TINVDTL-CUR.                                           03370036
                                                                        03370036
           EXEC SQL                                                             
               OPEN TINVDTL_LOC_CSR                                             
           END-EXEC.                                                            
                                                                        03370036
           IF SQLCODE NOT = +0                                                  
               MOVE '2410-OPEN-TINVDTL-CUR ' TO PV-PARAGRAPH-NAME               
               MOVE 'OPEN TINENTY_LOC_CSR' TO PV-DB2-OPER-ATTEMPTED             
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                        03370036
       2420-CLOSE-TINVDTL-CUR.                                          03370036
                                                                                
           EXEC SQL                                                             
              CLOSE TINVDTL_LOC_CSR                                             
           END-EXEC.                                                            
                                                                        03370036
           IF SQLCODE NOT = +0                                                  
               MOVE '2420-CLOSE-TINVDTL-CUR' TO PV-PARAGRAPH-NAME               
               MOVE 'CLOSE TINVDTL_LOC_CSR' TO PV-DB2-OPER-ATTEMPTED            
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                        03370036
       2430-FETCH-TINVDTL-CUR.                                          03360036
                                                                        03370036
           EXEC SQL                                                             
              FETCH TINVDTL_LOC_CSR                                             
               INTO  :INVDTL-REC-QTY                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   ADD INVDTL-REC-QTY   TO IN100-INVALID-UNITS                  
               WHEN SQLCODE = +100                                              
                   SET PS-TINVDTL-AT-END-YES TO TRUE                            
               WHEN OTHER                                                       
                   MOVE '2430-FETCH-TINVDTL-CUR ' TO PV-PARAGRAPH-NAME          
                   MOVE 'FETCH TINVDTL LOC CURSOR'                              
                                 TO  PV-DB2-OPER-ATTEMPTED                      
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                        03370036
       2500-PROCESS-BY-DATE.                                            02600036
                                                                        03370036
           SET PS-BY-DATE-FINISHED-NO TO TRUE.                          03370036
                                                                        03370036
           PERFORM 2510-OPEN-TINVPAR-CUR.                               03370036
                                                                        03370036
           PERFORM 2600-PROCESS-BY-DATE-CUR                             03370036
               UNTIL PS-BY-DATE-FINISHED-YES.                           03370036
                                                                        03370036
           PERFORM 2520-CLOSE-TINVPAR-CUR.                              03370036
                                                                        03370036
       2510-OPEN-TINVPAR-CUR.                                           03370036
                                                                        03370036
           EXEC SQL                                                             
               OPEN TINVPAR_LOC_CSR                                             
           END-EXEC.                                                            
                                                                        03370036
           IF SQLCODE NOT = +0                                                  
               MOVE '2510-OPEN-TINVPAR-CUR ' TO PV-PARAGRAPH-NAME               
               MOVE 'OPEN TINVPAR_LOC_CSR' TO PV-DB2-OPER-ATTEMPTED             
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                        03370036
       2520-CLOSE-TINVPAR-CUR.                                          03370036
                                                                                
           EXEC SQL                                                             
              CLOSE TINVPAR_LOC_CSR                                             
           END-EXEC.                                                            
                                                                        03370036
           IF SQLCODE NOT = +0                                                  
               MOVE '2520-CLOSE-TINVPAR-CUR' TO PV-PARAGRAPH-NAME               
               MOVE 'CLOSE TINVPAR_LOC_CSR' TO PV-DB2-OPER-ATTEMPTED            
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                        03370036
                                                                        03370036
       2600-PROCESS-BY-DATE-CUR.                                        02600036
                                                                        03360036
           INITIALIZE IN100-SUMMARY-REPORT-RECORD                       03370036
S28545                PA-SHEET-TOTAL-QTY                                03370036
S28545                PA-SHEET-EXTD-RTL-TOT.                            03370036
                                                                        03360036
           SET PS-TSUMINV-AT-END-NO TO TRUE.                            03360036
           SET PS-TINENTY-AT-END-NO TO TRUE.                            03360036
           SET PS-TINVDTL-AT-END-NO TO TRUE.                            03370036
                                                                        03360036
           PERFORM 2610-FETCH-INVPAR-DATE-LOC.                          03360036
                                                                        03360036
           IF PS-BY-DATE-FINISHED-NO                                    03360036
               PERFORM 2200-TSUMINV-PROCESS-ROWS                        03360036
                   UNTIL PS-TSUMINV-AT-END-YES                          03360036
                                                                        03360036
               PERFORM 2300-TINENTY-PROCESS-ROWS                        03370036
                   UNTIL PS-TINENTY-AT-END-YES                          03360036
                                                                        03370036
               PERFORM 2400-TINVDTL-PROCESS-ROWS                        03370036
                   UNTIL PS-TINVDTL-AT-END-YES                          03370036
                                                                        03370036
               PERFORM 5000-FORMAT-REPORT-AND-CUM                       03370036
           END-IF.                                                      03370036
                                                                        03370036
       2610-FETCH-INVPAR-DATE-LOC.                                      03360036
                                                                        03370036
           EXEC SQL                                                             
              FETCH TINVPAR_LOC_CSR                                             
               INTO  :INVPAR-LOC-NBR                                            
                    ,:INVPAR-ACTL-INV-DTE                                       
S28545              ,:INVPAR-SHEET-INV-CPLD-IND                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE INVPAR-LOC-NBR  TO PV-LOC-NBR                           
                                           IN004-STR-NUMBER                     
                                           IN100-STORE-NBR-9                    
                   MOVE INVPAR-ACTL-INV-DTE                                     
                                        TO IN100-INV-DATE                       
               WHEN SQLCODE = +100                                              
                   SET PS-BY-DATE-FINISHED-YES TO TRUE                          
               WHEN OTHER                                                       
                   MOVE '2610-FETCH-INVPAR-DATE-LOC '                           
                                                  TO PV-PARAGRAPH-NAME          
                   MOVE 'FETCH TINVPAR LOC CURSOR'                              
                                 TO  PV-DB2-OPER-ATTEMPTED                      
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                        03370036
       3000-BLOCKED-TRANS-CHECK.                                                
                                                                        05570036
           SET PS-BLOCKED-TRANS-FOUND-NO TO TRUE.                       05580036
                                                                        05570036
           PERFORM                                                      05590036
               VARYING PT-SKIP-CLASS-INDX FROM 1 BY 1                   05600036
               UNTIL PT-SKIP-CLASS-INDX > PT-SKIP-CLASS-COUNT           05610036
                   OR PS-BLOCKED-TRANS-FOUND-YES                        05620036
               IF PT-SKIP-DEPT-X(PT-SKIP-CLASS-INDX)                    05630036
                                                = PV-EDIT-DEPT-2-4      05630036
                   AND (PT-SKIP-CLASS-X(PT-SKIP-CLASS-INDX) = ZEROS OR  05640036
                        PT-SKIP-CLASS-X(PT-SKIP-CLASS-INDX)             05650036
                            = PV-EDIT-SCL-3-4)                          05660036
                   SET PS-BLOCKED-TRANS-FOUND-YES TO TRUE               05670036
               END-IF                                                   05680036
           END-PERFORM.                                                 05690036
                                                                        03370036
       4000-LOOK-UP-ATTRIBUTES.                                         03700036
                                                                        03820036
           INITIALIZE IN400-PV-INPUT-AREA.                              03830036
                                                                        03840036
           MOVE INVPAR-ACTL-INV-DTE     TO IN400-PV-EFF-DTE-IN.         03870036
           MOVE 'ASKU'                  TO IN400-PV-LOOKUP-TYPE-IN.     03880036
           MOVE INENTY-SKU-NBR          TO IN400-PV-SKU-IN-NUM.         03890036
           MOVE IN004-STR-NUMBER        TO IN400-PV-LOC-IN-NUM.         03900036
                                                                        03940036
           PERFORM IN400-LOOKUP-RETAIL.                                 03950036
                                                                        03960036
           IF IN400-PV-ATT-FOUND                                        03970036
               MOVE IN400-ATT-DEPT-OUT   TO PV-EDIT-DEPT-NBR            04530036
               MOVE IN400-ATT-SCL-OUT    TO PV-EDIT-SCL-NBR             04570036
           ELSE                                                         04130036
               IF IN400-PV-ATT-NOT-FOUND                                04140036
                   DISPLAY 'IN400 ATTR LOOK UP NOT FOUND'               04170036
                   DISPLAY 'DTE ' IN400-PV-EFF-DTE-IN                   04180036
                   DISPLAY 'TYPE ' IN400-PV-LOOKUP-TYPE-IN              04190036
                   DISPLAY 'LOC '  IN400-PV-LOC-IN-NUM                  04200036
                   DISPLAY 'SKU ' IN400-PV-SKU-IN-NUM                   04210036
                   DISPLAY 'SQL ' IN400-PV-ATT-SQLCDE                   04220036
                   DISPLAY 'RSN ' IN400-PV-RETURN-RSN                   04230036
           ELSE                                                         04240036
                   DISPLAY 'IN400-PV-ATT-RTNCDE '                       04250036
                                            IN400-PV-ATT-RTNCDE         04260036
                   MOVE '4100-LOOK-UP '                                 04270036
                                         TO PV-PARAGRAPH-NAME           04280036
                   MOVE 'INPD400 ATTRIBUTE LOOK UP '                    04290036
                                 TO  PV-DB2-OPER-ATTEMPTED              04300036
                   PERFORM 9900-SQL-ABEND                               04310036
                                                                        04320036
           END-IF.                                                      04330036
                                                                        04620036
       5000-FORMAT-REPORT-AND-CUM.                                      03370036
                                                                        04640036
           ADD +1 TO PA-RPT-LINES.                                      04640036
                                                                        04640036
           IF PA-RPT-LINES > +52                                        04640036
               PERFORM 5100-PRINT-HEADINGS                              04640036
               MOVE +1 TO PA-RPT-LINES.                                 04640036
                                                                        04640036
           MOVE IN004-STR-NUMBER            TO IN100-STORE-NBR.         04640036
           PERFORM 5050-GET-STORE-NAME.                                 04640036
           MOVE INVPAR-ACTL-INV-DTE (6:2)   TO IN100-INV-DATE (1:2).    04640036
           MOVE '/'                         TO IN100-INV-DATE (3:1).    04640036
           MOVE INVPAR-ACTL-INV-DTE (9:2)   TO IN100-INV-DATE (4:2).    04640036
S28545*                                                                 04640036
S28545* IF THE LOCATION HAS ALREADY HAD THE SHEET DATA PROCESSED THEN   04640036
S28545* DELETE THE QTY AND RETAIL FROM THE 'PHYSICAL' REPORT TOTALS     04640036
S28545     IF INVPAR-SHEET-INV-CPLD-IND = 'Y'                           03600036
S28545         COMPUTE IN100-PHY-INV-UNITS =                            03600036
S28545                (IN100-PHY-INV-UNITS - PA-SHEET-TOTAL-QTY)        03600036
S28545         COMPUTE IN100-PHY-INV-RTL   =                            03600036
S28545                (IN100-PHY-INV-RTL   - PA-SHEET-EXTD-RTL-TOT)     03600036
S28545     END-IF                                                       01610036
                                                                        01610036
           COMPUTE IN100-TOTAL-UNITS = IN100-PHY-INV-UNITS +            04640036
                                       IN100-INVALID-UNITS              04640036
           COMPUTE IN100-AVE-PRICE-POINT ROUNDED =                      04640036
                      (IN100-PHY-INV-RTL / IN100-TOTAL-UNITS).          04640036
                                                                        04640036
           MOVE IN100-STORE-NBR             TO PDL-STORE-NBR.           04650036
           MOVE IN100-STORE-NAME            TO PDL-STORE-NAME.          04650036
           MOVE IN100-INV-DATE              TO PDL-INV-DATE.            04650036
           MOVE IN100-AVE-PRICE-POINT       TO PDL-AVE-PRICE-POINT.     04650036
                                                                        04640036
           MOVE IN100-PHY-INV-RTL           TO PDL-PHY-INV-RTL.         04660036
           MOVE IN100-PHY-INV-UNITS         TO PDL-PHY-INV-UNITS.       04660036
           MOVE IN100-TRANSFER-SHEET-RTL    TO PDL-TRANSFER-SHEET-RTL.  04660036
           MOVE IN100-BLOCKED-TRANS-RTL     TO PDL-BLOCKED-TRANS-RTL.   04660036
           MOVE IN100-INVALID-UNITS         TO PDL-INVALID-UNITS.       04660036
           MOVE IN100-TOTAL-UNITS           TO PDL-TOTAL-UNITS.         04660036
                                                                        04800036
           WRITE SUM-REPORT-REC FROM PRINT-DETAIL-LINE                  04800036
                                         AFTER ADVANCING 1 LINE.        04800036
                                                                        04800036
           WRITE CUM-REC     FROM IN100-SUMMARY-REPORT-RECORD.          04800036
           ADD +1 TO PA-CUM-CNT.                                        04800036
                                                                        03600036
       5050-GET-STORE-NAME.                                             04640036
                                                                        03600036
           EXEC SQL                                                             
             SELECT LOC_NM                                                      
               INTO :IN100-STORE-NAME                                           
             FROM XMT_LOC                                                       
             WHERE LOC_NBR =:PV-LOC-NBR                                         
                                                                        03600036
           END-EXEC.                                                            
                                                                        03370036
           IF SQLCODE = +0                                                      
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '5050-GET-STORE-NAME ' TO PV-PARAGRAPH-NAME                 
               MOVE 'SELECT XMT00001 '     TO PV-DB2-OPER-ATTEMPTED             
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                        03370036
                                                                        03600036
       5100-PRINT-HEADINGS.                                             04640036
                                                                        03600036
           ADD +1           TO PA-PAGE-CNT.                                     
                                                                        03600036
           MOVE PA-PAGE-CNT TO DP132-PAGE-NUMBER.                       03600036
                                                                        03600036
           WRITE SUM-REPORT-REC FROM                                    03600036
                      DP132-STANDRD-HEADER-132-COLS                     03600036
                          AFTER ADVANCING PAGE.                         03600036
                                                                        03600036
           WRITE SUM-REPORT-REC FROM PRINT-HEAD-2                       03600036
                                   AFTER ADVANCING 2 LINES.             03600036
                                                                        03600036
           MOVE SPACES TO SUM-REPORT-REC.                               03600036
                                                                        03600036
           WRITE SUM-REPORT-REC AFTER ADVANCING 2 LINES.                03600036
                                                                        03600036
           WRITE SUM-REPORT-REC FROM PRINT-HEAD-3                       03600036
                                   AFTER ADVANCING 1 LINE.              03600036
                                                                        03600036
           WRITE SUM-REPORT-REC FROM PRINT-HEAD-4                       03600036
                                   AFTER ADVANCING 1 LINE.              03600036
                                                                        03600036
           WRITE SUM-REPORT-REC FROM PRINT-HEAD-5                       03600036
                                   AFTER ADVANCING 1 LINE.              03600036
                                                                        03600036
           MOVE SPACES TO SUM-REPORT-REC.                               03600036
                                                                        03600036
           WRITE SUM-REPORT-REC AFTER ADVANCING 1 LINE.                 03600036
                                                                        03600036
                                                                        03600036
       6000-READ-LOCATIONS.                                             03610036
                                                                        03620036
           READ IN-LOCATION-FILE INTO IN004-INVENTORY-COUNT-RECORD      03630036
               AT END                                                   03640036
                  SET PS-LOCATION-FILE-AT-END-YES TO TRUE.              03650036
                                                                        03660036
           IF PS-LOCATION-FILE-AT-END-NO                                03670036
               ADD +1 TO PA-INPUT-LOC-CNT                               03680036
               MOVE IN004-STR-NUMBER TO PV-LOC-NBR                      03680036
           END-IF.                                                              
                                                                        03690036
                                                                        06390036
       8000-END-PROCESSING.                                             06400036
                                                                        06410036
           CLOSE IN-DATE-FILE                                                   
                 IN-LOCATION-FILE                                               
                 SKIP-CLASS-FILE                                                
                 SUMMARY-RPT-FILE                                               
                 CUM-FILE.                                              02770036
                                                                        06720036
           DISPLAY 'PA-INPUT-LOC-CNT ' PA-INPUT-LOC-CNT.                06730036
           DISPLAY 'IN-DATE ' IN-DATE.                                  06410036
           DISPLAY 'PA-CUM-CNT ' PA-CUM-CNT.                            06410036
                                                                        06410036
      *************************************************************     06740036
      *    COPYBOOK FOR COMMON PRICING PROCESS PROCEDURE                06750036
      *************************************************************     06760036
           COPY INPD400.                                                06770036
                                                                        06780036
                                                                        06870036
       9800-ABEND.                                                      06880036
           CALL 'ILBOABN0'  USING ABEND-CODE.                           06890036
                                                                        06900036
                                                                        06910036
      ***************************************************************   06920036
      *    SQL ABEND PROCESSING                                     *   06930036
      ***************************************************************   06940036
       9900-SQL-ABEND.                                                  06950036
                                                                        06960036
           DISPLAY '** ABEND     **'.                                   06970036
           DISPLAY '** PROGRAM   = ' PC-PROGRAM-ID.                     06980036
           DISPLAY '** PARAGRAPH = ' PV-PARAGRAPH-NAME.                 06990036
           DISPLAY '** OPERATION = ' PV-DB2-OPER-ATTEMPTED.             07000036
                                                                        07010036
           COPY DPPD004.                                                07020036
                                                                        07030036
           CALL 'ILBOABN0'  USING ABEND-CODE.                           07040036
                                                                        07050036
