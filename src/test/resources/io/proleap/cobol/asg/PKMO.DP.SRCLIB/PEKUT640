      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.      PEKUT640.                                       00050000
       AUTHOR.          TOM BOYD.                                       00060000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00070000
       DATE-WRITTEN.    AUGUST 2005.                                    00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ******************************************************************00110000
      * THIS PROGRAM WILL TRUNCATE ORACLE PE TABLES PASSED IN.          00120000
      ******************************************************************00122000
      **  HISTORY LOG:                                                 *00123000
      ******************************************************************00124000
      * DATE:         08/22/2005                                       *00125000
      * WR/IR#:       WR28048                                          *00126000
      * PROGRAMMER:   TOM BOYD                                         *00127000
      * DESCRIPTION:  INITIAL CREATION.                                *00128000
      *                                                                *00129000
      * DATE:         10/10/2005                                       *00130000
      * WR/IR#:       WR28048                                          *00140000
      * PROGRAMMER:   TOM BOYD                                         *00150000
      * DESCRIPTION:  ADDED REBUILD OF PEV_TSKST                       *00160000
      *                                                                *00160100
      * DATE:         10/19/2005                                       *00161000
      * WR/IR#:       WR28048                                          *00162000
      * PROGRAMMER:   TOM BOYD                                         *00163000
      * DESCRIPTION:  REMOVED REBUILD OF PEV_TSKST.  CREATED NEW PROGRAM00164000
      *               TO PERFORM THIS TASK.                            *00165000
      *                                                                *00166000
      * DATE:         06/20/2006                                       *00167000
      * WR/IR#:       WR29858                                          *00168000
      * PROGRAMMER:   DARYL WOELFEL                                    *00169000
      * DESCRIPTION:  EXPANDED SIZE OF TABLE NAME FIELD TO ALLOW FOR   *00169100
      *               SLIGHTLY LONGER TABLE NAMES.                     *00169200
      *                                                                *00169302
      * DATE:         07/30/2018                                       *00169502
      * WR/IR#:       CHG0204126                                       *00169602
      * PROGRAMMER:   COGNIZANT                                        *00169702
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *00169802
      ******************************************************************00170000
                                                                        00180000
      ******************************************************************00190000
       ENVIRONMENT DIVISION.                                            00200000
      ******************************************************************00210000
                                                                        00220000
       CONFIGURATION SECTION.                                           00230000
                                                                        00240000
       SOURCE-COMPUTER. IBM-3090.                                       00250000
       OBJECT-COMPUTER. IBM-3090.                                       00260000
                                                                        00270000
       INPUT-OUTPUT SECTION.                                            00280000
                                                                        00290000
       FILE-CONTROL.                                                    00300000
           SELECT TABLE-FILE                                            00310000
                  ASSIGN                    TO INP01.                   00320000
                                                                        00331001
           SELECT ORACLE-LOGON-FILE                                     00332001
                  ASSIGN                    TO ORALOGON.                00333001
                                                                        00334001
      ******************************************************************00340000
       DATA DIVISION.                                                   00350000
      ******************************************************************00360000
                                                                        00370000
       FILE SECTION.                                                    00380000
      *    DEPT-FILE WILL CONTAIN ALL OF THE DEPARTMENT TO PROCESS      00390000
       FD  TABLE-FILE                                                   00400000
           RECORDING MODE F                                             00410000
           BLOCK CONTAINS 0 RECORDS                                     00420000
           LABEL RECORDS ARE OMITTED                                    00430000
           DATA RECORD IS DEPT-RECORD.                                  00440000
       01  TABLE-RECORD                     PIC X(80).                  00450000
                                                                        00460000
       FD  ORACLE-LOGON-FILE                                            00461001
           RECORDING MODE IS F                                          00462001
           LABEL RECORDS ARE STANDARD                                   00463001
           BLOCK CONTAINS 0 RECORDS                                     00464001
           RECORD CONTAINS 80 CHARACTERS                                00465001
           DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                      00466001
       01  OL-ORACLE-LOGON-RECORD      PIC X(80).                       00467001
                                                                        00468001
       WORKING-STORAGE SECTION.                                         00470000
                                                                        00480000
      ******************************************************************00490000
      * PC PROGRAM CONSTANTS                                            00500000
      ******************************************************************00510000
       01  PC-PROGRAM-CONSTANTS.                                        00520000
           05  PC-PROGRAM-NAME              PIC X(08)      VALUE        00530001
                                                           'PEKUT640'.  00540000
                                                                        00550000
      ******************************************************************00560000
      * PV PROGRAM VARIABLES                                            00570000
      ******************************************************************00580000
       01  PV-PROGRAM-VARIABLES.                                        00590000
           05  PV-ORACLE-USERID-LEN        PIC S9(4)   VALUE ZEROS      00591001
                                                       COMP SYNC.       00592001
           05  PV-ORACLE-PASSWORD-LEN      PIC S9(4)   VALUE ZEROS      00593001
                                                       COMP SYNC.       00594001
           05  PV-TABLE-NAME                PIC X(40) VALUE SPACES.     00600000
           05  PV-SQL-CODE                  PIC 9(09) VALUE 0.          00610000
           05  PV-PARAGRAPH-NAME            PIC X(40) VALUE SPACES.     00620000
           05  PV-OPERATION-MSG             PIC X(60)  VALUE SPACES.    00630000
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACES.     00640000
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACES.     00650000
           05  PV-CPU-TIME                  PIC  9(08) VALUE 0.         00660000
           05  PV-CURRENT-TIME              PIC X(08) VALUE SPACES.     00670000
           05  PV-RUN-DATE                  PIC X(10) VALUE SPACES.     00680000
           05  PV-TABLE                     PIC X(40) VALUE ZEROES.     00690000
                                                                        00713000
       01  CC-CONTROL-CARD-TBL-REC.                                     00714000
           05  CC-TABLE                       PIC X(40) VALUE ZEROES.   00715000
           05  FILLER                         PIC X(40) VALUE SPACES.   00716000
                                                                        00717000
      ******************************************************************00718000
      * PS PROGRAM SWITCHES                                            *00719000
      ******************************************************************00720000
       01  PS-PROGRAM-SWITCHES.                                         00730000
006510     05  PS-END-OF-LOGON-FILE-SW     PIC X(01)   VALUE 'N'.       00731001
006530         88  PS-END-OF-LOGON-FILE                VALUE 'Y'.       00732001
           05  PS-END-OF-PROC-INST-FILE-SW PIC X(01)   VALUE 'N'.       00733001
               88  PS-END-OF-PROC-INST-FILE            VALUE 'Y'.       00734001
               88  PS-NOT-END-OF-PROC-INST-FILE        VALUE 'N'.       00735001
           05  PS-END-OF-TABLE-FILE-SW      PIC X(01)  VALUE 'N'.       00740000
               88  PS-END-OF-TABLE-FILE                VALUE 'Y'.       00750000
               88  PS-NOT-END-OF-TABLE-FILE            VALUE 'N'.       00760000
           05  PS-ORA-SRC-CTL-DONE-SW  PIC  X(01)      VALUE 'N'.       00761001
               88  PS-ORA-SRC-EMPTY                    VALUE 'Y'.       00762001
                                                                        00770000
       01  CC-ORACLE-CONNECTION.                                        00772001
           05  CC-ORACLE-USERID             PIC X(40).                  00773001
               88 CC-AUTO-LOGON                         VALUE '/'.      00774001
           05  CC-ORACLE-PASSWORD           PIC X(40).                  00775001
                                                                        00776001
      ******************************************************************00780000
      * PA PROGRAM ACCUMULATORS                                        *00790000
      ******************************************************************00800000
       01  PA-PROGRAM-ACCUMULATORS.                                     00810000
           05  PA-RECS-WRITTEN               PIC S9(13) COMP-3 VALUE 0. 00820000
           05  PA-READ-COUNTER               PIC S9(11) COMP-3 VALUE 0. 00830000
                                                                        00840000
      ******************************************************************00850000
      * AC ABEND CODES                                                 *00860000
      ******************************************************************00870000
                                                                        00880000
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0. 00890000
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.   00900000
           88  ABEND-4002-READ-ERROR                     VALUE +4002.   00910000
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.   00920000
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.   00930000
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.   00940000
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.   00950000
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.   00960000
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.   00970000
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.   00980000
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.   00990000
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.   01000000
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.   01010000
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.   01020000
                                                                        01030000
      *----------------------------------------------------------       01040000
      *                      O  R  A  C  L  E                           01050000
      *==========================================================       01060000
      *                                                                 01070000
      * ORACLE DECLARE SECTION                                          01080000
      * ORACLE USED AND RETURNED VARIABLES                              01090000
      *                                                                 01100000
      *----------------------------------------------------------       01110000
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                     01120000
                                                                        01130000
      *----------------------------------------------------------       01140000
      * ORACLE LOGON WORKING STORAGE COPYBOOK                           01150000
      *----------------------------------------------------------       01160000
                                                                        01170000
       01  PC-ENT01-TYPE-CDE                PIC X(02)  VALUE '01'.      01180000
       01  PV-ORACLE-ID                     PIC X(1)   VALUE '/'.       01190000
                                                                        01200000
       01  USERNAME                         PIC X(10)  VARYING.         01210000
       01  PASSWD                           PIC X(10)  VARYING.         01220000
                                                                        01230000
       01  ORACLE-RETURN                    PIC X(200) VALUE SPACES.    01240000
                                                                        01250000
           EXEC SQL END DECLARE SECTION END-EXEC.                       01260000
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                       01270000
                                                                        01280000
       EJECT                                                            01290000
                                                                        01300000
       PROCEDURE DIVISION.                                              01310000
                                                                        01320000
      ******************************************************************01330000
      * MAIN PROCESSING                                                 01340000
      ******************************************************************01350000
       A100-PROGRAM-MAINLINE.                                           01360000
                                                                        01370000
           PERFORM B100-INITIALIZE.                                     01380000
                                                                        01390000
           PERFORM B200-READ-TABLE-FILE.                                01400000
           PERFORM C100-CONTROL                                         01410000
             UNTIL PS-END-OF-TABLE-FILE.                                01420000
                                                                        01430000
           DISPLAY ' '.                                                 01440000
           DISPLAY 'ALL RECORDS SUCCESSFULLY TRUNCATED'.                01450000
           DISPLAY ' '.                                                 01460000
                                                                        01470000
           PERFORM B900-EOJ-ROUTINE.                                    01490000
                                                                        01500000
           GOBACK.                                                      01510000
                                                                        01520000
      ******************************************************************01530000
      * OPEN FILE                                                       01540000
      ******************************************************************01550000
       B100-INITIALIZE.                                                 01560000
                                                                        01570000
           OPEN INPUT ORACLE-LOGON-FILE.                                01571001
                                                                        01572001
           PERFORM 7000-READ-ORACLE-LOGON.                              01573001
                                                                        01574001
           CLOSE ORACLE-LOGON-FILE.                                     01575001
                                                                        01576001
           IF ((PS-END-OF-LOGON-FILE)                                   01577001
                   OR (CC-ORACLE-CONNECTION = SPACES))                  01578001
               DISPLAY '** ABEND **'                                    01579001
               DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '  01579101
                         'NOT PROVIDED'                                 01579201
               SET PS-ORA-SRC-EMPTY TO TRUE                             01579301
               CALL 'ILBOABN0' USING ABEND-CODE                         01579401
           END-IF.                                                      01579501
                                                                        01579601
           IF CC-AUTO-LOGON                                             01579701
               DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'        01579801
               EXEC SQL                                                 01579901
                    CONNECT :USERNAME                                   01580001
               END-EXEC                                                 01580101
           ELSE                                                         01580201
               INITIALIZE PV-ORACLE-USERID-LEN                          01580301
                          PV-ORACLE-PASSWORD-LEN                        01580401
               INSPECT CC-ORACLE-USERID                                 01580501
                   TALLYING PV-ORACLE-USERID-LEN                        01580601
                FOR CHARACTERS BEFORE INITIAL SPACE                     01580701
            INSPECT CC-ORACLE-PASSWORD                                  01580801
                TALLYING PV-ORACLE-PASSWORD-LEN                         01580901
                FOR CHARACTERS BEFORE INITIAL SPACE                     01581001
            MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO           01581101
                      USERNAME-ARR                                      01581201
            MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                   01581301
            MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO       01581401
                      PASSWD-ARR                                        01581501
            MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                   01581601
            DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '       01581701
                      USERNAME-ARR                                      01581801
            EXEC SQL                                                    01581901
              CONNECT :USERNAME IDENTIFIED BY :PASSWD                   01582001
            END-EXEC                                                    01582101
            INITIALIZE CC-ORACLE-PASSWORD                               01582201
                       PASSWD                                           01582301
            END-IF.                                                     01582401
                                                                        01582501
020500     IF (SQLCODE NOT = 0)                                         01582601
020900         DISPLAY PV-PARAGRAPH-NAME                                01582701
020910         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'  01582801
021000         PERFORM 9900-ORACLE-ABEND                                01582901
021200     END-IF.                                                      01583001
                                                                        01583101
      ******************************************************************01583201
      * NEEDS TO BE IN FOR NOW TIL KURT FIXES CURSOR SHARING BUG        01583301
      * OTHERWISE I GET A ORA-3113 ERROR                                01583401
      ******************************************************************01583501
           EXEC SQL                                                     01583601
            ALTER SESSION                                               01583701
                  SET CURSOR_SHARING = 'EXACT'                          01583801
           END-EXEC.                                                    01583901
                                                                        01584001
           OPEN INPUT TABLE-FILE.                                       01585000
                                                                        01590000
      ******************************************************************01620000
      * B200-READ-TABLE-FILE.                                           01630000
      *     READS THE TABLE NAME FOR THE RECORD THAT SHOULD BE TRUNCATED01640000
      ******************************************************************01650000
       B200-READ-TABLE-FILE.                                            01660000
                                                                        01670000
           READ TABLE-FILE INTO CC-CONTROL-CARD-TBL-REC                 01680000
                AT END SET PS-END-OF-TABLE-FILE TO TRUE.                01690000
                                                                        01700000
           MOVE CC-TABLE TO PV-TABLE.                                   01710000
                                                                        01720000
           IF PS-NOT-END-OF-TABLE-FILE                                  01730000
              PERFORM 8000-GET-CURRENT-TIME                             01740000
              DISPLAY 'TRUNCATING TABLE: ' PV-TABLE                     01750000
           END-IF.                                                      01760000
                                                                        01770000
      *B200-EXIT.                                                       01780000
       EJECT.                                                           01790000
                                                                        01800000
      *--------------------------------------------------------------   01810000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT      01820000
      * LOGS ON TO ORACLE.                                              01830000
      *---------------------------------------------------------------  01840000
      *    COPY PEPD700.                                                01850001
                                                                        01860000
      ******************************************************************01980000
      * CLEAN UP                                                        01990000
      ******************************************************************02000000
       B900-EOJ-ROUTINE.                                                02010000
                                                                        02020000
           CLOSE TABLE-FILE.                                            02030000
                                                                        02040000
                                                                        02050000
      ******************************************************************02060000
      * CONTROL PROCESSING BASED ON DEPARTMENT NUMBERS.                 02070000
      ******************************************************************02080000
       C100-CONTROL.                                                    02090000
                                                                        02100000
           PERFORM D100-TRUNCATE-TABLE.                                 02110000
           PERFORM B200-READ-TABLE-FILE.                                02120000
                                                                        02130000
      ******************************************************************02140000
      * TRUNCATE RECORD                                                 02150000
      ******************************************************************02160000
       D100-TRUNCATE-TABLE.                                             02170000
                                                                        02180000
           EXEC SQL EXECUTE                                             02190000
             BEGIN                                                      02200000
               PMSP_TRUNC_TABLE(:PV-TABLE);                             02210000
             END;                                                       02220000
           END-EXEC.                                                    02230000
                                                                        02240000
           EVALUATE TRUE                                                02250000
             WHEN SQLCODE = ZERO                                        02260000
                 DISPLAY 'FINISHED TRUNCATING : ' PV-TABLE              02270000
             WHEN OTHER                                                 02280000
                 MOVE SQLCODE                    TO PV-SQL-CODE         02290000
                 MOVE PV-TABLE                   TO PV-TABLE-NAME       02300000
                 MOVE 'D100-TRUNCATE-TABLE' TO PV-PARAGRAPH-NAME        02310000
                 PERFORM 9900-ORACLE-ABEND                              02320000
           END-EVALUATE.                                                02330000
                                                                        02340000
      *D100 END                                                         02350000
                                                                        02360000
034503****************************************************************  02370001
034504** READ THE ORACLE LOGON CONTROL CARD.                        **  02380001
034505****************************************************************  02390001
034506 7000-READ-ORACLE-LOGON.                                          02400001
                                                                        02410001
034510     READ ORACLE-LOGON-FILE                                       02411001
034520         INTO CC-ORACLE-CONNECTION                                02411101
034530         AT END                                                   02411201
034540             SET PS-END-OF-LOGON-FILE TO TRUE                     02411301
034550     END-READ.                                                    02411401
034600*                                                                 02411501
                                                                        02411601
      ******************************************************************02411800
       8000-GET-CURRENT-TIME.                                           02411900
      ******************************************************************02412000
                                                                        02412100
            MOVE SPACES TO PV-CURRENT-TIME.                             02412200
                                                                        02412300
           EXEC SQL                                                     02412400
              SELECT                                                    02412500
                 TO_CHAR(SYSDATE,'HH24:MI:SS')                          02412600
              INTO :PV-CURRENT-TIME                                     02412700
              FROM DUAL                                                 02412800
            END-EXEC.                                                   02412900
                                                                        02413000
           EVALUATE TRUE                                                02413100
             WHEN SQLCODE = ZERO                                        02413200
                 DISPLAY 'CURRENT TIME: ' PV-CURRENT-TIME               02413300
             WHEN OTHER                                                 02413400
                 MOVE SQLCODE                    TO PV-SQL-CODE         02413500
                 MOVE 'DUAL'                      TO PV-TABLE-NAME      02413600
                 MOVE '8000-GET-CURRENT-TIME   ' TO PV-PARAGRAPH-NAME   02413700
                 PERFORM 9900-ORACLE-ABEND                              02413800
           END-EVALUATE.                                                02413900
                                                                        02414000
      *-----------------------------------------------------------      02415000
      * Z100-ABEND                                                      02416000
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE              02417000
      *  - CALLED BY PEPD70B!                                           02418000
      *-----------------------------------------------------------      02419000
       Z100-ABEND.                                                      02420000
                                                                        02430000
           DISPLAY '                       '.                           02440000
           DISPLAY '** ABEND **       ** = SQLERROR'.                   02450000
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.           02460001
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                   02470000
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                02480000
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                   02490000
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  02500000
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                02510000
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.         02520000
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.             02530000
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.          02540000
           DISPLAY '                       '.                           02550000
                                                                        02560000
           CALL 'ILBOABN0' USING ABEND-CODE.                            02570000
                                                                        02580000
      *----------------------------------------------------------       02590000
      * 9900-ORACLE-ABEND.                                              02600000
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.            02610000
      *----------------------------------------------------------       02620000
       9900-ORACLE-ABEND.                                               02630000
                                                                        02640000
           DISPLAY '                       '.                           02650000
           DISPLAY '** ABEND **       ** = SQLERROR'.                   02660000
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.           02670001
           DISPLAY '** SQLCODE        ** = ' PV-SQL-CODE.               02680000
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                02690000
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                   02700000
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  02710000
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                02720000
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.         02730000
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.             02740000
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.          02750000
           DISPLAY '                       '.                           02760000
                                                                        02770000
           CALL 'ILBOABN0' USING ABEND-CODE.                            02780000
                                                                        02790000
      *9900-EXIT.                                                       02800000
