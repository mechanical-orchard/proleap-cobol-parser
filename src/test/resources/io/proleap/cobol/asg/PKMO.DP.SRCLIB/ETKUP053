       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.              ETKUP053.                                       
       AUTHOR.                  SUE PARI.                                       
       DATE-WRITTEN.            09/21/00.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      ********************  EDI 864 TEXT MESSAGE  **********************        
      ******************************************************************        
      * 1.  A TEXT MESSAGE IS ENTERED INTO THE EDI TEXT MESSAGE LOTUS  *        
      *     NOTES FORM.  THE FORM NEEDS TO BE APPROVED BY A MANAGER.   *        
      * 2.  THE INFORMATION FROM THE LOTUS NOTES FORM IS LOADED INTO   *        
      *     THE TETTXHD, TETTXDT, AND TETTXDS TABLES BY "DECS".        *        
      * 3.  THIS PROGRAM PULLS THE INFORMATION FROM THE DB2 TABLES     *        
      *     AND CREATES THE 864 TEXT MESSAGE EXTRACT FILE.  THIS       *        
      *     EXTRACT FILE IS THEN SENT TO UNIX TO BE TRANSLATED.        *        
      ******************************************************************        
      *  CHANGE HISTORY:                                                        
      *                                                                         
JF0602*   DATE 06/17/2002                                                       
JF0602*   ADDED LOGIC FOR PROCESSING TEXT MESSAGES SENT TO 816 VENDORS.         
JF0602*   MADE BY: JON FIEDLER             WR/IR #: 22778                       
      *                                                                         
WG0310*   DATE 03/08/2010                                                       
WG0310*   ADDED LOGIC FOR PROCESSING TEXT MESSAGES SENT TO 310 VENDORS.         
WG0310*   ADDED LOGIC FOR PROCESSING TEXT MESSAGES SENT TO 860 VENDORS.         
WG0310*   MADE BY: WADE M GROVE            WR/IR #: WR30431                     
      *                                                                         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT OUTPUT-FILE                                                   
              ASSIGN TO OUT01.                                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 260 CHARACTERS                                       
           DATA RECORD IS OUTPUT-RECORD.                                        
                                                                                
       01  OUTPUT-RECORD               PIC X(260).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  END-OF-ETTXHD-SW            PIC X(1)   VALUE 'N'.                
               88  END-OF-ETTXHD                      VALUE 'Y'.                
           05  END-OF-ETTXDS-SW            PIC X(1)   VALUE 'N'.                
               88  END-OF-ETTXDS                      VALUE 'Y'.                
           05  END-OF-ETTXDT-SW            PIC X(1)   VALUE 'N'.                
               88  END-OF-ETTXDT                      VALUE 'Y'.                
           05  END-OF-VENDOR-SW            PIC X(1)   VALUE 'N'.                
               88  END-OF-VENDOR                      VALUE 'Y'.                
           05  END-OF-ALLVND-SW            PIC X(1)   VALUE 'N'.                
               88  END-OF-ALLVND                      VALUE 'Y'.                
           05  VENDOR-SELECTED-SW          PIC X(1)   VALUE 'N'.                
               88  VENDOR-SELECTED                    VALUE 'Y'.                
           05  TXT-MSG-RELATIONSHIP-SW     PIC X(1)   VALUE 'N'.                
               88  TXT-MSG-RELATIONSHIP               VALUE 'Y'.                
                                                                                
       01  VENDOR-TABLE.                                                        
           05  WS-VENDOR-TABLE OCCURS 3000 TIMES.                               
               10  WS-TBL-ENT-ID           PIC 9(10).                           
               10  WS-TBL-VENDOR           PIC X(30).                           
                                                                                
       01  WS-EXTRACT-RECORD.                                                   
           05  WS-EXTRACT-ETRD109          PIC X(250).                          
           05  WS-EXTRACT-MSG-NUMBER       PIC X(10).                           
                                                                                
       01  PROGRAM-VARIABLES.                                                   
                                                                                
           05  SUB                         PIC 9(4).                            
                                                                                
           05  WS-ENT-ID                   PIC S9(9) COMP.                      
           05  WS-ENT-ID-10                PIC 9(10).                           
                                                                                
           05  PREV-ENT-ID                 PIC 9(6).                            
           05  PREV-TP-DKEY                PIC 9(9).                            
                                                                                
           05  WS-PHONE-NUMBER             PIC X(12).                           
           05  WS-PHONE-NBR-REDEFINED REDEFINES WS-PHONE-NUMBER.                
               10  WS-AREA-CDE-NBR         PIC 9(3).                            
               10  WS-DASH-1               PIC X(1).                            
               10  WS-EXCH-NBR             PIC 9(3).                            
               10  WS-DASH-2               PIC X(1).                            
               10  WS-LAST-4-NBR           PIC 9(4).                            
                                                                                
           05  PV-PARAGRAPH-NAME           PIC X(30).                           
           05  PV-DB2-OPER-ATTEMPTED       PIC X(30).                           
                                                                                
       01  ABEND-CODE                PIC S9(04)  VALUE +0 COMP SYNC.            
           88 ABEND-4013                         VALUE +4013.                   
                                                                                
                                                                                
      *****************************************************************         
      *  EDI 864 TEXT MESSAGE EXTRACT FILE                            *         
      *****************************************************************         
           COPY ETRD109.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                            *         
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
                                                                                
           EXEC SQL                                                             
              INCLUDE TETTXHD                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TETTXDS                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TETTXDT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDTRN                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDMLT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *  RETRIEVE ALL TEXT MESSAGES FROM THE TEXT MESSAGE HEADER      *         
      *  TABLE THAT HAVE NOT YET BEEN PROCESSED.                      *         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
              DECLARE ETTXHD_CURSOR CURSOR FOR                                  
              SELECT  *                                                         
              FROM    TETTXHD                                                   
              WHERE   TXT_MSG_SNT_IND = 'N'                                     
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *  RETRIEVE ALL VENDORS THAT WERE CHOSEN IN THE DROP-DOWN BOX   *         
      *  ON THE LOTUS NOTES FORM FROM THE TEXT MESSAGE DESTINATION    *         
      *  TABLE.                                                       *         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
              DECLARE ETTXDS_CURSOR CURSOR FOR                                  
              SELECT  ENT_ID,                                                   
                      VND_NM                                                    
              FROM    TETTXDS                                                   
              WHERE   TXT_MSG_DKEY_ID = :ETTXHD-TXT-MSG-DKEY-ID                 
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *  RETRIEVE THE TEXT MESSAGE AND LINE NUMBER FROM THE TEXT      *         
      *  MESSAGE DETAIL TABLE.                                        *         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
              DECLARE ETTXDT_CURSOR CURSOR FOR                                  
              SELECT  DTL_LNE_NBR,                                              
                      DTL_MSG_TXT                                               
              FROM    TETTXDT                                                   
              WHERE   TXT_MSG_DKEY_ID = :ETTXHD-TXT-MSG-DKEY-ID                 
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *  RETRIEVE ALL ACTIVE PRODUCTION EDI RELATIONSHIPS.  THIS      *         
      *  CURSOR IS USED WHEN A DOCUMENT TYPE OR TEST/PROD STATUS IS   *         
      *  SELECTED ON THE LOTUS NOTES FORM.                            *         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
              DECLARE VENDOR_CURSOR CURSOR FOR                                  
              SELECT  A.TP_DKEY,                                                
                      A.ENT_ID,                                                 
                      B.TRAN_SET_CDE,                                           
                      B.PROD_TEST_IND                                           
              FROM    TTRDPRT A,                                                
                      TTRDTRN B                                                 
              WHERE   A.INACTV_DTE = '9999-09-09'                               
                AND   B.TP_DKEY = A.TP_DKEY                                     
                AND   B.INACTV_DTE = '9999-09-09'                               
             ORDER BY A.ENT_ID                                                  
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *  RETRIEVE ALL ACTIVE PRODUCTION 864 RELATIONSHIPS.  THIS      *         
      *  CURSOR IS USED WHEN "ALL VENDORS" IS CHOSEN FROM THE DROP-   *         
      *  DOWN BOX ON THE LOTUS NOTES FORM.                            *         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
              DECLARE ALLVND_CURSOR CURSOR FOR                                  
              SELECT  A.ENT_ID                                                  
              FROM    TTRDPRT A,                                                
                      TTRDTRN B                                                 
              WHERE   A.INACTV_DTE = '9999-09-09'                               
                AND   B.TP_DKEY = A.TP_DKEY                                     
                AND   B.TRAN_SET_CDE = '864'                                    
                AND   B.PROD_TEST_IND = 'P'                                     
                AND   B.DIR_IND = 'S'                                           
                AND   B.INACTV_DTE = '9999-09-09'                               
             ORDER BY A.ENT_ID                                                  
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN OUTPUT OUTPUT-FILE.                                             
                                                                                
           PERFORM 1000-GET-TEXT-MESSAGES                                       
              THRU 1000-GET-TEXT-MESSAGES-EXIT.                                 
                                                                                
           CLOSE OUTPUT-FILE.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *  ACCESS THE TETTXHD TABLE TO GET ALL THE TEXT MESSAGES THAT   *         
      *  HAVE NOT YET BEEN PROCESSED.                                 *         
      *****************************************************************         
                                                                                
       1000-GET-TEXT-MESSAGES.                                                  
                                                                                
           EXEC SQL                                                             
              OPEN ETTXHD_CURSOR                                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE '1000-GET-TEXT-MESSAGES' TO PV-PARAGRAPH-NAME                
              MOVE 'OPEN ETTXHD CURSOR' TO PV-DB2-OPER-ATTEMPTED                
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
           MOVE 'N' TO END-OF-ETTXHD-SW.                                        
                                                                                
           PERFORM 2000-CREATE-EXTRACT-FILE                                     
              THRU 2000-CREATE-EXTRACT-FILE-EXIT                                
                 UNTIL END-OF-ETTXHD.                                           
                                                                                
           EXEC SQL                                                             
              CLOSE ETTXHD_CURSOR                                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE '1000-GET-TEXT-MESSAGES' TO PV-PARAGRAPH-NAME                
              MOVE 'CLOSE ETTXHD CURSOR' TO PV-DB2-OPER-ATTEMPTED               
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
       1000-GET-TEXT-MESSAGES-EXIT.                                             
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *  FOR EACH TEXT MESSAGE THAT IS TO BE PROCESSED.....           *         
      *  1.  LOAD THE VENDOR-TABLE WITH THE ENT-ID'S OF THE VENDORS   *         
      *      THAT WILL RECEIVE THE 864.                               *         
      *  2.  CREATE RECORDS ON THE EXTRACT FILE FOR EACH OF THE       *         
      *      ENT-ID'S ON THE VENDOR-TABLE.                            *         
      *  3.  UPDATE TXT_MSG_SNT_IND ON THE TETTXHD TABLE TO INDICATE  *         
      *      THAT THIS 864 TEXT MESSAGE HAS BEEN SENT.                *         
      *****************************************************************         
                                                                                
       2000-CREATE-EXTRACT-FILE.                                                
                                                                                
           EXEC SQL                                                             
              FETCH ETTXHD_CURSOR                                               
              INTO :ETTXHD-TXT-MSG-DKEY-ID,                                     
                   :ETTXHD-TXT-MSG-TMST,                                        
                   :ETTXHD-TXT-MSG-210-IND,                                     
                   :ETTXHD-TXT-MSG-214-IND,                                     
                   :ETTXHD-TXT-MSG-810-IND,                                     
                   :ETTXHD-TXT-MSG-820-IND,                                     
                   :ETTXHD-TXT-MSG-855-IND,                                     
                   :ETTXHD-TXT-MSG-856-IND,                                     
                   :ETTXHD-TXT-MSG-852-IND,                                     
                   :ETTXHD-TXT-MSG-850-IND,                                     
                   :ETTXHD-TXT-MSG-TST-IND,                                     
                   :ETTXHD-TXT-MSG-PROD-IND,                                    
                   :ETTXHD-PHN-AREA-CDE-NBR,                                    
                   :ETTXHD-PHN-EXCH-NBR,                                        
                   :ETTXHD-PHN-LAST-4-NBR,                                      
                   :ETTXHD-FAX-AREA-CDE-NBR,                                    
                   :ETTXHD-FAX-EXCH-NBR,                                        
                   :ETTXHD-FAX-LAST-4-NBR,                                      
                   :ETTXHD-TXT-MSG-SNT-IND,                                     
                   :ETTXHD-TXT-MSG-CNTCT-NM,                                    
                   :ETTXHD-TXT-MSG-SBJCT-DESC,                                  
WG0310             :ETTXHD-TXT-MSG-816-IND,                                     
WG0310             :ETTXHD-TXT-MSG-310-IND,                                     
WG0310             :ETTXHD-TXT-MSG-860-IND                                      
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
              CONTINUE                                                          
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 MOVE 'Y' TO END-OF-ETTXHD-SW                                   
                 GO TO 2000-CREATE-EXTRACT-FILE-EXIT                            
              ELSE                                                              
                 MOVE '2000-CREATE-EXTRACT-FILE' TO PV-PARAGRAPH-NAME           
                 MOVE 'FETCH ETTXHD CURSOR' TO PV-DB2-OPER-ATTEMPTED            
                 PERFORM 9999-SQL-ERROR.                                        
                                                                                
           INITIALIZE VENDOR-TABLE.                                             
           MOVE 0 TO SUB.                                                       
                                                                                
           IF ETTXHD-TXT-MSG-210-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-214-IND = 'N' AND                                  
WG0310        ETTXHD-TXT-MSG-310-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-810-IND = 'N' AND                                  
JF0602        ETTXHD-TXT-MSG-816-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-820-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-852-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-855-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-856-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-850-IND = 'N' AND                                  
WG0310        ETTXHD-TXT-MSG-860-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-TST-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-PROD-IND = 'N'                                     
                 PERFORM 3000-GET-VENDOR-LIST                                   
                    THRU 3000-GET-VENDOR-LIST-EXIT                              
           ELSE                                                                 
              PERFORM 4000-GET-VENDOR-TYPE                                      
                 THRU 4000-GET-VENDOR-TYPE-EXIT.                                
                                                                                
           IF WS-TBL-ENT-ID (1) = ZERO AND                                      
              WS-TBL-VENDOR (1) = 'All                           '              
                 PERFORM 5000-GET-ALL-VENDORS                                   
                    THRU 5000-GET-ALL-VENDORS-EXIT.                             
                                                                                
           MOVE 0 TO SUB.                                                       
                                                                                
           PERFORM VARYING SUB FROM 1 BY 1                                      
           UNTIL SUB > 3000 OR WS-TBL-ENT-ID (SUB) = ZERO                       
              PERFORM 6000-MULT-ENT-ID                                          
                 THRU 6000-MULT-ENT-ID-EXIT                                     
              PERFORM 6100-RECORD-TYPE-01                                       
                 THRU 6100-RECORD-TYPE-01-EXIT                                  
              PERFORM 6200-RECORD-TYPE-02                                       
                 THRU 6200-RECORD-TYPE-02-EXIT                                  
              PERFORM 6300-RECORD-TYPE-03                                       
                 THRU 6300-RECORD-TYPE-03-EXIT                                  
           END-PERFORM.                                                         
                                                                                
           PERFORM 7000-UPDATE-TETTXHD                                          
              THRU 7000-UPDATE-TETTXHD-EXIT.                                    
                                                                                
       2000-CREATE-EXTRACT-FILE-EXIT.                                           
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *  THE TETTXDS TABLE CONTAINS ROWS FOR THE 864 TEXT MESSAGE IF  *         
      *  VENDOR NAMES HAVE BEEN CHOSEN FROM THE DROP-DOWN BOX ON THE  *         
      *  LOTUS NOTES FORM.  EACH VENDOR NAME, AND THE CORRESPONDING   *         
      *  ENT-ID, IS LOADED INTO THE VENDOR TABLE.                     *         
      *****************************************************************         
                                                                                
       3000-GET-VENDOR-LIST.                                                    
                                                                                
           EXEC SQL                                                             
              OPEN ETTXDS_CURSOR                                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE '3000-GET-VENDOR-LIST' TO PV-PARAGRAPH-NAME                  
              MOVE 'OPEN ETTXDS CURSOR' TO PV-DB2-OPER-ATTEMPTED                
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
           MOVE 'N' TO END-OF-ETTXDS-SW.                                        
                                                                                
           PERFORM 3100-PROCESS-VENDOR-LIST                                     
              THRU 3100-PROCESS-VENDOR-LIST-EXIT                                
                 UNTIL END-OF-ETTXDS.                                           
                                                                                
           EXEC SQL                                                             
              CLOSE ETTXDS_CURSOR                                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE '3000-GET-VENDOR-LIST' TO PV-PARAGRAPH-NAME                  
              MOVE 'CLOSE ETTXDS CURSOR' TO PV-DB2-OPER-ATTEMPTED               
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
       3000-GET-VENDOR-LIST-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       3100-PROCESS-VENDOR-LIST.                                                
                                                                                
           EXEC SQL                                                             
              FETCH ETTXDS_CURSOR                                               
              INTO :ETTXDS-ENT-ID,                                              
                   :ETTXDS-VND-NM                                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
              ADD 1 TO SUB                                                      
              MOVE ETTXDS-ENT-ID TO WS-TBL-ENT-ID (SUB)                         
              MOVE ETTXDS-VND-NM TO WS-TBL-VENDOR (SUB)                         
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 MOVE 'Y' TO END-OF-ETTXDS-SW                                   
              ELSE                                                              
                 MOVE '3100-PROCESS-VENDOR-LIST' TO PV-PARAGRAPH-NAME           
                 MOVE 'FETCH ETTXDS CURSOR' TO PV-DB2-OPER-ATTEMPTED            
                 PERFORM 9999-SQL-ERROR.                                        
                                                                                
       3100-PROCESS-VENDOR-LIST-EXIT.                                           
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *  IF DOCUMENT TYPE OR TEST/PROD STATUS HAS BEEN CHOSEN ON THE  *         
      *  LOTUS NOTES FORM, LOAD THE VENDOR-TABLE WITH THE ENT-ID'S    *         
      *  OF THE VENDORS THAT MEET THE CRITERIA SELECTED. LOAD ONLY    *         
      *  VENDORS THAT HAVE A PRODUCTION 864 RELATIONSHIP SET UP.      *         
      *****************************************************************         
                                                                                
       4000-GET-VENDOR-TYPE.                                                    
                                                                                
           EXEC SQL                                                             
              OPEN VENDOR_CURSOR                                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE '4000-GET-VENDOR-TYPE' TO PV-PARAGRAPH-NAME                  
              MOVE 'OPEN VENDOR CURSOR' TO PV-DB2-OPER-ATTEMPTED                
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
           MOVE 'N' TO END-OF-VENDOR-SW.                                        
                                                                                
           PERFORM 4100-PROCESS-VENDOR-TYPE                                     
              THRU 4100-PROCESS-VENDOR-TYPE-EXIT                                
                 UNTIL END-OF-VENDOR.                                           
                                                                                
           IF VENDOR-SELECTED AND TXT-MSG-RELATIONSHIP                          
              ADD 1 TO SUB                                                      
              MOVE PREV-ENT-ID TO WS-TBL-ENT-ID (SUB).                          
                                                                                
           EXEC SQL                                                             
              CLOSE VENDOR_CURSOR                                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE '4000-GET-VENDOR-TYPE' TO PV-PARAGRAPH-NAME                  
              MOVE 'CLOSE VENDOR CURSOR' TO PV-DB2-OPER-ATTEMPTED               
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
       4000-GET-VENDOR-TYPE-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       4100-PROCESS-VENDOR-TYPE.                                                
                                                                                
           EXEC SQL                                                             
              FETCH VENDOR_CURSOR                                               
              INTO :TRDPRT-TP-DKEY,                                             
                   :TRDPRT-ENT-ID,                                              
                   :TRDTRN-TRAN-SET-CDE,                                        
                   :TRDTRN-PROD-TEST-IND                                        
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
              CONTINUE                                                          
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 MOVE 'Y' TO END-OF-VENDOR-SW                                   
                 GO TO 4100-PROCESS-VENDOR-TYPE-EXIT                            
              ELSE                                                              
                 MOVE '4100-PROCESS-VENDOR-TYPE' TO PV-PARAGRAPH-NAME           
                 MOVE 'FETCH VENDOR CURSOR' TO PV-DB2-OPER-ATTEMPTED            
                 PERFORM 9999-SQL-ERROR.                                        
                                                                                
           IF TRDPRT-ENT-ID NOT = PREV-ENT-ID                                   
              IF VENDOR-SELECTED AND TXT-MSG-RELATIONSHIP                       
                 ADD 1 TO SUB                                                   
                 MOVE PREV-ENT-ID TO WS-TBL-ENT-ID (SUB)                        
              END-IF                                                            
              MOVE TRDPRT-ENT-ID TO PREV-ENT-ID                                 
              MOVE 'N' TO VENDOR-SELECTED-SW                                    
              MOVE 'N' TO TXT-MSG-RELATIONSHIP-SW                               
           END-IF.                                                              
                                                                                
           IF TRDPRT-TP-DKEY NOT = PREV-TP-DKEY                                 
              MOVE TRDPRT-TP-DKEY TO PREV-TP-DKEY                               
              PERFORM 4200-CHECK-864-STATUS                                     
                 THRU 4200-CHECK-864-STATUS-EXIT.                               
                                                                                
           IF (ETTXHD-TXT-MSG-210-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'Y' AND                                 
               TRDTRN-TRAN-SET-CDE = '210' AND                                  
               TRDTRN-PROD-TEST-IND = 'T')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-210-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'Y' AND                                
               TRDTRN-TRAN-SET-CDE = '210' AND                                  
               TRDTRN-PROD-TEST-IND = 'P')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-210-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'N' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'N' AND                                
               TRDTRN-TRAN-SET-CDE = '210')                                     
                  SET VENDOR-SELECTED TO TRUE.                                  
                                                                                
           IF (ETTXHD-TXT-MSG-214-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'Y' AND                                 
               TRDTRN-TRAN-SET-CDE = '214' AND                                  
               TRDTRN-PROD-TEST-IND = 'T')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-214-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'Y' AND                                
               TRDTRN-TRAN-SET-CDE = '214' AND                                  
               TRDTRN-PROD-TEST-IND = 'P')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-214-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'N' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'N' AND                                
               TRDTRN-TRAN-SET-CDE = '214')                                     
                  SET VENDOR-SELECTED TO TRUE.                                  
                                                                                
           IF (ETTXHD-TXT-MSG-810-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'Y' AND                                 
               TRDTRN-TRAN-SET-CDE = '810' AND                                  
               TRDTRN-PROD-TEST-IND = 'T')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-810-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'Y' AND                                
               TRDTRN-TRAN-SET-CDE = '810' AND                                  
               TRDTRN-PROD-TEST-IND = 'P')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-810-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'N' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'N' AND                                
               TRDTRN-TRAN-SET-CDE = '810')                                     
                  SET VENDOR-SELECTED TO TRUE.                                  
                                                                                
JF0602     IF (ETTXHD-TXT-MSG-816-IND = 'Y' AND                                 
JF0602         ETTXHD-TXT-MSG-TST-IND = 'Y' AND                                 
JF0602         TRDTRN-TRAN-SET-CDE = '816' AND                                  
JF0602         TRDTRN-PROD-TEST-IND = 'T')                                      
JF0602                   OR                                                     
JF0602        (ETTXHD-TXT-MSG-816-IND = 'Y' AND                                 
JF0602         ETTXHD-TXT-MSG-PROD-IND = 'Y' AND                                
JF0602         TRDTRN-TRAN-SET-CDE = '816' AND                                  
JF0602         TRDTRN-PROD-TEST-IND = 'P')                                      
JF0602                   OR                                                     
JF0602        (ETTXHD-TXT-MSG-816-IND = 'Y' AND                                 
JF0602         ETTXHD-TXT-MSG-TST-IND = 'N' AND                                 
JF0602         ETTXHD-TXT-MSG-PROD-IND = 'N' AND                                
JF0602         TRDTRN-TRAN-SET-CDE = '816')                                     
JF0602            SET VENDOR-SELECTED TO TRUE.                                  
                                                                                
WG0310     IF (ETTXHD-TXT-MSG-310-IND = 'Y' AND                                 
WG0310         ETTXHD-TXT-MSG-TST-IND = 'Y' AND                                 
WG0310         TRDTRN-TRAN-SET-CDE = '310' AND                                  
WG0310         TRDTRN-PROD-TEST-IND = 'T')                                      
WG0310                   OR                                                     
WG0310        (ETTXHD-TXT-MSG-310-IND = 'Y' AND                                 
WG0310         ETTXHD-TXT-MSG-PROD-IND = 'Y' AND                                
WG0310         TRDTRN-TRAN-SET-CDE = '310' AND                                  
WG0310         TRDTRN-PROD-TEST-IND = 'P')                                      
WG0310                   OR                                                     
WG0310        (ETTXHD-TXT-MSG-310-IND = 'Y' AND                                 
WG0310         ETTXHD-TXT-MSG-TST-IND = 'N' AND                                 
WG0310         ETTXHD-TXT-MSG-PROD-IND = 'N' AND                                
WG0310         TRDTRN-TRAN-SET-CDE = '310')                                     
WG0310            SET VENDOR-SELECTED TO TRUE.                                  
                                                                                
WG0310     IF (ETTXHD-TXT-MSG-860-IND = 'Y' AND                                 
WG0310         ETTXHD-TXT-MSG-TST-IND = 'Y' AND                                 
WG0310         TRDTRN-TRAN-SET-CDE = '860' AND                                  
WG0310         TRDTRN-PROD-TEST-IND = 'T')                                      
WG0310                   OR                                                     
WG0310        (ETTXHD-TXT-MSG-860-IND = 'Y' AND                                 
WG0310         ETTXHD-TXT-MSG-PROD-IND = 'Y' AND                                
WG0310         TRDTRN-TRAN-SET-CDE = '860' AND                                  
WG0310         TRDTRN-PROD-TEST-IND = 'P')                                      
WG0310                   OR                                                     
WG0310        (ETTXHD-TXT-MSG-860-IND = 'Y' AND                                 
WG0310         ETTXHD-TXT-MSG-TST-IND = 'N' AND                                 
WG0310         ETTXHD-TXT-MSG-PROD-IND = 'N' AND                                
WG0310         TRDTRN-TRAN-SET-CDE = '860')                                     
WG0310            SET VENDOR-SELECTED TO TRUE.                                  
                                                                                
           IF (ETTXHD-TXT-MSG-820-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'Y' AND                                 
               TRDTRN-TRAN-SET-CDE = '820' AND                                  
               TRDTRN-PROD-TEST-IND = 'T')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-820-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'Y' AND                                
               TRDTRN-TRAN-SET-CDE = '820' AND                                  
               TRDTRN-PROD-TEST-IND = 'P')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-820-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'N' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'N' AND                                
               TRDTRN-TRAN-SET-CDE = '820')                                     
                  SET VENDOR-SELECTED TO TRUE.                                  
                                                                                
           IF (ETTXHD-TXT-MSG-855-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'Y' AND                                 
               TRDTRN-TRAN-SET-CDE = '855' AND                                  
               TRDTRN-PROD-TEST-IND = 'T')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-855-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'Y' AND                                
               TRDTRN-TRAN-SET-CDE = '855' AND                                  
               TRDTRN-PROD-TEST-IND = 'P')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-855-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'N' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'N' AND                                
               TRDTRN-TRAN-SET-CDE = '855')                                     
                  SET VENDOR-SELECTED TO TRUE.                                  
                                                                                
           IF (ETTXHD-TXT-MSG-856-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'Y' AND                                 
               TRDTRN-TRAN-SET-CDE = '856' AND                                  
               TRDTRN-PROD-TEST-IND = 'T')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-856-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'Y' AND                                
               TRDTRN-TRAN-SET-CDE = '856' AND                                  
               TRDTRN-PROD-TEST-IND = 'P')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-856-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'N' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'N' AND                                
               TRDTRN-TRAN-SET-CDE = '856')                                     
                  SET VENDOR-SELECTED TO TRUE.                                  
                                                                                
           IF (ETTXHD-TXT-MSG-852-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'Y' AND                                 
               TRDTRN-TRAN-SET-CDE = '852' AND                                  
               TRDTRN-PROD-TEST-IND = 'T')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-852-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'Y' AND                                
               TRDTRN-TRAN-SET-CDE = '852' AND                                  
               TRDTRN-PROD-TEST-IND = 'P')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-852-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'N' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'N' AND                                
               TRDTRN-TRAN-SET-CDE = '852')                                     
                  SET VENDOR-SELECTED TO TRUE.                                  
                                                                                
           IF (ETTXHD-TXT-MSG-850-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'Y' AND                                 
               TRDTRN-TRAN-SET-CDE = '850' AND                                  
               TRDTRN-PROD-TEST-IND = 'T')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-850-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'Y' AND                                
               TRDTRN-TRAN-SET-CDE = '850' AND                                  
               TRDTRN-PROD-TEST-IND = 'P')                                      
                         OR                                                     
              (ETTXHD-TXT-MSG-850-IND = 'Y' AND                                 
               ETTXHD-TXT-MSG-TST-IND = 'N' AND                                 
               ETTXHD-TXT-MSG-PROD-IND = 'N' AND                                
               TRDTRN-TRAN-SET-CDE = '850')                                     
                  SET VENDOR-SELECTED TO TRUE.                                  
                                                                                
           IF ETTXHD-TXT-MSG-210-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-214-IND = 'N' AND                                  
WG0310        ETTXHD-TXT-MSG-310-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-810-IND = 'N' AND                                  
JF0602        ETTXHD-TXT-MSG-816-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-820-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-855-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-856-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-852-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-850-IND = 'N' AND                                  
WG0310        ETTXHD-TXT-MSG-860-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-TST-IND = 'Y' AND                                  
              TRDTRN-PROD-TEST-IND = 'T'                                        
                 SET VENDOR-SELECTED TO TRUE.                                   
                                                                                
           IF ETTXHD-TXT-MSG-210-IND = 'N' AND                                  
WG0310        ETTXHD-TXT-MSG-310-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-214-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-810-IND = 'N' AND                                  
JF0602        ETTXHD-TXT-MSG-816-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-820-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-855-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-856-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-852-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-850-IND = 'N' AND                                  
WG0310        ETTXHD-TXT-MSG-860-IND = 'N' AND                                  
              ETTXHD-TXT-MSG-PROD-IND = 'Y' AND                                 
              TRDTRN-PROD-TEST-IND = 'P'                                        
                 SET VENDOR-SELECTED TO TRUE.                                   
                                                                                
       4100-PROCESS-VENDOR-TYPE-EXIT.                                           
           EXIT.                                                                
                                                                                
                                                                                
       4200-CHECK-864-STATUS.                                                   
                                                                                
           EXEC SQL                                                             
              SELECT TP_DKEY                                                    
              INTO   :TRDTRN-TP-DKEY                                            
              FROM   TTRDTRN                                                    
              WHERE  TP_DKEY = :TRDPRT-TP-DKEY                                  
                AND  TRAN_SET_CDE = '864'                                       
                AND  PROD_TEST_IND = 'P'                                        
                AND  DIR_IND = 'S'                                              
                AND  INACTV_DTE = '9999-09-09'                                  
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
              MOVE 'Y' TO TXT-MSG-RELATIONSHIP-SW                               
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE '4200-CHECK-864-STATUS' TO PV-PARAGRAPH-NAME              
                 MOVE 'SELECT 864 RELATIONSHP' TO PV-DB2-OPER-ATTEMPTED         
                 PERFORM 9999-SQL-ERROR.                                        
                                                                                
       4200-CHECK-864-STATUS-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *  IF "ALL VENDORS" WAS CHOSEN FROM THE DROP-DOWN BOX ON THE    *         
      *  LOTUS NOTES FORM, LOAD THE VENDOR-TABLE WITH THE ENT-ID'S    *         
      *  OF ALL VENDORS THAT HAVE A PRODUCTION 864 RELATIONSHIP       *         
      *  SET UP.                                                      *         
      *****************************************************************         
                                                                                
       5000-GET-ALL-VENDORS.                                                    
                                                                                
           EXEC SQL                                                             
              OPEN ALLVND_CURSOR                                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE '5000-GET-ALL-VENDORS' TO PV-PARAGRAPH-NAME                  
              MOVE 'OPEN ALLVND CURSOR' TO PV-DB2-OPER-ATTEMPTED                
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
           INITIALIZE VENDOR-TABLE.                                             
           MOVE 0 TO SUB.                                                       
                                                                                
           MOVE 'N' TO END-OF-ALLVND-SW.                                        
                                                                                
           PERFORM 5100-PROCESS-ALL-VENDORS                                     
              THRU 5100-PROCESS-ALL-VENDORS-EXIT                                
                 UNTIL END-OF-ALLVND.                                           
                                                                                
           EXEC SQL                                                             
              CLOSE ALLVND_CURSOR                                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE '5000-GET-ALL-VENDORS' TO PV-PARAGRAPH-NAME                  
              MOVE 'CLOSE ALLVND CURSOR' TO PV-DB2-OPER-ATTEMPTED               
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
       5000-GET-ALL-VENDORS-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       5100-PROCESS-ALL-VENDORS.                                                
                                                                                
           EXEC SQL                                                             
              FETCH ALLVND_CURSOR                                               
              INTO :TRDPRT-ENT-ID                                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
              CONTINUE                                                          
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 MOVE 'Y' TO END-OF-ALLVND-SW                                   
                 GO TO 5100-PROCESS-ALL-VENDORS-EXIT                            
              ELSE                                                              
                 MOVE '5100-PROCESS-ALL-VENDORS' TO PV-PARAGRAPH-NAME           
                 MOVE 'FETCH ALLVND CURSOR' TO PV-DB2-OPER-ATTEMPTED            
                 PERFORM 9999-SQL-ERROR.                                        
                                                                                
           IF TRDPRT-ENT-ID NOT = PREV-ENT-ID                                   
              MOVE TRDPRT-ENT-ID TO PREV-ENT-ID                                 
              ADD 1 TO SUB                                                      
              MOVE TRDPRT-ENT-ID TO WS-TBL-ENT-ID (SUB).                        
                                                                                
       5100-PROCESS-ALL-VENDORS-EXIT.                                           
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *  CREATE RECORDS ON THE EXTRACT FILE FOR EACH OF THE ENT-ID'S  *         
      *  ON THE VENDOR-TABLE.  RECORD TYPE 03 WILL OCCUR MULTIPLE     *         
      *  TIMES DEPENDING ON HOW MANY LINES ARE IN THE 864 MESSAGE.    *         
      *  CHECK EACH VENDOR FOR MULTIPLE ENT-ID'S.                     *         
      *****************************************************************         
                                                                                
       6000-MULT-ENT-ID.                                                        
                                                                                
           MOVE WS-TBL-ENT-ID (SUB) TO WS-ENT-ID.                               
                                                                                
           EXEC SQL                                                             
              SELECT C.PRNT_ENT_ID                                              
              INTO   :TRDMLT-PRNT-ENT-ID                                        
              FROM   TTRDPRT A,                                                 
                     TTRDTRN B,                                                 
                     TTRDMLT C                                                  
              WHERE  A.ENT_ID = :WS-ENT-ID                                      
                AND  A.INACTV_DTE = '9999-09-09'                                
                AND  B.TP_DKEY = A.TP_DKEY                                      
                AND  B.TRAN_SET_CDE = '864'                                     
                AND  B.PROD_TEST_IND = 'P'                                      
                AND  B.INACTV_DTE = '9999-09-09'                                
                AND  C.VND_INCHG_ID = A.VEND_INCHG_ID                           
                AND  C.VND_INCHG_ID_DESC = A.VEND_INCHG_ID_DESC                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
              MOVE TRDMLT-PRNT-ENT-ID TO WS-ENT-ID-10                           
           ELSE                                                                 
              MOVE WS-ENT-ID TO WS-ENT-ID-10.                                   
                                                                                
       6000-MULT-ENT-ID-EXIT.                                                   
           EXIT.                                                                
                                                                                
                                                                                
       6100-RECORD-TYPE-01.                                                     
                                                                                
           INITIALIZE ET109-EDI-EXTRACT-RECORD.                                 
                                                                                
           MOVE '01'         TO ET109-RECORD-TYPE-IND.                          
           MOVE WS-ENT-ID-10 TO ET109-ENT-ID.                                   
           MOVE 'TX'         TO ET109-MSG-TYPE.                                 
           MOVE ZERO         TO ET109-ID-SEQ-NBR.                               
           MOVE ZERO         TO ET109-MSG-SEQ-NBR.                              
                                                                                
           MOVE ETTXHD-TXT-MSG-DKEY-ID TO WS-EXTRACT-MSG-NUMBER.                
                                                                                
           MOVE ET109-EDI-EXTRACT-RECORD TO WS-EXTRACT-ETRD109.                 
           WRITE OUTPUT-RECORD FROM WS-EXTRACT-RECORD.                          
                                                                                
       6100-RECORD-TYPE-01-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       6200-RECORD-TYPE-02.                                                     
                                                                                
           INITIALIZE ET109-EDI-EXTRACT-RECORD.                                 
                                                                                
           MOVE '02'         TO ET109-RECORD-TYPE-IND.                          
           MOVE WS-ENT-ID-10 TO ET109-ENT-ID.                                   
           MOVE 'TX'         TO ET109-MSG-TYPE.                                 
           MOVE 1            TO ET109-ID-SEQ-NBR.                               
           MOVE ZERO         TO ET109-MSG-SEQ-NBR.                              
                                                                                
           MOVE ETTXHD-TXT-MSG-DKEY-ID TO ET109-MSG-ID-NBR.                     
           MOVE ETTXHD-TXT-MSG-DKEY-ID TO WS-EXTRACT-MSG-NUMBER.                
                                                                                
           MOVE ETTXHD-TXT-MSG-SBJCT-DESC TO ET109-SUBJECT.                     
           INSPECT ET109-SUBJECT REPLACING ALL X'1A' BY X'7D'.                  
           INSPECT ET109-SUBJECT REPLACING ALL X'0D25' BY X'4040'.              
           MOVE ETTXHD-TXT-MSG-CNTCT-NM   TO ET109-CONTACT.                     
                                                                                
           MOVE ETTXHD-FAX-AREA-CDE-NBR TO WS-AREA-CDE-NBR.                     
           MOVE '-'                     TO WS-DASH-1.                           
           MOVE ETTXHD-FAX-EXCH-NBR     TO WS-EXCH-NBR.                         
           MOVE '-'                     TO WS-DASH-2.                           
           MOVE ETTXHD-FAX-LAST-4-NBR   TO WS-LAST-4-NBR.                       
           MOVE WS-PHONE-NUMBER         TO ET109-FAX-NUMBER.                    
                                                                                
           MOVE ETTXHD-PHN-AREA-CDE-NBR TO WS-AREA-CDE-NBR.                     
           MOVE '-'                     TO WS-DASH-1.                           
           MOVE ETTXHD-PHN-EXCH-NBR     TO WS-EXCH-NBR.                         
           MOVE '-'                     TO WS-DASH-2.                           
           MOVE ETTXHD-PHN-LAST-4-NBR   TO WS-LAST-4-NBR.                       
           MOVE WS-PHONE-NUMBER         TO ET109-PHONE-NUMBER.                  
                                                                                
           MOVE ET109-EDI-EXTRACT-RECORD TO WS-EXTRACT-ETRD109.                 
           WRITE OUTPUT-RECORD FROM WS-EXTRACT-RECORD.                          
                                                                                
       6200-RECORD-TYPE-02-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       6300-RECORD-TYPE-03.                                                     
                                                                                
           INITIALIZE ET109-EDI-EXTRACT-RECORD.                                 
                                                                                
           MOVE '03'         TO ET109-RECORD-TYPE-IND.                          
           MOVE WS-ENT-ID-10 TO ET109-ENT-ID.                                   
           MOVE 'TX'         TO ET109-MSG-TYPE.                                 
           MOVE 1            TO ET109-ID-SEQ-NBR.                               
                                                                                
           EXEC SQL                                                             
              OPEN ETTXDT_CURSOR                                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE '6300-RECORD-TYPE-03' TO PV-PARAGRAPH-NAME                   
              MOVE 'OPEN ETTXDT CURSOR' TO PV-DB2-OPER-ATTEMPTED                
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
           MOVE 'N' TO END-OF-ETTXDT-SW.                                        
                                                                                
           PERFORM 6400-GET-TEXT-MESSAGE                                        
              THRU 6400-GET-TEXT-MESSAGE-EXIT                                   
                 UNTIL END-OF-ETTXDT.                                           
                                                                                
           EXEC SQL                                                             
              CLOSE ETTXDT_CURSOR                                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE '6300-RECORD-TYPE-03' TO PV-PARAGRAPH-NAME                   
              MOVE 'CLOSE ETTXDT CURSOR' TO PV-DB2-OPER-ATTEMPTED               
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
       6300-RECORD-TYPE-03-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       6400-GET-TEXT-MESSAGE.                                                   
                                                                                
           EXEC SQL                                                             
              FETCH ETTXDT_CURSOR                                               
              INTO :ETTXDT-DTL-LNE-NBR,                                         
                   :ETTXDT-DTL-MSG-TXT                                          
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
             MOVE ETTXDT-DTL-LNE-NBR TO ET109-MSG-SEQ-NBR                       
             MOVE ETTXDT-DTL-MSG-TXT TO ET109-DTL-MSG-TEXT                      
             INSPECT ET109-DTL-MSG-TEXT REPLACING ALL X'1A' BY X'7D'            
             INSPECT ET109-DTL-MSG-TEXT REPLACING ALL X'0D25' BY X'4040'        
             MOVE ETTXHD-TXT-MSG-DKEY-ID TO WS-EXTRACT-MSG-NUMBER               
             MOVE ET109-EDI-EXTRACT-RECORD TO WS-EXTRACT-ETRD109                
             WRITE OUTPUT-RECORD FROM WS-EXTRACT-RECORD                         
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 MOVE 'Y' TO END-OF-ETTXDT-SW                                   
              ELSE                                                              
                 MOVE '6400-GET-TEXT-MESSAGE' TO PV-PARAGRAPH-NAME              
                 MOVE 'FETCH ETTXDT CURSOR' TO PV-DB2-OPER-ATTEMPTED            
                 PERFORM 9999-SQL-ERROR.                                        
                                                                                
       6400-GET-TEXT-MESSAGE-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *  UPDATE TXT_MSG_SNT_IND ON THE TETTXHD TABLE TO INDICATE      *         
      *  THAT THIS 864 TEXT MESSAGE HAS BEEN SENT.                    *         
      *****************************************************************         
                                                                                
       7000-UPDATE-TETTXHD.                                                     
                                                                                
           EXEC SQL                                                             
              UPDATE TETTXHD                                                    
              SET    TXT_MSG_SNT_IND = 'Y'                                      
              WHERE  TXT_MSG_DKEY_ID =: ETTXHD-TXT-MSG-DKEY-ID                  
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
              CONTINUE                                                          
           ELSE                                                                 
              MOVE '7000-UPDATE-TETTXHD' TO PV-PARAGRAPH-NAME                   
              MOVE 'UPDATE TETTXHD TABLE' TO PV-DB2-OPER-ATTEMPTED              
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
       7000-UPDATE-TETTXHD-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       9999-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
