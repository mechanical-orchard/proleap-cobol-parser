       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    SDKUT177.                                         00030000
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  11/29/12.                                         00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *  EXTRACT OF TRUCK ACTUAL DELIVERIES FOR EDW                     00090100
      *  FROM DB2 TABLES SDT_STR_RCVG & SDT_STR_RCVG_MDSE               00090200
      ******************************************************************00251000
                                                                        00260000
       ENVIRONMENT DIVISION.                                            00270000
       CONFIGURATION SECTION.                                           00280000
                                                                        00290000
       SOURCE-COMPUTER.  IBM-3090.                                      00300000
       OBJECT-COMPUTER.  IBM-3090.                                      00310000
                                                                        00320000
       INPUT-OUTPUT SECTION.                                            00330000
       FILE-CONTROL.                                                    00340000
                                                                        00350000
           SELECT EXTRACT-FILE ASSIGN TO OUT01.                         00360000
                                                                        00372000
       DATA DIVISION.                                                   00380000
       FILE SECTION.                                                    00390000
                                                                        00400000
       FD  EXTRACT-FILE                                                 00410000
           RECORDING MODE IS F                                          00420000
           LABEL RECORDS ARE STANDARD                                   00430000
           RECORD CONTAINS 120 CHARACTERS                               00442004
           BLOCK CONTAINS 0 RECORDS                                     00450000
           DATA RECORD IS EXTRACT-RECORD.                               00460000
                                                                        00470000
       01  EXTRACT-RECORD                  PIC X(120).                  00482004
                                                                        00482100
      ******************************************************************00500000
      *                     WORKING STORAGE                             00510000
      ******************************************************************00520000
       WORKING-STORAGE SECTION.                                         00530000
                                                                        00540000
      ******************************************************************00580000
      * PS - PROGRAM SWITCHES                                           00590000
      ******************************************************************00600000
       01  PS-PROGRAM-SWITCHES.                                         00610000
           05  PS-END-OF-CSR-SW             PIC X         VALUE SPACE.  00620000
               88  PS-END-OF-CSR                          VALUE 'Y'.    00630000
               88  PS-NOT-END-OF-CSR                      VALUE 'N'.    00640000
           05  PS-VALID-WEEK-END-DTE-SW     PIC X         VALUE SPACE.  00641000
               88  PS-VALID-WEEK-END-DTE                  VALUE 'Y'.    00642000
               88  PS-INVALID-WEEK-END-DTE                VALUE 'N'.    00643000
           05  PS-FOUND-ALL-VALID-WK-ENDS-SW PIC X         VALUE SPACE. 00644000
               88  PS-FOUND-ALL-VALID-WK-ENDS             VALUE 'Y'.    00645000
               88  PS-PROCESSING-VALID-WK-ENDS            VALUE 'N'.    00646000
                                                                        00650000
      ******************************************************************00660000
      * PC - PROGRAM CONSTANTS                                          00670000
      ******************************************************************00680000
       01  PC-PROGRAM-CONSTANTS.                                        00690000
           05  PC-PROGRAM-NAME             PIC X(08) VALUE 'SDKUT177'.  00700000
           05  PC-FIELD-DELIMITER          PIC X(01) VALUE '|'.         00820000
           05  PC-SQL-MAX-RETRIES          PIC 9(01) VALUE 5.           00821005
                                                                        00830000
      ******************************************************************00840000
      * PV - PROGRAM VARIABLES                                          00850000
      ******************************************************************00860000
       01  PV-PROGRAM-VARIABLES.                                        00870000
           05  PV-CURRENT-DATE             PIC X(10) VALUE SPACES.      02090000
                                                                        02091000
         05  PV-SQL-RETRY-LOGIC.                                        02092005
             10  PV-SQL-SUB                PIC  9(01) VALUE ZERO.       02093005
                                                                        02094005
         05  PV-ABEND-VARIABLES.                                        02100000
           10  PV-PARAGRAPH-NAME              PIC X(30)  VALUE SPACES.  02110000
           10  PV-OPERATION-MSG               PIC X(40)  VALUE SPACES.  02120000
           10  PV-MSG-TEXT                    PIC X(40)  VALUE SPACES.  02130000
           10  PV-DB2-OPERATION               PIC X(30)  VALUE SPACES.  02140000
                                                                        02290000
      ******************************************************************02300000
      * PA - PROGRAM ACCUMULATORS (COUNTERS)                            02310000
      ******************************************************************02320000
       01  PA-PROGRAM-ACCUMULATORS.                                     02330000
           05  PA-FETCH-COUNT              PIC 9(07) VALUE ZEROES.      02340000
           05  PA-WRITE-COUNT              PIC 9(07) VALUE ZEROES.      02341000
           05  PA-SKIP-COUNT               PIC 9(07) VALUE ZEROES.      02342000
                                                                        02350000
      ******************************************************************02360000
      *               SYSTEM TIME AND DATE VARIABLES                    02370000
      ******************************************************************02380000
       01  SYS-DATE-TIME.                                               02390000
           05  SYS-DATE                  PIC  X(08)  VALUE SPACES.      02400000
           05  SYS-TIME                  PIC  X(08)  VALUE SPACES.      02410000
                                                                        02420000
       01  ST-BEG-TIME.                                                 02430000
           05  ST-BEG-HR                 PIC  9(02)  VALUE ZEROS.       02440000
           05  FILLER                    PIC  X(01)  VALUE ':'.         02450000
           05  ST-BEG-MN                 PIC  9(02)  VALUE ZEROS.       02460000
                                                                        02470000
       01  ST-END-TIME.                                                 02480000
           05  ST-END-HR                 PIC  9(02)  VALUE ZEROS.       02490000
           05  FILLER                    PIC  X(01)  VALUE ':'.         02500000
           05  ST-END-MN                 PIC  9(02)  VALUE ZEROS.       02510000
                                                                        02520000
       01  SD-EDIT-DATE.                                                02530000
           05  SD-MONTH                  PIC  9(02)  VALUE ZEROS.       02540000
           05  FILLER                    PIC  X(01)  VALUE '/'.         02550000
           05  SD-DAY                    PIC  9(02)  VALUE ZEROS.       02560000
           05  FILLER                    PIC  X(01)  VALUE '/'.         02570000
           05  SD-CENT                   PIC  9(02)  VALUE ZEROS.       02580000
           05  SD-YEAR                   PIC  9(02)  VALUE ZEROS.       02590000
                                                                        02591000
       01  PV-TODAYS-DATE.                                              02592002
           05 PV-YEAR                    PIC 9(04) VALUE ZEROES.        02593002
           05 FILLER                     PIC X(01) VALUE '-'.           02594002
           05 PV-MONTH                   PIC 9(02) VALUE ZEROES.        02595002
           05 FILLER                     PIC X(01) VALUE '-'.           02596002
           05 PV-DAY                     PIC 9(02) VALUE ZEROES.        02597002
                                                                        02660000
       01  PV-DATE-GREG.                                                02670000
           10 PV-GREG-MONTH              PIC 9(02) VALUE ZEROES.        02680000
           10 PV-GREG-DAY                PIC 9(02) VALUE ZEROES.        02690000
           10 PV-GREG-YEAR               PIC 9(02) VALUE ZEROES.        02700000
                                                                        02710000
       01  PV-CUR-FP-1ST-DTE             PIC X(10) VALUE SPACES.        02720000
       01  PV-CUR-FP-LAST-DTE            PIC X(10) VALUE SPACES.        02721000
       01  PV-PRV-FP-1ST-DTE             PIC X(10) VALUE SPACES.        02722000
       01  PV-PRV-FP-LAST-DTE            PIC X(10) VALUE SPACES.        02723000
       01  PV-NXT-FP-1ST-DTE             PIC X(10) VALUE SPACES.        02724000
       01  PV-NXT-FP-LAST-DTE            PIC X(10) VALUE SPACES.        02725000
                                                                        02732000
       01  PV-TIME.                                                     02740000
           05 PV-HOUR                    PIC 9(02) VALUE ZEROES.        02750000
           05 PV-MINUTE                  PIC 9(02) VALUE ZEROES.        02760000
           05 PV-SEC                     PIC 9(02) VALUE ZEROES.        02770000
           05 PV-HSEC                    PIC 9(02) VALUE ZEROES.        02780000
                                                                        02790000
      ******************************************************************02910000
      * AC - ABEND ERROR CODES                                          02920000
      ******************************************************************02930000
       01  ABEND-CODE                  PIC S9(04)  VALUE +0 COMP SYNC.  02940000
         88  ABEND-4013-DB2-ERROR                        VALUE +4013.   02950000
           88  AC-DATE-ROUTINE-ERROR                 VALUE +4019.       02960000
                                                                        02990000
      ******************************************************************02991000
      * PARAMETER LIST FOR THE CALENDAR ROUTINE                         02992000
      ******************************************************************02993000
           COPY DPWS500.                                                02994000
                                                                        02995000
      ******************************************************************03000000
      * EXTRACT RECORD LAYOUT                                           03010000
      ******************************************************************03020000
           COPY SDRD177.                                                03030000
                                                                        03109000
      ******************************************************************03110000
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004         03120000
      ******************************************************************03130000
           COPY DPWS004.                                                03140000
                                                                        03150000
      ******************************************************************03160000
      * DB2 MESSAGE AREA                                                03170000
      ******************************************************************03180000
           COPY SQLCA2.                                                 03190000
                                                                        03200000
      ******************************************************************03210000
      * STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                 03220000
      ******************************************************************03230000
           EXEC SQL                                                     03240000
               INCLUDE SQLCA                                            03250000
           END-EXEC.                                                    03260000
                                                                        03270000
      ******************************************************************03280000
      * SDT_STR_RCVG TABLE                                              03290000
      ******************************************************************03300000
           EXEC SQL                                                     03301000
               INCLUDE SDT00032                                         03302000
           END-EXEC.                                                    03303000
                                                                        03303100
      ******************************************************************03303200
      * SDT_STR_RCVG_MDSE TABLE                                         03303300
      ******************************************************************03303400
           EXEC SQL                                                     03303500
               INCLUDE SDT00033                                         03303600
           END-EXEC.                                                    03303700
                                                                        03304000
      ******************************************************************03570000
      * TRUCK ACTUAL DELIVERIES - MAIN CURSOR                           03580000
      ******************************************************************03590000
           EXEC SQL                                                     03600000
             DECLARE TRCK-ACTL-DLVR-CSR CURSOR FOR                      03610000
               SELECT STR_NBR                                           03630002
                     ,TRLR_ARRVL_DTE                                    03640002
                     ,TRLR_VEH_NBR                                      03641002
                     ,TRLR_SIZE_MU                                      03651002
                 FROM SDT_STR_RCVG                                      03670002
                WHERE TRLR_ARRVL_DTE >= :PV-PRV-FP-1ST-DTE              03680002
                  AND TRLR_ARRVL_DTE < CURRENT DATE                     03693002
               ORDER BY STR_NBR, TRLR_ARRVL_DTE, TRLR_VEH_NBR           03700000
               FOR FETCH ONLY                                           03710000
               WITH UR                                                  03770000
           END-EXEC.                                                    03780000
                                                                        03809900
      ******************************************************************03810000
       PROCEDURE DIVISION.                                              03811000
      ******************************************************************03820000
      * MAIN DRIVER                                                     03830000
      ******************************************************************03840000
       0000-MAINLINE.                                                   03850000
                                                                        03860000
           PERFORM 1000-INITIALIZATION.                                 03870000
                                                                        03871000
           PERFORM 2000-EXTRACT-PROCESSING                              03880000
                   UNTIL PS-END-OF-CSR.                                 03881000
                                                                        03881100
           PERFORM 3000-WRAP-UP.                                        03881300
                                                                        03882000
           PERFORM 9999-SUMMARY.                                        03890000
                                                                        03900000
           GOBACK.                                                      03910000
                                                                        03930000
      ******************************************************************03941000
      * 1000-INITIALIZATION                                             03942000
      ******************************************************************03945000
       1000-INITIALIZATION.                                             03946000
                                                                        03947000
           DISPLAY '*************************************'.             03948000
           DISPLAY '* SDKUT177 - TRUCK ACTUAL DELIVERY  *'.             03949000
           DISPLAY '*            DB2 TABLES EXTRACT     *'.             03949100
           DISPLAY '*************************************'.             03949200
           DISPLAY '*'.                                                 03949300
           DISPLAY '* - PROCESSING...'.                                 03949400
                                                                        03949500
           OPEN OUTPUT EXTRACT-FILE.                                    03950300
                                                                        03950400
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 03950500
                                                                        03950600
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            03950700
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            03950800
                                                                        03950900
           MOVE SYS-DATE (1:2) TO SD-CENT.                              03951000
           MOVE SYS-DATE (3:2) TO SD-YEAR.                              03951100
           MOVE SYS-DATE (5:2) TO SD-MONTH.                             03951200
           MOVE SYS-DATE (7:2) TO SD-DAY.                               03951300
                                                                        03951400
           MOVE SYS-DATE (1:4)         TO PV-YEAR.                      03951502
           MOVE SYS-DATE (5:2)         TO PV-MONTH.                     03951602
           MOVE SYS-DATE (7:2)         TO PV-DAY.                       03951702
           DISPLAY '* TODAYS DATE: ' PV-TODAYS-DATE.                    03951802
                                                                        03951902
           PERFORM 1100-DETERMINE-DATES.                                03952000
                                                                        03952100
           PERFORM 2300-OPEN-TRCK-ACTL-DLVR-CSR.                        03952202
           PERFORM 2400-FETCH-TRCK-ACTL-DLVR-CSR.                       03952302
           MOVE SPACE TO SDRD177-EXTRACT-RECORD.                        03952411
                                                                        03952500
      ******************************************************************03952600
      * DETERMINE FISCAL MONTH START / END DATES                        03952700
      ******************************************************************03952800
       1100-DETERMINE-DATES.                                            03952900
                                                                        03953000
           MOVE '1100-DETERMINE-DATES' TO PV-PARAGRAPH-NAME.            03953100
           DISPLAY 'SYSTEM DATE: ' SD-EDIT-DATE.                        03953200
                                                                        03953300
      ***GET START/END DATES OF CURRENT FISCAL MONTH                    03953400
                                                                        03953500
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03953600
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                       03953700
           SET DPG52-LK-DTE-GREG-CC-FMT  TO TRUE.                       03953800
           MOVE SD-EDIT-DATE             TO DPG52-GREG-CC-DATE-FMT.     03953900
                                                                        03954000
           DISPLAY 'DPG52-GREG-CC-DATE = ' DPG52-GREG-CC-DATE-FMT.      03954100
                                                                        03954200
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                03954300
                                                   DPG52                03954400
                                                   DPG53                03954500
                                                   DPG54                03954600
                                                   DPG55                03954700
                                                   DPG56.               03954800
                                                                        03954900
           EVALUATE TRUE                                                03955000
               WHEN DPG54-NO-ERROR                                      03955100
                   MOVE DPG56-FIRST-FP-DB2-ISO-FMT TO PV-CUR-FP-1ST-DTE 03955200
                   MOVE DPG56-LAST-FP-DB2-ISO-FMT TO PV-CUR-FP-LAST-DTE 03955300
               WHEN OTHER                                               03955400
                   DISPLAY 'ERROR GETTING FISCAL MONTH START DATE'      03955500
                   PERFORM Z5000-ERROR-ROUTINE                          03955600
           END-EVALUATE.                                                03955700
                                                                        03955800
      ***GET START/END DATES OF PREVIOUS FISCAL MONTH                   03955900
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03956000
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                       03956100
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                       03956200
           MOVE PV-CUR-FP-1ST-DTE        TO DPG52-DB2-ISO-DATE-FMT.     03956300
           SET DPG51-DECREMENT-DATE      TO TRUE.                       03956400
           MOVE 1                        TO DPG51-INCR-DECR-DAYS-9.     03956500
                                                                        03956600
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                03956700
                                                   DPG52                03956800
                                                   DPG53                03956900
                                                   DPG54                03957000
                                                   DPG55                03957100
                                                   DPG56.               03957200
                                                                        03957300
           EVALUATE TRUE                                                03957400
               WHEN DPG54-NO-ERROR                                      03957500
                   MOVE DPG56-FIRST-FP-DB2-ISO-FMT TO PV-PRV-FP-1ST-DTE 03957600
               WHEN OTHER                                               03957700
                   DISPLAY 'ERROR GETTING PREV FISCAL MONTH DATES'      03957800
                   PERFORM Z5000-ERROR-ROUTINE                          03957900
           END-EVALUATE.                                                03958000
                                                                        03960100
           DISPLAY 'PREV FISCAL MONTH START DATE: ' PV-PRV-FP-1ST-DTE.  03960200
           DISPLAY '...'.                                               03960800
                                                                        04015500
      ******************************************************************04017200
      * 2000-EXTRACT-PROCESSING                                         04017300
      ******************************************************************04017400
       2000-EXTRACT-PROCESSING.                                         04017500
                                                                        04017600
      *    SET ALL KEY FIELDS AND DEFAULTS FOR QTY / DELIM FIELDS       04017705
           PERFORM 2100-MOVE-KEY-FIELDS-TO-LAYOUT.                      04018405
      *    LOOKUP QUERY TO GET TOTAL UNITS (SUM OF ALL CTG CODES)       04018805
           PERFORM 2500-QUERY-TOTAL-TRUCK-UNITS.                        04019005
      *    LOOKUP QUERY TO GET HARDLINE QTY ('HL' CTG CODE)             04019101
           PERFORM 2510-QUERY-HARDLINE-UNITS.                           04019205
      *    LOOKUP QUERY TO GET SOFTLINE QTY ('SL' CTG CODE)             04019301
           PERFORM 2520-QUERY-SOFTLINE-UNITS.                           04019405
      *    LOOKUP QUERY TO GET JEWLRY QTY ('JEW' CTG CODE)              04019501
           PERFORM 2530-QUERY-JEWELRY-UNITS.                            04019605
      *    LOOKUP QUERY TO GET HALLMARK QTY ('HAL' CTG CODE)            04019701
           PERFORM 2540-QUERY-HALLMARK-UNITS.                           04019805
      *    LOOKUP QUERY TO GET COSMETIC QTY ('COS' CTG CODE)            04019905
           PERFORM 2550-QUERY-COSMETIC-UNITS.                           04020405
                                                                        04020505
           PERFORM 2200-WRITE-EXTRACT-RECORD                            04020801
                                                                        04020901
           PERFORM 2400-FETCH-TRCK-ACTL-DLVR-CSR.                       04021002
                                                                        04021900
      ******************************************************************04022000
      * 2100-MOVE-TO-LAYOUT.                                            04022100
      ******************************************************************04022200
       2100-MOVE-KEY-FIELDS-TO-LAYOUT.                                  04022302
                                                                        04022400
           MOVE SDT00032-STR-NBR         TO SDRD177-STR-NBR.            04022500
           MOVE PC-FIELD-DELIMITER       TO SDRD177-DELIMITER-1.        04022600
           MOVE PV-TODAYS-DATE           TO SDRD177-CURRENT-DATE.       04022702
           MOVE PC-FIELD-DELIMITER       TO SDRD177-DELIMITER-2.        04022802
           MOVE SDT00032-TRLR-VEH-NBR    TO SDRD177-TRAILER-ID.         04022903
           MOVE PC-FIELD-DELIMITER       TO SDRD177-DELIMITER-3.        04023002
           MOVE SDT00032-TRLR-SIZE-MU    TO SDRD177-TRUCK-SIZE.         04023102
           MOVE PC-FIELD-DELIMITER       TO SDRD177-DELIMITER-4.        04023202
           MOVE SDT00032-TRLR-ARRVL-DTE  TO SDRD177-TRIP-DATE.          04023303
           MOVE PC-FIELD-DELIMITER       TO SDRD177-DELIMITER-5.        04023402
           MOVE ZEROES                   TO SDRD177-TRUCK-UNITS.        04023502
           MOVE PC-FIELD-DELIMITER       TO SDRD177-DELIMITER-6.        04023602
           MOVE ZEROES                   TO SDRD177-HARDLINE-QTY.       04023702
           MOVE PC-FIELD-DELIMITER       TO SDRD177-DELIMITER-7.        04023802
           MOVE ZEROES                   TO SDRD177-SOFTLINE-QTY.       04023902
           MOVE PC-FIELD-DELIMITER       TO SDRD177-DELIMITER-8.        04024002
           MOVE ZEROES                   TO SDRD177-JEWELRY-QTY.        04024102
           MOVE PC-FIELD-DELIMITER       TO SDRD177-DELIMITER-9.        04024202
           MOVE ZEROES                   TO SDRD177-HALLMARK-QTY.       04024302
           MOVE PC-FIELD-DELIMITER       TO SDRD177-DELIMITER-10.       04024402
           MOVE ZEROES                   TO SDRD177-COSMETIC-QTY.       04024502
           MOVE PC-FIELD-DELIMITER       TO SDRD177-DELIMITER-11.       04024602
           MOVE SPACE                    TO SDRD177-CHANGE-ACTN-CDE.    04024702
                                                                        04026100
      ******************************************************************04026200
      * 2200-WRITE-EXTRACT-RECORD.                                      04026300
      ******************************************************************04026400
       2200-WRITE-EXTRACT-RECORD.                                       04026500
                                                                        04026600
           WRITE EXTRACT-RECORD                                         04026700
               FROM SDRD177-EXTRACT-RECORD.                             04026800
                                                                        04026900
           ADD +1 TO PA-WRITE-COUNT.                                    04027000
                                                                        04027100
      ******************************************************************04027200
      * 2300-OPEN-TRCK-ACTL-DLVR-CSR.                                   04027302
      ******************************************************************04027400
       2300-OPEN-TRCK-ACTL-DLVR-CSR.                                    04027502
                                                                        04027600
           EXEC SQL                                                     04027700
             OPEN TRCK-ACTL-DLVR-CSR                                    04027802
           END-EXEC.                                                    04027900
                                                                        04028000
           EVALUATE TRUE                                                04028100
             WHEN SQLCODE = +0                                          04028200
               CONTINUE                                                 04028300
                                                                        04028400
             WHEN OTHER                                                 04028500
               MOVE 'OPENING SQL CURSOR    ' TO PV-DB2-OPERATION        04028600
               MOVE '2300-OPEN-TRCK-ACTL-DLVR' TO PV-PARAGRAPH-NAME     04028702
               PERFORM 9020-SQL-ABEND-ROUTINE                           04028800
           END-EVALUATE.                                                04028900
                                                                        04029000
      ******************************************************************04029100
      * 2400-FETCH-TRCK-ACTL-DLVR-CSR.                                  04029202
      ******************************************************************04029300
       2400-FETCH-TRCK-ACTL-DLVR-CSR.                                   04029402
                                                                        04029500
           INITIALIZE DCLSDT00032.                                      04029602
                                                                        04029700
           EXEC SQL                                                     04029800
             FETCH TRCK-ACTL-DLVR-CSR                                   04029902
               INTO :SDT00032-STR-NBR                                   04030002
                  , :SDT00032-TRLR-ARRVL-DTE                            04030102
                  , :SDT00032-TRLR-VEH-NBR                              04030202
                  , :SDT00032-TRLR-SIZE-MU                              04030302
           END-EXEC                                                     04030502
                                                                        04030602
           EVALUATE TRUE                                                04030702
             WHEN SQLCODE = +0                                          04030802
               ADD +1 TO PA-FETCH-COUNT                                 04030902
               SET PS-NOT-END-OF-CSR TO TRUE                            04031002
                                                                        04031102
             WHEN SQLCODE = +100                                        04031202
               SET PS-END-OF-CSR TO TRUE                                04031302
                                                                        04031402
             WHEN OTHER                                                 04031502
               MOVE 'FETCHING CSR         ' TO PV-DB2-OPERATION         04031602
               MOVE '2400-                ' TO PV-PARAGRAPH-NAME        04031702
               PERFORM 9020-SQL-ABEND-ROUTINE                           04031802
           END-EVALUATE.                                                04031902
                                                                        04032002
      *----------------------------------------------------------------*04032105
      *  2500-QUERY-TOTAL-TRUCK-UNITS.                                  04032205
      *  - RETRIEVE STORE LOCATION STATUS FOR THIS STYLE.               04032305
      *----------------------------------------------------------------*04032405
       2500-QUERY-TOTAL-TRUCK-UNITS.                                    04032505
                                                                        04032605
           INITIALIZE PV-SQL-SUB                                        04032705
                      SDT00033-MDSE-ITM-QTY.                            04032905
                                                                        04033005
           PERFORM                                                      04033105
              WITH TEST AFTER                                           04033205
              VARYING PV-SQL-SUB                                        04033305
              FROM 1 BY 1                                               04033405
              UNTIL (PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES              04033505
                        OR  SQLCODE = +0                                04033605
                        OR  SQLCODE = +100)                             04033705
                                                                        04033805
              EXEC SQL                                                  04033909
                 SELECT  COALESCE(SUM(M.MDSE_ITM_QTY),0)                04034009
                 INTO    :SDT00033-MDSE-ITM-QTY                         04034105
                 FROM    SDT_STR_RCVG R                                 04034205
                        ,SDT_STR_RCVG_MDSE M                            04034306
                 WHERE   R.STR_NBR        = :SDT00032-STR-NBR           04034405
                   AND   R.TRLR_ARRVL_DTE = :SDT00032-TRLR-ARRVL-DTE    04034505
                   AND   R.TRLR_VEH_NBR   = :SDT00032-TRLR-VEH-NBR      04034605
                   AND   R.STR_NBR        = M.STR_NBR                   04034707
                   AND   R.TRLR_ARRVL_DTE = M.TRLR_ARRVL_DTE            04034807
                   AND   R.TRLR_VEH_NBR   = M.TRLR_VEH_NBR              04034907
                 WITH UR                                                04035005
              END-EXEC                                                  04035105
                                                                        04035205
              EVALUATE TRUE                                             04035305
                WHEN SQLCODE = -904                                     04035405
                WHEN SQLCODE = -911                                     04035505
                WHEN SQLCODE = -913                                     04035605
                  CONTINUE                                              04035705
                WHEN SQLCODE = +0                                       04035805
                  MOVE SDT00033-MDSE-ITM-QTY TO SDRD177-TRUCK-UNITS     04035905
                WHEN SQLCODE = +100                                     04036005
                  CONTINUE                                              04036105
                WHEN OTHER                                              04036205
                  MOVE '2500-QUERY-TOTAL-TRCK-UNT' TO PV-PARAGRAPH-NAME 04036308
                  MOVE 'SELECT MDSE ITM QTY FIELD' TO PV-DB2-OPERATION  04036408
                  PERFORM 9020-SQL-ABEND-ROUTINE                        04036505
              END-EVALUATE                                              04036605
           END-PERFORM.                                                 04036705
                                                                        04036805
           IF PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES                     04036905
             MOVE '2500-QUERY-TOTAL-TRCK-UNT' TO PV-PARAGRAPH-NAME      04037008
             MOVE 'SQL MAX RETRIES EXCEEDED ' TO PV-DB2-OPERATION       04037108
             PERFORM 9020-SQL-ABEND-ROUTINE                             04037205
           END-IF.                                                      04037305
                                                                        04037405
      *----------------------------------------------------------------*04037505
      *  2510-QUERY-HARDLINE-UNITS.                                     04037605
      *  - RETRIEVE STORE LOCATION STATUS FOR THIS STYLE.               04037705
      *----------------------------------------------------------------*04037805
       2510-QUERY-HARDLINE-UNITS.                                       04037905
                                                                        04038005
           INITIALIZE PV-SQL-SUB                                        04038105
                      SDT00033-MDSE-ITM-QTY.                            04038205
                                                                        04038305
           PERFORM                                                      04038405
              WITH TEST AFTER                                           04038505
              VARYING PV-SQL-SUB                                        04038605
              FROM 1 BY 1                                               04038705
              UNTIL (PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES              04038805
                        OR  SQLCODE = +0                                04038905
                        OR  SQLCODE = +100)                             04039005
                                                                        04039105
              EXEC SQL                                                  04039205
                 SELECT  M.MDSE_ITM_QTY                                 04039306
                 INTO    :SDT00033-MDSE-ITM-QTY                         04039405
                 FROM    SDT_STR_RCVG R                                 04039505
                        ,SDT_STR_RCVG_MDSE M                            04039606
                 WHERE   R.STR_NBR        = :SDT00032-STR-NBR           04039705
                   AND   R.TRLR_ARRVL_DTE = :SDT00032-TRLR-ARRVL-DTE    04039805
                   AND   R.TRLR_VEH_NBR   = :SDT00032-TRLR-VEH-NBR      04039905
                   AND   R.STR_NBR        = M.STR_NBR                   04040007
                   AND   R.TRLR_ARRVL_DTE = M.TRLR_ARRVL_DTE            04040107
                   AND   R.TRLR_VEH_NBR   = M.TRLR_VEH_NBR              04040207
                   AND   M.MDSE_CTG_CDE   = 'HL'                        04040305
                 WITH UR                                                04040405
              END-EXEC                                                  04040505
                                                                        04040605
              EVALUATE TRUE                                             04040705
                WHEN SQLCODE = -904                                     04040805
                WHEN SQLCODE = -911                                     04040905
                WHEN SQLCODE = -913                                     04041005
                  CONTINUE                                              04041105
                WHEN SQLCODE = +0                                       04041205
                  MOVE SDT00033-MDSE-ITM-QTY TO SDRD177-HARDLINE-QTY    04041305
                WHEN SQLCODE = +100                                     04041405
                  CONTINUE                                              04041505
                WHEN OTHER                                              04041605
                  MOVE '2510-QUERY-HARDLINE-UNITS' TO PV-PARAGRAPH-NAME 04041708
                  MOVE 'SELECT MDSE ITM QTY FIELD' TO PV-DB2-OPERATION  04041808
                  PERFORM 9020-SQL-ABEND-ROUTINE                        04041905
              END-EVALUATE                                              04042005
           END-PERFORM.                                                 04042105
                                                                        04042205
           IF PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES                     04042305
             MOVE '2510-QUERY-HARDLINE-UNITS' TO PV-PARAGRAPH-NAME      04042408
             MOVE 'SQL MAX RETRIES EXCEEDED ' TO PV-DB2-OPERATION       04042508
             PERFORM 9020-SQL-ABEND-ROUTINE                             04042605
           END-IF.                                                      04042705
                                                                        04042805
      *----------------------------------------------------------------*04042905
      *  2520-QUERY-SOFTLINE-UNITS.                                     04043005
      *  - RETRIEVE STORE LOCATION STATUS FOR THIS STYLE.               04043105
      *----------------------------------------------------------------*04043205
       2520-QUERY-SOFTLINE-UNITS.                                       04043305
                                                                        04043405
           INITIALIZE PV-SQL-SUB                                        04043505
                      SDT00033-MDSE-ITM-QTY.                            04043605
                                                                        04043705
           PERFORM                                                      04043805
              WITH TEST AFTER                                           04043905
              VARYING PV-SQL-SUB                                        04044005
              FROM 1 BY 1                                               04044105
              UNTIL (PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES              04044205
                        OR  SQLCODE = +0                                04044305
                        OR  SQLCODE = +100)                             04044405
                                                                        04044505
              EXEC SQL                                                  04044605
                 SELECT  M.MDSE_ITM_QTY                                 04044706
                 INTO    :SDT00033-MDSE-ITM-QTY                         04044805
                 FROM    SDT_STR_RCVG R                                 04044905
                        ,SDT_STR_RCVG_MDSE M                            04045006
                 WHERE   R.STR_NBR        = :SDT00032-STR-NBR           04045105
                   AND   R.TRLR_ARRVL_DTE = :SDT00032-TRLR-ARRVL-DTE    04045205
                   AND   R.TRLR_VEH_NBR   = :SDT00032-TRLR-VEH-NBR      04045305
                   AND   R.STR_NBR        = M.STR_NBR                   04045407
                   AND   R.TRLR_ARRVL_DTE = M.TRLR_ARRVL_DTE            04045507
                   AND   R.TRLR_VEH_NBR   = M.TRLR_VEH_NBR              04045607
                   AND   M.MDSE_CTG_CDE   = 'SL'                        04045705
                 WITH UR                                                04045805
              END-EXEC                                                  04045905
                                                                        04046005
              EVALUATE TRUE                                             04046105
                WHEN SQLCODE = -904                                     04046205
                WHEN SQLCODE = -911                                     04046305
                WHEN SQLCODE = -913                                     04046405
                  CONTINUE                                              04046505
                WHEN SQLCODE = +0                                       04046605
                  MOVE SDT00033-MDSE-ITM-QTY TO SDRD177-SOFTLINE-QTY    04046705
                WHEN SQLCODE = +100                                     04046805
                  CONTINUE                                              04046905
                WHEN OTHER                                              04047005
                  MOVE '2520-QUERY-SOFTLINE-UNITS' TO PV-PARAGRAPH-NAME 04047108
                  MOVE 'SELECT MDSE ITM QTY FIELD' TO PV-DB2-OPERATION  04047208
                  PERFORM 9020-SQL-ABEND-ROUTINE                        04047305
              END-EVALUATE                                              04047405
           END-PERFORM.                                                 04047505
                                                                        04047605
           IF PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES                     04047705
             MOVE '2520-QUERY-SOFTLINE-UNITS'  TO PV-PARAGRAPH-NAME     04047808
             MOVE 'SQL MAX RETRIES EXCEEDED ' TO PV-DB2-OPERATION       04047908
             PERFORM 9020-SQL-ABEND-ROUTINE                             04048005
           END-IF.                                                      04048105
                                                                        04048205
      *----------------------------------------------------------------*04048305
      *  2530-QUERY-JEWELRY-UNITS.                                      04048405
      *  - RETRIEVE STORE LOCATION STATUS FOR THIS STYLE.               04048505
      *----------------------------------------------------------------*04048605
       2530-QUERY-JEWELRY-UNITS.                                        04048705
                                                                        04048805
           INITIALIZE PV-SQL-SUB                                        04048905
                      SDT00033-MDSE-ITM-QTY.                            04049005
                                                                        04049105
           PERFORM                                                      04049205
              WITH TEST AFTER                                           04049305
              VARYING PV-SQL-SUB                                        04049405
              FROM 1 BY 1                                               04049505
              UNTIL (PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES              04049605
                        OR  SQLCODE = +0                                04049705
                        OR  SQLCODE = +100)                             04049805
                                                                        04049905
              EXEC SQL                                                  04050005
                 SELECT  M.MDSE_ITM_QTY                                 04050106
                 INTO    :SDT00033-MDSE-ITM-QTY                         04050205
                 FROM    SDT_STR_RCVG R                                 04050305
                        ,SDT_STR_RCVG_MDSE M                            04050406
                 WHERE   R.STR_NBR        = :SDT00032-STR-NBR           04050505
                   AND   R.TRLR_ARRVL_DTE = :SDT00032-TRLR-ARRVL-DTE    04050605
                   AND   R.TRLR_VEH_NBR   = :SDT00032-TRLR-VEH-NBR      04050705
                   AND   R.STR_NBR        = M.STR_NBR                   04050807
                   AND   R.TRLR_ARRVL_DTE = M.TRLR_ARRVL_DTE            04050907
                   AND   R.TRLR_VEH_NBR   = M.TRLR_VEH_NBR              04051007
                   AND   M.MDSE_CTG_CDE   = 'JEW'                       04051105
                 WITH UR                                                04051205
              END-EXEC                                                  04051305
                                                                        04051405
              EVALUATE TRUE                                             04051505
                WHEN SQLCODE = -904                                     04051605
                WHEN SQLCODE = -911                                     04051705
                WHEN SQLCODE = -913                                     04051805
                  CONTINUE                                              04051905
                WHEN SQLCODE = +0                                       04052005
                  MOVE SDT00033-MDSE-ITM-QTY TO SDRD177-JEWELRY-QTY     04052105
                WHEN SQLCODE = +100                                     04052205
                  CONTINUE                                              04052305
                WHEN OTHER                                              04052405
                  MOVE '2530-QUERY-JEWELRY-UNITS'  TO PV-PARAGRAPH-NAME 04052508
                  MOVE 'SELECT MDSE ITM QTY FIELD' TO PV-DB2-OPERATION  04052608
                  PERFORM 9020-SQL-ABEND-ROUTINE                        04052705
              END-EVALUATE                                              04052805
           END-PERFORM.                                                 04052905
                                                                        04053005
           IF PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES                     04053105
             MOVE '2530-QUERY-JEWELRY-UNITS'  TO PV-PARAGRAPH-NAME      04053208
             MOVE 'SQL MAX RETRIES EXCEEDED'  TO PV-DB2-OPERATION       04053308
             PERFORM 9020-SQL-ABEND-ROUTINE                             04053405
           END-IF.                                                      04053505
                                                                        04053605
      *----------------------------------------------------------------*04053705
      *  2540-QUERY-HALLMARK-UNITS.                                     04053805
      *  - RETRIEVE STORE LOCATION STATUS FOR THIS STYLE.               04053905
      *----------------------------------------------------------------*04054005
       2540-QUERY-HALLMARK-UNITS.                                       04054105
                                                                        04054205
           INITIALIZE PV-SQL-SUB                                        04054305
                      SDT00033-MDSE-ITM-QTY.                            04054405
                                                                        04054505
           PERFORM                                                      04054605
              WITH TEST AFTER                                           04054705
              VARYING PV-SQL-SUB                                        04054805
              FROM 1 BY 1                                               04054905
              UNTIL (PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES              04055005
                        OR  SQLCODE = +0                                04055105
                        OR  SQLCODE = +100)                             04055205
                                                                        04055305
              EXEC SQL                                                  04055405
                 SELECT  M.MDSE_ITM_QTY                                 04055506
                 INTO    :SDT00033-MDSE-ITM-QTY                         04055605
                 FROM    SDT_STR_RCVG R                                 04055705
                        ,SDT_STR_RCVG_MDSE M                            04055806
                 WHERE   R.STR_NBR        = :SDT00032-STR-NBR           04055905
                   AND   R.TRLR_ARRVL_DTE = :SDT00032-TRLR-ARRVL-DTE    04056005
                   AND   R.TRLR_VEH_NBR   = :SDT00032-TRLR-VEH-NBR      04056105
                   AND   R.STR_NBR        = M.STR_NBR                   04056207
                   AND   R.TRLR_ARRVL_DTE = M.TRLR_ARRVL_DTE            04056307
                   AND   R.TRLR_VEH_NBR   = M.TRLR_VEH_NBR              04056407
                   AND   M.MDSE_CTG_CDE   = 'HAL'                       04056505
                 WITH UR                                                04056605
              END-EXEC                                                  04056705
                                                                        04056805
              EVALUATE TRUE                                             04056905
                WHEN SQLCODE = -904                                     04057005
                WHEN SQLCODE = -911                                     04057105
                WHEN SQLCODE = -913                                     04057205
                  CONTINUE                                              04057305
                WHEN SQLCODE = +0                                       04057405
                  MOVE SDT00033-MDSE-ITM-QTY TO SDRD177-HALLMARK-QTY    04057505
                WHEN SQLCODE = +100                                     04057605
                  CONTINUE                                              04057705
                WHEN OTHER                                              04057805
                  MOVE '2540-QUERY-HALLMARK-UNITS' TO PV-PARAGRAPH-NAME 04057908
                  MOVE 'SELECT MDSE ITM QTY FIELD' TO PV-DB2-OPERATION  04058008
                  PERFORM 9020-SQL-ABEND-ROUTINE                        04058105
              END-EVALUATE                                              04058205
           END-PERFORM.                                                 04058305
                                                                        04058405
           IF PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES                     04058505
             MOVE '2540-QUERY-HALLMARK-UNITS' TO PV-PARAGRAPH-NAME      04058608
             MOVE 'SQL MAX RETRIES EXCEEDED'  TO PV-DB2-OPERATION       04058708
             PERFORM 9020-SQL-ABEND-ROUTINE                             04058805
           END-IF.                                                      04058905
                                                                        04059005
      *----------------------------------------------------------------*04059105
      *  2550-QUERY-COSMETIC-UNITS.                                     04059205
      *  - RETRIEVE STORE LOCATION STATUS FOR THIS STYLE.               04059305
      *----------------------------------------------------------------*04059405
       2550-QUERY-COSMETIC-UNITS.                                       04059505
                                                                        04059605
           INITIALIZE PV-SQL-SUB                                        04059705
                      SDT00033-MDSE-ITM-QTY.                            04059805
                                                                        04059905
           PERFORM                                                      04060005
              WITH TEST AFTER                                           04060105
              VARYING PV-SQL-SUB                                        04060205
              FROM 1 BY 1                                               04060305
              UNTIL (PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES              04060405
                        OR  SQLCODE = +0                                04060505
                        OR  SQLCODE = +100)                             04060605
                                                                        04060705
              EXEC SQL                                                  04060805
                 SELECT  M.MDSE_ITM_QTY                                 04060906
                 INTO    :SDT00033-MDSE-ITM-QTY                         04061005
                 FROM    SDT_STR_RCVG R                                 04061105
                        ,SDT_STR_RCVG_MDSE M                            04061206
                 WHERE   R.STR_NBR        = :SDT00032-STR-NBR           04061305
                   AND   R.TRLR_ARRVL_DTE = :SDT00032-TRLR-ARRVL-DTE    04061405
                   AND   R.TRLR_VEH_NBR   = :SDT00032-TRLR-VEH-NBR      04061505
                   AND   R.STR_NBR        = M.STR_NBR                   04061607
                   AND   R.TRLR_ARRVL_DTE = M.TRLR_ARRVL_DTE            04061707
                   AND   R.TRLR_VEH_NBR   = M.TRLR_VEH_NBR              04061807
                   AND   M.MDSE_CTG_CDE   = 'COS'                       04061905
                 WITH UR                                                04062005
              END-EXEC                                                  04062105
                                                                        04062205
              EVALUATE TRUE                                             04062305
                WHEN SQLCODE = -904                                     04062405
                WHEN SQLCODE = -911                                     04062505
                WHEN SQLCODE = -913                                     04062605
                  CONTINUE                                              04062705
                WHEN SQLCODE = +0                                       04062805
                  MOVE SDT00033-MDSE-ITM-QTY TO SDRD177-COSMETIC-QTY    04062905
                WHEN SQLCODE = +100                                     04063005
                  CONTINUE                                              04063105
                WHEN OTHER                                              04063205
                  MOVE '2550-QUERY-COSMETIC-UNITS' TO PV-PARAGRAPH-NAME 04063308
                  MOVE 'SELECT MDSE ITM QTY FIELD' TO PV-DB2-OPERATION  04063408
                  PERFORM 9020-SQL-ABEND-ROUTINE                        04063505
              END-EVALUATE                                              04063605
           END-PERFORM.                                                 04063705
                                                                        04063805
           IF PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES                     04063905
             MOVE '2550-QUERY-COSMETIC-UNITS' TO PV-PARAGRAPH-NAME      04064008
             MOVE 'SQL MAX RETRIES EXCEEDED ' TO PV-DB2-OPERATION       04064108
             PERFORM 9020-SQL-ABEND-ROUTINE                             04064205
           END-IF.                                                      04064305
                                                                        04064408
      ******************************************************************04064505
      * 3000-WRAP-UP                                                    04064605
      ******************************************************************04064705
       3000-WRAP-UP.                                                    04064805
                                                                        04064905
           CLOSE EXTRACT-FILE.                                          04065005
           PERFORM 3100-CLOSE-TRCK-ACTL-DLVR-CSR.                       04065105
                                                                        04065205
           DISPLAY '*'.                                                 04065305
           DISPLAY '* - TRUCK ACTUAL DELIVERIES EXTRACT COMPLETED'.     04065405
                                                                        04065505
      ******************************************************************04065605
      * 3100-CLOSE-TRCK-ACTL-DLVR-CSR                                   04065705
      ******************************************************************04065805
       3100-CLOSE-TRCK-ACTL-DLVR-CSR.                                   04065905
                                                                        04066005
           EXEC SQL                                                     04066105
             CLOSE TRCK-ACTL-DLVR-CSR                                   04066205
           END-EXEC.                                                    04066305
                                                                        04066405
           EVALUATE TRUE                                                04066505
             WHEN SQLCODE = +0                                          04066605
               CONTINUE                                                 04066705
                                                                        04066805
             WHEN OTHER                                                 04066905
               MOVE 'CLOSING CSR           '  TO PV-DB2-OPERATION       04067005
               MOVE '3100-CLOSE-CSR         ' TO PV-PARAGRAPH-NAME      04067105
               PERFORM 9020-SQL-ABEND-ROUTINE                           04067205
           END-EVALUATE.                                                04067305
                                                                        04067405
      ******************************************************************04067505
      * 9020-SQL-ABEND-ROUTINE                                          04067605
      ******************************************************************04067705
       9020-SQL-ABEND-ROUTINE.                                          04067805
                                                                        04067905
           PERFORM 9999-SUMMARY.                                        04068005
                                                                        04068105
           DISPLAY '*'.                                                 04068205
           DISPLAY '** ABEND     **'.                                   04068305
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                04068405
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              04068505
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               04068605
                                                                        04068705
           COPY DPPD004.                                                04068805
           SET ABEND-4013-DB2-ERROR TO TRUE.                            04068905
           CALL 'ILBOABN0' USING ABEND-CODE.                            04069005
                                                                        04069105
      ******************************************************************04069205
      * DATE-ERROR-ROUTINE.                                             04069305
      ******************************************************************04069405
       Z5000-ERROR-ROUTINE.                                             04069505
                                                                        04069605
           PERFORM 9999-SUMMARY.                                        04069705
                                                                        04069805
           DISPLAY 'PARAGRAPH: ' PV-PARAGRAPH-NAME                      04069905
           DISPLAY 'ERROR: ' PV-OPERATION-MSG                           04070005
                                                                        04070105
           CALL 'ILBOABN0' USING ABEND-CODE.                            04070205
                                                                        04070305
      ******************************************************************04070405
      * 9999-SUMMARY.                                                   04070505
      ******************************************************************04070605
       9999-SUMMARY.                                                    04070705
                                                                        04070805
           DISPLAY '*'.                                                 04070905
           DISPLAY '*************************************'.             04071005
           DISPLAY '* PROGRAM SUMMARY                   *'.             04071105
           DISPLAY '*************************************'.             04071205
           DISPLAY '* - ROWS EXTRACTED      : ' PA-FETCH-COUNT '   *'.  04071305
           DISPLAY '* - ROWS WRITTEN TO FILE: ' PA-WRITE-COUNT '   *'.  04071405
           DISPLAY '*************************************'.             04071505
                                                                        04071605
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 04071705
                                                                        04071805
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            04071905
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            04072005
                                                                        04072105
           DISPLAY '                    '.                              04072205
           DISPLAY '**********************'.                            04072305
           DISPLAY 'RUN DATE  = ' SD-EDIT-DATE.                         04072405
           DISPLAY 'BEG TIME  = ' ST-BEG-TIME.                          04072505
           DISPLAY 'END TIME  = ' ST-END-TIME.                          04072605
           DISPLAY '======================'.                            04072705
           DISPLAY '                    '.                              04073000
      ******************************************************************04080000
