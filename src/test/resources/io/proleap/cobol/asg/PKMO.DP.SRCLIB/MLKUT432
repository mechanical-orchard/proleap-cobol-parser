       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    MLKUT432.                                         00020000
       AUTHOR.        PAUL KEBER - STRATAGEM.                           00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  04-09-99.                                         00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      *                                                                 00080000
      * MLKUT432 - CREATE THE DATA INTEGRITY EXTRACT FILE FOR TVAMCL.   00090000
      *                                                                 00100000
      * THIS PROGRAM WILL FETCH TVAMCL DB2 TABLE ROWS USING A CURSOR    00110000
      * WHICH WILL SUM AMOUNTS AT THE FISCAL MONTH END DATE, FISCAL     00120000
      * MONTH NUMBER, DEPARTMENT, AND MAJOR CLASS LEVEL.  THESE AMOUNTS 00130000
      * WILL BE USED TO CREATE THE DATA INTEGRITY EXTRACT FILE.         00131000
      *                                                                 00132000
      *                                                                 00133005
      *CHANGE HISTORY:                                                  00134005
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00135005
      * ------- ---------- ----------- -------------------------------- 00136005
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION     00137005
      ******************************************************************00140000
      *                                                                 00150000
       ENVIRONMENT DIVISION.                                            00160000
      *                                                                 00170000
       CONFIGURATION SECTION.                                           00180000
      *                                                                 00190000
       SOURCE-COMPUTER.        IBM-3090.                                00200000
       OBJECT-COMPUTER.        IBM-3090.                                00210000
      *                                                                 00220000
       INPUT-OUTPUT SECTION.                                            00230000
       FILE-CONTROL.                                                    00240000
                                                                        00250000
           SELECT DATA-INTEGRITY-CTL-FILE                               00260000
               ASSIGN TO INP01.                                         00270000
                                                                        00280000
           SELECT DATA-INTEGRITY-EXTR-FILE                              00290000
               ASSIGN TO OUT01.                                         00300000
                                                                        00310000
      ******************************************************************00320000
       DATA DIVISION.                                                   00330000
      ******************************************************************00340000
                                                                        00350000
       FILE SECTION.                                                    00360000
                                                                        00370000
       FD  DATA-INTEGRITY-CTL-FILE                                      00380000
           LABEL RECORDS ARE STANDARD                                   00390000
           RECORDING MODE IS F                                          00400000
           BLOCK CONTAINS 0 RECORDS.                                    00410000
       01  DATA-INTEGRITY-CTL-REC      PIC X(80).                       00420000
                                                                        00430000
       FD  DATA-INTEGRITY-EXTR-FILE                                     00440000
           RECORDING MODE IS F                                          00450000
           LABEL RECORDS ARE STANDARD                                   00460000
           BLOCK CONTAINS 0 RECORDS.                                    00470000
                                                                        00480000
W26603 01  DATA-INTEGRITY-EXTR-REC     PIC X(120).                      00490005
                                                                        00500000
      *                                                                 00510000
      ******************************************************************00520000
       WORKING-STORAGE SECTION.                                         00530000
      ******************************************************************00540000
                                                                        00550000
       01  PS-PROGRAM-SWITCHES.                                         00560000
           05  PS-TVAMCL-DONE          PIC X       VALUE 'N'.           00570000
               88  PS-TVAMCL-IS-DONE               VALUE 'Y'.           00580000
               88  PS-TVAMCL-IS-NOT-DONE           VALUE 'N'.           00590000
                                                                        00600000
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.              00610000
           05  PA-TVAMCL-ROWS-FETCHED  PIC S9(13)  VALUE ZERO.          00620000
           05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.          00630000
                                                                        00640000
       01  CD-COUNTER-DISPLAY.                                          00650000
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.           00660000
                                                                        00670000
       01 ABEND-AREAS.                                                  00680000
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO           00690000
                                                   COMP SYNC.           00700000
               88  AC-BAD-DATA                     VALUE +4003.         00710000
               88  AC-DB2-ERROR                    VALUE +4013.         00720000
           05  AA-ABEND-LIT            PIC X(40)   VALUE                00730000
                 '*****       ABEND'.                                   00740000
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                00750000
                   '*****   PROGRAM: MLKUT432'.                         00760000
           05  AA-PARAGRAPH-LIT.                                        00770000
               10  FILLER              PIC X(17)   VALUE                00780000
                   '***** PARAGRAPH: '.                                 00790000
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        00800000
           05  AA-MESSAGE-LINE.                                         00810000
               10  FILLER              PIC X(06)   VALUE '*****'.       00820000
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        00830000
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                00840000
                   '*****    DB2 ERROR'.                                00850000
           05  AA-DB2-OPERATION-LIT.                                    00860000
               10  FILLER              PIC X(17)   VALUE                00870000
                   '***** OPERATION: '.                                 00880000
               10  AA-DB2-OPERATION.                                    00890000
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        00900000
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        00910000
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        00920000
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        00930000
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        00940000
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        00950000
                                                                        00960000
       EJECT                                                            00970000
                                                                        00980000
      *** DATA INTEGRITY CONTROL CARD                                   00990000
           COPY MLRD110.                                                01000000
                                                                        01010000
                                                                        01020000
      *** DATA INTEGRITY TVAMCL EXTRACT FILE                            01030000
           COPY MLRD111.                                                01040000
                                                                        01050000
       EJECT                                                            01060000
                                                                        01070000
      ******************************************************************01080000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01090000
      ******************************************************************01100000
           COPY DPWS004.                                                01110000
                                                                        01120000
      ******************************************************************01130000
      *  DB2 ERRORS                                                     01140000
      ******************************************************************01150000
           COPY SQLCA2.                                                 01160000
                                                                        01170000
      ******************************************************************01180000
      *  DB2 COMMUNICATION AREA                                         01190000
      ******************************************************************01200000
           EXEC SQL                                                     01210000
                INCLUDE SQLCA                                           01220000
           END-EXEC.                                                    01230000
                                                                        01240000
      ******************************************************************01250000
      *  DB2 TVAMCL TABLE LAYOUT                                        01260000
      ******************************************************************01270000
           EXEC SQL                                                     01280000
                INCLUDE TVAMCL                                          01290000
           END-EXEC.                                                    01300000
                                                                        01310000
      ******************************************************************01320000
      *  DB2 TVAMCL CURSOR                                              01330000
      ******************************************************************01340000
           EXEC SQL                                                     01350000
               DECLARE TVAMCL_CURSOR CURSOR FOR                         01360000
                SELECT FSCL_MN_END_DTE                                  01370000
                      ,FSCL_MN_NBR                                      01380000
                      ,DEPT_NBR                                         01390000
                      ,MAJ_CL_NBR                                       01400000
                      ,SUM(SLS_REG_RTL_AMT)                             01420400
                      ,SUM(SLS_PROMO_RTL_AMT)                           01420500
                      ,SUM(SLS_PROCLR_RTL_AMT)                          01420600
                      ,SUM(SLS_CLR_RTL_AMT)                             01420700
                      ,SUM(SLS_OTH_RTL_AMT)                             01420800
                      ,SUM(MKDN_PLU_RTL_AMT)                            01420900
                      ,SUM(MKDN_RGST_RTL_AMT)                           01421002
                      ,SUM(MKDN_UMTCH_RTL_AMT)                          01421100
                      ,SUM(MKDN_BYR_RTL_AMT)                            01421200
                      ,SUM(MKDN_STR_RTL_AMT)                            01421300
                      ,SUM(MKUP_RTL_AMT)                                01421400
                      ,SUM(XFER_IN_RTL_AMT)                             01421500
                      ,SUM(XFER_OUT_RTL_AMT)                            01421600
                      ,SUM(RCPT_RTL_AMT)                                01421700
                      ,SUM(RCPT_NET_COST_AMT)                           01421800
                      ,SUM(PADJ_RTL_AMT)                                01421900
                      ,SUM(PADJ_NET_COST_AMT)                           01422000
                      ,SUM(INV_ADJ_RTL_AMT)                             01422100
                      ,SUM(SHNK_RTL_AMT)                                01422200
                      ,SUM(END_INV_RTL_AMT)                             01422304
                  FROM TVAMCL                                           01520000
                 WHERE (FSCL_MN_END_DTE >= :ML110-STRT-DTE)             01530000
                   AND (FSCL_MN_END_DTE <= :ML110-END-DTE)              01540000
                GROUP BY FSCL_MN_END_DTE,                               01550001
                         FSCL_MN_NBR,                                   01551001
                         DEPT_NBR,                                      01560001
                         MAJ_CL_NBR                                     01570001
                ORDER BY FSCL_MN_END_DTE,                               01571001
                         FSCL_MN_NBR,                                   01572001
                         DEPT_NBR,                                      01573001
                         MAJ_CL_NBR                                     01574001
W26603             WITH UR                                              01575005
           END-EXEC.                                                    01580000
                                                                        01590000
       EJECT                                                            01600000
                                                                        01610000
      ******************************************************************01620000
       PROCEDURE DIVISION.                                              01630000
      ******************************************************************01640000
                                                                        01650000
      ******************************************************************01660000
       0000-MAINLINE-MLKUT432.                                          01670000
      ******************************************************************01680000
      *                                                                 01690000
           PERFORM 1000-INITIALIZATION.                                 01700000
                                                                        01710000
           PERFORM 2000-MAIN-PROCESS.                                   01720000
                                                                        01730000
           PERFORM 8000-EOJ.                                            01740000
                                                                        01750000
           STOP RUN.                                                    01760000
                                                                        01770000
       EJECT                                                            01780000
                                                                        01790000
      ******************************************************************01800000
       1000-INITIALIZATION.                                             01810000
      ******************************************************************01820000
      *                                                                 01830000
           OPEN INPUT  DATA-INTEGRITY-CTL-FILE.                         01840000
           OPEN OUTPUT DATA-INTEGRITY-EXTR-FILE.                        01850000
                                                                        01860000
           PERFORM 4000-READ-CONTROL-CARD.                              01870000
                                                                        01880000
           DISPLAY ' '.                                                 01890000
           DISPLAY '*** MLKUT432 INPUT CONTROL CARD VALUES ***'         01900000
           DISPLAY ML110-DATA-INTEGRITY-CC-REC.                         01910000
                                                                        01920000
       EJECT                                                            01930000
                                                                        01940000
      ******************************************************************01950000
       2000-MAIN-PROCESS.                                               01960000
      ******************************************************************01970000
      *                                                                 01980000
           PERFORM 7100-OPEN-CURSOR-TVAMCL.                             01990000
                                                                        02000000
           PERFORM 7200-FETCH-TVAMCL.                                   02010000
                                                                        02020000
           PERFORM                                                      02030000
             UNTIL PS-TVAMCL-IS-DONE                                    02040000
               PERFORM 5000-WRITE-EXTRACT                               02050000
               PERFORM 7200-FETCH-TVAMCL                                02060000
           END-PERFORM.                                                 02070000
                                                                        02080000
           PERFORM 7300-CLOSE-CURSOR-TVAMCL.                            02090000
                                                                        02100000
       EJECT                                                            02110000
                                                                        02120000
      ******************************************************************02130000
       4000-READ-CONTROL-CARD.                                          02140000
      ******************************************************************02150000
      *                                                                 02160000
           READ DATA-INTEGRITY-CTL-FILE                                 02170000
             INTO ML110-DATA-INTEGRITY-CC-REC                           02180000
               AT END                                                   02190000
                   MOVE '4000-READ-CONTROL-CARD'                        02200000
                                       TO AA-PARAGRAPH-NAME             02210000
                   MOVE 'CONTROL CARD IS MISSING'                       02220000
                                       TO AA-MESSAGE                    02230000
                   MOVE +4001          TO ABEND-CODE                    02240000
                   PERFORM Z990-COMMON-ABEND-ROUTINE                    02250000
           END-READ.                                                    02260000
                                                                        02270000
       EJECT                                                            02280000
                                                                        02290000
      ******************************************************************02300000
       5000-WRITE-EXTRACT.                                              02310000
      ******************************************************************02320000
      *                                                                 02330000
           MOVE VAMCL-FSCL-MN-END-DTE  TO ML111-FSCL-MN-END-DTE.        02340000
           MOVE VAMCL-FSCL-MN-NBR      TO ML111-FSCL-MN-NBR.            02350000
           MOVE VAMCL-DEPT-NBR         TO ML111-DEPT-NBR.               02360000
           MOVE VAMCL-MAJ-CL-NBR       TO ML111-MAJ-CL-NBR.             02370000
           MOVE '00'                   TO ML111-SUB-CL-NBR.             02380000
           MOVE 0                      TO ML111-STR-NBR.                02390000
           COMPUTE ML111-SLS-RTL-AMT  = VAMCL-SLS-REG-RTL-AMT           02402000
                                      + VAMCL-SLS-PROMO-RTL-AMT         02403000
                                      + VAMCL-SLS-PROCLR-RTL-AMT        02404000
                                      + VAMCL-SLS-CLR-RTL-AMT           02405000
                                      + VAMCL-SLS-OTH-RTL-AMT.          02406000
           COMPUTE ML111-MKDN-RTL-AMT = VAMCL-MKDN-PLU-RTL-AMT          02407000
                                      + VAMCL-MKDN-RGST-RTL-AMT         02408002
                                      + VAMCL-MKDN-UMTCH-RTL-AMT        02409000
                                      + VAMCL-MKDN-BYR-RTL-AMT          02409100
                                      + VAMCL-MKDN-STR-RTL-AMT.         02409200
           MOVE VAMCL-MKUP-RTL-AMT     TO ML111-MKUP-RTL-AMT.           02420000
           MOVE VAMCL-XFER-IN-RTL-AMT  TO ML111-XFER-IN-RTL-AMT.        02430000
           MOVE VAMCL-XFER-OUT-RTL-AMT TO ML111-XFER-OUT-RTL-AMT.       02440000
           MOVE VAMCL-RCPT-RTL-AMT     TO ML111-RCPT-RTL-AMT.           02450000
           MOVE VAMCL-RCPT-NET-COST-AMT                                 02460000
                                       TO ML111-RCPT-NET-COST-AMT.      02470000
           MOVE VAMCL-PADJ-RTL-AMT     TO ML111-PADJ-RTL-AMT.           02480000
           MOVE VAMCL-PADJ-NET-COST-AMT                                 02490000
                                       TO ML111-PADJ-NET-COST-AMT.      02500000
           MOVE VAMCL-INV-ADJ-RTL-AMT  TO ML111-INV-ADJ-RTL-AMT.        02510000
           MOVE VAMCL-SHNK-RTL-AMT     TO ML111-SHNK-RTL-AMT.           02520000
           MOVE VAMCL-END-INV-RTL-AMT  TO ML111-END-INV-RTL-AMT.        02521003
                                                                        02530000
           WRITE DATA-INTEGRITY-EXTR-REC                                02540000
               FROM ML111-DATA-INTEGRITY-EXTR-REC.                      02550000
           ADD 1                       TO PA-OUTPUT-RECS-WRITTEN.       02560000
                                                                        02570000
       EJECT                                                            02580000
                                                                        02590000
      ******************************************************************02600000
       7100-OPEN-CURSOR-TVAMCL.                                         02610000
      ******************************************************************02620000
      *                                                                 02630000
           EXEC SQL                                                     02640000
               OPEN TVAMCL_CURSOR                                       02650000
           END-EXEC.                                                    02660000
                                                                        02670000
           EVALUATE TRUE                                                02680000
               WHEN SQLCODE = 0                                         02690000
                   CONTINUE                                             02690100
               WHEN OTHER                                               02690200
                   MOVE '7100-OPEN-CURSOR-TVAMCL'                       02690300
                                       TO AA-PARAGRAPH-NAME             02690400
                   MOVE 'TVAMCL TABLE ERROR'                            02690500
                                       TO AA-MESSAGE                    02690600
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              02690700
                   PERFORM Z998-SQL-ERROR                               02690800
           END-EVALUATE.                                                02690900
                                                                        02691000
       EJECT                                                            02692000
                                                                        02693000
      ******************************************************************02694000
       7200-FETCH-TVAMCL.                                               02695000
      ******************************************************************02696000
      *                                                                 02697000
           EXEC SQL                                                     02698000
               FETCH TVAMCL_CURSOR                                      02699000
                INTO :VAMCL-FSCL-MN-END-DTE,                            02700000
                     :VAMCL-FSCL-MN-NBR,                                02710000
                     :VAMCL-DEPT-NBR,                                   02720000
                     :VAMCL-MAJ-CL-NBR,                                 02730000
                     :VAMCL-SLS-REG-RTL-AMT,                            02731000
                     :VAMCL-SLS-PROMO-RTL-AMT,                          02732000
                     :VAMCL-SLS-PROCLR-RTL-AMT,                         02733000
                     :VAMCL-SLS-CLR-RTL-AMT,                            02734000
                     :VAMCL-SLS-OTH-RTL-AMT,                            02735000
                     :VAMCL-MKDN-PLU-RTL-AMT,                           02736000
                     :VAMCL-MKDN-RGST-RTL-AMT,                          02737002
                     :VAMCL-MKDN-UMTCH-RTL-AMT,                         02738000
                     :VAMCL-MKDN-BYR-RTL-AMT,                           02739000
                     :VAMCL-MKDN-STR-RTL-AMT,                           02739100
                     :VAMCL-MKUP-RTL-AMT,                               02739200
                     :VAMCL-XFER-IN-RTL-AMT,                            02739300
                     :VAMCL-XFER-OUT-RTL-AMT,                           02739400
                     :VAMCL-RCPT-RTL-AMT,                               02739500
                     :VAMCL-RCPT-NET-COST-AMT,                          02739600
                     :VAMCL-PADJ-RTL-AMT,                               02739700
                     :VAMCL-PADJ-NET-COST-AMT,                          02739800
                     :VAMCL-INV-ADJ-RTL-AMT,                            02739900
                     :VAMCL-SHNK-RTL-AMT,                               02840004
                     :VAMCL-END-INV-RTL-AMT                             02841004
           END-EXEC.                                                    02850000
                                                                        02860000
           EVALUATE TRUE                                                02870000
               WHEN SQLCODE = 0                                         02880000
                   ADD +1              TO PA-TVAMCL-ROWS-FETCHED        02890000
               WHEN SQLCODE = +100                                      02900000
                   MOVE 'Y'            TO PS-TVAMCL-DONE                02910000
               WHEN OTHER                                               02920000
                   MOVE '7200-FETCH-TVAMCL'                             02930000
                                       TO AA-PARAGRAPH-NAME             02940000
                   MOVE 'TVAMCL TABLE ERROR'                            02950000
                                       TO AA-MESSAGE                    02960000
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              02970000
                   PERFORM Z998-SQL-ERROR                               02980000
           END-EVALUATE.                                                02990000
                                                                        03000000
       EJECT                                                            03010000
                                                                        03020000
      ******************************************************************03030000
       7300-CLOSE-CURSOR-TVAMCL.                                        03040000
      ******************************************************************03050000
      *                                                                 03060000
           EXEC SQL                                                     03070000
               CLOSE TVAMCL_CURSOR                                      03080000
           END-EXEC.                                                    03090000
                                                                        03100000
           EVALUATE TRUE                                                03110000
               WHEN SQLCODE = 0                                         03120000
                   CONTINUE                                             03130000
               WHEN OTHER                                               03140000
                   MOVE '7300-CLOSE-CURSOR-TVAMCL'                      03150000
                                       TO AA-PARAGRAPH-NAME             03160000
                   MOVE 'TVAMCL TABLE ERROR'                            03170000
                                       TO AA-MESSAGE                    03180000
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              03190000
                   PERFORM Z998-SQL-ERROR                               03200000
           END-EVALUATE.                                                03210000
                                                                        03220000
       EJECT                                                            03230000
                                                                        03240000
      ******************************************************************03250000
       8000-EOJ.                                                        03260000
      ******************************************************************03270000
      *                                                                 03280000
           DISPLAY ' '.                                                 03290000
           DISPLAY '  PROGRAM MLKUT432 PROCESSING SUMMARY'.             03300000
           DISPLAY '  -----------------------------------'.             03310000
           DISPLAY ' '.                                                 03320000
      *                                                                 03330000
           MOVE PA-TVAMCL-ROWS-FETCHED TO CD-COUNT-HOLD1.               03340000
           DISPLAY 'TVAMCL ROWS FETCHED =       ' CD-COUNT-HOLD1.       03350000
      *                                                                 03360000
           MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.               03370000
           DISPLAY 'EXTRACT RECORDS WRITTEN =   ' CD-COUNT-HOLD1.       03380000
      *                                                                 03390000
           DISPLAY ' '.                                                 03400000
                                                                        03410000
           CLOSE DATA-INTEGRITY-CTL-FILE.                               03420000
           CLOSE DATA-INTEGRITY-EXTR-FILE.                              03430000
                                                                        03440000
       EJECT                                                            03450000
                                                                        03460000
      ******************************************************************03470000
       Z990-COMMON-ABEND-ROUTINE.                                       03480000
      ******************************************************************03490000
      *                                                                 03500000
           DISPLAY AA-ABEND-LIT.                                        03510000
           DISPLAY AA-PROGRAM-LIT.                                      03520000
           DISPLAY AA-PARAGRAPH-LIT.                                    03530000
           DISPLAY AA-MESSAGE-LINE.                                     03540000
           DISPLAY ABEND-CODE.                                          03550000
                                                                        03560000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03570000
                                                                        03580000
       EJECT                                                            03590000
                                                                        03600000
      ******************************************************************03610000
       Z998-SQL-ERROR.                                                  03620000
      ******************************************************************03630000
      *                                                                 03640000
           CLOSE DATA-INTEGRITY-CTL-FILE                                03650000
                 DATA-INTEGRITY-EXTR-FILE.                              03660000
                                                                        03670000
           DISPLAY AA-ABEND-LIT.                                        03680000
           DISPLAY AA-DB2-ERROR-LIT.                                    03690000
           DISPLAY AA-PROGRAM-LIT.                                      03700000
           DISPLAY AA-PARAGRAPH-LIT.                                    03710000
           DISPLAY AA-MESSAGE-LINE.                                     03720000
           DISPLAY AA-DB2-OPERATION-LIT.                                03730000
           SET AC-DB2-ERROR TO TRUE.                                    03740000
           MOVE +4013                  TO ABEND-CODE.                   03750000
                                                                        03760000
           COPY DPPD004.                                                03770000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03780000
                                                                        03790000
