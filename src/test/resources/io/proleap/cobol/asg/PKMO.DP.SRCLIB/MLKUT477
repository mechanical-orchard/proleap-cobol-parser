       IDENTIFICATION DIVISION.                                         00010001
       PROGRAM-ID.    MLKUT477.                                         00020010
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030001
       DATE-WRITTEN.  10-23-2000.                                       00040010
       DATE-COMPILED.                                                   00050001
                                                                        00060009
      ******************************************************************00070001
      * MLKUT477 - DESCRIPTION.............                             00080010
      * THIS PROGRAM WILL CREATE THE DATA INTEGRITY EXTRACT FILE FOR    00090001
      * TMNDPTC.                                                        00100010
W26603******************************************************************00110014
W26603* CHG ID   DATE        DESCRIPTION                               *00120014
W26603* ------   ----------  ----------------------------------------- *00130014
W26603* 26603    03-17-2004  STORE EXPANSION.                          *00140014
W26603*                      INCREASED LRECL FOR OUT01 BECAUSE OF      *00150014
W26603*                      CHANGES TO MLRD153.                       *00160014
W26603*                      EXPANDED WORKING STORAGE VARIABLE.        *00170014
W26603*                      ADDED 'WITH UR' TO A SQL SELECT.          *00180014
W26603*                      - RONNA MCDONALD                          *00190014
      ******************************************************************00200001
      *                                                                 00210001
       ENVIRONMENT DIVISION.                                            00220001
      *                                                                 00230001
       CONFIGURATION SECTION.                                           00240001
      *                                                                 00250001
       SOURCE-COMPUTER.        IBM-3090.                                00260001
       OBJECT-COMPUTER.        IBM-3090.                                00270001
      *                                                                 00280001
       INPUT-OUTPUT SECTION.                                            00290001
       FILE-CONTROL.                                                    00300001
                                                                        00310001
           SELECT DATA-INTEGRITY-CTL-FILE                               00320001
               ASSIGN TO INP01.                                         00330001
                                                                        00340001
           SELECT DATA-INTEGRITY-EXTR-FILE                              00350001
               ASSIGN TO OUT01.                                         00360001
                                                                        00370001
      ******************************************************************00380001
       DATA DIVISION.                                                   00390001
      ******************************************************************00400001
                                                                        00410001
       FILE SECTION.                                                    00420001
                                                                        00430001
       FD  DATA-INTEGRITY-CTL-FILE                                      00440001
           LABEL RECORDS ARE STANDARD                                   00450001
           RECORDING MODE IS F                                          00460001
           BLOCK CONTAINS 0 RECORDS.                                    00470001
       01  DATA-INTEGRITY-CTL-REC      PIC X(80).                       00480001
                                                                        00490001
       FD  DATA-INTEGRITY-EXTR-FILE                                     00500001
           RECORDING MODE IS F                                          00510001
           LABEL RECORDS ARE STANDARD                                   00520001
           BLOCK CONTAINS 0 RECORDS.                                    00530001
                                                                        00540001
W26603 01  DATA-INTEGRITY-EXTR-REC     PIC X(120).                      00550014
                                                                        00560001
      *                                                                 00570001
      ******************************************************************00580001
       WORKING-STORAGE SECTION.                                         00590001
      ******************************************************************00600001
                                                                        00610001
       01  PS-PROGRAM-SWITCHES.                                         00620001
           05  PS-TMNDPTC-DONE         PIC X         VALUE 'N'.         00630014
               88  PS-TMNDPTC-IS-DONE                VALUE 'Y'.         00640014
               88  PS-TMNDPTC-IS-NOT-DONE            VALUE 'N'.         00650014
                                                                        00660001
       01  PA-PROGRAM-ACCUMULATORS                   COMP-3.            00670014
           05  PA-TMNDPTC-ROWS-FETCHED PIC S9(13)    VALUE ZERO.        00680014
           05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)    VALUE ZERO.        00690014
W26603*    05  PA-WORK-AMOUNT          PIC S9(9)V99  VALUE ZEROS.       00700014
W26603     05  PA-WORK-AMOUNT          PIC S9(11)V99 VALUE ZEROS.       00710014
                                                                        00720001
       01  PC-PROGRAM-CONSTANTS.                                        00730004
           05  PC-MAJ-CL-NBR-ZEROS     PIC  X(02)  VALUE '00'.          00740004
           05  PC-SUB-CL-NBR-ZEROS     PIC  X(02)  VALUE '00'.          00750004
                                                                        00760001
       01  CD-COUNTER-DISPLAY.                                          00770001
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.           00780001
                                                                        00790001
       01 ABEND-AREAS.                                                  00800001
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO           00810001
                                                   COMP SYNC.           00820001
               88  AC-BAD-DATA                     VALUE +4003.         00830001
               88  AC-DB2-ERROR                    VALUE +4013.         00840001
           05  AA-ABEND-LIT            PIC X(40)   VALUE                00850001
                 '*****       ABEND'.                                   00860001
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                00870001
                   '*****   PROGRAM: MLKUT477'.                         00880010
           05  AA-PARAGRAPH-LIT.                                        00890001
               10  FILLER              PIC X(17)   VALUE                00900001
                   '***** PARAGRAPH: '.                                 00910001
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        00920001
           05  AA-MESSAGE-LINE.                                         00930001
               10  FILLER              PIC X(06)   VALUE '*****'.       00940001
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        00950001
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                00960001
                   '*****    DB2 ERROR'.                                00970001
           05  AA-DB2-OPERATION-LIT.                                    00980001
               10  FILLER              PIC X(17)   VALUE                00990001
                   '***** OPERATION: '.                                 01000001
               10  AA-DB2-OPERATION.                                    01010001
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        01020001
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        01030001
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        01040001
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        01050001
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        01060001
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        01070001
                                                                        01080001
       EJECT                                                            01090001
      ******************************************************************01100001
      *** DATA INTEGRITY CONTROL CARD                                   01110001
      ******************************************************************01120001
           COPY MLRD110.                                                01130001
                                                                        01140001
      ******************************************************************01150001
      *** DATA INTEGRITY TMNDPTC EXTRACT FILE                           01160010
      ******************************************************************01170001
           COPY MLRD153.                                                01180012
                                                                        01190001
       EJECT                                                            01200001
      ******************************************************************01210001
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01220001
      ******************************************************************01230001
           COPY DPWS004.                                                01240001
                                                                        01250001
      ******************************************************************01260001
      *  DB2 ERRORS                                                     01270001
      ******************************************************************01280001
           COPY SQLCA2.                                                 01290001
                                                                        01300001
      ******************************************************************01310001
      *  DB2 COMMUNICATION AREA                                         01320001
      ******************************************************************01330001
           EXEC SQL                                                     01340001
                INCLUDE SQLCA                                           01350001
           END-EXEC.                                                    01360001
                                                                        01370001
      ******************************************************************01380001
      *  DB2 TMNDPTC TABLE LAYOUT                                       01390010
      ******************************************************************01400001
           EXEC SQL                                                     01410001
                INCLUDE TMNDPTC                                         01420010
           END-EXEC.                                                    01430001
                                                                        01440001
      ******************************************************************01450001
      *  DB2 TMNDPTC CURSOR                                             01460010
      ******************************************************************01470001
           EXEC SQL                                                     01480001
               DECLARE TMNDPTC_CURSOR CURSOR FOR                        01490010
                SELECT FSCL_MN_NBR                                      01500010
                      ,FSCL_MN_END_DTE                                  01510010
                      ,DEPT_NBR                                         01520010
                      ,CHNL_CDE                                         01530010
                      ,SLS_RTL_AMT                                      01540010
                      ,MKDN_PLU_RTL_AMT                                 01550010
                      ,MKDN_RGST_RTL_AMT                                01560010
                      ,MKDN_UMTCH_RTL_AMT                               01570010
                      ,MKDN_BYR_RTL_AMT                                 01580010
                      ,MKDN_STR_RTL_AMT                                 01590010
                      ,MKUP_RTL_AMT                                     01600010
                      ,XFER_IN_RTL_AMT                                  01610010
                      ,XFER_OUT_RTL_AMT                                 01620010
                      ,RCPT_RTL_AMT                                     01630010
                      ,RCPT_NET_COST_AMT                                01640010
                      ,PADJ_RTL_AMT                                     01650010
                      ,PADJ_NET_COST_AMT                                01660010
                      ,PADJ_FRGT_COST_AMT                               01670010
                      ,INV_ADJ_RTL_AMT                                  01680010
                      ,SHNK_RTL_AMT                                     01690010
                      ,END_INV_RTL_AMT                                  01700010
                      ,CUM_MKUP_PCT                                     01710010
                      ,END_INV_COST_AMT                                 01720010
                      ,SLS_COST_AMT                                     01730010
                      ,GRO_MRGN_AMT                                     01740010
                  FROM TMNDPTC                                          01750010
                 WHERE (FSCL_MN_END_DTE >= :ML110-STRT-DTE)             01760001
                   AND (FSCL_MN_END_DTE <= :ML110-END-DTE)              01770001
****************** AND CHNL_CDE = 'A'                                   01780012
              ORDER BY FSCL_MN_END_DTE,                                 01790001
                       DEPT_NBR,                                        01800013
                       CHNL_CDE                                         01810013
W26603        WITH UR                                                   01820014
           END-EXEC.                                                    01830001
                                                                        01840001
       EJECT                                                            01850001
      ******************************************************************01860001
       PROCEDURE DIVISION.                                              01870001
      ******************************************************************01880001
                                                                        01890001
       0000-MAINLINE-MLKUT477.                                          01900010
      *                                                                 01910001
           PERFORM 1000-INITIALIZATION.                                 01920001
                                                                        01930001
           PERFORM 2000-MAIN-PROCESS.                                   01940001
                                                                        01950001
           PERFORM 8000-EOJ.                                            01960001
                                                                        01970001
           STOP RUN.                                                    01980001
                                                                        01990001
       EJECT                                                            02000001
      ******************************************************************02010001
       1000-INITIALIZATION.                                             02020001
      ******************************************************************02030001
      *                                                                 02040001
           OPEN INPUT  DATA-INTEGRITY-CTL-FILE                          02050001
                OUTPUT DATA-INTEGRITY-EXTR-FILE.                        02060001
                                                                        02070001
           PERFORM 4000-READ-CONTROL-CARD.                              02080001
                                                                        02090001
           DISPLAY ' '.                                                 02100001
           DISPLAY '*** MLKUT477 INPUT CONTROL CARD VALUES ***'         02110010
           DISPLAY ML110-DATA-INTEGRITY-CC-REC.                         02120001
                                                                        02130001
       EJECT                                                            02140001
      ******************************************************************02150001
       2000-MAIN-PROCESS.                                               02160001
      ******************************************************************02170001
      *                                                                 02180001
           PERFORM 7100-OPEN-CURSOR-TMNDPTC.                            02190010
                                                                        02200001
           PERFORM 7200-FETCH-TMNDPTC.                                  02210010
                                                                        02220001
           PERFORM                                                      02230001
             UNTIL PS-TMNDPTC-IS-DONE                                   02240010
               PERFORM 5000-WRITE-EXTRACT                               02250001
               PERFORM 7200-FETCH-TMNDPTC                               02260010
           END-PERFORM.                                                 02270001
                                                                        02280001
           PERFORM 7300-CLOSE-CURSOR-TMNDPTC.                           02290010
                                                                        02300001
       EJECT                                                            02310001
      ******************************************************************02320001
       4000-READ-CONTROL-CARD.                                          02330001
      ******************************************************************02340001
      *                                                                 02350001
           READ DATA-INTEGRITY-CTL-FILE                                 02360001
             INTO ML110-DATA-INTEGRITY-CC-REC                           02370001
               AT END                                                   02380001
                   MOVE '4000-READ-CONTROL-CARD'                        02390001
                                       TO AA-PARAGRAPH-NAME             02400001
                   MOVE 'CONTROL CARD IS MISSING'                       02410001
                                       TO AA-MESSAGE                    02420001
                   MOVE +4001          TO ABEND-CODE                    02430001
                   PERFORM 9900-COMMON-ABEND-ROUTINE                    02440001
           END-READ.                                                    02450001
                                                                        02460001
       EJECT                                                            02470001
      ******************************************************************02480001
       5000-WRITE-EXTRACT.                                              02490001
      ******************************************************************02500001
      *                                                                 02510001
           MOVE MNDPTC-FSCL-MN-END-DTE  TO ML153-FSCL-MN-END-DTE.       02520012
           MOVE MNDPTC-FSCL-MN-NBR      TO ML153-FSCL-MN-NBR.           02530012
           MOVE MNDPTC-DEPT-NBR         TO ML153-DEPT-NBR.              02540012
           MOVE PC-MAJ-CL-NBR-ZEROS     TO ML153-MAJ-CL-NBR.            02550012
           MOVE PC-SUB-CL-NBR-ZEROS     TO ML153-SUB-CL-NBR.            02560012
           MOVE ZEROS                   TO ML153-STR-NBR.               02570012
           MOVE MNDPTC-CHNL-CDE         TO ML153-CHNL-CDE.              02580012
           MOVE MNDPTC-SLS-RTL-AMT      TO ML153-SLS-RTL-AMT.           02590012
                                                                        02600010
           COMPUTE PA-WORK-AMOUNT      = MNDPTC-MKDN-PLU-RTL-AMT        02610010
                                       + MNDPTC-MKDN-RGST-RTL-AMT       02620010
                                       + MNDPTC-MKDN-UMTCH-RTL-AMT      02630010
                                       + MNDPTC-MKDN-BYR-RTL-AMT        02640010
                                       + MNDPTC-MKDN-STR-RTL-AMT.       02650010
027200     MOVE PA-WORK-AMOUNT         TO ML153-MKDN-RTL-AMT.           02660012
                                                                        02670010
           MOVE MNDPTC-MKUP-RTL-AMT     TO ML153-MKUP-RTL-AMT.          02680012
           MOVE MNDPTC-XFER-IN-RTL-AMT  TO ML153-XFER-IN-RTL-AMT.       02690012
           MOVE MNDPTC-XFER-OUT-RTL-AMT TO ML153-XFER-OUT-RTL-AMT.      02700012
           MOVE MNDPTC-RCPT-RTL-AMT     TO ML153-RCPT-RTL-AMT.          02710012
           MOVE MNDPTC-RCPT-NET-COST-AMT                                02720010
                                        TO ML153-RCPT-NET-COST-AMT.     02730012
           MOVE MNDPTC-PADJ-RTL-AMT     TO ML153-PADJ-RTL-AMT.          02740012
           MOVE MNDPTC-PADJ-NET-COST-AMT                                02750010
                                        TO ML153-PADJ-NET-COST-AMT.     02760012
           MOVE MNDPTC-INV-ADJ-RTL-AMT  TO ML153-INV-ADJ-RTL-AMT.       02770012
           MOVE MNDPTC-SHNK-RTL-AMT     TO ML153-SHNK-RTL-AMT.          02780012
           MOVE MNDPTC-END-INV-RTL-AMT  TO ML153-END-INV-RTL-AMT.       02790012
                                                                        02800001
           WRITE DATA-INTEGRITY-EXTR-REC                                02810001
               FROM ML153-DATA-INTEGRITY-EXTR-REC.                      02820012
           ADD 1                        TO PA-OUTPUT-RECS-WRITTEN.      02830001
                                                                        02840001
       EJECT                                                            02850001
      ******************************************************************02860001
       7100-OPEN-CURSOR-TMNDPTC.                                        02870010
      ******************************************************************02880001
      *                                                                 02890001
           EXEC SQL                                                     02900001
               OPEN TMNDPTC_CURSOR                                      02910010
           END-EXEC.                                                    02920001
                                                                        02930001
           EVALUATE TRUE                                                02940001
               WHEN SQLCODE = 0                                         02950001
                   CONTINUE                                             02960001
               WHEN OTHER                                               02970001
                   MOVE '7100-OPEN-CURSOR-TMNDPTC'                      02980010
                                       TO AA-PARAGRAPH-NAME             02990001
                   MOVE 'TMNDPTC TABLE ERROR'                           03000010
                                       TO AA-MESSAGE                    03010001
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              03020001
                   PERFORM 9901-SQL-ERROR                               03030001
           END-EVALUATE.                                                03040001
                                                                        03050001
       EJECT                                                            03060001
      ******************************************************************03070001
       7200-FETCH-TMNDPTC.                                              03080010
      ******************************************************************03090001
      *                                                                 03100001
           EXEC SQL                                                     03110001
               FETCH TMNDPTC_CURSOR                                     03120010
               INTO :MNDPTC-FSCL-MN-NBR,                                03130010
                    :MNDPTC-FSCL-MN-END-DTE,                            03140010
                    :MNDPTC-DEPT-NBR,                                   03150010
                    :MNDPTC-CHNL-CDE,                                   03160010
                    :MNDPTC-SLS-RTL-AMT,                                03170010
                    :MNDPTC-MKDN-PLU-RTL-AMT,                           03180010
                    :MNDPTC-MKDN-RGST-RTL-AMT,                          03190010
                    :MNDPTC-MKDN-UMTCH-RTL-AMT,                         03200010
                    :MNDPTC-MKDN-BYR-RTL-AMT,                           03210010
                    :MNDPTC-MKDN-STR-RTL-AMT,                           03220010
                    :MNDPTC-MKUP-RTL-AMT,                               03230010
                    :MNDPTC-XFER-IN-RTL-AMT,                            03240010
                    :MNDPTC-XFER-OUT-RTL-AMT,                           03250010
                    :MNDPTC-RCPT-RTL-AMT,                               03260010
                    :MNDPTC-RCPT-NET-COST-AMT,                          03270010
                    :MNDPTC-PADJ-RTL-AMT,                               03280010
                    :MNDPTC-PADJ-NET-COST-AMT,                          03290010
                    :MNDPTC-PADJ-FRGT-COST-AMT,                         03300010
                    :MNDPTC-INV-ADJ-RTL-AMT,                            03310010
                    :MNDPTC-SHNK-RTL-AMT,                               03320010
                    :MNDPTC-END-INV-RTL-AMT,                            03330010
                    :MNDPTC-CUM-MKUP-PCT,                               03340010
                    :MNDPTC-END-INV-COST-AMT,                           03350010
                    :MNDPTC-SLS-COST-AMT,                               03360010
                    :MNDPTC-GRO-MRGN-AMT                                03370010
           END-EXEC.                                                    03380001
                                                                        03390001
           EVALUATE TRUE                                                03400001
               WHEN SQLCODE = 0                                         03410001
                   ADD +1              TO PA-TMNDPTC-ROWS-FETCHED       03420010
               WHEN SQLCODE = +100                                      03430001
                   MOVE 'Y'            TO PS-TMNDPTC-DONE               03440010
               WHEN OTHER                                               03450001
                   MOVE '7200-FETCH-TMNDPTC'                            03460010
                                       TO AA-PARAGRAPH-NAME             03470001
                   MOVE 'TMNDPTC TABLE ERROR'                           03480010
                                       TO AA-MESSAGE                    03490001
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              03500001
                   PERFORM 9901-SQL-ERROR                               03510001
           END-EVALUATE.                                                03520001
                                                                        03530001
       EJECT                                                            03540001
      ******************************************************************03550001
       7300-CLOSE-CURSOR-TMNDPTC.                                       03560010
      ******************************************************************03570001
      *                                                                 03580001
           EXEC SQL                                                     03590001
               CLOSE TMNDPTC_CURSOR                                     03600010
           END-EXEC.                                                    03610001
                                                                        03620001
           EVALUATE TRUE                                                03630001
               WHEN SQLCODE = 0                                         03640001
                   CONTINUE                                             03650001
               WHEN OTHER                                               03660001
                   MOVE '7300-CLOSE-CURSOR-TMNDPTC'                     03670010
                                       TO AA-PARAGRAPH-NAME             03680001
                   MOVE 'TMNDPTC TABLE ERROR'                           03690010
                                       TO AA-MESSAGE                    03700001
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              03710001
                   PERFORM 9901-SQL-ERROR                               03720001
           END-EVALUATE.                                                03730001
                                                                        03740001
       EJECT                                                            03750001
      ******************************************************************03760001
       8000-EOJ.                                                        03770001
      ******************************************************************03780001
      *                                                                 03790001
           DISPLAY ' '.                                                 03800001
           DISPLAY '  PROGRAM MLKUT477 PROCESSING SUMMARY'.             03810010
           DISPLAY '  -----------------------------------'.             03820001
           DISPLAY ' '.                                                 03830001
      *                                                                 03840001
           MOVE PA-TMNDPTC-ROWS-FETCHED TO CD-COUNT-HOLD1.              03850010
           DISPLAY 'TMNDPTC ROWS FETCHED =      ' CD-COUNT-HOLD1.       03860010
      *                                                                 03870001
           MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.               03880001
           DISPLAY 'EXTRACT RECORDS WRITTEN =   ' CD-COUNT-HOLD1.       03890001
      *                                                                 03900001
           DISPLAY ' '.                                                 03910001
                                                                        03920001
           CLOSE DATA-INTEGRITY-CTL-FILE.                               03930001
           CLOSE DATA-INTEGRITY-EXTR-FILE.                              03940001
                                                                        03950001
       EJECT                                                            03960001
      ******************************************************************03970001
       9900-COMMON-ABEND-ROUTINE.                                       03980001
      ******************************************************************03990001
      *                                                                 04000001
           DISPLAY AA-ABEND-LIT.                                        04010001
           DISPLAY AA-PROGRAM-LIT.                                      04020001
           DISPLAY AA-PARAGRAPH-LIT.                                    04030001
           DISPLAY AA-MESSAGE-LINE.                                     04040001
           DISPLAY ABEND-CODE.                                          04050001
                                                                        04060001
           CALL 'ILBOABN0' USING ABEND-CODE.                            04070001
                                                                        04080001
       EJECT                                                            04090001
      ******************************************************************04100001
       9901-SQL-ERROR.                                                  04110001
      ******************************************************************04120001
      *                                                                 04130001
           CLOSE DATA-INTEGRITY-CTL-FILE                                04140001
                 DATA-INTEGRITY-EXTR-FILE.                              04150001
                                                                        04160001
           DISPLAY AA-ABEND-LIT.                                        04170001
           DISPLAY AA-DB2-ERROR-LIT.                                    04180001
           DISPLAY AA-PROGRAM-LIT.                                      04190001
           DISPLAY AA-PARAGRAPH-LIT.                                    04200001
           DISPLAY AA-MESSAGE-LINE.                                     04210001
           DISPLAY AA-DB2-OPERATION-LIT.                                04220001
           SET AC-DB2-ERROR TO TRUE.                                    04230001
           MOVE +4013                  TO ABEND-CODE.                   04240001
                                                                        04250001
           COPY DPPD004.                                                04260001
           CALL 'ILBOABN0' USING ABEND-CODE.                            04270001
                                                                        04280001
