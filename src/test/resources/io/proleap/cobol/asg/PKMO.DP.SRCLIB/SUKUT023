000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    SUKUT023.                                                 
000600 AUTHOR.        DENISE HAHN                                               
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   JULY, 1999.                                               
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM WILL CREATE A TIMESTAMP CONTROL FILE USED                 
001310*  TO DELETE ROWS FROM THE LABEL PRINT DATA TABLE (TLAPTDA)               
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
002900                                                                          
002901     SELECT TIMESTAMP-FILE                                                
002902         ASSIGN TO OUT01.                                                 
002910                                                                          
003200******************************************************************        
003300 DATA DIVISION.                                                           
003400******************************************************************        
003500                                                                          
003600 FILE SECTION.                                                            
003700                                                                          
003800                                                                          
003801 FD  TIMESTAMP-FILE                                                       
003802     RECORDING MODE IS F                                                  
003803     LABEL RECORDS ARE STANDARD                                           
003804     BLOCK CONTAINS 0 RECORDS.                                            
003805                                                                          
003810 01  TIMESTAMP-RECORD           PIC  X(50).                               
003900                                                                          
003901******************************************************************        
003910 WORKING-STORAGE SECTION.                                                 
004000******************************************************************        
004100                                                                          
005800*-----------------------------------------------------------------        
005900*  RECORD LAYOUT FOR THE TIMESTAMP FILE.                                  
005910*-----------------------------------------------------------------        
006100     COPY RCRD011.                                                        
006200                                                                          
006300*-----------------------------------------------------------------        
006600*  DB2 FORMATTED MESSAGE AREA                                             
006610*-----------------------------------------------------------------        
006800     COPY DPWS004.                                                        
007100                                                                          
007110*-----------------------------------------------------------------        
007300*  COMMUNICATIONS AREA FOR DB2                                            
007310*-----------------------------------------------------------------        
007500     EXEC SQL                                                             
007600          INCLUDE SQLCA                                                   
007700     END-EXEC.                                                            
007800                                                                          
007810*-----------------------------------------------------------------        
008000*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE COMPANY CONTROL            
008100*  TABLE                                                                  
008110*-----------------------------------------------------------------        
008300     EXEC SQL                                                             
008400          INCLUDE TCOCNTL                                                 
008500     END-EXEC.                                                            
009300                                                                          
009310*-----------------------------------------------------------------        
009320*                                                                         
009330*-----------------------------------------------------------------        
009400 01  ABEND-CODE                     PIC S9(04) VALUE +0                   
009500                                               COMP SYNC.                 
010200                                                                          
010300 01  PC-PROGRAM-CONSTANTS.                                                
010860     05  PC-DEFAULT-TIME            PIC  X(16) VALUE                      
010870                                               '-00.00.00.000000'.        
010871     05  PC-CURRENT-PROGRAM-NAME    PIC  X(08) VALUE                      
010880         'SUKUT023'.                                                      
010900                                                                          
011000 01  PV-PROGRAM-VARIABLES.                                                
011010     05  PV-CURRENT-TIMESTAMP       PIC  X(26) VALUE SPACES.              
011020     05  PV-CURRENT-TIMESTAMP-RDF REDEFINES PV-CURRENT-TIMESTAMP.         
011030         10  PV-CURRENT-DATE        PIC X(10).                            
011040         10  PV-CURRENT-TIME        PIC X(16).                            
011050     05  PV-EXTRACT-DATE            PIC X(10).                            
011060                                                                          
011200     05  PV-PARAGRAPH-NAME          PIC  X(30) VALUE SPACES.              
011300     05  PV-DB2-OPERATION-ATTEMPTED PIC  X(30) VALUE SPACES.              
012900                                                                          
013600                                                                          
013700******************************************************************        
013800 PROCEDURE DIVISION.                                                      
013900******************************************************************        
014000                                                                          
014010                                                                          
014020*-----------------------------------------------------------------        
014030* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
014040*      PROGRAM.                                                           
014050*-----------------------------------------------------------------        
014100 0000-PROGRAM-MAINLINE.                                                   
014600                                                                          
014700     PERFORM 1000-INITIALIZE.                                             
015200     PERFORM 9000-EOJ-ROUTINE.                                            
015300                                                                          
015400     STOP RUN.                                                            
015500                                                                          
015501                                                                          
015510*-----------------------------------------------------------------        
015520*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
015530*-----------------------------------------------------------------        
015600 1000-INITIALIZE.                                                         
016200                                                                          
016300     OPEN OUTPUT TIMESTAMP-FILE.                                          
016310     PERFORM 1100-CREATE-CONTROL-CARD.                                    
016500                                                                          
016501                                                                          
016502*-----------------------------------------------------------------        
016503*  CREATE THE CONTROL CARD.                                               
016504*-----------------------------------------------------------------        
016505 1100-CREATE-CONTROL-CARD.                                                
016508                                                                          
016509     EXEC SQL                                                             
016510          SELECT DISTINCT                                                 
016512                 DATE(BTCH_PRC_DTE - 2 DAYS)                              
016513            INTO                                                          
016515                 :PV-EXTRACT-DATE                                         
016516            FROM                                                          
016517                 TCOCNTL                                                  
016518     END-EXEC.                                                            
016519                                                                          
016520     EVALUATE TRUE                                                        
016521         WHEN SQLCODE = ZERO                                              
016522             DISPLAY ' EXTRACT DATE IS ' PV-EXTRACT-DATE                  
016533                                                                          
016534         WHEN SQLWARN0 NOT = SPACES                                       
016535         WHEN SQLCODE < ZERO                                              
016536         WHEN SQLCODE > ZERO                                              
016537             MOVE '1100-CREATE CONTROL CARD'                              
016538                                 TO PV-PARAGRAPH-NAME                     
016539             MOVE 'ATTEMPTING TO SELECT CURRENT TIMESTAMP                 
016540-                 'FROM TCOCNTL'                                          
016541                                 TO PV-DB2-OPERATION-ATTEMPTED            
016542             PERFORM 9100-SQL-ABEND                                       
016543     END-EVALUATE.                                                        
016544                                                                          
016545     INITIALIZE RC011-CONTROL-RECORD.                                     
016546                                                                          
016547     MOVE PV-EXTRACT-DATE  TO  PV-CURRENT-DATE.                           
016548     MOVE PC-DEFAULT-TIME  TO  PV-CURRENT-TIME.                           
016549                                                                          
016550     MOVE PV-CURRENT-TIMESTAMP TO RC011-CONTROL-RECORD.                   
016551     DISPLAY ' SUKUT023 CONTROL CARD -- ' RC011-CONTROL-TMST.             
016552     PERFORM 8100-WRITE-TIMESTAMP-RECORD.                                 
016560                                                                          
016600                                                                          
016700*-----------------------------------------------------------------        
016800*  WRITE THE TIMESTAMP RECORD                                             
016900*-----------------------------------------------------------------        
022606 8100-WRITE-TIMESTAMP-RECORD.                                             
022607                                                                          
022612     WRITE TIMESTAMP-RECORD                                               
022613         FROM RC011-CONTROL-RECORD.                                       
022700                                                                          
022710                                                                          
022800 9000-EOJ-ROUTINE.                                                        
022810*-----------------------------------------------------------------        
023000*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
023010*-----------------------------------------------------------------        
023200     CLOSE TIMESTAMP-FILE.                                                
023800                                                                          
023900                                                                          
023910*-----------------------------------------------------------------        
023920*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
023930*  ERROR OR WARNING CODE OCCURS.                                          
023940*-----------------------------------------------------------------        
024000 9100-SQL-ABEND.                                                          
024100                                                                          
024500     DISPLAY '9100-SQL-ABEND'.                                            
024600     DISPLAY '** ABEND     **'.                                           
024700     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
024800     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
024900     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
025000                                                                          
025100******************************************************************        
025200* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
025300* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
025400* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
025500* FORMAT.                                                                 
025600******************************************************************        
025700     COPY DPPD004.                                                        
025800                                                                          
025900     MOVE +4013                  TO ABEND-CODE.                           
026000     CALL 'ILBOABN0' USING ABEND-CODE.                                    
