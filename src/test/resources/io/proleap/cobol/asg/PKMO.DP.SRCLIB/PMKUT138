      *----------------------------------------------------------               
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------               
       PROGRAM-ID.             PMKUT138.                                        
       AUTHOR.                 TODD C. MICHALEK.                                
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           AUGUST 2005.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      * EXTRACT PROMOTIONAL INFORMATION FOR VENDOR AD SPACE ASSIGNMENT *        
      * REPORTING                                                      *        
      ******************************************************************        
      * 09/30/2005 - TODD C. MICHALEK - WR29320                        *        
      * - NEW PROGRAM                                                  *        
      ******************************************************************        
      * 10/03/2005 - TODD C. MICHALEK - P000807446                     *        
      * - CHANGE THE PROGRAM TO RETURN VALID VALUES WHEN AD_PG_SEQ_NBR *        
      *   AND/OR AD_SP_CDE ARE/IS NULL                                 *        
      ******************************************************************        
      * 10/19/2005 - DARYL WOELFEL - P000813668                        *        
      * - USE 0 AS THE SIZE FOR FCVR, BCVR, AND SPRD, IN ORDER TO      *        
      *   WEED THESE OUT AS A TEMP FIX PER FINANCE'S REQUEST.  ALSO    *        
      *   WEED OUT NIGHT OWLS.                                         *        
      ******************************************************************        
      * 01/30/2006 - DARYL WOELFEL - WR30149                           *        
      * - PUT BACK THE ORIGINAL VALUES FOR FCVR, BCVR, AND SPRD,       *        
      *   PER FINANCE.                                                 *        
      ******************************************************************        
      * 01/31/2007 - JOHN BOLDT, STRATAGEM - WR32012                   *        
      * - CHANGE AD SPACE FOR SPRD IN VNDADSP CURSOR SQL FROM 1 TO 2   *        
      *   PER FINANCE.                                                 *        
      ******************************************************************        
INC681* 11/12/2012 - COGNIZANT (KARTHIK K) - INC000002422681           *        
INC681* - HANDLED NULL VALUE IN GMM, DMM & BUYER FIRST/LAST NAME       *        
INC681*   IN THE CURSOR VNDADSP                                        *        
INC681******************************************************************        
      * 10/29/2014 - COGNIZANT             - CHG0097324                *        
      * - MODIFIED THE PROGRAM TO FETCH THE DATA BASED ON PROMO END    *        
      *   TIMESTAMP                                                    *        
      ******************************************************************        
      * 07/30/2018 - COGNIZANT             - CHG0204126                *        
      * - ORACLE LOGON STANDARDS PROGRAM CHANGE                        *        
      ******************************************************************        
      *----------------------------------------------------------               
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------               
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT DATE-FILE            ASSIGN TO INP01.                         
           SELECT COST-FILE            ASSIGN TO INP02.                         
070941     SELECT ORACLE-LOGON-FILE                                             
070941         ASSIGN                      TO ORALOGON.                         
                                                                                
           SELECT VENDOR-AD-SPACE-FILE ASSIGN TO OUT01.                         
      *----------------------------------------------------------               
       DATA DIVISION.                                                           
      *----------------------------------------------------------               
       FILE SECTION.                                                            
                                                                                
       FD  DATE-FILE                                                            
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  DATE-REC                         PIC  X(80).                         
                                                                                
       FD  COST-FILE                                                            
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  COST-REC                         PIC  X(80).                         
                                                                                
       FD  VENDOR-AD-SPACE-FILE                                                 
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  VENDOR-AD-SPACE-REC              PIC  X(300).                        
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD           PIC X(80).                          
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------               
      * ABEND- ABEND VARIABLES                                                  
      *----------------------------------------------------------               
       01  ABEND-CODE                       PIC S9(04) COMP VALUE +0.           
           88  ABEND-4001-WRITE-ERROR                  VALUE +4001.             
           88  ABEND-4002-READ-ERROR                   VALUE +4002.             
           88  ABEND-4003-CTRL-CARD-ERROR              VALUE +4003.             
           88  ABEND-4004-TABLE-ERROR                  VALUE +4004.             
           88  ABEND-4005-OPEN-ERROR                   VALUE +4005.             
           88  ABEND-4006-CLOSE-ERROR                  VALUE +4006.             
           88  ABEND-4007-SEQUENCE-ERROR               VALUE +4007.             
           88  ABEND-4008-DATA-ERROR                   VALUE +4008.             
           88  ABEND-4009-KEY-DATA-ERROR               VALUE +4009.             
           88  ABEND-4013-SQL-ERROR                    VALUE +4013.             
           88  ABEND-4014-ORACLE-LOGON-ERROR           VALUE +4014.             
           88  ABEND-4019-DATE-ROUTINE-ERROR           VALUE +4019.             
           88  ABEND-4444-FORCED-ABEND                 VALUE +4444.             
      *----------------------------------------------------------               
      * DATE CONTROL CARD                                                       
      *----------------------------------------------------------               
       01  CC-DATE-REC.                                                         
           05  CC-START-DATE                  PIC  X(10).                       
           05  FILLER                         PIC  X(01).                       
           05  CC-END-DATE                    PIC  X(10).                       
           05  FILLER                         PIC  X(59).                       
      *----------------------------------------------------------               
      * VENDOR AD SPACE COST PER PAGE FILE                                      
      *----------------------------------------------------------               
           COPY PMRD140.                                                        
      *----------------------------------------------------------               
      * PS- PROGRAM SWITCHES                                                    
      *----------------------------------------------------------               
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-CC-DATE-SW                PIC  X(01) VALUE 'N'.               
               88  PS-END-OF-CC-DATE                   VALUE 'Y'.               
               88  PS-NOT-END-OF-CC-DATE               VALUE 'N'.               
           05  PS-VNDADSP-COST-FILE-SW      PIC  X(01) VALUE 'N'.               
               88  PS-END-OF-VNDADSP-COST-FILE         VALUE 'Y'.               
           05  PS-END-OF-VNDADSP-CSR-SW     PIC  X(01) VALUE 'N'.               
               88  PS-END-OF-VNDADSP-CSR               VALUE 'Y'.               
               88  PS-NOT-END-OF-VNDADSP-CSR           VALUE 'N'.               
070941     05  PS-END-OF-LOGON-FILE-SW      PIC X(1)   VALUE 'N'.               
070941         88  PS-END-OF-LOGON-FILE                VALUE 'Y'.               
070941     05  PS-ORA-SRC-CTL-DONE-SW       PIC X(1)   VALUE 'N'.               
070941         88  PS-ORA-SRC-EMPTY                    VALUE 'Y'.               
070941                                                                          
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID             PIC X(40).                          
070941         88 CC-AUTO-LOGON                        VALUE '/'.               
070941     05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
      *----------------------------------------------------------               
      * PC- PROGRAM CONSTANTS                                                   
      *----------------------------------------------------------               
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME      PIC  X(08) VALUE                    
                                                       'PMKUT138'.              
       01  CT-CPU-TIME.                                                         
           05  CT-CPU-HR                        PIC  9(02) VALUE ZERO.          
           05  CT-CPU-MIN                       PIC  9(02) VALUE ZERO.          
           05  CT-CPU-SEC                       PIC  9(02) VALUE ZERO.          
           05  CT-CPU-HSEC                      PIC  9(02) VALUE ZERO.          
                                                                                
       01  PV-CPU-TIME.                                                         
           05  PV-CPU-HR                        PIC  9(02) VALUE ZERO.          
           05  FILLER                           PIC  X(01) VALUE ":".           
           05  PV-CPU-MIN                       PIC  9(02) VALUE ZERO.          
           05  FILLER                           PIC  X(01) VALUE ":".           
           05  PV-CPU-SEC                       PIC  9(02) VALUE ZERO.          
           05  FILLER                           PIC  X(01) VALUE ":".           
           05  PV-CPU-HSEC                      PIC  9(02) VALUE ZERO.          
      *----------------------------------------------------------               
      * PV- PROGRAM VARIABLES                                                   
      *----------------------------------------------------------               
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN         PIC S9(4) VALUE ZEROS               
070941                                                COMP SYNC.                
070941     05  PV-ORACLE-PASSWORD-LEN       PIC S9(4) VALUE ZEROS               
070941                                                COMP SYNC.                
           05  PV-TABLE-NAME                PIC  X(30) VALUE SPACES.            
           05  PV-SQL-CODE                  PIC  9(09) VALUE 0.                 
           05  PV-OPERATION-MSG             PIC  X(60) VALUE SPACES.            
           05  PV-OPERATION-MSG-1           PIC  X(40) VALUE SPACES.            
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACES.            
                                                                                
       01  PV-SELECT-VARIABLES.                                                 
           05  PV-RECS-DIV                  PIC S9(09) VALUE 0.                 
           05  PV-RECS-REM                  PIC S9(09) VALUE 0.                 
      *----------------------------------------------------------               
      * PA- PROGRAM ACCUMULATORS                                                
      *----------------------------------------------------------               
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-VNDADSP-FETCH-COUNT       PIC  9(09) VALUE ZEROES.            
           05  PA-RECS-WRITTEN              PIC  9(09) VALUE ZEROES.            
      *----------------------------------------------------------               
      * COPYBOOK FOR THE VENDOR AD SPACE ASSIGNMENT EXTRACT FILE                
      *----------------------------------------------------------               
           COPY PMRD138.                                                        
      *----------------------------------------------------------               
      *                      O  R  A  C  L  E                                   
      *==========================================================               
      *                                                                         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *                                                                         
      *----------------------------------------------------------               
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
      *----------------------------------------------------------               
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *----------------------------------------------------------               
       01  PC-ENT01-TYPE-CDE                PIC  X(02)  VALUE '01'.             
       01  PV-ORACLE-ID                     PIC  X(1)   VALUE '/'.              
                                                                                
       01  USERNAME                         PIC  X(10)  VARYING.                
       01  PASSWD                           PIC  X(10)  VARYING.                
                                                                                
       01  ORACLE-RETURN                    PIC  X(200) VALUE SPACES.           
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
      *----------------------------------------------------------               
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------               
       0000-MAINLINE.                                                           
           PERFORM 1000-INITIALIZATION.                                         
           PERFORM 2100-OPEN-VNDADSP-CSR.                                       
           PERFORM 2110-FETCH-VNDADSP-CSR.                                      
           PERFORM 2000-PROCESS-VNDADSP-CSR                                     
                   UNTIL PS-END-OF-VNDADSP-CSR.                                 
           PERFORM 2120-CLOSE-VNDADSP-CSR.                                      
           PERFORM 9999-WRAPUP.                                                 
           GOBACK.                                                              
      *----------------------------------------------------------               
      * 1000-INITIALIZATION.                                                    
      *----------------------------------------------------------               
       1000-INITIALIZATION.                                                     
           OPEN INPUT  DATE-FILE                                                
                       COST-FILE                                                
                OUTPUT VENDOR-AD-SPACE-FILE.                                    
070941     PERFORM 1150-ORACLE-SETUP.                                           
           PERFORM 1050-READ-DATE-FILE.                                         
           PERFORM 1100-READ-VNDADSP-COST-FILE.                                 
           PERFORM 1300-DECLARE-VNDADSP-CSR.                                    
      *----------------------------------------------------------               
      * 1050-READ-DATE-FILE.                                                    
      *----------------------------------------------------------               
       1050-READ-DATE-FILE.                                                     
           READ DATE-FILE INTO CC-DATE-REC                                      
              AT END                                                            
                 SET PS-END-OF-CC-DATE TO TRUE                                  
           END-READ.                                                            
                                                                                
           IF PS-END-OF-CC-DATE                                                 
               MOVE '1050-READ-DATE-FILE' TO PV-PARAGRAPH-NAME                  
               MOVE 'NO DATE RANGE CONTROL CARD'                                
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM 9800-ABEND                                               
           ELSE                                                                 
               DISPLAY 'START DATE = ' CC-START-DATE                            
               DISPLAY 'END DATE   = ' CC-END-DATE                              
           END-IF.                                                              
      *---------------------------------------------------------------          
      * 1100-READ-VNDADSP-COST-FILE.                                            
      *---------------------------------------------------------------          
       1100-READ-VNDADSP-COST-FILE.                                             
           READ COST-FILE INTO PM140-COST-REC                                   
                AT END SET PS-END-OF-VNDADSP-COST-FILE TO TRUE.                 
                                                                                
           IF PS-END-OF-VNDADSP-COST-FILE                                       
               MOVE '1100-READ-VNDADSP-COST-FILE' TO PV-PARAGRAPH-NAME          
               MOVE 'NO VENDOR AD SPACE COST FILE'                              
                                        TO PV-OPERATION-MSG-1                   
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM 9800-ABEND                                               
           ELSE                                                                 
               DISPLAY 'PM140-COST-SUNDAY-STD     = '                           
                        PM140-COST-SUNDAY-STD                                   
               DISPLAY 'PM140-COST-SUNDAY-TAB     = '                           
                        PM140-COST-SUNDAY-TAB                                   
               DISPLAY 'PM140-COST-SUNDAY-FLEXI   = '                           
                        PM140-COST-SUNDAY-FLEXI                                 
               DISPLAY 'PM140-COST-WEEKEND-STD    = '                           
                        PM140-COST-WEEKEND-STD                                  
               DISPLAY 'PM140-COST-WEEKEND-TAB    = '                           
                        PM140-COST-WEEKEND-TAB                                  
               DISPLAY 'PM140-COST-WEEKEND-FLEXI  = '                           
                        PM140-COST-WEEKEND-FLEXI                                
               DISPLAY 'PM140-COST-MID-WEEK-STD   = '                           
                        PM140-COST-MID-WEEK-STD                                 
               DISPLAY 'PM140-COST-MID-WEEK-TAB   = '                           
                        PM140-COST-MID-WEEK-TAB                                 
               DISPLAY 'PM140-COST-MID-WEEK-FLEXI = '                           
                        PM140-COST-MID-WEEK-FLEXI                               
           END-IF.                                                              
070941                                                                          
070941 1150-ORACLE-SETUP.                                                       
070941                                                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     PERFORM 1200-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-CURRENT-PROGRAM-NAME                                  
070941                ' - ORACLE LOGON CONTROL CARD NOT PROVIDED'               
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-CURRENT-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'        
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-CURRENT-PROGRAM-NAME ' -LOGON TO ORACLE AS USER '        
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-CURRENT-PROGRAM-NAME                                  
070941         ' - ERROR LOGGING ON TO ORACLE'                                  
070941         PERFORM Z100-ABEND                                               
070941     END-IF.                                                              
070941                                                                          
070941******************************************************************        
070941* NEEDS TO BE IN FOR NOW TIL KURT FIXES CURSOR SHARING BUG                
070941* OTHERWISE GET AN ORACLE ERROR (IE: 3113).                               
070941******************************************************************        
070941     EXEC SQL                                                             
070941      ALTER SESSION                                                       
070941            SET CURSOR_SHARING = 'EXACT'                                  
070941     END-EXEC.                                                            
070941                                                                          
070941                                                                          
070941*****************************************************************         
070941*  1200-READ-ORACLE-LOGON.                                      *         
070941* ==> PROCESSES:                                                *         
070941*   - READ ORCALE CONTROL CARD                                  *         
070941*****************************************************************         
070941 1200-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
                                                                                
      *--------------------------------------------------------------           
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT              
      * LOGS ON TO ORACLE.                                                      
      * FOR TESTING CHANGE PMPD700 TO PMPD70B                                   
      *---------------------------------------------------------------          
070941*    COPY PMPD700.                                                        
      *---------------------------------------------------------------          
      * 1300-DECLARE-VNDADSP-CSR.                                               
      *---------------------------------------------------------------          
       1300-DECLARE-VNDADSP-CSR.                                                
           EXEC SQL                                                             
                DECLARE VNDADSP CURSOR FOR                                      
                   SELECT                                                       
                       GMA_NBR                                                  
INC681*               ,GMM_FRST_NM                                              
INC681                ,NVL(GMM_FRST_NM,' ')                                     
INC681*               ,GMM_LAST_NM                                              
INC681                ,NVL(GMM_LAST_NM,' ')                                     
                      ,DMA_NBR                                                  
INC681*               ,DMM_FRST_NM                                              
INC681                ,NVL(DMM_FRST_NM,' ')                                     
INC681*               ,DMM_LAST_NM                                              
INC681                ,NVL(DMM_LAST_NM,' ')                                     
                      ,BYR_NBR                                                  
INC681*               ,BYR_FRST_NM                                              
INC681                ,NVL(BYR_FRST_NM,' ')                                     
INC681*               ,BYR_LAST_NM                                              
INC681                ,NVL(BYR_LAST_NM,' ')                                     
                      ,VND_NBR                                                  
                      ,PRIM_VND_NM                                              
                      ,SECTION                                                  
                      ,SECTION_SORT                                             
                      ,MDA_SIZE                                                 
                      ,MDA_SIZE_SORT                                            
                      ,DECODE(SECTION                                           
                       ,'SUNDAY'                                                
                       ,DECODE(MDA_SIZE,'STD',:PM140-COST-SUNDAY-STD            
                               ,'TAB',:PM140-COST-SUNDAY-TAB                    
                               ,'FLEXI',:PM140-COST-SUNDAY-FLEXI,0)             
                       ,'WEEKEND'                                               
                       ,DECODE(MDA_SIZE,'STD',:PM140-COST-WEEKEND-STD           
                               ,'TAB',:PM140-COST-WEEKEND-TAB                   
                               ,'FLEXI',:PM140-COST-WEEKEND-FLEXI,0)            
                       ,'MID-WEEK'                                              
                       ,DECODE(MDA_SIZE,'STD',:PM140-COST-MID-WEEK-STD          
                               ,'TAB',:PM140-COST-MID-WEEK-TAB                  
                               ,'FLEXI',:PM140-COST-MID-WEEK-FLEXI,0)           
                       ,0) COST_PER_PG                                          
                      ,AD_EVNT_NBR_TXT                                          
                      ,AD_EVNT_NM                                               
                      ,PROMO_NBR_TXT                                            
                      ,PROMO_NM                                                 
                      ,TO_CHAR(PROMO_DFLT_STRT_TMST                             
                               ,'MM/DD/YYYY HH24:MI') PROMO_STRT_TMST           
                      ,TO_CHAR(PROMO_DFLT_END_TMST                              
                               ,'MM/DD/YYYY HH24:MI') PROMO_END_TMST            
                      ,MDA_PG_QTY                                               
                      ,AD_PG_NBR                                                
                      ,NVL(AD_PG_SEQ_NBR,0)                                     
                      ,NVL(AD_SP_CDE,' ')                                       
                      ,AD_SP_SZ                                                 
                      ,DECODE(SECTION                                           
                       ,'SUNDAY'                                                
                       ,DECODE(MDA_SIZE                                         
                       ,'STD',:PM140-COST-SUNDAY-STD * AD_SP_SZ                 
                       ,'TAB',:PM140-COST-SUNDAY-TAB * AD_SP_SZ                 
                        ,'FLEXI',:PM140-COST-SUNDAY-FLEXI * AD_SP_SZ,0)         
                       ,'WEEKEND'                                               
                       ,DECODE(MDA_SIZE                                         
                       ,'STD',:PM140-COST-WEEKEND-STD * AD_SP_SZ                
                       ,'TAB',:PM140-COST-WEEKEND-TAB * AD_SP_SZ                
                       ,'FLEXI',:PM140-COST-WEEKEND-FLEXI * AD_SP_SZ,0)         
                       ,'MID-WEEK'                                              
                       ,DECODE(MDA_SIZE                                         
                       ,'STD',:PM140-COST-MID-WEEK-STD * AD_SP_SZ               
                       ,'TAB',:PM140-COST-MID-WEEK-TAB * AD_SP_SZ               
                       ,'FLEXI',:PM140-COST-MID-WEEK-FLEXI * AD_SP_SZ,0)        
                       ,0) EST_COST                                             
                   FROM                                                         
                   (                                                            
                   SELECT /*+ ORDERED */ DISTINCT                               
                       PRSN.GMA_NBR                                             
                      ,PRSN.GMM_FRST_NM                                         
                      ,PRSN.GMM_LAST_NM                                         
                      ,PRSN.DMA_NBR                                             
                      ,PRSN.DMM_FRST_NM                                         
                      ,PRSN.DMM_LAST_NM                                         
                      ,PRSN.BYR_NBR                                             
                      ,PRSN.BYR_FRST_NM                                         
                      ,PRSN.BYR_LAST_NM                                         
                      ,VND.VND_NBR                                              
                      ,VND.PRIM_VND_NM                                          
                      ,DECODE(TO_CHAR(PROMO.PROMO_DFLT_STRT_TMST,'DY')          
                           ,'SUN','SUNDAY'                                      
                           ,'MON','MID-WEEK'                                    
                           ,'TUE','MID-WEEK'                                    
                           ,'WED','MID-WEEK'                                    
                           ,'THU','WEEKEND'                                     
                           ,'FRI','WEEKEND'                                     
                           ,'SAT','WEEKEND'                                     
                           ,'UNKNOWN') SECTION                                  
                      ,DECODE(TO_CHAR(PROMO.PROMO_DFLT_STRT_TMST,'DY')          
                           ,'SUN',1                                             
                           ,'MON',3                                             
                           ,'TUE',3                                             
                           ,'WED',3                                             
                           ,'THU',2                                             
                           ,'FRI',2                                             
                           ,'SAT',2                                             
                           ,4) SECTION_SORT                                     
                      ,DECODE(VER.MDA_SIZE_ID                                   
                           ,62,'STD'                                            
                           ,63,'TAB'                                            
                           ,64,'FLEXI'                                          
                           ,503,'POLY'                                          
                           ,'UNKNOWN') MDA_SIZE                                 
                      ,DECODE(VER.MDA_SIZE_ID                                   
                           ,62,1                                                
                           ,63,2                                                
                           ,64,3                                                
                           ,503,4                                               
                           ,5) MDA_SIZE_SORT                                    
                      ,PROMO.AD_EVNT_NBR_TXT                                    
                      ,PROMO.AD_EVNT_NM                                         
                      ,REG.PROMO_NBR_TXT                                        
                      ,PROMO.PROMO_NM                                           
                      ,PROMO.PROMO_DFLT_STRT_TMST                               
                      ,PROMO.PROMO_DFLT_END_TMST                                
                      ,RLT.MDA_PG_QTY                                           
                      ,ADSP.AD_PG_NBR                                           
                      ,ADSP.AD_PG_SEQ_NBR                                       
                      ,ADSP.AD_SP_CDE                                           
                      ,DECODE(TRIM(ADSP.AD_SP_CDE)                              
                           ,'FCVR',1.000                                        
                           ,'BCVR',1.000                                        
                           ,'SPRD',2.000                                        
                           ,'FULL',1.000                                        
                           ,'HALF',0.500                                        
                           ,'THRD',0.333                                        
                           ,'QTR',0.250                                         
                           ,'1/6',0.166                                         
                           ,'1/8',0.125                                         
                           ,'1/9',0.111                                         
                           ,'1/12',0.083                                        
                           ,'1/16',0.062                                        
                           ,'PBON',0.111                                        
                           ,0.000) AD_SP_SZ                                     
                   FROM                                                         
                       PMV_PROMO_DIM PROMO                                      
                      ,PMT_PROMO_REG REG                                        
                      ,PMT_SKU_LVL_PRICG_BY_LOC PRICG                           
                      ,PMT_AD_SP_ASGMT ADSP                                     
                      ,PMT_PROMO_PRT_VER VER                                    
                      ,PMT_PROMO_PRT_VER_RLT RLT                                
                      ,XKT_VND_STYL STYL                                        
                      ,XKT_VND VND                                              
                      ,XKT_VND_TYP_VND VNDTYP                                   
                      ,PMV_PRSN_MDSE_HIER PRSN                                  
                   WHERE                                                        
                       PROMO.PROMO_ID = REG.PROMO_ID                            
                   AND PROMO.PROMO_ID = PRICG.PROMO_ID                          
                   AND PRICG.PROMO_ITM_ID = ADSP.PROMO_ITM_ID                   
                   AND ADSP.PROMO_PRT_VER_ID = VER.PROMO_PRT_VER_ID             
                   AND VER.PROMO_PRT_VER_ID = RLT.PROMO_PRT_VER_ID              
                   AND PRICG.DEPT_NBR = STYL.DEPT_NBR                           
                   AND PRICG.MAJ_CL_NBR = STYL.MAJ_CL_NBR                       
                   AND PRICG.SUB_CL_NBR = STYL.SUB_CL_NBR                       
                   AND PRICG.VS_ID = STYL.VS_ID                                 
                   AND STYL.ENT_ID = VND.ENT_ID                                 
                   AND VND.ENT_ID = VNDTYP.ENT_ID                               
                   AND STYL.DEPT_NBR = PRSN.DEPT_NBR                            
                   AND TRUNC(PROMO.PROMO_DFLT_END_TMST)                         
                       BETWEEN TO_DATE(:CC-START-DATE,'MM/DD/YYYY')             
                           AND TO_DATE(:CC-END-DATE,'MM/DD/YYYY')               
                   AND PRICG.LOC_NBR IN (222,447,620,654,951)                   
                   AND ADSP.PRIM_PRT_VER_IND = 'Y'                              
                   AND VND.STAT_CDE = 'A'                                       
                   AND VNDTYP.VND_TYP_CDE = 'M'                                 
                   AND PROMO.PROMO_NM <> 'NIGHT OWLS'                           
                   AND PROMO.AD_EVNT_STAT_CDE = 'S'                             
                   AND PROMO.PROMO_STAT_CDE = 'S'                               
                   AND ADSP.STAT_CDE = 'S'                                      
                   AND VER.STAT_CDE = 'S'                                       
                   AND RLT.STAT_CDE = 'S'                                       
                   )                                                            
                   ORDER BY                                                     
                       GMA_NBR                                                  
                      ,DMA_NBR                                                  
                      ,BYR_NBR                                                  
                      ,PRIM_VND_NM                                              
                      ,SECTION_SORT                                             
                      ,MDA_SIZE_SORT                                            
                      ,PROMO_DFLT_STRT_TMST                                     
                      ,PROMO_DFLT_END_TMST DESC                                 
                      ,AD_PG_NBR                                                
                      ,AD_PG_SEQ_NBR                                            
           END-EXEC.                                                            
      *----------------------------------------------------------               
      * 2000-PROCESS-VNDADSP-CSR                                                
      *----------------------------------------------------------               
       2000-PROCESS-VNDADSP-CSR.                                                
           PERFORM 9000-WRITE-VENDOR-AD-SPACE-REC.                              
           PERFORM 2110-FETCH-VNDADSP-CSR.                                      
      *----------------------------------------------------------               
      * 2100-OPEN-VNDADSP-CSR                                                   
      *----------------------------------------------------------               
       2100-OPEN-VNDADSP-CSR.                                                   
           PERFORM 9200-GET-TIME.                                               
           DISPLAY 'OPENING CURSOR: ' PV-CPU-TIME.                              
                                                                                
           EXEC SQL                                                             
                 OPEN VNDADSP                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    SET PS-NOT-END-OF-VNDADSP-CSR TO TRUE                       
                    PERFORM 9200-GET-TIME                                       
                    DISPLAY 'CURSOR OPENED: ' PV-CPU-TIME                       
               WHEN OTHER                                                       
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE SQLCODE             TO PV-SQL-CODE                     
                    MOVE '2100-OPEN-VNDADSP-CSR'                                
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'OPEN ERROR ON VNDADSP CURSOR'                         
                                      TO PV-OPERATION-MSG                       
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
      *----------------------------------------------------------               
      * 2110-FETCH-VNDADSP-CSR                                                  
      *----------------------------------------------------------               
       2110-FETCH-VNDADSP-CSR.                                                  
           EXEC SQL                                                             
               FETCH VNDADSP                                                    
                 INTO :PM138-GMA-NBR                                            
                     ,:PM138-GMM-FIRST-NM                                       
                     ,:PM138-GMM-LAST-NM                                        
                     ,:PM138-DMA-NBR                                            
                     ,:PM138-DMM-FIRST-NM                                       
                     ,:PM138-DMM-LAST-NM                                        
                     ,:PM138-BYR-NBR                                            
                     ,:PM138-BYR-FIRST-NM                                       
                     ,:PM138-BYR-LAST-NM                                        
                     ,:PM138-VND-NBR                                            
                     ,:PM138-VND-NM                                             
                     ,:PM138-SECTION                                            
                     ,:PM138-SECTION-SORT                                       
                     ,:PM138-MDA-SIZE                                           
                     ,:PM138-MDA-SIZE-SORT                                      
                     ,:PM138-COST-PER-PAGE                                      
                     ,:PM138-AD-EVNT-NBR-TXT                                    
                     ,:PM138-AD-EVNT-NM                                         
                     ,:PM138-PROMO-NBR-TXT                                      
                     ,:PM138-PROMO-NM                                           
                     ,:PM138-PROMO-STRT-TMST                                    
                     ,:PM138-PROMO-END-TMST                                     
                     ,:PM138-MDA-PG-QTY                                         
                     ,:PM138-AD-PG-NBR                                          
                     ,:PM138-AD-PG-SEQ-NBR                                      
                     ,:PM138-AD-SP-CDE                                          
                     ,:PM138-AD-SP-SIZE                                         
                     ,:PM138-EST-COST                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    ADD +1 TO PA-VNDADSP-FETCH-COUNT                            
               WHEN SQLCODE = 1403                                              
                    SET PS-END-OF-VNDADSP-CSR                                   
                                       TO TRUE                                  
               WHEN OTHER                                                       
                    MOVE SQLCODE       TO PV-SQL-CODE                           
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '2110-FETCH-VNDADSP-CSR'                               
                                       TO PV-PARAGRAPH-NAME                     
                    MOVE 'FETCH ERROR ON VNDADSP CURSOR'                        
                                       TO PV-OPERATION-MSG                      
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
      *----------------------------------------------------------               
      * 2120-CLOSE-VNDADSP-CSR                                                  
      *----------------------------------------------------------               
       2120-CLOSE-VNDADSP-CSR.                                                  
           EXEC SQL                                                             
                CLOSE VNDADSP                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '2120-CLOSE-VNDADSP-CSR'                               
                                     TO PV-PARAGRAPH-NAME                       
                    MOVE 'CLOSE ERROR ON VNDADSP CURSOR'                        
                                     TO PV-OPERATION-MSG                        
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
      *----------------------------------------------------------               
      * 9000-WRITE-VENDOR-AD-SPACE-REC                                          
      *     WRITES THE MERCHANDISE RECORD TO FILE.                              
      *----------------------------------------------------------               
       9000-WRITE-VENDOR-AD-SPACE-REC.                                          
           WRITE VENDOR-AD-SPACE-REC FROM PM138-VENDOR-ADSP-REC.                
                                                                                
           ADD +1 TO PA-RECS-WRITTEN.                                           
                                                                                
           DIVIDE PA-RECS-WRITTEN BY 50000                                      
             GIVING PV-RECS-DIV                                                 
             REMAINDER PV-RECS-REM                                              
           IF PV-RECS-REM = 0                                                   
             PERFORM 9200-GET-TIME                                              
             DISPLAY 'RECS WRITTEN: ' PA-RECS-WRITTEN '  ' PV-CPU-TIME          
           END-IF.                                                              
      *****************************************************************         
      * 9200-GET-TIME                                                 *         
      *****************************************************************         
       9200-GET-TIME.                                                           
           ACCEPT CT-CPU-TIME FROM TIME.                                        
           MOVE CT-CPU-HR     TO  PV-CPU-HR.                                    
           MOVE CT-CPU-MIN    TO  PV-CPU-MIN.                                   
           MOVE CT-CPU-SEC    TO  PV-CPU-SEC.                                   
           MOVE CT-CPU-HSEC   TO  PV-CPU-HSEC.                                  
      *----------------------------------------------------------               
      * 9800-ABEND.                                                             
      *     PERFORMS A NON-SQL-ABEND.                                           
      *----------------------------------------------------------               
       9800-ABEND.                                                              
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
      *----------------------------------------------------------               
      * 9900-ORACLE-ABEND.                                                      
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       9900-ORACLE-ABEND.                                                       
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
           DISPLAY '** SQLCODE        ** = ' PV-SQL-CODE.                       
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
      *-----------------------------------------------------------              
      * Z100-ABEND                                                              
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE                      
      *  - CALLED BY PEPD700!                                                   
      *-----------------------------------------------------------              
       Z100-ABEND.                                                              
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                           
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
      *----------------------------------------------------------               
      * 9999-WRAPUP                                                             
      *----------------------------------------------------------               
       9999-WRAPUP.                                                             
           CLOSE DATE-FILE                                                      
                 COST-FILE                                                      
                 VENDOR-AD-SPACE-FILE.                                          
           DISPLAY ' '.                                                         
           DISPLAY 'NUMBER OF VNDADSP RECS FETCHED...: '                        
                                       PA-VNDADSP-FETCH-COUNT.                  
           DISPLAY 'NUMBER OF VNDADSP RECS WRITTEN...: '                        
                                       PA-RECS-WRITTEN.                         
