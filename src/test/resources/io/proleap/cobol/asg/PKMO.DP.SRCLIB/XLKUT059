000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400 PROGRAM-ID.    XLKUT059.                                                 
000500 AUTHOR.        PAUL OMAN.                                                
000600 INSTALLATION.  KOHLS.                                                    
000700 DATE-WRITTEN   APRIL 2003.                                               
000800 DATE-COMPILED.                                                           
000900                                                                          
001000*****************************************************************         
001100*    PROGRAM COMPARES RMS AND DOMAIN INFORMATION BY THE PACK    *         
001200*    SKU NUMBER AND THE UPC NUMBER. ANY RECORDS ON              *         
001210*    RMS, BUT NOT ON DOMAIN OR ON DOMAIN BUT NOT ON RMS ARE     *         
001220*    WRITTEN TO AN ERROR FILE.  ANY RECORDS THAT MATCH ON THE   *         
001230*    BOTH OF THE KEY FIELDS, BUT HAS DATA THAT DOESN'T MATCH    *         
001240*    WILL BE WRITTEN TO AN ERROR FILE.                          *         
001300*                                                               *         
001500*****************************************************************         
001600 ENVIRONMENT DIVISION.                                                    
001700 INPUT-OUTPUT SECTION.                                                    
001800                                                                          
001900 FILE-CONTROL.                                                            
002000                                                                          
002100     SELECT RMS-SKU-FILE                                                  
002200         ASSIGN TO INP01.                                                 
002300                                                                          
002400     SELECT LOGISTIC-SKU-FILE                                             
002500         ASSIGN TO INP02.                                                 
002600                                                                          
002700     SELECT ERROR-FILE                                                    
002800         ASSIGN TO OUT01.                                                 
002900                                                                          
003000*****************************************************************         
003100 DATA DIVISION.                                                           
003200*****************************************************************         
003300 FILE SECTION.                                                            
003400                                                                          
003500 FD  RMS-SKU-FILE                                                         
003600     RECORDING MODE IS F                                                  
003700     BLOCK CONTAINS 0 RECORDS                                             
003800     LABEL RECORDS ARE STANDARD.                                          
003900                                                                          
004000 01  RMS-SKU-RECORD               PIC X(144).                             
004100                                                                          
004200 FD  LOGISTIC-SKU-FILE                                                    
004300     RECORDING MODE IS F                                                  
004400     BLOCK CONTAINS 0 RECORDS                                             
004500     LABEL RECORDS ARE STANDARD.                                          
004600                                                                          
004700 01  LOGISTIC-SKU-RECORD          PIC X(144).                             
004800                                                                          
004900                                                                          
005000 FD  ERROR-FILE                                                           
005100     RECORDING MODE IS F                                                  
005200     BLOCK CONTAINS 0 RECORDS                                             
005300     LABEL RECORDS ARE STANDARD.                                          
005400                                                                          
005500 01  ERROR-RECORD                 PIC X(144).                             
005600                                                                          
005700                                                                          
005800*****************************************************************         
005900 WORKING-STORAGE SECTION.                                                 
006000                                                                          
006100 01  PS-PROGRAM-SWITCHES.                                                 
006200     05  PS-EOF-INPUT1-SW             PIC X(01)  VALUE 'N'.               
006300         88  PS-EOF-INPUT1-YES                   VALUE 'Y'.               
006400         88  PS-EOF-INPUT1-NO                    VALUE 'N'.               
006500     05  PS-EOF-INPUT2-SW             PIC X(01)  VALUE 'N'.               
006600         88  PS-EOF-INPUT2-YES                   VALUE 'Y'.               
006700         88  PS-EOF-INPUT2-NO                    VALUE 'N'.               
006800     05  PS-EMAIL-SW                  PIC X(01)  VALUE 'N'.               
006900         88  PS-EMAIL-YES                        VALUE 'Y'.               
007000         88  PS-EMAIL-NO                         VALUE 'N'.               
007100                                                                          
007200 01  PC-PROGRAM-CONSTANTS.                                                
007300     05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'XLKUT059'.        
007400     05  PC-FIVE-BLANKS               PIC  X(05) VALUE SPACES.            
007500                                                                          
007600 01  PV-PROGRAM-VARIABLES.                                                
007700     05  PV-RECORDS-READ-1            PIC  9(09) VALUE  0.                
007800     05  PV-RECORDS-READ-2            PIC  9(09) VALUE  0.                
007900     05  PV-RECORDS-WRITTEN           PIC  9(09) VALUE  0.                
008000     05  PV-RECORDS-ON-BOTH           PIC  9(09) VALUE  0.                
008100     05  PV-RECORDS-ON-RMS-ONLY       PIC  9(09) VALUE  0.                
008200     05  PV-RECORDS-ON-LEGACY-ONLY    PIC  9(09) VALUE  0.                
008300     05  PV-RECORDS-DATA-MISMATCH     PIC  9(09) VALUE  0.                
008400     05  PV-ALL-NINES                 PIC  9(09) VALUE 99999999.          
008800                                                                          
008900 01  PV-ABEND-FIELDS.                                                     
009000     05  PV-ABEND-CODE                PIC  S9(4) VALUE +0                 
009100                                                 COMP SYNC.               
009200     05  PV-ABEND-PROGRAM             PIC  X(08) VALUE 'XLKUT059'.        
009300     05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.            
009400     05  PV-ABEND-OPERATION           PIC  X(50) VALUE SPACES.            
009500     05  PV-ABEND-TABLE               PIC  X(12) VALUE SPACES.            
009600     05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.            
009700     05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.            
009800     05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.            
009900                                                                          
010000*--------------------------------------------------------------*          
010100*    IMRD031 - INPUT AND OUTPUT RECORD LAYOUT FOR PACK DATA.   *          
010200*                                                              *          
010300*--------------------------------------------------------------*          
010400                                                                          
010500 01  PV-RMS-INPUT.                                                        
010600     COPY XLRD031.                                                        
010700                                                                          
010800 01  PV-LOGISTIC-INPUT.                                                   
010900     COPY XLRD031.                                                        
011000                                                                          
011100*****************************************************************         
011200 PROCEDURE DIVISION.                                                      
011300*****************************************************************         
011400     DISPLAY ' '.                                                         
011500     DISPLAY '**** BEGINNING XLKUT059 ****'.                              
011600     DISPLAY ' '.                                                         
011700                                                                          
011800     OPEN INPUT  RMS-SKU-FILE                                             
011900                 LOGISTIC-SKU-FILE                                        
012000          OUTPUT ERROR-FILE.                                              
012100     SET PS-EMAIL-NO       TO TRUE.                                       
012200                                                                          
012300     PERFORM 1000-READ-RMS-INPUT.                                         
012400     PERFORM 1100-READ-LOGISTIC-INPUT.                                    
012500                                                                          
012600     PERFORM 2000-PROCESS-INPUT-FILE                                      
012700       UNTIL PS-EOF-INPUT1-YES AND                                        
012800             PS-EOF-INPUT2-YES.                                           
012900                                                                          
013000     CLOSE RMS-SKU-FILE                                                   
013100           LOGISTIC-SKU-FILE                                              
013200           ERROR-FILE.                                                    
013300                                                                          
013400     DISPLAY ' '.                                                         
013500     DISPLAY '  INPUT RMS SKU REC    = '                                  
013600              PV-RECORDS-READ-1.                                          
013700     DISPLAY '  INPUT DOMAIN SKU REC = '                                  
013800              PV-RECORDS-READ-2.                                          
013900     DISPLAY ' '.                                                         
014000     DISPLAY 'FILES ARE MATCHED BY PACK SKU NUMBER'.                      
014100     DISPLAY '                     UPC NUMBER     '.                      
014200     DISPLAY ' '.                                                         
014300     DISPLAY PC-FIVE-BLANKS                                               
014400             'RECORDS ON BOTH FILES ---------> '                          
014500              PV-RECORDS-ON-BOTH.                                         
014600     DISPLAY ' '.                                                         
014700     DISPLAY '  OUTPUT RECORDS WRITTEN ------> '                          
014800              PV-RECORDS-WRITTEN.                                         
014900     DISPLAY PC-FIVE-BLANKS                                               
015000             'RECORDS ON RMS ONLY -----------> '                          
015100              PV-RECORDS-ON-RMS-ONLY.                                     
015200     DISPLAY PC-FIVE-BLANKS                                               
015300             'RECORDS ON LEGACY ONLY --------> '                          
015400              PV-RECORDS-ON-LEGACY-ONLY.                                  
015500     DISPLAY PC-FIVE-BLANKS                                               
015600             'RECORDS WITH DATA MISMATCH ----> '                          
015700              PV-RECORDS-DATA-MISMATCH.                                   
015800                                                                          
015900     DISPLAY ' '.                                                         
016000     DISPLAY '****  END OF  XLKUT059  ****'.                              
016100     DISPLAY ' '.                                                         
016200                                                                          
016300*    SET RETURN CODE IF E-MAIL SWITCH IS TRUE.                            
016400     IF PS-EMAIL-YES                                                      
016500         MOVE 4095 TO RETURN-CODE                                         
016600     END-IF.                                                              
016700     GOBACK.                                                              
016800                                                                          
016900*****************************************************************         
017000*  READ A RMS SKU RECORD                                                  
017100*****************************************************************         
017200 1000-READ-RMS-INPUT.                                                     
017300                                                                          
017400     MOVE '1000-READ-INPUT-FILE    ' TO PV-ABEND-PARAGRAPH.               
017500                                                                          
017600     INITIALIZE PV-RMS-INPUT.                                             
017700                                                                          
017800     READ RMS-SKU-FILE                                                    
017900          INTO PV-RMS-INPUT                                               
018000            AT END                                                        
018100               SET PS-EOF-INPUT1-YES TO TRUE                              
018200     END-READ.                                                            
018300                                                                          
018400     IF PS-EOF-INPUT1-YES                                                 
018500        MOVE PV-ALL-NINES TO XL031-UPC-PK-SKU-NBR                         
018600                          OF PV-RMS-INPUT                                 
018700        MOVE PV-ALL-NINES TO XL031-UPC-UPC-NBR                            
018800                          OF PV-RMS-INPUT                                 
018900     ELSE                                                                 
019000        ADD +1 TO PV-RECORDS-READ-1                                       
019100     END-IF.                                                              
019200                                                                          
019300*****************************************************************         
019400*  READ A LOGISTIC SKU RECORD                                             
019500*****************************************************************         
019600 1100-READ-LOGISTIC-INPUT.                                                
019700                                                                          
019800     MOVE '1100-READ-LOGISTIC-INPUT' TO PV-ABEND-PARAGRAPH.               
019900                                                                          
020000     INITIALIZE PV-LOGISTIC-INPUT.                                        
020100                                                                          
020200     READ LOGISTIC-SKU-FILE                                               
020300          INTO PV-LOGISTIC-INPUT                                          
020400            AT END                                                        
020500               SET PS-EOF-INPUT2-YES TO TRUE                              
020600     END-READ.                                                            
020700                                                                          
020800     IF PS-EOF-INPUT2-YES                                                 
020900        MOVE PV-ALL-NINES TO XL031-UPC-PK-SKU-NBR                         
021000                          OF PV-LOGISTIC-INPUT                            
021100        MOVE PV-ALL-NINES TO XL031-UPC-UPC-NBR                            
021200                          OF PV-LOGISTIC-INPUT                            
021300     ELSE                                                                 
021400        ADD +1 TO PV-RECORDS-READ-2                                       
021500     END-IF.                                                              
021600                                                                          
021700*****************************************************************         
021800*                                                               *         
021900*****************************************************************         
022000 2000-PROCESS-INPUT-FILE.                                                 
022100                                                                          
022200     MOVE '2000-PROCESS-INPUT-FILE ' TO PV-ABEND-PARAGRAPH.               
022300                                                                          
022400     EVALUATE TRUE                                                        
022500         WHEN XL031-UPC-PK-SKU-NBR OF PV-RMS-INPUT <                      
022600              XL031-UPC-PK-SKU-NBR OF PV-LOGISTIC-INPUT                   
022700*             ************************************                        
022800*             *      ON RMS NOT ON DOMAIN                                 
022900*             ************************************                        
023000              ADD 1 TO PV-RECORDS-ON-RMS-ONLY                             
025800              MOVE 'NOT ON DOMAIN PACK UPC'                               
                                    TO XL031-UPC-ERROR-MESSAGE                  
025800                              OF PV-RMS-INPUT                             
023100              PERFORM 2100-WRITE-RMS-ERROR                                
023200              PERFORM 1000-READ-RMS-INPUT                                 
023300                                                                          
023400         WHEN XL031-UPC-PK-SKU-NBR OF PV-RMS-INPUT >                      
023500              XL031-UPC-PK-SKU-NBR OF PV-LOGISTIC-INPUT                   
023600*             ************************************                        
023700*             *       ON DOMAIN NOT ON RMS                                
023800*             ************************************                        
023900              ADD 1 TO PV-RECORDS-ON-LEGACY-ONLY                          
025800              MOVE 'NOT ON RMS PACK UPC'                                  
                                          TO XL031-UPC-ERROR-MESSAGE            
025800                                    OF PV-LOGISTIC-INPUT                  
024000              PERFORM 2150-WRITE-LOGISTIC-ERROR                           
024100              PERFORM 1100-READ-LOGISTIC-INPUT                            
024200                                                                          
024300         WHEN XL031-UPC-UPC-NBR OF PV-RMS-INPUT <                         
024400              XL031-UPC-UPC-NBR OF PV-LOGISTIC-INPUT                      
024500*             ************************************                        
024600*             *      ON RMS NOT ON DOMAIN                                 
024700*             ************************************                        
024800              ADD 1 TO PV-RECORDS-ON-RMS-ONLY                             
025800              MOVE 'NOT ON DOMAIN PACK UPC'                               
                                    TO XL031-UPC-ERROR-MESSAGE                  
025800                              OF PV-RMS-INPUT                             
024900              PERFORM 2100-WRITE-RMS-ERROR                                
025000              PERFORM 1000-READ-RMS-INPUT                                 
025100                                                                          
025200         WHEN XL031-UPC-UPC-NBR OF PV-RMS-INPUT >                         
025300              XL031-UPC-UPC-NBR OF PV-LOGISTIC-INPUT                      
025400*             ************************************                        
025500*             *       ON DOMAIN NOT ON RMS                                
025600*             ************************************                        
025700              ADD 1 TO PV-RECORDS-ON-LEGACY-ONLY                          
025800              MOVE 'NOT ON RMS PACK UPC'                                  
                                          TO XL031-UPC-ERROR-MESSAGE            
025800                                    OF PV-LOGISTIC-INPUT                  
025800              PERFORM 2150-WRITE-LOGISTIC-ERROR                           
025900              PERFORM 1100-READ-LOGISTIC-INPUT                            
026000                                                                          
026100         WHEN OTHER                                                       
026200*             ************************************                        
026300*             *       ON RMS AND ON DOMAIN                                
026400*             ************************************                        
026500              ADD 1 TO PV-RECORDS-ON-BOTH                                 
026600              IF XL031-UPC-EDI-ORD-IND OF PV-RMS-INPUT =                  
026700                 XL031-UPC-EDI-ORD-IND OF PV-LOGISTIC-INPUT               
026800                 PERFORM 1000-READ-RMS-INPUT                              
026900                 PERFORM 1100-READ-LOGISTIC-INPUT                         
027000              ELSE                                                        
027100*                ************************************                     
027200*                *       DATA DOESN'T MATCH                               
027300*                ************************************                     
027400                 ADD 1 TO PV-RECORDS-DATA-MISMATCH                        
025800                 MOVE 'DATA MISMATCH - RMS'                               
                                            TO XL031-UPC-ERROR-MESSAGE          
025800                                      OF PV-RMS-INPUT                     
027500                 PERFORM 2100-WRITE-RMS-ERROR                             
025800                 MOVE 'DATA MISMATCH - DOMAIN'                            
                                            TO XL031-UPC-ERROR-MESSAGE          
025800                                      OF PV-LOGISTIC-INPUT                
027600                 PERFORM 2150-WRITE-LOGISTIC-ERROR                        
027700                 PERFORM 1000-READ-RMS-INPUT                              
027800                 PERFORM 1100-READ-LOGISTIC-INPUT                         
027900              END-IF                                                      
028000                                                                          
028100     END-EVALUATE.                                                        
028200                                                                          
028300*****************************************************************         
028400*  WRITE A RMS RECORD TO THE ERROR FILE                                   
028500*****************************************************************         
028600 2100-WRITE-RMS-ERROR.                                                    
028700                                                                          
028800     WRITE ERROR-RECORD FROM                                              
028900         PV-RMS-INPUT                                                     
029000     END-WRITE.                                                           
029100     ADD +1                 TO PV-RECORDS-WRITTEN.                        
029200                                                                          
029300*    SET SWITCH FOR E-MAIL NOTIFICATION OF RMS ERROR.                     
029400     SET PS-EMAIL-YES       TO TRUE.                                      
029500                                                                          
029600*****************************************************************         
029700*  WRITE A LOGISTIC RECORD TO THE ERROR FILE                              
029800*****************************************************************         
029900 2150-WRITE-LOGISTIC-ERROR.                                               
030000                                                                          
030100     WRITE ERROR-RECORD FROM                                              
030200         PV-LOGISTIC-INPUT                                                
030300     END-WRITE.                                                           
030400     ADD +1                 TO PV-RECORDS-WRITTEN.                        
030500                                                                          
030600*    SET SWITCH FOR E-MAIL NOTIFICATION OF LOGISTIC ERROR.                
030700     SET PS-EMAIL-YES       TO TRUE.                                      
030800                                                                          
