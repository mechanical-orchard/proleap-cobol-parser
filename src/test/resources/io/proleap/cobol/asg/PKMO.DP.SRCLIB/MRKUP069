00001 ***************************************************************** 10/24/95
00002  IDENTIFICATION DIVISION.                                         MRKUP069
00003 *****************************************************************    LV029
00004                                                                      CL**1
00005  PROGRAM-ID.             MRKUP069.                                   CL**1
00006  AUTHOR.                 HOWARD MENDELSON.  GREENBRIER & RUSSEL.     CL**1
00007  INSTALLATION.           KOHLS DEPARTMENT STORES.                    CL**1
00008  DATE-WRITTEN            09-05-95.                                   CL**1
00009  DATE-COMPILED.                                                      CL**1
00010                                                                      CL**1
00011 *****************************************************************    CL**1
00012 *                POPULATE INFOREM 'A3' SCREEN.                  *    CL**1
00013 *                                                               *    CL**1
00014 *  THIS PROGRAM WILL LOAD DB2 TABLE TMRISHS.  THIS TABLE WILL   *    CL**1
00015 *  CONTAIN INFOREM CALCULATED VALUES (DD, MADSS).  THIS         *    CL**1
00016 *  INFORMATION WILL BE INPUT INTO INFOREM AND VIEWABLE THROUGH  *    CL**1
00017 *  THE INFOREM 'A3' SCREEN.  THESE VALUES ARE NEEDED TO HELP    *    CL**1
00018 *  IDENTIFY PROBLEMS WITH ITEMS.                                *    CL**1
00019 *                                                               *    CL**1
00020 *****************************************************************    CL**1
00021 *  CHANGE MADE: CAF - 2/4/99  - MOVED THE FULL 4 DIGIT YEAR INTO*    CL**1
00022 *  MRISHS-YR-NBR INSTEAD OF THE TWO DIGIT YEAR.                 *    CL**1
00023 *****************************************************************    CL**1
00248 *  PIT # 309322                                                 *    CL*26
00021 *  CHANGE MADE: AEK - 5/12/99 - TOOK OUT DELETE OF ROWS FROM THE*    CL**1
 0022 *  TMRISHS HISTORY TABLE. CODE WAS ADDED TO MKRUP059 TO REPLACE *    CL**1
00248 *  THE DELETE CODE IN THIS PROGRAM.                             *    CL*26
00248 *                                                               *    CL*26
00248 *        PERFORM E100-OPEN-ITEM-HIST-CURSOR                     *    CL*26
00249 *        PERFORM E200-FETCH-ITEM-HIST-CURSOR                    *    CL*26
00250 *        PERFORM B300-PROCESS-DELETES                           *    CL*26
00251 *            UNTIL PS-NO-MORE-DELETES                           *    CL*26
00252 *        PERFORM E300-CLOSE-ITEM-HIST-CURSOR                    *    CL*26
00025 *****************************************************************    CL**1
002600*  CHANGED 06/10/04 BY ANNE KINTOPF               WR# 02223     *         
002700*    CHANGED PROGRAM TO EXPECT A 5 DIGIT NUMERIC STORE          *         
002800*    NUMBER FOR STORE NUMBER EXPANSION.                         *         
00025 *****************************************************************    CL**1
002600*  CHANGED 02/17/06 BY LINDA FRIESS             PITS# 849440    *         
002700*    STORE NUMBER EXPANSION FIX--REMOVED 3-DIGIT STORE NUMBER   *         
003100*****************************************************************         
00026  ENVIRONMENT DIVISION.                                               CL**1
00027 *****************************************************************    CL**1
00028                                                                      CL**1
00029  CONFIGURATION SECTION.                                              CL**1
00030                                                                      CL**1
00031  SOURCE-COMPUTER.        IBM-3090.                                   CL**1
00032  OBJECT-COMPUTER.        IBM-3090.                                   CL**1
00033                                                                      CL**1
00034  INPUT-OUTPUT SECTION.                                               CL**1
00035                                                                      CL**1
00036  FILE-CONTROL.                                                       CL**1
00037 *                                                                    CL**1
00038      SELECT ITEM-STR-REPL-FILE    ASSIGN   TO  INP01.                CL**5
00039 *                                                                    CL**1
00040 *****************************************************************    CL**1
00041  DATA DIVISION.                                                      CL**1
00042 *****************************************************************    CL**1
00043 *                                                                    CL**1
00044  FILE SECTION.                                                       CL**1
00045 *                                                                    CL**1
00046  FD  ITEM-STR-REPL-FILE                                              CL*14
00047      LABEL RECORDS ARE STANDARD                                      CL**1
00048      BLOCK CONTAINS 0 RECORDS                                        CL**1
00049      RECORD CONTAINS 35 CHARACTERS                                   CL**2
00050      DATA RECORD IS ITEM-STR-REPL-REC.                               CL**5
00051  01  ITEM-STR-REPL-REC               PIC  X(35).                     CL**5
00052 *                                                                    CL**1
00053  WORKING-STORAGE SECTION.                                            CL**1
00054 *                                                                    CL**1
00055  01  FILLER.                                                         CL**1
00056      05  FILLER                      PIC  X(36)    VALUE             CL**1
00057          ' ***** WORKING STORAGE PGM=MRKUP069 '.                     CL**2
00058      05  PGM-COMPILED                PIC  X(20)    VALUE SPACE.      CL**1
00059      05  FILLER                      PIC  X(10)    VALUE             CL**1
00060          ' LOCATION='.                                               CL**1
00061      05  PGM-LOCATION                PIC  X(04)    VALUE SPACE.      CL**1
00062      05  FILLER                      PIC  X(06)    VALUE             CL**1
00063          ' *****'.                                                   CL**1
00064  EJECT                                                               CL**1
00065  01  PROGRAM-SWITCHES.                                               CL**1
00066      05  PS-EOF-ITEM-STR-REPL-FILE   PIC  X(01)    VALUE 'N'.        CL**5
00067          88  EOF-ITEM-STR-REPL-FILE                VALUE 'Y'.        CL**5
00071 *                                                                    CL**1
00072 /                                                                    CL**1
00073  01  PC-CONSTANTS.                                                   CL**1
00074      05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100        CL**1
00075                                                    COMP SYNC.        CL**1
00076      05  PC-NOT-REF-INTEGRAL         PIC S9(04)    VALUE -530        CL**1
00077                                                    COMP SYNC.        CL**1
00076      05  PC-DUPLICATE-KEY            PIC S9(04)    VALUE -803        CL**1
00077                                                    COMP SYNC.        CL**1
00078      05  PC-1                        PIC S9(03)    VALUE +1          CL**1
00079                                                    COMP-3.           CL**1
00080      05  PC-6                        PIC S9(03)    VALUE +6          CL*26
00081                                                    COMP-3.           CL*26
00082      05  PC-19                       PIC S9(03)    VALUE +19         CL*13
00083                                                    COMP-3.           CL*13
00084      05  PC-20                       PIC S9(03)    VALUE +20         CL*13
00085                                                    COMP-3.           CL*13
00088      05  PC-N                        PIC  X(01)    VALUE 'N'.        CL**1
00089      05  PC-Y                        PIC  X(01)    VALUE 'Y'.        CL**1
00090      05  PC-MRKUP069                 PIC  X(08)    VALUE             CL**5
00091                                                    'MRKUP069'.       CL**5
00092      05  PC-PGM-DPKUT500             PIC  X(08)    VALUE             CL**1
00093                                                    'DPKUT500'.       CL**1
00094      05  PC-ASTERISK                 PIC  X(30)    VALUE             CL**1
00095          '*****************************'.                            CL**1
00096  EJECT                                                               CL**1
00097 *                                                                    CL**1
00098  01  PV-ACCUMULATOR-FIELDS.                                          CL**1
00099      05  PV-UPDATE-CNT               PIC S9(04)    VALUE ZERO        CL**1
00100                                                    COMP-3.           CL**1
00101  EJECT                                                               CL**1
00102 *                                                                    CL**1
00103  01  PV-MISC-FIELDS.                                                 CL**1
00104      05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO        CL**1
00105                                                    COMP SYNC.        CL**1
00106      05  PV-ITM-REV-NULL-IND         PIC S9(04)    VALUE ZERO        CL*16
00107                                                    COMP.             CL*16
00108      05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.     CL**1
00109      05  PV-CURR-TMST                PIC  X(26)    VALUE SPACE.      CL**1
00110      05  PV-COMMIT-TMST              PIC  X(26)    VALUE SPACE.      CL**1
00111      05  PV-TODAYS-DAY               PIC  9(01)    VALUE ZERO.       CL**1
00112      05  PV-CURRENT-DATE.                                            CL**1
00113          10  PV-CURRENT-YEAR.                                        CL**1
00114              15 PV-CURRENT-DATE-CC   PIC  9(02)    VALUE ZERO.       CL**1
00115              15 PV-CURRENT-DATE-YR   PIC  9(02)    VALUE ZERO.       CL**1
00116          10  FILLER                  PIC  X(01)    VALUE '-'.        CL**1
00117          10  PV-CURRENT-DATE-MO      PIC  9(02)    VALUE ZERO.       CL**1
00118          10  FILLER                  PIC  X(01)    VALUE '-'.        CL**1
00119          10  PV-CURRENT-DATE-DY      PIC  9(02)    VALUE ZERO.       CL**1
00120      05  PV-TODAYS-TIME.                                             CL**1
00121          10  PV-TODAYS-TIME-HH       PIC  9(02)    VALUE ZERO.       CL**1
00122          10  PV-TODAYS-TIME-MM       PIC  9(02)    VALUE ZERO.       CL**1
00123          10  PV-TODAYS-TIME-SS       PIC  9(02)    VALUE ZERO.       CL**1
00124          10  FILLER                  PIC  9(02)    VALUE ZERO.       CL**1
00125  EJECT                                                               CL**1
00126 *                                                                    CL**1
00127      COPY DPWS500.                                                   CL**1
00128 *                                                                    CL**1
00129      COPY MRRD026.                                                   CL*29
00130 *                                                                    CL**1
00131 * DB2 TABLE LAYOUTS                                                  CL**1
00132      EXEC SQL                                                        CL**1
00133          INCLUDE TCKRSTCN                                            CL**8
00134      END-EXEC.                                                       CL**1
00135 *                                                                    CL**8
00136      EXEC SQL                                                        CL**8
00137          INCLUDE TMRISHS                                             CL**8
00138      END-EXEC.                                                       CL**8
00139  EJECT                                                               CL**1
00140 * DB2 COMMUNICATION AREA                                             CL**1
00141 *                                                                    CL**1
00142      EXEC SQL                                                        CL**1
00143          INCLUDE SQLCA                                               CL**1
00144      END-EXEC.                                                       CL**1
00145 *                                                                    CL*23
00146 * CURSOR FOR TMRISHS                                                 CL*23
00147 *                                                                    CL*23
00148      EXEC SQL                                                        CL*23
00149          DECLARE ITEM_HIST_CSR CURSOR FOR                            CL*23
00150           SELECT                                                     CL*23
00151                  ITM_NBR                                             CL*23
00152                , STR_NBR                                             CL*23
00153                , YR_NBR                                              CL*23
00154                , WK_NBR                                              CL*23
00155                , ITM_REV_ID                                          CL*23
00156            FROM                                                      CL*23
00157                  TMRISHS                                             CL*23
00158           WHERE  ITM_NBR = :MRISHS-ITM-NBR                           CL*23
00159             AND  STR_NBR = :MRISHS-STR-NBR                           CL*23
00160        ORDER BY  YR_NBR     DESC                                     CL*23
00161                , WK_NBR     DESC                                     CL*23
00162                , ITM_REV_ID DESC                                     CL*23
00163      END-EXEC.                                                       CL*23
00164 *                                                                    CL*23
00165  EJECT                                                               CL**1
00166 * DB2 COMMUNICATION AREA2                                            CL**1
00167 *                                                                    CL**1
00168      COPY SQLCA2.                                                    CL**1
00169  EJECT                                                               CL**1
00170 *                                                                    CL**1
00171  LINKAGE SECTION.                                                    CL**1
00172 *****************************************************************    CL**1
00173 *PROCEDURE DIVISION.                                                 CL**1
00174 *****************************************************************    CL**1
00175  PROCEDURE DIVISION.                                                 CL**1
00176 *                                                                    CL**1
00177  A000-MAINLINE-PROCESSING.                                           CL**1
00178 *                                                                    CL**1
00179      PERFORM A100-INITIALIZATION.                                    CL**1
00180 *                                                                    CL**1
00181      PERFORM B000-PROCESS-ITEM-STR-REPL-REC                          CL**5
00182              UNTIL EOF-ITEM-STR-REPL-FILE.                           CL**5
00183 *                                                                    CL**1
00184      PERFORM Y100-END-ROUTINE.                                       CL**1
00185 *                                                                    CL**1
00186      GOBACK.                                                         CL**1
00187 *                                                                    CL**1
00188  EJECT                                                               CL**1
00189 ******************************************************************   CL**1
00190 ** OPEN THE SYSTEM FILE AND ACCEPT CONTROL CARDS.                    CL**1
00191 ** READ THE FIRST RECORD.                                            CL**1
00192 ******************************************************************   CL**1
00193  A100-INITIALIZATION.                                                CL**1
00194 *                                                                    CL**1
00195      OPEN  INPUT ITEM-STR-REPL-FILE.                                 CL**5
00196 *                                                                    CL**1
00197      ACCEPT DPG51-SYSTEM-DATE FROM DATE.                             CL**1
00198 *                                                                    CL**1
00199      MOVE DPG51-SYSTEM-DATE-YR TO PV-CURRENT-DATE-YR.                CL**1
00200      MOVE DPG51-SYSTEM-DATE-MO TO PV-CURRENT-DATE-MO.                CL**1
00201      MOVE DPG51-SYSTEM-DATE-DY TO PV-CURRENT-DATE-DY.                CL**1
00202      IF DPG51-SYSTEM-DATE-YR > 88                                    CL**1
00203          MOVE PC-19           TO PV-CURRENT-DATE-CC                  CL*13
00204      ELSE                                                            CL**1
00205          MOVE PC-20           TO PV-CURRENT-DATE-CC                  CL*13
00206      END-IF.                                                         CL**1
00207 *                                                                    CL**1
00208      PERFORM R000-READ-ITEM-STR-REPL-FILE.                           CL**5
00209 *                                                                    CL*27
00210       IF EOF-ITEM-STR-REPL-FILE                                      CL**5
00211          CLOSE  ITEM-STR-REPL-FILE                                   CL**5
00212          GOBACK.                                                     CL**1
00213 *                                                                    CL**1
00214 *  SET UP FIRST SCHEDULED COMMIT TIME                                CL**1
00215                                                                      CL**1
00216      EXEC SQL                                                        CL**1
00217          SELECT  (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       CL**1
00218            INTO  :PV-COMMIT-TMST                                     CL**1
00219            FROM  TCKRSTCNTL                                          CL**1
00220           WHERE  PLAN_NAME = 'MRKUP069'                              CL**2
00221      END-EXEC.                                                       CL**1
00222                                                                      CL**1
00223      IF SQLCODE = +0                                                 CL**1
00224          CONTINUE                                                    CL**1
00225      ELSE                                                            CL**1
00226          DISPLAY 'PARAGRAPH A100, BAD SELECT FOR TIMESTAMP'          CL**1
00227          DISPLAY ' '                                                 CL**1
00228          PERFORM Z999-SQL-ERROR.                                     CL**1
00229                                                                      CL**1
00230 *                                                                    CL**1
00231  EJECT                                                               CL**1
00232 ******************************************************************   CL**1
00233 *  THIS IS THE MAIN CONTROLLING PARAGRAPH FOR PROCESSING THE     *   CL**1
00234 *  ITEM STORE REPLENISHMENT RECORD.                              *   CL**6
00235 ******************************************************************   CL**1
00236  B000-PROCESS-ITEM-STR-REPL-REC.                                     CL**5
00237 *                                                                    CL**1
00238      IF MR026-HISTDD-FCTR EQUAL ZERO AND                             CL*29
00239              MR026-HISTMAD-QTY EQUAL ZERO                            CL*29
00240          CONTINUE                                                    CL*18
00241      ELSE                                                            CL*18
00242          PERFORM B100-FORMAT-ITEM-STR-REPL                           CL*18
00243          PERFORM B200-INSERT-TMRISHS                                 CL*18
00253          PERFORM Z900-COMMIT-TEST                                    CL*18
00254      END-IF.                                                         CL*18
00255 *                                                                    CL**1
00256      PERFORM R000-READ-ITEM-STR-REPL-FILE.                           CL**6
00257 *                                                                    CL**1
00258  EJECT                                                               CL**1
00259 *                                                                *   CL**1
00260 ******************************************************************   CL**1
00261 *   FORMAT FIELDS AND INSERT ON THE ITEM/STORE REPLENISHMENT     *   CL**6
00262 *   HISTORY TABLE WITH DATA FROM THE REPLENISHMENT RECORD.       *   CL**6
00263 ******************************************************************   CL**1
00264  B100-FORMAT-ITEM-STR-REPL.                                          CL*12
00265 *                                                                    CL**1
00266 *    DISPLAY 'B100-FORMAT-ITEM-STR-REPL'.                            CL*12
00267 *                                                                    CL**1
00268      MOVE MR026-ITEM-NBR       TO  MRISHS-ITM-NBR.                   CL*29
00269      MOVE MR026-STR-NBR        TO  MRISHS-STR-NBR.                   CL*29
00270      MOVE MR026-YEAR-NBR       TO  MRISHS-YR-NBR.                    CL*29
00271      MOVE MR026-WEEK-NBR       TO  MRISHS-WK-NBR.                    CL*29
00272      MOVE MR026-HISTCPS-QTY    TO  MRISHS-SLS-HIST-QTY.              CL*29
00273      MOVE MR026-HISTOH-QTY     TO  MRISHS-OH-HIST-QTY.               CL*29
00274      MOVE MR026-HISTMAD-QTY    TO  MRISHS-MAD-HIST-QTY.              CL*29
00275      MOVE MR026-HISTDD-FCTR    TO  MRISHS-DSD-HIST-QTY.              CL*29
00276      MOVE ZERO                 TO  MRISHS-ITM-REV-ID.                CL*18
00277 *                                                                    CL*10
00278      PERFORM C100-SELECT-MAX-ITEM-REV-ID.                            CL*18
00279 *                                                                    CL**1
00280      COMPUTE MRISHS-ITM-REV-ID = MRISHS-ITM-REV-ID + PC-1.           CL*18
00281 *                                                                    CL**1
00282 ******************************************************************   CL**1
00283 * THIS PARAGRAPH PERFORMS THE INSERT OF THE ITEM/STORE TABLE     *   CL**9
00284 ******************************************************************   CL**1
00285  B200-INSERT-TMRISHS.                                                CL*12
00286 *                                                                    CL**1
00287 *    DISPLAY 'B200-INSERT-TMRISHS'.                                  CL*12
00288 *                                                                    CL**1
00289      MOVE 'B200'                   TO PGM-LOCATION.                  CL*12
00290      MOVE 'B200-INSERT-TMRISHS'    TO PV-ABEND-PARAGRAPH.            CL*12
00291 *                                                                    CL**1
00292      EXEC SQL                                                        CL**1
00293          INSERT INTO TMRISHS                                         CL*12
00294                 ( ITM_NBR,                                           CL*12
00295                   STR_NBR,                                           CL*12
00296                   YR_NBR,                                            CL*12
00297                   WK_NBR,                                            CL*12
00298                   ITM_REV_ID,                                        CL*12
00299                   SLS_HIST_QTY,                                      CL*12
00300                   OH_HIST_QTY,                                       CL*12
00301                   MAD_HIST_QTY,                                      CL*12
00302                   DSD_HIST_QTY                                       CL*12
00303                 )                                                    CL*12
00304          VALUES (:MRISHS-ITM-NBR,                                    CL*12
00305                  :MRISHS-STR-NBR,                                    CL*12
00306                  :MRISHS-YR-NBR,                                     CL*12
00307                  :MRISHS-WK-NBR,                                     CL*12
00308                  :MRISHS-ITM-REV-ID,                                 CL*12
00309                  :MRISHS-SLS-HIST-QTY,                               CL*12
00310                  :MRISHS-OH-HIST-QTY,                                CL*12
00311                  :MRISHS-MAD-HIST-QTY,                               CL*12
00312                  :MRISHS-DSD-HIST-QTY                                CL*12
00313                 )                                                    CL*12
00314      END-EXEC.                                                       CL**1
00315 *                                                                    CL**1
00316      EVALUATE TRUE                                                   CL**1
00317        WHEN SQLCODE         = ZERO                                   CL**1
             WHEN SQLCODE         = PC-DUPLICATE-KEY                            
00318        WHEN SQLCODE         = PC-ROW-NOT-FOUND                       CL**1
00319          CONTINUE                                                    CL**1
00320        WHEN SQLCODE         = PC-NOT-REF-INTEGRAL                    CL*12
00321          CONTINUE                                                    CL*12
00322        WHEN OTHER                                                    CL**1
00323          DISPLAY 'ITEM NBR:  ' MRISHS-ITM-NBR                        CL*12
00324          DISPLAY 'STORE NBR: ' MRISHS-STR-NBR                        CL*12
00325          DISPLAY PV-ABEND-PARAGRAPH                                  CL*12
00326          PERFORM Z999-SQL-ERROR                                      CL**1
00327      END-EVALUATE.                                                   CL**1
00328 *                                                                    CL**1
00329  EJECT                                                               CL**1
00330 *                                                                    CL**1
00382 ******************************************************************   CL**1
00383 * THIS PARAGRAPH SELECTS THE MAX WEEK NUMBER FOR SEQUENCE NUMBER *   CL**1
00384 * RENUMBERING.                                                   *   CL**1
00385 ******************************************************************   CL**1
00386  C100-SELECT-MAX-ITEM-REV-ID.                                        CL*12
00387 *                                                                    CL**1
00388      MOVE 'C100'                   TO PGM-LOCATION.                  CL*12
00389      MOVE 'C100-SELECT-MAX-ITEM-REV-ID '                             CL*12
00390                                    TO PV-ABEND-PARAGRAPH.            CL**1
00391 *                                                                    CL**1
00392      EXEC SQL                                                        CL**1
00393          SELECT   MAX(ITM_REV_ID)                                    CL*12
00394            INTO   :MRISHS-ITM-REV-ID :PV-ITM-REV-NULL-IND            CL*17
00395            FROM   TMRISHS                                            CL*12
00396           WHERE   ITM_NBR = :MRISHS-ITM-NBR                          CL*12
00397             AND   STR_NBR = :MRISHS-STR-NBR                          CL*12
00398             AND   YR_NBR  = :MRISHS-YR-NBR                           CL*12
00399             AND   WK_NBR  = :MRISHS-WK-NBR                           CL*12
00400      END-EXEC.                                                       CL**1
00401 *                                                                    CL**1
00402      EVALUATE TRUE                                                   CL**1
00403        WHEN SQLCODE         = ZERO                                   CL**1
00404            CONTINUE                                                  CL*12
00405        WHEN SQLCODE         = PC-ROW-NOT-FOUND                       CL**1
00406            CONTINUE                                                  CL**1
00407        WHEN OTHER                                                    CL**1
00408            DISPLAY PV-ABEND-PARAGRAPH                                CL**1
00409            PERFORM Z999-SQL-ERROR                                    CL**1
00410      END-EVALUATE.                                                   CL**1
00493 ******************************************************************   CL*12
00494 *  THIS PARAGRAPH READS THE REPLENISHMENT FILE FOR PROCESSING    *   CL*12
00495 ******************************************************************   CL*12
00496  R000-READ-ITEM-STR-REPL-FILE.                                       CL*12
00497 *                                                                    CL*12
00498      READ ITEM-STR-REPL-FILE  INTO  ITEM-STORE-REPL-RECORD           CL*12
00499          AT END                                                      CL*12
00500             MOVE PC-Y TO PS-EOF-ITEM-STR-REPL-FILE.                  CL*12
00501 *                                                                    CL*12
00502 ******************************************************************   CL**1
00503 *   CLOSES FILES AND DISPLAYS MESSAGES.                          *   CL**1
00504 ******************************************************************   CL**1
00505  Y100-END-ROUTINE.                                                   CL**1
00506 *                                                                    CL**1
00507      CLOSE  ITEM-STR-REPL-FILE.                                      CL**6
00508 *                                                                    CL**1
00509  EJECT                                                               CL**1
00510 ******************************************************************   CL**1
00511 *     DB2 COMMIT TEST                                            *   CL**1
00512 *                                                                *   CL**1
00513 ******************************************************************   CL**1
00514                                                                      CL**1
00515  Z900-COMMIT-TEST.                                                   CL**1
00516 *-----------------                                                   CL**1
00517                                                                      CL**1
00518 *  GET THE CURRENT TIMESTAMP                                         CL**1
00519                                                                      CL**1
00520      EXEC SQL                                                        CL**1
00521          SET :PV-CURR-TMST = CURRENT TIMESTAMP                       CL**1
00522      END-EXEC.                                                       CL**1
00523                                                                      CL**1
00524      IF SQLCODE = +0                                                 CL**1
00525          CONTINUE                                                    CL**1
00526      ELSE                                                            CL**1
00527          DISPLAY 'PARAGRAPH Z900, BAD SET FOR CURRENT TIMESTAMP'     CL**1
00528          DISPLAY ' '                                                 CL**1
00529          PERFORM Z999-SQL-ERROR.                                     CL**1
00530                                                                      CL**1
00531 *  COMPARE TO THE NEXT SCHEDULED COMMIT TIME                         CL**1
00532                                                                      CL**1
00533      IF PV-CURR-TMST > PV-COMMIT-TMST                                CL**1
00534          PERFORM Z910-COMMIT.                                        CL**1
00535                                                                      CL**1
00536  EJECT                                                               CL**1
00537 ******************************************************************   CL**1
00538 *     DB2 COMMIT PROCESSING                                      *   CL**1
00539 *                                                                *   CL**1
00540 ******************************************************************   CL**1
00541                                                                      CL**1
00542  Z910-COMMIT.                                                        CL**1
00543 *------------                                                        CL**1
00544                                                                      CL**1
00545 *  TAKE COMMIT                                                       CL**1
00546                                                                      CL**1
00547      EXEC SQL                                                        CL**1
00548          COMMIT                                                      CL**1
00549      END-EXEC.                                                       CL**1
00550                                                                      CL**1
00551      IF SQLCODE = +0                                                 CL**1
00552          CONTINUE                                                    CL**1
00553      ELSE                                                            CL**1
00554          DISPLAY 'PARAGRAPH Z910, BAD COMMIT'                        CL**1
00555          DISPLAY ' '                                                 CL**1
00556          PERFORM Z999-SQL-ERROR.                                     CL**1
00557                                                                      CL**1
00558 *  GET THE NEXT SCHEDULED COMMIT TIME                                CL**1
00559                                                                      CL**1
00560      EXEC SQL                                                        CL**1
00561          SELECT  (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       CL**1
00562            INTO  :PV-COMMIT-TMST                                     CL**1
00563            FROM  TCKRSTCNTL                                          CL**1
00564           WHERE  PLAN_NAME = 'MRKUP069'                              CL**7
00565      END-EXEC.                                                       CL**1
00566                                                                      CL**1
00567      IF SQLCODE = +0                                                 CL**1
00568          CONTINUE                                                    CL**1
00569      ELSE                                                            CL**1
00570          DISPLAY 'PARAGRAPH Z910, SELECT FOR NEXT TIMESTAMP'         CL**1
00571          DISPLAY ' '                                                 CL**1
00572          PERFORM Z999-SQL-ERROR.                                     CL**1
00573                                                                      CL**1
00574 *                                                                    CL**1
00575  Z999-SQL-ERROR.                                                     CL**1
00576 *---------------                                                     CL**1
00577                                                                      CL**1
00578      MOVE SQLCODE    TO SQLCODE2.                                    CL**1
00579      MOVE SQLERRD(3) TO SQLERRD3.                                    CL**1
00580      DISPLAY '** ABEND **       ** = SQLERROR'.                      CL**1
00581      DISPLAY '** PROGRAM        ** = MRKUP069'.                      CL**7
00582      DISPLAY '** SQLCODE        ** = ' SQLCODE2.                     CL**1
00583      DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                     CL**1
00584      DISPLAY '** SQLERRM        ** = ' SQLERRM.                      CL**1
00585      DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                     CL**1
00586      MOVE +4013 TO PV-ABEND-CODE.                                    CL**1
00587      CALL 'ILBOABN0' USING PV-ABEND-CODE.                            CL**1
00588 *                                                                    CL**1
00589 ******************************************************************   CL**1
00590 ***********************END OF PROGRAM*****************************   CL*19
00591 ******************************************************************   CL*26
