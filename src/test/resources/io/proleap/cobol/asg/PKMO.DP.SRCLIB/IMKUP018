       IDENTIFICATION DIVISION.                                         09/16/94
                                                                           LV020
       PROGRAM-ID.             IMKUP018.                                   CL**1
       AUTHOR.                 PAUL KEBER.                                 CL**1
       INSTALLATION.           KOHLS DEPARTMENT STORES.                    CL**1
       DATE-WRITTEN.           09/10/2001.                                 CL**1
       DATE-COMPILED.                                                      CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      *  IMKUP018 - LEGACY ITEM MAINTENANCE TSKXREF INSERT TRIGGER RPC *   CL**1
      ******************************************************************   CL**1
      * THIS STORED PROCEDURE PROGRAM WILL BE USED TO FORMAT LEGACY        CL**1
      * ITEM MAINTENANCE SKU ADD AND SKU ADD 2 TRANSACTION RECORDS AND     CL**1
      * INSERT THEM TO THE ITEM EVENT MESSAGE BRIDGE DB2 TABLE.            CL**1
      ******************************************************************   CL**1
      * MAINT LOG.                                                              
      *                                                                         
      * 10/07/02  P. KEBER    CHANGE ID: WR23974                                
      *   ADDED LOGIC TO SET IM008-SKU-ADD-ITEM-NMBR, IM008-SKU-ADD-SKU-        
      *   STAT-DESC , IM008-SKU-ADD-SIZE-DESC, AND IM008-SKU-ADD-SHRT-          
      *   CLOR-DESC ON THE SKU ADD MESSAGE.  CREATE A NEW SKU ADD 2             
      *   MESSAGE (TYPE 041) CONTAINING IM008-SKU-ADD2-TX-PRD-CDE-DESC          
      *   WHENEVER A SKU ADD MESSAGE IS CREATED.                                
      *                                                                         
      * 10/31/02  P. KEBER    CHANGE ID: WR23974                                
      *   ADDED LOGIC TO ADD IM008-SKU-ADD2-NRF-CLOR-DESC.                      
      *                                                                         
      * 07/31/03  P. KEBER    CHANGE ID: WR24363                                
      *   RELINK THE LOAD MODULE TO SPECIFY A WORK LOAD MANAGED ENV.            
      *                                                                         
      * 01/28/04  P. KEBER    CHANGE ID: WR24363                                
      *   DISPLAY MORE INFORMATION IN ERROR MESSAGES.                           
      *                                                                         
      * 01/04/05  P. KEBER    CHANGE ID: WR27639                                
      *   CHANGE PC-OPERCO-NBR TO A CHAR(2) FIELD INSTEAD OF A CHAR(3)          
      *   FIELD SO THAT SELECTS ON TUPCPLS USE MORE COLUMNS IN THE              
      *   CLUSTERING INDEX.                                                     
      *                                                                         
      * 12/16/05  P. KEBER    CHANGE ID: WR29737                                
      *   ADDED LOGIC TO ADD IM008-SKU-ADD-TRNSTN-IND.                          
      *                                                                         
      * 10/18/07  P. KEBER    CHANGE ID: WR33240                                
      *   ADDED LOGIC TO ADD IM008-SKU-ADD-PO-SHRT-CYC-IND.                     
W38861*                                                                         
W38861* 08/04/10  COGNIZANT   CHANGE ID: WR38861                                
W38861*   ADDED LOGIC TO SET IM008-SKU-ADD-WHSE-RPLNT-IND ON THE SKU            
W38861*   ADD MESSAGE (TYPE 040)                                                
W36546*                                                                         
W36546* 08/04/10  COGNIZANT   CHANGE ID: WR36546                                
W36546*   ADD LOGIC TO SET IM008-SKU-ADD2-TST-PD-TP-SEQ AND                     
W36546*   IM008-SKU-ADD2-TST-PD-TP-DESC ON THE SKU ADD 2 MESSAGE                
W36546*   (TYPE 041)                                                            
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************   CL**1
      * PROGRAM CONSTANTS                                              *   CL**1
      ******************************************************************   CL**1
                                                                           CL**1
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PGM-NAME             PIC  X(08)     VALUE 'IMKUP018'.         
           05  PC-MAXIMUM-RETRY-COUNT  PIC S9(04)     VALUE +2                  
                                                           COMP SYNC.           
           05  PC-TX-PRD-CD-CHARTC-CDE PIC S9(04)     VALUE +14 COMP.           
           05  PC-WT-UOM-CHARTC-CDE    PIC S9(04)     VALUE +11 COMP.           
           05  PC-OPERCO-NBR           PIC  X(02)     VALUE '03'.               
           05  PC-ORIGIN-CNTRY         PIC  X(03)     VALUE 'US '.              
           05  PC-SPACES               PIC  X(03)     VALUE '   '.              
W36546     05  PC-TST-PRD-TYP-CHARTC-CDE                                        
W36546                                 PIC S9(04)     VALUE +34 COMP.           
                                                                                
      ******************************************************************   CL**1
      * PROGRAM SWITCHES                                              *    CL**1
      ******************************************************************   CL**1
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-DB-ERROR-SW          PIC X(01)      VALUE 'N'.           CL**1
               88  PS-DB-ERROR                        VALUE 'Y'.           CL**1
               88  PS-NO-DB-ERROR                     VALUE 'N'.           CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      * PROGRAM VARIABLES                                              *   CL**1
      ******************************************************************   CL**1
                                                                           CL*17
       01  PV-PROGRAM-VARIABLES.                                           CL**1
           05  PV-UPC-ID-DOUBLE        PIC S9(10)  VALUE ZERO                   
                                                   COMP SYNC.                   
           05  PV-RETRY-COUNT          PIC S9(04)  VALUE ZERO                   
                                                   COMP SYNC.                   
           05  PV-PARAGRAPH-NAME       PIC  X(30)  VALUE SPACES.                
           05  PV-DB2-OPERATION        PIC  X(50)  VALUE SPACES.                
           05  PV-DB2-KEY-INFO         PIC  X(50)  VALUE SPACES.                
           05  PV-LITERAL              PIC  X(01)  VALUE SPACE.                 
           05  PV-TX-PRD-CDE-DESC      PIC  X(20)  VALUE SPACES.                
           05  PV-WT-UM-CDE            PIC  9(01)  VALUE ZERO.                  
           05  PV-WT-UM-DESC           PIC  X(04)  VALUE SPACES.                
W36546     05  PV-TST-PRD-TYP-DESC     PIC  X(20)  VALUE SPACES.                
                                                                                
           05  PV-TUPC-KEY-INFO.                                           CL**1
               10  FILLER                      PIC X(08)   VALUE           CL**1
                   'UPC_ID: '.                                                  
               10  PV-TUPC-KEY-UPC-ID          PIC Z(10)   VALUE ZERO.     CL**1
                                                                                
           05  PV-TUPCPLS-KEY-INFO.                                        CL**1
               10  FILLER                      PIC X(08)   VALUE           CL**1
                   'UPC_ID: '.                                                  
               10  PV-TUPCPLS-KEY-UPC-ID       PIC Z(10)   VALUE ZERO.     CL**1
                                                                                
           05  PV-TMITGPL-KEY-INFO.                                        CL**1
               10  FILLER                      PIC X(12)   VALUE           CL**1
                   'MEITGP_NBR: '.                                              
               10  PV-TMITGPL-KEY-MEITGP-NBR   PIC Z(09)   VALUE ZERO.     CL**1
                                                                                
           05  PV-TVSCHVL-KEY-INFO.                                             
               10  FILLER                      PIC X(12)   VALUE           CL**1
                   'CHARTC_CDE: '.                                              
               10  PV-TVSCHVL-KEY-CHARTC-CDE   PIC Z(4)    VALUE ZERO.          
               10  FILLER                      PIC X(10)   VALUE                
                   ' SEQ_NBR: '.                                                
               10  PV-TVSCHVL-KEY-SEQ-NBR      PIC Z(4)    VALUE ZERO.          
                                                                                
           05  PV-TUPCSTA-KEY-INFO.                                        CL**1
               10  FILLER                      PIC X(14)   VALUE           CL**1
                   'UPC_STAT_CDE: '.                                            
               10  PV-TUPCSTA-KEY-UPC-STAT-CDE PIC X(02)   VALUE SPACES.   CL**1
                                                                                
           05  PV-TSIZE-KEY-INFO.                                          CL**1
               10  FILLER                      PIC X(16)   VALUE           CL**1
                   'KOHLS_SIZE_CDE: '.                                          
               10  PV-TSIZE-KEY-SIZE-CDE       PIC Z(05)   VALUE ZERO.     CL**1
                                                                                
           05  PV-TCOLOR-KEY-INFO.                                         CL**1
               10  FILLER                      PIC X(14)   VALUE           CL**1
                   'NRF_CLOR_CDE: '.                                            
               10  PV-TCOLOR-KEY-COLOR-CDE     PIC X(03)   VALUE SPACES.   CL**1
                                                                                
           05  PV-UPC-GEN-ERR-MSG.                                         CL**1
               10  FILLER                      PIC X(38)   VALUE           CL**1
                   'UPC NUMBER GENERATOR PROGRAM ERR IND: '.                    
               10  PV-UPC-GEN-ERR-IND          PIC X(01)   VALUE SPACE.    CL**1
                                                                           CL**1
      *****************************************************************         
      *  LEGACY ITEM MAINTENANCE INFO COPYBOOK                                  
      ******************************************************************        
           COPY IMRD008.                                                        
                                                                                
      ******************************************************************        
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                
      ******************************************************************        
                                                                                
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  ITEM EVENT MESSAGE BRIDGE TABLE                                        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE IMT00001                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  ITEM COLOR TABLE                                                       
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TCOLOR                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  EXTERNAL ENTITY TABLE                                                  
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TEXTENT                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  MERCHANDISE AREA VENDOR STYLE TABLE                                    
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDARVS                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  MERCHANDISE ITEM GROUP PRICE AD LOCATION TABLE                         
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TMITGPL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  SIZE TABLE                                                             
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TSIZE                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  UPC TABLE                                                              
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TUPC                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  UPC PHYSICAL LOCATION STATUS TABLE                                     
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TUPCPLS                                                  
           END-EXEC.                                                            
                                                                                
      ************************************************************              
      *  SKU STATUS TABLE                                                       
      ************************************************************              
           EXEC SQL                                                             
               INCLUDE TUPCSTA                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  VENDOR STYLE TABLE                                                     
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TVNDSTL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  VENDOR STYLE CHARACTERISTIC VALUE TABLE                                
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TVSCHVL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    DB2 COMMUNICATION AREA                                               
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
       01  LS-ITM-NBR                   PIC S9(15)V COMP-3.                     
       01  LS-INTR-UPC-ID               PIC S9(9)   COMP.                       
       01  LS-DEPT                      PIC S9(4)V  COMP-3.                     
       01  LS-CLASS                     PIC S9(4)V  COMP-3.                     
       01  LS-SUBCLASS                  PIC S9(4)V  COMP-3.                     
       01  LS-SKU                       PIC S9(8)V  COMP-3.                     
                                                                                
                                                                                
       PROCEDURE DIVISION USING  LS-ITM-NBR                                     
                               , LS-INTR-UPC-ID                                 
                               , LS-DEPT                                        
                               , LS-CLASS                                       
                               , LS-SUBCLASS                                    
                               , LS-SKU.                                        
                                                                                
       1000-MAIN-MODULE.                                                        
                                                                                
           SET PS-NO-DB-ERROR TO TRUE.                                          
                                                                                
      ******************************************************************        
      *  OBTAIN INFORMATION NEEDED TO FORMAT SKU ADD BUSINESS EVENTS.           
      ******************************************************************        
                                                                                
      *    GET TUPC, TMDARVS, TVNDSTL, AND TEXTENT INFO                         
           PERFORM 8000-GET-UPC-KEY-DATA.                                       
                                                                                
      *    GET TUPCPLS INFO                                                     
           IF PS-NO-DB-ERROR                                                    
               PERFORM 8100-GET-UPCPLS-INFO                                     
           END-IF.                                                              
                                                                                
      *    GET GROUP PRICE INFORMATION FROM TMITGPL IF NEEDED                   
           IF PS-NO-DB-ERROR                                                    
             AND UPCPLS-MEITGP-NBR GREATER THAN ZERO                            
               PERFORM 8300-GET-MITGPL-INFO                                     
           END-IF.                                                              
                                                                                
      *    GET WEIGHT UOM INFORMATION FROM TVSCHVL IF NEEDED                    
           IF UPC-WT-UM-CDE(1:1) = '0' OR ' '                                   
              MOVE SPACES TO PV-WT-UM-DESC                                      
           ELSE                                                                 
               IF PS-NO-DB-ERROR                                                
                   MOVE PC-WT-UOM-CHARTC-CDE TO VSCHVL-CHARTC-CDE               
                   MOVE UPC-WT-UM-CDE(1:1) TO PV-WT-UM-CDE                      
                   MOVE PV-WT-UM-CDE TO VSCHVL-CHARTC-SEQ-NBR                   
                   PERFORM 8400-GET-VSCHVL-INFO                                 
                   MOVE VSCHVL-CHARTC-VAL-DESC TO PV-WT-UM-DESC                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *    GET TAX PRODUCT CODE DESCRIPTION FROM TVSCHVL IF NEEDED              
           IF UPC-TX-PRD-CDE-SEQ-NBR = ZERO                                     
              MOVE SPACES TO PV-TX-PRD-CDE-DESC                                 
           ELSE                                                                 
               IF PS-NO-DB-ERROR                                                
                   MOVE PC-TX-PRD-CD-CHARTC-CDE TO VSCHVL-CHARTC-CDE            
                   MOVE UPC-TX-PRD-CDE-SEQ-NBR                                  
                                    TO VSCHVL-CHARTC-SEQ-NBR                    
                   PERFORM 8400-GET-VSCHVL-INFO                                 
                   MOVE VSCHVL-CHARTC-VAL-DESC TO PV-TX-PRD-CDE-DESC            
               END-IF                                                           
           END-IF.                                                              
                                                                                
W36546*    GET TEST PRODUCT TYPE DESCRIPTION FROM TVSCHVL IF NEEDED             
W36546     IF  UPC-TST-PRD-TYP-SEQ-NBR = ZERO                                   
W36546         MOVE "NONE"                      TO PV-TST-PRD-TYP-DESC          
W36546     ELSE                                                                 
W36546         IF  PS-NO-DB-ERROR                                               
W36546             INITIALIZE                     VSCHVL-CHARTC-SEQ-NBR         
W36546                                            VSCHVL-CHARTC-CDE             
W36546                                            VSCHVL-CHARTC-VAL-DESC        
W36546             MOVE PC-TST-PRD-TYP-CHARTC-CDE                               
W36546                                          TO VSCHVL-CHARTC-CDE            
W36546             MOVE UPC-TST-PRD-TYP-SEQ-NBR TO VSCHVL-CHARTC-SEQ-NBR        
W36546             PERFORM 8400-GET-VSCHVL-INFO                                 
W36546             MOVE VSCHVL-CHARTC-VAL-DESC  TO PV-TST-PRD-TYP-DESC          
W36546         END-IF                                                           
W36546     END-IF.                                                              
W36546                                                                          
      *    GET THE STATUS CODE DESCRIPTION                                      
           IF PS-NO-DB-ERROR                                                    
               PERFORM 8500-GET-UPCSTA-STAT-CDE-DESC                            
           END-IF.                                                              
                                                                                
      *    GET THE SKU SIZE DESCRIPTION DATA                                    
           IF PS-NO-DB-ERROR                                                    
               PERFORM 8600-GET-SIZE-INFO                                       
           END-IF.                                                              
                                                                                
           IF PS-NO-DB-ERROR                                                    
               PERFORM 8700-GET-COLOR-INFO                                      
           END-IF.                                                              
                                                                                
           IF PS-NO-DB-ERROR                                                    
               PERFORM 2000-CREATE-SKU-ADD-REC                                  
           END-IF.                                                              
                                                                                
           IF PS-NO-DB-ERROR                                                    
               PERFORM 2100-CREATE-SKU-ADD2-REC                                 
           END-IF.                                                              
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  CREATE THE SKU ADD BUSINESS EVENT                                      
      ******************************************************************        
       2000-CREATE-SKU-ADD-REC.                                                 
                                                                                
           SET IM008-BUS-EVENT-SKU-ADD TO TRUE.                                 
           MOVE SPACES              TO IM008-CRE-TMST.                          
                                                                                
      *    FILL IN THE ITEM KEY DATA FIELDS                                     
           INITIALIZE IM008-ITEM-KEY-DATA.                                      
           MOVE UPC-VS-ID           TO IM008-ITEM-KEY-VS-ID.                    
           MOVE VNDSTL-ENT-ID       TO IM008-ITEM-KEY-ENT-ID.                   
           MOVE EXTENT-DUN-NBR      TO IM008-ITEM-KEY-DUN-NBR.                  
           MOVE VNDSTL-VS-NBR       TO IM008-ITEM-KEY-VS-NBR.                   
           MOVE UPC-SUB-CL-MDSEAR-ID                                            
                                    TO IM008-ITEM-KEY-SUBCL-MDSE-ID.            
           MOVE LS-DEPT             TO IM008-ITEM-KEY-DEPT.                     
           MOVE LS-CLASS            TO IM008-ITEM-KEY-CLASS.                    
           MOVE LS-SUBCLASS         TO IM008-ITEM-KEY-SUBCL.                    
           MOVE MDARVS-RMS-STYL-ID  TO IM008-ITEM-KEY-RMS-STYL-ID.              
           MOVE LS-SKU              TO IM008-ITEM-KEY-SKU.                      
           MOVE LS-INTR-UPC-ID      TO PV-UPC-ID-DOUBLE.                        
           MOVE PV-UPC-ID-DOUBLE    TO IM008-ITEM-KEY-INTR-UPC-ID               
                                       IM008-ITEM-KEY-UPC-ID.                   
           MOVE UPC-UPC-NBR         TO IM008-ITEM-KEY-UPC-NBR.                  
           MOVE PC-ORIGIN-CNTRY     TO IM008-ITEM-KEY-ORIGIN-CNTRY.             
                                                                                
      *    FILL IN THE SKU ADD DATA                                             
           INITIALIZE IM008-SKU-ADD-DATA.                                       
           MOVE UPC-KOHLS-SIZE-CDE  TO IM008-SKU-ADD-KOHLS-SIZE-CDE.            
           MOVE UPC-NRF-CLOR-CDE    TO IM008-SKU-ADD-NRF-CLOR-CDE.              
           MOVE UPC-CSTM-CLOR-DESC  TO IM008-SKU-ADD-CSTM-CLOR-DESC.            
           MOVE UPC-GP-CDE          TO IM008-SKU-ADD-GP-CDE.                    
           MOVE UPC-KY-ITM-IND      TO IM008-SKU-ADD-KY-ITM-IND.                
           MOVE UPC-INNR-PK-QTY     TO IM008-SKU-ADD-INNR-PK-QTY.               
           MOVE UPC-OUTR-PK-QTY     TO IM008-SKU-ADD-OUTR-PK-QTY.               
           MOVE UPC-SEA-SEQ-NBR     TO IM008-SKU-ADD-SEA-SEQ-NBR.               
           MOVE UPC-WD-UM-QTY       TO IM008-SKU-ADD-WD-UM-QTY.                 
           MOVE UPC-DPTH-UM-QTY     TO IM008-SKU-ADD-DPTH-UM-QTY.               
           MOVE UPC-HT-UM-QTY       TO IM008-SKU-ADD-HT-UM-QTY.                 
           MOVE UPC-SIZE-UM-CDE     TO IM008-SKU-ADD-SIZE-UM-CDE.               
           MOVE UPC-WT-UM-QTY       TO IM008-SKU-ADD-WT-UM-QTY.                 
           MOVE PV-WT-UM-DESC       TO IM008-SKU-ADD-WT-UM-CDE.                 
           MOVE UPC-POG-IND         TO IM008-SKU-ADD-POG-IND.                   
           MOVE UPC-TX-PRD-CDE-SEQ-NBR                                          
                                    TO IM008-SKU-ADD-TX-PRD-CDE-SEQ.            
           MOVE UPC-UNT-COST-1ST-AMT                                            
                                    TO IM008-SKU-ADD-UNT-COST-1ST.              
           MOVE UPC-REGN-PRIC-IND   TO IM008-SKU-ADD-REGN-PRIC-IND.             
           MOVE UPC-EFF-BEG-DTE     TO IM008-SKU-ADD-EFF-BEG-DTE.               
           MOVE UPC-CUSTFACG-CLOR-DESC                                          
                                    TO IM008-SKU-ADD-CUSTFACG-CLOR.             
           MOVE UPC-RPLNMT-IND      TO IM008-SKU-ADD-RPLMNT-IND.                
           MOVE UPC-EDI-ORD-IND     TO IM008-SKU-ADD-EDI-ORD-IND.               
           MOVE UPC-INTR-UPC-DESC   TO IM008-SKU-ADD-SKU-DESC.                  
           MOVE UPC-TRNSTN-IND      TO IM008-SKU-ADD-TRNSTN-IND.                
           MOVE UPC-PO-SHRT-CYC-IND TO IM008-SKU-ADD-PO-SHRT-CYC-IND.           
           MOVE UPCPLS-UNT-COST-AMT TO IM008-SKU-ADD-UNT-COST-AMT.              
           MOVE UPCPLS-UNT-RTL-AMT  TO IM008-SKU-ADD-UNT-RTL-AMT.               
           MOVE UPCPLS-MEITGP-NBR   TO IM008-SKU-ADD-MEITGP-NBR.                
           MOVE UPCPLS-UPC-STAT-CDE TO IM008-SKU-ADD-UPC-STAT-CDE.              
           MOVE UPCPLS-UPC-STAT-CHG-DTE                                         
                                    TO IM008-SKU-ADD-UPC-STAT-CHG.              
           MOVE MITGPL-MITGPL-QTY   TO IM008-SKU-ADD-MITGPL-QTY.                
           MOVE MITGPL-GP-AMT       TO IM008-SKU-ADD-MITGPL-GP-AMT.             
           MOVE UPCSTA-UPC-STAT-DESC                                            
                                    TO IM008-SKU-ADD-SKU-STAT-DESC.             
           MOVE SIZE-KOHLS-DESC     TO IM008-SKU-ADD-SIZE-DESC.                 
           MOVE COLOR-SHRT-CLOR-DESC                                            
                                    TO IM008-SKU-ADD-SHRT-CLOR-DESC.            
           MOVE UPCPLS-ITM-NBR      TO IM008-SKU-ADD-ITEM-NMBR.                 
W38861     MOVE UPC-WHSE-RPLNT-IND  TO IM008-SKU-ADD-WHSE-RPLNT-IND.            
           MOVE LOW-VALUES          TO IM008-SKU-ADD-FILLER.                    
                                                                                
      *    INSERT SKU ADD BUSINESS EVENT RECORD TO THE ITEM EVENT               
      *    MESSAGE BRIDGE DB2 TABLE.                                            
           COMPUTE IMT00001-MSG-TXT-LEN =                                       
              LENGTH OF IMRD008-RECORD -                                        
              LENGTH OF IM008-SKU-ADD-FILLER.                                   
           PERFORM 8900-INSERT-IMT-EVNT-MSG.                                    
                                                                                
      ******************************************************************        
      *  CREATE THE SKU ADD 2 BUSINESS EVENT                                    
      ******************************************************************        
       2100-CREATE-SKU-ADD2-REC.                                                
                                                                                
           SET IM008-BUS-EVENT-SKU-ADD2 TO TRUE.                                
           MOVE SPACES              TO IM008-CRE-TMST.                          
                                                                                
      *    FILL IN THE SKU ADD 2 DATA                                           
           INITIALIZE IM008-SKU-ADD2-DATA.                                      
           MOVE PV-TX-PRD-CDE-DESC  TO IM008-SKU-ADD2-TX-PRD-CDE-DESC.          
           MOVE COLOR-NRF-CLOR-DESC TO IM008-SKU-ADD2-NRF-CLOR-DESC.            
W36546     MOVE UPC-TST-PRD-TYP-SEQ-NBR                                         
W36546                              TO IM008-SKU-ADD2-TST-PD-TP-SEQ.            
W36546     MOVE PV-TST-PRD-TYP-DESC TO                                          
W36546          IM008-SKU-ADD2-TST-PD-TP-DESC.                                  
           MOVE LOW-VALUES          TO IM008-SKU-ADD2-FILLER.                   
                                                                                
      *    INSERT SKU ADD 2 BUSINESS EVENT RECORD TO THE ITEM EVENT             
      *    MESSAGE BRIDGE DB2 TABLE.                                            
           COMPUTE IMT00001-MSG-TXT-LEN =                                       
              LENGTH OF IMRD008-RECORD -                                        
              LENGTH OF IM008-SKU-ADD2-FILLER.                                  
           PERFORM 8900-INSERT-IMT-EVNT-MSG.                                    
                                                                                
      ******************************************************************        
      *  GET TUPC, TMDARVS, TVNDSTL, AND TEXTENT INFO FOR SKU ADD RECS.         
      ******************************************************************        
       8000-GET-UPC-KEY-DATA.                                                   
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
      *                                                                         
           PERFORM  WITH TEST AFTER                                             
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PS-DB-ERROR                                            
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                   SELECT  UPC.UPC_NBR                                          
                         , UPC.SUB_CL_MDSEAR_ID                                 
                         , UPC.VS_ID                                            
                         , UPC.EFF_BEG_DTE                                      
                         , UPC.KOHLS_SIZE_CDE                                   
                         , UPC.NRF_CLOR_CDE                                     
                         , UPC.CSTM_CLOR_DESC                                   
                         , UPC.EDI_ORD_IND                                      
                         , UPC.GP_CDE                                           
                         , UPC.RPLNMT_IND                                       
                         , UPC.KY_ITM_IND                                       
                         , UPC.UNT_COST_1ST_AMT                                 
                         , UPC.INNR_PK_QTY                                      
                         , UPC.OUTR_PK_QTY                                      
                         , UPC.SEA_SEQ_NBR                                      
                         , UPC.WD_UM_QTY                                        
                         , UPC.DPTH_UM_QTY                                      
                         , UPC.HT_UM_QTY                                        
                         , UPC.SIZE_UM_CDE                                      
                         , UPC.WT_UM_QTY                                        
                         , UPC.WT_UM_CDE                                        
                         , UPC.POG_IND                                          
                         , UPC.REGN_PRIC_IND                                    
                         , UPC.TX_PRD_CDE_SEQ_NBR                               
                         , UPC.CUSTFACG_CLOR_DESC                               
                         , UPC.INTR_UPC_DESC                                    
                         , UPC.TRNSTN_IND                                       
                         , UPC.PO_SHRT_CYC_IND                                  
                         , MDARVS.RMS_STYL_ID                                   
                         , VNDSTL.VS_NBR                                        
                         , VNDSTL.ENT_ID                                        
                         , EXTENT.DUN_NBR                                       
W36546                   , UPC.TST_PRD_TYP_SEQ_NBR                              
W38861                   , UPC.WHSE_RPLNT_IND                                   
                     INTO :UPC-UPC-NBR                                          
                         ,:UPC-SUB-CL-MDSEAR-ID                                 
                         ,:UPC-VS-ID                                            
                         ,:UPC-EFF-BEG-DTE                                      
                         ,:UPC-KOHLS-SIZE-CDE                                   
                         ,:UPC-NRF-CLOR-CDE                                     
                         ,:UPC-CSTM-CLOR-DESC                                   
                         ,:UPC-EDI-ORD-IND                                      
                         ,:UPC-GP-CDE                                           
                         ,:UPC-RPLNMT-IND                                       
                         ,:UPC-KY-ITM-IND                                       
                         ,:UPC-UNT-COST-1ST-AMT                                 
                         ,:UPC-INNR-PK-QTY                                      
                         ,:UPC-OUTR-PK-QTY                                      
                         ,:UPC-SEA-SEQ-NBR                                      
                         ,:UPC-WD-UM-QTY                                        
                         ,:UPC-DPTH-UM-QTY                                      
                         ,:UPC-HT-UM-QTY                                        
                         ,:UPC-SIZE-UM-CDE                                      
                         ,:UPC-WT-UM-QTY                                        
                         ,:UPC-WT-UM-CDE                                        
                         ,:UPC-POG-IND                                          
                         ,:UPC-REGN-PRIC-IND                                    
                         ,:UPC-TX-PRD-CDE-SEQ-NBR                               
                         ,:UPC-CUSTFACG-CLOR-DESC                               
                         ,:UPC-INTR-UPC-DESC                                    
                         ,:UPC-TRNSTN-IND                                       
                         ,:UPC-PO-SHRT-CYC-IND                                  
                         ,:MDARVS-RMS-STYL-ID                                   
                         ,:VNDSTL-VS-NBR                                        
                         ,:VNDSTL-ENT-ID                                        
                         ,:EXTENT-DUN-NBR                                       
W36546                   ,:UPC-TST-PRD-TYP-SEQ-NBR                              
W38861                   ,:UPC-WHSE-RPLNT-IND                                   
                     FROM TUPC UPC,                                             
                          TMDARVS MDARVS,                                       
                          TVNDSTL VNDSTL,                                       
                          TEXTENT EXTENT                                        
                    WHERE UPC.UPC_ID    = :LS-INTR-UPC-ID                       
                      AND MDARVS.SUB_CL_MDSEAR_ID                               
                                        = UPC.SUB_CL_MDSEAR_ID                  
                      AND MDARVS.VS_ID  = UPC.VS_ID                             
                      AND VNDSTL.VS_ID  = UPC.VS_ID                             
                      AND EXTENT.ENT_ID = VNDSTL.ENT_ID                         
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO PV-RETRY-COUNT                         
                   WHEN OTHER                                                   
                       MOVE '8000-GET-UPC-KEY-DATA'                            C
                                      TO PV-PARAGRAPH-NAME                 CL**1
                       MOVE 'SELECT FROM TUPC'                             CL*11
                                      TO PV-DB2-OPERATION                  CL**1
                       MOVE LS-INTR-UPC-ID                                 CL*11
                                      TO PV-UPC-ID-DOUBLE                  CL**1
                       MOVE PV-UPC-ID-DOUBLE                               CL*11
                                      TO PV-TUPC-KEY-UPC-ID                CL**1
                       MOVE PV-TUPC-KEY-INFO                               CL*11
                                      TO PV-DB2-KEY-INFO                   CL**1
                       PERFORM 9998-SQL-ROLLBACK                           CL**1
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '8000-GET-UPC-KEY-DATA'                                CL**1
                                      TO PV-PARAGRAPH-NAME                 CL**1
               MOVE 'SELECT FROM TUPC'                                     CL*11
                                      TO PV-DB2-OPERATION                  CL**1
               MOVE LS-INTR-UPC-ID    TO PV-UPC-ID-DOUBLE                  CL**1
               MOVE PV-UPC-ID-DOUBLE  TO PV-TUPC-KEY-UPC-ID                CL**1
               MOVE PV-TUPC-KEY-INFO  TO PV-DB2-KEY-INFO                   CL**1
               PERFORM 9998-SQL-ROLLBACK                                   CL**1
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  GET TUPCPLS INFO                                                       
      ******************************************************************        
       8100-GET-UPCPLS-INFO.                                                    
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
      *                                                                         
           PERFORM  WITH TEST AFTER                                             
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PS-DB-ERROR                                            
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                   SELECT  UNT_COST_AMT                                         
                         , UNT_RTL_AMT                                          
                         , ITM_NBR                                              
                         , MEITGP_NBR                                           
                         , UPC_STAT_CDE                                         
                         , UPC_STAT_CHG_DTE                                     
                     INTO :UPCPLS-UNT-COST-AMT                                  
                         ,:UPCPLS-UNT-RTL-AMT                                   
                         ,:UPCPLS-ITM-NBR                                       
                         ,:UPCPLS-MEITGP-NBR                                    
                         ,:UPCPLS-UPC-STAT-CDE                                  
                         ,:UPCPLS-UPC-STAT-CHG-DTE                              
                     FROM TUPCPLS                                               
                    WHERE UPC_ID       = :LS-INTR-UPC-ID                        
                      AND OPERCO_NBR   = :PC-OPERCO-NBR                         
                      AND STR_NBR      = 0                                      
                      AND EFF_END_TMST IS NULL                                  
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO PV-RETRY-COUNT                         
                   WHEN OTHER                                                   
                       MOVE '8100-GET-UPCPLS-INFO'                             C
                                      TO PV-PARAGRAPH-NAME                 CL**1
                       MOVE 'SELECT FROM TUPCPLS'                          CL*11
                                      TO PV-DB2-OPERATION                  CL**1
                       MOVE LS-INTR-UPC-ID                                 CL*11
                                      TO PV-UPC-ID-DOUBLE                  CL**1
                       MOVE PV-UPC-ID-DOUBLE                               CL*11
                                      TO PV-TUPCPLS-KEY-UPC-ID             CL**1
                       MOVE PV-TUPCPLS-KEY-INFO                            CL*11
                                      TO PV-DB2-KEY-INFO                   CL**1
                       PERFORM 9998-SQL-ROLLBACK                           CL**1
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '8100-GET-UPCPLS-INFO'                                 CL**1
                                      TO PV-PARAGRAPH-NAME                 CL**1
               MOVE 'SELECT FROM TUPCPLS'                                  CL*11
                                      TO PV-DB2-OPERATION                  CL**1
               MOVE LS-INTR-UPC-ID    TO PV-UPC-ID-DOUBLE                  CL**1
               MOVE PV-UPC-ID-DOUBLE  TO PV-TUPCPLS-KEY-UPC-ID             CL**1
               MOVE PV-TUPCPLS-KEY-INFO                                    CL**1
                                      TO PV-DB2-KEY-INFO                   CL**1
               PERFORM 9998-SQL-ROLLBACK                                   CL**1
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  GET GROUP PRICE INFORMATION FROM TMITGPL                               
      ******************************************************************        
       8300-GET-MITGPL-INFO.                                                    
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
      *                                                                         
           PERFORM  WITH TEST AFTER                                             
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PS-DB-ERROR                                            
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                   SELECT  MITGPL_QTY                                           
                         , GP_AMT                                               
                     INTO :MITGPL-MITGPL-QTY                                    
                         ,:MITGPL-GP-AMT                                        
                     FROM TMITGPL                                               
                    WHERE MEITGP_NBR = :UPCPLS-MEITGP-NBR                       
                      AND OPERCO_NBR = :PC-OPERCO-NBR                           
                      AND ADLOC_ID   = :PC-SPACES                               
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO PV-RETRY-COUNT                         
                   WHEN OTHER                                                   
                       MOVE '8300-GET-MITGPL-INFO'                             C
                                      TO PV-PARAGRAPH-NAME                 CL**1
                       MOVE 'SELECT FROM TMITGPL'                          CL*11
                                      TO PV-DB2-OPERATION                  CL**1
                       MOVE UPCPLS-MEITGP-NBR                              CL*11
                                      TO PV-UPC-ID-DOUBLE                  CL**1
                       MOVE PV-UPC-ID-DOUBLE                               CL*11
                                      TO PV-TMITGPL-KEY-MEITGP-NBR         CL**1
                       MOVE PV-TMITGPL-KEY-INFO                            CL*11
                                      TO PV-DB2-KEY-INFO                   CL**1
                       PERFORM 9998-SQL-ROLLBACK                           CL**1
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '8300-GET-MITGPL-INFO'                                 CL**1
                                      TO PV-PARAGRAPH-NAME                 CL**1
               MOVE 'SELECT FROM TMITGPL'                                  CL**1
                                      TO PV-DB2-OPERATION                  CL**1
               MOVE UPCPLS-MEITGP-NBR TO PV-UPC-ID-DOUBLE                  CL**1
               MOVE PV-UPC-ID-DOUBLE  TO PV-TMITGPL-KEY-MEITGP-NBR         CL**1
               MOVE PV-TMITGPL-KEY-INFO                                    CL*11
                                      TO PV-DB2-KEY-INFO                   CL**1
               PERFORM 9998-SQL-ROLLBACK                                   CL**1
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  GET A TVSCHVL DESCRIPTION CORRESPONDING TO A SEQUENCE NUMBER           
      ******************************************************************        
       8400-GET-VSCHVL-INFO.                                                    
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           PERFORM  WITH TEST AFTER                                             
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PS-DB-ERROR                                            
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                   SELECT  CHARTC_VAL_DESC                                      
                     INTO :VSCHVL-CHARTC-VAL-DESC                               
                     FROM TVSCHVL                                               
                    WHERE CHARTC_CDE     = :VSCHVL-CHARTC-CDE                   
                      AND CHARTC_SEQ_NBR = :VSCHVL-CHARTC-SEQ-NBR               
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO PV-RETRY-COUNT                         
                   WHEN OTHER                                                   
                       MOVE '8400-GET-VSCHVL-INFO'                             C
                                      TO PV-PARAGRAPH-NAME                 CL**1
                       MOVE 'SELECT FROM TVSCHVL'                          CL*11
                                      TO PV-DB2-OPERATION                  CL**1
                       MOVE VSCHVL-CHARTC-CDE                              CL*11
                                      TO PV-TVSCHVL-KEY-CHARTC-CDE         CL**1
                       MOVE VSCHVL-CHARTC-SEQ-NBR                          CL*11
                                      TO PV-TVSCHVL-KEY-SEQ-NBR            CL**1
                       MOVE PV-TVSCHVL-KEY-INFO                            CL*11
                                      TO PV-DB2-KEY-INFO                   CL**1
                       PERFORM 9998-SQL-ROLLBACK                           CL**1
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '8400-GET-VSCHVL-INFO'                                 CL**1
                                      TO PV-PARAGRAPH-NAME                 CL**1
               MOVE 'SELECT FROM TVSCHVL'                                  CL*11
                                      TO PV-DB2-OPERATION                  CL**1
               MOVE VSCHVL-CHARTC-CDE TO PV-TVSCHVL-KEY-CHARTC-CDE         CL**1
               MOVE VSCHVL-CHARTC-SEQ-NBR                                  CL*11
                                      TO PV-TVSCHVL-KEY-SEQ-NBR            CL**1
               MOVE PV-TVSCHVL-KEY-INFO                                    CL*11
                                      TO PV-DB2-KEY-INFO                   CL**1
               PERFORM 9998-SQL-ROLLBACK                                   CL**1
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  GET THE STATUS CODE DESCRIPTION FROM TUPCSTA                           
      ******************************************************************        
       8500-GET-UPCSTA-STAT-CDE-DESC.                                           
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
      *                                                                         
           PERFORM  WITH TEST AFTER                                             
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PS-DB-ERROR                                            
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                   SELECT  UPC_STAT_DESC                                        
                     INTO :UPCSTA-UPC-STAT-DESC                                 
                     FROM  TUPCSTA                                              
                    WHERE  UPC_STAT_CDE = :UPCPLS-UPC-STAT-CDE                  
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO PV-RETRY-COUNT                         
                   WHEN OTHER                                                   
                       MOVE '8500-GET-UPCSTA-STAT-CDE-DESC'                    C
                                      TO PV-PARAGRAPH-NAME                 CL**1
                       MOVE 'SELECT FROM TUPCSTA'                          CL*11
                                      TO PV-DB2-OPERATION                  CL**1
                       MOVE UPCPLS-UPC-STAT-CDE                            CL*11
                                      TO PV-TUPCSTA-KEY-UPC-STAT-CDE       CL**1
                       MOVE PV-TUPCSTA-KEY-INFO                            CL*11
                                      TO PV-DB2-KEY-INFO                   CL**1
                       PERFORM 9998-SQL-ROLLBACK                           CL**1
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '8500-GET-UPCSTA-STAT-CDE-DESC'                        CL**1
                                      TO PV-PARAGRAPH-NAME                 CL**1
               MOVE 'SELECT FROM TUPCSTA'                                  CL**1
                                      TO PV-DB2-OPERATION                  CL**1
               MOVE UPCPLS-UPC-STAT-CDE                                    CL*11
                                      TO PV-TUPCSTA-KEY-UPC-STAT-CDE       CL**1
               MOVE PV-TUPCSTA-KEY-INFO                                    CL*11
                                      TO PV-DB2-KEY-INFO                   CL**1
               PERFORM 9998-SQL-ROLLBACK                                   CL**1
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  GET THE SKU SIZE DESCRIPTION DATA FROM TSIZE                           
      ******************************************************************        
       8600-GET-SIZE-INFO.                                                      
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
      *                                                                         
           PERFORM  WITH TEST AFTER                                             
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PS-DB-ERROR                                            
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                   SELECT  KOHLS_DESC                                           
                     INTO :SIZE-KOHLS-DESC                                      
                     FROM TSIZE                                                 
                    WHERE KOHLS_SIZE_CDE = :UPC-KOHLS-SIZE-CDE                  
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO PV-RETRY-COUNT                         
                   WHEN OTHER                                                   
                       MOVE '8600-GET-SIZE-INFO'                               C
                                      TO PV-PARAGRAPH-NAME                 CL**1
                       MOVE 'SELECT FROM TSIZE'                            CL*11
                                      TO PV-DB2-OPERATION                  CL**1
                       MOVE UPC-KOHLS-SIZE-CDE                             CL*11
                                      TO PV-TSIZE-KEY-SIZE-CDE             CL**1
                       MOVE PV-TSIZE-KEY-INFO                              CL*11
                                      TO PV-DB2-KEY-INFO                   CL**1
                       PERFORM 9998-SQL-ROLLBACK                           CL**1
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '8600-GET-SIZE-INFO'                                   CL**1
                                      TO PV-PARAGRAPH-NAME                 CL**1
               MOVE 'SELECT FROM TSIZE'                                    CL**1
                                      TO PV-DB2-OPERATION                  CL**1
               MOVE UPC-KOHLS-SIZE-CDE                                     CL*11
                                      TO PV-TSIZE-KEY-SIZE-CDE             CL**1
               MOVE PV-TSIZE-KEY-INFO TO PV-DB2-KEY-INFO                   CL**1
               PERFORM 9998-SQL-ROLLBACK                                   CL**1
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  GET THE COLOR DESCRIPTION DATA FROM TCOLOR                             
      ******************************************************************        
       8700-GET-COLOR-INFO.                                                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
      *                                                                         
           PERFORM  WITH TEST AFTER                                             
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PS-DB-ERROR                                            
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                   SELECT  NRF_CLOR_DESC                                        
                          ,SHRT_CLOR_DESC                                       
                     INTO :COLOR-NRF-CLOR-DESC                                  
                         ,:COLOR-SHRT-CLOR-DESC                                 
                     FROM TCOLOR                                                
                    WHERE NRF_CLOR_CDE = :UPC-NRF-CLOR-CDE                      
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       ADD 1          TO PV-RETRY-COUNT                         
                   WHEN OTHER                                                   
                       MOVE '8700-GET-COLOR-INFO'                              C
                                      TO PV-PARAGRAPH-NAME                 CL**1
                       MOVE 'SELECT FROM TCOLOR'                           CL*11
                                      TO PV-DB2-OPERATION                  CL**1
                       MOVE UPC-NRF-CLOR-CDE                               CL*11
                                      TO PV-TCOLOR-KEY-COLOR-CDE           CL**1
                       MOVE PV-TCOLOR-KEY-INFO                             CL*11
                                      TO PV-DB2-KEY-INFO                   CL**1
                       PERFORM 9998-SQL-ROLLBACK                           CL**1
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '8700-GET-COLOR-INFO'                                  CL**1
                                      TO PV-PARAGRAPH-NAME                 CL**1
               MOVE 'SELECT FROM TCOLOR'                                   CL**1
                                      TO PV-DB2-OPERATION                  CL**1
               MOVE UPC-NRF-CLOR-CDE  TO PV-TCOLOR-KEY-COLOR-CDE           CL**1
               MOVE PV-TCOLOR-KEY-INFO                                     CL*11
                                      TO PV-DB2-KEY-INFO                   CL**1
               PERFORM 9998-SQL-ROLLBACK                                   CL**1
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  INSERT A BUSINESS EVENT RECORD TO THE ITEM EVENT MESSAGE               
      *  BRIDGE DB2 TABLE.                                                      
      ******************************************************************        
       8900-INSERT-IMT-EVNT-MSG.                                                
                                                                                
           MOVE IM008-ITEM-KEY-RMS-STYL-ID                                      
                                    TO IMT00001-RMS-STYL-ID.                    
           MOVE IM008-BUSINESS-EVENT-ID                                         
                                    TO IMT00001-BUS-EVNT-TYP-CDE.               
           MOVE IMRD008-RECORD      TO IMT00001-MSG-TXT-TEXT.                   
                                                                                
           PERFORM WITH TEST AFTER                                              
                 UNTIL SQLCODE = +0                                             
                    OR PS-DB-ERROR                                              
                    OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                  
           EXEC SQL                                                             
                INSERT INTO IMT_EVNT_MSG_BRG                                    
                       ( CRE_TMST                                               
                       ,RMS_STYL_ID                                             
                       ,BUS_EVNT_TYP_CDE                                        
                       ,MSG_TXT )                                               
                VALUES                                                          
                      ( CURRENT TIMESTAMP                                       
                      ,:IMT00001-RMS-STYL-ID                                    
                      ,:IMT00001-BUS-EVNT-TYP-CDE                               
                      ,:IMT00001-MSG-TXT)                                       
           END-EXEC                                                             
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                    CONTINUE                                                    
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                    ADD +1 TO PV-RETRY-COUNT                                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                    MOVE '8900-INSERT-IMT-EVNT-MSG'                          C  
                                   TO PV-PARAGRAPH-NAME                  CL**1  
                    MOVE 'INSERT INTO IMT_EVNT_MSG_BRG'                  CL*11  
                                   TO PV-DB2-OPERATION                   CL**1  
                    PERFORM 9998-SQL-ROLLBACK                            CL**1  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE '8900-INSERT-IMT-EVNT-MSG'                             CL**1
                                      TO PV-PARAGRAPH-NAME                 CL**1
               MOVE 'INSERT INTO IMT_EVNT_MSG_BRG'                         CL**1
                                      TO PV-DB2-OPERATION                  CL**1
               PERFORM 9998-SQL-ROLLBACK                                   CL**1
           END-IF.                                                              
                                                                                
       9998-SQL-ROLLBACK.                                                       
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES MODULE DSNTIAR TO FORMAT             
      *    THE INFO IN SQLCA INTO UNDERSTANDABLE VERBAGE.                       
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9999-ROLLBACK.                                               
                                                                                
                                                                                
       9999-ROLLBACK.                                                           
                                                                                
           DISPLAY '** IMKUP018 SQLERROR IN PARAGRAPH: '                        
                   PV-PARAGRAPH-NAME.                                           
           DISPLAY '** IMKUP018 OPERATION: ' PV-DB2-OPERATION.                  
           DISPLAY '** IMKUP018 KEY INFO: ' PV-DB2-KEY-INFO.                    
                                                                                
           EXEC SQL                                                             
               ROLLBACK                                                         
           END-EXEC.                                                            
                                                                                
           SET PS-DB-ERROR TO TRUE.                                             
