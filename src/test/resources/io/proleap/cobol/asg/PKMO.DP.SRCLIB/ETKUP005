00001 ******************************************************************07/05/95
00002  IDENTIFICATION DIVISION.                                         ETKUP005
00003 ******************************************************************   LV002
00004                                                                   ETKUP005
00005  PROGRAM-ID.              ETKUP005.                               ETKUP005
00006  AUTHOR.                  STEVE ROEGNER.                          ETKUP005
00007  INSTALLATION.            KOHLS DEPARTMENT STORES.                ETKUP005
00008                                                                   ETKUP005
00009  DATE-WRITTEN.            2-03-94.                                ETKUP005
00010  DATE-COMPILED.                                                   ETKUP005
00011                                                                   ETKUP005
00012 ******************************************************************ETKUP005
00013 *       THIS WILL DELETE ALL ROWS FROM TUNUPCS (UNMATCHED UPC    *ETKUP005
00014 *  TABLE) WHERE THE CREATE DATE (CRE_DTE) SUBTRACTED FROM THE     ETKUP005
00015 *  UNMATCHED DATE (CAT_UNMATCH_DTE) IS GREATER THAN 60 DAYS AND  *ETKUP005
00016 *  THE UNMATCHED DATE IS NOT = '9999-09-09' AND THE TP_REQ_IND   *ETKUP005
00017 *  IS = 'N'.                                                     *ETKUP005
00018 *  FOR EACH ROW DELETED, A RECORD IS WRITTEN TO A GDG AND THE    *ETKUP005
00019 *  UPC CODE AND VENDOR NAME ARE DISPLAYED.                       *ETKUP005
00020 *      IF THE VENDOR NAME IS NOT ON TUNUPCS, THE VENDOR ID IS     ETKUP005
00021 *  IS USED TO READ THE TNETVND TABLE.  FROM THAT TABLE, THE       ETKUP005
00022 *  ENT_ID IS USED TO READ THE TENTNME TABLE WHERE THE VENDOR      ETKUP005
00023 *  DESCRIPTION IS RETRIEVED AND ADDED TO THE DISPLAYED OUTPUT.    ETKUP005
00024 *                                                                *ETKUP005
00025 ******************************************************************   CL**2
00026 *  MAINTENANCE:                                                  *   CL**2
00027 *                                                                *   CL**2
00028 *  07/05/95 - MADE BY DAN FRANKLIN - WR # 3500                   *   CL**2
00029 *  DELETE THE ROW EVEN IF THE TP_REQ_IND = 'Y'                   *   CL**2
00030 ******************************************************************ETKUP005
00031                                                                   ETKUP005
00032 **************************************************************    ETKUP005
00033 *****                                                             ETKUP005
00034  ENVIRONMENT DIVISION.                                            ETKUP005
00035 ******************************************************************ETKUP005
00036                                                                   ETKUP005
00037  CONFIGURATION SECTION.                                           ETKUP005
00038                                                                   ETKUP005
00039  SOURCE-COMPUTER.        IBM-3090.                                ETKUP005
00040  OBJECT-COMPUTER.        IBM-3090.                                ETKUP005
00041                                                                   ETKUP005
00042  INPUT-OUTPUT SECTION.                                            ETKUP005
00043                                                                   ETKUP005
00044  FILE-CONTROL.                                                    ETKUP005
00045                                                                   ETKUP005
00046      SELECT DELETED-UPC-ROW-FILE                                  ETKUP005
00047          ASSIGN TO OUT01.                                         ETKUP005
00048                                                                   ETKUP005
00049 ******************************************************************ETKUP005
00050  DATA DIVISION.                                                   ETKUP005
00051 ******************************************************************ETKUP005
00052                                                                   ETKUP005
00053  FILE SECTION.                                                    ETKUP005
00054                                                                   ETKUP005
00055  FD  DELETED-UPC-ROW-FILE                                         ETKUP005
00056      RECORDING MODE IS F                                          ETKUP005
00057      LABEL RECORDS ARE STANDARD                                   ETKUP005
00058      RECORD CONTAINS 185 CHARACTERS                               ETKUP005
00059      BLOCK CONTAINS 0 RECORDS.                                    ETKUP005
00060                                                                   ETKUP005
00061  01  DELETED-UPC-ROW-RECORD PIC X(185).                           ETKUP005
00062                                                                   ETKUP005
00063      EJECT                                                        ETKUP005
00064                                                                   ETKUP005
00065  WORKING-STORAGE SECTION.                                         ETKUP005
00066                                                                   ETKUP005
00067 *                                                                 ETKUP005
00068 *  DB2 SQL ERROR/WARNING PROCESSING                               ETKUP005
00069 *                                                                 ETKUP005
00070                                                                   ETKUP005
00071      COPY DPWS004.                                                ETKUP005
00072                                                                   ETKUP005
00073  EJECT                                                            ETKUP005
00074                                                                   ETKUP005
00075 *                                                                 ETKUP005
00076 * THIS IS A WORKING STORAGE LAYOUT FOR ABEND CODE VALUES.         ETKUP005
00077 *     USE THE SET COMMAND TO PROVIDE VALUES TO ABEND-CODE.        ETKUP005
00078 *     IE SET AE-DB2-ERR TO TRUE:  CALL USING ABEND-CODE.          ETKUP005
00079 *                                                                 ETKUP005
00080                                                                   ETKUP005
00081  01  ABEND-CODE                  PIC S9(04)   VALUE +0            ETKUP005
00082                                               COMP                ETKUP005
00083                                               SYNC.               ETKUP005
00084                                                                   ETKUP005
00085      88  AC-INVALID-KEY-WRITE                 VALUE +4001.        ETKUP005
00086      88  AC-INVALID-KEY-READ                  VALUE +4002.        ETKUP005
00087      88  AC-INVALID-CONTROL-CARD              VALUE +4003.        ETKUP005
00088      88  AC-TABLE-OVERFLOW                    VALUE +4004.        ETKUP005
00089      88  AC-OPEN-FILE-ERROR                   VALUE +4005.        ETKUP005
00090      88  AC-CLOSE-FILE-ERROR                  VALUE +4006.        ETKUP005
00091      88  AC-SEQUENCE-ERROR                    VALUE +4007.        ETKUP005
00092      88  AC-INVALID-DATA                      VALUE +4008.        ETKUP005
00093      88  AC-INVALID-KEYPUNCH                  VALUE +4009.        ETKUP005
00096      88  AC-INTERNAL-SORT-ERROR               VALUE +4012.        ETKUP005
00097      88  AC-DB2-ERROR                         VALUE +4013.        ETKUP005
00098                                                                   ETKUP005
00099 *                                                                 ETKUP005
00100  01  PA-PROGRAM-ACCUMULATORS.                                     ETKUP005
00101      05  PA-RETRY-COUNT          PIC S9(04)   VALUE 0 COMP.       ETKUP005
00102 *                                                                 ETKUP005
00103  01  PC-PROGRAM-CONSTANTS.                                        ETKUP005
00104      05  PC-UNKNOWN-VENDOR-NAME  PIC X(20)    VALUE               ETKUP005
00105                                              'NO VENDOR NAME  '.  ETKUP005
00106      05  PC-MORE-THAN-MAX-RETRY  PIC S9(04)   VALUE 4 COMP.       ETKUP005
00107      05  PC-SIXTY                PIC 9(02)    VALUE 60.           ETKUP005
00108      05  PC-HUNDRED              PIC 9(03)    VALUE 100.          ETKUP005
00109      05  PC-MAX-UNMATCH-DTE      PIC X(10)    VALUE               ETKUP005
00110                                               '9999-09-09'.       ETKUP005
00111      05  PC-MAX-RETRIES          PIC S9(4)    VALUE +3            ETKUP005
00112                                               COMP SYNC.          ETKUP005
00113  01  PV-PROGRAM-VARIABLES.                                        ETKUP005
00114      05  PV-PARAGRAPH-NMBR       PIC X(05)    VALUE SPACES.       ETKUP005
00115      05  PV-SQL-SUB              PIC 9(03)    VALUE ZEROS.        ETKUP005
00116      05  PV-DISPLAY-UPC          PIC X(15)    VALUE SPACES.       ETKUP005
00117      05  PV-DB2-OPERATION        PIC X(20)    VALUE SPACES.       ETKUP005
00118      05  PV-DAYS-DIFF            PIC S9(09)   COMP                ETKUP005
00119                                               VALUE ZEROS.        ETKUP005
00120                                                                   ETKUP005
00121  01  PS-PROGRAM-SWITCHES.                                         ETKUP005
00122      05  PS-END-UPCS-SW          PIC X(01)    VALUE 'N'.          ETKUP005
00123          88  PS-END-UPCS-YES                  VALUE 'Y'.          ETKUP005
00124                                                                   ETKUP005
00125  01  OR-OUTPUT-REC.                                               ETKUP005
00126      05  OR-UPC-CODE             PIC S9(15)V  COMP-3              ETKUP005
00127                                               VALUE ZEROS.        ETKUP005
00128      05  OR-CRE-DTE              PIC X(10)    VALUE SPACES.       ETKUP005
00129      05  OR-PROD-ID              PIC X(20)    VALUE SPACES.       ETKUP005
00130      05  OR-PROD-DESC            PIC X(30)    VALUE SPACES.       ETKUP005
00131      05  OR-SIZE-CDE             PIC X(05)    VALUE SPACES.       ETKUP005
00132      05  OR-SIZE-DESC            PIC X(20)    VALUE SPACES.       ETKUP005
00133      05  OR-COLOR-CDE            PIC X(03)    VALUE SPACES.       ETKUP005
00134      05  OR-COLOR-DESC           PIC X(20)    VALUE SPACES.       ETKUP005
00135      05  OR-TRANS-IND            PIC X(01)    VALUE SPACES.       ETKUP005
00136      05  OR-UPC-CAT-VEND-ID      PIC X(15)    VALUE SPACES.       ETKUP005
00137      05  OR-UPC-CAT-VEND-NAME    PIC X(35)    VALUE SPACES.       ETKUP005
00138      05  OR-CAT-UNMATCH-DTE      PIC X(10)    VALUE SPACES.       ETKUP005
00139      05  OR-NETWK-ID             PIC X(01)    VALUE SPACES.       ETKUP005
00140      05  OR-SYS-ID               PIC X(02)    VALUE SPACES.       ETKUP005
00141      05  OR-PO-NBR               PIC S9(09)   COMP                ETKUP005
00142                                               VALUE ZEROS.        ETKUP005
00143 *                                                                 ETKUP005
00144 ****                                                              ETKUP005
00145 *   SQLCA                                                         ETKUP005
00146 ****                                                              ETKUP005
00147      EXEC SQL                                                     ETKUP005
00148          INCLUDE SQLCA                                            ETKUP005
00149      END-EXEC.                                                    ETKUP005
00150                                                                   ETKUP005
00151      EJECT                                                        ETKUP005
00152                                                                   ETKUP005
00153 ****                                                              ETKUP005
00154 *   DCLGEN FOR THE UPC TABLE                                      ETKUP005
00155 ****                                                              ETKUP005
00156      EXEC SQL                                                     ETKUP005
00157          INCLUDE TUNUPCS                                          ETKUP005
00158      END-EXEC.                                                    ETKUP005
00159                                                                   ETKUP005
00160      EJECT                                                        ETKUP005
00161                                                                   ETKUP005
00162 ****                                                              ETKUP005
00163 *   DCLGEN FOR THE TNETVND TABLE                                  ETKUP005
00164 ****                                                              ETKUP005
00165      EXEC SQL                                                     ETKUP005
00166          INCLUDE TNETVND                                          ETKUP005
00167      END-EXEC.                                                    ETKUP005
00168                                                                   ETKUP005
00169      EJECT                                                        ETKUP005
00170                                                                   ETKUP005
00171 ****                                                              ETKUP005
00172 *   DCLGEN FOR THE VENDOR NAME TABLE.                             ETKUP005
00173 ****                                                              ETKUP005
00174      EXEC SQL                                                     ETKUP005
00175          INCLUDE TENTNME                                          ETKUP005
00176      END-EXEC.                                                    ETKUP005
00177                                                                   ETKUP005
00178      EJECT                                                        ETKUP005
00179                                                                   ETKUP005
00180 ****                                                              ETKUP005
00181 *  SQL DB2 STORE UPC CURSOR DECLARATION                           ETKUP005
00182 ****                                                              ETKUP005
00183      EXEC SQL                                                     ETKUP005
00184          DECLARE UPC_CSR CURSOR FOR                               ETKUP005
00185              SELECT UPC_CDE,                                      ETKUP005
00186                     CRE_DTE,                                      ETKUP005
00187                     PROD_ID,                                      ETKUP005
00188                     PROD_DESC,                                    ETKUP005
00189                     SIZE_CDE,                                     ETKUP005
00190                     SIZE_DESC,                                    ETKUP005
00191                     COLOR_CDE,                                    ETKUP005
00192                     COLOR_DESC,                                   ETKUP005
00193                     TRANS_IND,                                    ETKUP005
00194                     UPC_CAT_VEND_ID,                              ETKUP005
00195                     UPC_CAT_VEND_NME,                             ETKUP005
00196                     CAT_UNMATCH_DTE,                              ETKUP005
00197                     NETWK_ID,                                     ETKUP005
00198                     SYST_ID,                                      ETKUP005
00199                     PO_NBR,                                       ETKUP005
00200              DAYS(CAT_UNMATCH_DTE) - DAYS(CRE_DTE)                ETKUP005
00201              FROM TUNUPCS                                         ETKUP005
00202      END-EXEC.                                                    ETKUP005
00203                                                                   ETKUP005
00204 *                                                                 ETKUP005
00205 ****                                                              ETKUP005
00206 *  SQL DB2 SKU CURSOR FOR TNETVND                                 ETKUP005
00207 ****                                                              ETKUP005
00208 *                                                                 ETKUP005
00209      EXEC SQL                                                     ETKUP005
00210          DECLARE VND_CSR CURSOR FOR                               ETKUP005
00211              SELECT ENT_ID                                        ETKUP005
00212                  FROM TNETVND                                     ETKUP005
00213                  WHERE  UPC_CAT_VEND_ID =                         ETKUP005
00214                  :UNUPCS-UPC-CAT-VEND-ID                          ETKUP005
00215      END-EXEC.                                                    ETKUP005
00216                                                                   ETKUP005
00217      EJECT                                                        ETKUP005
00218 *                                                                 ETKUP005
00219 *                                                                 ETKUP005
00220 ******************************************************************ETKUP005
00221  PROCEDURE DIVISION.                                              ETKUP005
00222 ******************************************************************ETKUP005
00223 *                                                                 ETKUP005
00224  A000-MAINLINE.                                                   ETKUP005
00225 *                                                                 ETKUP005
00226      OPEN OUTPUT DELETED-UPC-ROW-FILE.                            ETKUP005
00227                                                                   ETKUP005
00228      PERFORM                                                      ETKUP005
00229          WITH TEST AFTER                                          ETKUP005
00230          VARYING PV-SQL-SUB                                       ETKUP005
00231          FROM 1 BY 1                                              ETKUP005
00232          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES) OR                   ETKUP005
00233                (SQLCODE = 0) OR                                   ETKUP005
00234                (SQLCODE = 100)                                    ETKUP005
00235                                                                   ETKUP005
00236          EXEC SQL                                                 ETKUP005
00237               OPEN UPC_CSR                                        ETKUP005
00238          END-EXEC                                                 ETKUP005
00239 *                                                                 ETKUP005
00240          EVALUATE TRUE                                            ETKUP005
00241              WHEN SQLCODE = -904                                  ETKUP005
00242              WHEN SQLCODE = -911                                  ETKUP005
00243                   CONTINUE                                        ETKUP005
00244              WHEN SQLWARN0 NOT = SPACES                           ETKUP005
00245              WHEN SQLCODE < ZEROS                                 ETKUP005
00246              WHEN SQLCODE > ZEROS                                 ETKUP005
00247                   MOVE 'A100-' TO PV-PARAGRAPH-NMBR               ETKUP005
00248                   MOVE 'UPC_CSR OPEN' TO PV-DB2-OPERATION         ETKUP005
00249                   PERFORM Z100-SQL-ABEND                          ETKUP005
00250              WHEN SQLCODE = ZERO                                  ETKUP005
00251              PERFORM B100-FETCH-UPC UNTIL                         ETKUP005
00252                               PS-END-UPCS-SW = 'Y'                ETKUP005
00253           END-EVALUATE                                            ETKUP005
00254      END-PERFORM.                                                 ETKUP005
00255                                                                   ETKUP005
00256      IF PV-SQL-SUB > PC-MAX-RETRIES                               ETKUP005
00257           MOVE 'A100-' TO PV-PARAGRAPH-NMBR                       ETKUP005
00258           MOVE 'EXCEEDED MAX RETRIES' TO PV-DB2-OPERATION         ETKUP005
00259           PERFORM Z100-SQL-ABEND                                  ETKUP005
00260      END-IF                                                       ETKUP005
00261                                                                   ETKUP005
00262      EXEC SQL                                                     ETKUP005
00263           CLOSE UPC_CSR                                           ETKUP005
00264      END-EXEC.                                                    ETKUP005
00265                                                                   ETKUP005
00266      EVALUATE TRUE                                                ETKUP005
00267          WHEN SQLWARN0 NOT = SPACES                               ETKUP005
00268          WHEN SQLCODE < ZEROS                                     ETKUP005
00269          WHEN SQLCODE > ZEROS                                     ETKUP005
00270               MOVE 'A100-' TO PV-PARAGRAPH-NMBR                   ETKUP005
00271               MOVE 'UPC_CSR CLOSE' TO PV-DB2-OPERATION            ETKUP005
00272               PERFORM Z100-SQL-ABEND                              ETKUP005
00273          WHEN SQLCODE = ZERO                                      ETKUP005
00274               CONTINUE                                            ETKUP005
00275      END-EVALUATE.                                                ETKUP005
00276                                                                   ETKUP005
00277      CLOSE DELETED-UPC-ROW-FILE                                   ETKUP005
00278      STOP RUN.                                                    ETKUP005
00279                                                                   ETKUP005
00280                                                                   ETKUP005
00281                                                                   ETKUP005
00282                                                                   ETKUP005
00283                                                                   ETKUP005
00284 *                                                                 ETKUP005
00285 ******************************************************************ETKUP005
00286 *  THIS PARAGRAPH FETCHES THE ROWS OFF TUNUPCS TABLE.  THE       *ETKUP005
00287 *  VALUES ON THE UNMATCHED DATE AND THE CREATE DATE ARE          *ETKUP005
00288 *  COMPARED TO DETERMINE IF THE DIFFERENCE IS GREATER THAN 60,   *ETKUP005
00289 *  AND THE UNMATCHED DATE IS NOT '9999-09-09' TO SEE IF THE ROW  *   CL**2
00290 *  IS TO BE DELETED.                                             *   CL**2
00291 *                                                                *ETKUP005
00292 * ***  PERFORMED BY A100-MAINLINE                            *** *ETKUP005
00293 ******************************************************************ETKUP005
00294 *                                                                 ETKUP005
00295  B100-FETCH-UPC.                                                  ETKUP005
00296      EXEC SQL                                                     ETKUP005
00297        FETCH UPC_CSR INTO :UNUPCS-UPC-CDE,                        ETKUP005
00298                           :UNUPCS-CRE-DTE,                        ETKUP005
00299                           :UNUPCS-PROD-ID,                        ETKUP005
00300                           :UNUPCS-PROD-DESC,                      ETKUP005
00301                           :UNUPCS-SIZE-CDE,                       ETKUP005
00302                           :UNUPCS-SIZE-DESC,                      ETKUP005
00303                           :UNUPCS-COLOR-CDE,                      ETKUP005
00304                           :UNUPCS-COLOR-DESC,                     ETKUP005
00305                           :UNUPCS-TRANS-IND,                      ETKUP005
00306                           :UNUPCS-UPC-CAT-VEND-ID,                ETKUP005
00307                           :UNUPCS-UPC-CAT-VEND-NME,               ETKUP005
00308                           :UNUPCS-CAT-UNMATCH-DTE,                ETKUP005
00309                           :UNUPCS-NETWK-ID,                       ETKUP005
00310                           :UNUPCS-SYST-ID,                        ETKUP005
00311                           :UNUPCS-PO-NBR,                         ETKUP005
00312                           :PV-DAYS-DIFF                           ETKUP005
00313      END-EXEC.                                                    ETKUP005
00314 *                                                                 ETKUP005
00315          EVALUATE TRUE                                            ETKUP005
00316              WHEN SQLCODE = -904                                  ETKUP005
00317              WHEN SQLCODE = -913                                  ETKUP005
00318              WHEN SQLWARN0 NOT = SPACES                           ETKUP005
00319              WHEN (SQLCODE NOT = ZERO  AND                        ETKUP005
00320                   SQLCODE NOT = PC-HUNDRED)                       ETKUP005
00321                   MOVE 'B100-' TO PV-PARAGRAPH-NMBR               ETKUP005
00322                   MOVE 'UPC_CSR FETCH' TO PV-DB2-OPERATION        ETKUP005
00323                   PERFORM Z100-SQL-ABEND                          ETKUP005
00324              WHEN SQLCODE = PC-HUNDRED                            ETKUP005
00325                   SET PS-END-UPCS-YES TO TRUE                     ETKUP005
00326              WHEN SQLCODE = ZERO                                  ETKUP005
00327                   IF (PV-DAYS-DIFF > PC-SIXTY AND                 ETKUP005
00328                      UNUPCS-CAT-UNMATCH-DTE NOT =                 ETKUP005
00329                      PC-MAX-UNMATCH-DTE)                             CL**2
00330                        PERFORM C100-CREATE-OUTPUT-REC             ETKUP005
00331                        PERFORM F100-DELETE-ROW                    ETKUP005
00332                   ELSE                                            ETKUP005
00333                      CONTINUE                                     ETKUP005
00334                   END-IF                                          ETKUP005
00335          END-EVALUATE.                                            ETKUP005
00336                                                                   ETKUP005
00337                                                                   ETKUP005
00338 ******************************************************************ETKUP005
00339 *  THIS PARAGRAPH CONTROLS THE WRITING OF OUTPUT RECORDS.        *ETKUP005
00340 *  IT CONTROLS THE DETERMINATION OF THE VENDOR NAME IF IT        *ETKUP005
00341 *  DOES NOT EXIST ON THE UNMATCHED UPC TABLE.                    *ETKUP005
00342 *                                                                *ETKUP005
00343 * *** PERFORMED BY B100-FETCH-UPC ***                            *ETKUP005
00344 ******************************************************************ETKUP005
00345 *                                                                 ETKUP005
00346  C100-CREATE-OUTPUT-REC.                                          ETKUP005
00347      IF UNUPCS-UPC-CAT-VEND-NME = SPACES                          ETKUP005
00348         MOVE DCLTUNUPCS TO OR-OUTPUT-REC                          ETKUP005
00349         PERFORM D100-VEND-NAME-RETRIEVE                           ETKUP005
00350      ELSE                                                         ETKUP005
00351         MOVE DCLTUNUPCS TO OR-OUTPUT-REC                          ETKUP005
00352      END-IF.                                                      ETKUP005
00353                                                                   ETKUP005
00354      MOVE UNUPCS-UPC-CDE TO PV-DISPLAY-UPC                        ETKUP005
00355      DISPLAY 'UPC = ' PV-DISPLAY-UPC                              ETKUP005
00356      DISPLAY 'VENDOR = ' OR-UPC-CAT-VEND-NAME                     ETKUP005
00357      DISPLAY '     '                                              ETKUP005
00358                                                                   ETKUP005
00359      WRITE DELETED-UPC-ROW-RECORD FROM OR-OUTPUT-REC.             ETKUP005
00360      INITIALIZE OR-OUTPUT-REC.                                    ETKUP005
00361      EJECT                                                        ETKUP005
00362                                                                   ETKUP005
00363 ******************************************************************ETKUP005
00364 *  THIS PARAGRAPH CONTROLS THE RETRIEVAL OF THE VENDOR NAME IF   *ETKUP005
00365 *  IT DOES NOT ALREADY EXIST.                                    *ETKUP005
00366 *                                                                *ETKUP005
00367 *  *** PERFORMED BY C100-CREATE-OUTPUT-REC ***                   *ETKUP005
00368 ******************************************************************ETKUP005
00369 *                                                                 ETKUP005
00370  D100-VEND-NAME-RETRIEVE.                                         ETKUP005
00371 *                                                                 ETKUP005
00372 *                                                                 ETKUP005
00373      PERFORM                                                      ETKUP005
00374          WITH TEST AFTER                                          ETKUP005
00375          VARYING PV-SQL-SUB                                       ETKUP005
00376          FROM 1 BY 1                                              ETKUP005
00377          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES) OR                   ETKUP005
00378                (SQLCODE = 0)                                      ETKUP005
00379          EXEC SQL                                                 ETKUP005
00380               OPEN VND_CSR                                        ETKUP005
00381          END-EXEC                                                 ETKUP005
00382 *                                                                 ETKUP005
00383          EVALUATE TRUE                                            ETKUP005
00384              WHEN SQLCODE = -904                                  ETKUP005
00385              WHEN SQLCODE = -911                                  ETKUP005
00386                  CONTINUE                                         ETKUP005
00387              WHEN SQLWARN0 NOT = SPACES                           ETKUP005
00388              WHEN SQLCODE < ZEROS                                 ETKUP005
00389              WHEN SQLCODE > ZEROS                                 ETKUP005
00390                   MOVE 'D100-' TO PV-PARAGRAPH-NMBR               ETKUP005
00391                   MOVE 'VND_CSR OPEN' TO PV-DB2-OPERATION         ETKUP005
00392                   PERFORM Z100-SQL-ABEND                          ETKUP005
00393              WHEN SQLCODE = ZERO                                  ETKUP005
00394                   CONTINUE                                        ETKUP005
00395          END-EVALUATE                                             ETKUP005
00396      END-PERFORM.                                                 ETKUP005
00397                                                                   ETKUP005
00398      IF PV-SQL-SUB > PC-MAX-RETRIES                               ETKUP005
00399           MOVE 'D100-' TO PV-PARAGRAPH-NMBR                       ETKUP005
00400           MOVE 'EXCEEDED MAX RETRIES' TO PV-DB2-OPERATION         ETKUP005
00401           PERFORM Z100-SQL-ABEND                                  ETKUP005
00402      END-IF.                                                      ETKUP005
00403                                                                   ETKUP005
00404      EXEC SQL                                                     ETKUP005
00405           FETCH VND_CSR INTO :NETVND-ENT-ID                       ETKUP005
00406      END-EXEC.                                                    ETKUP005
00407                                                                   ETKUP005
00408      EVALUATE TRUE                                                ETKUP005
00409          WHEN SQLWARN0 NOT = SPACES                               ETKUP005
00410          WHEN (SQLCODE NOT = ZERO AND                             ETKUP005
00411               SQLCODE NOT = PC-HUNDRED)                           ETKUP005
00412               MOVE 'D100-' TO PV-PARAGRAPH-NMBR                   ETKUP005
00413               MOVE 'VND_CSR FETCH' TO PV-DB2-OPERATION            ETKUP005
00414               PERFORM Z100-SQL-ABEND                              ETKUP005
00415          WHEN SQLCODE = PC-HUNDRED                                ETKUP005
00416               MOVE PC-UNKNOWN-VENDOR-NAME TO                      ETKUP005
00417                    OR-UPC-CAT-VEND-NAME                           ETKUP005
00418          WHEN SQLCODE = ZERO                                      ETKUP005
00419               PERFORM E100-VEND-NAME                              ETKUP005
00420      END-EVALUATE.                                                ETKUP005
00421                                                                   ETKUP005
00422      EXEC SQL                                                     ETKUP005
00423           CLOSE VND_CSR                                           ETKUP005
00424      END-EXEC.                                                    ETKUP005
00425                                                                   ETKUP005
00426      EVALUATE TRUE                                                ETKUP005
00427          WHEN SQLWARN0 NOT = SPACES                               ETKUP005
00428          WHEN SQLCODE < ZEROS                                     ETKUP005
00429          WHEN SQLCODE > ZEROS                                     ETKUP005
00430               MOVE 'D100-' TO PV-PARAGRAPH-NMBR                   ETKUP005
00431               MOVE 'VND_CSR CLOSE' TO PV-DB2-OPERATION            ETKUP005
00432               PERFORM Z100-SQL-ABEND                              ETKUP005
00433          WHEN SQLCODE = ZERO                                      ETKUP005
00434               CONTINUE                                            ETKUP005
00435      END-EVALUATE.                                                ETKUP005
00436 *                                                                 ETKUP005
00437 ******************************************************************ETKUP005
00438 * THIS PARAGRAPH GETS THE VENDOR NAME OFF TABLE TENTNME BASED ON  ETKUP005
00439 * THE ENT_ID WHICH WAS RETRIEVED FROM TNETVND.                    ETKUP005
00440 *                                                                 ETKUP005
00441 * *** PERFORMED BY D100-VEND-NAME-RETRIEVE ***                    ETKUP005
00442 ******************************************************************ETKUP005
00443 *                                                                 ETKUP005
00444  E100-VEND-NAME.                                                  ETKUP005
00445 *                                                                 ETKUP005
00446                                                                   ETKUP005
00447      EXEC SQL                                                     ETKUP005
00448          SELECT ENT_NAME_DESC                                     ETKUP005
00449          INTO  :ENTNME-ENT-NAME-DESC                              ETKUP005
00450          FROM   TENTNME                                           ETKUP005
00451          WHERE  ENT_ID   = :NETVND-ENT-ID AND                     ETKUP005
00452                 TYPE_CDE = '01'                                   ETKUP005
00453      END-EXEC.                                                    ETKUP005
00454                                                                   ETKUP005
00455      EVALUATE TRUE                                                ETKUP005
00456          WHEN SQLWARN0 NOT = SPACES                               ETKUP005
00457          WHEN (SQLCODE NOT = ZERO AND                             ETKUP005
00458               SQLCODE NOT = PC-HUNDRED)                           ETKUP005
00459               MOVE 'E100-' TO PV-PARAGRAPH-NMBR                   ETKUP005
00460               MOVE 'VENDNME FETCH' TO PV-DB2-OPERATION            ETKUP005
00461               PERFORM Z100-SQL-ABEND                              ETKUP005
00462          WHEN SQLCODE = PC-HUNDRED                                ETKUP005
00463               MOVE PC-UNKNOWN-VENDOR-NAME TO                      ETKUP005
00464                    OR-UPC-CAT-VEND-NAME                           ETKUP005
00465          WHEN SQLCODE = ZERO                                      ETKUP005
00466               MOVE ENTNME-ENT-NAME-DESC TO                        ETKUP005
00467               OR-UPC-CAT-VEND-NAME                                ETKUP005
00468      END-EVALUATE.                                                ETKUP005
00469 *                                                                 ETKUP005
00470 *                                                                 ETKUP005
00471 ******************************************************************ETKUP005
00472 * THIS PARAGRAPH DELETES THE ROW OFF OF THE UNMATCHED UPC TABLE   ETKUP005
00473 * IF THE DIFFERENCE BETWEEN THE UNMATCHED DATE AND THE CREATE     ETKUP005
00474 * DATE IS GREATER THAN 60.                                        ETKUP005
00475 *                                                                 ETKUP005
00476 * *** PERFORMED BY B100-FETCH-UPC ***                             ETKUP005
00477 ******************************************************************ETKUP005
00478 *                                                                 ETKUP005
00479  F100-DELETE-ROW.                                                 ETKUP005
00480 *                                                                 ETKUP005
00481      EXEC SQL                                                     ETKUP005
00482           DELETE FROM TUNUPCS                                     ETKUP005
00483           WHERE UPC_CDE = :UNUPCS-UPC-CDE                         ETKUP005
00484      END-EXEC.                                                    ETKUP005
00485                                                                   ETKUP005
00486      EVALUATE TRUE                                                ETKUP005
00487          WHEN SQLWARN0 NOT = SPACES                               ETKUP005
00488          WHEN SQLCODE < ZEROS                                     ETKUP005
00489          WHEN SQLCODE > ZEROS                                     ETKUP005
00490               MOVE 'F100-' TO PV-PARAGRAPH-NMBR                   ETKUP005
00491               MOVE 'DELETE ROW' TO PV-DB2-OPERATION               ETKUP005
00492               PERFORM Z100-SQL-ABEND                              ETKUP005
00493          WHEN SQLCODE = ZERO                                      ETKUP005
00494               CONTINUE                                            ETKUP005
00495      END-EVALUATE.                                                ETKUP005
00496 ******************************************************************ETKUP005
00497 * Z100-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE      ETKUP005
00498 * EVENT THAT AN SQL ERROR CODE OCCURS.                            ETKUP005
00499 ******************************************************************ETKUP005
00500  Z100-SQL-ABEND.                                                  ETKUP005
00501 *                                                                 ETKUP005
00502      DISPLAY '** ABEND **         **'.                            ETKUP005
00503      DISPLAY '** PROGRAM          ** = FSKUT036'.                 ETKUP005
00504      DISPLAY '** PARAGRAPH NMBR   ** = ' PV-PARAGRAPH-NMBR.       ETKUP005
00505      DISPLAY '** DB2 OPERATION    ** = ' PV-DB2-OPERATION.        ETKUP005
00506                                                                   ETKUP005
00507      EJECT                                                        ETKUP005
00508                                                                   ETKUP005
00509 *                                                                 ETKUP005
00510 **********                                                        ETKUP005
00511 *  COPYBOOK FOR STANDARD DB2 ERROR ROUTINE                        ETKUP005
00512 *  THIS SHOULD BE COPIED TOGETHER DPWS004 FOR THE WORKING-STORAGE ETKUP005
00513 **********                                                        ETKUP005
00514 *                                                                 ETKUP005
00515      COPY DPPD004.                                                ETKUP005
00516 *                                                                 ETKUP005
00517      IF SQLCODE NOT = -911                                        ETKUP005
00518          EXEC SQL                                                 ETKUP005
00519               COMMIT                                              ETKUP005
00520          END-EXEC                                                 ETKUP005
00521      END-IF.                                                      ETKUP005
00522 *                                                                 ETKUP005
00523      SET AC-DB2-ERROR TO TRUE.                                    ETKUP005
00524      CALL 'ILBOABN0' USING ABEND-CODE.                            ETKUP005
00525 *                                                                 ETKUP005
