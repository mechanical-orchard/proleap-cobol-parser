      ***************************************************************** 00010000
       IDENTIFICATION DIVISION.                                         00020000
      ***************************************************************** 00030000
                                                                        00040000
       PROGRAM-ID.             XSKUP906.                                00050000
       AUTHOR.                 KOHLS.                                   00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            FEBRUARY 2005.                           00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ***************************************************************** 00110000
      *                                                                 00120000
      *  THIS PROGRAM IS RESPONSIBLE FOR UPDATING THE SLS_VOL_RNK_CDE   00130000
      *  FIELD ON THE XST_LOC_ATTR TABLE IN THE XS DOMAIN. IT USES A    00140000
      *  CONTROL CARD THAT CONTAINS A STORE NUMBER ALONG WITH THE       00150000
      *  NEW SLS_VOL_RNK_CDE THAT THE STORE WILL BE CHANGED TO.         00160000
      *                                                                 00170000
      *  THIS PROGRAM RUNS ON REQUEST AND WILL BE USED TO UPDATE STORES 00180000
      *  AS THEY ARE BROUGHT ON TO THE NEW PROMO SYSTEM.                00190000
      *                                                                 00200000
      *                                                                 00210000
      *   INPUT: INP01 - CONTROL CARD THAT CONTAINS STORE NUMBER AND    00220000
      *                  THE NEW SLS_VOL_RNK_CDE.                       00230000
      *                                                                 00250000
      ***************************************************************** 00260000
      * HISTORY LOG:                                                    00270000
      ***************************************************************** 00280000
      * DATE:                                                           00290000
      * WR/IR#:                                                         00300000
      * PROGRAMMER:                                                     00310000
      * DESCRIPTION:                                                    00320000
      *                                                                 00330000
      ***************************************************************** 00340000
       ENVIRONMENT DIVISION.                                            00350000
      ***************************************************************** 00360000
                                                                        00370000
       CONFIGURATION SECTION.                                           00380000
                                                                        00390000
       SOURCE-COMPUTER.        IBM-3090.                                00400000
       OBJECT-COMPUTER.        IBM-3090.                                00410000
                                                                        00420000
       INPUT-OUTPUT SECTION.                                            00430000
                                                                        00440000
       FILE-CONTROL.                                                    00450000
                                                                        00460000
           SELECT CONTROL-CARD-CONV-STORES                              00470000
               ASSIGN TO INP01.                                         00480000
                                                                        00490000
      ******************************************************************00500000
       DATA DIVISION.                                                   00510000
      ******************************************************************00520000
                                                                        00530000
       FILE SECTION.                                                    00540000
                                                                        00550000
       FD  CONTROL-CARD-CONV-STORES                                     00560000
           RECORDING MODE IS F                                          00570000
           BLOCK CONTAINS 0 RECORDS                                     00580000
           LABEL RECORDS ARE STANDARD.                                  00590000
                                                                        00600000
       01  CONTROL-CARD-STORE-RECORD        PIC X(80).                  00610000
                                                                        00620000
      ******************************************************************00630000
       WORKING-STORAGE SECTION.                                         00640000
      ******************************************************************00650000
                                                                        00660000
      ******************************************************************00670000
      * INPUT CONTROL CARD 1 CONTAINS TOMORROWS DATE (SCHEDULED DATE +1)00680000
      ******************************************************************00690000
                                                                        00700000
       01  CC-CONTROL-CARD-1.                                           00710000
           10  PV-CONV-STORE                PIC 9(05)   VALUE 0.        00720000
           10  PV-SLS-VOL-RNK-CDE           PIC X(02)   VALUE SPACES.   00730000
           10  PV-FILLER                    PIC X(73)   VALUE SPACES.   00740000
                                                                        00750000
       01  AC-ABEND-CODE                    PIC S9(04) VALUE ZERO       00760000
                                                       COMP SYNC.       00770000
           88  AC-CONTROL-CARD-ERROR                   VALUE +4003.     00780000
           88  AC-DB2-ERROR                            VALUE +4013.     00790000
                                                                        00800000
       01  PS-PROGRAM-SWITCHES.                                         00810000
           05  PS-EOF-CC1-SW                PIC  X(01) VALUE 'N'.       00820000
               88  PS-EOF-CC1-YES                      VALUE 'Y'.       00830000
                                                                        00840000
       01  PC-CONSTANTS.                                                00850000
           05  PC-MAX-ATTEMPT-COUNT         PIC  9(01) VALUE 5.         00860000
           05  PC-CURRENT-PROGRAM-NAME      PIC  X(08) VALUE            00870000
                                            'XSKUP906'.                 00880000
                                                                        00890000
       01  PV-MISC-FIELDS.                                              00900000
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.     00910000
           05  PV-OPERATION-MSG-1           PIC  X(40) VALUE SPACE.     00920000
           05  PV-OPERATION-MSG-2           PIC  X(40) VALUE SPACE.     00930000
           05  PV-DB2-OPERATION             PIC  X(40) VALUE SPACE.     00940000
           05  PV-ATTEMPT-COUNT             PIC  9(04) VALUE ZEROS.     00950000
           05  PV-HOLD-STORE-ID             PIC  S9(04) COMP.           00960000
           05  PV-HOLD-SLS-VOL-RNK-CDE      PIC  X(02).                 00970000
           05  PV-LAST-ERR-POS-LVL-ID       PIC  9(05) VALUE ZEROS.     00980000
                                                                        00990000
      ******************************************************************01000000
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                            01010000
      ******************************************************************01020000
                                                                        01030000
           COPY DPWS004.                                                01040000
                                                                        01050000
      ******************************************************************01060000
      *  DB2 COMMUNICATION AREA.                                        01070000
      ******************************************************************01080000
                                                                        01090000
           EXEC SQL                                                     01100000
                INCLUDE SQLCA                                           01110000
           END-EXEC.                                                    01120000
                                                                        01130000
      ******************************************************************01140000
      *  DB2 TABLE XST_LOC_ATTR.                                        01150000
      ******************************************************************01160000
                                                                        01170000
           EXEC SQL                                                     01180000
                INCLUDE XST00025                                        01190000
           END-EXEC.                                                    01200000
                                                                        01210000
      ***************************************************************** 01220000
       PROCEDURE DIVISION.                                              01230000
      ***************************************************************** 01240000
                                                                        01250000
      ******************************************************************01260000
      *  0000-MAINLINE-PARAGRAPH.                                       01270000
      ******************************************************************01280000
       0000-MAINLINE-PARAGRAPH.                                         01290000
                                                                        01300000
           PERFORM 1000-INITIALIZE.                                     01310000
                                                                        01320000
           PERFORM 2000-PROCESS-STORES-FILE                             01330000
             UNTIL PS-EOF-CC1-YES.                                      01340000
                                                                        01350000
           PERFORM 9000-CLOSE-FILES.                                    01360000
                                                                        01370000
           STOP RUN.                                                    01380000
                                                                        01390000
      *0000-EXIT.                                                       01400000
       EJECT.                                                           01410000
      ******************************************************************01420000
      *  1000-INITIALIZE.                                               01430000
      *     OPEN AND READS INPUT FILES AND PROCESSES CONTROL CARDS.     01440000
      ******************************************************************01450000
       1000-INITIALIZE.                                                 01460000
                                                                        01470000
           OPEN INPUT CONTROL-CARD-CONV-STORES.                         01480000
                                                                        01490000
           READ CONTROL-CARD-CONV-STORES INTO CC-CONTROL-CARD-1         01500000
              AT END                                                    01510000
                 SET PS-EOF-CC1-YES          TO TRUE.                   01520000
                                                                        01530000
           MOVE PV-CONV-STORE      TO PV-HOLD-STORE-ID                  01540000
           MOVE PV-SLS-VOL-RNK-CDE TO PV-HOLD-SLS-VOL-RNK-CDE.          01550000
                                                                        01560000
           IF PS-EOF-CC1-YES                                            01570000
              MOVE '1000-INITIALIZE'        TO PV-PARAGRAPH-NAME        01580000
              MOVE 'CONTROL CARD IS EMPTY'  TO PV-OPERATION-MSG-1       01590000
              SET AC-CONTROL-CARD-ERROR     TO TRUE                     01600000
              PERFORM 9999-ABEND                                        01610000
           END-IF.                                                      01620000
                                                                        01630000
           DISPLAY 'PROGRAM NAME:       ' PC-CURRENT-PROGRAM-NAME.      01640000
           DISPLAY 'CONTROL CARD 1:     ' CC-CONTROL-CARD-1.            01650000
                                                                        01660000
      *1000-EXIT.                                                       01670000
       EJECT.                                                           01680000
      ******************************************************************01690000
       2000-PROCESS-STORES-FILE.                                        01700000
      ******************************************************************01710000
                                                                        01720000
           PERFORM                                                      01730000
               WITH TEST AFTER                                          01740000
               VARYING PV-ATTEMPT-COUNT                                 01750000
               FROM 1 BY 1                                              01760000
               UNTIL (PV-ATTEMPT-COUNT GREATER PC-MAX-ATTEMPT-COUNT     01770000
                         OR  SQLCODE = 0                                01780000
                         OR  SQLCODE = +100)                            01790000
                                                                        01800000
                EXEC SQL                                                01810000
                   UPDATE XST_LOC_ATTR                                  01820000
                      SET SLS_VOL_RNK_CDE   = :PV-HOLD-SLS-VOL-RNK-CDE  01830000
                    WHERE LOC_NBR           = :PV-HOLD-STORE-ID         01840000
                END-EXEC                                                01850000
                                                                        01860000
                EVALUATE SQLCODE                                        01870000
                   WHEN ZERO                                            01880000
                      CONTINUE                                          01890000
                   WHEN +100                                            01900000
                       DISPLAY 'STORE NOT FOUND: ' PV-CONV-STORE        01910000
                       DISPLAY 'SLS-VOL-RNK-CDE:  ' PV-SLS-VOL-RNK-CDE  01920000
                   WHEN -904                                            01930000
                   WHEN -911                                            01940000
                   WHEN -913                                            01950000
                      CONTINUE                                          01960000
                   WHEN OTHER                                           01970000
                       DISPLAY 'STORE NOT FOUND: ' PV-CONV-STORE        01980000
                       DISPLAY 'SLS-VOL-RNK-CDE:  ' PV-SLS-VOL-RNK-CDE  01990000
                      PERFORM 9100-SQL-ABEND                            02000000
                END-EVALUATE                                            02010000
           END-PERFORM.                                                 02020000
                                                                        02030000
           IF PV-ATTEMPT-COUNT         > PC-MAX-ATTEMPT-COUNT           02040000
              MOVE SQLCODE         TO PV-DB2-OPERATION                  02050000
              PERFORM 9100-SQL-ABEND                                    02060000
           END-IF.                                                      02070000
                                                                        02080000
           READ CONTROL-CARD-CONV-STORES INTO CC-CONTROL-CARD-1         02090000
              AT END                                                    02100000
                 SET PS-EOF-CC1-YES          TO TRUE.                   02110000
                                                                        02120000
           IF NOT PS-EOF-CC1-YES                                        02130000
              DISPLAY 'CONTROL CARD 1:     ' CC-CONTROL-CARD-1          02140000
              MOVE PV-CONV-STORE          TO PV-HOLD-STORE-ID           02150000
              MOVE PV-SLS-VOL-RNK-CDE     TO PV-HOLD-SLS-VOL-RNK-CDE    02160000
           END-IF.                                                      02170000
                                                                        02180000
      *2000-EXIT.                                                       02190000
       EJECT.                                                           02200000
       EJECT.                                                           02210000
      ****************************************************************  02220000
      *CLOSES FILES                                                     02230000
      ****************************************************************  02240000
       9000-CLOSE-FILES.                                                02250000
                                                                        02260000
           CLOSE CONTROL-CARD-CONV-STORES.                              02270000
                                                                        02280000
      *9000-EXIT.                                                       02290000
       EJECT.                                                           02300000
      ******************************************************************02310000
      * 9100-SQL-ABEND                                                  02320000
      *     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.02330000
      ******************************************************************02340000
       9100-SQL-ABEND.                                                  02350000
           DISPLAY '** ABEND     **'.                                   02360000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        02370000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              02380000
                                                                        02390000
      ******************************************************************02400000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     02410000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      02420000
      ******************************************************************02430000
           COPY DPPD004.                                                02440000
                                                                        02450000
           SET AC-DB2-ERROR TO TRUE.                                    02460000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         02470000
                                                                        02480000
      *9100-EXIT.                                                       02490000
       EJECT.                                                           02500000
      ******************************************************************02510000
      * 9999-ABEND                                                      02520000
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  02530000
      ******************************************************************02540000
       9999-ABEND.                                                      02550000
           DISPLAY '** ABEND           **'.                             02560000
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  02570000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        02580000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       02590000
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       02600000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         02610000
                                                                        02620000
      *9999-EXIT.                                                       02630000
       EJECT.                                                           02640000
