       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    IMKUT094.                                         00020000
       AUTHOR.        PAUL KEBER.                                       00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  10-01-2007.                                       00050000
       DATE-COMPILED.                                                   00060000
                                                                                
      ******************************************************************00070000
      *     IMKUT094 - ITEM DOMAIN SKU/STYLE UNLOADS MERGE              00080000
      ******************************************************************00090000
      *     THIS PROGRAM READS IN THE ITEM DOMAIN TUPC/TSKXREF UNLOADS  00092000
      *     MERGE FILE AND THE SORTED STYLE UNLOAD FILE.  IT MERGES THEM00092000
      *     ON VS ID AND SUB CLASS MERCHANDISE ID TO CREATE THE ITEM    00092000
      *     DOMAIN SKU UNLOAD FILE.                                     00092000
      *-----------------------------------------------------------------        
      *    WR/PITS#     DATE        DESCRIPTION                                 
      *    --------   --------     ---------------------------------            
      *    WR32284    07/14/2008   D. THEEL - INCREASED OUTPUT LRECL AND        
      *                            ADDED LOGIC TO WRITE NEW EFF BEG DTE         
      *                            FROM INPUT FILE TO OUTPUT FILE.              
W36546*                                                                         
W36546*    WR36546    08/19/2010   COGNIZANT - PROCESS THE NEW TUPC             
W36546*                            TEST PRODUCT TYPE SEQUENCE NUMBER.           
W36546*                                                                         
W38861*    WR38861    08/19/2010   COGNIZANT - PROCESS THE NEW TUPC             
W38861*                            WARE HOUSE REPLENISHMENT INDICATOR.          
W38861*                                                                         
      ******************************************************************00093000
      *                                                                 00094000
       ENVIRONMENT DIVISION.                                            00094100
      *                                                                 00094200
       CONFIGURATION SECTION.                                           00094300
      *                                                                 00094400
       SOURCE-COMPUTER.        IBM-3090.                                00094500
       OBJECT-COMPUTER.        IBM-3090.                                00094600
      *                                                                 00094700
       INPUT-OUTPUT SECTION.                                            00094800
       FILE-CONTROL.                                                    00094900
                                                                        00095000
           SELECT SKU-UNLOAD-FILE                                       00095100
               ASSIGN TO INP01.                                         00095200
                                                                        00095300
           SELECT STYLE-UNLOAD-FILE                                     00095400
               ASSIGN TO INP02.                                         00095500
                                                                        00095600
           SELECT MERGE-OUTPUT-FILE                                     00095700
               ASSIGN TO OUT01.                                         00095800
                                                                        00095900
      ******************************************************************00096000
       DATA DIVISION.                                                   00097000
      ******************************************************************00098000
                                                                        00099000
       FILE SECTION.                                                    00100000
                                                                        00110000
       FD  SKU-UNLOAD-FILE                                              00120000
           RECORDING MODE IS F                                          00130000
           LABEL RECORDS ARE STANDARD                                   00140000
           BLOCK CONTAINS 0 RECORDS.                                    00150000
                                                                        00160000
       01  SKU-UNLOAD-RECORD           PIC X(160).                      00170000
                                                                        00180000
       FD  STYLE-UNLOAD-FILE                                            00190000
           RECORDING MODE IS F                                          00200000
           LABEL RECORDS ARE STANDARD                                   00210000
           BLOCK CONTAINS 0 RECORDS.                                    00220000
                                                                        00230000
       01  STYLE-UNLOAD-RECORD         PIC X(145).                      00240000
                                                                        00250000
       FD  MERGE-OUTPUT-FILE                                            00260000
           RECORDING MODE IS F                                          00261000
           LABEL RECORDS ARE STANDARD                                   00262000
           BLOCK CONTAINS 0 RECORDS.                                    00263000
                                                                        00264000
W38861*01  MERGE-OUTPUT-RECORD         PIC X(147).                      00265000
W38861 01  MERGE-OUTPUT-RECORD         PIC X(150).                      00265000
                                                                        00266000
      *                                                                 00267000
      ******************************************************************00268000
       WORKING-STORAGE SECTION.                                         00269000
      ******************************************************************00270000
                                                                        00280000
       01  PC-PROGRAM-CONSTANTS.                                        00290000
           05  PC-MAX-SKU              PIC  9(08)     VALUE 99999999.   00300000
                                                                        00310000
       01  PV-PROGRAM-VARIABLES.                                        00320000
           05  PV-UPC-ID-DOUBLE        PIC S9(10)     VALUE +0  COMP.           
           05  PV-SKU-UNLOAD-READ      PIC S9(09)     VALUE +0  COMP.   00330000
           05  PV-STYLE-UNLOAD-READ    PIC S9(09)     VALUE +0  COMP.   00340000
           05  PV-ITEM-MATCHES-WRITTEN PIC S9(09)     VALUE +0  COMP.   00360000
           05  PV-SKU-UNLOAD-DROPPED   PIC S9(09)     VALUE +0  COMP.   00330000
           05  PV-UPC-ID-DISPLAY       PIC Z(10)      VALUE ZERO.       00340000
           05  PV-VS-ID-DISPLAY        PIC Z(09)      VALUE ZERO.       00340000
                                                                        00370000
       01  PS-PROGRAM-SWITCHES.                                         00380000
           05  PS-SKU-UNLOAD-DONE-SW   PIC   X(01)     VALUE 'N'.       00390001
               88  PS-SKU-UNLOAD-DONE                  VALUE 'Y'.       00400000
               88  PS-SKU-UNLOAD-NOT-DONE              VALUE 'N'.       00410000
           05  PS-STYLE-UNLOAD-DONE-SW PIC X(01)       VALUE 'N'.       00420001
               88  PS-STYLE-UNLOAD-DONE                VALUE 'Y'.       00430000
               88  PS-STYLE-UNLOAD-NOT-DONE            VALUE 'N'.       00440000
                                                                        00450000
      ******************************************************************00530000
      *  RECORD LAYOUT FOR THE ITEM DOMAIN TUPC/TSKXREF UNLOADS MERGE   00540000
      *  INPUT FILE                                                     00540000
      ******************************************************************00580000
           COPY IMRD063.                                                00450000
                                                                        00450000
      ******************************************************************00530000
      *  RECORD LAYOUT FOR THE ITEM DOMAIN STYLE UNLOAD INPUT FILE      00540000
      ******************************************************************00580000
           COPY IMRD050.                                                00450000
                                                                        00450000
      ******************************************************************00530000
      *  RECORD LAYOUT FOR THE ITEM DOMAIN SKU UNLOAD FILE              00540000
      ******************************************************************00580000
           COPY IMRD052.                                                00450000
                                                                        00590000
      ******************************************************************00600000
       PROCEDURE DIVISION.                                              00610000
      ******************************************************************00620000
                                                                        00630000
       0100-MAINLINE-IMKUT094.                                          00640000
      *                                                                 00650000
           PERFORM 1000-INITIALIZATION.                                 00651000
                                                                        00652000
           PERFORM 2000-PROCESS-ITEM-RECS                               00653000
               UNTIL PS-SKU-UNLOAD-DONE                                 00654000
                 OR  PS-STYLE-UNLOAD-DONE.                              00655000
                                                                        00656000
           PERFORM 8000-EOJ.                                            00657000
                                                                        00658000
           GOBACK.                                                      00659000
                                                                        00660000
       EJECT                                                            00670000
                                                                        00680000
      ******************************************************************00690000
      *  OPEN FILES AND READ THE FIRST RECORD OF EACH INPUT FILE.       00700000
      ******************************************************************00710000
       1000-INITIALIZATION.                                             00720000
      *                                                                 00730000
           OPEN INPUT  SKU-UNLOAD-FILE                                  00740000
                       STYLE-UNLOAD-FILE.                               00750000
           OPEN OUTPUT MERGE-OUTPUT-FILE.                               00760000
                                                                        00770000
           MOVE +0 TO IM063-VS-ID                                       00770000
                      IM063-SUB-CL-MDSEAR-ID                                    
                      IM050-VS-ID                                       00770000
                      IM050-SUB-CL-MDSEAR-ID.                                   
                                                                        00770000
           PERFORM 4000-READ-SKU-UNLOAD-DATA.                           00780000
           PERFORM 4100-READ-STYLE-UNLOAD-DATA.                         00790000
                                                                        00800000
           DISPLAY '****************************************************00810000
      -            '**'.                                                00820000
                                                                        00830000
       EJECT                                                            00840000
                                                                        00850000
      ******************************************************************00860000
      *  FIND ANY MATCHING ITEMS WITH THE SAME VS_ID, SUB_CL_MDSEAR_ID  00870000
      ******************************************************************00880000
       2000-PROCESS-ITEM-RECS.                                          00890000
      *                                                                 00900000
           IF IM063-VS-ID = IM050-VS-ID                                 00910000
             AND IM063-SUB-CL-MDSEAR-ID = IM050-SUB-CL-MDSEAR-ID        00910000
               PERFORM 5000-CREATE-MERGE-FILE-REC                       00940000
               PERFORM 4000-READ-SKU-UNLOAD-DATA                        00950000
           ELSE                                                         00970000
               IF IM063-VS-ID > IM050-VS-ID                             00971000
                 OR (IM063-VS-ID = IM050-VS-ID                          00971000
                    AND IM063-SUB-CL-MDSEAR-ID > IM050-SUB-CL-MDSEAR-ID)00910000
                   PERFORM 4100-READ-STYLE-UNLOAD-DATA                  01010000
               ELSE                                                     01011000
                   ADD +1 TO PV-SKU-UNLOAD-DROPPED                              
                   MOVE IM063-VS-ID TO PV-VS-ID-DISPLAY                         
                   DISPLAY 'EXTRA SKU UNLOAD RECORD DROPPED FOR VS ID: '        
                           PV-VS-ID-DISPLAY ' SUB CL MDSEAR ID: '               
                           IM063-SUB-CL-MDSEAR-ID                               
                   PERFORM 4000-READ-SKU-UNLOAD-DATA                    01011100
               END-IF                                                   01016200
           END-IF.                                                      01016900
                                                                        01017000
       EJECT                                                            01018000
                                                                        01019000
      ***************************************************************** 01020000
      *  READ AN ITEM DOMAIN TUPC/TSKXREF UNLOADS MERGE FILE RECORD     01030000
      ***************************************************************** 01040000
       4000-READ-SKU-UNLOAD-DATA.                                       01050000
      *                                                                 01060000
           READ SKU-UNLOAD-FILE INTO IM063-ITEM-TUPC-MERGE-REC          01070000
             AT END                                                     01080000
                 SET PS-SKU-UNLOAD-DONE TO TRUE                         01090001
             NOT AT END                                                 01110000
                 ADD +1   TO PV-SKU-UNLOAD-READ                         01120000
           END-READ.                                                    01130000
                                                                        01140000
      ***************************************************************** 01150000
      *  READ AN ITEM DOMAIN STYLE UNLOAD RECORD.                       01160000
      ***************************************************************** 01170000
       4100-READ-STYLE-UNLOAD-DATA.                                     01180000
                                                                        01190000
           READ STYLE-UNLOAD-FILE INTO IM050-ITEM-STYLE-UNLD-REC        01200000
             AT END                                                     01210000
                 SET PS-STYLE-UNLOAD-DONE TO TRUE                       01090001
             NOT AT END                                                 01230000
                 ADD +1   TO PV-STYLE-UNLOAD-READ                       01240000
           END-READ.                                                    01250000
                                                                        01260000
       EJECT                                                            01270000
                                                                        01280000
      ******************************************************************01290000
      *  CREATE AN ITEM DOMAIN TUPC/STYLE UNLOADS MERGE FILE RECORD     01300000
      ******************************************************************01310000
       5000-CREATE-MERGE-FILE-REC.                                      01320000
      *                                                                 01330000
           MOVE IM063-UPC-ID             TO IM052-INTR-UPC-ID.          01340000
           MOVE IM050-RMS-STYL-ID        TO IM052-RMS-STYL-ID.          01340000
           MOVE IM063-ITM-NBR            TO IM052-ITM-NBR.              01340000
           MOVE IM063-SKU                TO IM052-SKU.                  01340000
           MOVE IM063-INTR-UPC-DESC      TO IM052-INTR-UPC-DESC.        01340000
           MOVE IM063-SKU-STAT-CDE       TO IM052-SKU-STAT-CDE.         01340000
           MOVE IM063-INNR-PK-QTY        TO IM052-INNR-PK-QTY.          01340000
           MOVE IM063-OUTR-PK-QTY        TO IM052-OUTR-PK-QTY.          01340000
           MOVE IM063-KOHLS-SIZE-CDE     TO IM052-KOHLS-SIZE-CDE.       01340000
           MOVE IM063-NRF-CLOR-CDE       TO IM052-NRF-CLOR-CDE.         01340000
           MOVE IM063-CSTM-CLOR-DESC     TO IM052-CSTM-CLOR-DESC.       01340000
           MOVE IM063-CUSTFACG-CLOR-DESC TO IM052-CUSTFACG-CLOR-DESC.   01340000
           MOVE IM063-REGN-PRIC-IND      TO IM052-REGN-PRIC-IND.        01340000
           MOVE IM063-RPLNMT-IND         TO IM052-RPLNMT-IND.           01340000
           MOVE IM063-KY-ITM-IND         TO IM052-KY-ITM-IND.           01340000
           MOVE IM063-POG-IND            TO IM052-POG-IND.              01340000
           MOVE IM063-GP-CDE             TO IM052-GP-CDE.               01340000
           MOVE IM063-SEA-SEQ-NBR        TO IM052-SEA-SEQ-NBR.          01340000
           MOVE IM063-UNT-COST-1ST-AMT   TO IM052-UNT-COST-1ST-AMT.     01340000
           MOVE IM063-TX-PRD-CDE-SEQ-NBR TO IM052-TX-PRD-CDE-SEQ-NBR.   01340000
           MOVE IM063-WT-UM-CDE          TO IM052-WT-UM-CDE.            01340000
           MOVE IM063-WT-UM-QTY          TO IM052-WT-UM-QTY.            01340000
           MOVE IM063-SIZE-UM-CDE        TO IM052-SIZE-UM-CDE.          01340000
           MOVE IM063-WD-UM-QTY          TO IM052-WD-UM-QTY.            01340000
           MOVE IM063-HT-UM-QTY          TO IM052-HT-UM-QTY.            01340000
           MOVE IM063-DPTH-UM-QTY        TO IM052-DPTH-UM-QTY.          01340000
           MOVE IM063-TRNSTN-IND         TO IM052-TRNSTN-IND.           01340000
           MOVE IM063-PO-SHRT-CYC-IND    TO IM052-PO-SHRT-CYC-IND.              
           MOVE IM063-EFF-BEG-DTE        TO IM052-EFF-BEG-DTE.                  
W36546     MOVE IM063-TST-PRD-TYP-SEQ-NBR                                       
W36546                                   TO IM052-TST-PRD-TYP-SEQ-NBR.          
W38861     MOVE IM063-WHSE-RPLNT-IND     TO IM052-WHSE-RPLNT-IND.               
                                                                                
           WRITE MERGE-OUTPUT-RECORD                                    01341000
               FROM IM052-ITEM-SKU-UNLD-REC.                            01350000
                                                                        01360000
           ADD 1 TO PV-ITEM-MATCHES-WRITTEN.                            01370000
                                                                        01380000
      ******************************************************************01390000
      *  FINAL PROCESSING FOR THE PROGRAM.                              01400000
      ******************************************************************01410000
       8000-EOJ.                                                        01420000
      *                                                                 01430000
           IF PS-SKU-UNLOAD-NOT-DONE                                            
               PERFORM 8100-EXTRA-SKU-UNLOAD-RECS                           0065
                 UNTIL PS-SKU-UNLOAD-DONE                                   0065
           END-IF.                                                      01016900
                                                                        01430000
           DISPLAY ' '.                                                 01440000
           DISPLAY '*** IMKUT094 RECORD COUNT SUMMARY'.                 01450000
           DISPLAY 'SKU UNLOAD RECORDS READ    = '   PV-SKU-UNLOAD-READ.460000  
           DISPLAY 'STYLE UNLOAD RECORDS READ  = '                      01470000
                 PV-STYLE-UNLOAD-READ.                                          
           DISPLAY 'ITEM MERGE RECORDS WRITTEN = '                      01480000
                 PV-ITEM-MATCHES-WRITTEN.                               01490000
           DISPLAY 'SKU UNLOAD RECORDS DROPPED = '                      01460000
                 PV-SKU-UNLOAD-DROPPED.                                 01490000
           DISPLAY '****************************************************00810000
      -            '**'.                                                00820000
                                                                        01500000
           CLOSE SKU-UNLOAD-FILE                                        01510000
                 STYLE-UNLOAD-FILE                                      01520000
                 MERGE-OUTPUT-FILE.                                     01530000
                                                                        01540000
      ******************************************************************01390000
      *  PROCESS EXTRA SKU UNLOAD RECORDS.                              01400000
      ******************************************************************01410000
       8100-EXTRA-SKU-UNLOAD-RECS.                                      01420000
      *                                                                 01430000
           ADD +1 TO PV-SKU-UNLOAD-DROPPED.                                     
           MOVE IM063-VS-ID TO PV-VS-ID-DISPLAY.                                
           DISPLAY 'EXTRA SKU UNLOAD RECORD DROPPED FOR VS ID: '                
                   PV-VS-ID-DISPLAY ' SUB CL MDSEAR ID: '                       
                   IM063-SUB-CL-MDSEAR-ID.                                      
           PERFORM 4000-READ-SKU-UNLOAD-DATA.                                   
                                                                                
