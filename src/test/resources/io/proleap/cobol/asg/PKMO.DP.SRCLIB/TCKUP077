      ****************************************************************  00010000
       IDENTIFICATION DIVISION.                                         00020000
      ****************************************************************  00030000
                                                                        00040000
       PROGRAM-ID.             TCKUP077.                                00050000
       AUTHOR.                 DAN PAYNTER.                             00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            JUNE 2023.                               00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ****************************************************************  00110000
      *                                                                 00120000
      *   ***  COBRAND TRAILING TRANSACTION GL UPDATE PROGRAM   ***     00130000
      *                                                                 00140000
      *  FOR RELEASE 1 OF COBRAND VISA GIFT CARDS (REPLACEMENT TO       00150000
      *  THE ORIGINAL KOHL'S CHARGE CARD THAT SETTLES WITH FISERV),     00160000
      *  THERE WILL BE TRANSACTIONS GENERATED BY CUSTOMER SALES,        00170000
      *  PAYMENTS, PRICE ADJUSTMENTS, AND RETURNS SHORTLY AFTER AN      00180000
      *  ACCOUNT IS CONVERTED FROM FISERV KOHL'S CHARGE TO CAP ONE      00190000
      *  COBRAND VISA THAT WILL COME WITH THE SMALLER FISERV ACCOUNT    00200000
      *  NUMBER, BUT STILL REQUIRE SETTLEMENT TO FLOW TO CAPITAL ONE.   00210000
      *  ANY SUCH TRANSACTIONS WILL RECORD AS KOHL'S CHARGE TRANSACTIONS00220000
      *  FROM A POC/ECOM PERSPECTIVE FOR HARD TOTALS AND DAILY SALES    00230000
      *  TOTALS (TSASUM).  THESE TRANSACTIONS ARE ONLY IDENTIFIED AS    00240000
      *  CONVERTED ACCOUNTS VIA THE NIGHTLY BATCH SETTLEMENT PROCESSES. 00250000
      *  THIS MEANS THAT A GL ADJUSTMENT MUST BE MADE TO MOVE THE TOTAL 00260000
      *  AMOUNT OF TRAILING TRANSACTION SALE/RETURNS AND PAYMENTS FROM  00270000
      *  THE LEGACY KOHL'S CHARGE GL BUCKET INTO THE CORRESPONDING      00280000
      *  COBRAND GL BUCKETS. THIS PROGRAM WILL INSERT/UPDATE THE        00290000
      *  TSASUM CATEGORY TOTALS RELATED TO TRAILING TRANSACTIONS FOR    00291000
      *  SALES/RETURNS AS WELL AS PAYMENTS AT BOTH THE STORE/REGISTER   00292000
      *  LEVEL AND AT THE STORE TOTALS LEVEL. THE WEEKLY GL SAK PROCESS 00293000
      *  WILL THEN READ THESE BUCKETS AND AUTOMATICALLY MOVE THE COUNTS 00294000
      *  AND AMOUNTS FROM THE LEGACY KOHL'S CHARGE GL CATEGORIES INTO   00295000
      *  THE COBRAND GL CATEGORIES SO THAT THE FINANCE TEAMS DON'T HAVE 00296000
      *  TO MAKE MANUAL ENTRIES. ONCE  TRAILING TRANSACTIONS ARE NO     00296100
      *  LONGER IN EXISTENCE, THEN THIS ENTIRE PROCESS CAN BE REMOVED.  00296200
      *                                                                 00300000
      *   INPUT: INP02 - JOB NAME EXECUTING THIS PROCESS.               00310000
      *                                                                 00330000
      ****************************************************************  00331000
      *                                                                 00332000
      * CHANGE LOG:                                                     00333000
      * DATE:  2023-06-08                                               00334000
      * PRGMR: DAN PAYNTER                                              00335000
      * WHY:   NEW PROGRAM TO INSERT TRAILING TRANSACTION GL CATEGORIES.00336000
      * CHANGE: CHG0289532                                              00336100
      ****************************************************************  00337000
      ****************************************************************  00340000
       ENVIRONMENT DIVISION.                                            00350000
      ****************************************************************  00360000
                                                                        00370000
       CONFIGURATION SECTION.                                           00380000
                                                                        00390000
       SOURCE-COMPUTER.        IBM-3090.                                00400000
                                                                        00410000
       OBJECT-COMPUTER.        IBM-3090.                                00420000
                                                                        00430000
       INPUT-OUTPUT SECTION.                                            00440000
                                                                        00450000
       FILE-CONTROL.                                                    00460000
                                                                        00460100
           SELECT JOB-CARD-FILE ASSIGN TO INP02.                        00461000
                                                                        00470000
      ****************************************************************  00480000
       DATA DIVISION.                                                   00490000
      ****************************************************************  00500000
                                                                        00510000
       FILE SECTION.                                                    00520000
                                                                        00530000
       FD  JOB-CARD-FILE                                                00531000
           RECORDING MODE IS F                                          00532000
           BLOCK CONTAINS 0 RECORDS                                     00533000
           LABEL RECORDS ARE OMITTED.                                   00534000
                                                                        00535000
       01  CONTROL-CARD-REC                 PIC  X(80).                 00536000
                                                                        00537000
      ***************************************************************** 00550000
       WORKING-STORAGE SECTION.                                         00560000
      ***************************************************************** 00570000
       01  FILLER                            PIC X(50)  VALUE           00580000
           ' *** WORKING STORAGE STARTS HERE FOR TCKUP077 *** '.        00590000
                                                                        00600000
      ***************************************************************** 00610000
      *                         SWITCHES                                00620000
      ***************************************************************** 00630000
       01  PROGRAM-SWITCHES.                                            00640000
           05  PS-RESTART-REQUIRED-SW       PIC  X(01) VALUE 'N'.       00650000
               88  PS-RESTART-REQUIRED                 VALUE 'Y'.       00660000
               88  PS-RESTART-NOT-REQUIRED             VALUE 'N'.       00670000
           05  PS-END-OF-JOB-FILE-SW        PIC  X(01) VALUE 'N'.       00671000
               88  PS-END-OF-JOB-FILE                  VALUE 'Y'.       00672000
                                                                        00680000
      ******************************************************************00690000
      *                       ACCUMULATORS                              00700000
      ******************************************************************00710000
       01  PROGRAM-ACCUM.                                               00720000
           05  PA-READ                      PIC  9(11) VALUE ZEROS.     00730000
           05  PA-CKPT                      PIC  9(11) VALUE ZEROS.     00740000
                                                                        00750000
      ******************************************************************00760000
      *                           CONSTANTS                             00770000
      ******************************************************************00780000
       01  PROGRAM-CONSTANTS.                                           00790000
           05  PC-MAX-RETRIES               PIC S9(04) VALUE +3         00800000
                                                       COMP SYNC.       00810000
           05  PC-MAX-DEADLOCKS             PIC S9(04) VALUE +3         00820000
                                                       COMP SYNC.       00830000
                                                                        00840000
           05  PC-CKPT-PLAN-NAME            PIC  X(08) VALUE 'TCKUP077'.00860000
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'TCKUP077'.00870000
                                                                        00880000
           05  PC-TRAN-PRES-FUNC-CODES.                                 00900000
               10  PC-TRAN-PRES-FUNC-OPEN   PIC  X(05) VALUE 'OPEN '.   00910000
               10  PC-TRAN-PRES-FUNC-TRANS  PIC  X(05) VALUE 'TRANS'.   00920000
               10  PC-TRAN-PRES-FUNC-RSTRT  PIC  X(05) VALUE 'RSTRT'.   00930000
               10  PC-TRAN-PRES-FUNC-CLOSE  PIC  X(05) VALUE 'CLOSE'.   00940000
                                                                        00950000
           05  PC-MINUS-ONE                 PIC S9(01) VALUE -1.        00960000
           05  PC-SYSTEM                    PIC  X(08) VALUE 'SYSTEM  '.00970000
                                                                        00970100
       01  AC-ABEND-CODE                     PIC S9(04) VALUE ZERO      00971000
                                                        COMP SYNC.      00972000
           88  AC-CONTROL-CARD-ERROR                    VALUE +4003.    00973000
           88  AC-DB2-ERROR                             VALUE +4013.    00974000
           88  AC-CALENDAR-ROUTINE-ERROR                VALUE +4019.    00975000
                                                                        00980000
      ***************************************************************** 00981000
      * RECORD LAYOUT FOR INPUT JOB CARD  (JOB NAME)                    00982000
      ***************************************************************** 00983000
       01  CC-CONTROL-CARD.                                             00984000
           05  CC-CONTROL-CARD-DISPLAY.                                 00985000
               10  CC-CONTROL-NAME     PIC X(11)    VALUE SPACE.        00986000
                   88  CC-VALID-CONTROL-NAME        VALUE               00987000
                       'JOB-NAME = '.                                   00988000
               10  CC-JOB-NAME         PIC X(6)     VALUE SPACE.        00989000
           05  FILLER                  PIC X(63)    VALUE SPACE.        00989100
                                                                        00989200
      ******************************************************************00990000
      *                          VARIABLES                              01000000
      ******************************************************************01010000
       01  PROGRAM-VARIABLES.                                           01020000
           05  PV-YES-VALUE                 PIC  X(01) VALUE 'Y'.       01030000
           05  PV-SQL-SUB                   PIC  9(03) VALUE ZERO.      01031000
           05  PV-DEADLOCK-COUNTER          PIC  9(03) VALUE ZERO.      01040000
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.     01050000
           05  PV-DB2-OPERATION             PIC  X(30) VALUE SPACE.     01060000
           05  PV-OPERATION-MSG-1          PIC  X(40)    VALUE SPACE.   01061000
           05  PV-OPERATION-MSG-2          PIC  X(40)    VALUE SPACE.   01062000
           05  PV-OPERATION-MSG             PIC  X(35) VALUE SPACE.     01070000
           05  PV-RET-CODE                  PIC  9(04) VALUE ZEROS.     01080000
                                                                        01090000
       01  PV-DB2-VARIABLES.                                            01100000
           05  PV-DB2-PARTN-NBR             PIC S9(04) COMP             01110000
                                                       VALUE ZERO.      01120000
                                                                        01130000
       01  PV-ABEND-DISPLAYS.                                           01140000
           05  PV-ABEND-PARTN-NBR           PIC  9(04) VALUE ZERO.      01150000
           05  PV-ABEND-STR-NBR             PIC  9(04) VALUE ZERO.      01160000
           05  PV-ABEND-RGST-ID             PIC  9(03) VALUE ZERO.      01170000
           05  PV-ABEND-TRN-NBR             PIC  9(04) VALUE ZERO.      01180000
                                                                        01190000
      ******************************************************************01200000
      *               SYSTEM TIME AND DATE VARIABLES                    01210000
      ******************************************************************01220000
       01  SYS-DATE-TIME.                                               01230000
           05  SYS-DATE                     PIC  X(08) VALUE SPACES.    01240000
           05  SYS-TIME                     PIC  X(08) VALUE SPACES.    01250000
                                                                        01260000
       01  ST-BEG-TIME.                                                 01270000
           05  ST-BEG-HR                    PIC  9(02) VALUE ZEROS.     01280000
           05  FILLER                       PIC  X(01) VALUE ':'.       01290000
           05  ST-BEG-MN                    PIC  9(02) VALUE ZEROS.     01300000
                                                                        01310000
       01  ST-END-TIME.                                                 01320000
           05  ST-END-HR                    PIC  9(02) VALUE ZEROS.     01330000
           05  FILLER                       PIC  X(01) VALUE ':'.       01340000
           05  ST-END-MN                    PIC  9(02) VALUE ZEROS.     01350000
                                                                        01360000
       01  SD-EDIT-DATE.                                                01370000
           05  SD-MONTH                     PIC  9(02) VALUE ZEROS.     01380000
           05  FILLER                       PIC  X(01) VALUE '-'.       01390000
           05  SD-DAY                       PIC  9(02) VALUE ZEROS.     01400000
           05  FILLER                       PIC  X(01) VALUE '-'.       01410000
           05  SD-CENT                      PIC  9(02) VALUE ZEROS.     01420000
           05  SD-YEAR                      PIC  9(02) VALUE ZEROS.     01430000
                                                                        01440000
      ******************************************************************01450000
      *                        ERROR HANDLING                           01460000
      ******************************************************************01470000
       01  ABEND-CODE                       PIC S9(04) VALUE ZEROS      01480000
                                                       COMP SYNC.       01490000
           88  ABEND-4002-READ-ERROR                   VALUE +4002.     01500000
           88  ABEND-4013-DB2-ERROR                    VALUE +4013.     01510000
           88  ABEND-4019-CAL-SUB-ERROR                VALUE +4019.     01520000
                                                                        01530000
      ***************************************************************** 01541000
      *  WS FIELDS FOR TSASUM DB2 INSERT/UPDATE                         01542000
      ***************************************************************** 01543000
           COPY TCRD075.                                                01544000
                                                                        01545000
      ******************************************************************01600000
      *  DB2 FORMATTED ERROR MESSAGE                                    01610000
      ******************************************************************01620000
           COPY DPWS004.                                                01630000
                                                                        01640000
      ******************************************************************01650000
      *  SQL COMMUNICATIONS WORK AREA                                   01660000
      ******************************************************************01670000
           COPY SQLCA2.                                                 01680000
                                                                        01690000
      ******************************************************************01700000
      *  COMMUNICATIONS AREA FOR DB2                                    01710000
      ******************************************************************01720000
           EXEC SQL                                                     01730000
               INCLUDE SQLCA                                            01740000
           END-EXEC.                                                    01750000
                                                                        01760000
                                                                        01830000
      ***************************************************************** 01840000
      *  DB2 TABLE TSASUM - SUMMARY TOTALS TABLE FOR CASH OFFICE (GL)   01850000
      ***************************************************************** 01860000
           EXEC SQL                                                     01870000
                INCLUDE TSASUM                                          01880000
           END-EXEC.                                                    01890000
                                                                        01900000
      ******************************************************************01910000
      *  TRANSACTION PREPARATION MODULE COMMUNICATIONS AREA             01920000
      ******************************************************************01930000
           COPY DPWS991.                                                01940000
                                                                        01940100
           COPY DPLS001.                                                01941000
           05  WORK-STORAGE-PASSED.                                     01950000
                                                                        01960000
      ******************************************************************01970000
      *PA - PROGRAM ACCUMULATORS                                        01980000
      ******************************************************************01990000
               10  PROGRAM-ACCUMULATORS.                                02000000
                   15  PA-TSASUM-ROWS-INSERTED   PIC S9(9) VALUE ZERO   02010000
                                                           COMP-3.      02020000
                   15  PA-TSASUM-ROWS-UPDATED    PIC S9(9) VALUE ZERO   02030000
                                                           COMP-3.      02040000
                                                                        02050000
       01  FILLER                           PIC X(4096)  VALUE SPACE.   02060000
                                                                        02070000
      ***************************************************************** 02080000
      * PROCEDURE DIVISION.                                           * 02090000
      ***************************************************************** 02100000
       PROCEDURE DIVISION.                                              02110000
                                                                        02120000
      ***************************************************************** 02130000
      * 0000-MAINLINE-PARAGRAPH                                         02140000
      ***************************************************************** 02150000
       0000-MAINLINE-PARAGRAPH.                                         02160000
                                                                        02170000
           PERFORM A100-INITIALIZE.                                     02180000
                                                                        02190000
           PERFORM B100-PROCESS-UPDATE-RECORDS                          02200000
             UNTIL DP001-PREP-TRANS-EOF.                                02210000
                                                                        02220000
           PERFORM Z990-EOJ-ROUTINE.                                    02230000
                                                                        02240000
           GOBACK.                                                      02250000
                                                                        02260000
                                                                        02270000
      ******************************************************************02280000
      * PREFORMS TASK INITIATION                                        02290000
      ******************************************************************02300000
       A100-INITIALIZE.                                                 02310000
                                                                        02320000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 02330000
                                                                        02340000
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            02350000
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            02360000
                                                                        02370000
           MOVE SYS-DATE (1:2) TO SD-CENT.                              02380000
           MOVE SYS-DATE (3:2) TO SD-YEAR.                              02390000
           MOVE SYS-DATE (5:2) TO SD-MONTH.                             02400000
           MOVE SYS-DATE (7:2) TO SD-DAY.                               02410000
                                                                        02420000
           PERFORM A110-PROCESS-JOB-CARD.                               02421000
                                                                        02422000
           MOVE PC-TRAN-PRES-FUNC-OPEN TO DP001-FUNC-CODE.              02430000
                                                                        02440000
           PERFORM X100-BUILD-COMM-AREA-TRAN-PRES.                      02450000
                                                                        02460000
      ******************************************************************02461000
      * A110-PROCESS-JOB-CARD                                           02462000
      ******************************************************************02463000
       A110-PROCESS-JOB-CARD.                                           02464000
                                                                        02465000
           OPEN INPUT JOB-CARD-FILE.                                    02465100
                                                                        02465200
           READ JOB-CARD-FILE INTO CC-CONTROL-CARD                      02466000
               AT END                                                   02467000
                   SET PS-END-OF-JOB-FILE TO TRUE.                      02468000
           CLOSE JOB-CARD-FILE.                                         02469000
                                                                        02469100
           IF PS-END-OF-JOB-FILE                                        02469200
               MOVE 'A110-PROCESS-JOB-CARD '  TO PV-PARAGRAPH-NAME      02469300
               MOVE 'JOB CARD MISSING       ' TO PV-OPERATION-MSG-1     02469400
               SET AC-CONTROL-CARD-ERROR TO TRUE                        02469500
               PERFORM 9999-ABEND                                       02469600
           ELSE                                                         02469700
               DISPLAY 'CC USED = ',CC-CONTROL-CARD-DISPLAY             02469800
           END-IF.                                                      02469900
                                                                        02471000
      ******************************************************************02480000
      * PROCESS UPDATE RECORDS                                          02490000
      ******************************************************************02500000
       B100-PROCESS-UPDATE-RECORDS.                                     02510000
                                                                        02520000
           SET PS-RESTART-NOT-REQUIRED TO TRUE.                         02530000
                                                                        02540000
           PERFORM C100-UPDATE-FIELDS.                                  02550000
                                                                        02560000
      *----------------------------------------------------------------*02570000
      *  IF A RESTART IS REQUIRED, ISSUE A RESTART CALL RATHER THAN A  *02580000
      *  TRANS CALL.  THIS WILL ALLOW PROCESSING TO RESUME WITH THE    *02590000
      *  FIRST TRANSACTION AFTER THE LAST COMMIT.                      *02600000
      *----------------------------------------------------------------*02610000
           IF  PS-RESTART-REQUIRED                                      02620000
               MOVE PC-TRAN-PRES-FUNC-RSTRT TO DP001-FUNC-CODE          02630000
           ELSE                                                         02640000
               MOVE PC-TRAN-PRES-FUNC-TRANS TO DP001-FUNC-CODE          02650000
           END-IF.                                                      02660000
                                                                        02670000
           PERFORM X100-BUILD-COMM-AREA-TRAN-PRES.                      02680000
                                                                        02690000
           ADD 1 TO PA-READ.                                            02700000
                                                                        02710000
                                                                        02720000
      ******************************************************************02730000
      * MOVE THE UPDATE FIELDS TO THE TEPITM AND TEPEID LAYOUTS.        02740000
      ******************************************************************02750000
       C100-UPDATE-FIELDS.                                              02760000
                                                                        02770000
ZX         MOVE TC075-SASUM-PARTN-NBR        TO SASUM-PARTN-NBR.        02780000
ZX         MOVE TC075-SASUM-STR-NBR          TO SASUM-STR-NBR.          02800000
ZX         MOVE TC075-SASUM-RGST-ID          TO SASUM-RGST-ID.          02820000
ZX         MOVE TC075-SASUM-SUM-CTG-CDE      TO SASUM-SUM-CTG-CDE.      02840000
ZX         MOVE TC075-SASUM-AMOUNT           TO SASUM-CTG-AMT.          02860000
ZX         MOVE TC075-SASUM-COUNT            TO SASUM-CTG-NBR-OF-ITM.   02880000
ZX         MOVE 'D'                          TO SASUM-SUM-TYP-CDE.      02890000
ZX         MOVE 'S'                          TO SASUM-CTG-SRC-CDE.      02900000
                                                                        02980000
ZX         PERFORM 5100-INSERT-TSASUM-ROW.                              02990100
                                                                        03010000
      ***************************************************************** 03860000
      * 5000-UPDATE-TSASUM-ROW                                          03870000
      *     UPDATE THE TSASUM ROW FOR TRAILING TRANSACTION TOTALS       03880000
      ***************************************************************** 03890000
       5000-UPDATE-TSASUM-ROW.                                          03900000
                                                                        03910000
           PERFORM WITH TEST AFTER                                      03920000
             VARYING PV-SQL-SUB                                         03930000
             FROM 1 BY 1                                                03940000
             UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                         03950000
                    OR SQLCODE = -911                                   03960000
                    OR SQLCODE = +100                                   03970000
                    OR SQLCODE = 0)                                     03980000
                                                                        03980100
                EXEC SQL                                                03981000
                    UPDATE TSASUM                                       03982000
                      SET CTG_AMT        = CTG_AMT + :SASUM-CTG-AMT,    03983000
                      CTG_NBR_OF_ITM = CTG_NBR_OF_ITM +                 03984000
                                      :SASUM-CTG-NBR-OF-ITM             03985000
                    WHERE                                               03986000
                      PARTN_NBR      = :SASUM-PARTN-NBR    AND          03987000
                      STR_NBR        = :SASUM-STR-NBR      AND          03988000
                      RGST_ID        = :SASUM-RGST-ID      AND          03989000
                      SUM_TYP_CDE    = :SASUM-SUM-TYP-CDE  AND          03989100
                      SUM_CTG_CDE    = :SASUM-SUM-CTG-CDE               03989200
                END-EXEC                                                03989300
                                                                        03990000
           EVALUATE TRUE                                                04140000
               WHEN SQLCODE = 0                                         04150000
                   ADD +1 TO PA-TSASUM-ROWS-UPDATED                     04160000
                                                                        04170000
               WHEN SQLCODE = -911                                      04180000
                   ADD +1 TO PV-DEADLOCK-COUNTER                        04190000
                   DISPLAY '                   '                        04200000
                   DISPLAY 'DEADLOCK INCOUNTERED -911'                  04210000
                   DISPLAY 'DEADLOCK-COUNTER = ' PV-DEADLOCK-COUNTER    04220000
                   IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS            04230000
                       MOVE '5000-UPDATE-TSASUM-ROW       '             04240000
                                            TO PV-PARAGRAPH-NAME        04250000
                       MOVE 'MAXIMUM DEADLOCKS EXCEEDED    '            04260000
                                            TO PV-DB2-OPERATION         04270000
                       PERFORM Z200-SQL-ABEND                           04280000
                   ELSE                                                 04290000
                       SET PS-RESTART-REQUIRED TO TRUE                  04300000
                   END-IF                                               04310000
                                                                        04320000
               WHEN OTHER                                               04330000
                  DISPLAY '               '                             04340000
                  DISPLAY 'PARTN-NBR    = ' SASUM-PARTN-NBR             04350000
                  DISPLAY 'STR-NBR      = ' SASUM-STR-NBR               04360000
                  DISPLAY 'RGST-ID      = ' SASUM-RGST-ID               04370000
                  DISPLAY 'SUM-TYP-CDE  = ' SASUM-SUM-TYP-CDE           04380000
                  DISPLAY 'SUM-CTG-CDE  = ' SASUM-SUM-CTG-CDE           04390000
                  DISPLAY 'CTG-AMT      = ' SASUM-CTG-AMT               04400000
                  DISPLAY 'CTG-NBR-OF-ITM=' SASUM-CTG-NBR-OF-ITM        04410000
                  DISPLAY 'CTG-SRC-CDE  = ' SASUM-CTG-SRC-CDE           04420000
                  DISPLAY '               '                             04421000
                                                                        04430000
                  MOVE '5000-UPDATE-TSASUM-ROW       '                  04440000
                                                  TO PV-PARAGRAPH-NAME  04450000
                  MOVE 'ERROR ON TSASUM UPDATE'  TO PV-DB2-OPERATION    04460000
                  PERFORM Z200-SQL-ABEND                                04470000
           END-EVALUATE                                                 04480000
           END-PERFORM.                                                 04490000
                                                                        04500000
           IF PV-SQL-SUB > PC-MAX-RETRIES                               04510000
               MOVE '5000-UPDATE-TSASUM-ROW       '                     04520000
                                              TO PV-PARAGRAPH-NAME      04530000
               MOVE 'VERIFY RETRIES EXCEEDED' TO PV-DB2-OPERATION       04540000
               PERFORM Z200-SQL-ABEND                                   04550000
           END-IF.                                                      04560000
                                                                        04586000
      ***************************************************************** 04587000
      * 5100-INSERT-TSASUM-ROW                                          04588000
      *     INSERT THE TSASUM ROW FOR TRAILING TRANSACTION TOTALS       04589000
      ***************************************************************** 04589100
       5100-INSERT-TSASUM-ROW.                                          04589200
                                                                        04589300
           PERFORM WITH TEST AFTER                                      04589400
             VARYING PV-SQL-SUB                                         04589500
             FROM 1 BY 1                                                04589600
             UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                         04589700
                    OR SQLCODE = -911                                   04589800
                    OR SQLCODE = -803                                   04589900
                    OR SQLCODE = +100                                   04590000
                    OR SQLCODE = 0)                                     04590100
                                                                        04590200
ZX             EXEC SQL                                                 04591600
ZX               INSERT INTO TSASUM  (  PARTN_NBR                       04591700
ZX                                   ,STR_NBR                           04591800
ZX                                   ,RGST_ID                           04591900
ZX                                   ,SUM_TYP_CDE                       04592000
ZX                                   ,SUM_CTG_CDE                       04592100
ZX                                   ,CTG_AMT                           04592200
ZX                                   ,CTG_NBR_OF_ITM                    04592300
ZX                                   ,CTG_SRC_CDE  )                    04592400
ZX              VALUES                                                  04592500
ZX               (                                                      04592600
ZX                :SASUM-PARTN-NBR,                                     04592700
ZX                :SASUM-STR-NBR,                                       04592800
ZX                :SASUM-RGST-ID,                                       04592900
ZX                :SASUM-SUM-TYP-CDE,                                   04593000
ZX                :SASUM-SUM-CTG-CDE,                                   04593100
ZX                :SASUM-CTG-AMT,                                       04593200
ZX                :SASUM-CTG-NBR-OF-ITM,                                04593300
ZX                :SASUM-CTG-SRC-CDE                                    04593400
ZX               )                                                      04593500
ZX          END-EXEC                                                    04593600
                                                                        04593700
           EVALUATE TRUE                                                04593800
               WHEN SQLCODE = 0                                         04593900
                   ADD +1 TO PA-TSASUM-ROWS-INSERTED                    04594000
                                                                        04594100
               WHEN SQLCODE = -803                                      04594200
                   PERFORM 5000-UPDATE-TSASUM-ROW                       04594300
                                                                        04594400
               WHEN SQLCODE = -911                                      04594500
                   ADD +1 TO PV-DEADLOCK-COUNTER                        04594600
                   DISPLAY '                   '                        04594700
                   DISPLAY 'DEADLOCK INCOUNTERED -911'                  04594800
                   DISPLAY 'DEADLOCK-COUNTER = ' PV-DEADLOCK-COUNTER    04594900
                   IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS            04595000
                       MOVE '5100-INSERT-TSASUM-ROW       '             04595100
                                            TO PV-PARAGRAPH-NAME        04595200
                       MOVE 'MAXIMUM DEADLOCKS EXCEEDED    '            04595300
                                            TO PV-DB2-OPERATION         04595400
                       PERFORM Z200-SQL-ABEND                           04595500
                   ELSE                                                 04595600
                       SET PS-RESTART-REQUIRED TO TRUE                  04595700
                   END-IF                                               04595800
                                                                        04595900
               WHEN OTHER                                               04596000
                  DISPLAY '               '                             04596100
                  DISPLAY 'PARTN-NBR    = ' SASUM-PARTN-NBR             04596200
                  DISPLAY 'STR-NBR      = ' SASUM-STR-NBR               04596300
                  DISPLAY 'RGST-ID      = ' SASUM-RGST-ID               04596400
                  DISPLAY 'SUM-TYP-CDE  = ' SASUM-SUM-TYP-CDE           04596500
                  DISPLAY 'SUM-CTG-CDE  = ' SASUM-SUM-CTG-CDE           04596600
                  DISPLAY 'CTG-AMT      = ' SASUM-CTG-AMT               04596700
                  DISPLAY 'CTG-NBR-OF-ITM=' SASUM-CTG-NBR-OF-ITM        04596800
                  DISPLAY 'CTG-SRC-CDE  = ' SASUM-CTG-SRC-CDE           04596900
                  DISPLAY '               '                             04597000
                                                                        04597100
                  MOVE '5100-INSERT-TSASUM-ROW       '                  04597200
                                                  TO PV-PARAGRAPH-NAME  04597300
                  MOVE 'ERROR ON TSASUM INSERT'  TO PV-DB2-OPERATION    04597400
                  PERFORM Z200-SQL-ABEND                                04597500
           END-EVALUATE                                                 04597600
           END-PERFORM.                                                 04597700
                                                                        04597800
           IF PV-SQL-SUB > PC-MAX-RETRIES                               04597900
               MOVE '5100-INSERT-TSASUM-ROW       '                     04598000
                                              TO PV-PARAGRAPH-NAME      04598100
               MOVE 'VERIFY RETRIES EXCEEDED' TO PV-DB2-OPERATION       04598200
               PERFORM Z200-SQL-ABEND                                   04598300
           END-IF.                                                      04598400
                                                                        04598500
      ******************************************************************04599000
      * BUILD THE COMMUNICATION AREA FOR A CALL TO THE TRANSACTION      04600000
      * PRESENTATION MODULE                                             04610000
      ******************************************************************04620000
       X100-BUILD-COMM-AREA-TRAN-PRES.                                  04630000
                                                                        04640000
           MOVE LENGTH OF                                               04650000
                WORK-STORAGE-PASSED TO DP001-WORK-STRG-LENGTH.          04660000
           MOVE CC-JOB-NAME         TO DP001-JOB-NAME.                  04670000
           MOVE PC-CKPT-PLAN-NAME   TO DP001-PLAN-NAME.                 04680000
                                                                        04690000
           PERFORM X200-CALL-TRANS-PRES-MODULE.                         04700000
                                                                        04710000
           MOVE DP001-TRANS-RECORD  TO TC075-TRAN-UPDT-RECORD.          04720000
                                                                        04730000
           IF  DP001-CKPT-TAKEN                                         04740000
               ADD 1 TO PA-CKPT                                         04750000
                                                                        04760000
               INITIALIZE PV-DEADLOCK-COUNTER                           04770000
           END-IF.                                                      04780000
                                                                        04790000
                                                                        04800000
      ******************************************************************04810000
      * VERIFY THE RETURN CODE ON THE CALLED MODULE                     04820000
      ******************************************************************04830000
       X200-CALL-TRANS-PRES-MODULE.                                     04840000
                                                                        04850000
           PERFORM Z900-CALL-TRANS-PRES-MODULE.                         04860000
                                                                        04870000
           IF  DP001-GOOD-RET-CODE                                      04880000
           OR  DP001-CKPT-TAKEN                                         04890000
           OR  DP001-RESTART                                            04900000
               CONTINUE                                                 04910000
                                                                        04920000
           ELSE                                                         04930000
               MOVE 'X200-CALL-TRANS-PRES-MODULE' TO PV-PARAGRAPH-NAME  04940000
               MOVE '** BAD CALL TO TRANS PREP MODULE **'               04950000
                                                  TO PV-OPERATION-MSG   04960000
               MOVE DP001-RET-CODE                TO PV-RET-CODE        04970000
                                                                        04980000
               SET ABEND-4019-CAL-SUB-ERROR TO TRUE                     04990000
                                                                        05000000
               PERFORM Z100-ABEND                                       05010000
           END-IF.                                                      05020000
                                                                        05030000
                                                                        05040000
      ******************************************************************05050000
      * NON-DB2 ABEND                                                   05060000
      ******************************************************************05070000
       Z100-ABEND.                                                      05080000
                                                                        05090000
           PERFORM Z999-CONTROLS.                                       05100000
                                                                        05110000
           DISPLAY '                     '.                             05120000
           DISPLAY '** ABEND           **'.                             05130000
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          05140000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        05150000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.         05160000
           DISPLAY '** RETURN CODE     ** = ' PV-RET-CODE.              05170000
                                                                        05180000
           CALL 'ILBOABN0' USING ABEND-CODE.                            05190000
                                                                        05200000
                                                                        05210000
      ******************************************************************05220000
      * ABEND THE PROGRAM IN THE EVENT THAT A SQL ERROR OR WARNING      05230000
      * CODE OCCURS.                                                    05240000
      ******************************************************************05250000
       Z200-SQL-ABEND.                                                  05260000
                                                                        05270000
           PERFORM Z999-CONTROLS.                                       05280000
                                                                        05290000
           DISPLAY '** ABEND     **'.                                   05300000
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                05310000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              05320000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               05330000
                                                                        05340000
      ******************************************************************05350000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    05360000
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         05370000
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     05380000
      * FORMAT.                                                         05390000
      ******************************************************************05400000
           COPY DPPD004.                                                05410000
                                                                        05420000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            05430000
                                                                        05440000
           CALL 'ILBOABN0' USING ABEND-CODE.                            05450000
                                                                        05460000
                                                                        05470000
      ******************************************************************05480000
      * PERFORMS TASK TERMINATION PROCESSING                            05490000
      ******************************************************************05500000
       Z990-EOJ-ROUTINE.                                                05510000
                                                                        05520000
           MOVE PC-TRAN-PRES-FUNC-CLOSE TO DP001-FUNC-CODE.             05530000
                                                                        05540000
           PERFORM X100-BUILD-COMM-AREA-TRAN-PRES.                      05550000
                                                                        05560000
           PERFORM Z999-CONTROLS.                                       05570000
                                                                        05580000
                                                                        05590000
      ******************************************************************05600000
      * DISPLAY CONTROLS FROM THE PROGRAM                               05610000
      ******************************************************************05620000
       Z999-CONTROLS.                                                   05630000
                                                                        05640000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 05650000
                                                                        05660000
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            05670000
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            05680000
                                                                        05690000
           DISPLAY '                    '.                              05700000
           DISPLAY '**********************'.                            05710000
           DISPLAY '   TCKUP077 CONTROLS  '.                            05720000
           DISPLAY '**********************'.                            05730000
           DISPLAY 'RUN DATE  = ' SD-EDIT-DATE.                         05740000
           DISPLAY 'BEG TIME  = ' ST-BEG-TIME.                          05750000
           DISPLAY 'END TIME  = ' ST-END-TIME.                          05760000
           DISPLAY '======================'.                            05770000
           DISPLAY 'RECORDS READ   = ' PA-READ.                         05780000
           DISPLAY 'CHECKPOINTS    = ' PA-CKPT.                         05790000
           DISPLAY '                '.                                  05800000
           DISPLAY 'TSASUM ROWS UPDATED  = ' PA-TSASUM-ROWS-UPDATED.    05810000
           DISPLAY 'TSASUM ROWS INSERTED = ' PA-TSASUM-ROWS-INSERTED.   05820000
           DISPLAY '                '.                                  05830000
                                                                        05840000
      ***************************************************************** 05841000
      * 9999-ABEND                                                      05842000
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  05843000
      ***************************************************************** 05844000
       9999-ABEND.                                                      05845000
           DISPLAY '** ABEND           **'.                             05846000
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          05847000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        05848000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       05849000
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       05849100
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         05849200
                                                                        05850000
      ******************************************************************05860000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    05870000
      * MENTS THAT ARE REQUIRED TO INVOKE THE TRANSACTION PRESENTATION  05880000
      * MODULE (DPKUT901) OF THE DB2 CHECKPOINT RESTART SYSTEM.         05890000
      ******************************************************************05900000
           COPY DPPD001.                                                05910000
                                                                        05920000
