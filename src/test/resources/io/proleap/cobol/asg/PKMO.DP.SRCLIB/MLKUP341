       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    MLKUP341.                                                 
       AUTHOR.        JON FIEDLER.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  08-01-2000.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * MLKUP341 - M/L WEEKLY DEPARTMENT/STORE TABLE (TWKDPTS) RECALC   *       
      ******************************************************************        
      *                                                                         
      *  THIS PROGRAM WILL RECALCULATE THE FOLLOWING FIELDS ON TWKDPTS **       
      *    SHNK_RTL_AMT     - RETAIL SHRINK                            *        
      *    END_INV_RTL_AMT  - RETAIL ENDING INVENTORY                  *        
      *    CUM_MKUP_PCT     - CUM-TO-DATE MARKUP PERCENT               *        
      *    END_INV_COST_AMT - COST ENDING INVENTORY                    *        
      *    SLS_COST_AMT     - COST AS A BASIS OF SALES                 *        
      *    GRO_MRGN_AMT     - GROSS MARGIN DOLLAR AMOUNT               *        
      ******************************************************************        
      *                                                                         
      * THIS PGM WILL USE CHECKPOINT RESTART LOGIC TO DETERMINE IF AND *        
      * WHEN A CHECKPOINT IS TO BE TAKEN. A MINIMUM OF 10 SECONDS WILL *        
      * EXPIRE BEFORE A CHECKPOINT / COMMIT WILL BE PERFORMED.         *        
      * CONDITIONS ....                                                *        
      * NORMAL START - INITIAL CURSOR WILL SELECT ALL DEPT/MCL ROWS    *        
      *   WITH ACTIVITY - CHECK IF TIME FOR A COMMIT ONLY AFTER AN     *        
      *   ENTIRE DEPT/MCL COMBINATION HAS BEEN RECALCED FOR THE ENTIRE *        
      *   DATE RANGE.                                                  *        
      * RESTART - SELECT CHECKPOINT RESTART KEY FROM TCKRSTINF, USE    *        
      *   DEPT/MCL KEY TO DETERMINE WHICH ROWS TO SELECT FOR RECALC -  *        
      *   WHICH IS ALL ROWS >= TO LAST COMMITED DEPT/MCL.              *        
      ******************************************************************        
      *   02/04/01 - RONNA BUTZ - CHANGED PC-MAX-SEASON-WEEKS FROM              
      *              A VALUE OF 27 TO A VALUE OF 28 TO HANDLE PROCESSING        
      *              THE 53RD WEEK OF THE FISCAL YEAR.                          
      ******************************************************************        
      *   06/20/01 - SCOTT JACKSON - CHANGE SEASON-TO-DATE ACCUMULATOR          
      *              LOGIC THAT SUMS SEASON RETAIL INPUTS. CORRECT AN           
      *  062001      ISSUE WITH SUMMING OF SEASONAL TOTALS WHEN THE 1ST         
      *              WEEK OF A MONTH DIDN'T EXIST FOR A DEPT/STORE ROW.         
      *              THIS ONLY IMPACTED ENDINV COST/SALES COST VALUES.          
      *              CHG MADE TO BRING COST VALUES IN LINE WITH TMNDPTS.        
W31475*   06/01/09 - COGNIZANT  - INTRODUCE MULTI-ROW FETCH - CHANGED AS        
W31475*                           PART OF ML BATCH PERF TUNING PROJECT          
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT WKDPTS-EXTR-FILE                                              
               ASSIGN TO INP01.                                                 
      **                                                                        
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      **                                                                        
       FD  WKDPTS-EXTR-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  WKDPTS-EXTR-REC             PIC X(18).                               
      **                                                                        
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-COMMIT-COUNT                 PIC  9(09) VALUE ZEROES.         
           05  PA-INSERT-COUNT                 PIC  9(09) VALUE ZEROES.         
           05  PA-UPDATE-COUNT                 PIC  9(09) VALUE ZEROES.         
           05  PA-EXTRACT-RECS-READ            PIC  9(09) VALUE ZEROES.         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EOF-EXTR-FILE-SW             PIC X(01) VALUE 'N'.             
               88  PS-EOF-EXTR-FILE                      VALUE 'Y'.             
           05  PS-EOF-EXTR-FILE2-SW            PIC X(01) VALUE 'N'.             
               88  PS-EOF-EXTR-FILE2                     VALUE 'Y'.             
           05  PS-CONTROL-TABLE-EMPTY-SW       PIC X     VALUE 'N'.             
               88  PS-EMPTY-CONTROL-TABLE                VALUE 'Y'.             
           05  PS-FIRST-TIME-SW                PIC X     VALUE 'N'.             
               88  PS-FIRST-TIME-THRU                    VALUE 'Y'.             
           05  PS-FIRST-COMMIT-SW              PIC X     VALUE 'N'.             
               88  PS-FIRST-COMMIT                       VALUE 'Y'.             
           05  PS-LOAD-DATE-TABLE-SW           PIC X     VALUE 'N'.             
               88  PS-DATE-TABLE-LOADED                  VALUE 'Y'.             
           05  PS-SEA-LOAD-DATE-TABLE-SW       PIC X     VALUE 'N'.             
               88  PS-SEA-DATE-TABLE-LOADED              VALUE 'Y'.             
           05  PS-SEASON-DATE-TABLE-SW         PIC X     VALUE 'N'.             
               88  PS-SEASON-DATE-TABLE-LOADED           VALUE 'Y'.             
               88  PS-SEASON-DATE-TAB-NOT-LOADED         VALUE 'N'.             
           05  PS-TABLE-PROCESSING-SW          PIC X     VALUE 'N'.             
               88  PS-TABLE-PROCESSED                    VALUE 'Y'.             
           05  PS-CURR-WEEK-SW                 PIC X     VALUE 'N'.             
               88  PS-CURR-WEEK-ROW-NOT-FOUND            VALUE 'Y'.             
           05  PS-PREV-WEEK-ROW-SW             PIC X     VALUE 'N'.             
               88  PS-PREV-WEEK-ROW-NOT-FOUND            VALUE 'Y'.             
           05  PS-CARRY-FORWARD-SW             PIC X     VALUE 'N'.             
               88  PS-PFM-INV-DOLLARS-FOUND              VALUE 'Y'.             
           05  PS-PREV-SEASON-INV-SW           PIC X     VALUE 'N'.             
               88  PS-NO-PREV-SEASONAL-INV               VALUE 'Y'.             
           05  PS-CURRENT-PURCH-COST-SW        PIC X     VALUE 'N'.             
               88  PS-NO-CURRENT-PURCH-COSTS             VALUE 'Y'.             
           05  PS-RESTART-ROW-SW               PIC X     VALUE 'N'.             
               88  PS-RESTART-KEY-ESTABLISHED            VALUE 'Y'.             
           05  PS-COMMIT-TAKEN-SW              PIC X     VALUE 'N'.             
               88  PS-COMMIT-TAKEN                       VALUE 'Y'.             
           05  PS-CURR-SEASON-WKS-SW           PIC X     VALUE 'N'.             
               88  PS-END-OF-CURR-SEASON-WKS             VALUE 'Y'.             
               88  PS-NOT-END-OF-CURR-SEASON-WKS         VALUE 'N'.             
           05  PS-SEASON-WK-SW                 PIC X     VALUE 'N'.             
               88  PS-SEASON-WK-FOUND                    VALUE 'Y'.             
               88  PS-SEASON-WK-NOT-FOUND                VALUE 'N'.             
062001*    SWITCH DETERMINES WHETHER SEASON-TO-DATE ACCUM HAVE BEGUN            
062001     05  PS-SEASON-ACCUMULATOR           PIC X     VALUE 'N'.             
062001         88  PS-SEAS-ACCUM-STARTED                 VALUE 'Y'.             
062001         88  PS-SEAS-NOT-YET-ACCUMULATED           VALUE 'N'.             
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK395'.            
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP341'.          
           05  PC-MAX-SEASON-WEEKS     PIC  S9(4)    VALUE +28  COMP.           
                                                                                
       01  PE-PROGRAM-ERROR-MESSAGES.                                           
           05  PE-MSG-01                       PIC X(30) VALUE                  
                'INSPECT WHERE CLAUSE VARIABLE'.                                
           05  PE-MSG-02                       PIC X(30) VALUE                  
                'DB2 SELECT ATTEMPTED         '.                                
           05  PE-MSG-03                       PIC X(30) VALUE                  
                'ATTEMPTING TO OPEN DB2 CURSOR'.                                
           05  PE-MSG-04                       PIC X(30) VALUE                  
                'ATTEMPT TO FETCH CURSOR ROW  '.                                
           05  PE-MSG-05                       PIC X(30) VALUE                  
                'ATTEMPT TO CLOSE DB2 CURSOR  '.                                
           05  PE-MSG-06                       PIC X(30) VALUE                  
                'ERROR ON UPDATE OF TWKDPTS    '.                               
           05  PE-MSG-07                       PIC X(30) VALUE                  
                'ERROR IN SELECT - TCKRSTCNTL '.                                
           05  PE-MSG-08                       PIC X(30) VALUE                  
                'UPDATE WORK_STRG - TCKRSTINF '.                                
           05  PE-MSG-09                       PIC X(30) VALUE                  
                'PGM ABEND DURING A COMMIT    '.                                
           05  PE-MSG-10                       PIC X(30) VALUE                  
                'UPDATE OF TCKRSTCNTL FAILED  '.                                
           05  PE-MSG-11                       PIC X(30) VALUE                  
                'ERROR IN SELECT - TCKRSTINF  '.                                
           05  PE-MSG-12                       PIC X(30) VALUE                  
                'UNABLE TO OBTAIN TIMESTAMP   '.                                
           05  PE-MSG-13                       PIC X(30) VALUE                  
                'SHRINK NOT FND FOR DEPT/DATE '.                                
           05  PE-MSG-14                       PIC X(30) VALUE                  
                'ERROR ON ''INSERT'' TO TWKDPTS'.                               
           05  PE-MSG-15                       PIC X(30) VALUE                  
                'CURRENT DATE LIMIT IS 27 WKS.'.                                
           05  PE-MSG-17                       PIC X(30) VALUE                  
                'ERROR IN PFW WK NBR - DTATTR.'.                                
           05  PE-MSG-18                       PIC X(30) VALUE                  
                'ERR PREV SEA WK NBR - DTATTR.'.                                
           05  PE-MSG-20                       PIC X(30) VALUE                  
                'ERR IN OPN SEA MTH - TDTATTR.'.                                
           05  PE-MSG-21                       PIC X(30) VALUE                  
                'ERR IN FETCH SEA MTH - DTATTR'.                                
           05  PE-MSG-22                       PIC X(30) VALUE                  
                'ERROR IN NBR OF SEAONS      .'.                                
           05  PE-MSG-23                       PIC X(30) VALUE                  
                'ERROR IN CLOS SEA MTH - DTAT.'.                                
       01  MISC.                                                                
           05  MISC-HOLD-DEPT                  PIC X(03)  VALUE SPACES.         
      *                                                                         
W31475 01  WS-SHNK-RTL-AMT-NULL-IND     PIC S9(04) COMP VALUE 0.                
  |    01  WS-COMMIT-TIME.                                                      
  |        05  WS-COMMIT-HH             PIC  9(02) VALUE 0.                     
  |        05  WS-COMMIT-MIN            PIC  9(02) VALUE 0.                     
  |        05  WS-COMMIT-SEC            PIC  9(02) VALUE 0.                     
  |        05  WS-COMMIT-MSEC           PIC  9(02) VALUE 0.                     
  |    01  WS-HCOMMIT-TIME.                                                     
  |        05  WS-HCOMMIT-HH            PIC  9(02) VALUE 0.                     
  |        05  WS-HCOMMIT-MIN           PIC  9(02) VALUE 0.                     
  |        05  WS-HCOMMIT-SEC           PIC  9(02) VALUE 0.                     
  |        05  WS-HCOMMIT-MSEC          PIC  9(02) VALUE 0.                     
  |    01  WS-CURRENT-TIME.                                                     
  |        05  WS-CURRENT-HH            PIC  9(02) VALUE 0.                     
  |        05  WS-CURRENT-MIN           PIC  9(02) VALUE 0.                     
  |        05  WS-CURRENT-SEC           PIC  9(02) VALUE 0.                     
W31475     05  WS-CURRENT-MSEC          PIC  9(02) VALUE 0.                     
       01  PROGRAM-VARIABLES.                                                   
           05  PV-TWKDPTS-UPDATE-VARIABLES.                                     
      * THESE VARIABLES WILL UPDATE CURRENT TWKDPTS ROW *****************       
               10  PV-SHNK-RTL-AMT             PIC  S9(11)V9(2) COMP-3.         
               10  PV-END-INV-RTL-AMT          PIC  S9(11)V9(2) COMP-3.         
               10  PV-CUM-MKUP-PCT             PIC  S9(4)V9(7)  COMP-3.         
               10  PV-CUM-MKUP-PCT-MAX         PIC  S9(9)V9(7)  COMP-3.         
               10  PV-END-INV-COST-AMT         PIC  S9(11)V9(2) COMP-3.         
               10  PV-SLS-COST-AMT             PIC  S9(11)V9(2) COMP-3.         
               10  PV-GRO-MRGN-AMT             PIC  S9(11)V9(2) COMP-3.         
      ******************************************************************        
           05  PV-SEASON-TO-DATE-CSR-VAR.                                       
               10  PV-WKDPTS-RCPT-RTL-AMT      PIC  S9(11)V9(2) COMP-3.         
               10  PV-WKDPTS-RCPT-NET-COST-AMT PIC  S9(11)V9(2) COMP-3.         
               10  PV-WKDPTS-PADJ-RTL-AMT      PIC  S9(11)V9(2) COMP-3.         
               10  PV-WKDPTS-PADJ-NET-COST-AMT PIC  S9(11)V9(2) COMP-3.         
               10  PV-WKDPTS-MKUP-RTL-AMT      PIC  S9(11)V9(2) COMP-3.         
      *        10  PV-WKDPTS-XFER-NET-AMT      PIC  S9(11)V9(2) COMP-3.         
               10  PV-WKDPTS-XFER-IN-RTL-AMT   PIC  S9(11)V9(2) COMP-3.         
               10  PV-WKDPTS-XFER-OUT-RTL-AMT  PIC  S9(11)V9(2) COMP-3.         
           05  PV-CURR-WEEK-COST-VAR.                                           
               10  PV-CURR-WEEK-RCPT-NET-COST PIC   S9(11)V9(2) COMP-3.         
               10  PV-CURR-WEEK-PADJ-NET-COST PIC   S9(11)V9(2) COMP-3.         
           05  PV-PREV-WEEK-VAR.                                                
               10  PV-PREV-WEEK-END-INV-RTL    PIC  S9(11)V9(2) COMP-3.         
               10  PV-PREV-WEEK-END-INV-COST   PIC  S9(11)V9(2) COMP-3.         
           05  PV-PREV-SEASON-VAR.                                              
               10  PV-PREV-SEASON-PURCH-RETAIL PIC  S9(11)V9(2) COMP-3.         
               10  PV-PREV-SEASON-PURCH-COST   PIC  S9(11)V9(2) COMP-3.         
           05  PV-CUM-TO-DATE-VARIABLES.                                        
               10  PV-CUM-TO-DATE-PURCH-RETAIL PIC  S9(11)V9(2) COMP-3.         
               10  PV-CUM-TO-DATE-PURCH-COST   PIC  S9(11)V9(2) COMP-3.         
               10  PV-CUM-SEASON-MKUP          PIC  S9(11)V9(2) COMP-3.         
      *                                                                         
      *                                                                         
       01  PV-VARIABLE-FIELDS.                                                  
           05  PV-SUB1                         PIC S9(04) VALUE +0 COMP.        
           05  PV-SEA-SUB                      PIC S9(04) VALUE +0 COMP.        
           05  PV-MAX-DATE-SUB                 PIC S9(04) VALUE +0 COMP.        
           05  PV-HOLD-ROW.                                                     
               10  PV-HOLD-DEPT-NBR            PIC  X(03) VALUE SPACES.         
               10  PV-HOLD-STR-NBR             PIC S9(04) COMP VALUE +0.        
           05  PV-HOLD-DATE                    PIC  X(10) VALUE SPACES.         
           05  PV-HOLD-PFW-WK-NBR              PIC  X(02) VALUE SPACES.         
PREVWK     05  PV-HOLD-PFW-END-DTE             PIC  X(10) VALUE SPACES.         
FRSTWK     05  PV-HOLD-MONTH-FIRST-WEEK        PIC  X(10) VALUE SPACES.         
           05  PV-HOLD-PFM-END-DTE             PIC  X(10) VALUE SPACES.         
           05  PV-TEMP-PFW-END-DTE             PIC  X(10) VALUE SPACES.         
           05  PV-HOLD-SEA-BEG-DTE             PIC  X(10) VALUE SPACES.         
           05  PV-TEMP-SEA-BEG-DTE             PIC  X(10) VALUE SPACES.         
           05  PV-HOLD-PREV-SEA-WK-NBR         PIC  X(02) VALUE SPACES.         
           05  PV-TEMP-PREV-SEA-WK-NBR         PIC  X(02) VALUE SPACES.         
           05  PV-HOLD-PREV-SEA-END-DTE        PIC  X(10) VALUE SPACES.         
           05  PV-TEMP-PREV-SEA-END-DTE        PIC  X(10) VALUE SPACES.         
           05  PV-HOLD-OPN-WK-END-DTE          PIC  X(10) VALUE SPACES.         
           05  PV-HOLD-FSCL-WK-NBR             PIC  X(02) VALUE SPACES.         
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES.         
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES.         
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES.         
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES.         
           05  PV-OPERATION-ATTEMPTED          PIC  X(30) VALUE SPACES.         
           05  PV-XFER-NET                     PIC  S9(11)V99   COMP-3.         
           05  PV-SEA-WK-DTE-DB2               PIC  X(10) VALUE SPACES.         
           05  PV-SEA-WK-NBR-DB2               PIC  X(02) VALUE SPACES.         
           05  PV-SEASON-WK-DTE                PIC  X(10) VALUE SPACES.         
           05  PV-SEASON-WK-NBR                PIC  X(02) VALUE SPACES.         
      *                                                                         
       01  SAVE-SEASON-VARIABLES.                                               
           05  SSV-XFER-IN-RTL-AMT     PIC  S9(11)V99  COMP-3 VALUE +0.         
           05  SSV-XFER-OUT-RTL-AMT    PIC  S9(11)V99  COMP-3 VALUE +0.         
           05  SSV-XFER-NET-AMT        PIC  S9(11)V99  COMP-3 VALUE +0.         
           05  SSV-RCPT-RTL-AMT        PIC  S9(11)V99  COMP-3 VALUE +0.         
           05  SSV-RCPT-NET-COST-AMT   PIC  S9(11)V99  COMP-3 VALUE +0.         
           05  SSV-PADJ-RTL-AMT        PIC  S9(11)V99  COMP-3 VALUE +0.         
           05  SSV-PADJ-NET-COST-AMT   PIC  S9(11)V99  COMP-3 VALUE +0.         
           05  SSV-MKUP-RTL-AMT        PIC  S9(11)V99  COMP-3 VALUE +0.         
                                                                                
      *                                                                         
      *** TWKDPTS EXTRACT FILE                                                  
       01  EXTR-RECORD.                                                         
           05  EXTR-DEPT-NBR    PIC X(03)       VALUE SPACES.                   
           05  EXTR-STR-NBR     PIC S9(04) COMP VALUE +0.                       
                                                                                
                                                                                
      * REFORMAT CALENDAR NUMBER TO PIC 9 FIELD FOR USE AS A SUBSCRIPT          
       01  REFORMAT-ALPHA-CALENDAR.                                             
           05  FMT-CALENDAR-MO-NUMERIC          PIC 9(2)  VALUE ZEROES.         
      *                                                                         
      *                                                                         
      * TABLE TO CONTAIN DATE INFORMATION FOR ALL WEEKS RE-CALCULATED           
       01  DATE-TABLE.                                                          
           05  CONTROL-DATES OCCURS 27 TIMES.                                   
               10  DT-OPN-WK-END-DTE            PIC X(10) VALUE SPACES.         
               10  DT-CAL-MN-NBR                PIC X(02) VALUE SPACES.         
               10  DT-PFM-END-DTE               PIC X(10) VALUE SPACES.         
               10  DT-PFW-WK-NBR                PIC X(02) VALUE SPACES.         
               10  DT-PFW-END-DTE               PIC X(10) VALUE SPACES.         
               10  DT-SEA-BEG-DTE               PIC X(10) VALUE SPACES.         
               10  DT-PREV-SEA-END-DTE          PIC X(10) VALUE SPACES.         
               10  DT-PREV-SEA-WK-NBR           PIC X(02) VALUE SPACES.         
               10  DT-FSCL-WK-NBR               PIC X(02) VALUE SPACES.         
               10  DT-FSCL-YR-NBR               PIC X(04) VALUE SPACES.         
               10  DT-SEA-WK-INFO  OCCURS 27 TIMES.                             
                   15  DT-SEA-WK-DTE            PIC X(10) VALUE SPACES.         
                   15  DT-SEA-WK-NBR            PIC X(02) VALUE SPACES.         
                                                                                
                                                                                
      ****************************************************                      
      *  WEEKLY SHRINKAGE TABLE                          *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TWKSHNK                                                  
           END-EXEC.                                                            
      ****************************************************                      
      *  COMMUNICATION AREAS FOR DB2                     *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      ****************************************************                      
      *  M/L WEEKLY DEPARTMENT/STORE TABLE                                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TWKDPTS                                                  
           END-EXEC.                                                            
      ****************************************************                      
      * M/L DATE TABLE AND CURRENT OPEN FISCAL DATE INFO *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
      *                                                                         
      ****************************************************                      
      *CHECKPOINT RESTART CONTROL AND INFORMATION TABLES *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
                                                                                
                                                                                
      **********************************************************                
      * WEEKS TO BE PROCESSED - ALL WEEKS FOR THE CURRENT MONTH                 
      **********************************************************                
           EXEC SQL                                                             
               DECLARE FISCAL_DATE_CSR CURSOR FOR                               
                SELECT  DISTINCT FSCL_WK_END_DTE                                
                  FROM  TDTATTR                                                 
                 WHERE FSCL_WK_END_DTE BETWEEN                                  
                                            :PV-HOLD-MONTH-FIRST-WEEK           
                                        AND :MLCNTL-MXPSTG-FSCL-WK-DTE          
                 ORDER BY FSCL_WK_END_DTE                                       
                 FOR FETCH ONLY                                                 
           END-EXEC.                                                            
      *                                                                         
      *************************************************************             
      * WEEKS TO BE PROCESSED - ALL DATES IN THE CURRENT SEASON                 
      *                                         ------------------              
      **************************************************************            
           EXEC SQL                                                             
               DECLARE SEASON_DATE_CSR CURSOR FOR                               
                SELECT  DISTINCT FSCL_WK_END_DTE                                
                                ,FSCL_WK_NBR                                    
                  FROM  TDTATTR                                                 
                 WHERE FSCL_WK_END_DTE BETWEEN                                  
                                            :PV-TEMP-SEA-BEG-DTE                
                                        AND :PV-HOLD-DATE                       
                 ORDER BY FSCL_WK_END_DTE                                       
                 FOR FETCH ONLY                                                 
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      * CHECKPOINT RESTART VARIABLES                                   *        
      ******************************************************************        
       01  CR-CHECKPOINT-RESTART-AREA.                                          
           05  CR-AREA-LEN                  PIC S9(04) VALUE +65  COMP.         
           05  CR-CHECKPOINT-RESTART-VAR.                                       
               10  CR-PREV-DTL-KEY          PIC  X(21)  VALUE SPACES.           
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                            
                   15  CR-FISCAL-WK-END-DTE PIC  X(10).                         
                   15  FILLER               PIC  X(01).                         
                   15  CR-FISCAL-WK-NBR     PIC  X(02).                         
                   15  FILLER               PIC  X(01).                         
                   15  CR-DEPT-MCL-KEY.                                         
                       20  CR-DEPT-NBR      PIC  X(03).                         
                       20  CR-STR-NBR       PIC S9(04) COMP.                    
               10  FILLER                   PIC  X(01) VALUE SPACES.            
               10  CR-TWKDPTS-UPDATE-COUNT  PIC  9(09) VALUE ZEROES.            
               10  FILLER                   PIC  X(01) VALUE SPACES.            
               10  CR-TWKDPTS-INSERT-COUNT  PIC  9(09) VALUE ZEROES.            
               10  FILLER                   PIC  X(01) VALUE SPACES.            
               10  CR-TWKDPTS-COMMIT-COUNT  PIC  9(09) VALUE ZEROES.            
               10  FILLER                   PIC  X(01) VALUE SPACES.            
               10  CR-TWKDPTS-INPUT-COUNT   PIC  9(09) VALUE ZEROES.            
           EJECT                                                                
      *                                                                         
      ****************************************************                      
      *  ERROR MESSAGE AREA FOR DB2                      *                      
      ****************************************************                      
           COPY DPWS004.                                                        
      *                                                                         
      *                                                                         
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-INPUT                                           
                UNTIL PS-EOF-EXTR-FILE.                                         
                                                                                
           PERFORM 8000-EOJ-ROUTINE.                                            
                                                                                
                                                                                
                                                                                
           STOP RUN.                                                            
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 1000-INITIALIZATION.                                                    
      * LOAD TABLE WITH WEEKS TO BE RECALCULATED - OPN_WK THRU MXPSTG           
      * DETERMINE IF THIS IS A 'RESTART' (TCKRSTINF-CKPT-STAT-CODE = 9)         
      ******************************************************************        
      *                                                                         
       1000-INITIALIZATION.                                                     
      *                                                                         
      *                                                                         
           PERFORM 7000-READ-CONTROL-TABLE.                                     
           PERFORM 7025-PROCESS-CNTL-TBL.                                       
      *                                                                         
                                                                                
           PERFORM 7650-INIT-CHECKPOINT-SELECT.                                 
      *                                                                         
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
                PERFORM 7700-SELECT-RESTART-INFO                                
                MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                           
                                              CR-CHECKPOINT-RESTART-VAR         
                MOVE CR-DEPT-MCL-KEY      TO PV-HOLD-ROW                        
                DISPLAY 'RESTART-LAST DEPT/MCL COMMIT: ',                       
                         ' DEPT:  ' CR-DEPT-NBR                                 
                         ' STORE: ' CR-STR-NBR                                  
                 DISPLAY '          '                                           
                 DISPLAY '--------------------------------------------'         
                 DISPLAY '          '                                           
                MOVE CR-DEPT-NBR   TO PV-HOLD-DEPT-NBR                          
                MOVE CR-STR-NBR    TO PV-HOLD-STR-NBR                           
           ELSE                                                                 
               SET PS-FIRST-COMMIT TO TRUE                                      
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                             
      *                                                                         
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                          
                                 TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'        
               PERFORM 7600-GET-COMMIT-TIMESTAMP                                
           END-IF.                                                              
      *                                                                         
           SET PS-FIRST-TIME-THRU TO TRUE.                                      
           INITIALIZE PA-PROGRAM-ACCUMULATORS.                                  
      *                                                                         
           OPEN INPUT WKDPTS-EXTR-FILE.                                         
                                                                                
      *    **************************************************                   
      *    **  THE INPUT EXTRACT FILE IS THE DRIVING FORCE                      
      *    **  IN THIS PROGRAM.                                                 
      *    **************************************************                   
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
               PERFORM 4000-READ-EXTR-FILE                                      
                   UNTIL                                                        
                   (PA-EXTRACT-RECS-READ = CR-TWKDPTS-INPUT-COUNT)              
                    OR (PS-EOF-EXTR-FILE)                                       
           ELSE                                                                 
               PERFORM 4000-READ-EXTR-FILE                                      
           END-IF.                                                              
                                                                                
           MOVE EXTR-DEPT-NBR TO MISC-HOLD-DEPT.                                
           DISPLAY 'PROCESSING DEPT: ' MISC-HOLD-DEPT.                          
      *                                                                         
                                                                                
           IF  PS-EOF-EXTR-FILE                                                 
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                               
                   DISPLAY 'NO RECORDS TO PROCESSED AFTER RESTART'              
               ELSE                                                             
                   IF TCKRSTCNTL-CKPT-STAT-CODE = '0'                           
                       DISPLAY 'NO ROWS SELECTED FOR RECALCULATION'             
                       DISPLAY 'CHECK IF INPUT FILE IS EMPTY      '             
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *                                                                         
      *                                                                         
      ******************************************************************        
      * MAIN PROCESSING PARAGRAPH-PERFORMED UNTIL END OF CSR PROCESSING         
      * WHEN PV-SUB1 > PV-MAX-SUB - THEN ALL DATES FOR THIS DEPT/MCL/STR        
      *                 HAVE BEEN PROCESSED, CHECK IF COMMIT REQUIRED.          
      ******************************************************************        
       2000-PROCESS-INPUT.                                                      
                                                                                
           MOVE 1 TO PV-SUB1.                                                   
      *                                                                         
062001     SET PS-SEAS-NOT-YET-ACCUMULATED TO TRUE.                             
      *                                                                         
           PERFORM 2020-PROCESS-MCLS                                            
               UNTIL PV-SUB1 > PV-MAX-DATE-SUB.                                 
                                                                                
           PERFORM 7800-COMMIT-PROCESSING.                                      
                                                                                
           PERFORM 4000-READ-EXTR-FILE.                                         
                                                                                
           IF PS-EOF-EXTR-FILE                                                  
               DISPLAY 'END OF INPUT FILE'                                      
           ELSE                                                                 
               IF EXTR-DEPT-NBR NOT = MISC-HOLD-DEPT                            
                   MOVE EXTR-DEPT-NBR TO MISC-HOLD-DEPT                         
                   DISPLAY 'PROCESSING DEPT: ' MISC-HOLD-DEPT                   
               END-IF                                                           
               IF PS-COMMIT-TAKEN                                               
                   MOVE 'N' TO PS-COMMIT-TAKEN-SW                               
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *                                                                         
      *                                                                         
      ******************************************************************        
      * DEPT LEVEL PROCESSING PARAGRAPH                                         
      * PROCESS ALL DATES FOR THIS DEPARTMENT                                   
      ******************************************************************        
       2020-PROCESS-MCLS.                                                       
                                                                                
           INITIALIZE  PROGRAM-VARIABLES.                                       
           MOVE 'N' TO PS-CURR-WEEK-SW                                          
                       PS-PREV-WEEK-ROW-SW                                      
                       PS-RESTART-ROW-SW                                        
                       PS-CARRY-FORWARD-SW.                                     
                                                                                
           MOVE EXTR-DEPT-NBR   TO PV-HOLD-DEPT-NBR.                            
           MOVE EXTR-STR-NBR    TO PV-HOLD-STR-NBR.                             
                                                                                
           PERFORM 2800-FORMAT-DATES.                                           
                                                                                
      *                                                                         
      * DETERMINE IF CURRENT DOLLARS AND PRIOR INVENTORY DOLLARS EXIST          
           PERFORM 7350-SELECT-PREV-WEEK-END-INV.                               
           PERFORM 7300-SELECT-CURRENT-DOLLARS.                                 
      *                                                                         
      ******************************************************************        
      *****                CARRY FORWARD LOGIC                     *****        
      ******************************************************************        
      *    CURRENT $ FOUND FOR THIS WEEK   - RECALC ROW                *        
      * NO CURRENT $ FOUND/NO PREV $ FOUND - BYPASS RECALC THIS WEEK   *        
      * NO CURRENT $ FOUND/   PREV $ FOUND - CARRY FORWARD INVENTORY $ *        
      ******************************************************************        
           IF PS-CURR-WEEK-ROW-NOT-FOUND                                        
               IF PS-PREV-WEEK-ROW-NOT-FOUND                                    
                   CONTINUE                                                     
               ELSE                                                             
                   IF PS-PFM-INV-DOLLARS-FOUND                                  
                       PERFORM 7950-INSERT-CARRY-FWD-ROW                        
                       PERFORM 7300-SELECT-CURRENT-DOLLARS                      
                       PERFORM 2035-RECALC-PROCESSING                           
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               PERFORM 2035-RECALC-PROCESSING                                   
           END-IF.                                                              
                                                                                
           ADD 1 TO PV-SUB1.                                                    
      *                                                                         
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                *        
      *            ********* RECALC PROCESSING **********              *        
      *                                                                *        
      ******************************************************************        
       2035-RECALC-PROCESSING.                                                  
      *                                                                         
           MOVE EXTR-DEPT-NBR     TO PV-HOLD-DEPT-NBR.                          
           MOVE EXTR-STR-NBR      TO PV-HOLD-STR-NBR.                           
      *                                                                         
           PERFORM 7200-OBTAIN-SHRINK-DLRS.                                     
           PERFORM 2150-CALC-ENDINV-RETAIL.                                     
                                                                                
           PERFORM 7400-PREV-SEASON-END-INV.                                    
      *                                                                         
           PERFORM 2200-CALC-PREV-SEASN-END-PURCH.                              
                                                                                
      **   RETRIEVE SEASON-TO-DATE PURCHS FOR FIRST WK OF RECALC MN             
062001**                                                                        
062001     IF  PS-SEAS-NOT-YET-ACCUMULATED                                      
062001**                                                                        
               SET PS-NOT-END-OF-CURR-SEASON-WKS TO TRUE                        
               PERFORM 2250-CALC-SEASON-TO-DATE-PURCH                           
                   VARYING PV-SEA-SUB FROM 1 BY 1                               
                   UNTIL PS-END-OF-CURR-SEASON-WKS                              
062001**                                                                        
062001         PERFORM 2275-UPD-SEASON-TO-DATE-PURCH                            
062001**                                                                        
           ELSE                                                                 
               PERFORM 2275-UPD-SEASON-TO-DATE-PURCH                            
           END-IF.                                                              
                                                                                
      *                                                                         
           PERFORM 2300-CALC-CUMM-TO-DATE-PURCH.                                
           PERFORM 2400-CALC-CUMM-MARKUP-PCT.                                   
                                                                                
      * CURRENT WEEK ENDING INVENTORY COST AMOUNT                               
           PERFORM 2500-CALC-END-INV-COST.                                      
                                                                                
      * SALES COST AMOUNT                                                       
W31475*    PERFORM 7475-CURR-SEASON-COST.                                       
           PERFORM 2600-CALC-SALES-COST-AMT.                                    
                                                                                
      * GROSS MARGIN AMOUNT                                                     
           PERFORM 2700-CALC-GROSS-MARGIN-AMT.                                  
           PERFORM 7500-UPDATE-PROCESSING.                                      
                                                                                
                                                                                
      *                                                                         
      *                                                                         
      **************************************************                        
      *                                                *                        
      * FOLLOWING PARAGRAPHS PERFORM ALL CALCULATIONS  *                        
      *                                                *                        
      ******************************************************************        
      * CALCULATE THE ENDING INVENTORY OF CURRENT WEEK BASED ON THE             
      *       ENDING INVENTORY FROM THE PRIOR WEEK                              
      ******************************************************************        
       2150-CALC-ENDINV-RETAIL.                                                 
      *                                                                         
           COMPUTE PV-END-INV-RTL-AMT = (  PV-PREV-WEEK-END-INV-RTL             
                                         + WKDPTS-RCPT-RTL-AMT                  
                                         + WKDPTS-PADJ-RTL-AMT                  
                                         + WKDPTS-MKUP-RTL-AMT                  
                                         - WKDPTS-SLS-RTL-AMT                   
                                         + WKDPTS-XFER-IN-RTL-AMT               
                                         - WKDPTS-XFER-OUT-RTL-AMT              
                                         - WKDPTS-MKDN-RTL-AMT                  
                                         - WKDPTS-INV-ADJ-RTL-AMT               
                                         - PV-SHNK-RTL-AMT    ).                
      *                                                                         
      ******************************************************************        
      * CALCULATE CUMULATIVE MARKUP PERCENTAGE FOR UPDATE                       
      ******************************************************************        
       2200-CALC-PREV-SEASN-END-PURCH.                                          
      *                                                                         
           IF PS-NO-PREV-SEASONAL-INV                                           
                MOVE 0 TO PV-PREV-SEASON-PURCH-RETAIL                           
                MOVE 0 TO PV-PREV-SEASON-PURCH-COST                             
           ELSE                                                                 
                MOVE WKDPTS-END-INV-RTL-AMT TO                                  
                                         PV-PREV-SEASON-PURCH-RETAIL            
                MOVE WKDPTS-END-INV-COST-AMT TO                                 
                                         PV-PREV-SEASON-PURCH-COST              
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * ACCUMULATE CURRENT SEASON RETAIL/COST AMTS FROM SEASON BEGINNING        
      ******************************************************************        
       2250-CALC-SEASON-TO-DATE-PURCH.                                          
      *                                                                         
           PERFORM 7450-GET-SEASONAL-WEEK.                                      
                                                                                
           IF PS-SEASON-WK-FOUND                                                
               ADD WKDPTS-XFER-IN-RTL-AMT  TO PV-WKDPTS-XFER-IN-RTL-AMT         
               ADD WKDPTS-XFER-OUT-RTL-AMT TO PV-WKDPTS-XFER-OUT-RTL-AMT        
               ADD WKDPTS-RCPT-RTL-AMT     TO PV-WKDPTS-RCPT-RTL-AMT            
               ADD WKDPTS-RCPT-NET-COST-AMT                                     
                                          TO PV-WKDPTS-RCPT-NET-COST-AMT        
               ADD WKDPTS-PADJ-RTL-AMT    TO PV-WKDPTS-PADJ-RTL-AMT             
               ADD WKDPTS-PADJ-NET-COST-AMT                                     
                                          TO PV-WKDPTS-PADJ-NET-COST-AMT        
               ADD WKDPTS-MKUP-RTL-AMT    TO PV-WKDPTS-MKUP-RTL-AMT             
           END-IF.                                                              
      *                                                                         
      *                                                                         
---->  2275-UPD-SEASON-TO-DATE-PURCH.                                           
      *                                                                         
062001**   IF PV-SUB1 = 1                                                       
062001     IF PS-SEAS-NOT-YET-ACCUMULATED                                       
               MOVE PV-WKDPTS-XFER-IN-RTL-AMT   TO SSV-XFER-IN-RTL-AMT          
               MOVE PV-WKDPTS-XFER-OUT-RTL-AMT  TO SSV-XFER-OUT-RTL-AMT         
               MOVE PV-WKDPTS-RCPT-RTL-AMT      TO SSV-RCPT-RTL-AMT             
               MOVE PV-WKDPTS-RCPT-NET-COST-AMT TO SSV-RCPT-NET-COST-AMT        
               MOVE PV-WKDPTS-PADJ-RTL-AMT      TO SSV-PADJ-RTL-AMT             
               MOVE PV-WKDPTS-PADJ-NET-COST-AMT TO SSV-PADJ-NET-COST-AMT        
               MOVE PV-WKDPTS-MKUP-RTL-AMT      TO SSV-MKUP-RTL-AMT             
062001                                                                          
062001         SET PS-SEAS-ACCUM-STARTED TO TRUE                                
062001                                                                          
           ELSE                                                                 
     ******    RETRIEVE CURRENT WEEK PURCHASE VARIABLES                         
               PERFORM 7460-GET-WEEK-PURCH                                      
     ******    ACCUMULATE NEW UPDATED SEASON-TO-DATE SAVE TOTALS                
               ADD  WKDPTS-XFER-IN-RTL-AMT   TO SSV-XFER-IN-RTL-AMT             
               ADD  WKDPTS-XFER-OUT-RTL-AMT  TO SSV-XFER-OUT-RTL-AMT            
               ADD  WKDPTS-RCPT-RTL-AMT      TO SSV-RCPT-RTL-AMT                
               ADD  WKDPTS-RCPT-NET-COST-AMT TO SSV-RCPT-NET-COST-AMT           
               ADD  WKDPTS-PADJ-RTL-AMT      TO SSV-PADJ-RTL-AMT                
               ADD  WKDPTS-PADJ-NET-COST-AMT TO SSV-PADJ-NET-COST-AMT           
               ADD  WKDPTS-MKUP-RTL-AMT      TO SSV-MKUP-RTL-AMT                
           END-IF.                                                              
      *                                                                         
      *                                                                         
      *                                                                         
      *                                                                         
      ******************************************************************        
      * DETERMINE CUMULATIVE RETAIL AND COST                                    
      * ADD PRIOR SEASON END INV AMTS TO CURRENT SEASON-TO-DATE AMTS            
      ******************************************************************        
       2300-CALC-CUMM-TO-DATE-PURCH.                                            
      *                                                                         
---->      COMPUTE SSV-XFER-NET-AMT = (  SSV-XFER-IN-RTL-AMT                    
                                       - SSV-XFER-OUT-RTL-AMT )                 
      *                                                                         
           COMPUTE PV-CUM-TO-DATE-PURCH-COST   =                                
                                       (   PV-PREV-SEASON-PURCH-COST            
                                         + SSV-RCPT-NET-COST-AMT                
                                         + SSV-PADJ-NET-COST-AMT)       .       
                                                                                
           COMPUTE PV-CUM-TO-DATE-PURCH-RETAIL =                                
                                       (   PV-PREV-SEASON-PURCH-RETAIL          
                                         + SSV-XFER-NET-AMT                     
                                         + SSV-RCPT-RTL-AMT                     
                                         + SSV-PADJ-RTL-AMT                     
                                         + SSV-MKUP-RTL-AMT).                   
      *                                                                         
      *                                                                         
      ******************************************************************        
      * FINAL CALCULATIONS OF VALUES TO UPDATE TWKDPTS - CUM_MKUP_PCT           
      ******************************************************************        
       2400-CALC-CUMM-MARKUP-PCT.                                               
      *                                                                         
           COMPUTE PV-CUM-SEASON-MKUP = (  PV-CUM-TO-DATE-PURCH-RETAIL          
                                         - PV-CUM-TO-DATE-PURCH-COST  ).        
      * CUM SEASON RETAIL CANNOT EQUAL ZERO IN DIVISOR ('ON SIZE ERROR')        
           IF PV-CUM-TO-DATE-PURCH-RETAIL = 0                                   
                MOVE 100 TO PV-CUM-MKUP-PCT                                     
           ELSE                                                                 
                COMPUTE PV-CUM-MKUP-PCT ROUNDED =                               
               (PV-CUM-SEASON-MKUP * 100) / PV-CUM-TO-DATE-PURCH-RETAIL         
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * FINAL CALCULATIONS OF VALUES TO UPDATE TWKDPTS - END_INV_COST_AMT       
      ******************************************************************        
       2500-CALC-END-INV-COST.                                                  
      *                                                                         
           COMPUTE PV-END-INV-COST-AMT ROUNDED =                                
                        ( (100 - PV-CUM-MKUP-PCT)  *                            
                               ( PV-END-INV-RTL-AMT) ) / 100.                   
      *                                                                         
      *                                                                         
      ******************************************************************        
      * FINAL CALCULATIONS OF VALUES TO UPDATE TWKDPTS - SALES_COST_AMT *       
      ******************************************************************        
       2600-CALC-SALES-COST-AMT.                                                
      *                                                                         
           IF PS-NO-CURRENT-PURCH-COSTS                                         
               MOVE ZEROES            TO PV-CURR-WEEK-RCPT-NET-COST             
                                         PV-CURR-WEEK-PADJ-NET-COST             
           END-IF.                                                              
      *                                                                         
           COMPUTE PV-SLS-COST-AMT =                                            
                 (  PV-PREV-WEEK-END-INV-COST                                   
                  + PV-CURR-WEEK-RCPT-NET-COST                                  
                  + PV-CURR-WEEK-PADJ-NET-COST                                  
                  - PV-END-INV-COST-AMT         ).                              
      *                                                                         
      ******************************************************************        
      * FINAL CALCULATIONS OF VALUES TO UPDATE TWKDPTS - GRO_MRGN_AMT  *        
      ******************************************************************        
       2700-CALC-GROSS-MARGIN-AMT.                                              
      *                                                                         
           IF WKDPTS-SLS-RTL-AMT = 0                                            
               MOVE 0 TO PV-GRO-MRGN-AMT                                        
           ELSE                                                                 
               COMPUTE PV-GRO-MRGN-AMT =                                        
               ( ( WKDPTS-SLS-RTL-AMT ) - ( PV-SLS-COST-AMT ) )                 
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * MOVE SUBSCRIPTED VARIABLE TO FORMAT USABLE BY DB2                       
      ******************************************************************        
       2800-FORMAT-DATES.                                                       
           MOVE DT-SEA-BEG-DTE(PV-SUB1)    TO PV-HOLD-SEA-BEG-DTE.              
           MOVE DT-OPN-WK-END-DTE(PV-SUB1) TO PV-HOLD-OPN-WK-END-DTE.           
           MOVE DT-PFW-WK-NBR(PV-SUB1)     TO PV-HOLD-PFW-WK-NBR.               
           MOVE DT-PFW-END-DTE(PV-SUB1)    TO PV-HOLD-PFW-END-DTE.              
           MOVE DT-PFM-END-DTE(PV-SUB1)    TO PV-HOLD-PFM-END-DTE.              
           MOVE DT-FSCL-WK-NBR(PV-SUB1)    TO PV-HOLD-FSCL-WK-NBR.              
           MOVE DT-PREV-SEA-END-DTE(PV-SUB1)                                    
                                           TO PV-HOLD-PREV-SEA-END-DTE.         
           MOVE DT-PREV-SEA-WK-NBR(PV-SUB1)                                     
                                           TO PV-HOLD-PREV-SEA-WK-NBR.          
                                                                                
                                                                                
      *                                                                         
      *                                                                         
      ******************************************************************        
      * DISTINCT DEPT / MCL /STR                                                
      ******************************************************************        
      *                                                                         
       4000-READ-EXTR-FILE.                                                     
                                                                                
           READ WKDPTS-EXTR-FILE INTO EXTR-RECORD                               
               AT END                                                           
                  SET PS-EOF-EXTR-FILE TO TRUE.                                 
                                                                                
           IF PS-EOF-EXTR-FILE                                                  
               CONTINUE                                                         
           ELSE                                                                 
               ADD 1 TO PA-EXTRACT-RECS-READ                                    
           END-IF.                                                              
         EJECT                                                                  
      *                                                                         
      *                                                                         
      ******************************************************************        
      * READ THE DB2 CONTROL TABLE CONTAINING OPEN FISCAL DATE INFO             
      ******************************************************************        
       7000-READ-CONTROL-TABLE.                                                 
      *                                                                         
            EXEC SQL                                                            
                SELECT OPN_FSCL_WK_DTE                                          
                      ,OPN_FSCL_WK_NBR                                          
                      ,MXPSTG_FSCL_WK_DTE                                       
                      ,MXPSTG_FSCL_WK_NBR                                       
                 INTO  :MLCNTL-OPN-FSCL-WK-DTE                                  
                      ,:MLCNTL-OPN-FSCL-WK-NBR                                  
                      ,:MLCNTL-MXPSTG-FSCL-WK-DTE                               
                      ,:MLCNTL-MXPSTG-FSCL-WK-NBR                               
                 FROM  TMLCNTL                                                  
            END-EXEC.                                                           
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   MOVE MLCNTL-OPN-FSCL-WK-DTE TO CR-FISCAL-WK-END-DTE          
                   MOVE MLCNTL-OPN-FSCL-WK-NBR TO CR-FISCAL-WK-NBR              
                 DISPLAY '--------------------------------------------'         
                   DISPLAY 'CONTROL DATES (TMLCNTL)'                            
                   DISPLAY 'FISCAL OPEN WEEK DATE: ',                           
                                                 MLCNTL-OPN-FSCL-WK-DTE         
                   DISPLAY '       OPEN WEEK NBR : ',                           
                                                 MLCNTL-OPN-FSCL-WK-NBR         
                   DISPLAY 'MAXPSTG  WEEK END DTE: ',                           
                                              MLCNTL-MXPSTG-FSCL-WK-DTE         
                   DISPLAY '     MAX WEEK NUMBER : ',                           
                                              MLCNTL-MXPSTG-FSCL-WK-NBR         
                 DISPLAY '--------------------------------------------'         
                 DISPLAY '     '                                                
               WHEN OTHER                                                       
                  MOVE '7000-READ-CONTROL-TABLE' TO PV-PARAGRAPH-NAME           
                  MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      ******************************************************************        
      * LOAD ALL DATES TO BE PROCESSED TO INTERNAL TABLE                        
      ******************************************************************        
       7025-PROCESS-CNTL-TBL.                                                   
      *                                                                         
            PERFORM 7035-GET-FIRST-WEEK.                                        
            PERFORM 7050-OPEN-DATE-CURSOR.                                      
            PERFORM 7075-PROCESS-DATE-CURSOR                                    
                UNTIL PS-DATE-TABLE-LOADED.                                     
      *                                                                         
      ******************************************************************        
      * RETRIEVE 1ST WEEK OF OPEN MONTH AS A STARTING POINT TO BEGIN            
      * RECALC OF THE CURRENT DATE RANGE. THIS WILL ENSURE RECALC OF            
      * UPDATES TO ALL CURRENT WEEKS WITHIN THE CURRENT MONTH                   
      ******************************************************************        
      **FOR THE FIRST WEEK OF ANY FISCAL MONTH, THE PREVIOUS MONTH'S            
      * FISCAL END DATE WILL NEED TO BE RETRIEVED AS A STARTING POINT           
      * TO ENSURE CARRY-FORWARD LOGIC IS BROUGHT FORWARD PROPERLY               
      ******************************************************************        
       7035-GET-FIRST-WEEK.                                                     
      *                                                                         
           EXEC SQL                                                             
               SELECT  PFM_END_DTE                                              
                     ,(PFM_END_DTE + 7 DAYS)                                    
                INTO  :PV-HOLD-PFM-END-DTE                                      
                     ,:PV-HOLD-MONTH-FIRST-WEEK                                 
                FROM TDTATTR                                                    
                WHERE KY_DTE  = :MLCNTL-OPN-FSCL-WK-DTE                         
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                  DISPLAY 'RECALC STARTS AT ', PV-HOLD-PFM-END-DTE              
               WHEN +100                                                        
                  DISPLAY 'NO ROWS QUALIFY FOR RECALCULATION'                   
               WHEN OTHER                                                       
                  MOVE '7035-GET-FIRST-WEEK' TO PV-PARAGRAPH-NAME               
                  MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      *****************************************************************         
      * OPEN CURSOR FOR UPDATE AT INITIALIZATION & AFTER EACH COMMIT            
      *****************************************************************         
       7050-OPEN-DATE-CURSOR.                                                   
      *                                                                         
           EXEC SQL                                                             
               OPEN FISCAL_DATE_CSR                                             
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                  MOVE '7050-OPEN-DATE-CSR' TO PV-PARAGRAPH-NAME                
                  MOVE PE-MSG-03 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      *****************************************************************         
      * INCREMENT SUBSCRIPT AND CONTINUE LOADING INTERNAL TABLE                 
      *****************************************************************         
       7075-PROCESS-DATE-CURSOR.                                                
      *                                                                         
           EXEC SQL                                                             
                  FETCH FISCAL_DATE_CSR                                         
                   INTO :PV-HOLD-DATE                                           
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                 DISPLAY '--------------------------------------------'         
                   ADD 1 TO PV-SUB1                                             
                   DISPLAY 'WEEK # ', PV-SUB1                                   
                   DISPLAY 'PROCESS DATE: ', PV-HOLD-DATE                       
                   PERFORM 7100-SELECT-DB2-DATES                                
                   PERFORM 7125-LOAD-DATE-TABLE                                 
               WHEN +100                                                        
                 DISPLAY '     '                                                
                 DISPLAY '--------------------------------------------'         
                 DISPLAY 'NBR OF WEEKS PROCESSED THIS RUN: ', PV-SUB1           
                   IF PV-SUB1 > 1                                               
                       DISPLAY '   ' PV-HOLD-MONTH-FIRST-WEEK ' THRU '          
                                      MLCNTL-MXPSTG-FSCL-WK-DTE                 
                   END-IF                                                       
                 DISPLAY '--------------------------------------------'         
                 DISPLAY '     '                                                
                   MOVE PV-SUB1 TO PV-MAX-DATE-SUB                              
                   MOVE ZEROES  TO PV-SUB1                                      
                   SET PS-DATE-TABLE-LOADED TO TRUE                             
               WHEN OTHER                                                       
                   MOVE '7075-PROCESS-DATE-CSR' TO PV-PARAGRAPH-NAME            
                   MOVE PE-MSG-04 TO PV-OPERATION-ATTEMPTED                     
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
           IF PV-SUB1 > PC-MAX-SEASON-WEEKS                                     
               MOVE '7075-PROCESS-DATE-CSR' TO PV-PARAGRAPH-NAME                
               MOVE PE-MSG-15 TO PV-OPERATION-ATTEMPTED                         
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       7100-SELECT-DB2-DATES.                                                   
      *                                                                         
           EXEC SQL                                                             
               SELECT  CAL_MN_NBR                                               
                      ,PFM_END_DTE                                              
                      ,SEA_BEG_DTE                                              
                      ,PREV_SEA_END_DTE                                         
                      ,(FSCL_WK_END_DTE - 7 DAYS)                               
                      ,FSCL_WK_NBR                                              
                      ,FSCL_YR_NBR                                              
                INTO  :DTATTR-CAL-MN-NBR                                        
                     ,:DTATTR-PFM-END-DTE                                       
                     ,:DTATTR-SEA-BEG-DTE                                       
                     ,:DTATTR-PREV-SEA-END-DTE                                  
                     ,:PV-HOLD-PFW-END-DTE                                      
                     ,:DTATTR-FSCL-WK-NBR                                       
                     ,:DTATTR-FSCL-YR-NBR                                       
                FROM TDTATTR                                                    
                WHERE KY_DTE  = :PV-HOLD-DATE                                   
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN +100                                                        
                  DISPLAY '**************************************'              
                  DISPLAY 'TDTATTR-KEY ' PV-HOLD-DATE                           
                  DISPLAY 'NO ROW FOUND ON TDTATTR TABLE'                       
                  MOVE '7100-SELECT-DB2-DATES' TO PV-PARAGRAPH-NAME             
                  MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
               WHEN OTHER                                                       
                  DISPLAY 'TDTATTR-KEY ' PV-HOLD-DATE                           
                  MOVE '7100-SELECT-DB2-DATES' TO PV-PARAGRAPH-NAME             
                  MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
      *                                                                         
       7112-SELECT-PREV-WK-NBR.                                                 
      *                                                                         
           EXEC SQL                                                             
               SELECT  FSCL_WK_NBR                                              
                INTO  :PV-HOLD-PFW-WK-NBR                                       
                FROM TDTATTR                                                    
                WHERE KY_DTE  = :PV-TEMP-PFW-END-DTE                            
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  DISPLAY '******************************************'          
                  DISPLAY 'PREV FSCL WK DTE :  ' PV-TEMP-PFW-END-DTE            
                  DISPLAY ' '                                                   
                  MOVE '7112-SELECT-PREV-WK-NBR' TO PV-PARAGRAPH-NAME           
                  MOVE PE-MSG-17 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
      *                                                                         
       7114-SELECT-PREV-SEA-WK-NBR.                                             
      *                                                                         
           EXEC SQL                                                             
               SELECT  FSCL_WK_NBR                                              
                INTO  :PV-TEMP-PREV-SEA-WK-NBR                                  
                FROM TDTATTR                                                    
                WHERE KY_DTE  = :PV-TEMP-PREV-SEA-END-DTE                       
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  DISPLAY '********************************************'        
                  DISPLAY 'PREV SEA END DTE:  ' PV-TEMP-PREV-SEA-END-DTE        
                  DISPLAY ' '                                                   
                  MOVE '7114-SELECT-PREV-SEA-WK-NBR'                            
                        TO PV-PARAGRAPH-NAME                                    
                  MOVE PE-MSG-18 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      *****************************************************************         
      * POPULATE INTERNAL TABLE WITH DATES RELATED TO EACH WEEK                 
      * TO BE RE-CALCULATED                                                     
      *****************************************************************         
       7125-LOAD-DATE-TABLE.                                                    
           MOVE PV-HOLD-DATE            TO DT-OPN-WK-END-DTE(PV-SUB1).          
                                                                                
           MOVE DTATTR-CAL-MN-NBR       TO DT-CAL-MN-NBR(PV-SUB1).              
PFW        MOVE PV-HOLD-PFW-END-DTE     TO DT-PFW-END-DTE(PV-SUB1).             
           MOVE DTATTR-PFM-END-DTE      TO DT-PFM-END-DTE(PV-SUB1).             
           MOVE DTATTR-SEA-BEG-DTE      TO DT-SEA-BEG-DTE(PV-SUB1)              
                                           PV-TEMP-SEA-BEG-DTE.                 
           MOVE DTATTR-PREV-SEA-END-DTE TO DT-PREV-SEA-END-DTE(PV-SUB1).        
           MOVE DTATTR-FSCL-WK-NBR      TO DT-FSCL-WK-NBR(PV-SUB1).             
           MOVE DTATTR-FSCL-YR-NBR      TO DT-FSCL-YR-NBR(PV-SUB1).             
                                                                                
           MOVE PV-HOLD-PFW-END-DTE     TO PV-TEMP-PFW-END-DTE.                 
JS0200     PERFORM 7112-SELECT-PREV-WK-NBR.                                     
           MOVE PV-HOLD-PFW-WK-NBR      TO DT-PFW-WK-NBR(PV-SUB1).              
                                                                                
           MOVE DTATTR-PREV-SEA-END-DTE TO PV-TEMP-PREV-SEA-END-DTE.            
JS0200     PERFORM 7114-SELECT-PREV-SEA-WK-NBR.                                 
           MOVE PV-TEMP-PREV-SEA-WK-NBR TO DT-PREV-SEA-WK-NBR(PV-SUB1).         
                                                                                
JS0200     PERFORM 7150-GET-SEASON-DATE-RANGE.                                  
                                                                                
                                                                                
                                                                                
      *    ********************                                                 
      *    **  DISPLAY FIELDS                                                   
      *    ********************                                                 
                                                                                
           DISPLAY '--------------------------------------------'               
           DISPLAY '    OPN WK END DTE    ' DT-OPN-WK-END-DTE(PV-SUB1).         
                                                                                
           DISPLAY '    CAL MN NBR        ' DT-CAL-MN-NBR(PV-SUB1).             
                                                                                
           DISPLAY '    PREV FSCL WK NBR  ' DT-PFW-WK-NBR(PV-SUB1).             
           DISPLAY '    PREV FSCL WK END DTE ' DT-PFW-END-DTE(PV-SUB1).         
           DISPLAY '    PREV FSCL MN END DTE ' DT-PFM-END-DTE(PV-SUB1).         
                                                                                
           DISPLAY '    SEA BEG DTE       ' DT-SEA-BEG-DTE(PV-SUB1).            
                                                                                
           DISPLAY '    PREV SEA END DTE  '                                     
                   DT-PREV-SEA-END-DTE(PV-SUB1).                                
           DISPLAY '    PREV SEA WK NBR   '                                     
                   DT-PREV-SEA-WK-NBR(PV-SUB1).                                 
                                                                                
           DISPLAY '    FSCL WK NBR       ' DT-FSCL-WK-NBR(PV-SUB1).            
           DISPLAY '    FSCL YR NBR       ' DT-FSCL-YR-NBR(PV-SUB1).            
                                                                                
                                                                                
           IF PS-SEASON-DATE-TABLE-LOADED                                       
               SUBTRACT 2 FROM PV-SEA-SUB                                       
           ELSE                                                                 
               SUBTRACT 1 FROM PV-SEA-SUB                                       
           END-IF.                                                              
                                                                                
           DISPLAY ' '                                                          
           DISPLAY '    NBR OF SEA WEEKS PROCESSED FOR THIS DATE: ',            
                            PV-SEA-SUB                                          
           IF PV-SEA-SUB > 0                                                    
               DISPLAY '        ' DT-SEA-BEG-DTE(PV-SUB1)' THRU ' ,             
                             PV-HOLD-DATE                                       
               DISPLAY ' '                                                      
               DISPLAY '      1ST WEEK DTE ' DT-SEA-WK-DTE(PV-SUB1, 1)          
                       '    WK NBR '      DT-SEA-WK-NBR(PV-SUB1, 1)             
               DISPLAY '     LAST WEEK DTE '                                    
                                   DT-SEA-WK-DTE(PV-SUB1, PV-SEA-SUB)           
                       '    WK NBR '                                            
                                   DT-SEA-WK-NBR(PV-SUB1, PV-SEA-SUB)           
           ELSE                                                                 
               DISPLAY ' ************************'                              
               DISPLAY '   NO SEASONAL DATA EXISTS '                            
               MOVE '7125-LOAD-DATE-TABLE' TO PV-PARAGRAPH-NAME                 
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
      *                                                                         
      *                                                                         
      *****************************************************************         
      *                                                                         
      *****************************************************************         
                                                                                
JS0200 7150-GET-SEASON-DATE-RANGE.                                              
                                                                                
           SET PS-SEASON-DATE-TAB-NOT-LOADED TO TRUE.                           
           PERFORM 7152-OPN-SEA-DTE-RANGE-CUR.                                  
           PERFORM 7154-PROCESS-SEA-DATE-CURSOR                                 
               VARYING PV-SEA-SUB FROM 1 BY 1                                   
               UNTIL PV-SEA-SUB > PC-MAX-SEASON-WEEKS                           
                 OR PS-SEASON-DATE-TABLE-LOADED.                                
           PERFORM 7158-CLOSE-SEA-DTE-RANGE-CUR.                                
           IF PS-SEASON-DATE-TAB-NOT-LOADED                                     
               DISPLAY '**********************************************'         
               DISPLAY 'SEASONS PROCESSED > ' PC-MAX-SEASON-WEEKS               
               MOVE '7156-LOAD-SEA-DATE-RANGE-TABLE'                            
                   TO PV-PARAGRAPH-NAME                                         
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
      *                                                                         
      *****************************************************************         
      * OPEN CURSOR FOR UPDATE AT INITIALIZATION & AFTER EACH COMMIT            
      *****************************************************************         
       7152-OPN-SEA-DTE-RANGE-CUR.                                              
      *                                                                         
           EXEC SQL                                                             
               OPEN SEASON_DATE_CSR                                             
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                  MOVE '7152-OPEN-SEASON-CSR' TO PV-PARAGRAPH-NAME              
                  MOVE PE-MSG-20 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      *****************************************************************         
      *                                                                         
      *****************************************************************         
       7154-PROCESS-SEA-DATE-CURSOR.                                            
      *                                                                         
           EXEC SQL                                                             
                  FETCH SEASON_DATE_CSR                                         
                   INTO :PV-SEASON-WK-DTE                                       
                       ,:PV-SEASON-WK-NBR                                       
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   MOVE PV-SEASON-WK-DTE TO                                     
                         DT-SEA-WK-DTE (PV-SUB1, PV-SEA-SUB)                    
                   MOVE PV-SEASON-WK-NBR TO                                     
                         DT-SEA-WK-NBR (PV-SUB1, PV-SEA-SUB)                    
               WHEN +100                                                        
                   SET PS-SEASON-DATE-TABLE-LOADED TO TRUE                      
               WHEN OTHER                                                       
                   MOVE '7154-PROCESS-SEA-DATE-CSR'                             
                        TO PV-PARAGRAPH-NAME                                    
                   MOVE PE-MSG-21 TO PV-OPERATION-ATTEMPTED                     
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      *****************************************************************         
      * CLOSE CURSOR FOR UPDATE AT INITIALIZATION & AFTER EACH COMMIT           
      *****************************************************************         
       7158-CLOSE-SEA-DTE-RANGE-CUR.                                            
      *                                                                         
           EXEC SQL                                                             
               CLOSE SEASON_DATE_CSR                                            
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                  MOVE '7158-CLOSE-SEASON-CSR' TO PV-PARAGRAPH-NAME             
                  MOVE PE-MSG-23 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *****************************************************************         
      * RETRIEVES CURRENT WEEK SHRINK DOLLARS FROM TWKSHNK                      
      *****************************************************************         
       7200-OBTAIN-SHRINK-DLRS.                                                 
                                                                                
            EXEC SQL                                                            
                SELECT SUM(SHNK_RTL_AMT)                                        
                 INTO  :PV-SHNK-RTL-AMT                                         
W31475                 :WS-SHNK-RTL-AMT-NULL-IND                                
                 FROM  TWKSHNK                                                  
               WHERE  FSCL_WK_END_DTE = :PV-HOLD-OPN-WK-END-DTE                 
                 AND  DEPT_NBR        = :PV-HOLD-DEPT-NBR                       
                 AND  STR_NBR         = :PV-HOLD-STR-NBR                        
            END-EXEC.                                                           
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
W31475             IF WS-SHNK-RTL-AMT-NULL-IND  = -1                            
  |                   MOVE ZERO TO PV-SHNK-RTL-AMT                              
  |                END-IF                                                       
  |   *            CONTINUE                                                     
  |   *        WHEN -305                                                        
W31475*            MOVE +0            TO PV-SHNK-RTL-AMT                        
               WHEN OTHER                                                       
                  MOVE '7200-OBTAIN-SHRINK-DLRS' TO PV-PARAGRAPH-NAME           
                  MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      *****************************************************************         
      * SELECT A ROW FROM DB2 TO BE RECALCED & UPDATED (OR INSERT/UPD)          
      *****************************************************************         
       7300-SELECT-CURRENT-DOLLARS.                                             
      *                                                                         
W31475     MOVE 'N'    TO PS-CURRENT-PURCH-COST-SW.                             
      *                                                                         
           EXEC SQL                                                             
                SELECT  SLS_RTL_AMT                                             
                       ,MKDN_RTL_AMT                                            
                       ,MKUP_RTL_AMT                                            
                       ,XFER_IN_RTL_AMT                                         
                       ,XFER_OUT_RTL_AMT                                        
                       ,RCPT_RTL_AMT                                            
                       ,PADJ_RTL_AMT                                            
                       ,INV_ADJ_RTL_AMT                                         
W31475                 ,RCPT_NET_COST_AMT                                       
W31475                 ,PADJ_NET_COST_AMT                                       
                  INTO :WKDPTS-SLS-RTL-AMT                                      
                      ,:WKDPTS-MKDN-RTL-AMT                                     
                      ,:WKDPTS-MKUP-RTL-AMT                                     
                      ,:WKDPTS-XFER-IN-RTL-AMT                                  
                      ,:WKDPTS-XFER-OUT-RTL-AMT                                 
                      ,:WKDPTS-RCPT-RTL-AMT                                     
                      ,:WKDPTS-PADJ-RTL-AMT                                     
                      ,:WKDPTS-INV-ADJ-RTL-AMT                                  
W31475                ,:PV-CURR-WEEK-RCPT-NET-COST                              
W31475                ,:PV-CURR-WEEK-PADJ-NET-COST                              
                  FROM TWKDPTS                                                  
                 WHERE FSCL_WK_NBR     = :PV-HOLD-FSCL-WK-NBR                   
                   AND FSCL_WK_END_DTE = :PV-HOLD-OPN-WK-END-DTE                
                   AND DEPT_NBR        = :PV-HOLD-DEPT-NBR                      
                   AND STR_NBR         = :PV-HOLD-STR-NBR                       
           END-EXEC                                                             
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                 SET PS-CURR-WEEK-ROW-NOT-FOUND    TO TRUE                      
W31475           SET PS-NO-CURRENT-PURCH-COSTS     TO TRUE                      
               WHEN OTHER                                                       
                   MOVE '7300-SELECT-CURRENT'  TO PV-PARAGRAPH-NAME             
                   MOVE PE-MSG-04              TO PV-OPERATION-ATTEMPTED        
                   PERFORM 9999-SQL-ABEND                                       
             END-EVALUATE.                                                      
      *                                                                         
      *                                                                         
      ******************************************************************        
      * RETRIEVE LAST WEEK'S ENDING INVENTORY RETAIL/COST                       
      *     THIS IS ALSO THE BEGINNING INVENTORY FOR THIS WEEK                  
      ******************************************************************        
       7350-SELECT-PREV-WEEK-END-INV.                                           
      *                                                                         
           EXEC SQL                                                             
                SELECT  END_INV_RTL_AMT                                         
                      , END_INV_COST_AMT                                        
                  INTO  :PV-PREV-WEEK-END-INV-RTL                               
                       ,:PV-PREV-WEEK-END-INV-COST                              
                  FROM  TWKDPTS                                                 
JS0200           WHERE (FSCL_WK_NBR     = :PV-HOLD-PFW-WK-NBR)                  
JS0200             AND (FSCL_WK_END_DTE = :PV-HOLD-PFW-END-DTE)                 
                   AND (DEPT_NBR        = :PV-HOLD-DEPT-NBR)                    
                   AND (STR_NBR         = :PV-HOLD-STR-NBR)                     
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   IF ((PV-PREV-WEEK-END-INV-RTL   NOT = 0) OR                  
                       (PV-PREV-WEEK-END-INV-COST NOT = 0))                     
                       SET PS-PFM-INV-DOLLARS-FOUND TO TRUE                     
                   END-IF                                                       
               WHEN +100                                                        
                   SET PS-PREV-WEEK-ROW-NOT-FOUND TO TRUE                       
                   MOVE ZEROES TO PV-PREV-WEEK-END-INV-RTL                      
                                  PV-PREV-WEEK-END-INV-COST                     
               WHEN OTHER                                                       
                   MOVE PE-MSG-02            TO PV-OPERATION-ATTEMPTED          
                   MOVE  '7350-SELECT-PREV-WEEK-END-INV'                        
                                             TO PV-PARAGRAPH-NAME               
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7400-PREV-SEASON-END-INV SELECTS THE ENDING INVENTORY COST &   *        
      *      RETAIL FROM THE LAST DAY OF THE PREVIOUS FISCAL SEASON    *        
      ******************************************************************        
       7400-PREV-SEASON-END-INV.                                                
      *                                                                         
           MOVE 'N'    TO PS-PREV-SEASON-INV-SW.                                
      *                                                                         
           EXEC SQL                                                             
                SELECT  END_INV_RTL_AMT                                         
                       ,END_INV_COST_AMT                                        
                  INTO  :WKDPTS-END-INV-RTL-AMT                                 
                       ,:WKDPTS-END-INV-COST-AMT                                
                  FROM  TWKDPTS                                                 
                 WHERE (FSCL_WK_NBR =     :PV-HOLD-PREV-SEA-WK-NBR)             
                   AND (FSCL_WK_END_DTE = :PV-HOLD-PREV-SEA-END-DTE)            
                   AND (DEPT_NBR =        :PV-HOLD-DEPT-NBR)                    
                   AND (STR_NBR    =      :PV-HOLD-STR-NBR)                     
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN +100                                                        
                   SET PS-NO-PREV-SEASONAL-INV       TO TRUE                    
               WHEN OTHER                                                       
                   MOVE PE-MSG-02            TO PV-OPERATION-ATTEMPTED          
                   MOVE  '7400-PREV-SEASON-END-INV'                             
                                             TO PV-PARAGRAPH-NAME               
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      ******************************************************************        
      * 7450-GET-SEASONAL-WEEK WILL RETRIEVE ALL ROWS WITH AN ENDING            
      * INVENTORY AMOUNT FOR EACH WEEK END DATE FOR THE CURRENT SEASON.         
      ******************************************************************        
       7450-GET-SEASONAL-WEEK.                                                  
                                                                                
           IF DT-SEA-WK-DTE (PV-SUB1, PV-SEA-SUB) NOT EQUAL SPACES              
               MOVE DT-SEA-WK-DTE (PV-SUB1, PV-SEA-SUB)                         
                     TO PV-SEA-WK-DTE-DB2                                       
               MOVE DT-SEA-WK-NBR (PV-SUB1, PV-SEA-SUB)                         
                     TO PV-SEA-WK-NBR-DB2                                       
           ELSE                                                                 
               SET PS-SEASON-WK-NOT-FOUND TO TRUE                               
               SET PS-END-OF-CURR-SEASON-WKS TO TRUE.                           
                                                                                
           IF PS-NOT-END-OF-CURR-SEASON-WKS                                     
               EXEC SQL                                                         
               SELECT  XFER_IN_RTL_AMT                                          
                       ,XFER_OUT_RTL_AMT                                        
                       ,RCPT_RTL_AMT                                            
                       ,RCPT_NET_COST_AMT                                       
                       ,PADJ_RTL_AMT                                            
                       ,PADJ_NET_COST_AMT                                       
                       ,MKUP_RTL_AMT                                            
                 INTO  :WKDPTS-XFER-IN-RTL-AMT                                  
                      ,:WKDPTS-XFER-OUT-RTL-AMT                                 
                      ,:WKDPTS-RCPT-RTL-AMT                                     
                      ,:WKDPTS-RCPT-NET-COST-AMT                                
                      ,:WKDPTS-PADJ-RTL-AMT                                     
                      ,:WKDPTS-PADJ-NET-COST-AMT                                
                      ,:WKDPTS-MKUP-RTL-AMT                                     
                 FROM  TWKDPTS                                                  
                 WHERE (FSCL_WK_NBR =     :PV-SEA-WK-NBR-DB2)                   
                   AND (FSCL_WK_END_DTE = :PV-SEA-WK-DTE-DB2)                   
                   AND (DEPT_NBR   =      :PV-HOLD-DEPT-NBR)                    
                   AND (STR_NBR    =      :PV-HOLD-STR-NBR)                     
               END-EXEC                                                         
                                                                                
               EVALUATE SQLCODE                                                 
                    WHEN 0                                                      
                        SET PS-SEASON-WK-FOUND TO TRUE                          
                    WHEN +100                                                   
                        SET PS-SEASON-WK-NOT-FOUND TO TRUE                      
                    WHEN OTHER                                                  
                        MOVE PE-MSG-04      TO PV-OPERATION-ATTEMPTED           
                        MOVE '7450-GET-SEASONAL-WEEK'                           
                                                   TO PV-PARAGRAPH-NAME         
                        PERFORM 9999-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-IF.                                                              
                                                                                
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       7460-GET-WEEK-PURCH.                                                     
                                                                                
               EXEC SQL                                                         
               SELECT   XFER_IN_RTL_AMT                                         
                       ,XFER_OUT_RTL_AMT                                        
                       ,RCPT_RTL_AMT                                            
                       ,RCPT_NET_COST_AMT                                       
                       ,PADJ_RTL_AMT                                            
                       ,PADJ_NET_COST_AMT                                       
                       ,MKUP_RTL_AMT                                            
                 INTO  :WKDPTS-XFER-IN-RTL-AMT                                  
                      ,:WKDPTS-XFER-OUT-RTL-AMT                                 
                      ,:WKDPTS-RCPT-RTL-AMT                                     
                      ,:WKDPTS-RCPT-NET-COST-AMT                                
                      ,:WKDPTS-PADJ-RTL-AMT                                     
                      ,:WKDPTS-PADJ-NET-COST-AMT                                
                      ,:WKDPTS-MKUP-RTL-AMT                                     
                 FROM  TWKDPTS                                                  
                 WHERE (FSCL_WK_NBR =     :PV-HOLD-FSCL-WK-NBR)                 
                   AND (FSCL_WK_END_DTE = :PV-HOLD-OPN-WK-END-DTE)              
                   AND (DEPT_NBR   =      :PV-HOLD-DEPT-NBR)                    
                   AND (STR_NBR    =      :PV-HOLD-STR-NBR)                     
               END-EXEC                                                         
                                                                                
               EVALUATE SQLCODE                                                 
                    WHEN 0                                                      
                        CONTINUE                                                
                    WHEN +100                                                   
    ***                 SET PS-NO-WEEKLY-DATA-FOUND TO TRUE                     
                        MOVE 0 TO WKDPTS-XFER-IN-RTL-AMT                        
                                  WKDPTS-XFER-OUT-RTL-AMT                       
                                  WKDPTS-RCPT-RTL-AMT                           
                                  WKDPTS-RCPT-NET-COST-AMT                      
                                  WKDPTS-PADJ-RTL-AMT                           
                                  WKDPTS-PADJ-NET-COST-AMT                      
                                  WKDPTS-MKUP-RTL-AMT                           
                    WHEN OTHER                                                  
                        MOVE PE-MSG-04      TO PV-OPERATION-ATTEMPTED           
                        MOVE '7460-GET-WEEK-PURCH'                              
                                                   TO PV-PARAGRAPH-NAME         
                        PERFORM 9999-SQL-ABEND                                  
               END-EVALUATE.                                                    
                                                                                
      *                                                                         
      ******************************************************************        
      * 7475-CURR-SEASON-COST SELECTS THE ENDING INVENTORY COST &      *        
      *      RETAIL FROM THE LAST DAY OF THE PREVIOUS FISCAL SEASON    *        
      ******************************************************************        
W31475*7475-CURR-SEASON-COST.                                                   
  |   *                                                                         
  |   *    MOVE 'N'    TO PS-CURRENT-PURCH-COST-SW.                             
  |   *                                                                         
  |   *    EXEC SQL                                                             
  |   *         SELECT  RCPT_NET_COST_AMT                                       
  |   *                ,PADJ_NET_COST_AMT                                       
  |   *           INTO  :PV-CURR-WEEK-RCPT-NET-COST                             
  |   *                ,:PV-CURR-WEEK-PADJ-NET-COST                             
  |   *           FROM  TWKDPTS                                                 
  |   *          WHERE (FSCL_WK_NBR     = :PV-HOLD-FSCL-WK-NBR)                 
  |   *            AND (FSCL_WK_END_DTE = :PV-HOLD-OPN-WK-END-DTE)              
  |   *            AND (DEPT_NBR =        :PV-HOLD-DEPT-NBR)                    
  |   *            AND (STR_NBR    =      :PV-HOLD-STR-NBR)                     
  |   *    END-EXEC.                                                            
  |   *                                                                         
  |   *    EVALUATE SQLCODE                                                     
  |   *        WHEN 0                                                           
  |   *            CONTINUE                                                     
  |   *        WHEN +100                                                        
  |   *            SET PS-NO-CURRENT-PURCH-COSTS     TO TRUE                    
  |   *        WHEN OTHER                                                       
  |   *            MOVE PE-MSG-02            TO PV-OPERATION-ATTEMPTED          
  |   *            MOVE  '7475-CURRENT-SEASON-COST'                             
  |   *                                      TO PV-PARAGRAPH-NAME               
  |   *            PERFORM 9999-SQL-ABEND                                       
W31475*    END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      ******************************************************************        
      * UPDATE COLUMNS WITH VALUES JUST CALCULATED FOR THIS DEPARTMENT          
      ******************************************************************        
       7500-UPDATE-PROCESSING.                                                  
      *                                                                         
           EXEC SQL                                                             
               UPDATE TWKDPTS                                                   
                  SET  SHNK_RTL_AMT     = :PV-SHNK-RTL-AMT                      
                      ,END_INV_RTL_AMT  = :PV-END-INV-RTL-AMT                   
                      ,CUM_MKUP_PCT     = :PV-CUM-MKUP-PCT                      
                      ,END_INV_COST_AMT = :PV-END-INV-COST-AMT                  
                      ,SLS_COST_AMT     = :PV-SLS-COST-AMT                      
                      ,GRO_MRGN_AMT     = :PV-GRO-MRGN-AMT                      
                WHERE FSCL_WK_NBR       = :PV-HOLD-FSCL-WK-NBR                  
                  AND FSCL_WK_END_DTE   = :PV-HOLD-OPN-WK-END-DTE               
                  AND DEPT_NBR          = :PV-HOLD-DEPT-NBR                     
                  AND STR_NBR           = :PV-HOLD-STR-NBR                      
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    ADD 1                TO PA-UPDATE-COUNT                     
               WHEN OTHER                                                       
                    DISPLAY '*************************************'             
                    DISPLAY 'ROW IN PROCESS : ', PV-HOLD-ROW                    
                    DISPLAY 'WEEK-END-DATE  : ',                                
                                              PV-HOLD-OPN-WK-END-DTE            
                    DISPLAY 'FISCAL WEEK NBR: ',                                
                                                 PV-HOLD-FSCL-WK-NBR            
                    MOVE PE-MSG-06        TO PV-OPERATION-ATTEMPTED             
                    MOVE '7500-UPDATE-PROCESSING'                               
                                          TO PV-PARAGRAPH-NAME                  
                    PERFORM 9999-SQL-ABEND                                      
           END-EVALUATE.                                                        
      ******************************************************************        
      * 7600-GET-COMMIT-TIMESTAMP.                                     *        
      *   GET THE TIMESTAMP FROM THE CHECKPOINT TABLE FOR NEXT COMMIT  *        
      ******************************************************************        
       7600-GET-COMMIT-TIMESTAMP.                                               
W31475*                                                                         
  |   *    EXEC SQL                                                             
  |   *      SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)               
  |   *        INTO :PV-COMMIT-TIMESTAMP                                        
  |   *        FROM TCKRSTCNTL                                                  
  |   *       WHERE JOB_NAME  = :PC-JOB-NAME                                    
  |   *         AND PLAN_NAME = :PC-PROGRAM-NAME                                
  |   *    END-EXEC.                                                            
  |   *                                                                         
  |   *    EVALUATE TRUE                                                        
  |   *        WHEN SQLCODE = 0                                                 
  |   *            CONTINUE                                                     
  |   *        WHEN SQLCODE NOT EQUAL ZERO                                      
  |   *            MOVE '7600-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME        
  |   *            MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED         
  |   *            PERFORM 9999-SQL-ABEND                                       
  |   *    END-EVALUATE.                                                        
  |        ACCEPT WS-COMMIT-TIME FROM TIME.                                     
  |        MOVE WS-COMMIT-TIME   TO WS-HCOMMIT-TIME.                            
  |        ADD TCKRSTCNTL-CKPT-FRQNCY-QTY                                       
W31475                           TO WS-HCOMMIT-SEC.                             
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7650-INIT-CHECKPOINT-SELECT                                    *        
      * CHECK STATUS CODE TO SEE IF THIS IS A RESTART CONDITION        *        
      ******************************************************************        
       7650-INIT-CHECKPOINT-SELECT.                                             
      *                                                                         
           EXEC SQL                                                             
             SELECT  CKPT_STAT_CODE                                             
                    ,CKPT_FRQNCY_QTY                                            
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                                 
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                                
               FROM  TCKRSTCNTL                                                 
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '7650-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME          
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7700-SELECT-RESTART-INFO                                       *        
      * OBTAIN INFORMATION REGARDING WHERE TO RESTART TABLE PROCESSING *        
      ******************************************************************        
       7700-SELECT-RESTART-INFO.                                                
      *                                                                         
           EXEC SQL                                                             
             SELECT  WORK_STRG_DESC                                             
               INTO  :TCKRSTINF-WORK-STRG-DESC                                  
               FROM  TCKRSTINF                                                  
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '*7700-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME           
               MOVE PE-MSG-11             TO PV-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7750-SET-CURRENT-TIMESTAMP.                                    *        
      * CURRENT TIMESTAMP SET TO BE COMPARED TO COMMIT TIMESTAMP       *        
      ******************************************************************        
       7750-SET-CURRENT-TIMESTAMP.                                              
      *                                                                         
W31475*    EXEC SQL                                                             
  |   *         SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
  |   *    END-EXEC.                                                            
  |   *                                                                         
  |   *    IF SQLCODE = 0                                                       
  |   *        CONTINUE                                                         
  |   *    ELSE                                                                 
  |   *        MOVE PE-MSG-12             TO PV-OPERATION-ATTEMPTED             
  |   *        MOVE '*7750-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME         
  |   *        PERFORM 9999-SQL-ABEND                                           
  |   *    END-IF.                                                              
  |        ACCEPT WS-CURRENT-TIME FROM TIME.                                    
W31475*                                                                         
      *                                                                         
      ******************************************************************        
      * 7800-COMMIT-PROCESSING.                                        *        
      * COMPARE COMMIT TIMESTAMP TO CURRENT TO DETERMINE IF COMMIT REQD*        
      ******************************************************************        
       7800-COMMIT-PROCESSING.                                                  
           MOVE '*7800-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.             
      *                                                                         
           PERFORM 7750-SET-CURRENT-TIMESTAMP.                                  
      *                                                                         
W31475*    IF (PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP)  OR                  
W31475     IF ((WS-CURRENT-TIME > WS-HCOMMIT-TIME) OR                           
W31475         (WS-CURRENT-TIME < WS-COMMIT-TIME)) OR                           
              (PS-EOF-EXTR-FILE)                                                
      * PREPARE COMMIT RECORD FOR UPDATE                                        
               ADD 1                          TO PA-COMMIT-COUNT                
               MOVE PV-HOLD-OPN-WK-END-DTE    TO CR-FISCAL-WK-END-DTE           
               MOVE PV-HOLD-FSCL-WK-NBR       TO CR-FISCAL-WK-NBR               
               MOVE PV-HOLD-DEPT-NBR          TO CR-DEPT-NBR                    
               MOVE PV-HOLD-STR-NBR           TO CR-STR-NBR                     
               MOVE PA-EXTRACT-RECS-READ      TO CR-TWKDPTS-INPUT-COUNT         
               MOVE PA-UPDATE-COUNT           TO CR-TWKDPTS-UPDATE-COUNT        
               MOVE PA-INSERT-COUNT           TO CR-TWKDPTS-INSERT-COUNT        
               MOVE PA-COMMIT-COUNT           TO CR-TWKDPTS-COMMIT-COUNT        
      * MOVE COMMIT INFO TO WORKING STORAGE ROW                                 
               MOVE CR-CHECKPOINT-RESTART-AREA TO                               
                                                TCKRSTINF-WORK-STRG-DESC        
               MOVE 'Y' TO PS-COMMIT-TAKEN-SW                                   
               IF PS-FIRST-COMMIT                                               
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                        
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                        
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                               
               END-IF                                                           
      *                                                                         
               PERFORM 7850-COMMIT-CHANGES                                      
      *                                                                         
               PERFORM 7600-GET-COMMIT-TIMESTAMP                                
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7850-COMMIT-CHANGES.                                                    
      * UPDATE CHECKPOINT INFORMATION TABLE. TAKE A COMMIT.                     
      ******************************************************************        
       7850-COMMIT-CHANGES.                                                     
      *                                                                         
           EXEC SQL                                                             
             UPDATE TCKRSTINF                                                   
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
              WHERE JOB_NAME       = :PC-JOB-NAME                               
                AND PLAN_NAME      = :PC-PROGRAM-NAME                           
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PE-MSG-08                  TO PV-OPERATION-ATTEMPTED        
               MOVE  '* 7850-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PE-MSG-09                  TO PV-OPERATION-ATTEMPTED        
               MOVE  '* 7850-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      * UPDATE CHECKPOINT STATUS                                                
      ******************************************************************        
       7900-UPDATE-CHECKPOINT-STATUS.                                           
      *                                                                         
           EXEC SQL                                                             
             UPDATE  TCKRSTCNTL                                                 
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME        
               MOVE PE-MSG-10                  TO PV-OPERATION-ATTEMPTED        
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
           EJECT                                                                
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7950-INSERT-CARRY-FWD-ROW                                               
      * PERFORMED IF NO ROW IS FOUND ON DB2 (FOR THIS PROCESSING WEEK)          
      * BUT INVENTORY DOLLARS (COST AND/OR RETAIL) FOUND FOR LAST WEEK          
      ******************************************************************        
       7950-INSERT-CARRY-FWD-ROW.                                               
      *                                                                         
      *                                                                         
           EXEC SQL                                                             
               INSERT INTO TWKDPTS                                              
                   ( FSCL_WK_NBR                                                
                    ,FSCL_WK_END_DTE                                            
                    ,DEPT_NBR                                                   
                    ,STR_NBR                                                    
                    ,SLS_RTL_AMT                                                
                    ,MKDN_RTL_AMT                                               
                    ,MKUP_RTL_AMT                                               
                    ,XFER_IN_RTL_AMT                                            
                    ,XFER_OUT_RTL_AMT                                           
                    ,RCPT_RTL_AMT                                               
                    ,RCPT_NET_COST_AMT                                          
                    ,PADJ_RTL_AMT                                               
                    ,PADJ_NET_COST_AMT                                          
                    ,INV_ADJ_RTL_AMT                                            
                    ,SHNK_RTL_AMT                                               
                    ,END_INV_RTL_AMT                                            
                    ,CUM_MKUP_PCT                                               
                    ,END_INV_COST_AMT                                           
                    ,SLS_COST_AMT                                               
                    ,GRO_MRGN_AMT )                                             
               VALUES (:PV-HOLD-FSCL-WK-NBR                                     
                      ,:PV-HOLD-OPN-WK-END-DTE                                  
                      ,:PV-HOLD-DEPT-NBR                                        
                      ,:PV-HOLD-STR-NBR                                         
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,0                                                        
                      ,:PV-PREV-WEEK-END-INV-RTL                                
                      ,0                                                        
                      ,:PV-PREV-WEEK-END-INV-COST                               
                      ,0                                                        
                      ,0  )                                                     
           END-EXEC                                                             
      *                                                                         
           IF SQLCODE = 0                                                       
               ADD 1                      TO PA-INSERT-COUNT                    
           ELSE                                                                 
               MOVE '7950-INSERT-CARRY-FWD-ROW' TO PV-PARAGRAPH-NAME            
               MOVE PE-MSG-14             TO PV-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT. *        
      ******************************************************************        
      ******************************************************************        
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *        
      ******************************************************************        
       8000-EOJ-ROUTINE.                                                        
      *                                                                         
      *    COMMIT ANY FINAL CHANGES THAT MAY HAVE BEEN MADE                     
                                                                                
           PERFORM 7800-COMMIT-PROCESSING                                       
                                                                                
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                               
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                               
           CLOSE WKDPTS-EXTR-FILE.                                              
      *                                                                         
           DISPLAY '                                             '.             
           DISPLAY '******* END OF JOB STATISTICS ********'.                    
           DISPLAY '                                             '.             
           DISPLAY 'INPUT EXTRACT RECORDS READ ' PA-EXTRACT-RECS-READ.          
           DISPLAY '  TWKDPTS ROWS UPDATED   ', PA-UPDATE-COUNT.                
           DISPLAY '  TWKDPTS ROWS INSERTED  ', PA-INSERT-COUNT.                
           DISPLAY '                                             '.             
           DISPLAY '  COMMITS TAKEN         ', PA-COMMIT-COUNT.                 
           DISPLAY '                                             '.             
           DISPLAY 'EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ '.          
      *                                                                         
      ******************************************************************00000100
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM. 00000200
      ******************************************************************00000300
       9999-ABEND.                                                      00000400
                                                                        00000500
           DISPLAY '**  ABEND     **'.                                  00000700
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            00000900
           DISPLAY '**'                                                 00001100
           DISPLAY '**'                                                 00001200
           DISPLAY '**********************************'                 00001300
           CALL 'ILBOABN0'  USING ABEND-CODE.                           00002000
                                                                        00010000
      *                                                                         
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.               
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
      *                                                                         
