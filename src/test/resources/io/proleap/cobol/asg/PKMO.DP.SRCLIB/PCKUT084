      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
       PROGRAM-ID.             PCKUT084.                                        
       AUTHOR.                 V.SUDHAN.                                        
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           NOVEMBER, 2009.                                  
       DATE-COMPILED.                                                           
      *****************************************************************         
      *                                                               *         
      *  THIS PROGRAM WILL CREATE AN EXTRACT FILE CONTAINING THE LIST *         
      *  OF STORES THAT CAN HAVE THE DATA THAT CAN BE PURGED.         *         
      *                                                               *         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT PC-OVERRIDE-VALUE-FILE                                        
                  ASSIGN TO INP01.                                              
      *                                                                         
           SELECT PC-STORE-DATA-PURGE-EXT                                       
                  ASSIGN TO OUT01.                                              
      *                                                                         
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  PC-OVERRIDE-VALUE-FILE                                               
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0  RECORDS.                                           
       01  PC-OVERRIDE-VALUE-REC           PIC  X(080).                         
      *                                                                         
       FD  PC-STORE-DATA-PURGE-EXT                                              
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0  RECORDS.                                           
       01  PC-STORE-DATA-PURGE-REC         PIC  X(080).                         
      *                                                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  ABEND-CODE                      PIC S9(04)    VALUE +0               
                                                         COMP SYNC.             
      *                                                                         
       01  PROGRAM-SWITCHES.                                                    
           05  PS-EOF-STORE-CSR-SW         PIC  X(01)    VALUE 'N'.             
               88  PS-EOF-STORE-DATA-CSR                 VALUE 'Y'.             
               88  PS-NOT-EOF-STORE-DATA-CSR             VALUE 'N'.             
      *                                                                         
       01  PC-YEAR                         PIC  S9(04) COMP VALUE ZEROS.        
       01  PC-MONTH-S                      PIC  S9(02) COMP VALUE ZEROS.        
       01  PC-MONTH-E                      PIC  S9(02) COMP VALUE ZEROS.        
       01  PC-STORE-RANGE                  PIC  9(02)       VALUE ZEROS.        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
                                                         'PCKUT084'.            
           05  PC-CURRENT-DATE.                                                 
               10  PC-CURRENT-YEAR.                                             
                   15  PC-CURRENT-CC       PIC 9(2)    VALUE ZEROES.            
                   15  PC-CURRENT-YY       PIC 9(2)    VALUE ZEROES.            
               10  PC-CURRENT-MM           PIC 9(2)    VALUE ZEROES.            
               10  PC-CURRENT-DD           PIC 9(2)    VALUE ZEROES.            
      *                                                                         
       01  PV-YY                           PIC  9(04)  VALUE ZEROES.            
       01  PV-OVERRIDE-REC.                                                     
           05 PV-STORE-RANGE               PIC  9(02)  VALUE ZEROES.            
           05 PV-YEAR                      PIC  9(04)  VALUE ZEROES.            
           05 PV-MONTH-S                   PIC  9(02)  VALUE ZEROES.            
           05 PV-MONTH-E                   PIC  9(02)  VALUE ZEROES.            
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-FIN-BK-DTE-PLUS60        PIC  X(10)  VALUE SPACES.            
           05  PV-CURRENT-LOC              PIC  9(04)  VALUE ZEROES.            
           05  PV-CURRENT-BK-DATE          PIC  X(10)  VALUE SPACES.            
           05  PV-PROCESS-DATE             PIC  X(06)  VALUE SPACES.            
           05  PV-PROC-DATE.                                                    
               10 PV-PROC-DATE-CCYY        PIC  X(04)  VALUE SPACES.            
               10 PV-PROC-DATE-MM          PIC  X(02)  VALUE SPACES.            
               10 PV-PROC-DATE-DD          PIC  X(02)  VALUE SPACES.            
           05  PV-ABEND-PARAGRAPH          PIC  X(30)  VALUE SPACES.            
           05  PV-PARAGRAPH-NAME           PIC  X(30)  VALUE SPACES.            
           05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)  VALUE SPACES.            
                                                                                
           05  PV-STORE-DATA-CNT           PIC  9(09)  VALUE ZEROS.             
           05  PV-STORE-RANGE-CNT          PIC  9(09)  VALUE ZEROS.             
           05  PV-TOTAL-CNT                PIC  9(09)  VALUE ZEROS.             
      *                                                                         
      ******************************************************************        
      *  COPY BOOK FOR THE STORE DATA PURGE EXTRACT FILE                        
      ******************************************************************        
           COPY PCRD067.                                                        
      ******************************************************************        
      *  DB2 FORMATTED MESSAGE AREA                                             
      ******************************************************************        
           COPY DPWS004.                                                        
      ******************************************************************        
      *  DB2 COMMUNICATION AREA                                                 
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      ******************************************************************        
      *  DB2 COMMUNICATION AREA2                                                
      ******************************************************************        
           COPY SQLCA2.                                                         
      ******************************************************************        
      *  COPY BOOK FOR THE MERCHANDISE INVENTORY PARMETER TABLE                 
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE XLT00025                                                
           END-EXEC.                                                            
      ******************************************************************        
      *  COPY BOOK FOR THE LOCATION TABLE                                       
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE XLT00010                                                
           END-EXEC.                                                            
      *                                                                         
           EXEC SQL                                                             
             DECLARE STORE-DATA-CSR CURSOR FOR                                  
                 SELECT                                                         
                         L.LOC_NBR                                              
                       , P.PLND_INV_DTE                                         
                       , P.INV_DTE                                              
                       , P.SCN_INV_IND                                          
                       , P.UNIT_BK_DTE                                          
                       , P.FIN_BK_DTE                                           
                       , P.FIN_BK_DTE + 60 DAYS  AS BOOK_PLUS60                 
                       , P.PLND_UNIT_BK_DTE                                     
                       , P.PLND_FIN_BK_DTE                                      
                  FROM XLT_MDSE_INV_PARM P,                                     
                       XLT_LOC           L                                      
                      WHERE                                                     
                       YEAR(P.FIN_BK_DTE + 60 DAYS) = :PC-YEAR                  
                       AND YEAR(P.FIN_BK_DTE)   <>  9999                        
                       AND MONTH(P.FIN_BK_DTE + 60 DAYS) >= :PC-MONTH-S         
                       AND MONTH(P.FIN_BK_DTE + 60 DAYS) <= :PC-MONTH-E         
                       AND P.LOC_NBR             = L.LOC_NBR                    
                       AND L.LOC_TYP_CDE         = 'DS'                         
                       AND L.CLO_DTE             > CURRENT DATE                 
                       AND                                                      
                          (                                                     
                           (P.SCHD_PHY_CNT_CDE   = 'PI'                         
                        AND P.SCN_INV_IND        = 'Y')                         
                         OR                                                     
                           (L.E_BUS_IND          = 'Y'                          
                        AND L.LOC_TYP_CDE        = 'DS')                        
                          )                                                     
                    ORDER BY BOOK_PLUS60,                                       
                             L.LOC_NBR                                          
           END-EXEC.                                                            
      *                                                                         
       PROCEDURE DIVISION.                                                      
      *                                                                         
      ******************************************************************        
      **  THIS IS THE MAIN DRIVER FOR THE PROGRAM.                    **        
      ******************************************************************        
      *                                                                         
       0000-MAINLINE-PROCESSING.                                                
      *                                                                         
           PERFORM 1000-INITIALIZATION.                                         
      *                                                                         
           PERFORM 2000-PROCESS-STORES-DATA UNTIL                               
                   PS-EOF-STORE-DATA-CSR.                                       
      *                                                                         
           IF PV-STORE-DATA-CNT GREATER THAN ZEROES                             
              MOVE XLT00010-LOC-NBR   TO PC067-STORE-END-NUM                    
              MOVE PV-CURRENT-BK-DATE TO PC067-PURGE-DATE                       
              MOVE 7                  TO PC067-OFFSET-DATE                      
              MOVE 'N'                TO PC067-PROCESSED-REC-SW                 
              PERFORM 8000-WRITE-EXTRACT-RECORD                                 
           END-IF                                                               
      *                                                                         
           DISPLAY '                                        '.                  
           DISPLAY '** PCKUT084-TOTAL NO OF STORES FETCHED  : '                 
                   PV-STORE-DATA-CNT                                            
           DISPLAY '** PCKUT084-TOTAL OUTPUT RECS WRITTEN   : '                 
                   PV-TOTAL-CNT.                                                
           DISPLAY '                                        '.                  
           DISPLAY '** PROGRAM PCKUT084 EXECUTION ENDED   **'.                  
      *                                                                         
           PERFORM 3000-CLOSE-STORE-DATA-CSR.                                   
           CLOSE PC-OVERRIDE-VALUE-FILE                                         
                 PC-STORE-DATA-PURGE-EXT.                                       
      *                                                                         
           GOBACK.                                                              
      *                                                                         
      ******************************************************************        
      ** PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.              **        
      ******************************************************************        
      *                                                                         
       1000-INITIALIZATION.                                                     
      *                                                                         
           DISPLAY '** PROGRAM PCKUT084 EXECUTION STARTED **'.                  
      *                                                                         
           OPEN INPUT  PC-OVERRIDE-VALUE-FILE                                   
                OUTPUT PC-STORE-DATA-PURGE-EXT.                                 
           PERFORM 1100-READ-OVERRIDE-VALUE-FILE.                               
      *                                                                         
           SET  PS-NOT-EOF-STORE-DATA-CSR TO TRUE.                              
      *                                                                         
           INITIALIZE  PC067-STORE-DATA-PURGE-EXTRACT.                          
           PERFORM 1200-OPEN-STORE-DATA-CSR.                                    
           PERFORM 8100-FETCH-STORE-DATA-CSR.                                   
           MOVE XLT00010-LOC-NBR     TO PC067-STORE-START-NUM                   
           MOVE PV-FIN-BK-DTE-PLUS60 TO PV-CURRENT-BK-DATE.                     
      *                                                                         
      ******************************************************************        
      **  THIS PARAGRAPH READS THE OVERRIDE VALUE FILE.               **        
      ******************************************************************        
      *                                                                         
       1100-READ-OVERRIDE-VALUE-FILE.                                           
      *                                                                         
           MOVE '1100-READ-OVERRIDE-VALUE-FILE' TO  PV-ABEND-PARAGRAPH.         
                                                                                
           READ PC-OVERRIDE-VALUE-FILE                                          
               INTO                                                             
                   PV-OVERRIDE-REC                                              
               AT END                                                           
                  ACCEPT PV-PROCESS-DATE FROM DATE                              
                   MOVE PV-PROCESS-DATE(1:2) TO PC-CURRENT-YY                   
                   MOVE PV-PROCESS-DATE(3:2) TO PC-CURRENT-MM                   
                   MOVE PV-PROCESS-DATE(5:2) TO PC-CURRENT-DD                   
                  IF PC-CURRENT-YY < 99                                         
                     MOVE 20 TO PC-CURRENT-CC                                   
                   ELSE                                                         
                     MOVE 21 TO PC-CURRENT-CC                                   
                   END-IF                                                       
                   MOVE 20                   TO PC-STORE-RANGE                  
                   MOVE PC-CURRENT-YEAR      TO PV-YY                           
                   MOVE PV-YY                TO PC-YEAR                         
                   MOVE PC-CURRENT-MM        TO PC-MONTH-S                      
                                                PC-MONTH-E                      
               NOT AT END                                                       
                   MOVE PV-STORE-RANGE       TO PC-STORE-RANGE                  
                   MOVE PV-YEAR              TO PC-YEAR                         
                   MOVE PV-MONTH-S           TO PC-MONTH-S                      
                   MOVE PV-MONTH-E           TO PC-MONTH-E                      
           END-READ.                                                            
      ******************************************************************        
      **  THIS PARAGRAPH OPENS THE CURSOR WHICH WILL CONTAIN THE NO   **        
      **  OF STORES THAT CAN HAVE THE DATA WHICH CAN BE PURGED.       **        
      ******************************************************************        
      *                                                                         
       1200-OPEN-STORE-DATA-CSR.                                                
      *                                                                         
           EXEC SQL                                                             
                OPEN STORE-DATA-CSR                                             
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '1200-OPEN-STORE-DATA-CSR'                              
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE 'OPEN THE STORE-DATA-CSR        '                       
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      **  THIS PARAGRAPH READS THE RECORDS FROM STORE DATA PURGE      **        
      **  INTERNAL TABLE AND WRITE A RECORD TO THE OUTPUT FILE FOR    **        
      **  EVERY 20 STORES RANGE OR WHEN EVER THE BOOK DATE CHNAGES.   **        
      ******************************************************************        
      *                                                                         
       2000-PROCESS-STORES-DATA.                                                
      *                                                                         
            IF (PV-STORE-RANGE-CNT > PC-STORE-RANGE) OR                         
               (PV-FIN-BK-DTE-PLUS60 NOT EQUAL PV-CURRENT-BK-DATE)              
                 MOVE PV-CURRENT-LOC     TO PC067-STORE-END-NUM                 
                 MOVE PV-CURRENT-BK-DATE TO PC067-PURGE-DATE                    
                 MOVE 7                  TO PC067-OFFSET-DATE                   
                 MOVE 'N'                TO PC067-PROCESSED-REC-SW              
                 PERFORM 8000-WRITE-EXTRACT-RECORD                              
                 MOVE 1      TO PV-STORE-RANGE-CNT                              
                 MOVE PV-FIN-BK-DTE-PLUS60 TO PV-CURRENT-BK-DATE                
                 MOVE XLT00010-LOC-NBR  TO PC067-STORE-START-NUM                
            END-IF                                                              
            MOVE XLT00010-LOC-NBR     TO PV-CURRENT-LOC                         
            PERFORM 8100-FETCH-STORE-DATA-CSR.                                  
      *                                                                         
      ******************************************************************        
      **  THIS PARAGRAPH CLOSES THE CURSOR WHICH WILL CONTAIN THE NO  **        
      **  OF STORES THAT CAN HAVE THE DATA WHICH CAN BE PURGED.       **        
      ******************************************************************        
      *                                                                         
       3000-CLOSE-STORE-DATA-CSR.                                               
      *                                                                         
           EXEC SQL                                                             
               CLOSE STORE-DATA-CSR                                             
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                    MOVE '3000-CLOSE-STORE-DATA-CSR'                            
                                    TO PV-PARAGRAPH-NAME                        
                    MOVE 'CLOSE THE STORE-DATA-CSR              '               
                                    TO PV-DB2-OPERATION-ATTEMPTED               
                    PERFORM 9000-SQL-ABEND                                      
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      **  THIS PARAGRAPH WRITES THE RECORDS IN TO THE OUTPUT FILE     **        
      **  IN PCRD067 LAYOUT.                                          **        
      ******************************************************************        
      *                                                                         
       8000-WRITE-EXTRACT-RECORD.                                               
      *                                                                         
            WRITE PC-STORE-DATA-PURGE-REC                                       
             FROM PC067-STORE-DATA-PURGE-EXTRACT.                               
              ADD 1 TO PV-TOTAL-CNT.                                            
      *                                                                         
      ******************************************************************        
      **  THIS PARAGRAPH FETCHES THE CURSOR WHICH WILL CONTAIN THE NO **        
      **  OF STORES THAT CAN HAVE THE DATA WHICH CAN BE PURGED.       **        
      ******************************************************************        
      *                                                                         
       8100-FETCH-STORE-DATA-CSR.                                               
      *                                                                         
           EXEC SQL                                                             
                FETCH STORE-DATA-CSR                                            
                INTO  :XLT00010-LOC-NBR                                         
                    , :XLT00025-PLND-INV-DTE                                    
                    , :XLT00025-INV-DTE                                         
                    , :XLT00025-SCN-INV-IND                                     
                    , :XLT00025-UNIT-BK-DTE                                     
                    , :XLT00025-FIN-BK-DTE                                      
                    , :PV-FIN-BK-DTE-PLUS60                                     
                    , :XLT00025-PLND-UNIT-BK-DTE                                
                    , :XLT00025-PLND-FIN-BK-DTE                                 
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
                  ADD  1  TO  PV-STORE-DATA-CNT                                 
                              PV-STORE-RANGE-CNT                                
             WHEN SQLCODE = +100                                                
                  SET PS-EOF-STORE-DATA-CSR TO TRUE                             
             WHEN SQLWARN0 NOT = SPACES                                         
             WHEN SQLCODE < ZERO                                                
             WHEN SQLCODE > ZERO                                                
                 MOVE '8100-FETCH-STORE-DATA-CSR'                               
                                     TO PV-PARAGRAPH-NAME                       
                 MOVE 'FETCH STORE-DATA-CSR CURSOR '                            
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                 PERFORM 9000-SQL-ABEND                                         
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      *  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL   *        
      *  ERROR OR WARNING CODE OCCURS.                                 *        
      ******************************************************************        
      *                                                                         
       9000-SQL-ABEND.                                                          
      *                                                                         
           DISPLAY '9000-SQL-ABEND'.                                            
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
      *                                                                         
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
      * FORMAT.                                                        *        
      ******************************************************************        
      *                                                                         
           COPY DPPD004.                                                        
      *                                                                         
           MOVE +4013         TO ABEND-CODE.                                    
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
      *                                                                         
