       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.         PXKUT019.                                    00020000
       AUTHOR.             DEANNA BRANTNER.                             00030000
       INSTALLATION.       KOHLS DEPARTMENT STORES.                     00040000
       DATE-WRITTEN.       OCTOBER, 2009.                               00050000
       DATE-COMPILED.                                                   00060000
      ***************************************************************** 00070000
      * THIS PROGRAM PREPARES THE FILE PROCESSED BY PXKUP019            00080000
      * TO BE PROCESSED UNDER THE DB2 CHECKPOINT RESTART SYSTEM.        00090000
      *                                                                 00100000
      * THE PROGRAM READS THE INPUT RECORDS, REFORMATS THEM             00110000
      * AND SETS A CHECKPOINT INDICATOR ON THE RECORD BASED             00120000
      * ON HOW CHECKPOINTS WILL BE TAKEN BY THE MAINTENANCE PROGRAM.    00130000
      * AFTER A RECORD HAS BEEN REFORMATTED IT IS PASSED TO THE         00140000
      * TRANSACTION PREPARATION MODULE (DPKUT900) WHICH DOES SOME MORE  00150000
      * PROCESSING ON THE RECORDS AND WRITES THE REFORMATTED RECORDS    00160000
      * OUT TO A SEQUENTIAL FILE.                                       00170000
      *                                                                 00180000
      * THE PROGRAM COMMUNICATES WITH THE TRANSACTION PREPARATION       00190000
      * MODULE USING FUNCTION CODES (OPEN, WRITE, & CLOSE) AND CHECKS   00200000
      * THE RETURN CODE AFTER EACH REQUEST.  IF AN INVALID RETURN CODE  00210000
      * IS RECEIVED, ANY INFORMATION THAT MAY BE HELPFUL IN RESOLVING   00220000
      * THE BAD RETURN CODE IS DISPLAYED AND THE PROGRAM ABENDS.        00230000
      ***************************************************************** 00240000
260107*========================== CHANGE LOG ===========================00241002
260107*  CHANGE         BY          DATE     DESCRIPTION                00242002
260107*=================================================================00243002
260107* CHG0260107   JIM HUNTER   08-16-21   ADD COPYBOOK DPWS990 TO    00246002
260107*                                      MAKE DPKUT900 CALL DYNAMIC.00247002
260107******************************************************************00248002
       ENVIRONMENT DIVISION.                                            00250000
       CONFIGURATION SECTION.                                           00260000
       SOURCE-COMPUTER.    IBM-3090.                                    00270000
       OBJECT-COMPUTER.    IBM-3090.                                    00280000
                                                                        00290000
       INPUT-OUTPUT SECTION.                                            00300000
       FILE-CONTROL.                                                    00310000
           SELECT INPUT-FILE ASSIGN TO INP01.                           00320000
      ******************************************************************00330000
       DATA DIVISION.                                                   00340000
       FILE SECTION.                                                    00350000
                                                                        00360000
       FD  INPUT-FILE                                                   00370000
           RECORDING MODE IS F                                          00380000
           BLOCK CONTAINS 0 RECORDS                                     00390000
           LABEL RECORDS ARE OMITTED.                                   00400000
                                                                        00410000
       01  INPUT-FILE-RECORD       PIC  X(027).                         00420000
      ******************************************************************00430000
       WORKING-STORAGE SECTION.                                         00440000
       01  ABEND-CODE                       PIC S9(04) VALUE +0  COMP.  00450000
                                                                        00460000
       01  PROGRAM-CONSTANTS.                                           00470000
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'PXKUT019'.00480000
           05  PC-TRAN-PREP-FUNC-CODES.                                 00490000
               10  PC-TRAN-PREP-FUNC-OPEN   PIC X(05)  VALUE 'OPEN '.   00500000
               10  PC-TRAN-PREP-FUNC-WRITE  PIC X(05)  VALUE 'WRITE'.   00510000
               10  PC-TRAN-PREP-FUNC-CLOSE  PIC X(05)  VALUE 'CLOSE'.   00520000
           05  PC-JOB-NAME                  PIC X(08)  VALUE 'PXK049  '.00530000
           05  PC-PLAN-NAME                 PIC X(08)  VALUE 'PXKUP019'.00540000
           05  PC-TRANS-RECORD-LENGTH       PIC S9(04) VALUE +027 COMP. 00550000
                                                                        00560000
      ******************************************************************00570000
       01  PROGRAM-VARIABLES.                                           00580000
           05  PV-CHECKPOINT-TYPE           PIC X(01)  VALUE SPACE.     00590000
               88  CONTROL-BREAK                       VALUE 'C'.       00600000
               88  LOGICAL-TRANSACTION                 VALUE 'L'.       00610000
               88  TIME-INTERVAL                       VALUE 'T'.       00620000
               88  REQUESTED-BY-PROGRAM                VALUE 'R'.       00630000
           05  PV-HOLD-SKU-NBR            PIC  S9(8)V COMP-3 VALUE ZERO.00640001
                                                                        00650000
      ******************************************************************00660000
       01  PROGRAM-ACCUMULATORS.                                        00670000
           05  PA-INPUT-RECORDS-READ        PIC S9(11) VALUE 0  COMP-3. 00680000
                                                                        00690000
           05  PA-OUTPUT-RECS-WRITTEN       PIC S9(11) VALUE 0  COMP-3. 00700000
                                                                        00710000
      ******************************************************************00720000
       01  PROGRAM-SWITCHES.                                            00730000
           05  PS-INPUT-UPDATE-EOF-SW       PIC X(01)  VALUE 'N'.       00740000
               88  PS-INPUT-UPDATE-EOF                 VALUE 'Y'.       00750000
           05  PS-FIRST-TRANS-READ-SW       PIC X(01)  VALUE 'Y'.       00760000
               88  FIRST-TRANS-READ                    VALUE 'Y'.       00770000
                                                                        00780000
      ******************************************************************00790000
      *  INPUT FILE LAYOUT                                              00800000
      ******************************************************************00810000
           COPY PXRD032.                                                00820000
                                                                        00830000
      ******************************************************************00840000
      *  TRANSACTION PREPARATION MODULE LINKAGE SECTION                *00850000
      ******************************************************************00860000
260107     COPY DPWS990.                                                00870002
                                                                        00880000
           COPY DPLS000.                                                00881002
                                                                        00882002
      ******************************************************************00890000
      *  SQL COMMUNICATIONS WORK AREA                                  *00900000
      ******************************************************************00910000
           COPY SQLCA2.                                                 00920000
                                                                        00930000
      ******************************************************************00940000
       PROCEDURE DIVISION.                                              00950000
      ******************************************************************00960000
      * A100-MAINLINE-PROCESSING                                        00970000
      *    CONTROLS FLOW OF THE PROGRAM                                *00980000
      ******************************************************************00990000
       A100-MAINLINE-PROCESSING.                                        01000000
                                                                        01010000
           PERFORM 1000-INITIALIZE.                                     01020000
                                                                        01030000
           PERFORM 2000-PROCESS-UPDATE-INPUT-FILE                       01040000
               UNTIL PS-INPUT-UPDATE-EOF.                               01050000
                                                                        01060000
           PERFORM 3000-END-OF-JOB-ROUTINE.                             01070000
                                                                        01080000
           GOBACK.                                                      01090000
      ******************************************************************01100000
      *  INITIALIZE                                                     01110000
      ******************************************************************01120000
       1000-INITIALIZE.                                                 01130000
                                                                        01140000
           PERFORM C100-ISSUE-OPEN-REQUEST.                             01150000
                                                                        01160000
           OPEN INPUT INPUT-FILE.                                       01170000
                                                                        01180000
           PERFORM C200-READ-UPDATE-INPUT-FILE.                         01190000
           IF PS-INPUT-UPDATE-EOF                                       01200000
               DISPLAY ' '                                              01210000
               DISPLAY 'MESSAGES FOR PROGRAM ' PC-PROGRAM-NAME          01220000
               DISPLAY 'UPDATE INPUT FILE IS EMPTY'                     01230000
               DISPLAY ' '                                              01240000
           END-IF.                                                      01250000
                                                                        01260000
      ******************************************************************01270000
      * 2000-PROCESS-UPDATE-INPUT-FILE.                                *01280000
      *     READ AN UPDATE INPUT RECORD.                               *01290000
      *     IF END-OF-FILE, STOP PROCESSING.                           *01300000
      *     IF A RECORD IS READ, GATHER WHATEVER OTHER DATA            *01310000
      *     IS NECESSARY TO TAKE CHECKPOINTS BASED ON THE TYPE OF      *01320000
      *     CHECKPOINTS BEING TAKEN BY XSKUP096 AND ISSUE A WRITE      *01330000
      *     REQUEST TO THE TRANSACTION PREPARATION PROGRAM.            *01340000
      ******************************************************************01350000
       2000-PROCESS-UPDATE-INPUT-FILE.                                  01360000
                                                                        01370000
           IF FIRST-TRANS-READ                                          01380000
               MOVE 'N'                 TO PS-FIRST-TRANS-READ-SW       01390000
           END-IF.                                                      01400000
                                                                        01410000
           IF CONTROL-BREAK                                             01420000
               PERFORM C300-CKPT-BY-CNTL-BREAK                          01430000
           ELSE                                                         01440000
               IF LOGICAL-TRANSACTION                                   01450000
                   PERFORM C400-CKPT-BY-LOGICAL-TRANS                   01460000
               ELSE                                                     01470000
                   PERFORM C500-CKPT-BY-TIME-OR-REQUEST                 01480000
               END-IF                                                   01490000
           END-IF.                                                      01500000
                                                                        01510000
           PERFORM C200-READ-UPDATE-INPUT-FILE.                         01520000
                                                                        01530000
      ******************************************************************01540000
      * 3000-END-OF-JOB-ROUTINE                                        *01550000
      ******************************************************************01560000
       3000-END-OF-JOB-ROUTINE.                                         01570000
                                                                        01580000
           CLOSE INPUT-FILE.                                            01590000
                                                                        01600000
           DISPLAY ' '.                                                 01610000
           DISPLAY 'MESSAGES FOR PROGRAM ' PC-PROGRAM-NAME.             01620000
           DISPLAY 'NUMBER OF INPUT RECORDS READ:          '            01630000
                   PA-INPUT-RECORDS-READ.                               01640000
           DISPLAY 'NUMBER OF TRANS/PREP RECORDS WRITTEN:  '            01650000
                   PA-OUTPUT-RECS-WRITTEN.                              01660000
           DISPLAY ' '.                                                 01670000
                                                                        01680000
           PERFORM C600-ISSUE-CLOSE-REQUEST.                            01690000
                                                                        01700000
      ******************************************************************01710000
      * C100-ISSUE-OPEN-REQUEST                                        *01720000
      *     ISSUE AN 'OPEN' REQUEST TO THE TRANSACTION PREPARATION     *01730000
      *     MODULE.                                                    *01740000
      ******************************************************************01750000
       C100-ISSUE-OPEN-REQUEST.                                         01760000
                                                                        01770000
           MOVE PC-TRAN-PREP-FUNC-OPEN TO DP000-FUNC-CODE.              01780000
           MOVE PC-JOB-NAME            TO DP000-JOB-NAME.               01790000
           MOVE PC-PLAN-NAME           TO DP000-PLAN-NAME.              01800000
                                                                        01810000
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                         01820000
                                                                        01830000
           MOVE DP000-CKPT-TYPE-CODE   TO PV-CHECKPOINT-TYPE.           01840000
                                                                        01850000
      ******************************************************************01860000
      * C200-READ-UPDATE-INPUT-FILE                                    *01870000
      ******************************************************************01880000
       C200-READ-UPDATE-INPUT-FILE.                                     01890000
                                                                        01900000
           READ INPUT-FILE                                              01910000
                INTO PX032-VALID-MOS-ITEMS                              01920000
               AT END                                                   01930000
                   SET PS-INPUT-UPDATE-EOF TO TRUE.                     01940000
                                                                        01950000
           IF PS-INPUT-UPDATE-EOF                                       01960000
               CONTINUE                                                 01970000
           ELSE                                                         01980000
               ADD +1 TO PA-INPUT-RECORDS-READ                          01990000
           END-IF.                                                      02000000
                                                                        02010000
      ******************************************************************02020000
      * C300-CKPT-BY-CNTL-BREAK                                        *02030000
      *     THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSAC- *02040000
      *     TION RECORD "ON" IF THERE IS A CHANGE IN THE STORE         *02050000
      *     NUMBER.  AFTER THE TRANSACTION RECORD HAS BEEN FORMATTED,  *02060000
      *     A WRITE REQUEST IS ISSUED TO THE TRANSACTION PREPARATION   *02070000
      *     MODULE.                                                    *02080000
      * NOTE: AT THIS TIME CONTROL/LOGIC BREAK PROCESSING IS IDENTICAL.*02090000
      ******************************************************************02100000
       C300-CKPT-BY-CNTL-BREAK.                                         02110000
                                                                        02120000
           IF PX032-SKU-NBR       = PV-HOLD-SKU-NBR                     02130000
               MOVE 'N' TO DP000-CHECKPOINT-IND                         02140000
           ELSE                                                         02150000
               MOVE 'Y' TO DP000-CHECKPOINT-IND                         02160000
               MOVE PX032-SKU-NBR       TO PV-HOLD-SKU-NBR              02170000
           END-IF.                                                      02180000
                                                                        02190000
           MOVE PC-TRANS-RECORD-LENGTH   TO DP000-TRANS-REC-LENGTH.     02200000
           MOVE PX032-VALID-MOS-ITEMS    TO DP000-TRANS-REC.            02210000
           MOVE PC-TRAN-PREP-FUNC-WRITE  TO DP000-FUNC-CODE.            02220000
           MOVE PC-JOB-NAME              TO DP000-JOB-NAME.             02230000
           MOVE PC-PLAN-NAME             TO DP000-PLAN-NAME.            02240000
                                                                        02250000
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                         02260000
           ADD +1 TO PA-OUTPUT-RECS-WRITTEN.                            02270000
                                                                        02280000
      ******************************************************************02290000
      * C400-CKPT-BY-LOGICAL-TRANS.                                    *02300000
      *     THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSAC- *02310000
      *     TION RECORD "ON".  THE UPDATE PROGRAM WILL CHECK THE       *02320000
      *     CHECKPOINT FREQUENCY TO KNOW WHETHER A CHECKPOINT SHOULD BE*02330000
      *     TAKEN.   AFTER THE TRANSACTION RECORD HAS BEEN FORMATTED,  *02340000
      *     A WRITE REQUEST IS ISSUED TO THE TRANSACTION PREPARATION   *02350000
      *     MODULE.                                                    *02360000
      ******************************************************************02370000
       C400-CKPT-BY-LOGICAL-TRANS.                                      02380000
                                                                        02390000
           MOVE 'Y'                      TO DP000-CHECKPOINT-IND.       02400000
           MOVE PC-TRANS-RECORD-LENGTH   TO DP000-TRANS-REC-LENGTH.     02410000
           MOVE PX032-VALID-MOS-ITEMS    TO DP000-TRANS-REC.            02421000
           MOVE PC-TRAN-PREP-FUNC-WRITE  TO DP000-FUNC-CODE.            02430000
           MOVE PC-JOB-NAME              TO DP000-JOB-NAME.             02440000
           MOVE PC-PLAN-NAME             TO DP000-PLAN-NAME.            02450000
                                                                        02460000
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                         02470000
           ADD +1 TO PA-OUTPUT-RECS-WRITTEN.                            02480000
                                                                        02490000
      ******************************************************************02500000
      * C500-CKPT-BY-TIME-OR-REQUEST.                                  *02510000
      *     THIS ROUTINE BUILDS THE TRANSACTION RECORD AND ISSUES A    *02520000
      *     WRITE REQUEST TO THE TRANSACTION PREPARATION MODULE IF     *02530000
      *     CHECKPOINTS ARE TAKEN BY TIME INTERVAL (SECONDS) OR        *02540000
      *     REQUESTED BY THE MAINTENANCE PROGRAM.                      *02550000
      ******************************************************************02560000
       C500-CKPT-BY-TIME-OR-REQUEST.                                    02570000
                                                                        02580000
           MOVE 'N'                        TO DP000-CHECKPOINT-IND.     02590000
           MOVE PC-TRANS-RECORD-LENGTH     TO DP000-TRANS-REC-LENGTH.   02600000
           MOVE PX032-VALID-MOS-ITEMS      TO DP000-TRANS-REC.          02611000
           MOVE PC-TRAN-PREP-FUNC-WRITE    TO DP000-FUNC-CODE.          02620000
           MOVE PC-JOB-NAME                TO DP000-JOB-NAME.           02630000
           MOVE PC-PLAN-NAME               TO DP000-PLAN-NAME.          02640000
                                                                        02650000
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                         02660000
           ADD +1 TO PA-OUTPUT-RECS-WRITTEN.                            02670000
                                                                        02680000
      ******************************************************************02690000
      * C600-ISSUE-CLOSE-REQUEST                                       *02700000
      *     ISSUE A 'CLOSE' REQUEST TO THE TRANSACTION PREPARATION     *02710000
      *     MODULE.                                                    *02720000
      ******************************************************************02730000
       C600-ISSUE-CLOSE-REQUEST.                                        02740000
                                                                        02750000
           MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.             02760000
           MOVE PC-JOB-NAME             TO DP000-JOB-NAME.              02770000
           MOVE PC-PLAN-NAME            TO DP000-PLAN-NAME.             02780000
                                                                        02790000
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                         02800000
                                                                        02810000
      ******************************************************************02820000
      *  TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES        *02830000
      ******************************************************************02840000
           COPY DPPD000.                                                02850000
