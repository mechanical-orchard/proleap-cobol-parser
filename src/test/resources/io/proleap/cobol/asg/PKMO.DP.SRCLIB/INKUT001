00001 ****************************************************************  01/25/95
00002  IDENTIFICATION DIVISION.                                         INKUT001
00003 ****************************************************************     LV001
00004  PROGRAM-ID.      INKUT001.                                          CL**2
00005  AUTHOR.          DIANE THEEL.                                       CL**2
00006  INSTALLATION.    KOHLS DEPARTMENT STORES.                        INKUT001
00007  DATE-WRITTEN.    01/18/95.                                          CL**2
00008  DATE-COMPILED.                                                   INKUT001
00009 ****************************************************************  INKUT001
00010 * THIS PROGRAM WILL READ IN TWO ACCUM TRANSFER FILES:          *     CL**4
00011 *     1)  PKMO.INV.CE.BEF.FINONLY - CONTAINS BEFORE INVENTORY  *     CL**3
00012 * CENTRAL STOCK TRANSFERS THAT WE ARE PROCESSING AS 'FINANCIAL *     CL**3
00013 * ONLY' FROM JOB CEK034.                                       *     CL**4
00014 *     2)  PKMO.INV.STR.BEF.FINONLY - CONTAINS BERORE INVENTORY *     CL**4
00015 * STORE TO STORE TRANSFERS THAT WE ARE PROCESSING AS 'FINANCIAL*     CL**4
00016 * ONLY' OUT OF JOB MKL010.                                     *     CL**4
00017 *     FOR EACH INCOMING TRANSFER RECORD, TWO OUTPUT RECORDS    *     CL**4
00018 * WILL BE CREATED - ONE REPRESENTING THE EFFECT OF THE 'FROM'  *     CL**4
00019 * STORE, AND ONE REPRESENTING THE EFFECT OF THE 'TO' STORE.    *     CL**4
00020 *                                                              *     CL**4
00021 ****************************************************************  INKUT001
00022 *                                                              *     CL**6
00023 *  CHANGED 06/29/99 BY JULIE SEIDLER                           *     CL**6
00024 *   WR# XXX CORRECTED LOGIC THAT DETERMINES THE TRANSFER YEAR. *     CL**6
00026 *   INSTEAD OF USING MONTH"04" AS A PIVOT MONTH, ANY DATA FROM *     CL**6
00026 *   A MONTH GREATER THAN CURRENT MONTH WILL BE CONSIDERED      *     CL**6
00026 *   TRANSFERS FROM THE PREVIOUS YEAR.                          *     CL**6
00026 *                                                              *     CL**6
00021 ****************************************************************  INKUT001
W26602*  CHANGED 04/06/04 BY SCOTT JACKSON                           *     CL**6
W26602*   WR#26602 STORE EXPANSION EFFORT. COPYBOOKS MLRD001/MLRD019 *     CL**6
W26602*   CHANGES TO EXPAND STORE NUMBER TO X(5). PROGRAM RECOMPILE. *     CL**6
00021 ****************************************************************  INKUT001
PM3403*  CHANGED 04/20/12 BY SCOTT JACKSON                           *     CL**6
PM3403*   PM3403 LARGE RETAIL RECOMPILE ONLY(MLRD001/MLRD019)              CL**6
00027 ****************************************************************     CL**6
00028  ENVIRONMENT DIVISION.                                            INKUT001
00029 ****************************************************************     CL**6
00030                                                                      CL**5
00031  CONFIGURATION SECTION.                                           INKUT001
00032  SOURCE-COMPUTER. IBM-3090.                                       INKUT001
00033  OBJECT-COMPUTER. IBM-3090.                                       INKUT001
00034                                                                      CL*10
00035  INPUT-OUTPUT SECTION.                                            INKUT001
00036  FILE-CONTROL.                                                    INKUT001
00037                                                                   INKUT001
00038      SELECT CENTRAL-STOCK-XFERS-IN                                   CL*10
00039          ASSIGN                       TO INP01.                      CL**5
00040      SELECT STORE-TO-STORE-XFER-IN                                   CL**7
00041          ASSIGN                       TO INP02.                      CL**4
00042      SELECT TRANSFER-ACCUM-FINONLY-OUT                               CL**4
00043          ASSIGN                       TO OUT01.                      CL**4
00044      EJECT                                                        INKUT001
00045                                                                      CL**6
00046 ****************************************************************     CL**6
00047  DATA DIVISION.                                                   INKUT001
00048 ****************************************************************     CL**6
00049                                                                      CL**6
00050  FILE SECTION.                                                    INKUT001
00051                                                                   INKUT001
00052  FD  CENTRAL-STOCK-XFERS-IN                                          CL*10
00053      RECORDING MODE F                                                CL**6
00054      BLOCK CONTAINS 0 RECORDS                                        CL**6
00055      RECORD CONTAINS 80 CHARACTERS                                   CL**5
00056      LABEL RECORDS ARE OMITTED                                       CL**6
00057      DATA RECORD IS CENTRAL-STOCK-XFERS-RECORD.                      CL**7
00058                                                                      CL**6
00059  01  CENTRAL-STOCK-XFERS-RECORD                      PIC X(80).      CL**7
00060                                                                      CL**6
00061  FD  STORE-TO-STORE-XFER-IN                                          CL**7
00062      RECORDING MODE F                                                CL**5
00063      BLOCK CONTAINS 0 RECORDS                                        CL**5
00064      RECORD CONTAINS 80 CHARACTERS                                   CL**5
00065      LABEL RECORDS ARE OMITTED                                       CL**5
00066      DATA RECORD IS STORE-TO-STORE-XFER-RECORD.                      CL**7
00067                                                                      CL**5
00068  01  STORE-TO-STORE-XFER-RECORD                      PIC X(80).      CL**7
00069                                                                      CL**5
00070  FD  TRANSFER-ACCUM-FINONLY-OUT                                      CL**5
00071      RECORDING MODE F                                                CL**5
00072      BLOCK CONTAINS 0 RECORDS                                        CL**5
00073      RECORD CONTAINS 80 CHARACTERS                                   CL**5
00074      LABEL RECORDS ARE OMITTED                                       CL**5
00075      DATA RECORD IS TRANSFER-ACCUM-FINONLY-RECORD.                   CL**5
00076                                                                      CL**5
00077  01  TRANSFER-ACCUM-FINONLY-RECORD                   PIC X(80).      CL**5
00078                                                                      CL**5
00079      EJECT                                                        INKUT001
00080                                                                      CL**6
00081  WORKING-STORAGE SECTION.                                         INKUT001
00082                                                                      CL**6
00083      EJECT                                                        INKUT001
00084 ****************************************************************  INKUT001
00085 ** COPYBOOK FOR STORE-TO-STORE TRANSFERS INPUT FILE AND        *     CL**5
00086 ** FOR DIRECT STORE TRANSFERS INPUT FILE.                      *     CL**5
00087 ****************************************************************  INKUT001
00088      COPY MLRD019.                                                   CL**5
00089      EJECT                                                        INKUT001
00090                                                                      CL**6
00091 ****************************************************************     CL**5
00092 ** COPYBOOK FOR 'FINANCIAL ONLY' TRANSFER ACCUM OUTPUT FILE    *     CL**5
00093 ****************************************************************     CL**5
00094      COPY MLRD001.                                                   CL**5
00095      EJECT                                                           CL**5
00096                                                                      CL**5
00097  01  PS-PROGRAM-SWITCHES.                                            CL**6
00098      05  PS-CENTRAL-STOCK-EOF-SW      PIC X(01)      VALUE 'N'.      CL**5
00099          88  PS-CENTRAL-STOCK-EOF                    VALUE 'Y'.      CL**5
00100      05  PS-STORE-TO-STORE-EOF-SW     PIC X(01)      VALUE 'N'.      CL**5
00101          88  PS-STORE-TO-STORE-EOF                   VALUE 'Y'.      CL**5
00102      05  PS-EMPTY-CENTRAL-STOCK-SW    PIC X(01)      VALUE 'N'.      CL**5
00103          88  PS-EMPTY-CENTRAL-STOCK                  VALUE 'Y'.      CL**5
00104      05  PS-EMPTY-STORE-TO-STORE-SW   PIC X(01)      VALUE 'N'.      CL**5
00105          88  PS-EMPTY-STORE-TO-STORE                 VALUE 'Y'.      CL**5
00106                                                                      CL*19
00107  01  PV-CURRENT-DATE.                                                CL**5
           05 PV-CURRENT-DATE-YY            PIC 9(2)       VALUE ZERO.          
           05 PV-CURRENT-DATE-MM            PIC 9(2)       VALUE ZERO.          
           05 PV-CURRENT-DATE-DD            PIC 9(2)       VALUE ZERO.          
                                                                                
00107  01  PV-CURRENT-CCYY                  PIC 9(4)       VALUE ZERO.     CL**5
00107  01  PV-CURRENT-CCYY-X REDEFINES PV-CURRENT-CCYY.                    CL**5
           05 PV-CURRENT-CC                 PIC 9(2).                           
           05 PV-CURRENT-YY                 PIC 9(2).                           
00106                                                                      CL*19
00107  01  PV-PROGRAM-COUNTERS.                                            CL**5
00108      05  PV-CS-READ-CNT               PIC S9(09)     VALUE ZERO      CL**5
00109                                                      COMP-3.         CL*15
00110      05  PV-SS-READ-CNT               PIC S9(09)     VALUE ZERO      CL**5
00111                                                      COMP-3.         CL**5
00112      05  PV-CS-TOTAL-RECS-WRITTEN     PIC S9(09)     VALUE ZERO      CL**5
00113                                                      COMP-3.         CL**5
00114      05  PV-CS-FROM-RECS-WRITTEN      PIC S9(09)     VALUE ZERO      CL**5
00115                                                      COMP-3.         CL**5
00116      05  PV-CS-TO-RECS-WRITTEN        PIC S9(09)     VALUE ZERO      CL**5
00117                                                      COMP-3.         CL**5
00118      05  PV-SS-TOTAL-RECS-WRITTEN     PIC S9(09)     VALUE ZERO      CL**5
00119                                                      COMP-3.         CL**5
00120      05  PV-SS-FROM-RECS-WRITTEN      PIC S9(09)     VALUE ZERO      CL**5
00121                                                      COMP-3.         CL**5
00122      05  PV-SS-TO-RECS-WRITTEN        PIC S9(09)     VALUE ZERO      CL**5
00123                                                      COMP-3.         CL**5
           05  PV-PREVIOUS-CCYY             PIC 9(4)       VALUE ZERO.          
           05  PV-PREVIOUS-CCYY-X REDEFINES PV-PREVIOUS-CCYY.                   
00108          10  PV-PREVIOUS-CC           PIC 9(02).                     CL**5
00108          10  PV-PREVIOUS-YY           PIC 9(02).                     CL**5
00124                                                                      CL*15
00125  01  PV-ABEND-CODE                    PIC S9(04)     VALUE +0     INKUT001
00126                                                      COMP SYNC.   INKUT001
00127                                                                      CL**4
00128  01  PV-PROGRAM-VARIABLES.                                           CL*15
00129      05  PV-ABEND-PARAGRAPH           PIC X(30)      VALUE SPACES.   CL*15
00130                                                                      CL*15
00131 ****************************************************************  INKUT001
00132  PROCEDURE DIVISION.                                              INKUT001
00133 ****************************************************************  INKUT001
00134                                                                   INKUT001
00135      PERFORM 1000-INITIALIZATION.                                    CL*19
00136                                                                   INKUT001
00137      PERFORM 2000-PROCESS-CENTRAL-STOCK                              CL**7
00138        UNTIL PS-CENTRAL-STOCK-EOF.                                   CL**7
00139                                                                      CL**7
00140      PERFORM 3000-PROCESS-STORE-TO-STORE                             CL**7
00141        UNTIL PS-STORE-TO-STORE-EOF.                                  CL**7
00142                                                                   INKUT001
00143      PERFORM 9000-END-ROUTINE.                                       CL*18
00144                                                                   INKUT001
00145      GOBACK.                                                      INKUT001
00146                                                                   INKUT001
00147      EJECT                                                        INKUT001
00148                                                                      CL*16
00149  1000-INITIALIZATION.                                                CL**2
00150 ****************************************************************     CL**2
00151 * 1000-INITIALIZATION OPENS ALL FILES AND INITAILZES ALL OF    *     CL**2
00152 * THE NECESSARY FIELDS.                                        *     CL**6
00153 ****************************************************************     CL**2
00154                                                                   INKUT001
00155      OPEN INPUT  CENTRAL-STOCK-XFERS-IN                              CL*10
00156                  STORE-TO-STORE-XFER-IN                              CL**7
00157           OUTPUT TRANSFER-ACCUM-FINONLY-OUT.                         CL**7
00158                                                                   INKUT001
           ACCEPT PV-CURRENT-DATE FROM DATE.                                    
                                                                                
00159      INITIALIZE ML001-TRANSFER-RECORD                                CL**6
00160                 ML019-TRANSFER-RECORD.                               CL**6
00161                                                                      CL**8
00162      PERFORM 7000-READ-CENTRAL-STOCK-XFERS.                          CL*10
00163      IF PS-CENTRAL-STOCK-EOF                                         CL**8
00164          SET PS-EMPTY-CENTRAL-STOCK TO TRUE                          CL**8
00165      END-IF.                                                         CL**8
00166                                                                   INKUT001
00167  2000-PROCESS-CENTRAL-STOCK.                                         CL**7
00168 ****************************************************************     CL*18
00169 *   THIS IS THE MAIN CONTROLLING PARAGRAPH FOR PROCESSING      *     CL*18
00170 *   CENTRAL STOCK TRANSFERS.                                   *     CL**9
00171 ****************************************************************     CL*18
00172                                                                      CL**3
00173      PERFORM 4100-FORMAT-CS-XFER-TO-REC.                             CL**3
00174      PERFORM 4200-FORMAT-CS-XFER-FROM-REC.                           CL**3
00175      PERFORM 7000-READ-CENTRAL-STOCK-XFERS.                          CL**3
00176                                                                      CL*14
00177  3000-PROCESS-STORE-TO-STORE.                                        CL**7
00178 ****************************************************************     CL**2
00179 *   THIS IS THE MAIN CONTROLLING PARAGRAPH FOR PROCESSING      *     CL**2
00180 *   STORE TO STORE TRANSFERS.                                  *     CL**2
00181 ****************************************************************     CL**2
00182                                                                      CL**3
00183      PERFORM 8000-READ-STORE-TO-STORE-XFER.                          CL**3
00184      IF PS-STORE-TO-STORE-EOF                                        CL**4
00185          CONTINUE                                                    CL**4
00186      ELSE                                                            CL**4
00187          PERFORM 4300-FORMAT-SS-XFER-TO-REC                          CL**3
00188          PERFORM 4400-FORMAT-SS-XFER-FROM-REC                        CL**3
00189      END-IF.                                                         CL**2
00190                                                                      CL**2
00191                                                                      CL**2
00192  4100-FORMAT-CS-XFER-TO-REC.                                         CL**9
00193 ****************************************************************     CL**8
00194 *  FORMAT CENTRAL STOCK 'TO' RECORD                            *     CL**4
00195 ****************************************************************     CL**8
00196                                                                      CL**4
00197      MOVE ML019-TRANSFER-CODE          TO ML001-TRANSFER-CODE.       CL*11
00198      MOVE '1'                          TO ML001-TRANSFER-CODE-2.     CL*11
00199      MOVE ML019-TRANSFER-FROM-STORE-NBR TO                           CL*11
00200          ML001-RELATED-STORE-NBR.                                    CL*11
00201      MOVE ML019-TRANSFER-NBR           TO ML001-TRANSFER-NBR.        CL*11
00202      MOVE ML019-TRANSFER-MM            TO ML001-TRANSFER-MM.         CL*11
00203      MOVE ML019-TRANSFER-DD            TO ML001-TRANSFER-DD.         CL*11
00204      PERFORM 5000-ASSIGN-YEAR.                                       CL**4
00205      MOVE ML019-TRANSFER-KOHLS-MM      TO ML001-TRANSFER-KOHLS-MM.   CL*11
00206      MOVE ML019-TRANSFER-QTY           TO ML001-TRANSFER-QTY.        CL*11
00207      MOVE ML019-TRANSFER-PER-UNT-RTL-AMT TO                          CL*10
00208          ML001-TRANSFER-PER-UNT-RTL-AMT.                             CL*10
00209      MOVE ML019-TRANSFER-BATCH-NBR     TO                            CL*12
00210          ML001-TRANSFER-BATCH-NBR.                                   CL*12
00211      MOVE ML019-TRANSFER-TO-STORE-NBR  TO ML001-STORE-NBR.           CL*11
00212      MOVE ML019-TRANSFER-DEPT-CLASS    TO                            CL*12
00213          ML001-TRANSFER-DEPT-CLASS.                                  CL*12
00214      MOVE ML019-TRANSFER-SEQ-NO        TO ML001-TRANSFER-SEQ-NBR.    CL*12
00215      MOVE 'C'                          TO ML001-TRANSFER-TYPE.       CL**2
00216      COMPUTE ML001-TRANSFER-EXT-RETAIL =                             CL**2
00217          ML019-TRANSFER-QTY * ML019-TRANSFER-PER-UNT-RTL-AMT.        CL**2
00218      WRITE TRANSFER-ACCUM-FINONLY-RECORD FROM                        CL**2
00219          ML001-TRANSFER-RECORD.                                      CL**2
00220      ADD 1 TO PV-CS-TO-RECS-WRITTEN                                  CL**3
00221               PV-CS-TOTAL-RECS-WRITTEN.                              CL**3
00222                                                                      CL**3
00223  4200-FORMAT-CS-XFER-FROM-REC.                                       CL**2
00224 ****************************************************************     CL**2
00225 *    FORMAT CENTRAL STOCK FROM RECORD                          *     CL**4
00226 ****************************************************************     CL**2
00227                                                                      CL**4
00228      MOVE ML019-TRANSFER-CODE          TO ML001-TRANSFER-CODE.       CL**2
00229      MOVE '0'                          TO ML001-TRANSFER-CODE-2.     CL**2
00230      MOVE ML019-TRANSFER-FROM-STORE-NBR TO                           CL**2
00231          ML001-STORE-NBR.                                            CL**2
00232      MOVE ML019-TRANSFER-NBR           TO ML001-TRANSFER-NBR.        CL**2
00233      MOVE ML019-TRANSFER-MM            TO ML001-TRANSFER-MM.         CL**2
00234      MOVE ML019-TRANSFER-DD            TO ML001-TRANSFER-DD.         CL**2
00235      PERFORM 5000-ASSIGN-YEAR.                                       CL**4
00236      MOVE ML019-TRANSFER-KOHLS-MM      TO ML001-TRANSFER-KOHLS-MM.   CL**2
00237      COMPUTE ML001-TRANSFER-QTY =                                    CL**2
00238          ML019-TRANSFER-QTY * -1.                                    CL**2
00239      MOVE ML019-TRANSFER-BATCH-NBR     TO                            CL**2
00240          ML001-TRANSFER-BATCH-NBR.                                   CL**2
00241      MOVE ML019-TRANSFER-TO-STORE-NBR  TO ML001-RELATED-STORE-NBR.   CL**2
00242      MOVE ML019-TRANSFER-DEPT-CLASS    TO                            CL**2
00243          ML001-TRANSFER-DEPT-CLASS.                                  CL**2
00244      MOVE ML019-TRANSFER-SEQ-NO        TO ML001-TRANSFER-SEQ-NBR.    CL**2
00245      MOVE 'C'                          TO ML001-TRANSFER-TYPE.       CL**2
00246      COMPUTE ML001-TRANSFER-EXT-RETAIL =                             CL**2
00247          ML001-TRANSFER-QTY * ML001-TRANSFER-PER-UNT-RTL-AMT.        CL**2
00248      COMPUTE ML001-TRANSFER-PER-UNT-RTL-AMT =                        CL**2
00249          ML019-TRANSFER-PER-UNT-RTL-AMT * -1.                        CL**2
00250                                                                      CL**7
00251      WRITE TRANSFER-ACCUM-FINONLY-RECORD FROM                        CL**2
00252          ML001-TRANSFER-RECORD.                                      CL**2
00253      ADD 1 TO PV-CS-FROM-RECS-WRITTEN                                CL**3
00254               PV-CS-TOTAL-RECS-WRITTEN.                              CL**3
00255                                                                      CL**2
00256  4300-FORMAT-SS-XFER-TO-REC.                                         CL**3
00257 ****************************************************************     CL**3
00258 *    FORMAT STORE TO STORE TRANSFER 'TO' RECORD                *     CL**4
00259 ****************************************************************     CL**3
00260                                                                      CL**4
00261      MOVE ML019-TRANSFER-CODE          TO ML001-TRANSFER-CODE.       CL**3
00262      MOVE '1'                          TO ML001-TRANSFER-CODE-2.     CL**3
00263      MOVE ML019-TRANSFER-FROM-STORE-NBR TO                           CL**3
00264          ML001-RELATED-STORE-NBR.                                    CL**3
00265      MOVE ML019-TRANSFER-NBR           TO ML001-TRANSFER-NBR.        CL**3
00266      MOVE ML019-TRANSFER-MM            TO ML001-TRANSFER-MM.         CL**3
00267      MOVE ML019-TRANSFER-DD            TO ML001-TRANSFER-DD.         CL**3
00268      PERFORM 5000-ASSIGN-YEAR.                                       CL**4
00269      MOVE ML019-TRANSFER-KOHLS-MM      TO ML001-TRANSFER-KOHLS-MM.   CL**3
00270      MOVE ML019-TRANSFER-QTY           TO ML001-TRANSFER-QTY.        CL**3
00271      MOVE ML019-TRANSFER-PER-UNT-RTL-AMT TO                          CL**3
00272          ML001-TRANSFER-PER-UNT-RTL-AMT.                             CL**3
00273      MOVE ML019-TRANSFER-BATCH-NBR     TO                            CL**3
00274          ML001-TRANSFER-BATCH-NBR.                                   CL**3
00275      MOVE ML019-TRANSFER-TO-STORE-NBR  TO ML001-STORE-NBR.           CL**3
00276      MOVE ML019-TRANSFER-DEPT-CLASS    TO                            CL**3
00277          ML001-TRANSFER-DEPT-CLASS.                                  CL**3
00278      MOVE ML019-TRANSFER-SEQ-NO        TO ML001-TRANSFER-SEQ-NBR.    CL**3
00279      MOVE 'S'                          TO ML001-TRANSFER-TYPE.       CL**3
00280      COMPUTE ML001-TRANSFER-EXT-RETAIL =                             CL**3
00281          ML019-TRANSFER-QTY * ML019-TRANSFER-PER-UNT-RTL-AMT.        CL**3
00282                                                                      CL**7
00283      WRITE TRANSFER-ACCUM-FINONLY-RECORD FROM                        CL**3
00284          ML001-TRANSFER-RECORD.                                      CL**3
00285      ADD 1 TO PV-SS-TO-RECS-WRITTEN                                  CL**3
00286               PV-SS-TOTAL-RECS-WRITTEN.                              CL**3
00287                                                                      CL**3
00288  4400-FORMAT-SS-XFER-FROM-REC.                                       CL**3
00289 ****************************************************************     CL**3
00290 *    FORMAT STORE TO STORE TRANSFER 'FROM' RECORD.             *     CL**4
00291 ****************************************************************     CL**3
00292                                                                      CL**4
00293      MOVE ML019-TRANSFER-CODE          TO ML001-TRANSFER-CODE.       CL**3
00294      MOVE '0'                          TO ML001-TRANSFER-CODE-2.     CL**3
00295      MOVE ML019-TRANSFER-FROM-STORE-NBR TO                           CL**3
00296          ML001-STORE-NBR.                                            CL**3
00297      MOVE ML019-TRANSFER-NBR           TO ML001-TRANSFER-NBR.        CL**3
00298      MOVE ML019-TRANSFER-MM            TO ML001-TRANSFER-MM.         CL**3
00299      MOVE ML019-TRANSFER-DD            TO ML001-TRANSFER-DD.         CL**3
00300      PERFORM 5000-ASSIGN-YEAR.                                       CL**4
00301      MOVE ML019-TRANSFER-KOHLS-MM      TO ML001-TRANSFER-KOHLS-MM.   CL**3
00302      COMPUTE ML001-TRANSFER-QTY =                                    CL**2
00303          ML019-TRANSFER-QTY * -1.                                    CL**2
00304      MOVE ML019-TRANSFER-BATCH-NBR     TO                            CL**2
00305          ML001-TRANSFER-BATCH-NBR.                                   CL**2
00306      MOVE ML019-TRANSFER-TO-STORE-NBR  TO ML001-RELATED-STORE-NBR.   CL**2
00307      MOVE ML019-TRANSFER-DEPT-CLASS    TO                            CL**2
00308          ML001-TRANSFER-DEPT-CLASS.                                  CL**2
00309      MOVE ML019-TRANSFER-SEQ-NO        TO ML001-TRANSFER-SEQ-NBR.    CL**2
00310      MOVE 'S'                          TO ML001-TRANSFER-TYPE.       CL**2
00311      COMPUTE ML001-TRANSFER-EXT-RETAIL =                             CL**2
00312          ML001-TRANSFER-QTY * ML001-TRANSFER-PER-UNT-RTL-AMT.        CL**2
00313      COMPUTE ML001-TRANSFER-PER-UNT-RTL-AMT =                        CL**2
00314          ML019-TRANSFER-PER-UNT-RTL-AMT * -1.                        CL**2
00315                                                                      CL**7
00316      WRITE TRANSFER-ACCUM-FINONLY-RECORD FROM                        CL**3
00317          ML001-TRANSFER-RECORD.                                      CL**3
00318      ADD 1 TO PV-SS-FROM-RECS-WRITTEN                                CL**3
00319               PV-SS-TOTAL-RECS-WRITTEN.                              CL**3
00320                                                                      CL**3
00321  5000-ASSIGN-YEAR.                                                   CL**7
00322 ****************************************************************     CL**4
00323 *  IF INCOMING MONTH IS GT THAN CURRENT MONTH ASSIGN PREVIOUS-YY     CL**4
00324 *  AS THE OUTPUT YEAR.  OTHERWISE ASSIGN CURRENT-YY IS THE           CL**4
00324 *  OUTPUT YEAR                                                       CL**4
00325 ****************************************************************     CL**4
00326                                                                      CL**4
           MOVE PV-CURRENT-DATE-YY TO PV-CURRENT-YY.                            
           IF PV-CURRENT-DATE-YY > 80                                           
               MOVE 19 TO PV-CURRENT-CC                                         
           ELSE                                                                 
               MOVE 20 TO PV-CURRENT-CC.                                        
                                                                                
00327      IF ML019-TRANSFER-MM > PV-CURRENT-DATE-MM                       CL**4
JS0398         COMPUTE PV-PREVIOUS-CCYY = PV-CURRENT-CCYY - 1                   
JS0398         MOVE PV-PREVIOUS-YY TO ML001-TRANSFER-YY                    CL**4
00329      ELSE                                                            CL**4
JS0398         MOVE PV-CURRENT-YY TO ML001-TRANSFER-YY                     CL**4
00331      END-IF.                                                         CL**4
00332                                                                      CL**4
00333  7000-READ-CENTRAL-STOCK-XFERS.                                      CL*10
00334 *****************************************************************    CL**2
00335 * READ THE CENTRAL STOCK TRANSFERS INPUT FILE.                  *    CL**7
00336 *****************************************************************    CL**2
00337                                                                      CL**2
00338      READ CENTRAL-STOCK-XFERS-IN                                     CL*10
00339        INTO ML019-TRANSFER-RECORD                                    CL**7
00340          AT END SET PS-CENTRAL-STOCK-EOF TO TRUE.                    CL**7
00341                                                                      CL**2
00342      IF PS-CENTRAL-STOCK-EOF                                         CL**7
00343          CONTINUE                                                    CL*15
00344      ELSE                                                            CL*15
00345          ADD 1 TO PV-CS-READ-CNT                                     CL**7
00346      END-IF.                                                         CL*15
00347                                                                   INKUT001
00348  8000-READ-STORE-TO-STORE-XFER.                                      CL*10
00349 *****************************************************************    CL**2
00350 * READ THE STORE TO STORE TRANSFERS INPUT FILE.                 *    CL**2
00351 *****************************************************************    CL**2
00352                                                                      CL**2
00353      READ STORE-TO-STORE-XFER-IN                                     CL**7
00354        INTO ML019-TRANSFER-RECORD                                    CL**2
00355          AT END SET PS-STORE-TO-STORE-EOF TO TRUE.                   CL**2
00356                                                                      CL**2
00357      IF PS-STORE-TO-STORE-EOF                                        CL**2
00358          IF PV-SS-READ-CNT < 1                                       CL**2
00359              SET PS-EMPTY-STORE-TO-STORE TO TRUE                     CL**2
00360          END-IF                                                      CL**2
00361      ELSE                                                            CL**2
00362          ADD 1 TO PV-SS-READ-CNT                                     CL**2
00363      END-IF.                                                         CL**2
00364                                                                      CL**2
00365  9000-END-ROUTINE.                                                   CL**2
00366 ***************************************************************** INKUT001
00367 * 9000-END-ROUTINE                                              *    CL**2
00368 ***************************************************************** INKUT001
00369                                                                   INKUT001
00370      IF PS-EMPTY-CENTRAL-STOCK                                       CL**7
00371        AND PS-EMPTY-STORE-TO-STORE                                   CL**7
00372          DISPLAY '*****   INKUT001    ************'                  CL**7
00373          DISPLAY ' CENTRAL STOCK INPUT FILE EMPTY '                  CL**2
00374          DISPLAY 'STORE TO STORE INPUT FILE EMPTY '                  CL**3
00375          DISPLAY '   NO RECORDS PROCESSED         '                  CL**4
00376          DISPLAY '********************************'                  CL**8
00377      END-IF.                                                         CL**2
00378                                                                      CL**4
00379      DISPLAY '*****   INKUT001  PROGRAM  COUNTERS    ********'.      CL**3
00380      DISPLAY '# OF CENTRAL STOCK RECS READ = ' PV-CS-READ-CNT.       CL**4
00381      DISPLAY '# OF CENTRAL STOCK RECS WRITTEN = '                    CL**8
00382                                       PV-CS-TOTAL-RECS-WRITTEN.      CL**8
00383      DISPLAY '# OF CENTRAL STOCK FROM RECS WRITTEN = '               CL**8
00384                                       PV-CS-FROM-RECS-WRITTEN.       CL**8
00385      DISPLAY '# OF CENTRAL STOCK TO RECS WRITTEN = '                 CL**8
00386                                       PV-CS-TO-RECS-WRITTEN.         CL**8
00387      DISPLAY '# OF STORE TO STORE RECS READ = ' PV-SS-READ-CNT.      CL**4
00388      DISPLAY '# OF STORE TO STORE RECS WRITTEN = '                   CL**2
00389                                       PV-SS-TOTAL-RECS-WRITTEN.      CL**2
00390      DISPLAY '# OF STORE TO STORE FROM RECS WRITTEN = '              CL**4
00391                                       PV-SS-FROM-RECS-WRITTEN.       CL**8
00392      DISPLAY '# OF STORE TO STORE TO RECS WRITTEN = '                CL**4
00393                                       PV-SS-TO-RECS-WRITTEN.         CL**8
00394      DISPLAY '***********************************************'.      CL*15
00395                                                                      CL**4
00396      CLOSE CENTRAL-STOCK-XFERS-IN                                    CL*10
00397            STORE-TO-STORE-XFER-IN                                    CL**7
00398            TRANSFER-ACCUM-FINONLY-OUT.                               CL**7
00399                                                                      CL**2
00400      EJECT                                                           CL**9
00401                                                                      CL**9
