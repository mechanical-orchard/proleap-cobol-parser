       IDENTIFICATION DIVISION.                                         00010005
       PROGRAM-ID.    PXKUT041.                                         00020005
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030005
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040005
       DATE-WRITTEN.  04/02/2007.                                       00050005
       DATE-COMPILED.                                                   00060005
      ******************************************************************00070005
      **PX MOS ONHAND RETRIEVAL                                         00080005
      ******************************************************************00090005
      **THIS PROGRAM WILL READ IN AN MOS ITEM FILE AND LOOKUP THE       00100005
      **     ONHAND FOR THE ITEMS AT THE STORES.                        00110005
      ******************************************************************00120005
      **DATE:07/09/2007 MADE BY:DAVID WELLER INITIAL PROGRAM WRITE      00130005
      ******************************************************************00140005
       ENVIRONMENT DIVISION.                                            00150005
       CONFIGURATION SECTION.                                           00160005
       SOURCE-COMPUTER.    IBM-3090.                                    00170005
       OBJECT-COMPUTER.    IBM-3090.                                    00180005
       INPUT-OUTPUT SECTION.                                            00190005
       FILE-CONTROL.                                                    00200005
                                                                        00210005
           SELECT INPUT-FILE                                            00220005
                  ASSIGN TO INP01.                                      00230005
           SELECT OUTPUT-FILE                                           00240005
                  ASSIGN TO OUT01.                                      00250005
                                                                        00260005
       DATA DIVISION.                                                   00270005
       FILE SECTION.                                                    00280005
                                                                        00290005
       FD  INPUT-FILE                                                   00300005
           RECORDING MODE IS F                                          00310005
           LABEL RECORDS OMITTED                                        00320005
           BLOCK CONTAINS 0 RECORDS                                     00330005
           DATA RECORD IS INPUT-RECORD.                                 00340005
       01  INPUT-RECORD                        PIC X(080).              00350005
                                                                        00360005
       FD  OUTPUT-FILE                                                  00370005
           RECORDING MODE IS F                                          00380005
           LABEL RECORDS OMITTED                                        00390005
           BLOCK CONTAINS 0 RECORDS                                     00400005
           DATA RECORD IS OUTPUT-RECORD.                                00410005
       01  OUTPUT-RECORD                       PIC X(80).               00420005
                                                                        00430005
       WORKING-STORAGE SECTION.                                         00440005
      ******************************************************************00450005
      **PROGRAM SWITCHES                                                00460005
      ******************************************************************00470005
       01  PS-SWITCH-AREAS.                                             00480005
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)  VALUE 'N'.    00490005
               88  PS-END-OF-INPUT-FILE                   VALUE 'Y'.    00500005
               88  PS-NOT-END-OF-INPUT-FILE               VALUE 'N'.    00510005
      ******************************************************************00520005
      **DATE TIME FIELDS                                                00530005
      ******************************************************************00540005
       01 DF-DATE-TIME-FIELDS.                                          00550005
          05 DF-ACCEPT-DATE                    PIC X(06) VALUE SPACES.  00560005
          05 DF-ACCEPT-TIME                    PIC X(08) VALUE SPACES.  00570005
      ******************************************************************00580005
      **PROGRAM COUNTERS                                                00590005
      ******************************************************************00600005
       01  PC-PROGRAM-COUNTERS.                                         00610005
           05  PC-INPUT-RECORDS-READ           PIC 9(09) VALUE ZEROES.  00620005
           05  PC-OUTPUT-RECORDS-WRITTEN       PIC 9(09) VALUE ZEROES.  00630005
      ****************************************************************  00640005
      **ABEND INFORMATION                                               00650005
      ****************************************************************  00660005
       01  AC-ABEND-FIELDS.                                             00670005
           05  AC-ABEND-CODE                   PIC S9(04) VALUE ZERO    00680005
                                                           COMP SYNC.   00690005
               88  AC-TABLE-ERROR                        VALUE +4004.   00700005
               88  AC-DB2-ERROR                          VALUE +4013.   00710005
       01  PV-MISC-ABEND-FIELDS.                                        00720005
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   00730005
           05  PV-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   00740005
           05  PV-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   00750005
           05  PV-DB2-OPERATION                PIC X(30) VALUE SPACE.   00760005
      ******************************************************************00770005
      **COPYBOOKS                                                       00780005
      ******************************************************************00790005
      ****INPUT RECORD LAYOUT                                           00800005
           COPY PXRD049.                                                00810005
      ****OUTPUT RECORD LAYOUT                                          00820005
           COPY PXRD050.                                                00830005
      ****ABEND ROUTINE INFORMATION                                     00840005
           COPY SQLCA2.                                                 00850005
      ****ABEND ROUTINE INFORMATION                                     00860005
           COPY DPWS004.                                                00870005
      ******************************************************************00880005
      **TABLE INCLUSIONS                                                00890005
      ******************************************************************00900005
      ***ONHAND TABLE                                                   00910005
           EXEC SQL                                                     00920005
                INCLUDE TSKST                                           00930005
           END-EXEC.                                                    00940005
      ***ITEM CROSS REFERENCE TABLE                                     00950005
           EXEC SQL                                                     00960005
                INCLUDE TSKXREF                                         00970005
           END-EXEC.                                                    00980005
      *****DB2 TABLE INCLUSION                                          00990005
           EXEC SQL                                                     01000005
                INCLUDE SQLCA                                           01010005
           END-EXEC.                                                    01020005
                                                                        01030005
       PROCEDURE DIVISION.                                              01040005
      ******************************************************************01050005
       000-MAINLINE-PROCESSING.                                         01060005
                                                                        01070005
           PERFORM 1000-INITIALIZATION.                                 01080005
           PERFORM 2000-PROCESS-RECORDS                                 01090005
                   UNTIL PS-END-OF-INPUT-FILE.                          01100005
           PERFORM 9999-WRAPUP.                                         01110005
           GOBACK.                                                      01120005
                                                                        01130005
       EJECT.                                                           01140005
      ******************************************************************01150005
      **INITIALIZATION ROUTINE                                          01160005
      ******************************************************************01170005
       1000-INITIALIZATION.                                             01180005
                                                                        01190005
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01200005
                                                                        01210005
           OPEN INPUT  INPUT-FILE                                       01220005
                OUTPUT OUTPUT-FILE.                                     01230005
                                                                        01240005
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01250005
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01260005
                                                                        01270005
           PERFORM 9999-READ-INPUT-RECORD.                              01280005
                                                                        01290005
       EJECT.                                                           01300005
      ******************************************************************01310005
      **PROCESS INPUT RECORD TO CHECK ROWS                              01320005
      ******************************************************************01330005
       2000-PROCESS-RECORDS.                                            01340005
                                                                        01350005
           MOVE '2000-PROCESS-RECORDS'   TO PV-PARAGRAPH-NAME.          01360005
                                                                        01370005
           PERFORM 2100-DATA-LOOKUP.                                    01380005
                                                                        01390005
           MOVE ZEROES                 TO PXRD050-PRCHG-ID.             01400005
           MOVE SPACES                 TO PXRD050-EVNT-CDE.             01410005
           MOVE PXRD049-EVNT-DTE       TO PXRD050-EVNT-DTE.             01420005
           MOVE PXRD049-STR-NBR        TO PXRD050-STR-NBR.              01430005
           MOVE PXRD049-SKU-NBR        TO PXRD050-SKU-NBR.              01440005
           MOVE PXRD049-NEW-UNT-RTL-AMT                                 01450005
                                       TO PXRD050-NEW-UNT-RTL-AMT.      01460005
           MOVE PXRD049-NW-GP-RTL-QTY  TO PXRD050-NW-GP-RTL-QTY.        01470005
           MOVE PXRD049-NW-GP-RTL-AMT  TO PXRD050-NW-GP-RTL-AMT.        01480005
           MOVE PXRD049-GP-RTL-QTY-NULL-IND                             01490005
                                       TO PXRD050-GP-RTL-QTY-NULL-IND.  01500005
           MOVE PXRD049-GP-RTL-AMT-NULL-IND                             01510005
                                       TO PXRD050-GP-RTL-AMT-NULL-IND.  01520005
           MOVE 'MOS'                  TO PXRD050-EVNT-CTG-CDE.         01530005
           MOVE 'MOS'                  TO PXRD050-PRCHG-TYP-CDE.        01540005
           MOVE TSKST-ONHAND-QTY       TO PXRD050-STR-EXPTED-QTY.       01550005
                                                                        01560005
      * DON'T WRITE OUT QUANTITIES OF ZERO OR LESS                      01570005
           IF PXRD050-STR-EXPTED-QTY > 0                                01580005
               PERFORM 9999-WRITE-OUTPUT-RECORD                         01590005
           END-IF.                                                      01600005
           PERFORM 9999-READ-INPUT-RECORD.                              01610005
                                                                        01620005
       EJECT.                                                           01630005
      ******************************************************************01640005
      **LOOKUP ONHAND QUANTITY FOR STORE                                01650005
      ******************************************************************01660005
       2100-DATA-LOOKUP.                                                01670005
                                                                        01680005
           MOVE '2100-DATA-LOOKUP'    TO PV-PARAGRAPH-NAME.             01690005
           MOVE PXRD049-SKU-NBR       TO SKXREF-SKU.                    01700005
           MOVE PXRD049-STR-NBR       TO TSKST-LOC-NBR.                 01710005
                                                                        01720005
           EXEC SQL                                                     01730005
                SELECT ONHAND_QTY                                       01740005
                INTO  :TSKST-ONHAND-QTY                                 01750005
                FROM   TSKST   A                                        01760005
                      ,TSKXREF B                                        01770005
                WHERE  A.ITEM_NMBR   = B.ITM_NBR                        01780005
                  AND  B.SKU         = :SKXREF-SKU                      01790005
                  AND  A.LOC_NBR     = :TSKST-LOC-NBR                   01800005
                WITH UR                                                 01810005
           END-EXEC.                                                    01820005
                                                                        01830005
           EVALUATE TRUE                                                01840005
              WHEN SQLCODE = +0                                         01850005
                   CONTINUE                                             01860005
              WHEN SQLCODE = +100                                       01870005
                   MOVE 0                TO TSKST-ONHAND-QTY            01880005
              WHEN OTHER                                                01890005
                   DISPLAY 'ERROR ON DATA LOOKUP....'                   01900005
                   DISPLAY 'SKU_NBR     ' SKXREF-SKU                    01910005
                   DISPLAY 'LOC-NBR     ' TSKST-LOC-NBR                 01920005
                   PERFORM 9999-DISPLAY-STATISTICS                      01930005
                   PERFORM 9999-SQL-ERROR                               01940005
           END-EVALUATE.                                                01950005
                                                                        01960005
       EJECT.                                                           01970005
      ******************************************************************01980005
      **READ INPUT RECORD                                               01990005
      ******************************************************************02000005
       9999-READ-INPUT-RECORD.                                          02010005
                                                                        02020005
           READ INPUT-FILE INTO PXRD049-DATA-RECORD                     02030005
                AT END                                                  02040005
                SET PS-END-OF-INPUT-FILE TO TRUE                        02050005
           END-READ.                                                    02060005
                                                                        02070005
           IF  PS-NOT-END-OF-INPUT-FILE                                 02080005
               ADD +1                      TO PC-INPUT-RECORDS-READ     02090005
           END-IF.                                                      02100005
                                                                        02110005
       EJECT.                                                           02120005
      ****************************************************************  02130005
      **WRITE OUTPUT RECORD                                             02140005
      ****************************************************************  02150005
       9999-WRITE-OUTPUT-RECORD.                                        02160005
                                                                        02170005
           WRITE OUTPUT-RECORD FROM PXRD050-DATA-RECORD.                02180005
                                                                        02190005
           ADD +1              TO PC-OUTPUT-RECORDS-WRITTEN.            02200005
                                                                        02210005
       EJECT.                                                           02220005
      ******************************************************************02230005
      **CLOSE INPUT FILES.                                              02240005
      ******************************************************************02250005
       9999-WRAPUP.                                                     02260005
                                                                        02270005
           CLOSE INPUT-FILE                                             02280005
                 OUTPUT-FILE.                                           02290005
                                                                        02300005
           PERFORM 9999-DISPLAY-STATISTICS.                             02310005
                                                                        02320005
       EJECT.                                                           02330005
      ******************************************************************02340005
      **DISPLAY PROGRAM STATS                                           02350005
      ******************************************************************02360005
       9999-DISPLAY-STATISTICS.                                         02370005
                                                                        02380005
           DISPLAY '*************************************************'. 02390005
           DISPLAY '* PROGRAM...........: PXKUT041'.                    02400005
           DISPLAY '* DATE..............: ' DF-ACCEPT-DATE.             02410005
           DISPLAY '* TIME..............: ' DF-ACCEPT-TIME.             02420005
           DISPLAY '* RECORDS READ......: ' PC-INPUT-RECORDS-READ.      02430005
           DISPLAY '* OUTPUT RECORDS WRITTEN...: '                      02440005
                                             PC-OUTPUT-RECORDS-WRITTEN. 02450005
           DISPLAY '*************************************************'. 02460005
                                                                        02470005
       EJECT.                                                           02480005
      ******************************************************************02490005
      * ABEND ROUTINE FOR BAD SQL CALLS                                *02500005
      ******************************************************************02510005
       9999-SQL-ERROR.                                                  02520005
                                                                        02530005
           DISPLAY '** ABEND     **'.                                   02540005
           DISPLAY '** PROGRAM   ** = PXKUT041'.                        02550005
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              02560005
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               02570005
      ***************************************************************** 02580005
      *    COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED  02590005
      *    TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.   02600005
      ***************************************************************** 02610005
           COPY DPPD004.                                                02620005
           SET AC-DB2-ERROR TO TRUE.                                    02630005
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         02640005
                                                                        02650005
       EJECT.                                                           02660005
