00001 *                                                                 02/05/92
00002 *                                                                 MBKCS112
00003 *                                                                    LV015
00004  IDENTIFICATION DIVISION.                                         MBKCS112
00005                                                                   MBKCS112
00006  PROGRAM-ID.    MBKCS112.                                            CL207
00007  AUTHOR.        BOB LOOS - STRATAGEM.                                CL*13
00008  INSTALLATION.  KOHLS DEPARTMENT STORES.                          MBKCS112
00009  DATE-WRITTEN.  06-26-91.                                            CL*13
00010  DATE-COMPILED.                                                   MBKCS112
00011                                                                   MBKCS112
00012 ***************************************************************** MBKCS112
00013 *    LIST PROCESSOR MERCHANDISE DEPARTMENT                           CL183
00014 *                                                                 MBKCS112
00015 *                                                                 MBKCS112
00016 ***************************************************************** MBKCS112
262978* CHANGED 10/22/2021    BY JIM HUNTER      WR: CHG0262978                 
262978* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
262978* MAKING THE CALL DYNAMIC VS STATIC.                                      
262978******************************************************************        
00017                                                                   MBKCS112
00018 *                                                                 MBKCS112
00019 *                                                                 MBKCS112
00020 *                                                                 MBKCS112
00021  ENVIRONMENT DIVISION.                                            MBKCS112
00022 *                                                                 MBKCS112
00023 *                                                                 MBKCS112
00024 *                                                                 MBKCS112
00025  DATA DIVISION.                                                   MBKCS112
00026 *                                                                 MBKCS112
00027 *                                                                 MBKCS112
00028  WORKING-STORAGE SECTION.                                         MBKCS112
00029  01  AM-ABEND-MESSAGES.                                              CL*57
00030      05  AM-ABEND-MESSAGE-8             PIC X(78)    VALUE           CL*57
00031      'ERROR TRYING TO DELETE TEMP STORAGE QUEUE'.                    CL*57
00032      05  AM-ABEND-MESSAGE-9             PIC X(78)    VALUE           CL*57
00033      'ERROR TRYING TO REWRITE TEMP STORAGE QUEUE'.                   CL*57
00034      05  AM-ABEND-MESSAGE-10            PIC X(78)    VALUE           CL*57
00035      'ERROR TRYING TO WRITE TEMP STORAGE QUEUE'.                     CL*57
00036      05  AM-ABEND-MESSAGE-11            PIC X(78)    VALUE           CL*57
00037      'LOGIC ERROR IN TRYING TO WRITE TO TEMP STORAGE QUEUE - BLOCK   CL*57
00038 -    '      '.                                                       CL*57
00039      05  FILLER REDEFINES AM-ABEND-MESSAGE-11.                       CL*57
00040          10  FILLER                     PIC X(61).                   CL*57
00041          10  AM-BLOCK-NUMBER-11         PIC Z(03)9-.                 CL*57
00042          10  FILLER                     PIC X(12).                   CL*57
00043      05  AM-ABEND-MESSAGE-12            PIC X(78)    VALUE           CL*57
00044      'ERROR TRYING TO READ TEMP STORAGE QUEUE'.                      CL*57
00045      05  AM-ABEND-MESSAGE-13            PIC X(78)    VALUE           CL*58
00046      'ERROR TRYING TO SELECT A RECORD FROM TMDSEAR TABLE - KEY       CL*58
00047 -    '      '.                                                       CL*58
00048      05  FILLER REDEFINES AM-ABEND-MESSAGE-13.                       CL*58
00049          10  FILLER                     PIC X(57).                   CL*58
00050          10  AM-OPERCO-NBR-13           PIC X(02).                   CL*58
00051          10  AM-GMA-NBR-13              PIC X(02).                   CL*58
00052          10  AM-DMA-NBR-13              PIC X(02).                   CL*58
00053          10  AM-DEPT-NBR-13             PIC X(03).                   CL*58
00054          10  FILLER                     PIC X(12).                   CL188
00055      05  AM-ABEND-MESSAGE-21            PIC X(78)    VALUE           CL*58
00056      'ERROR TRYING TO OPEN MERCH-CURSOR'.                            CL*58
00057      05  AM-ABEND-MESSAGE-22            PIC X(78)    VALUE           CL*58
00058      'ERROR TRYING TO CLOSE MERCH-CURSOR'.                           CL*58
00059  01  PC-PROGRAM-CONSTANTS.                                        MBKCS112
00060      05  PC-NAVIGATOR-TRAN-ID           PIC X(04)    VALUE 'Z008'.   CL**5
00061      05  PC-SELECTED                    PIC X(01)    VALUE 'S'.      CL*50
00062      05  PC-MAXIMUM-COMMAREA-LENGTH     PIC S9(04)   VALUE +4072  MBKCS112
00063                                                      COMP SYNC.   MBKCS112
00064      05  PC-CURRENT-MAP-NAME            PIC X(08)    VALUE        MBKCS112
00065      'MB112A  '.                                                     CL207
00066      05  PC-CURRENT-MAPSET-NAME         PIC X(08)    VALUE        MBKCS112
00067      'MBKM112 '.                                                     CL207
00068      05  PC-CURRENT-PROGRAM-NAME        PIC X(08)    VALUE        MBKCS112
00069      'MBKCS112'.                                                     CL207
00070 ***************************************************************** MBKCS112
00071 *    PC-ITEMS-PER-PANEL = MAXIMUM NUMBER OF ITEMS ON THE SCREEN   MBKCS112
00072 ***************************************************************** MBKCS112
00073      05  PC-ITEMS-PER-PANEL             PIC S9(04)   VALUE +14       CL*14
00074                                                      COMP SYNC.   MBKCS112
00075 ***************************************************************** MBKCS112
00076 *    PC-ITEMS-PER-BLOCK = MAXIMUM NUMBER OF ITEMS IN A BLOCK      MBKCS112
00077 ***************************************************************** MBKCS112
00078      05  PC-ITEMS-PER-BLOCK             PIC S9(04)   VALUE +18       CL*14
00079                                                      COMP SYNC.   MBKCS112
00080 ***************************************************************** MBKCS112
00081 *    PC-MAX-ITEMS-TO-BE-READ = THE MAXIMUM NUMBER OF ITEMS TO     MBKCS112
00082 *    BE READ FROM THE DATABASE AT ONE TIME.  USED WHEN THE USER   MBKCS112
00083 *    CHANGES THE BEGINNING LIST NUMBER.                              CL**4
00084 ***************************************************************** MBKCS112
00085      05  PC-MAX-ITEMS-TO-BE-READ        PIC S9(04)   VALUE +100      CL*15
00086                                                      COMP SYNC.   MBKCS112
00087      05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)   VALUE +2     MBKCS112
00088                                                      COMP SYNC.   MBKCS112
00089 ***************************************************************** MBKCS112
00090 *    PC-MAX-ITEMS = MAXIMUM NUMBER OF ITEMS THAT CAN APPEAR ON    MBKCS112
00091 *    THE LIST.                                                    MBKCS112
00092 ***************************************************************** MBKCS112
00093      05  PC-MAX-ITEMS                   PIC S9(09)   VALUE        MBKCS112
00094                                                      +1000000        CL*14
00095                                                      COMP-3.      MBKCS112
00096 ***************************************************************** MBKCS112
00097 *    PC-BLOCK-LENGTH = THE LENGTH OF 1 BLOCK WORTH OF DATA.       MBKCS112
00098 ***************************************************************** MBKCS112
00099      05  PC-BLOCK-LENGTH                PIC S9(04)   VALUE +811      CL195
00100                                                      COMP SYNC.   MBKCS112
00101 ***************************************************************** MBKCS112
00102 *    PC-FIELD-LENGTH = THE LENGTH OF THE BEGINNING LIST NUMBER.      CL**4
00103 ***************************************************************** MBKCS112
00104      05  PC-FIELD-LENGTH                PIC S9(04)   VALUE +7        CL*49
00105                                                      COMP SYNC.   MBKCS112
00106      05  PC-ERASE-EOF-COMP              PIC S9(04)   VALUE +128   MBKCS112
00107                                                      COMP SYNC.   MBKCS112
00108      05  FILLER REDEFINES PC-ERASE-EOF-COMP.                      MBKCS112
00109          10  FILLER                     PIC X(01).                MBKCS112
00110          10  PC-ERASE-EOF               PIC X(01).                MBKCS112
00111                                                                   MBKCS112
00112      05  PC-NO-END-OF-RANGE             PIC 9(02)    VALUE 51.       CL*51
00113      05  PC-END-BEFORE-START            PIC 9(02)    VALUE 52.       CL*51
00114      05  PC-NO-START-OF-RANGE           PIC 9(02)    VALUE 53.       CL*51
00115      05  PC-MULTIPLE-START-RANGES       PIC 9(02)    VALUE 54.       CL*51
00116      05  PC-MULTIPLE-END-RANGES         PIC 9(02)    VALUE 55.       CL*51
00117      05  PC-MAX-RETRYS                  PIC S9(04)   VALUE +3        CL*50
00118                                                      COMP SYNC.      CL*50
00119      05  PC-MAX-QUEUE-ITEMS             PIC S9(03)   VALUE +100      CL243
00120                                                      COMP-3.         CL243
00121      05  PC-OPERCO-NBR                  PIC X(02)    VALUE '03'.     CL*50
00122      05  PC-DPT-LITERAL                 PIC X(03)    VALUE 'DPT'.    CL*37
00123      05  PC-TSYMSG-NUMBERS.                                          CL179
00124          10  PC-TSYMSG-00005            PIC 9(05)    VALUE 00005.    CL179
00125          10  PC-TSYMSG-00008            PIC 9(05)    VALUE 00008.    CL179
00126          10  PC-TSYMSG-00030            PIC 9(05)    VALUE 00030.    CL181
00127          10  PC-TSYMSG-00032            PIC 9(05)    VALUE 00032.    CL181
00128          10  PC-TSYMSG-00045            PIC 9(05)    VALUE 00045.    CL243
00129          10  PC-TSYMSG-00050            PIC 9(05)    VALUE 00050.    CL243
00130          10  PC-TSYMSG-00051            PIC 9(05)    VALUE 00051.    CL179
00131          10  PC-TSYMSG-00052            PIC 9(05)    VALUE 00052.    CL179
00132          10  PC-TSYMSG-00053            PIC 9(05)    VALUE 00053.    CL179
00133          10  PC-TSYMSG-00054            PIC 9(05)    VALUE 00054.    CL179
00134          10  PC-TSYMSG-00055            PIC 9(05)    VALUE 00055.    CL179
00135          10  PC-TSYMSG-00056            PIC 9(05)    VALUE 00056.    CL179
00136          10  PC-TSYMSG-00057            PIC 9(05)    VALUE 00057.    CL254
00137          10  PC-TSYMSG-00060            PIC 9(05)    VALUE 00060.    CL179
00138          10  PC-TSYMSG-00069            PIC 9(05)    VALUE 00069.    CL179
00139          10  PC-TSYMSG-00070            PIC 9(05)    VALUE 00070.    CL179
00140          10  PC-TSYMSG-00076            PIC 9(05)    VALUE 00076.    CL180
00141          10  PC-TSYMSG-00077            PIC 9(05)    VALUE 00077.    CL180
00142          10  PC-TSYMSG-00078            PIC 9(05)    VALUE 00078.    CL180
00143          10  PC-TSYMSG-00079            PIC 9(05)    VALUE 00079.    CL180
00144          10  PC-TSYMSG-00081            PIC 9(05)    VALUE 00081.    CL180
00145          10  PC-TSYMSG-00146            PIC 9(05)    VALUE 00146.    CL245
00146          10  PC-TSYMSG-00207            PIC 9(05)    VALUE 00207.    CL*10
00147          10  PC-TSYMSG-99901            PIC 9(05)    VALUE 99901.    CL251
00148                                                                      CL*36
00149  01  PC-PROGRAM-COUNTERS.                                            CL*50
00150      05  PC-RETRY-CNTR                  PIC S9(04)   VALUE +0        CL*50
00151                                                      COMP SYNC.      CL*50
00152  01  PC-CURRENT-DATE                    PIC X(10)    VALUE SPACES.   CL189
00153                                                                      CL*50
00154  01  PM-PROGRAM-MESSAGES.                                         MBKCS112
00155      05  INVALID-NEXT-ACT-MSG           PIC X(78)    VALUE        MBKCS112
00156      'UNRECOGNIZED ACTION ENCOUNTERED BY THIS PROGRAM'.           MBKCS112
00157                                                                   MBKCS112
00158  01  PS-PROGRAM-SUBSCRIPTS.                                          CL243
00159      05  PS-SUB                         PIC S9(04)   VALUE +0        CL*50
00160                                                      COMP SYNC.      CL*50
00161      05  PS-ITEM-SUB                    PIC S9(09)   VALUE +0        CL*52
00162                                                      COMP-3.         CL*50
00163                                                                      CL**9
00164  01  PS-PROGRAM-SWITCHES.                                            CL**9
00165      05  PS-MAP-SEND-TYPE               PIC X(01)    VALUE '1'.   MBKCS112
00166          88  PS-SEND-MAP-N-DATA-FRSET                VALUE '1'.   MBKCS112
00167          88  PS-SEND-MAP-N-DATA-FRSET-CPOSN          VALUE '2'.   MBKCS112
00168          88  PS-SEND-DATA-ONLY-FRSET                 VALUE '3'.   MBKCS112
00169          88  PS-SEND-DATA-ONLY                       VALUE '4'.   MBKCS112
00170          88  PS-SEND-DATA-ONLY-ERASEAUP              VALUE '5'.   MBKCS112
00171          88  PS-SEND-MAP-ONLY-FRSET                  VALUE '6'.   MBKCS112
00172      05  PS-CONTINUE-SW                 PIC X(01)    VALUE 'N'.      CL241
00173          88  PS-CONTINUE-ON                          VALUE 'Y'.      CL241
00174          88  PS-DO-NOT-CONTINUE-ON                   VALUE 'N'.      CL241
00175      05  PS-INTER-APPL-SW               PIC X(01)    VALUE 'N'.      CL251
00176          88  PS-INTER-APPL-VALID                     VALUE 'Y'.      CL251
00177          88  PS-INTER-APPL-INVALID                   VALUE 'N'.      CL251
00178      05  PS-ERROR-SW                    PIC X(01)    VALUE 'N'.   MBKCS112
00179          88  PS-ERROR                                VALUE 'Y'.   MBKCS112
00180          88  PS-NO-ERROR                             VALUE 'N'.   MBKCS112
00181      05  PS-TOP-ITEM-IN-ARRAY-SW        PIC X(01)    VALUE 'N'.      CL**8
00182          88  PS-TOP-ITEM-IN-ARRAY                    VALUE 'Y'.      CL**8
00183          88  PS-TOP-ITEM-NOT-IN-ARRAY                VALUE 'N'.      CL**8
00184      05  PS-BOTTOM-ITEM-IN-ARRAY-SW     PIC X(01)    VALUE 'N'.      CL**8
00185          88  PS-BOTTOM-ITEM-IN-ARRAY                 VALUE 'Y'.      CL**8
00186          88  PS-BOTTOM-ITEM-NOT-IN-ARRAY             VALUE 'N'.      CL**8
00187      05  PS-TOP-ITEM-IN-TEMP-SW         PIC X(01)    VALUE 'N'.      CL**8
00188          88  PS-TOP-ITEM-IN-TEMP                     VALUE 'Y'.      CL**8
00189          88  PS-TOP-ITEM-NOT-IN-TEMP                 VALUE 'N'.      CL**8
00190      05  PS-BOTTOM-ITEM-IN-TEMP-SW      PIC X(01)    VALUE 'N'.      CL**8
00191          88  PS-BOTTOM-ITEM-IN-TEMP                  VALUE 'Y'.      CL**8
00192          88  PS-BOTTOM-ITEM-NOT-IN-TEMP              VALUE 'N'.      CL**8
00193      05  PS-LAST-BLOCK-IN-ARRAY-SW      PIC X(01)    VALUE 'N'.      CL**8
00194          88  PS-LAST-BLOCK-IN-ARRAY                  VALUE 'Y'.      CL**8
00195          88  PS-LAST-BLOCK-NOT-IN-ARRAY              VALUE 'N'.      CL**8
00196      05  PS-VALID-SELECTIONS-SW         PIC X(01)    VALUE 'N'.      CL**4
00197          88  PS-VALID-SELECTIONS                     VALUE 'Y'.      CL**4
00198          88  PS-INVALID-SELECTIONS                   VALUE 'N'.      CL**4
00199      05  PS-VALID-BEG-ITEM-SW           PIC X(01)    VALUE 'N'.      CL**4
00200          88  PS-VALID-BEG-ITEM                       VALUE 'Y'.      CL**4
00201          88  PS-INVALID-BEG-ITEM                     VALUE 'N'.      CL**4
00202      05  PS-DB2-RETRY-SW                PIC X(01)    VALUE 'N'.      CL*51
00203          88  PS-DB2-OK                               VALUE 'Y'.      CL*51
00204          88  PS-DB2-RETRY                            VALUE 'N'.      CL*51
00205      05  PS-CURSOR-POSITION-SW          PIC X(01)    VALUE 'N'.      CL*51
00206          88  PS-CURSOR-POSITIONED                    VALUE 'Y'.      CL*51
00207          88  PS-CURSOR-NOT-POSITIONED                VALUE 'N'.      CL*51
00208      05  PS-WARNING-SW                  PIC X(01)    VALUE 'N'.      CL*51
00209          88  PS-WARNING                              VALUE 'Y'.      CL*51
00210          88  PS-NO-WARNING                           VALUE 'N'.      CL*51
00211      05  PS-INFORMATIONAL-SW            PIC X(01)    VALUE 'N'.      CL*53
00212          88  PS-INFORMATIONAL                        VALUE 'Y'.      CL*53
00213          88  PS-NOT-INFORMATIONAL                    VALUE 'N'.      CL*53
00214      05  PS-SAME-ITEMS-DISPLAYED-SW     PIC X(01)    VALUE 'N'.      CL*53
00215          88  PS-SAME-ITEMS-DISPLAYED                 VALUE 'Y'.      CL*51
00216          88  PS-DIFFERENT-ITEMS-DISPLAYED            VALUE 'N'.      CL*56
00217      05  PS-SELECTION-FOUND-SW          PIC X(01)    VALUE 'N'.      CL*56
00218          88  PS-SELECTION-FOUND                      VALUE 'Y'.      CL*58
00219          88  PS-NO-SELECTION-FOUND                   VALUE 'N'.      CL*56
00220      05  PS-MERCH-CURSOR-OPENED-SW      PIC X(01)    VALUE 'N'.      CL*84
00221          88  PS-MERCH-CURSOR-NOT-OPENED              VALUE 'N'.      CL*85
00222          88  PS-MERCH-CURSOR-OPENED                  VALUE 'Y'.      CL*84
00223      05  PS-GMA-CURSOR-OPENED-SW        PIC X(01)    VALUE 'N'.      CL*98
00224          88  PS-GMA-CURSOR-NOT-OPENED                VALUE 'N'.      CL*98
00225          88  PS-GMA-CURSOR-OPENED                    VALUE 'Y'.      CL*98
00226      05  PS-DMA-CURSOR-OPENED-SW        PIC X(01)    VALUE 'N'.      CL100
00227          88  PS-DMA-CURSOR-NOT-OPENED                VALUE 'N'.      CL100
00228          88  PS-DMA-CURSOR-OPENED                    VALUE 'Y'.      CL100
00229      05  PS-DPT-CURSOR-OPENED-SW        PIC X(01)    VALUE 'N'.      CL100
00230          88  PS-DPT-CURSOR-NOT-OPENED                VALUE 'N'.      CL100
00231          88  PS-DPT-CURSOR-OPENED                    VALUE 'Y'.      CL100
00232      05  PS-GMA-ITEM-SW                 PIC X(01)    VALUE 'N'.      CL201
00233          88  PS-DO-NOT-HAVE-GMA-ITEM                 VALUE 'N'.      CL181
00234          88  PS-HAVE-GMA-ITEM                        VALUE 'Y'.      CL181
00235      05  PS-DMA-ITEM-SW                 PIC X(01)    VALUE 'N'.      CL201
00236          88  PS-DO-NOT-HAVE-DMA-ITEM                 VALUE 'N'.      CL181
00237          88  PS-HAVE-DMA-ITEM                        VALUE 'Y'.      CL181
00238      05  PS-DPT-ITEM-SW                 PIC X(01)    VALUE 'N'.      CL129
00239          88  PS-DO-NOT-HAVE-DPT-ITEM                 VALUE 'N'.      CL129
00240          88  PS-HAVE-DPT-ITEM                        VALUE 'Y'.      CL129
00241      05  PS-SELECTION-SW                PIC X(01)    VALUE 'N'.      CL157
00242          88  PS-NO-SELECTIONS-FOUND                  VALUE 'N'.      CL157
00243          88  PS-SELECTIONS-FOUND                     VALUE 'Y'.      CL157
00244      05  PS-DESELECTION-SW              PIC X(01)    VALUE 'N'.      CL242
00245          88  END-OF-ITEMS                            VALUE 'N'.      CL242
00246          88  NOT-END-OF-ITEMS                        VALUE 'Y'.      CL242
00247      05  PS-HISTORY-SW                  PIC X(01)    VALUE 'N'.      CL244
00248          88  PS-NO-SELECTED-HISTORY                  VALUE 'N'.      CL244
00249          88  PS-SELECTED-HISTORY                     VALUE 'Y'.      CL244
00250                                                                   MBKCS112
00251  01  PV-PROGRAM-VARIABLES.                                        MBKCS112
00252      05  PV-CICS-RESPONSE               PIC S9(04)   VALUE +0     MBKCS112
00253                                                      COMP SYNC.   MBKCS112
00254      05  PV-RESP                        PIC S9(04)   VALUE +0        CL243
00255                                                      COMP SYNC.      CL242
00256      05  PV-ITEM                        PIC S9(04)   VALUE +0        CL243
00257                                                      COMP SYNC.      CL243
00258      05  PV-MA-ITEM                     PIC S9(04)   VALUE +0        CL242
00259                                                      COMP SYNC.      CL242
00260      05  PV-SAVE-ITEM                   PIC S9(04)   VALUE +0        CL242
00261                                                      COMP SYNC.      CL242
00262      05  PV-HIGHEST-ITEM-TO-BE-READ     PIC S9(09)   VALUE +0     MBKCS112
00263                                                      COMP-3.      MBKCS112
00264      05  PV-ENDING-ITEM-ON-PANEL        PIC S9(09)   VALUE +0     MBKCS112
00265                                                      COMP-3.      MBKCS112
00266      05  PV-ITEM-IN-USE                 PIC S9(09)   VALUE +0     MBKCS112
00267                                                      COMP-3.      MBKCS112
00268      05  PV-TOP-ITEM                    PIC S9(09)   VALUE +0     MBKCS112
00269                                                      COMP-3.      MBKCS112
00270      05  PV-BOTTOM-ITEM                 PIC S9(09)   VALUE +0     MBKCS112
00271                                                      COMP-3.      MBKCS112
00272      05  PV-MAX-ITEMS-TO-BE-READ        PIC S9(09)   VALUE +0     MBKCS112
00273                                                      COMP-3.      MBKCS112
00274      05  PV-FIRST-ITEM-IN-ARRAY         PIC S9(09)   VALUE +0     MBKCS112
00275                                                      COMP-3.      MBKCS112
00276      05  PV-LAST-ITEM-IN-ARRAY          PIC S9(09)   VALUE +0     MBKCS112
00277                                                      COMP-3.      MBKCS112
00278      05  PV-LAST-ITEM-IN-TEMP           PIC S9(09)   VALUE +0     MBKCS112
00279                                                      COMP-3.      MBKCS112
00280      05  PV-BEGINNING-ITEM              PIC S9(09)   VALUE +0     MBKCS112
00281                                                      COMP-3.      MBKCS112
00282      05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)   VALUE +0     MBKCS112
00283                                                      COMP-3.      MBKCS112
00284      05  PV-BLOCK-NUMBER                PIC S9(04)   VALUE +0     MBKCS112
00285                                                      COMP SYNC.   MBKCS112
00286      05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)   VALUE +0     MBKCS112
00287                                                      COMP SYNC.   MBKCS112
00288      05  PV-LIST-ITEM-NUMBER            PIC S9(04)   VALUE +0     MBKCS112
00289                                                      COMP SYNC.   MBKCS112
00290      05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)   VALUE +0     MBKCS112
00291                                                      COMP SYNC.   MBKCS112
00292      05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)   VALUE +0     MBKCS112
00293                                                      COMP SYNC.   MBKCS112
00294      05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)   VALUE +0     MBKCS112
00295                                                      COMP SYNC.   MBKCS112
00296      05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)   VALUE +0     MBKCS112
00297                                                      COMP SYNC.   MBKCS112
00298      05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)   VALUE +0     MBKCS112
00299                                                      COMP SYNC.   MBKCS112
00300      05  PV-START-OF-SELECTED-RANGE     PIC S9(04)   VALUE +0     MBKCS112
00301                                                      COMP SYNC.   MBKCS112
00302      05  PV-END-OF-SELECTED-RANGE       PIC S9(04)   VALUE +0     MBKCS112
00303                                                      COMP SYNC.   MBKCS112
00304      05  PV-MESSAGE                     PIC X(78)    VALUE SPACES.   CL*74
00305      05  PV-DISPLAY-7                   PIC ZZZZZZ9.                 CL178
00306      05  PV-DB2-DATE.                                                CL*66
00307          10  PV-DB2-CENTURY             PIC X(02)    VALUE SPACES.   CL*66
00308          10  PV-DB2-YEAR                PIC X(02)    VALUE SPACES.   CL*66
00309          10  PV-DB2-DASH-1              PIC X(01)    VALUE SPACES.   CL*66
00310          10  PV-DB2-MONTH               PIC X(02)    VALUE SPACES.   CL*67
00311          10  PV-DB2-DASH-2              PIC X(01)    VALUE SPACES.   CL*66
00312          10  PV-DB2-DAY                 PIC X(02)    VALUE SPACES.   CL*66
00313                                                                      CL*66
00314      05  PV-PANEL-DATE.                                              CL*67
00315          10  PV-PANEL-MONTH             PIC X(02)    VALUE SPACES.   CL*66
00316          10  PV-PANEL-SLASH-1           PIC X(01)    VALUE '/'.      CL*66
00317          10  PV-PANEL-DAY               PIC X(02)    VALUE SPACES.   CL*66
00318          10  PV-PANEL-SLASH-2           PIC X(01)    VALUE '/'.      CL*66
00319          10  PV-PANEL-YEAR              PIC X(02)    VALUE SPACES.   CL*66
00320                                                                      CL*66
00321      05  PV-MSG-NUMBER-X                PIC X(05)    VALUE SPACES.   CL**7
00322      05  PV-GMA-CODE                    PIC X(03)    VALUE SPACES.   CL*36
00323      05  PV-DMA-CODE                    PIC X(03)    VALUE SPACES.   CL*36
00324      05  PV-DPT-CODE                    PIC X(03)    VALUE SPACES.   CL*36
00325      05  PV-GMA-NBR                     PIC X(02)    VALUE SPACES.   CL*81
00326      05  PV-DMA-NBR                     PIC X(02)    VALUE SPACES.   CL*81
00327      05  PV-DPT-NBR                     PIC X(03)    VALUE SPACES.   CL*81
00328 *                                                                    CL243
00329      05  PV-QUEUE-NAME.                                              CL243
00330          10  PV-TERM-ID                 PIC X(4).                    CL243
00331          10  PV-TASK-NUMBER             PIC S9(9)    COMP-3.         CL243
00332 *                                                                    CL243
00333  01  PV-TEMP-WORK-AREA.                                              CL243
00334      05  PV-TW-BLOCK-MODIFIED-SW      PIC X(01).                     CL243
00335      05  PV-TW-ITEM-ARRAY.                                           CL243
00336          10  PV-TW-ITEM-ENTRIES OCCURS 18 TIMES                      CL245
00337              INDEXED BY PV-TW-IDX.                                   CL245
00338              15  PV-TW-LIST-ITEM-NUMBER                              CL243
00339                                                       PIC S9(09)     CL243
00340                                                           COMP-3.    CL243
00341              15  PV-TW-SELECTED-SW                    PIC X(01).     CL243
00342                  88  PV-TW-DESELECT                   VALUE ' '.     CL243
00343                  88  PV-TW-SELECT                     VALUE 'S'.     CL243
00344            13    PV-TW-KEY-DATA.                                     CL243
00345              15  PV-TW-ITEM-GMA                       PIC X(02).     CL243
00346              15  PV-TW-ITEM-DMA                       PIC X(02).     CL243
00347              15  PV-TW-ITEM-DEPT                      PIC X(03).     CL243
00348            13    PV-TW-KEY-DATA-END.                                 CL243
00349              15  PV-TW-ITEM-DESC                      PIC X(25).     CL243
00350              15  PV-TW-ITEM-ACTION                    PIC X(07).     CL243
00351              15  PV-TW-COLOR                          PIC X(01).     CL243
00352 *                                                                    CL243
00353  EJECT                                                            MBKCS112
00354 ***************************************************************** MBKCS112
00355 *    STANDARD COMMAREA.                                           MBKCS112
00356 ***************************************************************** MBKCS112
00357                                                                   MBKCS112
00358      COPY DPWS020.                                                   CL*15
00359      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                MBKCS112
00360                                                                   MBKCS112
00361 ***************************************************************** MBKCS112
00362 *    INTER-APPLICATION COMMAREA.                                  MBKCS112
00363 ***************************************************************** MBKCS112
00364                                                                   MBKCS112
00365          10  INTER-APPL-COMM-AREA       PIC X(200).               MBKCS112
00366      COPY MBWS005.                                                   CL*15
00367                                                                      CL243
00368 *****************************************************************    CL243
00369 *    SPECIFIC COMMMAREA FOR MBKCS002.                                CL243
00370 *****************************************************************    CL243
00371                                                                      CL243
00372      COPY MBWS012.                                                   CL*15
00373                                                                      CL243
00374                                                                      CL244
00375 *****************************************************************    CL244
00376 *    TEMP STORAGE QUEUE RECORD FOR SELECTED ITEMS                    CL244
00377 *****************************************************************    CL244
00378  01  SELECTED-LIST-RECORD.                                           CL244
00379      COPY MBWS003.                                                   CL*15
00380                                                                      CL244
00381                                                                   MBKCS112
00382 ***************************************************************** MBKCS112
00383 *    ABEND PROCESSING COPYBOOK.                                   MBKCS112
00384 ***************************************************************** MBKCS112
00385                                                                   MBKCS112
00386      COPY DPWS013.                                                MBKCS112
00387                                                                   MBKCS112
00388 ***************************************************************** MBKCS112
00389 *    ATTRIBUTE SETTINGS COPYBOOK.                                 MBKCS112
00390 ***************************************************************** MBKCS112
00391                                                                   MBKCS112
00392      COPY DPWS015.                                                MBKCS112
00393                                                                   MBKCS112
00394 ***************************************************************** MBKCS112
00395 *    CURRENT DATE AND TIME COPYBOOK.                              MBKCS112
00396 ***************************************************************** MBKCS112
00397                                                                   MBKCS112
00398      COPY DPWS017.                                                MBKCS112
00399                                                                   MBKCS112
00400 ***************************************************************** MBKCS112
00401 *    TOGGLE FUNCTION KEY LINES COPYBOOK.                          MBKCS112
00402 ***************************************************************** MBKCS112
00403                                                                   MBKCS112
00404      COPY DPWS009.                                                MBKCS112
                                                                        MBKCS101
      *****************************************************************         
      *    SYSTEM MESSAGE FIELDS                                                
      *****************************************************************         
                                                                                
           COPY DPWS006.                                                        
                                                                                
00406 ***************************************************************** MBKCS112
00407 *    FUNCTION KEYS COPYBOOK.                                      MBKCS112
00408 ***************************************************************** MBKCS112
00409                                                                   MBKCS112
00410      COPY DPWS016.                                                MBKCS112
00411                                                                   MBKCS112
00412 ***************************************************************** MBKCS112
00413 *    DPKUT045 COPYBOOK.                                           MBKCS112
00414 *                                                                 MBKCS112
00415 *    DATA AREA NECESSARY TO INTERACT WITH SUB-ROUTINE DPKUT045    MBKCS112
00416 *                                                                 MBKCS112
00417 *    DPKUT045 IS A UTILITY ROUTINE THAT CONVERTS THE EIBAID       MBKCS112
00418 *    CHARACTER RECEIVED FROM THE TERMINAL INTO A INTEGER VALUE    MBKCS112
00419 *    FROM 1 TO 29. THIS INTEGER VALUE IS STORED IN DP020-SRC-AID  MBKCS112
00420 *    AND IS USED TO INTERROGATE THE DP020-FUNCTION-KEY-ARRAY      MBKCS112
00421 *    TABLE IN THE STANDARD COMMAREA TO DETERMINE WHAT TO DO NEXT. MBKCS112
00422 *    IN ADDITION DPKUT045 WILL PARSE THE SCREEN INPUT COMMAND     MBKCS112
00423 *    FIELD AND CONVERT RECOGNIZED SCREEN MANIPULATION COMMANDS    MBKCS112
00424 *    INTO THE DP020-SRC-AID EQUIVALENT.                           MBKCS112
00425 ***************************************************************** MBKCS112
00426                                                                   MBKCS112
00427      COPY DPWS045.                                                MBKCS112
00428                                                                   MBKCS112
00429 ***************************************************************** MBKCS112
00430 *    NUMERIC EDIT LAYOUT.                                         MBKCS112
00431 ***************************************************************** MBKCS112
00432                                                                   MBKCS112
00433      COPY DPWS010I.                                               MBKCS112
00434                                                                   MBKCS112
00435 *****************************************************************    CL**7
00436 *    SYSTEM MESSAGES RECORD LAYOUT                                   CL**7
00437 *****************************************************************    CL**7
00438                                                                      CL**7
00439      COPY DPRD507I.                                                  CL**7
00440                                                                      CL**7
00441 ***************************************************************** MBKCS112
00442 *    STANDARD ATTENTION IDENTIFIER LIST                           MBKCS112
00443 ***************************************************************** MBKCS112
00444                                                                   MBKCS112
00445      COPY DFHAID.                                                 MBKCS112
00446                                                                   MBKCS112
00447 ***************************************************************** MBKCS112
00448 *    MAP LAYOUT                                                   MBKCS112
00449 ***************************************************************** MBKCS112
00450                                                                   MBKCS112
00451      COPY MBKM112.                                                   CL208
00452                                                                   MBKCS112
00453      COPY MBWS013.                                                   CL*15
00454                                                                   MBKCS112
00455  EJECT                                                            MBKCS112
00456 *****************************************************************    CL*15
00457 *    DB2 COMMUNICATION AREA                                          CL*15
00458 *****************************************************************    CL*15
00459                                                                      CL*15
00460      EXEC SQL                                                        CL*15
00461          INCLUDE SQLCA                                               CL*15
00462      END-EXEC.                                                       CL*15
00463                                                                      CL*15
00464 *****************************************************************    CL*15
00465 *    MERCHANDISE AREA TABLE - TMDSEAR                                CL*15
00466 *****************************************************************    CL*15
00467                                                                      CL*15
00468      EXEC SQL                                                        CL*15
00469          INCLUDE TMDSEAR                                             CL*15
00470      END-EXEC.                                                       CL*15
00471                                                                      CL*15
00472 *****************************************************************    CL165
00473 *    DECLARE FOR THE MERCH-CURSOR                                    CL165
00474 *       THIS CURSOR IS USED IF THE SELECTION CRITERION               CL165
00475 *       IS FOR ALL GMAS, DMAS, AND DEPARTMENTS                       CL184
00476 *****************************************************************    CL165
00477                                                                      CL165
00478      EXEC SQL                                                        CL*90
00479          DECLARE MERCH-CURSOR CURSOR FOR                             CL*90
00480              SELECT  OPERCO_NBR                                      CL*90
00481                    , GMA_NBR                                         CL*90
00482                    , DMA_NBR                                         CL*90
00483                    , DEPT_NBR                                        CL*90
00484                    , MAJ_CL_NBR                                      CL*90
00485                    , SUB_CL_NBR                                      CL*90
00486                    , MDSELV_CDE                                      CL*90
00487                    , CHG_DTE                                         CL*90
00488                    , CRE_DTE                                         CL*90
00489                    , STRT_DTE                                        CL*90
00490                    , END_DTE                                         CL*90
00491                    , GEN_MDSE_TYP_CDE                                CL*90
00492                    , MDSEAR_DESC                                     CL*90
00493                    , BYR_NBR                                         CL*90
00494                    , LIST_SEQ_NBR                                    CL107
00495                FROM  TMDSEAR                                         CL*90
00496               WHERE  OPERCO_NBR    = :PC-OPERCO-NBR                  CL*90
00497                 AND  MDSELV_CDE    = :PC-DPT-LITERAL                 CL196
00498                 AND  LIST_SEQ_NBR  > :MB012-SAVE-SEQ-NBR             CL191
00499                 AND  CHAR(END_DTE,ISO)                               CL212
00500                                    > :MB012-SAVE-CUR-DATE            CL212
00501            ORDER BY  OPERCO_NBR                                      CL*90
00502                    , GMA_NBR                                         CL*90
00503                    , DMA_NBR                                         CL*90
00504                    , DEPT_NBR                                        CL*90
00505      END-EXEC.                                                       CL*90
00506                                                                      CL165
00507 *****************************************************************    CL165
00508 *    DECLARE FOR THE GMA-CURSOR                                      CL165
00509 *       THIS CURSOR IS USED IF THE SELECTION CRITERION               CL165
00510 *       IS FOR A SPECIFIC GMA, AND ALL LEVELS SELECTED WITHIN        CL165
00511 *       THE REQUESTED GMA                                            CL165
00512 *****************************************************************    CL165
00513                                                                      CL165
00514      EXEC SQL                                                        CL*97
00515          DECLARE GMA-CURSOR CURSOR FOR                               CL*97
00516              SELECT  OPERCO_NBR                                      CL*97
00517                    , GMA_NBR                                         CL*97
00518                    , DMA_NBR                                         CL*97
00519                    , DEPT_NBR                                        CL*97
00520                    , MAJ_CL_NBR                                      CL*97
00521                    , SUB_CL_NBR                                      CL*97
00522                    , MDSELV_CDE                                      CL*97
00523                    , CHG_DTE                                         CL*97
00524                    , CRE_DTE                                         CL*97
00525                    , STRT_DTE                                        CL*97
00526                    , END_DTE                                         CL*97
00527                    , GEN_MDSE_TYP_CDE                                CL*97
00528                    , MDSEAR_DESC                                     CL*97
00529                    , BYR_NBR                                         CL*97
00530                    , LIST_SEQ_NBR                                    CL107
00531                FROM  TMDSEAR                                         CL*97
00532               WHERE  OPERCO_NBR    = :PC-OPERCO-NBR                  CL*97
00533                 AND  MDSELV_CDE    = :PC-DPT-LITERAL                 CL196
00534                 AND  LIST_SEQ_NBR  > :MB012-SAVE-SEQ-NBR             CL183
00535                 AND  CHAR(END_DTE,ISO)                               CL212
00536                                    > :MB012-SAVE-CUR-DATE            CL212
00537                 AND  GMA_NBR       = :MB012-SAVE-GMA-NBR             CL183
00538            ORDER BY  OPERCO_NBR                                      CL*97
00539                    , GMA_NBR                                         CL*97
00540                    , DMA_NBR                                         CL*97
00541                    , DEPT_NBR                                        CL*97
00542      END-EXEC.                                                       CL*97
00543                                                                      CL165
00544 *****************************************************************    CL165
00545 *    DECLARE FOR THE DMA-CURSOR                                      CL165
00546 *       THIS CURSOR IS USED IF THE SELECTION CRITERION               CL165
00547 *       IS FOR A SPECIFIC DMA, AND ALL DEPARTMENT LEVELS             CL184
00548 *       SELECTED WITHIN THE REQUESTED DMA                            CL165
00549 *****************************************************************    CL165
00550                                                                      CL119
00551      EXEC SQL                                                        CL119
00552          DECLARE DMA-CURSOR CURSOR FOR                               CL119
00553              SELECT  OPERCO_NBR                                      CL119
00554                    , GMA_NBR                                         CL119
00555                    , DMA_NBR                                         CL119
00556                    , DEPT_NBR                                        CL119
00557                    , MAJ_CL_NBR                                      CL119
00558                    , SUB_CL_NBR                                      CL119
00559                    , MDSELV_CDE                                      CL119
00560                    , CHG_DTE                                         CL119
00561                    , CRE_DTE                                         CL119
00562                    , STRT_DTE                                        CL119
00563                    , END_DTE                                         CL119
00564                    , GEN_MDSE_TYP_CDE                                CL119
00565                    , MDSEAR_DESC                                     CL119
00566                    , BYR_NBR                                         CL119
00567                    , LIST_SEQ_NBR                                    CL119
00568                FROM  TMDSEAR                                         CL119
00569               WHERE  OPERCO_NBR    = :PC-OPERCO-NBR                  CL119
00570                 AND  MDSELV_CDE    = :PC-DPT-LITERAL                 CL196
00571                 AND  LIST_SEQ_NBR  > :MB012-SAVE-SEQ-NBR             CL183
00572                 AND  CHAR(END_DTE,ISO)                               CL212
00573                                    > :MB012-SAVE-CUR-DATE            CL212
00574                 AND  DMA_NBR       = :MB012-SAVE-DMA-NBR             CL183
00575            ORDER BY  OPERCO_NBR                                      CL119
00576                    , GMA_NBR                                         CL119
00577                    , DMA_NBR                                         CL119
00578                    , DEPT_NBR                                        CL119
00579                    , MAJ_CL_NBR                                      CL119
00580                    , SUB_CL_NBR                                      CL119
00581      END-EXEC.                                                       CL119
00582                                                                      CL165
00583 *****************************************************************    CL165
00584 *    DECLARE FOR THE DPT-CURSOR                                      CL165
00585 *       THIS CURSOR IS USED IF THE SELECTION CRITERION               CL165
00586 *       IS FOR A SPECIFIC DPT, AND ALL LEVELS,(DPT AND LOWER),       CL165
00587 *       SELECTED WITHIN THE REQUESTED DPT                            CL165
00588 *****************************************************************    CL165
00589                                                                      CL129
00590      EXEC SQL                                                        CL129
00591          DECLARE DPT-CURSOR CURSOR FOR                               CL129
00592              SELECT  OPERCO_NBR                                      CL129
00593                    , GMA_NBR                                         CL129
00594                    , DMA_NBR                                         CL129
00595                    , DEPT_NBR                                        CL129
00596                    , MAJ_CL_NBR                                      CL129
00597                    , SUB_CL_NBR                                      CL129
00598                    , MDSELV_CDE                                      CL129
00599                    , CHG_DTE                                         CL129
00600                    , CRE_DTE                                         CL129
00601                    , STRT_DTE                                        CL129
00602                    , END_DTE                                         CL129
00603                    , GEN_MDSE_TYP_CDE                                CL129
00604                    , MDSEAR_DESC                                     CL129
00605                    , BYR_NBR                                         CL129
00606                    , LIST_SEQ_NBR                                    CL129
00607                FROM  TMDSEAR                                         CL129
00608               WHERE  OPERCO_NBR    = :PC-OPERCO-NBR                  CL129
00609                 AND  MDSELV_CDE    = :PC-DPT-LITERAL                 CL196
00610                 AND  LIST_SEQ_NBR  > :MB012-SAVE-SEQ-NBR             CL183
00611                 AND  CHAR(END_DTE,ISO)                               CL212
00612                                    > :MB012-SAVE-CUR-DATE            CL212
00613                 AND  DEPT_NBR      = :MB012-SAVE-DPT-NBR             CL183
00614            ORDER BY  OPERCO_NBR                                      CL129
00615                    , GMA_NBR                                         CL129
00616                    , DMA_NBR                                         CL129
00617                    , DEPT_NBR                                        CL129
00618      END-EXEC.                                                       CL129
00619                                                                      CL148
00620  LINKAGE SECTION.                                                 MBKCS112
00621                                                                   MBKCS112
00622  01  DFHCOMMAREA.                                                 MBKCS112
00623      05  FILLER                         OCCURS  1 TO 4072 TIMES   MBKCS112
00624                                         DEPENDING ON EIBCALEN.    MBKCS112
00625          10  FILLER                     PIC X.                    MBKCS112
00626                                                                   MBKCS112
00627 *                                                                 MBKCS112
00628 *                                                                 MBKCS112
00629 *                                                                 MBKCS112
00630  PROCEDURE DIVISION.                                              MBKCS112
00631                                                                   MBKCS112
00632                                                                   MBKCS112
00633  A100-MAIN-MODULE.                                                MBKCS112
00634                                                                   MBKCS112
00635 ******************************************************************MBKCS112
00636 *    PROCESS THIS TRANSACTION IF THE COMMAREA IS VALID.           MBKCS112
00637 *                                                                 MBKCS112
00638 ******************************************************************MBKCS112
00639                                                                   MBKCS112
00640      PERFORM DP015-0000-CHECK-COMMAREA.                           MBKCS112
00641                                                                   MBKCS112
00642 ******************************************************************MBKCS112
00643 *    DP020-SRC-RETURN-W-TRAN-ID WILL BE TRUE IF THIS PROGRAM WAS  MBKCS112
00644 *    SENT A STANDARD COMMAREA. IF NOT SET TRUE (MEANING OTHER     MBKCS112
00645 *    THAN A STANDARD COMMAREA WAS SENT TO THIS PROGRAM, THIS      MBKCS112
00646 *    PROGRAM WILL RETURN TO CICS.                                 MBKCS112
00647 ******************************************************************MBKCS112
00648                                                                   MBKCS112
00649      IF DP020-SRC-RETURN-W-TRAN-ID                                MBKCS112
00650          PERFORM B100-CONTROL-PROCESSING                          MBKCS112
00651      END-IF.                                                      MBKCS112
00652                                                                   MBKCS112
00653      PERFORM DP003-0000-RETURN-TO-CICS.                           MBKCS112
00654  EJECT                                                            MBKCS112
00655  B100-CONTROL-PROCESSING.                                         MBKCS112
00656                                                                   MBKCS112
00657 ******************************************************************MBKCS112
00658 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT    MBKCS112
00659 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                    MBKCS112
00660 *                                                                 MBKCS112
00661 *    PERFORMED BY A100-MAIN-MODULE                                   CL*15
00662 ******************************************************************MBKCS112
00663                                                                   MBKCS112
00664      MOVE LENGTH OF MB012-BLOCK-ENTRIES (1)                          CL248
00665                                TO  PC-BLOCK-LENGTH.                  CL248
00666      EVALUATE TRUE                                                MBKCS112
00667                                                                   MBKCS112
00668          WHEN DP020-NEXT-ACT-INITIAL                                 CL*22
00669              SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                 MBKCS112
00670                                                                   MBKCS112
00671              PERFORM C100-INITIALIZE-COMMAREA                        CL**3
00672              PERFORM C200-SAVE-INITIAL-INTERAPPL                     CL**3
00673              IF PS-INTER-APPL-VALID                                  CL251
00674                  PERFORM D100-SETUP-INIT-PANEL                       CL251
00675                  IF PS-NO-SELECTIONS-FOUND                           CL251
00676                      MOVE DP016-PF3             TO DP020-SRC-AID     CL251
00677                      PERFORM DP015-1000-START-NAVIGATOR              CL251
00678                  END-IF                                              CL251
00679              ELSE                                                    CL251
00680                  MOVE DP016-PF3             TO DP020-SRC-AID         CL251
00681                  PERFORM DP015-1000-START-NAVIGATOR                  CL251
00682              END-IF                                                  CL251
00683                                                                      CL251
00684                                                                      CL157
00685          WHEN DP020-NEXT-ACT-READ-MAP                             MBKCS112
00686              PERFORM C300-PROCESS-PANEL                           MBKCS112
00687                                                                   MBKCS112
00688          WHEN DP020-NEXT-ACT-RETURN                               MBKCS112
00689              SET PS-SEND-MAP-N-DATA-FRSET   TO TRUE                  CL165
00690                                                                   MBKCS112
00691              PERFORM C400-UPDATE-INITIAL-INTERAPPL                   CL**3
00692              PERFORM C500-REFRESH-PANEL-FROM-TEMP                    CL**3
00693                                                                   MBKCS112
00694          WHEN DP020-NEXT-ACT-ERROR                                MBKCS112
00695                                                                   MBKCS112
00696 ******************************************************************MBKCS112
00697 *    THE FOLLOWING CODE HAS BEEN INSERTED IN ORDER TO PRESERVE    MBKCS112
00698 *    THE ERROR MESSAGE BEING RETURNED FROM THE NAVIGATOR. SINCE   MBKCS112
00699 *    THE ERROR BEING RETURNED WILL TYPICALLY BE A SECURITY        MBKCS112
00700 *    VIOLATION OR THE UNAVAILABILITY OF THE REQUESTED TRANSACTION MBKCS112
00701 *    THIS CODE ALSO CHECKS TO SEE HOW  THE NAVIGATION REQUEST WAS MBKCS112
00702 *    INVOKED. IF DP020-NAV-MNEMONIC FIELD IN THE STANDARD         MBKCS112
00703 *    COMMAREA IS SPACES, THE NAVIGATION REQUEST WAS VIA A FUNCTIONMBKCS112
00704 *    KEY. IF NOT, IT WAS VIA A COMMAND ENTERED IN THE COMMAND LINEMBKCS112
00705 *    COMMAND FIELD AND THIS LOGIC SETS THE ATTRIBUTE FOR THIS FIELMBKCS112
00706 *    TO RED, REVERSE VIDEO.                                       MBKCS112
00707 ******************************************************************MBKCS112
00708                                                                   MBKCS112
00709              MOVE DP020-MSG-MESSAGE         TO PV-MESSAGE            CL165
00710                                                                   MBKCS112
00711              IF DP020-NAV-MNEMONIC = SPACES                       MBKCS112
00712                  MOVE SPACES                TO ANAVCMDO              CL165
00713                  MOVE DP015-GREEN           TO ANAVCMDC              CL165
00714                  MOVE DP015-UNDERLINE       TO ANAVCMDH              CL165
00715              ELSE                                                 MBKCS112
00716                  MOVE DP015-RED             TO ANAVCMDC              CL165
00717                  MOVE DP015-REVERSE         TO ANAVCMDH              CL165
00718              END-IF                                               MBKCS112
00719                                                                   MBKCS112
00720              PERFORM E100-EDIT-SELECTIONS                         MBKCS112
00721                                                                   MBKCS112
00722              MOVE -1                        TO ANAVCMDL              CL165
00723              MOVE PV-MESSAGE                TO DP020-MSG-MESSAGE     CL165
00724                                                                   MBKCS112
00725              SET DP020-MSG-FATAL            TO TRUE                  CL165
00726              SET PS-SEND-DATA-ONLY-FRSET    TO TRUE                  CL165
00727              SET PS-SAME-ITEMS-DISPLAYED    TO TRUE                  CL165
00728              SET PS-CURSOR-POSITIONED       TO TRUE                  CL165
00729                                                                   MBKCS112
00730              PERFORM G100-DISPLAY-PANEL                           MBKCS112
00731                                                                   MBKCS112
00732          WHEN OTHER                                               MBKCS112
00733              SET DP013-LOGIC-ABEND          TO TRUE                  CL165
00734              SET DP013-NO-ROLLBACK          TO TRUE                  CL165
00735                                                                   MBKCS112
00736              MOVE 'B100-CONTROL-PROCESSING' TO                    MBKCS112
00737                   DP013-PARAGRAPH                                 MBKCS112
00738              MOVE INVALID-NEXT-ACT-MSG      TO                       CL**3
00739                   DP013-MESSAGE-TEXT(1)                           MBKCS112
00740                                                                   MBKCS112
00741              PERFORM DP013-0000-PROCESS-ABEND                     MBKCS112
00742                                                                   MBKCS112
00743      END-EVALUATE.                                                MBKCS112
00744  EJECT                                                            MBKCS112
00745  C100-INITIALIZE-COMMAREA.                                           CL**3
00746                                                                   MBKCS112
00747 ******************************************************************MBKCS112
00748 *    INITIALIZE THE APPLICATION SPECIFIC AND INTER-APPLICATION    MBKCS112
00749 *    PLACE WHATEVER INITIALIZATION LOGIC IS NECESSARY.            MBKCS112
00750 *                                                                 MBKCS112
00751 ******************************************************************MBKCS112
00752                                                                      CL*17
00753      INITIALIZE MB012-SPECIFIC-COMMAREA.                             CL183
00754                                                                      CL*17
00755  EJECT                                                            MBKCS112
00756  C200-SAVE-INITIAL-INTERAPPL.                                        CL**3
00757                                                                      CL**3
00758 ******************************************************************   CL**3
00759 *    SAVE THE INTER-APPLICATION COMMAREA ON THE FIRST CYCLE          CL**3
00760 *    OF THIS TRANSACTION.                                            CL**3
00761 *                                                                    CL**3
00762 *    THIS PARAGRAPH MUST BE CODED IF YOU PLAN ON USING ANY           CL**3
00763 *    DATA IN THE INTER APPLICATIONS DATA ARE OF THE COMMAREA.        CL**3
00764 *    A TWO HUNDRED BYTE AREA IN THE APPLICATIONS SPECIFIC            CL**3
00765 *    COMMAREA MUST BE CREATED TO HOLD THE INITIAL VALUE OF THE       CL**3
00766 *    TWO HUNDRED BYTE  INTER APPLICATION COMMAREA SENT TO THIS       CL**3
00767 *    TRANSACTION.                                                    CL**3
00768 *                                                                    CL**3
00769 *    IF THIS TRANSACTION WILL NOT USE THE INTER APPL COMMAREA        CL**3
00770 *    AT ALL, THIS PARAGRAPH SHOULD MOVE BE REMOVED AS WELL AS        CL**3
00771 *    ANY REFERENCE TO IT. YOU ALSO DO NOT NEED TO DEFINE A 200       CL**3
00772 *    BYTE AREA IN THE APPL SPECIFIC COMMAREA TO SAVE THE INITIAL     CL**3
00773 *    INTER-APPL-COMMAREA.                                            CL**3
00774 *                                                                    CL**3
00775 ******************************************************************   CL**3
00776                                                                      CL165
00777      IF DP020-MB-MERCH-DEPT-SELECT                                   CL251
00778          MOVE MB005-CO-NUMBER         TO MB012-CO-NUMBER             CL252
00779          MOVE MB005-GMA-NUMBER        TO MB012-GMA-NUMBER            CL252
00780          MOVE MB005-DMA-NUMBER        TO MB012-DMA-NUMBER            CL252
00781          MOVE MB005-DEPT-NUMBER       TO MB012-DEPT-NUMBER           CL252
00782          MOVE MB005-HISTORY-INDICATOR TO MB012-HISTORY-FLAG          CL252
00783          SET PS-INTER-APPL-VALID      TO TRUE                        CL252
00784      ELSE                                                            CL251
00785          SET PS-INTER-APPL-INVALID TO TRUE                           CL251
00786          SET DP020-MSG-FATAL       TO TRUE                           CL252
00787          MOVE -1                   TO ANAVCMDL                       CL251
00788          MOVE PC-TSYMSG-99901      TO DP020-MSG-NUMBER               CL251
00789      END-IF.                                                         CL165
00790                                                                      CL*59
00791  EJECT                                                               CL**3
00792  C300-PROCESS-PANEL.                                              MBKCS112
00793                                                                   MBKCS112
00794 ******************************************************************MBKCS112
00795 *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED  MBKCS112
00796 *    ON WHAT FUNCTION KEY WAS SELECTED.                           MBKCS112
00797 *                                                                 MBKCS112
00798 ******************************************************************MBKCS112
00799                                                                   MBKCS112
00800      PERFORM DP007-0000-GET-PANEL.                                MBKCS112
00801                                                                      CL162
00802      SET PS-SEND-DATA-ONLY-FRSET             TO TRUE.                CL165
00803                                                                   MBKCS112
00804      EVALUATE TRUE                                                MBKCS112
00805                                                                   MBKCS112
00806          WHEN DP020-SRC-AID = DP016-CLEAR                         MBKCS112
00807              SET PS-SEND-MAP-N-DATA-FRSET    TO TRUE                 CL165
00808              SET PS-SAME-ITEMS-DISPLAYED     TO TRUE                 CL165
00809                                                                   MBKCS112
00810              PERFORM E100-EDIT-SELECTIONS                         MBKCS112
00811              PERFORM G100-DISPLAY-PANEL                           MBKCS112
00812                                                                   MBKCS112
00813          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                        CL250
00814              SET PS-SEND-MAP-N-DATA-FRSET    TO TRUE                 CL165
00815              INITIALIZE ANAVKEYO ANAVCMDO                            CL213
00816                         DP020-NAV-KEY-DATA                           CL250
00817              PERFORM Z050-DELETE-TEMP-STORAGE                     MBKCS112
00818              PERFORM D100-SETUP-INIT-PANEL                        MBKCS112
00819                                                                   MBKCS112
00820          WHEN DP020-SRC-AID = DP016-PA2                           MBKCS112
00821              PERFORM DP009-0000-LOAD-FK-LINES                     MBKCS112
00822              PERFORM DP017-0000-GET-CURR-DATE-TIME                MBKCS112
00823                                                                   MBKCS112
00824              SET PS-SEND-DATA-ONLY           TO TRUE                 CL165
00825                                                                   MBKCS112
00826              MOVE -1 TO ANAVCMDL                                  MBKCS112
00827                                                                   MBKCS112
00828              PERFORM DP008-0000-WRITE-PANEL                       MBKCS112
00829                                                                   MBKCS112
00830          WHEN OTHER                                               MBKCS112
00831              SET PS-INVALID-SELECTIONS TO TRUE                       CL*40
00832              PERFORM D200-RECORD-SELECTIONS                       MBKCS112
00833                                                                   MBKCS112
00834              IF PS-VALID-SELECTIONS                               MBKCS112
00835                  PERFORM D300-CHECK-FUNCTION-KEY                  MBKCS112
00836              ELSE                                                 MBKCS112
00837                  SET PS-SAME-ITEMS-DISPLAYED TO TRUE                 CL*71
00838                  PERFORM G100-DISPLAY-PANEL                       MBKCS112
00839              END-IF                                               MBKCS112
00840                                                                   MBKCS112
00841      END-EVALUATE.                                                MBKCS112
00842  EJECT                                                            MBKCS112
00843  C400-UPDATE-INITIAL-INTERAPPL.                                      CL**3
00844                                                                      CL**3
00845 ******************************************************************   CL**3
00846 *    WHENEVER THIS TRANSACTION HAS UPDATED VALUES THAT WERE PASSED   CL**3
00847 *    TO IT THRU THE INTER APPLICATION COMMAREA, THIS PARAGRAPH       CL**3
00848 *    IS INVOKED AND IT UPDATES THE FIELDS IN THE TWO HUNDRED BYTE    CL**3
00849 *    AREA IN THE APPLICATIONS SPECIFIC COMMAREA THAT WAS RESERVED    CL**3
00850 *    FOR THE INITIAL VALUE OF THE ORIGINAL TWO HUNDRED BYTES SENT    CL**3
00851 *    TO THIS TRANSACTION, THIS ALLOWS THE TRANSACTION THAT INVOKED   CL**3
00852 *    THIS TRANSACTION TO RECEIVE UPDATES THRU THE INTER APPL COMM    CL**3
00853 *    AREA WHEN THE USER PRESSES F3 - END ON THIS SCREEN. IN          CL**3
00854 *    ADDITION THIS PARAGRAPH WILL BE INVOKED WHEN THIS TRANSACTION   CL**3
00855 *    IS INVOKED VIA AN F3 - END AID KEY NAVIGATION. CODE MUST BE     CL**3
00856 *    INSERTED IN THIS PARAGRAPH TO HANDLE ANY UPDATES PASSED BACK    CL**3
00857 *    TO THIS TRANSACTION AS WELL. THIS LOGIC MAY BECOME QUITE OMPL   CL**3
00858 *    COMPLEX. FOR AN EXAMPLE OF HOW A SPECIFIC PROGRAM IS DOING      CL**3
00859 *    THIS LOGIC SEE DPKCS019.                                        CL**3
00860 *                                                                    CL**3
00861 *    THIS PARAGRAPH MUST BE CODED IF YOU PLAN ON USING ANY           CL**3
00862 *    DATA IN THE INTER APPLICATIONS DATA AREA OF THE COMMAREA.       CL**3
00863 *    A TWO HUNDRED BYTE AREA IN THE APPLICATIONS SPECIFIC            CL**3
00864 *    COMMAREA MUST BE CREATED TO HOLD THE INITIAL VALUE OF THE       CL**3
00865 *    TWO HUNDRED BYTE  INTER APPLICATION COMMAREA SENT TO THIS       CL**3
00866 *    TRANSACTION.                                                    CL**3
00867 *                                                                    CL**3
00868 *    IF THIS TRANSACTION WILL NOT USE THE INTER APPL COMMAREA        CL**3
00869 *    AT ALL, THIS PARAGRAPH SHOULD MOVE BE REMOVED AS WELL AS        CL**3
00870 *    ANY REFERENCE TO IT. YOU ALSO DO NOT NEED TO DEFINE A 200       CL**3
00871 *    BYTE AREA IN THE APPL SPECIFIC COMMAREA TO SAVE THE INITIAL     CL**3
00872 *    INTER-APPL-COMMAREA.                                            CL**3
00873 *                                                                    CL**3
00874 ******************************************************************   CL**3
00875                                                                      CL*19
00876 *    MOVE LOW-VALUES TO MB007-INTER-APPL-COMMAREA.                   CL242
00877                                                                      CL*19
00878  EJECT                                                               CL**3
00879  C500-REFRESH-PANEL-FROM-TEMP.                                       CL**3
00880                                                                   MBKCS112
00881 ******************************************************************MBKCS112
00882 *    WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, MBKCS112
00883 *    SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.       MBKCS112
00884 *    EDIT THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL  MBKCS112
00885 *    TO THE USER.                                                 MBKCS112
00886 *                                                                 MBKCS112
00887 ******************************************************************MBKCS112
00888 *                                                                    CL242
00889      IF  WE-MUST-UPDATE-TSQ                                          CL242
00890          MOVE ZERO             TO  PV-SAVE-ITEM                      CL242
00891                                    PV-MA-ITEM                        CL242
00892          SET NOT-END-OF-ITEMS  TO  TRUE                              CL242
00893          PERFORM C600-UPDATE-TSQ                                     CL242
00894                  UNTIL END-OF-ITEMS                                  CL242
00895          PERFORM X900-DELETE-LIST-QUEUE                              CL*11
00896          MOVE SPACES           TO  MB005-LIST-QUEUE-NAME             CL*11
00897      END-IF.                                                         CL242
00898 *                                                                    CL242
00899      PERFORM D400-REFRESH-BLOCK-ARRAY.                            MBKCS112
00900      PERFORM E100-EDIT-SELECTIONS.                                MBKCS112
00901      SET PS-DIFFERENT-ITEMS-DISPLAYED TO TRUE.                       CL*71
00902      PERFORM G100-DISPLAY-PANEL.                                  MBKCS112
00903 *                                                                    CL242
00904  EJECT                                                               CL242
00905 *                                                                    CL242
00906 ******************************************************************   CL242
00907 *    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.          CL242
00908 *    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER      CL242
00909 *    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN        CL242
00910 *    THE TEMP QUEUE.                                                 CL242
00911 *                                                                    CL242
00912 *    PERFORMED BY C500                                               CL242
00913 ******************************************************************   CL242
00914 *                                                                    CL242
00915  C600-UPDATE-TSQ.                                                    CL242
00916 *                                                                    CL242
00917      ADD 1                     TO  PV-MA-ITEM.                       CL242
00918      PERFORM X600-READ-LIST-QUEUE.                                   CL242
00919      IF  PV-RESP EQUAL DFHRESP(NORMAL)                               CL242
00920          SET MB003-IDX         TO  1                                 CL255
00921          PERFORM C700-PROCESS-ITEMS                                  CL242
00922              UNTIL MB003-KEY-DATA (MB003-IDX) EQUAL SPACES           CL255
00923                OR  MB003-IDX GREATER THAN MB003-MAX-ITEMS            CL255
00924          PERFORM X550-WRITE-TS-QUEUE                                 CL242
00925      ELSE                                                            CL242
00926          SET END-OF-ITEMS      TO  TRUE                              CL**1
00927      END-IF.                                                         CL**1
00928 *                                                                    CL242
00929  EJECT                                                               CL242
00930 *                                                                    CL242
00931 ******************************************************************   CL242
00932 *    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.    CL242
00933 *    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE    CL242
00934 *    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.               CL242
00935 *                                                                    CL242
00936 *    PERFORMED BY C600                                               CL242
00937 ******************************************************************   CL242
00938 *                                                                    CL242
00939  C700-PROCESS-ITEMS.                                                 CL242
00940 *                                                                    CL242
00941      COMPUTE PV-ITEM = 1 + ((MB003-ITEM (MB003-IDX) - 1)             CL255
00942                      / PC-ITEMS-PER-BLOCK).                          CL242
00943 *                                                                    CL242
00944      IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                              CL242
00945          IF  PV-SAVE-ITEM NOT EQUAL ZERO                             CL242
00946              PERFORM X550-WRITE-TS-QUEUE                             CL242
00947          END-IF                                                      CL242
00948          MOVE PV-ITEM          TO  PV-SAVE-ITEM                      CL242
00949 *                                                                    CL242
00950          PERFORM X250-READ-TS-QUEUE                                  CL242
00951          IF  PV-RESP NOT EQUAL DFHRESP(NORMAL)                       CL242
00952              SET END-OF-ITEMS  TO  TRUE                              CL242
00953          END-IF                                                      CL242
00954      END-IF.                                                         CL242
00955 *                                                                    CL242
00956      IF  NOT-END-OF-ITEMS                                            CL242
00957          COMPUTE PS-SUB = (MB003-ITEM (MB003-IDX)                    CL255
00958                         - PV-TW-LIST-ITEM-NUMBER (1)) + 1            CL242
00959          SET PV-TW-DESELECT (PS-SUB)                                 CL242
00960                                TO  TRUE                              CL242
00961          SUBTRACT 1          FROM  MB005-NUMBER-PROCESSED-ITEMS      CL242
00962                                    MB012-NUMBER-OF-ITEMS-SELECTED    CL245
00963          IF  MB005-NUMBER-PROCESSED-ITEMS EQUAL ZERO                 CL242
00964              SET END-OF-ITEMS  TO  TRUE                              CL242
00965              SET MB003-IDX     TO  MB003-MAX-ITEMS                   CL255
00966          END-IF                                                      CL242
00967          SET MB003-IDX      UP BY  1                                 CL255
00968      END-IF.                                                         CL242
00969 *                                                                    CL242
00970  EJECT                                                            MBKCS112
00971  D100-SETUP-INIT-PANEL.                                           MBKCS112
00972                                                                   MBKCS112
00973 ******************************************************************MBKCS112
00974 *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                         MBKCS112
00975 *    INITIALIZE ALL COUNTERS AND FLAGS.                           MBKCS112
00976 *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.   MBKCS112
00977 *    DISPLAY THE PANEL TO USER.                                   MBKCS112
00978 *                                                                 MBKCS112
00979 *    MB012 - MEMBER NAME USED FOR SPECIFIC COMMAREA.                 CL183
00980 *                                                                 MBKCS112
00981 ******************************************************************MBKCS112
00982                                                                   MBKCS112
00983      MOVE +1                  TO MB012-BLOCK-SUB.                    CL183
00984                                                                   MBKCS112
00985      PERFORM I100-INITIALIZE-BLOCK.                               MBKCS112
00986                                                                   MBKCS112
00987      ADD +1                   TO MB012-BLOCK-SUB.                    CL183
00988                                                                   MBKCS112
00989      PERFORM I100-INITIALIZE-BLOCK.                               MBKCS112
00990                                                                   MBKCS112
00991      MOVE +1                  TO MB012-BLOCK-SUB.                    CL183
00992      MOVE +0                  TO MB012-ITEM-SUB.                     CL183
00993      MOVE +0                  TO MB012-NUMBER-OF-ITEMS-READ.         CL183
00994      MOVE +0                  TO MB012-NUMBER-OF-ITEMS-SELECTED.     CL183
00995      MOVE SPACES              TO MB012-TEMP-QUEUE-NAME.              CL183
00996      MOVE +0                  TO MB012-HIGHEST-BLOCK-WRITTEN.        CL183
00997      MOVE +0                  TO MB012-START-OF-SELECTED-RANGE.      CL183
00998      MOVE +0                  TO MB012-END-OF-SELECTED-RANGE.        CL183
00999      MOVE +0                  TO MB012-SAVE-SEQ-NBR.                 CL183
01000      MOVE PC-ITEMS-PER-PANEL  TO PV-HIGHEST-ITEM-TO-BE-READ.         CL165
01001                                                                      CL216
01002      PERFORM DP017-0000-GET-CURR-DATE-TIME.                          CL242
01003      MOVE DP017-EDITED-DATE-YYYYMMDD                                 CL246
01004                               TO  MB012-DB2-CURRENT-DATE.            CL242
01005 *                                                                    CL242
01006      IF MB012-HISTORY-FLAG = 'N'                                     CL223
01007         MOVE DP017-EDITED-DATE-YYYYMMDD TO MB012-SAVE-CUR-DATE       CL216
01008      ELSE                                                            CL216
01009         MOVE '1900-01-01' TO MB012-SAVE-CUR-DATE                     CL216
01010      END-IF.                                                         CL226
01011                                                                      CL249
01012      PERFORM D150-SETUP-DATABASE-KEYS.                               CL249
01013                                                                      CL197
01014      PERFORM D500-OPEN-CURSOR.                                       CL137
01015                                                                      CL137
01016      SET MB012-ITEMS-LEFT-ON-DB TO TRUE.                             CL183
01017                                                                   MBKCS112
01018      PERFORM H100-READ-ITEMS-FROM-DB.                             MBKCS112
01019                                                                   MBKCS112
01020      MOVE +1 TO MB012-ITEM-AT-TOP-OF-PANEL.                          CL183
01021                                                                      CL*71
01022      SET PS-DIFFERENT-ITEMS-DISPLAYED TO TRUE.                       CL161
01023                                                                      CL*71
01024      IF PS-NO-SELECTIONS-FOUND                                       CL161
01025          CONTINUE                                                    CL161
01026      ELSE                                                            CL161
01027          PERFORM G100-DISPLAY-PANEL                                  CL157
01028      END-IF.                                                         CL157
01029  EJECT                                                            MBKCS112
01030                                                                      CL249
01031  D150-SETUP-DATABASE-KEYS.                                           CL249
01032 ******************************************************************   CL249
01033 *                                                                    CL249
01034 *                                                                    CL249
01035 *                                                                    CL249
01036 *                                                                    CL249
01037 ******************************************************************   CL249
01038      SET MB012-NO-CURSOR-OPEN TO TRUE.                               CL249
01039                                                                      CL249
01040      IF MB012-ALL-DEPARTMENTS                                        CL249
01041          CONTINUE                                                    CL249
01042      ELSE                                                            CL249
01043          IF (MB012-DEPT-NUMBER = SPACES) OR                          CL249
01044             (MB012-DEPT-NUMBER = LOW-VALUES)                         CL249
01045              MOVE SPACES        TO MB012-SAVE-DPT-NBR                CL249
01046          ELSE                                                        CL249
01047              MOVE MB012-DEPT-NUMBER TO MB012-SAVE-DPT-NBR            CL249
01048              IF MB012-NO-CURSOR-OPEN                                 CL249
01049                  SET MB012-OPEN-DPT-CURSOR TO TRUE                   CL249
01050              END-IF                                                  CL249
01051          END-IF                                                      CL249
01052      END-IF.                                                         CL249
01053                                                                      CL249
01054      IF MB012-ALL-DMAS                                               CL249
01055          CONTINUE                                                    CL249
01056      ELSE                                                            CL249
01057          IF (MB012-DMA-NUMBER = SPACES) OR                           CL249
01058             (MB012-DMA-NUMBER = LOW-VALUES)                          CL249
01059              MOVE SPACES               TO MB012-SAVE-DMA-NBR         CL249
01060          ELSE                                                        CL249
01061              MOVE MB012-DMA-NUMBER     TO MB012-SAVE-DMA-NBR         CL249
01062              IF MB012-NO-CURSOR-OPEN                                 CL249
01063                  SET MB012-OPEN-DMA-CURSOR TO TRUE                   CL249
01064              END-IF                                                  CL249
01065          END-IF                                                      CL249
01066      END-IF.                                                         CL249
01067                                                                      CL249
01068      IF MB012-ALL-GMAS                                               CL249
01069          CONTINUE                                                    CL249
01070      ELSE                                                            CL249
01071         MOVE MB012-GMA-NUMBER       TO MB012-SAVE-GMA-NBR            CL249
01072         IF MB012-NO-CURSOR-OPEN                                      CL249
01073             SET MB012-OPEN-GMA-CURSOR TO TRUE                        CL249
01074         END-IF                                                       CL249
01075      END-IF.                                                         CL249
01076                                                                      CL249
01077      IF (MB012-ALL-GMAS        AND                                   CL249
01078          MB012-ALL-DMAS        AND                                   CL249
01079          MB012-ALL-DEPARTMENTS)                                      CL249
01080          SET MB012-OPEN-MERCH-CURSOR TO TRUE                         CL249
01081      END-IF.                                                         CL249
01082                                                                      CL249
01083      IF (MB012-ALL-GMAS               AND                            CL249
01084          MB012-SAVE-DMA-NBR = SPACES  AND                            CL249
01085          MB012-SAVE-DPT-NBR = SPACES  AND                            CL249
01086          MB012-NO-CURSOR-OPEN)                                       CL249
01087          SET MB012-OPEN-MERCH-CURSOR TO TRUE                         CL249
01088      END-IF.                                                         CL249
01089                                                                      CL249
01090      IF (MB012-ALL-GMAS               AND                            CL249
01091          MB012-ALL-DMAS               AND                            CL249
01092          MB012-SAVE-DPT-NBR = SPACES  AND                            CL249
01093          MB012-NO-CURSOR-OPEN)                                       CL249
01094          SET MB012-OPEN-MERCH-CURSOR TO TRUE                         CL249
01095      END-IF.                                                         CL249
01096  EJECT                                                               CL249
01097  D200-RECORD-SELECTIONS.                                          MBKCS112
01098                                                                   MBKCS112
01099 ******************************************************************MBKCS112
01100 *    IF THE USER HIT THE DESELECT KEY, OR A KEY WHICH INVOLVES    MBKCS112
01101 *    LEAVING THIS TRANSACTION AND GOING TO ANOTHER, SET A FLAG    MBKCS112
01102 *    INDICATING THAT ALL SELECTIONS ARE VALID.  IF ANY OTHER KEY  MBKCS112
01103 *    IS HIT, MOVE THE SELECTIONS MADE TO THE COMMAREA, AND EDIT   MBKCS112
01104 *    THOSE SELECTIONS.                                            MBKCS112
01105 *                                                                 MBKCS112
01106 *    IF THE USER ENTERED SOMETHING IN THE COMMAND LINE OR HIT     MBKCS112
01107 *    AN INVALID FUNCTION KEY, DO NOT EDIT THE SELECTIONS.         MBKCS112
01108 *                                                                 MBKCS112
01109 *    PERFORMED BY C300-PROCESS-PANEL.                                CL*92
01110 ******************************************************************MBKCS112
01111                                                                      CL*87
01112      IF (DP020-FK-DESELECT(DP020-SRC-AID))   OR                   MBKCS112
01113         (DP020-FK-SELECT-ALL(DP020-SRC-AID)) OR                      CL*71
01114         (DP020-FK-RETURN(DP020-SRC-AID))     OR                      CL*71
01115         (DP020-FK-GO-TO-TRAN(DP020-SRC-AID))                         CL*71
01116          SET PS-VALID-SELECTIONS TO TRUE                          MBKCS112
01117      ELSE                                                         MBKCS112
01118          PERFORM E200-MOVE-SELECTS-TO-COMMAREA                    MBKCS112
01119                                                                   MBKCS112
01120          IF (DP020-SRC-AID          = DP016-ENTER) AND            MBKCS112
01121             (DP020-NAV-MNEMONIC NOT = SPACES)      AND            MBKCS112
01122             (DP020-NAV-MNEMONIC NOT = LOW-VALUES)                 MBKCS112
01123              SET PS-VALID-SELECTIONS TO TRUE                      MBKCS112
01124          ELSE                                                     MBKCS112
01125                                                                   MBKCS112
01126              IF (DP020-FK-FIRST-PAGE(DP020-SRC-AID)) OR           MBKCS112
01127                 (DP020-FK-PREV-PAGE(DP020-SRC-AID))  OR           MBKCS112
01128                 (DP020-FK-NEXT-PAGE(DP020-SRC-AID))  OR           MBKCS112
01129                 (DP020-SRC-AID = DP016-ENTER)                     MBKCS112
01130                  PERFORM E100-EDIT-SELECTIONS                     MBKCS112
01131              ELSE                                                 MBKCS112
01132                  SET PS-VALID-SELECTIONS TO TRUE                  MBKCS112
01133              END-IF                                               MBKCS112
01134                                                                   MBKCS112
01135          END-IF                                                   MBKCS112
01136                                                                   MBKCS112
01137      END-IF.                                                      MBKCS112
01138  EJECT                                                            MBKCS112
01139  D300-CHECK-FUNCTION-KEY.                                         MBKCS112
01140                                                                   MBKCS112
01141 ******************************************************************MBKCS112
01142 *    PERFORM THE APPROPRIATE PARAGRAPH BASED ON WHICH FUNCTION    MBKCS112
01143 *    KEY HAD BEEN SELECTED BY THE USER.                           MBKCS112
01144 *                                                                 MBKCS112
01145 *    PERFORMED BY C300-PROCESS-PANEL.                                CL*92
01146 ******************************************************************MBKCS112
01147                                                                   MBKCS112
01148      EVALUATE TRUE                                                MBKCS112
01149                                                                   MBKCS112
01150          WHEN DP020-FK-GO-TO-TRAN(DP020-SRC-AID)                     CL242
01151              SET NO-UPDATE-TO-TSQ TO TRUE                            CL242
01152              PERFORM E300-NAVIGATE                                MBKCS112
01153 *                                                                    CL242
01154          WHEN DP020-FK-RETURN(DP020-SRC-AID)                         CL242
01155              PERFORM X900-DELETE-LIST-QUEUE                          CL242
01156              PERFORM E300-NAVIGATE                                   CL242
01157 *                                                                    CL242
01158          WHEN DP020-FK-EDIT-GOTO(DP020-SRC-AID)                      CL243
01159              IF MB012-NUMBER-OF-ITEMS-SELECTED > 0                   CL253
01160                  SET PS-NO-SELECTED-HISTORY TO TRUE                  CL253
01161                  PERFORM X100-PREPARE-LIST-QUEUE                     CL253
01162                  IF DP020-SRC-AID = DP016-PF15                       CL253
01163                     IF  PS-SELECTED-HISTORY                          CL253
01164                         MOVE PC-TSYMSG-00146 TO DP020-MSG-NUMBER     CL253
01165                         SET DP020-MSG-FATAL  TO TRUE                 CL253
01166                         PERFORM G100-DISPLAY-PANEL                   CL253
01167                     ELSE                                             CL253
01168                         SET WE-MUST-UPDATE-TSQ TO TRUE               CL253
01169                         PERFORM E300-NAVIGATE                        CL253
01170                     END-IF                                           CL253
01171                  ELSE                                                CL253
01172                     SET WE-MUST-UPDATE-TSQ TO TRUE                   CL253
01173                     PERFORM E300-NAVIGATE                            CL253
01174                  END-IF                                              CL253
01175              ELSE                                                    CL253
01176                  MOVE PC-TSYMSG-00057 TO DP020-MSG-NUMBER            CL253
01177                  SET DP020-MSG-WARNING TO TRUE                       CL*14
01178                  PERFORM G100-DISPLAY-PANEL                          CL253
01179              END-IF                                                  CL253
01180                                                                   MBKCS112
01181          WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                  MBKCS112
01182              PERFORM E400-DISPLAY-TOP-OF-LIST                     MBKCS112
01183                                                                   MBKCS112
01184          WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                   MBKCS112
01185              PERFORM E500-BROWSE-BACKWARD                         MBKCS112
01186                                                                   MBKCS112
01187          WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                   MBKCS112
01188              PERFORM E600-BROWSE-FORWARD                          MBKCS112
01189                                                                   MBKCS112
01190          WHEN (DP020-FK-DESELECT(DP020-SRC-AID))   OR                CL*72
01191               (DP020-FK-SELECT-ALL(DP020-SRC-AID))                   CL*72
01192              PERFORM E700-DESELECT-ALL-ITEMS                      MBKCS112
01193                                                                   MBKCS112
01194          WHEN DP020-SRC-AID = DP016-ENTER                         MBKCS112
01195                                                                   MBKCS112
01196              IF (DP020-NAV-MNEMONIC = LOW-VALUES) OR              MBKCS112
01197                 (DP020-NAV-MNEMONIC = SPACES)                     MBKCS112
01198                  PERFORM E800-CHECK-FOR-JUMP-BROWSE               MBKCS112
01199              ELSE                                                 MBKCS112
01200                  SET NO-UPDATE-TO-TSQ                                CL242
01201                                TO  TRUE                              CL242
01202                  PERFORM E300-NAVIGATE                            MBKCS112
01203              END-IF                                               MBKCS112
01204                                                                   MBKCS112
01205          WHEN OTHER                                               MBKCS112
01206              MOVE PC-TSYMSG-00045                                    CL242
01207                                TO  DP020-MSG-NUMBER                  CL242
01208              SET DP020-MSG-FATAL                                     CL242
01209                                TO  TRUE                              CL242
01210              PERFORM G100-DISPLAY-PANEL                              CL242
01211                                                                   MBKCS112
01212      END-EVALUATE.                                                MBKCS112
01213  EJECT                                                            MBKCS112
01214  D400-REFRESH-BLOCK-ARRAY.                                        MBKCS112
01215                                                                   MBKCS112
01216 ******************************************************************MBKCS112
01217 *    WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, MBKCS112
01218 *    READ TEMPORARY STORAGE FILLING IN BOTH BLOCKS IN THE ARRAY   MBKCS112
01219 *    WITH THE ITEMS THAT WERE AT THE TOP OF THE LIST WHEN WE      MBKCS112
01220 *    HAD LEFT PREVIOUSLY.                                         MBKCS112
01221 *                                                                 MBKCS112
01222 ******************************************************************MBKCS112
01223                                                                   MBKCS112
01224      COMPUTE PV-TEMP-BLOCK-NUMBER =                               MBKCS112
01225          1 + ((MB012-LIST-ITEM-NUMBER(1 1) - 1) /                    CL183
01226                PC-ITEMS-PER-BLOCK).                               MBKCS112
01227                                                                   MBKCS112
01228      MOVE PV-TEMP-BLOCK-NUMBER TO PV-BLOCK-NUMBER.                MBKCS112
01229      MOVE +1                   TO MB012-BLOCK-SUB.                   CL183
01230                                                                   MBKCS112
01231      PERFORM Z100-READ-TEMP-STORAGE.                              MBKCS112
01232                                                                   MBKCS112
01233      ADD +1 TO PV-BLOCK-NUMBER.                                   MBKCS112
01234                                                                   MBKCS112
01235      IF (PV-BLOCK-NUMBER < MB012-HIGHEST-BLOCK-WRITTEN) OR           CL183
01236         (PV-BLOCK-NUMBER = MB012-HIGHEST-BLOCK-WRITTEN)              CL183
01237          ADD +1 TO MB012-BLOCK-SUB                                   CL183
01238                                                                   MBKCS112
01239          PERFORM Z100-READ-TEMP-STORAGE                           MBKCS112
01240      END-IF.                                                      MBKCS112
01241  EJECT                                                            MBKCS112
01242 ******************************************************************   CL165
01243 *    DETERMIN WHETHER A CURSOR NEEDS TO BE OPENED                    CL165
01244 *                                                                    CL165
01245 ******************************************************************   CL165
01246                                                                      CL165
01247  D500-OPEN-CURSOR.                                                   CL137
01248                                                                      CL137
01249      EVALUATE TRUE                                                   CL137
01250                                                                      CL137
01251           WHEN MB012-OPEN-MERCH-CURSOR AND                           CL183
01252                PS-MERCH-CURSOR-NOT-OPENED                            CL152
01253                PERFORM Z200-OPEN-MERCH-CURSOR                        CL137
01254                                                                      CL137
01255           WHEN MB012-OPEN-GMA-CURSOR AND                             CL183
01256                PS-GMA-CURSOR-NOT-OPENED                              CL152
01257                PERFORM Z300-OPEN-GMA-CURSOR                          CL137
01258                                                                      CL137
01259           WHEN MB012-OPEN-DMA-CURSOR AND                             CL183
01260                PS-DMA-CURSOR-NOT-OPENED                              CL152
01261                PERFORM Z400-OPEN-DMA-CURSOR                          CL137
01262                                                                      CL137
01263           WHEN MB012-OPEN-DPT-CURSOR AND                             CL183
01264                PS-DPT-CURSOR-NOT-OPENED                              CL152
01265                PERFORM Z500-OPEN-DPT-CURSOR                          CL137
01266                                                                      CL137
01267      END-EVALUATE.                                                   CL138
01268                                                                      CL137
01269  EJECT                                                               CL137
01270  E100-EDIT-SELECTIONS.                                            MBKCS112
01271                                                                   MBKCS112
01272 ******************************************************************MBKCS112
01273 *    EDIT EACH SELECTION ENTRY.  IF EACH ENTRY IS CONSIDERED      MBKCS112
01274 *    VALID, CHECK IF A BEGINNING AND ENDING ENTRY WAS ENTERED,    MBKCS112
01275 *    AND CHECK THE FOLLOWING.                                     MBKCS112
01276 *                                                                 MBKCS112
01277 *    1) IF THERE IS A BEGINNING RANGE, THERE MUST BE AN ENDING.   MBKCS112
01278 *    2) IF THERE IS A ENDING RANGE, THERE MUST BE AN BEGINNING.   MBKCS112
01279 *    3) BEGINNING RANGE MUST PRECEDE ENDING RANGE.                MBKCS112
01280 *                                                                 MBKCS112
01281 *    IF A VALID BEGINNING AND ENDING RANGE HAVE BEEN SELECTED,    MBKCS112
01282 *    MARK ALL ITEMS IN THAT RANGE AS BEING SELECTED.              MBKCS112
01283 *                                                                 MBKCS112
01284 *    PERFORMED BY D200-RECORD-SELECTIONS                             CL*92
01285 ******************************************************************MBKCS112
01286                                                                   MBKCS112
01287      SET PS-VALID-SELECTIONS TO TRUE.                             MBKCS112
01288                                                                   MBKCS112
01289      COMPUTE PV-ENDING-ITEM-ON-PANEL =                            MBKCS112
01290          (MB012-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.      CL183
01291                                                                   MBKCS112
01292      MOVE MB012-ITEM-AT-TOP-OF-PANEL TO PV-ITEM-IN-USE.              CL183
01293      MOVE +1                         TO PS-SUB.                   MBKCS112
01294                                                                   MBKCS112
01295      PERFORM F100-EDIT                                            MBKCS112
01296          UNTIL (PV-ITEM-IN-USE > PV-ENDING-ITEM-ON-PANEL)    OR   MBKCS112
01297                (PV-ITEM-IN-USE > MB012-NUMBER-OF-ITEMS-READ)         CL183
01298                                                                   MBKCS112
01299      IF PS-VALID-SELECTIONS                                       MBKCS112
01300                                                                   MBKCS112
01301          IF MB012-START-OF-SELECTED-RANGE > +0                       CL183
01302                                                                   MBKCS112
01303              IF MB012-END-OF-SELECTED-RANGE   >                      CL183
01304                 MB012-START-OF-SELECTED-RANGE                        CL183
01305                  PERFORM F200-MARK-RANGE-AS-SELECTED              MBKCS112
01306              ELSE                                                 MBKCS112
01307                                                                   MBKCS112
01308                  IF MB012-END-OF-SELECTED-RANGE = +0                 CL183
01309                      MOVE PC-TSYMSG-00051    TO DP020-MSG-NUMBER     CL179
01310                      SET PS-CURSOR-POSITIONED  TO TRUE               CL*55
01311                      SET PS-WARNING            TO TRUE               CL*55
01312                      IF NOT DP020-NEXT-ACT-ERROR                     CL*19
01313                          SET DP020-MSG-WARNING TO TRUE               CL*55
01314                      END-IF                                          CL*19
01315                  ELSE                                             MBKCS112
01316                      SET PS-INVALID-SELECTIONS TO TRUE            MBKCS112
01317                      SET PS-CURSOR-POSITIONED  TO TRUE               CL*53
01318                      SET DP020-MSG-FATAL       TO TRUE               CL*19
01319                      MOVE PC-TSYMSG-00052    TO DP020-MSG-NUMBER     CL179
01320                      IF NOT DP020-NEXT-ACT-ERROR                     CL*20
01321                          MOVE MB012-ITEM-AT-TOP-OF-PANEL             CL183
01322                                                TO PV-ITEM-IN-USE     CL*20
01323                          MOVE +1               TO PS-SUB             CL*20
01324                          PERFORM                                     CL*20
01325                              UNTIL (PV-ITEM-IN-USE >                 CL*20
01326                                     PV-ENDING-ITEM-ON-PANEL)  OR     CL*20
01327                                    (PV-ITEM-IN-USE >                 CL*20
01328                                     MB012-NUMBER-OF-ITEMS-READ)      CL183
01329                              PERFORM K100-SET-SUBSCRIPTS             CL*20
01330                              IF (MB012-BEGIN(MB012-BLOCK-SUB         CL183
01331                                              MB012-ITEM-SUB)) OR     CL183
01332                                 (MB012-END(MB012-BLOCK-SUB           CL183
01333                                            MB012-ITEM-SUB))          CL183
01334                                  MOVE -1                      TO     CL*22
01335                                       MB013I-ASELL(PS-SUB)           CL185
01336                                  MOVE PV-ENDING-ITEM-ON-PANEL TO     CL*22
01337                                       PV-ITEM-IN-USE                 CL*22
01338                              END-IF                                  CL*22
01339                              ADD +1            TO PS-SUB             CL*22
01340                              ADD +1            TO PV-ITEM-IN-USE     CL*22
01341                          END-PERFORM                                 CL*22
01342                      END-IF                                          CL*22
01343                  END-IF                                              CL*22
01344              END-IF                                               MBKCS112
01345          ELSE                                                     MBKCS112
01346              IF MB012-END-OF-SELECTED-RANGE > +0                     CL183
01347                  SET PS-CURSOR-POSITIONED  TO TRUE                   CL*53
01348                  SET PS-WARNING            TO TRUE                   CL*22
01349                  MOVE PC-TSYMSG-00053      TO DP020-MSG-NUMBER       CL179
01350                  IF NOT DP020-NEXT-ACT-ERROR                         CL*22
01351                      SET DP020-MSG-WARNING TO TRUE                   CL*55
01352                  END-IF                                              CL*22
01353              END-IF                                               MBKCS112
01354          END-IF                                                   MBKCS112
01355      END-IF.                                                      MBKCS112
01356  EJECT                                                            MBKCS112
01357  E200-MOVE-SELECTS-TO-COMMAREA.                                   MBKCS112
01358                                                                   MBKCS112
01359 ******************************************************************MBKCS112
01360 *    MOVE THE SELECTIONS MADE FROM THE CURRENT PAGE INTO          MBKCS112
01361 *    THE COMMAREA.                                                MBKCS112
01362 *                                                                 MBKCS112
01363 *    PERFORMED BY D200-RECORD-SELECTIONS.                            CL*92
01364 ******************************************************************MBKCS112
01365                                                                   MBKCS112
01366      COMPUTE PV-ENDING-ITEM-ON-PANEL =                            MBKCS112
01367          (MB012-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.      CL183
01368                                                                   MBKCS112
01369      MOVE +1                         TO PS-SUB.                   MBKCS112
01370      MOVE MB012-ITEM-AT-TOP-OF-PANEL TO PV-ITEM-IN-USE.              CL183
01371                                                                   MBKCS112
01372      PERFORM F300-CHECK-SELECTIONS                                MBKCS112
01373          UNTIL PV-ITEM-IN-USE > PV-ENDING-ITEM-ON-PANEL.          MBKCS112
01374  EJECT                                                            MBKCS112
01375  E300-NAVIGATE.                                                   MBKCS112
01376                                                                   MBKCS112
01377 ******************************************************************MBKCS112
01378 *    DELETE THE TEMPORARY STORAGE IF THE USER HIT PF3.  IF WE ARE    CL*11
01379 *    GOING TO ANOTHER TRANSACTION, BUT PLANNING TO COME BACK,        CL*11
01380 *    WRITE OUT THE LAST 2 BLOCKS FROM THE ARRAY TO TEMPORARY         CL*11
01381 *    STORAGE. IN EITHER CASE, SET UP THE INTER APPLICATION           CL*11
01382 *    COMMAREA, AND START THE NAVIGATOR.                              CL*11
01383 *                                                                 MBKCS112
01384 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
01385 ******************************************************************MBKCS112
01386 *                                                                    CL242
01387      IF  NO-UPDATE-TO-TSQ                                            CL242
01388          PERFORM H400-WRITE-OUT-LAST-BLOCKS                          CL242
01389          PERFORM F400-SETUP-INTER-APPL                               CL**5
01390      END-IF.                                                         CL242
01391 *                                                                    CL242
01392      SET DP020-MB-MERCH-DEPT-LIST TO TRUE.                           CL**8
01393      PERFORM DP015-1000-START-NAVIGATOR.                          MBKCS112
01394  EJECT                                                            MBKCS112
01395  E400-DISPLAY-TOP-OF-LIST.                                        MBKCS112
01396                                                                   MBKCS112
01397 ******************************************************************MBKCS112
01398 *    IF THE USER HITS THE 1ST PAGE KEY, COMPUTE THE TOP ITEM      MBKCS112
01399 *    NUMBER FOR THE NEW PAGE AND DETERMINE IF THIS ITEM           MBKCS112
01400 *    IS CURRENTLY IN THE ARRAY, OR IN TEMPORARY STORAGE OR NEEDS  MBKCS112
01401 *    TO BE READ.                                                  MBKCS112
01402 *                                                                 MBKCS112
01403 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
01404 ******************************************************************MBKCS112
01405                                                                   MBKCS112
01406      IF MB012-NUMBER-OF-ITEMS-READ > +0                              CL183
01407          MOVE +1 TO PV-TOP-ITEM                                   MBKCS112
01408                                                                   MBKCS112
01409          PERFORM F500-BROWSE                                      MBKCS112
01410      ELSE                                                         MBKCS112
01411          SET PS-SAME-ITEMS-DISPLAYED TO TRUE                         CL*69
01412          PERFORM G100-DISPLAY-PANEL                               MBKCS112
01413      END-IF.                                                      MBKCS112
01414  EJECT                                                            MBKCS112
01415  E500-BROWSE-BACKWARD.                                            MBKCS112
01416                                                                   MBKCS112
01417 ******************************************************************MBKCS112
01418 *    IF THE USER HITS THE PAGE BACKARD KEY, COMPUTE THE TOP       MBKCS112
01419 *    ITEM NUMBER FOR THE NEW PAGE AND DETERMINE IF THIS ITEM      MBKCS112
01420 *    IS CURRENTLY IN THE ARRAY, OR IN TEMPORARY STORAGE OR NEEDS  MBKCS112
01421 *    TO BE READ.                                                  MBKCS112
01422 *                                                                 MBKCS112
01423 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
01424 ******************************************************************MBKCS112
01425                                                                   MBKCS112
01426      IF MB012-NUMBER-OF-ITEMS-READ > +0                              CL183
01427          COMPUTE PV-TOP-ITEM =                                    MBKCS112
01428              MB012-ITEM-AT-TOP-OF-PANEL - PC-ITEMS-PER-PANEL         CL183
01429                                                                   MBKCS112
01430          PERFORM F500-BROWSE                                      MBKCS112
01431      ELSE                                                         MBKCS112
01432          SET PS-SAME-ITEMS-DISPLAYED TO TRUE                         CL*31
01433          PERFORM G100-DISPLAY-PANEL                               MBKCS112
01434      END-IF.                                                      MBKCS112
01435  EJECT                                                            MBKCS112
01436  E600-BROWSE-FORWARD.                                             MBKCS112
01437                                                                   MBKCS112
01438 ******************************************************************MBKCS112
01439 *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP       MBKCS112
01440 *    ITEM NUMBER FOR THE NEW PAGE AND DETERMINE IF THIS ITEM      MBKCS112
01441 *    IS CURRENTLY IN THE ARRAY, OR IN TEMPORARY STORAGE OR NEEDS  MBKCS112
01442 *    TO BE READ.                                                  MBKCS112
01443 *                                                                    CL*92
01444 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
01445 ******************************************************************MBKCS112
01446                                                                   MBKCS112
01447      IF MB012-NUMBER-OF-ITEMS-READ > +0                              CL183
01448          COMPUTE PV-TOP-ITEM =                                    MBKCS112
01449              MB012-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL         CL183
01450                                                                   MBKCS112
01451          PERFORM F500-BROWSE                                      MBKCS112
01452      ELSE                                                         MBKCS112
01453          SET PS-SAME-ITEMS-DISPLAYED TO TRUE                         CL*31
01454          PERFORM G100-DISPLAY-PANEL                               MBKCS112
01455      END-IF.                                                      MBKCS112
01456  EJECT                                                            MBKCS112
01457  E700-DESELECT-ALL-ITEMS.                                         MBKCS112
01458                                                                   MBKCS112
01459 ******************************************************************MBKCS112
01460 *    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION       MBKCS112
01461 *    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.          MBKCS112
01462 *                                                                 MBKCS112
01463 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
01464 ******************************************************************MBKCS112
01465                                                                   MBKCS112
01466      IF MB012-NUMBER-OF-ITEMS-READ > +0                              CL183
01467          MOVE +1 TO MB012-BLOCK-SUB                                  CL183
01468                                                                   MBKCS112
01469          PERFORM F600-CLEAR-SELECTED-INDS                         MBKCS112
01470              VARYING MB012-ITEM-SUB FROM 1 BY 1                      CL183
01471              UNTIL MB012-ITEM-SUB > PC-ITEMS-PER-BLOCK               CL183
01472                                                                   MBKCS112
01473          ADD +1 TO MB012-BLOCK-SUB                                   CL183
01474                                                                   MBKCS112
01475          PERFORM F600-CLEAR-SELECTED-INDS                         MBKCS112
01476              VARYING MB012-ITEM-SUB FROM 1 BY 1                      CL183
01477              UNTIL MB012-ITEM-SUB > PC-ITEMS-PER-BLOCK               CL183
01478                                                                   MBKCS112
01479          MOVE MB012-LIST-ITEM-NUMBER(1 1) TO PV-LIST-ITEM-NUMBER     CL183
01480                                                                      CL**2
01481 ******************************************************************   CL**2
01482 *    CALCULATE THE BLOCK NUMBER OF THE FIRST BLOCK IN WORKING        CL**2
01483 *    STORAGE.  IF THIS BLOCK NUMBER IS NOT = 1, WE KNOW THAT         CL**2
01484 *    THERE ARE BLOCKS WITH SMALLER BLOCK NUMBERS SITTING OUT         CL**2
01485 *    IN TEMPORARY STORAGE THAT NEED TO BE RETRIEVED.                 CL**2
01486 *                                                                    CL**2
01487 *    IF THE BLOCK NUMBER DOES = 1, BUT THE HIGHEST BLOCK NUMBER      CL**2
01488 *    WRITTEN TO TEMPORARY STORAGE IS GREATER THAN THE MAXIMUM        CL**2
01489 *    NUMBER OF BLOCKS IN WORKING STORAGE, WE KNOW THAT               CL**2
01490 *    THERE ARE BLOCKS WITH LARGER BLOCK NUMBERS SITTING OUT          CL**2
01491 *    IN TEMPORARY STORAGE THAT NEED TO BE RETRIEVED.                 CL**2
01492 ******************************************************************   CL**2
01493                                                                   MBKCS112
01494          COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                        MBKCS112
01495              1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-BLOCK) MBKCS112
01496                                                                   MBKCS112
01497          IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                 MBKCS112
01498             (MB012-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)   CL183
01499              MOVE +1 TO MB012-BLOCK-SUB                              CL183
01500                                                                   MBKCS112
01501              PERFORM Z150-WRITE-TEMP-STORAGE                      MBKCS112
01502                                                                   MBKCS112
01503              MOVE +1 TO PV-BLOCK-NUMBER                           MBKCS112
01504                                                                   MBKCS112
01505              COMPUTE PV-PREV-BLOCK-IN-ARRAY =                     MBKCS112
01506                  PV-FIRST-BLOCK-IN-ARRAY - 1                      MBKCS112
01507                                                                   MBKCS112
01508              PERFORM                                              MBKCS112
01509                  UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY   MBKCS112
01510                                                                   MBKCS112
01511                  PERFORM Z100-READ-TEMP-STORAGE                   MBKCS112
01512                                                                   MBKCS112
01513                  PERFORM F600-CLEAR-SELECTED-INDS                 MBKCS112
01514                      VARYING MB012-ITEM-SUB FROM 1 BY 1              CL183
01515                      UNTIL MB012-ITEM-SUB > PC-ITEMS-PER-BLOCK       CL183
01516                                                                   MBKCS112
01517                  PERFORM Z150-WRITE-TEMP-STORAGE                  MBKCS112
01518                                                                   MBKCS112
01519                  ADD +1 TO PV-BLOCK-NUMBER                        MBKCS112
01520                                                                   MBKCS112
01521              END-PERFORM                                          MBKCS112
01522                                                                   MBKCS112
01523              COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                     MBKCS112
01524                  PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY MBKCS112
01525                                                                   MBKCS112
01526              PERFORM                                              MBKCS112
01527                  UNTIL PV-NEXT-BLOCK-IN-ARRAY >                   MBKCS112
01528                        MB012-HIGHEST-BLOCK-WRITTEN                   CL183
01529                                                                   MBKCS112
01530                  MOVE PV-NEXT-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER   MBKCS112
01531                                                                   MBKCS112
01532                  PERFORM Z100-READ-TEMP-STORAGE                   MBKCS112
01533                                                                   MBKCS112
01534                  PERFORM F600-CLEAR-SELECTED-INDS                 MBKCS112
01535                      VARYING MB012-ITEM-SUB FROM 1 BY 1              CL183
01536                      UNTIL MB012-ITEM-SUB > PC-ITEMS-PER-BLOCK       CL183
01537                                                                   MBKCS112
01538                  PERFORM Z150-WRITE-TEMP-STORAGE                  MBKCS112
01539                                                                   MBKCS112
01540                  ADD +1 TO PV-NEXT-BLOCK-IN-ARRAY                 MBKCS112
01541                                                                   MBKCS112
01542              END-PERFORM                                          MBKCS112
01543                                                                   MBKCS112
01544              MOVE PV-FIRST-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER      MBKCS112
01545                                                                   MBKCS112
01546              PERFORM Z100-READ-TEMP-STORAGE                       MBKCS112
01547          END-IF                                                   MBKCS112
01548                                                                   MBKCS112
01549      END-IF.                                                      MBKCS112
01550                                                                   MBKCS112
01551      IF DP020-FK-DESELECT(DP020-SRC-AID)                             CL*73
01552          MOVE +0 TO MB012-NUMBER-OF-ITEMS-SELECTED                   CL183
01553      END-IF.                                                         CL*73
01554                                                                      CL*73
01555      MOVE +0 TO MB012-START-OF-SELECTED-RANGE.                       CL183
01556      MOVE +0 TO MB012-END-OF-SELECTED-RANGE.                         CL183
01557                                                                      CL*92
01558      SET PS-SAME-ITEMS-DISPLAYED TO TRUE.                            CL*92
01559                                                                      CL*92
01560      PERFORM G100-DISPLAY-PANEL.                                  MBKCS112
01561  EJECT                                                            MBKCS112
01562  E800-CHECK-FOR-JUMP-BROWSE.                                      MBKCS112
01563                                                                   MBKCS112
01564 ******************************************************************MBKCS112
01565 *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO     MBKCS112
01566 *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.    MBKCS112
01567 *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST   MBKCS112
01568 *    RATHER THAN HITTING PF8 TO PAGE FORWARD TO THAT POINT.       MBKCS112
01569 *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT      MBKCS112
01570 *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.           MBKCS112
01571 *                                                                 MBKCS112
01572 ******************************************************************MBKCS112
01573                                                                   MBKCS112
01574      SET PS-VALID-BEG-ITEM TO TRUE.                               MBKCS112
01575                                                                   MBKCS112
01576      IF (ABEGITMA                      = PC-ERASE-EOF) OR            CL*57
01577         (MB012-NUMBER-OF-ITEMS-READ    = +0)                         CL183
01578          SET PS-SAME-ITEMS-DISPLAYED TO TRUE                         CL169
01579          PERFORM G100-DISPLAY-PANEL                               MBKCS112
01580      ELSE                                                         MBKCS112
01581                                                                   MBKCS112
01582          MOVE ABEGITMI             TO DP010I-UNEDITED-FIELD          CL*57
01583          MOVE PC-FIELD-LENGTH      TO DP010I-MAXIMUM-DIGITS          CL**2
01584          MOVE +0                   TO DP010I-MAXIMUM-DECIMALS        CL**2
01585          SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                  MBKCS112
262978         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262978              USING DP010I-NUMERIC-EDIT-AREA                              
01587                                                                      CL**2
01588 ******************************************************************   CL**2
01589 *    WE MUST MAKE SURE THAT THE NEW ITEM NUMBER THAT THE USER        CL**2
01590 *    WANTS TO JUMP TO WILL NOT REQUIRE US TO READ THE DATABASE       CL**2
01591 *    TOO MANY TIMES.  WE CALCULATE THE MAXIMUM ITEM NUMBER THAT      CL**2
01592 *    CAN BE AT THE TOP OF THE LIST WITHOUT EXCEEDING THE MAXIMUM     CL**2
01593 *    NUMBER OF READS THAT WE HAVE SET.  IF THE NUMBER THAT THE       CL**2
01594 *    USER WANTS AT THE TOP OF THE LIST IS GREATER THAN THE MAXIMUM   CL**2
01595 *    ITEM NUMBER THAT WE HAVE CALCULATED, A MESSAGE IS DISPLAYED,    CL**2
01596 *    AND WE START THE LIST USING THE MAXIMUM ITEM NUMBER.            CL**2
01597 ******************************************************************   CL**2
01598                                                                   MBKCS112
01599          IF NOT DP010I-ERROR-DETECTED                             MBKCS112
01600              MOVE DP010I-NUMERIC-FIELD TO MB013I-ABEGITMI            CL185
01601              MOVE MB013I-ABEGITMI      TO PV-BEGINNING-ITEM          CL185
01602                                                                   MBKCS112
01603              IF (PV-BEGINNING-ITEM >  0)          AND             MBKCS112
01604                 (PV-BEGINNING-ITEM <  PC-MAX-ITEMS)               MBKCS112
01605                  MOVE PV-BEGINNING-ITEM TO PV-TOP-ITEM            MBKCS112
01606                                                                   MBKCS112
01607                  COMPUTE PV-MAX-ITEMS-TO-BE-READ =                MBKCS112
01608                     ((MB012-NUMBER-OF-ITEMS-READ +                   CL183
01609                       PC-MAX-ITEMS-TO-BE-READ)   -                   CL**2
01610                       PC-ITEMS-PER-PANEL)        + 1                 CL**2
01611                                                                   MBKCS112
01612                  IF PV-TOP-ITEM > PV-MAX-ITEMS-TO-BE-READ         MBKCS112
01613                      MOVE PV-MAX-ITEMS-TO-BE-READ TO                 CL**2
01614                           PV-TOP-ITEM                                CL**2
01615                      MOVE -1                      TO ABEGITML        CL*33
01616                      IF MB012-NO-ITEMS-LEFT-ON-DB                    CL183
01617                          MOVE PC-TSYMSG-00070  TO                    CL179
01618                               DP020-MSG-NUMBER                       CL179
01619                      ELSE                                            CL**2
01620                          MOVE PC-TSYMSG-00207  TO                    CL*10
01621                               DP020-MSG-NUMBER                       CL179
01622                      END-IF                                       MBKCS112
01623                      SET PS-CURSOR-POSITIONED    TO TRUE             CL*53
01624                      SET DP020-MSG-INFORMATIONAL TO TRUE             CL*33
01625                  END-IF                                           MBKCS112
01626                                                                   MBKCS112
01627                  PERFORM F500-BROWSE                              MBKCS112
01628              ELSE                                                 MBKCS112
01629                  MOVE PC-TSYMSG-00005       TO DP020-MSG-NUMBER      CL179
01630                  MOVE -1                    TO ABEGITML              CL*33
01631                  MOVE DP015-REVERSE         TO ABEGITMH              CL*53
01632                  MOVE DP015-RED             TO ABEGITMC              CL*33
01633                  MOVE DP015-UNP-ALP-BRT-MDT TO ABEGITMA              CL167
01634                  SET PS-INVALID-BEG-ITEM    TO TRUE                  CL*33
01635                  SET PS-CURSOR-POSITIONED   TO TRUE                  CL*55
01636                  SET DP020-MSG-FATAL        TO TRUE                  CL*33
01637              END-IF                                               MBKCS112
01638                                                                   MBKCS112
01639          ELSE                                                     MBKCS112
01640              MOVE PC-TSYMSG-00008           TO DP020-MSG-NUMBER      CL179
01641              MOVE -1                        TO ABEGITML              CL*33
01642              MOVE DP015-REVERSE             TO ABEGITMH              CL*53
01643              MOVE DP015-RED                 TO ABEGITMC              CL*33
01644              MOVE DP015-UNP-ALP-BRT-MDT     TO ABEGITMA              CL167
01645              SET PS-INVALID-BEG-ITEM        TO TRUE                  CL*33
01646              SET PS-CURSOR-POSITIONED       TO TRUE                  CL*33
01647              SET DP020-MSG-FATAL            TO TRUE                  CL*33
01648          END-IF                                                   MBKCS112
01649                                                                   MBKCS112
01650      END-IF.                                                      MBKCS112
01651                                                                   MBKCS112
01652      IF PS-INVALID-BEG-ITEM                                       MBKCS112
01653          SET PS-SAME-ITEMS-DISPLAYED        TO TRUE                  CL*73
01654          PERFORM DP017-0000-GET-CURR-DATE-TIME                    MBKCS112
01655          MOVE DP017-EDITED-DATE-YYYYMMDD TO PC-CURRENT-DATE          CL189
01656          PERFORM I200-FORMAT-LIST-LINES                           MBKCS112
01657          PERFORM I300-FORMAT-NR-SELECTED                          MBKCS112
01658          PERFORM DP005-0000-FORMAT-MSG-LINES                      MBKCS112
01659          PERFORM DP008-0000-WRITE-PANEL                           MBKCS112
01660      END-IF.                                                      MBKCS112
01661  EJECT                                                            MBKCS112
01662  F100-EDIT.                                                       MBKCS112
01663                                                                   MBKCS112
01664 ******************************************************************MBKCS112
01665 *    EDIT THE SELECTION ENTRIES.                                  MBKCS112
01666 *                                                                 MBKCS112
01667 *    1) VALID ENTRIES ARE 'S' 'B' OR 'E'.                         MBKCS112
01668 *    2) THERE CAN ONLY BE 1 BEGINNING AND 1 ENDING RANGE.         MBKCS112
01669 *                                                                 MBKCS112
01670 *    PERFORMED BY E100-EDIT-SELECTIONS.                              CL*92
01671 ******************************************************************MBKCS112
01672                                                                   MBKCS112
01673      PERFORM K100-SET-SUBSCRIPTS.                                 MBKCS112
01674                                                                   MBKCS112
01675      IF NOT (MB012-NO-SELECT(MB012-BLOCK-SUB MB012-ITEM-SUB)) AND    CL183
01676         NOT (MB012-SELECT(MB012-BLOCK-SUB MB012-ITEM-SUB))           CL183
01677                                                                      CL**2
01678 ******************************************************************   CL**2
01679 *   IF THE CURRENT ENTRY IS A 'B', WE MUST MAKE SURE THAT THIS       CL**2
01680 *   IS THE ONLY 'B' ENTERED ON THE SCREEN.  TO VERIFY THIS, THE      CL**2
01681 *   CURRENT ITEM WE ARE POINTING TO SHOULD EQUAL THE START OF        CL**2
01682 *   SELECTED RANGE FIELD.  IF THEY ARE NOT EQUAL, AND THE START OF   CL**2
01683 *   SELECTED RANGE FIELD DOES NOT EQUAL 0, WE KNOW THAT THIS FIELD   CL**2
01684 *   CONTAINS THE LIST ITEM NUMBER THAT CURRENTLY CONTAINS A 'B'.     CL**2
01685 *   IF THE START OF SELECTED RANGE FIELD EQUALS 0, THAN THIS IS      CL**2
01686 *   THE FIRST 'B' ENTERED ON THE SCREEN, AND WE SET THIS FIELD       CL**2
01687 *   EQUAL TO THE CURRENT ITEM WE ARE POINTING TO.                    CL**2
01688 *                                                                    CL**2
01689 *   THE SAME TYPE OF LOGIC IS DONE FOR THE ENDING RANGE 'E'.         CL**2
01690 ******************************************************************   CL**2
01691                                                                   MBKCS112
01692          IF MB012-BEGIN(MB012-BLOCK-SUB MB012-ITEM-SUB)              CL183
01693                                                                   MBKCS112
01694              IF MB012-START-OF-SELECTED-RANGE NOT = PV-ITEM-IN-USE   CL183
01695                                                                   MBKCS112
01696                  IF MB012-START-OF-SELECTED-RANGE = +0               CL183
01697                      MOVE PV-ITEM-IN-USE TO                       MBKCS112
01698                          MB012-START-OF-SELECTED-RANGE               CL183
01699                  ELSE                                             MBKCS112
01700                                                                   MBKCS112
01701                      IF PS-VALID-SELECTIONS                       MBKCS112
01702                          MOVE PC-TSYMSG-00054 TO                     CL179
01703                               DP020-MSG-NUMBER                       CL179
01704                          IF NOT DP020-NEXT-ACT-ERROR                 CL*22
01705                              MOVE -1      TO MB013I-ASELL(PS-SUB)    CL185
01706                          END-IF                                      CL*22
01707                      END-IF                                       MBKCS112
01708                                                                   MBKCS112
01709                      SET DP020-MSG-FATAL       TO TRUE               CL*22
01710                      SET PS-INVALID-SELECTIONS TO TRUE               CL*22
01711                      SET PS-CURSOR-POSITIONED  TO TRUE               CL*53
01712                  END-IF                                           MBKCS112
01713              END-IF                                               MBKCS112
01714          ELSE                                                     MBKCS112
01715              IF MB012-END(MB012-BLOCK-SUB MB012-ITEM-SUB)            CL183
01716                                                                   MBKCS112
01717                  IF MB012-END-OF-SELECTED-RANGE NOT =                CL183
01718                     PV-ITEM-IN-USE                                MBKCS112
01719                                                                   MBKCS112
01720                      IF MB012-END-OF-SELECTED-RANGE = +0             CL183
01721                          MOVE PV-ITEM-IN-USE TO                   MBKCS112
01722                               MB012-END-OF-SELECTED-RANGE            CL183
01723                      ELSE                                         MBKCS112
01724                                                                   MBKCS112
01725                          IF PS-VALID-SELECTIONS                   MBKCS112
01726                              MOVE PC-TSYMSG-00055 TO                 CL179
01727                                   DP020-MSG-NUMBER                   CL179
01728                              IF NOT DP020-NEXT-ACT-ERROR             CL*22
01729                                 MOVE -1 TO MB013I-ASELL(PS-SUB)      CL185
01730                              END-IF                                  CL*22
01731                          END-IF                                   MBKCS112
01732                          SET DP020-MSG-FATAL       TO TRUE           CL*22
01733                          SET PS-INVALID-SELECTIONS TO TRUE        MBKCS112
01734                          SET PS-CURSOR-POSITIONED  TO TRUE           CL*53
01735                      END-IF                                       MBKCS112
01736                  END-IF                                           MBKCS112
01737              ELSE                                                 MBKCS112
01738                  IF PS-VALID-SELECTIONS                           MBKCS112
01739                      MOVE PC-TSYMSG-00005 TO DP020-MSG-NUMBER        CL179
01740                      IF NOT DP020-NEXT-ACT-ERROR                     CL*22
01741                         MOVE -1   TO MB013I-ASELL(PS-SUB)            CL185
01742                      END-IF                                          CL*22
01743                  END-IF                                              CL*22
01744                  SET DP020-MSG-FATAL       TO TRUE                   CL*22
01745                  SET PS-INVALID-SELECTIONS TO TRUE                   CL*22
01746                  SET PS-CURSOR-POSITIONED  TO TRUE                   CL*53
01747                  MOVE DP015-RED            TO MB013O-ASELC(PS-SUB)   CL185
01748                  MOVE DP015-REVERSE        TO MB013O-ASELH(PS-SUB)   CL185
01749              END-IF                                               MBKCS112
01750          END-IF                                                   MBKCS112
01751      END-IF.                                                      MBKCS112
01752                                                                   MBKCS112
01753      ADD +1 TO PV-ITEM-IN-USE.                                    MBKCS112
01754      ADD +1 TO PS-SUB.                                            MBKCS112
01755  EJECT                                                            MBKCS112
01756  F200-MARK-RANGE-AS-SELECTED.                                     MBKCS112
01757                                                                   MBKCS112
01758 ******************************************************************MBKCS112
01759 *    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE     MBKCS112
01760 *    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS     MBKCS112
01761 *    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.              MBKCS112
01762 *                                                                 MBKCS112
01763 *    PERFORMED BY E100-EDIT-SELECTIONS.                              CL*92
01764 ******************************************************************MBKCS112
01765                                                                   MBKCS112
01766      MOVE +1 TO MB012-BLOCK-SUB.                                     CL183
01767                                                                   MBKCS112
01768      PERFORM G200-MARK-SELECTED-INDS                              MBKCS112
01769          VARYING MB012-ITEM-SUB FROM 1 BY 1                          CL183
01770          UNTIL MB012-ITEM-SUB > PC-ITEMS-PER-BLOCK.                  CL183
01771                                                                   MBKCS112
01772      ADD +1 TO MB012-BLOCK-SUB.                                      CL183
01773                                                                   MBKCS112
01774      PERFORM G200-MARK-SELECTED-INDS                              MBKCS112
01775          VARYING MB012-ITEM-SUB FROM 1 BY 1                          CL183
01776          UNTIL MB012-ITEM-SUB > PC-ITEMS-PER-BLOCK.                  CL183
01777                                                                      CL**2
01778 ******************************************************************   CL**2
01779 *    AFTER MARKING THE ENTRIES IN THE BLOCKS IN WORKING STORAGE,     CL**2
01780 *    WE MUST SEE IF ANY OF THE ENTRIES IN THE BLOCKS IN TEMPORARY    CL**2
01781 *    STORAGE SHOULD BE MARKED.  TO DETERMINE THIS, WE CALCULATE      CL**2
01782 *    THE BLOCK NUMBERS OF THE BLOCKS IN WORKING STORAGE AND THE      CL**2
01783 *    BLOCK NUMBERS OF THE BLOCKS IN TEMPORARY STORAGE THAT FALL      CL**2
01784 *    WITHIN THE STARTING AND ENDING RANGE SPECIFED BY THE USER.      CL**2
01785 *    IF THE BLOCK NUMBERS OF THE BLOCKS IN TEMPORARY STORAGE         CL**2
01786 *    ARE LESS THAN OR GREATER THAN THE BLOCK NUMBERS OF THE          CL**2
01787 *    BLOCKS IN WORKING STORAGE, WE MUST READ THOSE BLOCKS FROM       CL**2
01788 *    TEMPORARY STORAGE AND MARK THE APPROPRIATE ENTRIES AS BEING     CL**2
01789 *    SELECTED.                                                       CL**2
01790 ******************************************************************   CL**2
01791                                                                   MBKCS112
01792      MOVE MB012-LIST-ITEM-NUMBER(1 1)   TO                           CL183
01793           PV-LIST-ITEM-NUMBER.                                    MBKCS112
01794                                                                   MBKCS112
01795      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            MBKCS112
01796          1 + ((PV-LIST-ITEM-NUMBER - 1) /                         MBKCS112
01797                PC-ITEMS-PER-BLOCK).                               MBKCS112
01798                                                                   MBKCS112
01799      MOVE MB012-START-OF-SELECTED-RANGE TO                           CL183
01800           PV-START-OF-SELECTED-RANGE.                             MBKCS112
01801                                                                   MBKCS112
01802      COMPUTE PV-FIRST-BLOCK-IN-RANGE =                            MBKCS112
01803          1 + ((PV-START-OF-SELECTED-RANGE - 1) /                  MBKCS112
01804                PC-ITEMS-PER-BLOCK).                               MBKCS112
01805                                                                   MBKCS112
01806      MOVE MB012-END-OF-SELECTED-RANGE   TO                           CL183
01807           PV-END-OF-SELECTED-RANGE.                               MBKCS112
01808                                                                   MBKCS112
01809      COMPUTE PV-LAST-BLOCK-IN-RANGE =                             MBKCS112
01810          1 + ((PV-END-OF-SELECTED-RANGE - 1) /                    MBKCS112
01811                PC-ITEMS-PER-BLOCK).                               MBKCS112
01812                                                                   MBKCS112
01813      COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                             MBKCS112
01814          PV-FIRST-BLOCK-IN-ARRAY + 1.                             MBKCS112
01815                                                                   MBKCS112
01816      IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR    MBKCS112
01817         (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)        MBKCS112
01818          MOVE +1 TO MB012-BLOCK-SUB                                  CL183
01819                                                                   MBKCS112
01820          PERFORM Z150-WRITE-TEMP-STORAGE                          MBKCS112
01821                                                                   MBKCS112
01822          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         MBKCS112
01823              PV-FIRST-BLOCK-IN-ARRAY - 1                          MBKCS112
01824                                                                   MBKCS112
01825          PERFORM                                                  MBKCS112
01826              UNTIL PV-FIRST-BLOCK-IN-RANGE >                      MBKCS112
01827                    PV-PREV-BLOCK-IN-ARRAY                         MBKCS112
01828                                                                   MBKCS112
01829              MOVE PV-FIRST-BLOCK-IN-RANGE TO PV-BLOCK-NUMBER      MBKCS112
01830                                                                   MBKCS112
01831              PERFORM Z100-READ-TEMP-STORAGE                       MBKCS112
01832                                                                   MBKCS112
01833              PERFORM G200-MARK-SELECTED-INDS                      MBKCS112
01834                  VARYING MB012-ITEM-SUB FROM 1 BY 1                  CL183
01835                  UNTIL MB012-ITEM-SUB > PC-ITEMS-PER-BLOCK           CL183
01836                                                                   MBKCS112
01837              PERFORM Z150-WRITE-TEMP-STORAGE                      MBKCS112
01838                                                                   MBKCS112
01839              ADD +1 TO PV-FIRST-BLOCK-IN-RANGE                    MBKCS112
01840                                                                   MBKCS112
01841          END-PERFORM                                              MBKCS112
01842                                                                   MBKCS112
01843          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         MBKCS112
01844              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     MBKCS112
01845                                                                   MBKCS112
01846          PERFORM                                                  MBKCS112
01847              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       MBKCS112
01848                    PV-LAST-BLOCK-IN-RANGE                         MBKCS112
01849                                                                   MBKCS112
01850              MOVE PV-NEXT-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER       MBKCS112
01851                                                                   MBKCS112
01852              PERFORM Z100-READ-TEMP-STORAGE                       MBKCS112
01853                                                                   MBKCS112
01854              PERFORM G200-MARK-SELECTED-INDS                      MBKCS112
01855                  VARYING MB012-ITEM-SUB FROM 1 BY 1                  CL183
01856                  UNTIL MB012-ITEM-SUB > PC-ITEMS-PER-BLOCK           CL183
01857                                                                   MBKCS112
01858              PERFORM Z150-WRITE-TEMP-STORAGE                      MBKCS112
01859                                                                   MBKCS112
01860              ADD +1 TO PV-NEXT-BLOCK-IN-ARRAY                     MBKCS112
01861                                                                   MBKCS112
01862          END-PERFORM                                              MBKCS112
01863                                                                   MBKCS112
01864          MOVE PV-FIRST-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER          MBKCS112
01865                                                                   MBKCS112
01866          PERFORM Z100-READ-TEMP-STORAGE                           MBKCS112
01867      END-IF.                                                      MBKCS112
01868                                                                   MBKCS112
01869      MOVE +0 TO MB012-START-OF-SELECTED-RANGE.                       CL183
01870      MOVE +0 TO MB012-END-OF-SELECTED-RANGE.                         CL183
01871  EJECT                                                            MBKCS112
01872  F300-CHECK-SELECTIONS.                                           MBKCS112
01873                                                                   MBKCS112
01874 ******************************************************************MBKCS112
01875 *    CHECK EACH ENTRY TO DETERMINE IF THE SELECTION CRITERIA IN   MBKCS112
01876 *    THE MAP DIFFERS FROM THAT IN THE ARRAY.  IF THEY DO DIFFER,  MBKCS112
01877 *    AND THE ARRAY ENTRY SHOWS THAT THE ITEM WAS PREVIOUSLY       MBKCS112
01878 *    SELECTED, SUBTRACT 1 FROM THE SELECTED COUNTER.  IF THEY DO  MBKCS112
01879 *    DIFFER, AND THE ARRAY ENTRY SHOWS THAT THIS ITEM WAS MARKED  MBKCS112
01880 *    AS EITHER THE BEGINNING OR ENDING SELECTION POINT, ZERO OUT  MBKCS112
01881 *    THE STARTING RANGE AND/OR ENDING RANGE.  IF THE ENTRY ON     MBKCS112
01882 *    THE MAP SHOWS THAT THIS ITEM WAS SELECTED, ADD 1 TO THE      MBKCS112
01883 *    SELECTED COUNTER.                                            MBKCS112
01884 *                                                                 MBKCS112
01885 *    PERFORMED BY E200-MOVE-SELECTIONS-TO-COMMAREA.                  CL*92
01886 ******************************************************************MBKCS112
01887                                                                   MBKCS112
01888      IF (PV-ITEM-IN-USE < MB012-NUMBER-OF-ITEMS-READ) OR             CL183
01889         (PV-ITEM-IN-USE = MB012-NUMBER-OF-ITEMS-READ)                CL183
01890                                                                   MBKCS112
01891          PERFORM K100-SET-SUBSCRIPTS                              MBKCS112
01892                                                                      CL**2
01893 ******************************************************************   CL**2
01894 *    IF THE ENTRY IN THE BLOCK DOES NOT EQUAL THE ENTRY ON THE       CL**2
01895 *    MAP, SET THE SWITCH SAYING THAT THIS BLOCK HAS BEEN MODIFIED.   CL**2
01896 *                                                                    CL**2
01897 *    IF THE ENTRY IN THE BLOCK EQUALS 'S', SUBTRACT 1 FROM THE       CL**2
01898 *    NUMBER OF SELECTED ITEMS.                                       CL**2
01899 *                                                                    CL**2
01900 *    IF THE ENTRY IN THE BLOCK EQUALS 'B' AND THIS ENTRY WAS EQUAL   CL**2
01901 *    TO THE START OF SELECTED RANGE, MOVE 0 TO THE START OF          CL**2
01902 *    SELECTED RANGE.  IF THERE WAS MORE THAN ONE 'B' ON THE          CL**2
01903 *    SCREEN (ERROR), THE START OF SELECTED RANGE WILL THAN BE        CL**2
01904 *    RESET LATER TO POINT TO ONE OF THE OTHER 'B' ENTRIES.           CL**2
01905 *                                                                    CL**2
01906 *    THE SAME LOGIC APPLIES TO AN 'E' ENTRY.                         CL**2
01907 *                                                                    CL**2
01908 ******************************************************************   CL**2
01909                                                                   MBKCS112
01910          IF MB012-SELECTED-SW(MB012-BLOCK-SUB                        CL183
01911                               MB012-ITEM-SUB) NOT =                  CL183
01912             MB013I-ASELI(PS-SUB)                                     CL185
01913                                                                      CL**2
01914              SET MB012-BLOCK-MODIFIED(MB012-BLOCK-SUB) TO TRUE       CL183
01915                                                                   MBKCS112
01916              IF MB012-SELECT(MB012-BLOCK-SUB MB012-ITEM-SUB)         CL183
01917                  SUBTRACT +1 FROM MB012-NUMBER-OF-ITEMS-SELECTED     CL183
01918              ELSE                                                 MBKCS112
01919                                                                   MBKCS112
01920                  IF MB012-BEGIN(MB012-BLOCK-SUB MB012-ITEM-SUB)      CL183
01921                                                                   MBKCS112
01922                      IF MB012-LIST-ITEM-NUMBER(MB012-BLOCK-SUB       CL183
01923                                                MB012-ITEM-SUB) =     CL183
01924                         MB012-START-OF-SELECTED-RANGE                CL183
01925                                                                      CL**2
01926                          MOVE +0 TO MB012-START-OF-SELECTED-RANGE    CL183
01927                      END-IF                                       MBKCS112
01928                                                                   MBKCS112
01929                  ELSE                                             MBKCS112
01930                                                                   MBKCS112
01931                      IF MB012-END(MB012-BLOCK-SUB MB012-ITEM-SUB)    CL183
01932                                                                   MBKCS112
01933                          IF MB012-LIST-ITEM-NUMBER(MB012-BLOCK-SUB   CL183
01934                                                    MB012-ITEM-SUB)   CL183
01935                          =  MB012-END-OF-SELECTED-RANGE              CL183
01936                                                                      CL**2
01937                              MOVE +0 TO                           MBKCS112
01938                                   MB012-END-OF-SELECTED-RANGE        CL183
01939                          END-IF                                   MBKCS112
01940                                                                   MBKCS112
01941                      END-IF                                       MBKCS112
01942                                                                   MBKCS112
01943                  END-IF                                           MBKCS112
01944                                                                   MBKCS112
01945              END-IF                                               MBKCS112
01946                                                                      CL**2
01947 ******************************************************************   CL**2
01948 *    IF THE ENTRY IN THE MAP EQUALS 'S', ADD 1 TO THE NUMBER OF      CL**2
01949 *    SELECTED ITEMS.                                                 CL**2
01950 *                                                                    CL**2
01951 *    IF THE ENTRY IN THE MAP EQUALS SPACES OR ERASE EOF, MOVE        CL**2
01952 *    SPACES TO THE ENTRY IN THE BLOCK.                               CL**2
01953 *                                                                    CL**2
01954 *    IF THE ENTRY IN THE MAP DOES NOT EQUAL SPACES OR ERASE EOF,     CL**2
01955 *    MOVE WHATEVER WAS ENTERED ON THE MAP TO THE ENTRY IN THE        CL**2
01956 *    BLOCK.                                                          CL**2
01957 *                                                                    CL**2
01958 ******************************************************************   CL**2
01959                                                                   MBKCS112
01960              IF MB013I-ASELI(PS-SUB) = PC-SELECTED                   CL185
01961                  ADD +1 TO MB012-NUMBER-OF-ITEMS-SELECTED            CL183
01962              END-IF                                               MBKCS112
01963                                                                   MBKCS112
01964              IF (MB013I-ASELA(PS-SUB) = PC-ERASE-EOF) OR             CL185
01965                 (MB013I-ASELI(PS-SUB) = SPACES)       OR             CL185
01966                 (MB013I-ASELI(PS-SUB) = LOW-VALUES)                  CL185
01967                  MOVE SPACES                TO                       CL*73
01968                       MB012-SELECTED-SW(MB012-BLOCK-SUB              CL183
01969                                         MB012-ITEM-SUB)              CL183
01970              ELSE                                                 MBKCS112
01971                  MOVE MB013I-ASELI(PS-SUB) TO                        CL185
01972                       MB012-SELECTED-SW(MB012-BLOCK-SUB              CL183
01973                                         MB012-ITEM-SUB)              CL183
01974              END-IF                                               MBKCS112
01975                                                                   MBKCS112
01976          END-IF                                                   MBKCS112
01977                                                                   MBKCS112
01978      END-IF.                                                      MBKCS112
01979                                                                   MBKCS112
01980      ADD +1 TO PV-ITEM-IN-USE.                                    MBKCS112
01981      ADD +1 TO PS-SUB.                                            MBKCS112
01982  EJECT                                                            MBKCS112
01983  F400-SETUP-INTER-APPL.                                           MBKCS112
01984                                                                   MBKCS112
01985 ******************************************************************MBKCS112
01986 *    PLACING THE 4 DIGIT CODE FOR THIS TRANSACTION IN THE            CL**2
01987 *    STANDARD COMMAREA WILL TELL THE TRANSACTION THAT WE ARE         CL**2
01988 *    GOING TO WHETHER OR NOT THAT THEY CAN USE THE INTER             CL**2
01989 *    APPLICATION COMMAREA.                                           CL**2
01990 *                                                                    CL**2
01991 *    IN THE INTER APPLICATION COMMAREA, WE MUST PUT THE TEMPORARY    CL**2
01992 *    STORAGE QUEUE NAME AND THE NUMBER OF SELECTED ITEMS.  THIS      CL**2
01993 *    WILL ALLOW THE TRANSACTION WE ARE GOING TO, TO READ THE         CL**2
01994 *    QUEUE AND PROCESS THE SELECTED ITEMS.                           CL**2
01995 *                                                                    CL**2
01996 *    PERFORMED BY E300-NAVIGATE.                                     CL*92
01997 ******************************************************************MBKCS112
01998                                                                   MBKCS112
01999 *    MOVE LOW-VALUES               TO INTER-APPL-COMM-AREA.          CL242
02000                                                                   MBKCS112
02001                                                                   MBKCS112
02002      MOVE MB012-TEMP-QUEUE-NAME          TO                          CL183
02003           MB005-LIST-QUEUE-NAME.                                     CL243
02004      MOVE MB012-NUMBER-OF-ITEMS-SELECTED TO                          CL183
02005           MB005-NUMBER-OF-SELECTED-ITEMS.                            CL242
02006      MOVE MB012-CO-NUMBER                TO                          CL183
02007           MB005-CO-NUMBER.                                           CL242
02008      MOVE MB012-GMA-NUMBER               TO                          CL183
02009           MB005-GMA-NUMBER.                                          CL242
02010      MOVE MB012-DMA-NUMBER               TO                          CL183
02011           MB005-DMA-NUMBER.                                          CL242
02012      MOVE MB012-DEPT-NUMBER              TO                          CL183
02013           MB005-DEPT-NUMBER.                                         CL242
02014      MOVE MB012-HISTORY-FLAG             TO                          CL220
02015           MB005-HISTORY-INDICATOR.                                   CL243
02016  EJECT                                                            MBKCS112
02017  F500-BROWSE.                                                     MBKCS112
02018                                                                   MBKCS112
02019 ******************************************************************MBKCS112
02020 *    CALCULATE THE NEW TOP AND BOTTOM ITEM NUMBERS BASED ON WHERE MBKCS112
02021 *    THE USER SPECIFIED TO GO ON THE LIST AND DETERMINE WHERE     MBKCS112
02022 *    THESE ITEMS CAN BE FOUND (I.E. ARRAY, TEMPORARY STORAGE).    MBKCS112
02023 *                                                                 MBKCS112
02024 *    PERFORMED BY E400-DISPLAY-TOP-OF-LIST.                          CL*92
02025 *    PERFORMED BY E500-BROWSE-BACKWARD.                              CL*92
02026 *    PERFORMED BY E600-BROWSE-FORWARD.                               CL*92
02027 *    PERFORMED BY E800-CHECK-FOR-JUMP-BROWSE.                        CL*92
02028 ******************************************************************MBKCS112
02029                                                                      CL**2
02030 ******************************************************************   CL**2
02031 *    USER HITS PAGE BACKWARD WHEN ALREADY AT THE TOP OF THE LIST.    CL**2
02032 ******************************************************************   CL**2
02033                                                                   MBKCS112
02034      IF PV-TOP-ITEM < +1                                          MBKCS112
02035          MOVE +1                         TO PV-TOP-ITEM              CL*28
02036                                                                   MBKCS112
02037          IF PS-CURSOR-NOT-POSITIONED                                 CL*28
02038              MOVE PC-TSYMSG-00069        TO DP020-MSG-NUMBER         CL179
02039              SET PS-CURSOR-POSITIONED    TO TRUE                     CL*28
02040              SET PS-INFORMATIONAL        TO TRUE                     CL*28
02041              SET DP020-MSG-INFORMATIONAL TO TRUE                     CL*28
02042          END-IF                                                      CL*28
02043      END-IF.                                                      MBKCS112
02044                                                                      CL**2
02045      IF MB012-NO-ITEMS-LEFT-ON-DB                                    CL183
02046                                                                      CL**2
02047 ******************************************************************   CL**2
02048 *    USER HITS PAGE FORWARD WHEN ALREADY AT THE END OF THE LIST.     CL**2
02049 ******************************************************************   CL**2
02050                                                                   MBKCS112
02051          IF PV-TOP-ITEM > MB012-NUMBER-OF-ITEMS-READ                 CL183
02052              IF  DP020-SRC-AID EQUAL DP016-ENTER                     CL*12
02053                  MOVE MB012-NUMBER-OF-ITEMS-READ TO PV-TOP-ITEM      CL*12
02054              ELSE                                                    CL*12
02055                  MOVE MB012-ITEM-AT-TOP-OF-PANEL TO PV-TOP-ITEM      CL*12
02056              END-IF                                                  CL*12
02057                                                                   MBKCS112
02058              IF PS-CURSOR-NOT-POSITIONED                             CL*28
02059                  MOVE -1                     TO ANAVCMDL             CL*28
02060                  SET DP020-MSG-INFORMATIONAL TO TRUE                 CL*28
02061                  SET PS-CURSOR-POSITIONED    TO TRUE                 CL*52
02062                  SET PS-INFORMATIONAL        TO TRUE                 CL*10
02063                  MOVE PC-TSYMSG-00070        TO DP020-MSG-NUMBER     CL*10
02064                                                                      CL*28
02065              END-IF                                                  CL*28
02066                                                                   MBKCS112
02067          END-IF                                                   MBKCS112
02068                                                                   MBKCS112
02069      END-IF.                                                      MBKCS112
02070                                                                   MBKCS112
02071      COMPUTE PV-BOTTOM-ITEM =                                     MBKCS112
02072          (PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1.                  MBKCS112
02073                                                                   MBKCS112
02074      IF MB012-NO-ITEMS-LEFT-ON-DB                                    CL183
02075                                                                      CL**2
02076 ******************************************************************   CL**2
02077 *    USER HITS PAGE FORWARD, AND THE NEXT PAGE WILL CAUSE US         CL**2
02078 *    TO BE AT THE END OF THE LIST.  IF TOP ITEM EQUALS 1, THE        CL**2
02079 *    MESSAGE WOULD HAVE BEEN FLAGGED ALREADY ABOVE.                  CL**2
02080 ******************************************************************   CL**2
02081                                                                   MBKCS112
02082          IF PV-BOTTOM-ITEM > MB012-NUMBER-OF-ITEMS-READ              CL183
02083              MOVE MB012-NUMBER-OF-ITEMS-READ TO PV-BOTTOM-ITEM       CL183
02084                                                                   MBKCS112
02085              IF PS-CURSOR-NOT-POSITIONED                             CL*69
02086                                                                      CL*69
02087                  IF PV-TOP-ITEM NOT = +1                             CL*69
02088                      SET PS-INFORMATIONAL        TO TRUE             CL*69
02089                      SET PS-CURSOR-POSITIONED    TO TRUE             CL*69
02090                      SET DP020-MSG-INFORMATIONAL TO TRUE             CL*69
02091                      MOVE PC-TSYMSG-00070        TO                  CL179
02092                           DP020-MSG-NUMBER                           CL*69
02093                  END-IF                                              CL*69
02094                                                                      CL*69
02095              END-IF                                                  CL*69
02096                                                                   MBKCS112
02097          END-IF                                                   MBKCS112
02098                                                                   MBKCS112
02099      END-IF.                                                      MBKCS112
02100                                                                      CL**2
02101 ******************************************************************   CL**2
02102 *    DETEMINE WHETHER THE ITEMS WE NEED ARE IN WORKING STORAGE,      CL**2
02103 *    TEMPORARY STORAGE, OR NEITHER.                                  CL**2
02104 ******************************************************************   CL**2
02105                                                                   MBKCS112
02106      PERFORM G300-DETERMINE-WHAT-IS-WHERE.                        MBKCS112
02107                                                                   MBKCS112
02108      IF PS-TOP-ITEM-IN-ARRAY                                      MBKCS112
02109                                                                   MBKCS112
02110          IF PS-BOTTOM-ITEM-NOT-IN-ARRAY                           MBKCS112
02111                                                                      CL**2
02112 ******************************************************************   CL**2
02113 *    TOP ITEM IS IN WORKING STORAGE AND BOTTOM ITEM IS IN            CL**2
02114 *    TEMPORARY STORAGE.                                              CL**2
02115 ******************************************************************   CL**2
02116                                                                   MBKCS112
02117              IF PS-BOTTOM-ITEM-IN-TEMP                            MBKCS112
02118                  PERFORM G500-SCROLL-FORWARD                      MBKCS112
02119              ELSE                                                 MBKCS112
02120                                                                      CL**2
02121 ******************************************************************   CL**2
02122 *    TOP ITEM IS IN WORKING STORAGE BUT BOTTOM ITEM HAS NOT          CL**2
02123 *    BEEN READ YET.  IF THE LAST LOGICAL BLOCK IS NOT IN WORKING     CL**2
02124 *    STORAGE, READ IT INTO WORKING STORAGE SO WE CAN START FILLING   CL**2
02125 *    IT UP WITH THE NEW ITEMS READ.                                  CL**2
02126 ******************************************************************   CL**2
02127                                                                   MBKCS112
02128                  IF PS-LAST-BLOCK-NOT-IN-ARRAY                    MBKCS112
02129                      PERFORM G500-SCROLL-FORWARD                  MBKCS112
02130                  END-IF                                           MBKCS112
02131                                                                   MBKCS112
02132                  PERFORM G400-READ-ITEMS-INTO-ARRAY               MBKCS112
02133                                                                   MBKCS112
02134              END-IF                                               MBKCS112
02135                                                                   MBKCS112
02136          END-IF                                                   MBKCS112
02137                                                                   MBKCS112
02138      ELSE                                                         MBKCS112
02139                                                                      CL**2
02140 ******************************************************************   CL**2
02141 *    BOTTOM ITEM IS IN WORKING STORAGE AND TOP ITEM IS IN            CL**2
02142 *    TEMPORARY STORAGE.                                              CL**2
02143 ******************************************************************   CL**2
02144                                                                   MBKCS112
02145          IF PS-BOTTOM-ITEM-IN-ARRAY                               MBKCS112
02146              PERFORM G600-SCROLL-BACKWARD                         MBKCS112
02147          ELSE                                                     MBKCS112
02148                                                                      CL**2
02149 ******************************************************************   CL**2
02150 *    TOP ITEM AND BOTTOM ITEM ARE NOT IN WORKING STORAGE.            CL**2
02151 ******************************************************************   CL**2
02152                                                                   MBKCS112
02153              IF PS-TOP-ITEM-IN-TEMP                               MBKCS112
02154                  PERFORM G700-GET-TOP-ITEMS-BLOCK                 MBKCS112
02155                  PERFORM G300-DETERMINE-WHAT-IS-WHERE             MBKCS112
02156                                                                      CL**2
02157 ******************************************************************   CL**2
02158 *    TOP ITEM IS IN TEMPORARY STORAGE AND BOTTOM ITEM HAS NOT        CL**2
02159 *    BEEN READ YET.                                                  CL**2
02160 ******************************************************************   CL**2
02161                                                                   MBKCS112
02162                  IF PS-BOTTOM-ITEM-NOT-IN-ARRAY                   MBKCS112
02163                      PERFORM G400-READ-ITEMS-INTO-ARRAY           MBKCS112
02164                  END-IF                                           MBKCS112
02165                                                                   MBKCS112
02166              ELSE                                                 MBKCS112
02167                                                                      CL**2
02168 ******************************************************************   CL**2
02169 *    TOP AND BOTTOM ITEM HAVE NOT BEEN READ YET.  IF THE LAST        CL**2
02170 *    LOGICAL BLOCK IS NOT IN WORKING STORAGE, READ IT INTO           CL**4
02171 *    WORKING STORAGE SO WE CAN START FILLING IT UP WITH THE          CL**4
02172 *    NEW ITEMS READ.                                                 CL**2
02173 ******************************************************************   CL**2
02174                                                                   MBKCS112
02175                  IF PS-LAST-BLOCK-NOT-IN-ARRAY                    MBKCS112
02176                      PERFORM G800-GET-LAST-BLOCK-IN-TEMP          MBKCS112
02177                  END-IF                                           MBKCS112
02178                                                                   MBKCS112
02179                  PERFORM G400-READ-ITEMS-INTO-ARRAY               MBKCS112
02180              END-IF                                               MBKCS112
02181                                                                   MBKCS112
02182          END-IF                                                   MBKCS112
02183                                                                   MBKCS112
02184      END-IF.                                                      MBKCS112
02185                                                                   MBKCS112
02186      IF PV-TOP-ITEM = MB012-ITEM-AT-TOP-OF-PANEL                     CL183
02187          SET PS-SAME-ITEMS-DISPLAYED      TO TRUE                    CL*69
02188      ELSE                                                            CL*69
02189          SET PS-DIFFERENT-ITEMS-DISPLAYED TO TRUE                    CL*69
02190          MOVE PV-TOP-ITEM TO MB012-ITEM-AT-TOP-OF-PANEL              CL183
02191      END-IF.                                                         CL*69
02192                                                                   MBKCS112
02193      PERFORM G100-DISPLAY-PANEL.                                  MBKCS112
02194  EJECT                                                            MBKCS112
02195  F600-CLEAR-SELECTED-INDS.                                        MBKCS112
02196                                                                   MBKCS112
02197 ******************************************************************MBKCS112
02198 *    MARK EACH ITEM AS BEING NOT SELECTED OR SELECTED BASED ON       CL*93
02199 *    WHICH FUNCTION KEY THAT WAS HIT (DESELECT ALL OR SELECT ALL)    CL*93
02200 *                                                                 MBKCS112
02201 *    PERFORMED BY E700-SELECT-DESELECT-ALL.                          CL*93
02202 ******************************************************************MBKCS112
02203                                                                   MBKCS112
02204      IF MB012-LIST-ITEM-NUMBER(MB012-BLOCK-SUB                       CL183
02205                                MB012-ITEM-SUB) NOT = +0              CL183
02206                                                                   MBKCS112
02207          IF DP020-FK-DESELECT(DP020-SRC-AID)                         CL*32
02208                                                                      CL*32
02209              IF NOT MB012-NO-SELECT(MB012-BLOCK-SUB                  CL183
02210                                     MB012-ITEM-SUB)                  CL183
02211                  SET MB012-NO-SELECT(MB012-BLOCK-SUB                 CL183
02212                                      MB012-ITEM-SUB)   TO TRUE       CL183
02213                                                                   MBKCS112
02214                  SET MB012-BLOCK-MODIFIED(MB012-BLOCK-SUB)           CL183
02215                                                        TO TRUE       CL*32
02216              END-IF                                                  CL*32
02217          ELSE                                                        CL*32
02218              IF NOT MB012-SELECT(MB012-BLOCK-SUB MB012-ITEM-SUB)     CL183
02219                  SET MB012-SELECT(MB012-BLOCK-SUB                    CL183
02220                                   MB012-ITEM-SUB)      TO TRUE       CL183
02221                  ADD +1 TO MB012-NUMBER-OF-ITEMS-SELECTED            CL183
02222                  SET MB012-BLOCK-MODIFIED(MB012-BLOCK-SUB)           CL183
02223                                                        TO TRUE       CL*32
02224              END-IF                                                  CL*32
02225                                                                      CL*32
02226          END-IF                                                      CL*32
02227                                                                   MBKCS112
02228      END-IF.                                                      MBKCS112
02229  EJECT                                                            MBKCS112
02230  G100-DISPLAY-PANEL.                                              MBKCS112
02231                                                                   MBKCS112
02232 ******************************************************************MBKCS112
02233 *    FILL THE MAP WITH THE NECESSARY DATA AND WRITE IT OUT.       MBKCS112
02234 *                                                                 MBKCS112
02235 ******************************************************************MBKCS112
02236                                                                   MBKCS112
02237      PERFORM DP017-0000-GET-CURR-DATE-TIME.                       MBKCS112
02238      MOVE DP017-EDITED-DATE-YYYYMMDD TO PC-CURRENT-DATE.             CL189
02239      MOVE DP017-EDITED-TIME-HMS                                      CL**9
02240                                TO  ATIMEO.                           CL**9
02241                                                                   MBKCS112
02242      IF PS-SEND-MAP-N-DATA-FRSET                                  MBKCS112
02243          PERFORM DP002-0000-FORMAT-TITLE-LINES                    MBKCS112
02244      END-IF.                                                      MBKCS112
02245                                                                   MBKCS112
02246      PERFORM H200-FORMAT-PANEL-BODY.                              MBKCS112
02247                                                                   MBKCS112
02248      PERFORM DP005-0000-FORMAT-MSG-LINES.                         MBKCS112
02249                                                                   MBKCS112
02250      IF PS-SEND-MAP-N-DATA-FRSET                                  MBKCS112
02251          PERFORM DP009-0000-LOAD-FK-LINES                         MBKCS112
02252      END-IF.                                                      MBKCS112
02253                                                                   MBKCS112
02254      PERFORM DP008-0000-WRITE-PANEL.                              MBKCS112
02255  EJECT                                                            MBKCS112
02256  G200-MARK-SELECTED-INDS.                                         MBKCS112
02257                                                                   MBKCS112
02258 ******************************************************************MBKCS112
02259 *    MARK EACH ITEM THAT FALLS WITHIN THE BEGINNING AND ENDING    MBKCS112
02260 *    RANGE AS BEING SELECTED.                                     MBKCS112
02261 *                                                                 MBKCS112
02262 *    PERFORMED BY F200-MARK-RANGE-AS-SELECTED.                       CL*92
02263 ******************************************************************MBKCS112
02264                                                                   MBKCS112
02265      IF (MB012-LIST-ITEM-NUMBER(MB012-BLOCK-SUB                      CL183
02266                                 MB012-ITEM-SUB) >                    CL183
02267          MB012-START-OF-SELECTED-RANGE) OR                           CL183
02268                                                                      CL**4
02269         (MB012-LIST-ITEM-NUMBER(MB012-BLOCK-SUB                      CL183
02270                                 MB012-ITEM-SUB) =                    CL183
02271          MB012-START-OF-SELECTED-RANGE)                              CL183
02272                                                                   MBKCS112
02273          IF (MB012-LIST-ITEM-NUMBER(MB012-BLOCK-SUB                  CL183
02274                                     MB012-ITEM-SUB) <                CL183
02275              MB012-END-OF-SELECTED-RANGE) OR                         CL183
02276                                                                      CL**4
02277             (MB012-LIST-ITEM-NUMBER(MB012-BLOCK-SUB                  CL183
02278                                     MB012-ITEM-SUB) =                CL183
02279              MB012-END-OF-SELECTED-RANGE)                            CL183
02280                                                                   MBKCS112
02281              IF NOT MB012-SELECT(MB012-BLOCK-SUB MB012-ITEM-SUB)     CL183
02282                                                                      CL**2
02283                  SET MB012-SELECT(MB012-BLOCK-SUB                    CL183
02284                                   MB012-ITEM-SUB)          TO TRUE   CL183
02285                                                                      CL**2
02286                  SET MB012-BLOCK-MODIFIED(MB012-BLOCK-SUB) TO TRUE   CL183
02287                                                                   MBKCS112
02288                  ADD +1 TO MB012-NUMBER-OF-ITEMS-SELECTED            CL183
02289              END-IF                                               MBKCS112
02290                                                                   MBKCS112
02291          END-IF                                                   MBKCS112
02292                                                                   MBKCS112
02293      END-IF.                                                      MBKCS112
02294  EJECT                                                            MBKCS112
02295  G300-DETERMINE-WHAT-IS-WHERE.                                    MBKCS112
02296                                                                   MBKCS112
02297 ******************************************************************MBKCS112
02298 *    DETERMINE WHETHER THE TOP ITEM AND BOTTOM ITEM CAN BE FOUND  MBKCS112
02299 *    IN THE ARRAY, IN TEMPORARY STORAGE, OR NEITHER.              MBKCS112
02300 *                                                                 MBKCS112
02301 *    PERFORMED BY F500-BROWSE.                                       CL*93
02302 ******************************************************************MBKCS112
02303                                                                   MBKCS112
02304 ******************************************************************   CL**2
02305 *    CALCULATE THE FIRST-ITEM-IN-ARRAY (WORKING   STORAGE)           CL**2
02306 *                  LAST-ITEM-IN-ARRAY  (WORKING   STORAGE)           CL**2
02307 *                  LAST-ITEM-IN-TEMP   (TEMPORARY STORAGE)           CL**2
02308 *                                                                    CL**2
02309 *    BY COMPARING THE TOP AND BOTTOM ITEMS TO THESE, WE CAN          CL**2
02310 *    FIGURE OUT WHERE THE ITEMS ARE STORED.                          CL**2
02311 ******************************************************************   CL**2
02312                                                                      CL**2
02313      MOVE MB012-LIST-ITEM-NUMBER(1 1) TO PV-FIRST-ITEM-IN-ARRAY.     CL183
02314                                                                      CL**2
02315                                                                   MBKCS112
02316      COMPUTE PV-LAST-ITEM-IN-ARRAY =                              MBKCS112
02317          (PV-FIRST-ITEM-IN-ARRAY +                                MBKCS112
02318          (PC-MAX-BLOCKS-IN-ARRAY * PC-ITEMS-PER-BLOCK)) - 1.      MBKCS112
02319                                                                   MBKCS112
02320      IF PV-LAST-ITEM-IN-ARRAY > MB012-NUMBER-OF-ITEMS-READ           CL183
02321          MOVE MB012-NUMBER-OF-ITEMS-READ TO PV-LAST-ITEM-IN-ARRAY    CL183
02322      END-IF.                                                      MBKCS112
02323                                                                   MBKCS112
02324      COMPUTE PV-LAST-ITEM-IN-TEMP =                               MBKCS112
02325          MB012-HIGHEST-BLOCK-WRITTEN * PC-ITEMS-PER-BLOCK.           CL183
02326                                                                   MBKCS112
02327      IF PV-LAST-ITEM-IN-TEMP > MB012-NUMBER-OF-ITEMS-READ            CL183
02328          MOVE MB012-NUMBER-OF-ITEMS-READ TO PV-LAST-ITEM-IN-TEMP     CL183
02329      END-IF.                                                      MBKCS112
02330                                                                   MBKCS112
02331      IF (PV-TOP-ITEM > PV-FIRST-ITEM-IN-ARRAY) OR                 MBKCS112
02332         (PV-TOP-ITEM = PV-FIRST-ITEM-IN-ARRAY)                    MBKCS112
02333                                                                   MBKCS112
02334          IF (PV-TOP-ITEM < PV-LAST-ITEM-IN-ARRAY) OR              MBKCS112
02335             (PV-TOP-ITEM = PV-LAST-ITEM-IN-ARRAY)                 MBKCS112
02336              SET PS-TOP-ITEM-IN-ARRAY     TO TRUE                 MBKCS112
02337          ELSE                                                     MBKCS112
02338              SET PS-TOP-ITEM-NOT-IN-ARRAY TO TRUE                 MBKCS112
02339          END-IF                                                   MBKCS112
02340                                                                   MBKCS112
02341      ELSE                                                         MBKCS112
02342          SET PS-TOP-ITEM-NOT-IN-ARRAY TO TRUE                     MBKCS112
02343      END-IF.                                                      MBKCS112
02344                                                                   MBKCS112
02345      IF (PV-BOTTOM-ITEM > PV-FIRST-ITEM-IN-ARRAY) OR              MBKCS112
02346         (PV-BOTTOM-ITEM = PV-FIRST-ITEM-IN-ARRAY)                 MBKCS112
02347                                                                   MBKCS112
02348          IF (PV-BOTTOM-ITEM < PV-LAST-ITEM-IN-ARRAY) OR           MBKCS112
02349             (PV-BOTTOM-ITEM = PV-LAST-ITEM-IN-ARRAY)              MBKCS112
02350              SET PS-BOTTOM-ITEM-IN-ARRAY     TO TRUE              MBKCS112
02351          ELSE                                                     MBKCS112
02352              SET PS-BOTTOM-ITEM-NOT-IN-ARRAY TO TRUE              MBKCS112
02353          END-IF                                                   MBKCS112
02354                                                                   MBKCS112
02355      ELSE                                                         MBKCS112
02356          SET PS-BOTTOM-ITEM-NOT-IN-ARRAY TO TRUE                  MBKCS112
02357      END-IF.                                                      MBKCS112
02358                                                                   MBKCS112
02359      IF (PV-TOP-ITEM < PV-LAST-ITEM-IN-TEMP) OR                   MBKCS112
02360         (PV-TOP-ITEM = PV-LAST-ITEM-IN-TEMP)                      MBKCS112
02361          SET PS-TOP-ITEM-IN-TEMP     TO TRUE                      MBKCS112
02362      ELSE                                                         MBKCS112
02363          SET PS-TOP-ITEM-NOT-IN-TEMP TO TRUE                      MBKCS112
02364      END-IF.                                                      MBKCS112
02365                                                                   MBKCS112
02366      IF (PV-BOTTOM-ITEM < PV-LAST-ITEM-IN-TEMP) OR                MBKCS112
02367         (PV-BOTTOM-ITEM = PV-LAST-ITEM-IN-TEMP)                   MBKCS112
02368          SET PS-BOTTOM-ITEM-IN-TEMP     TO TRUE                   MBKCS112
02369      ELSE                                                         MBKCS112
02370          SET PS-BOTTOM-ITEM-NOT-IN-TEMP TO TRUE                   MBKCS112
02371      END-IF.                                                      MBKCS112
02372                                                                   MBKCS112
02373      IF PV-LAST-ITEM-IN-TEMP > PV-LAST-ITEM-IN-ARRAY              MBKCS112
02374          SET PS-LAST-BLOCK-NOT-IN-ARRAY TO TRUE                   MBKCS112
02375      ELSE                                                         MBKCS112
02376          SET PS-LAST-BLOCK-IN-ARRAY     TO TRUE                   MBKCS112
02377      END-IF.                                                      MBKCS112
02378  EJECT                                                            MBKCS112
02379  G400-READ-ITEMS-INTO-ARRAY.                                      MBKCS112
02380                                                                   MBKCS112
02381 ******************************************************************MBKCS112
02382 *    DETERMINE THE SPOT IN THE ARRAY WHERE THE LAST ITEM FROM     MBKCS112
02383 *    THE DATA BASE WAS ADDED SO WE KNOW WHERE TO ADD THE NEW      MBKCS112
02384 *    ITEMS.                                                       MBKCS112
02385 *                                                                 MBKCS112
02386 *    PERFORMED BY F500-BROWSE.                                       CL*93
02387 ******************************************************************MBKCS112
02388                                                                   MBKCS112
02389      COMPUTE PS-ITEM-SUB =                                        MBKCS112
02390          (MB012-NUMBER-OF-ITEMS-READ -                               CL183
02391           MB012-LIST-ITEM-NUMBER(1 1)) + 1.                          CL183
02392                                                                   MBKCS112
02393      MOVE PS-ITEM-SUB TO MB012-ITEM-SUB.                             CL183
02394                                                                   MBKCS112
02395      IF MB012-ITEM-SUB > PC-ITEMS-PER-BLOCK                          CL183
02396          SUBTRACT PC-ITEMS-PER-BLOCK FROM MB012-ITEM-SUB             CL183
02397                                                                   MBKCS112
02398          MOVE PC-MAX-BLOCKS-IN-ARRAY TO MB012-BLOCK-SUB              CL183
02399      ELSE                                                         MBKCS112
02400          MOVE +1                     TO MB012-BLOCK-SUB              CL183
02401      END-IF.                                                      MBKCS112
02402                                                                   MBKCS112
02403      MOVE PV-BOTTOM-ITEM TO PV-HIGHEST-ITEM-TO-BE-READ.           MBKCS112
02404                                                                      CL164
02405      PERFORM D500-OPEN-CURSOR.                                       CL164
02406                                                                      CL164
02407      PERFORM H100-READ-ITEMS-FROM-DB.                             MBKCS112
02408                                                                   MBKCS112
02409      IF MB012-NO-ITEMS-LEFT-ON-DB                                    CL183
02410                                                                   MBKCS112
02411          IF PV-TOP-ITEM > MB012-NUMBER-OF-ITEMS-READ                 CL183
02412              MOVE MB012-NUMBER-OF-ITEMS-READ TO PV-TOP-ITEM          CL183
02413          END-IF                                                   MBKCS112
02414                                                                   MBKCS112
02415          MOVE PC-TSYMSG-00070        TO DP020-MSG-NUMBER             CL179
02416          SET DP020-MSG-INFORMATIONAL TO TRUE                         CL*29
02417          SET PS-INFORMATIONAL        TO TRUE                         CL*29
02418      END-IF.                                                      MBKCS112
02419  EJECT                                                            MBKCS112
02420  G500-SCROLL-FORWARD.                                             MBKCS112
02421                                                                   MBKCS112
02422 ******************************************************************MBKCS112
02423 *    IF THE TOP ITEM WE NEED IS IN THE ARRAY, BUT THE BOTTOM         CL**2
02424 *    ITEM WE NEED IS IN TEMPORARY STORAGE OR HAS NOT BEEN READ       CL**2
02425 *    YET, WRITE OUT THE 1ST BLOCK IN THE ARRAY TO TEMPORARY          CL**2
02426 *    STORAGE.  MOVE THE 2ND BLOCK IN THE ARRAY TO THE 1ST BLOCK      CL**2
02427 *    AND READ THE BLOCK NEEDED INTO THE 2ND BLOCK OF THE ARRAY.      CL**2
02428 *    THE TOP ITEM WILL BE LOCATED IN THE 1ST BLOCK, AND THE          CL**2
02429 *    BOTTOM ITEM WILL EITHER BE LOCATED IN THE 2ND BLOCK OR          CL**2
02430 *    WILL BE PLACED IN THE 2ND BLOCK.                                CL**2
02431 *                                                                 MBKCS112
02432 *    PERFORMED BY F500-BROWSE.                                       CL*93
02433 ******************************************************************MBKCS112
02434                                                                   MBKCS112
02435      MOVE +1 TO MB012-BLOCK-SUB.                                     CL183
02436                                                                   MBKCS112
02437      PERFORM Z150-WRITE-TEMP-STORAGE.                             MBKCS112
02438                                                                   MBKCS112
02439      MOVE MB012-BLOCK-ENTRIES(PC-MAX-BLOCKS-IN-ARRAY) TO             CL183
02440           MB012-BLOCK-ENTRIES(MB012-BLOCK-SUB).                      CL183
02441                                                                   MBKCS112
02442      COMPUTE PV-TEMP-BLOCK-NUMBER =                               MBKCS112
02443          1 + ((PV-BOTTOM-ITEM - 1) / PC-ITEMS-PER-BLOCK).         MBKCS112
02444                                                                   MBKCS112
02445      MOVE PV-TEMP-BLOCK-NUMBER TO PV-BLOCK-NUMBER.                MBKCS112
02446                                                                   MBKCS112
02447      ADD +1 TO MB012-BLOCK-SUB.                                      CL183
02448                                                                   MBKCS112
02449      PERFORM Z100-READ-TEMP-STORAGE.                              MBKCS112
02450  EJECT                                                            MBKCS112
02451  G600-SCROLL-BACKWARD.                                            MBKCS112
02452                                                                   MBKCS112
02453 ******************************************************************MBKCS112
02454 *    IF TOP ITEM WE NEED IS NOT IN THE ARRAY, BUT THE BOTTOM      MBKCS112
02455 *    ITEM IS, WRITE BOTH BLOCKS TO TEMPORARY STORAGE.  MOVE       MBKCS112
02456 *    THE 1ST BLOCK IN THE ARRAY TO THE 2ND BLOCK OF THE ARRAY.    MBKCS112
02457 *    READ THE BLOCK THAT CONTAINS THE TOP ITEM INTO THE 1ST BLOCK MBKCS112
02458 *    OF THE ARRAY.                                                MBKCS112
02459 *                                                                 MBKCS112
02460 *    PERFORMED BY F500-BROWSE.                                       CL*93
02461 ******************************************************************MBKCS112
02462                                                                   MBKCS112
02463      PERFORM H400-WRITE-OUT-LAST-BLOCKS.                             CL*93
02464                                                                   MBKCS112
02465      MOVE +1                                   TO                 MBKCS112
02466           MB012-BLOCK-SUB.                                           CL183
02467                                                                   MBKCS112
02468      MOVE MB012-BLOCK-ENTRIES(MB012-BLOCK-SUB) TO                    CL183
02469           MB012-BLOCK-ENTRIES(PC-MAX-BLOCKS-IN-ARRAY).               CL183
02470                                                                   MBKCS112
02471      COMPUTE PV-TEMP-BLOCK-NUMBER =                               MBKCS112
02472          1 + ((PV-TOP-ITEM - 1) / PC-ITEMS-PER-BLOCK).            MBKCS112
02473                                                                   MBKCS112
02474      MOVE PV-TEMP-BLOCK-NUMBER TO PV-BLOCK-NUMBER.                MBKCS112
02475                                                                   MBKCS112
02476      PERFORM Z100-READ-TEMP-STORAGE.                              MBKCS112
02477  EJECT                                                            MBKCS112
02478  G700-GET-TOP-ITEMS-BLOCK.                                        MBKCS112
02479                                                                   MBKCS112
02480 ******************************************************************MBKCS112
02481 *    IF BOTH THE TOP AND BOTTOM ITEMS WE NEED ARE NOT IN THE      MBKCS112
02482 *    ARRAY, BUT THE TOP ITEM IS IN TEMPORARY STORAGE, WRITE BOTH  MBKCS112
02483 *    BLOCKS IN THE ARRAY TO TEMPORARY STORAGE.  READ THE BLOCK    MBKCS112
02484 *    THAT CONTAINS THE TOP ITEM INTO THE 1ST BLOCK OF THE ARRAY.  MBKCS112
02485 *    IF THERE IS ANOTHER BLOCK IN TEMPORARY STORAGE FOLLOWING     MBKCS112
02486 *    THE ONE JUST READ, READ THAT ONE INTO THE 2ND BLOCK OF THE   MBKCS112
02487 *    ARRAY, OTHERWISE INITIALIZE THE 2ND BLOCK OF THE ARRAY.      MBKCS112
02488 *                                                                 MBKCS112
02489 *    PERFORMED BY F500-BROWSE.                                       CL*93
02490 ******************************************************************MBKCS112
02491                                                                   MBKCS112
02492      PERFORM H400-WRITE-OUT-LAST-BLOCKS.                             CL*93
02493                                                                   MBKCS112
02494      COMPUTE PV-TEMP-BLOCK-NUMBER =                               MBKCS112
02495          1 + ((PV-TOP-ITEM - 1) / PC-ITEMS-PER-BLOCK).            MBKCS112
02496                                                                   MBKCS112
02497      MOVE PV-TEMP-BLOCK-NUMBER TO PV-BLOCK-NUMBER.                MBKCS112
02498      MOVE +1                   TO MB012-BLOCK-SUB.                   CL183
02499                                                                   MBKCS112
02500      PERFORM Z100-READ-TEMP-STORAGE.                              MBKCS112
02501                                                                   MBKCS112
02502      ADD +1 TO PV-BLOCK-NUMBER.                                   MBKCS112
02503      ADD +1 TO MB012-BLOCK-SUB.                                      CL183
02504                                                                   MBKCS112
02505      IF (PV-BLOCK-NUMBER < MB012-HIGHEST-BLOCK-WRITTEN) OR           CL183
02506         (PV-BLOCK-NUMBER = MB012-HIGHEST-BLOCK-WRITTEN)              CL183
02507          PERFORM Z100-READ-TEMP-STORAGE                           MBKCS112
02508      ELSE                                                         MBKCS112
02509          PERFORM I100-INITIALIZE-BLOCK                            MBKCS112
02510      END-IF.                                                      MBKCS112
02511  EJECT                                                            MBKCS112
02512  G800-GET-LAST-BLOCK-IN-TEMP.                                     MBKCS112
02513                                                                   MBKCS112
02514 ******************************************************************MBKCS112
02515 *    IF BOTH THE TOP AND BOTTOM ITEMS WE NEED ARE NOT IN THE      MBKCS112
02516 *    ARRAY AND BOTH ITEMS ARE NOT IN TEMPORARY STORAGE (THE       MBKCS112
02517 *    ITEMS HAVE NOT BEEN READ FROM THE DATA BASE YET), AND THE    MBKCS112
02518 *    LAST ITEM READ FROM THE DATA BASE IS IN TEMPORARY STORAGE,   MBKCS112
02519 *    WRITE BOTH BLOCKS IN THE ARRAY TO TEMPORARY STORAGE.  READ   MBKCS112
02520 *    THE HIGHEST BLOCK IN TEMPORARY STORAGE INTO THE 1ST BLOCK    MBKCS112
02521 *    OF THE ARRAY, AND INITIALIZE THE 2ND BLOCK IN THE ARRAY.     MBKCS112
02522 *    THE 2ND BLOCK WILL BE USED FOR THE NEW ITEMS TO BE READ.     MBKCS112
02523 *    THE 1ST BLOCK WILL NOW CONTAIN THE ITEMS THAT WERE LAST      MBKCS112
02524 *    READ FROM THE DATA BASE.                                     MBKCS112
02525 *                                                                 MBKCS112
02526 *    PERFORMED BY F500-BROWSE.                                       CL*93
02527 ******************************************************************MBKCS112
02528                                                                   MBKCS112
02529      PERFORM H400-WRITE-OUT-LAST-BLOCKS.                             CL*93
02530                                                                   MBKCS112
02531      MOVE MB012-HIGHEST-BLOCK-WRITTEN TO PV-BLOCK-NUMBER.            CL183
02532      MOVE +1                          TO MB012-BLOCK-SUB.            CL183
02533                                                                   MBKCS112
02534      PERFORM Z100-READ-TEMP-STORAGE.                              MBKCS112
02535                                                                   MBKCS112
02536      ADD +1 TO MB012-BLOCK-SUB.                                      CL183
02537                                                                   MBKCS112
02538      PERFORM I100-INITIALIZE-BLOCK.                               MBKCS112
02539  EJECT                                                            MBKCS112
02540  H100-READ-ITEMS-FROM-DB.                                         MBKCS112
02541                                                                   MBKCS112
02542 ******************************************************************MBKCS112
02543 *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE     MBKCS112
02544 *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,      MBKCS112
02545 *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND   MBKCS112
02546 *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE      MBKCS112
02547 *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                    MBKCS112
02548 *                                                                 MBKCS112
02549 ******************************************************************MBKCS112
02550                                                                   MBKCS112
02551      PERFORM                                                      MBKCS112
02552          UNTIL (MB012-NO-ITEMS-LEFT-ON-DB)   OR                      CL183
02553                (MB012-NUMBER-OF-ITEMS-READ =                         CL183
02554                 PV-HIGHEST-ITEM-TO-BE-READ)                          CL102
02555                                                                   MBKCS112
02556          IF MB012-ITEM-SUB < PC-ITEMS-PER-BLOCK                      CL183
02557              ADD +1 TO MB012-ITEM-SUB                                CL183
02558          ELSE                                                     MBKCS112
02559              ADD +1 TO MB012-BLOCK-SUB                               CL183
02560                                                                   MBKCS112
02561              MOVE +1 TO MB012-ITEM-SUB                               CL183
02562                                                                   MBKCS112
02563              IF MB012-BLOCK-SUB > PC-MAX-BLOCKS-IN-ARRAY             CL183
02564                  MOVE +1 TO MB012-BLOCK-SUB                          CL183
02565                                                                   MBKCS112
02566                  PERFORM Z150-WRITE-TEMP-STORAGE                  MBKCS112
02567                                                                   MBKCS112
02568                  MOVE MB012-BLOCK-ENTRIES(PC-MAX-BLOCKS-IN-ARRAY)    CL183
02569                  TO   MB012-BLOCK-ENTRIES(MB012-BLOCK-SUB)           CL183
02570                                                                   MBKCS112
02571                  ADD +1 TO MB012-BLOCK-SUB                           CL183
02572                                                                   MBKCS112
02573                  PERFORM I100-INITIALIZE-BLOCK                    MBKCS112
02574              END-IF                                               MBKCS112
02575                                                                   MBKCS112
02576          END-IF                                                   MBKCS112
02577                                                                   MBKCS112
02578          PERFORM H300-READ-ITEMS-FROM-DATABASE                       CL*41
02579                                                                   MBKCS112
02580          IF MB012-ITEMS-LEFT-ON-DB                                   CL183
02581              ADD +1 TO MB012-NUMBER-OF-ITEMS-READ                    CL183
02582                                                                   MBKCS112
02583              MOVE MB012-NUMBER-OF-ITEMS-READ TO                      CL183
02584                   MB012-LIST-ITEM-NUMBER(MB012-BLOCK-SUB             CL183
02585                                          MB012-ITEM-SUB)             CL183
02586                                                                   MBKCS112
02587              MOVE MDSEAR-GMA-NBR             TO                      CL*47
02588                   MB012-ITEM-GMA(MB012-BLOCK-SUB                     CL183
02589                                  MB012-ITEM-SUB)                     CL183
02590              MOVE MDSEAR-DMA-NBR             TO                      CL*47
02591                   MB012-ITEM-DMA(MB012-BLOCK-SUB                     CL183
02592                                  MB012-ITEM-SUB)                     CL183
02593              MOVE MDSEAR-DEPT-NBR            TO                      CL*47
02594                   MB012-ITEM-DEPT(MB012-BLOCK-SUB                    CL183
02595                                   MB012-ITEM-SUB)                    CL183
02596              MOVE MDSEAR-DESC                TO                      CL*48
02597                   MB012-ITEM-DESC(MB012-BLOCK-SUB                    CL183
02598                                   MB012-ITEM-SUB)                    CL183
02599                                                                      CL242
02600              IF  MDSEAR-END-DTE LESS THAN MB012-DB2-CURRENT-DATE     CL242
02601                  MOVE DP015-TURQUOISE        TO                      CL242
02602                  MB012-COLOR (MB012-BLOCK-SUB MB012-ITEM-SUB)        CL242
02603              ELSE                                                    CL242
02604                  MOVE DP015-BLUE             TO                      CL242
02605                  MB012-COLOR (MB012-BLOCK-SUB MB012-ITEM-SUB)        CL242
02606              END-IF                                                  CL242
02607 *                                                                    CL242
02608              SET MB012-BLOCK-MODIFIED(MB012-BLOCK-SUB) TO TRUE       CL183
02609                                                                      CL239
02610          END-IF                                                   MBKCS112
02611                                                                   MBKCS112
02612      END-PERFORM.                                                 MBKCS112
02613                                                                      CL232
02614      IF MB012-NUMBER-OF-ITEMS-READ = +0 AND                          CL234
02615         MB012-NO-ITEMS-LEFT-ON-DB                                    CL234
02616         MOVE PC-TSYMSG-00050    TO DP020-MSG-NUMBER                  CL232
02617         SET PS-ERROR TO TRUE                                         CL232
02618         SET DP020-MSG-FATAL TO TRUE                                  CL232
02619      END-IF.                                                         CL232
02620                                                                      CL*10
02621 ******************************************************************   CL*10
02622 *    ONCE WE ARE DONE FILLING UP THE BLOCKS, WE MUST PERFORM         CL*10
02623 *    1 MORE READ AGAINST THE DB TO SEE IF THERE IS ANOTHER           CL*10
02624 *    RECORD OUT THERE.  THIS READ ONLY ALLOWS US TO DETERMINE        CL*10
02625 *    IF WE JUST READ THE LAST ITEM OR IF THERE ARE MORE TO READ.     CL*10
02626 ******************************************************************   CL*10
02627                                                                      CL*10
02628      IF MB012-ITEMS-LEFT-ON-DB                                       CL235
02629                                                                      CL239
02630          MOVE MDSEAR-GMA-NBR      TO MB012-SAVE-GMA-NBR              CL239
02631          MOVE MDSEAR-DMA-NBR      TO MB012-SAVE-DMA-NBR              CL239
02632          MOVE MDSEAR-DEPT-NBR     TO MB012-SAVE-DPT-NBR              CL239
02633          MOVE MDSEAR-LIST-SEQ-NBR TO MB012-SAVE-SEQ-NBR              CL239
02634                                                                      CL239
02635          SET PS-DB2-RETRY TO TRUE                                    CL238
02636          PERFORM H300-READ-ITEMS-FROM-DATABASE                       CL235
02637              UNTIL PS-DB2-OK                                         CL235
02638                                                                      CL235
02639          IF MB012-NUMBER-OF-ITEMS-READ > +0                          CL235
02640             SET PS-SELECTIONS-FOUND TO TRUE                          CL237
02641          END-IF                                                      CL235
02642                                                                      CL235
02643      END-IF.                                                         CL235
02644                                                                      CL102
02645      IF MB012-NO-ITEMS-LEFT-ON-DB                                    CL183
02646                                                                      CL112
02647          EVALUATE TRUE                                               CL112
02648                                                                      CL112
02649               WHEN MB012-OPEN-MERCH-CURSOR                           CL183
02650                    PERFORM Z220-CLOSE-MERCH-CURSOR                   CL112
02651                                                                      CL112
02652               WHEN MB012-OPEN-GMA-CURSOR                             CL183
02653                    PERFORM Z320-CLOSE-GMA-CURSOR                     CL112
02654                                                                      CL112
02655               WHEN MB012-OPEN-DMA-CURSOR                             CL183
02656                    PERFORM Z420-CLOSE-DMA-CURSOR                     CL127
02657                                                                      CL129
02658               WHEN MB012-OPEN-DPT-CURSOR                             CL183
02659                    PERFORM Z520-CLOSE-DPT-CURSOR                     CL129
02660                                                                      CL126
02661          END-EVALUATE                                                CL112
02662                                                                      CL112
02663          IF MB012-NUMBER-OF-ITEMS-READ = +0                          CL236
02664              SET DP020-MSG-FATAL TO TRUE                             CL249
02665              SET PS-NO-SELECTIONS-FOUND TO TRUE                      CL236
02666          ELSE                                                        CL236
02667              SET PS-SELECTIONS-FOUND    TO TRUE                      CL236
02668          END-IF                                                      CL236
02669      END-IF.                                                         CL157
02670  EJECT                                                            MBKCS112
02671  H200-FORMAT-PANEL-BODY.                                          MBKCS112
02672                                                                   MBKCS112
02673 ******************************************************************MBKCS112
02674 *    FORMAT THE MAP AND DETERMINE THE CURSOR POSITION.            MBKCS112
02675 *                                                                 MBKCS112
02676 ******************************************************************MBKCS112
02677                                                                   MBKCS112
02678      PERFORM I700-FORMAT-FROM-TO-LINE.                            MBKCS112
02679      PERFORM I200-FORMAT-LIST-LINES.                              MBKCS112
02680      PERFORM I300-FORMAT-NR-SELECTED.                             MBKCS112
02681      PERFORM I800-SET-CURSOR-POSITION.                               CL**4
02682  EJECT                                                               CL*92
02683  H300-READ-ITEMS-FROM-DATABASE.                                      CL*41
02684                                                                      CL*41
02685 ******************************************************************   CL*41
02686 *    READ ITEMS FROM DATABASE.                                       CL*41
02687 *         PERFORMS THE READ BASES ON THE CURSOR THAT IS OPEN         CL165
02688 ******************************************************************   CL*41
02689                                                                      CL*41
02690      SET PS-DB2-RETRY TO TRUE.                                       CL*41
02691                                                                      CL*41
02692      SET PS-DO-NOT-HAVE-DPT-ITEM TO TRUE.                            CL129
02693                                                                      CL126
02694      EVALUATE TRUE                                                   CL100
02695                                                                      CL100
02696           WHEN MB012-OPEN-MERCH-CURSOR                               CL183
02697                PERFORM Z210-READ-MERCH-AREA                          CL100
02698                    UNTIL PS-DB2-OK                                   CL100
02699                                                                      CL*41
02700           WHEN MB012-OPEN-GMA-CURSOR                                 CL183
02701                PERFORM H305-PROCESS-GMA-CURSOR                       CL181
02702                    UNTIL PS-HAVE-DPT-ITEM                            CL227
02703                    OR MB012-NO-ITEMS-LEFT-ON-DB                      CL183
02704                                                                      CL100
02705           WHEN MB012-OPEN-DMA-CURSOR                                 CL183
02706                PERFORM H310-PROCESS-DMA-CURSOR                       CL122
02707                    UNTIL PS-HAVE-DPT-ITEM                            CL227
02708                    OR MB012-NO-ITEMS-LEFT-ON-DB                      CL183
02709                                                                      CL129
02710           WHEN MB012-OPEN-DPT-CURSOR                                 CL183
02711                PERFORM H320-PROCESS-DPT-CURSOR                       CL129
02712                    UNTIL PS-HAVE-DPT-ITEM                            CL129
02713                    OR MB012-NO-ITEMS-LEFT-ON-DB                      CL183
02714                                                                      CL119
02715      END-EVALUATE.                                                   CL100
02716                                                                      CL227
02717                                                                      CL227
02718  EJECT                                                               CL*41
02719  H305-PROCESS-GMA-CURSOR.                                            CL181
02720                                                                      CL165
02721 ******************************************************************   CL165
02722 *    READ ITEMS FROM DATABASE USING THE GMA CURSOR                   CL181
02723 *       CHECK THE GMA NUMBER AFTER THE FETCH TO SEE IF IT IS         CL165
02724 *       WITHIN THE REQUESTED RANGE                                   CL165
02725 ******************************************************************   CL165
02726                                                                      CL133
02727      SET PS-DB2-RETRY TO TRUE.                                       CL128
02728                                                                      CL133
02729      PERFORM Z310-READ-GMA-AREA                                      CL181
02730          UNTIL PS-DB2-OK                                             CL181
02731                                                                      CL122
02732      IF MB012-ITEMS-LEFT-ON-DB                                       CL227
02733                                                                      CL227
02734          EVALUATE TRUE                                               CL227
02735                                                                      CL227
02736              WHEN MB012-ALL-GMAS                                     CL227
02737                   SET PS-HAVE-DPT-ITEM TO TRUE                       CL227
02738                                                                      CL227
02739              WHEN MB012-GMA-NUMBER = MDSEAR-GMA-NBR                  CL227
02740                   SET PS-HAVE-DPT-ITEM TO TRUE                       CL227
02741          END-EVALUATE                                                CL227
02742      END-IF.                                                         CL227
02743  EJECT                                                               CL227
02744  H310-PROCESS-DMA-CURSOR.                                            CL181
02745                                                                      CL181
02746 ******************************************************************   CL181
02747 *    READ ITEMS FROM DATABASE USING THE DMA CURSOR                   CL181
02748 *       CHECK THE GMA NUMBER AFTER THE FETCH TO SEE IF IT IS         CL181
02749 *       WITHIN THE REQUESTED RANGE                                   CL181
02750 ******************************************************************   CL181
02751                                                                      CL181
02752      SET PS-DB2-RETRY TO TRUE.                                       CL181
02753                                                                      CL181
02754      PERFORM Z410-READ-DMA-AREA                                      CL181
02755          UNTIL PS-DB2-OK.                                            CL181
02756                                                                      CL181
02757      IF MB012-ITEMS-LEFT-ON-DB                                       CL227
02758                                                                      CL227
02759          EVALUATE TRUE                                               CL227
02760                                                                      CL227
02761              WHEN MB012-ALL-GMAS                                     CL227
02762                   SET PS-HAVE-DPT-ITEM TO TRUE                       CL228
02763                                                                      CL227
02764              WHEN MB012-GMA-NUMBER = MDSEAR-GMA-NBR                  CL227
02765                   SET PS-HAVE-DPT-ITEM TO TRUE                       CL228
02766                                                                      CL227
02767          END-EVALUATE                                                CL227
02768      END-IF.                                                         CL227
02769                                                                      CL181
02770  EJECT                                                               CL181
02771  H320-PROCESS-DPT-CURSOR.                                            CL131
02772                                                                      CL181
02773 ******************************************************************   CL181
02774 *    READ ITEMS FROM DATABASE USING THE DPT CURSOR                   CL181
02775 *       CHECK THE GMA AND DMA NUMBERS AFTER THE FETCH TO SEE         CL181
02776 *       IF IT IS WITHIN  THE REQUESTED RANGE                         CL182
02777 *                                                                    CL181
02778 ******************************************************************   CL181
02779                                                                      CL132
02780      SET PS-DB2-RETRY TO TRUE.                                       CL129
02781                                                                      CL132
02782      PERFORM Z510-READ-DPT-AREA                                      CL131
02783          UNTIL PS-DB2-OK.                                            CL129
02784                                                                      CL129
02785      IF MB012-ITEMS-LEFT-ON-DB                                       CL229
02786                                                                      CL229
02787 *        EVALUATE TRUE                                               CL*13
02788 *                                                                    CL*13
02789 *            WHEN MB012-ALL-GMAS AND                                 CL*13
02790 *                 MB012-ALL-DMAS                                     CL*13
02791 *                 SET PS-HAVE-DPT-ITEM TO TRUE                       CL*13
02792 *                                                                    CL*13
02793 *            WHEN MB012-ALL-GMAS AND                                 CL*13
02794 *                 MB012-DMA-NUMBER = MDSEAR-DMA-NBR                  CL*13
02795 *                 SET PS-HAVE-DPT-ITEM TO TRUE                       CL*13
02796 *                                                                    CL*13
02797 *            WHEN MB012-GMA-NUMBER = MDSEAR-GMA-NBR AND              CL*13
02798 *                 MB012-ALL-DMAS                                     CL*13
02799 *                 SET PS-HAVE-DPT-ITEM TO TRUE                       CL*13
02800 *                                                                    CL*13
02801 *            WHEN MB012-GMA-NUMBER = MDSEAR-GMA-NBR AND              CL*13
02802 *                 MB012-DMA-NUMBER = MDSEAR-DMA-NBR                  CL*13
02803                   SET PS-HAVE-DPT-ITEM TO TRUE                       CL229
02804                                                                      CL229
02805 *        END-EVALUATE                                                CL*13
02806      END-IF.                                                         CL132
02807  EJECT                                                               CL122
02808  H400-WRITE-OUT-LAST-BLOCKS.                                         CL*92
02809                                                                      CL*92
02810 ******************************************************************   CL*92
02811 *    WRITE OUT THE 2 BLOCKS CURRENTLY IN WORKING STORAGE TO          CL*92
02812 *    TEMPORARY STORAGE.                                              CL*92
02813 *                                                                    CL*92
02814 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*92
02815 ******************************************************************   CL*92
02816                                                                      CL*92
02817      MOVE +1 TO MB012-BLOCK-SUB.                                     CL183
02818                                                                      CL*92
02819      PERFORM Z150-WRITE-TEMP-STORAGE.                                CL*92
02820                                                                      CL*92
02821      ADD +1  TO MB012-BLOCK-SUB.                                     CL183
02822                                                                      CL*92
02823      PERFORM Z150-WRITE-TEMP-STORAGE.                                CL*92
02824                                                                      CL*92
02825  EJECT                                                               CL*92
02826  I100-INITIALIZE-BLOCK.                                           MBKCS112
02827                                                                   MBKCS112
02828 ******************************************************************MBKCS112
02829 *    INITIALIZE A BLOCK IN THE ARRAY.                             MBKCS112
02830 *                                                                 MBKCS112
02831 ******************************************************************MBKCS112
02832                                                                   MBKCS112
02833      PERFORM                                                      MBKCS112
02834          VARYING PS-SUB FROM 1 BY 1                               MBKCS112
02835          UNTIL PS-SUB > PC-ITEMS-PER-BLOCK                        MBKCS112
02836                                                                   MBKCS112
02837          MOVE +0 TO MB012-LIST-ITEM-NUMBER(MB012-BLOCK-SUB PS-SUB)   CL183
02838                                                                   MBKCS112
02839          MOVE SPACES TO MB012-ITEM-GMA(MB012-BLOCK-SUB PS-SUB)       CL183
02840          MOVE SPACES TO MB012-ITEM-DMA(MB012-BLOCK-SUB PS-SUB)       CL183
02841          MOVE SPACES TO MB012-ITEM-DEPT(MB012-BLOCK-SUB PS-SUB)      CL183
02842          MOVE SPACES TO MB012-ITEM-DESC(MB012-BLOCK-SUB PS-SUB)      CL183
02843          MOVE SPACES TO                                              CL*29
02844               MB012-ITEM-ACTION(MB012-BLOCK-SUB PS-SUB)              CL183
02845                                                                   MBKCS112
02846          SET MB012-NO-SELECT(MB012-BLOCK-SUB PS-SUB) TO TRUE         CL183
02847      END-PERFORM.                                                 MBKCS112
02848                                                                   MBKCS112
02849      SET MB012-BLOCK-NOT-MODIFIED(MB012-BLOCK-SUB) TO TRUE.          CL183
02850  EJECT                                                            MBKCS112
02851  I200-FORMAT-LIST-LINES.                                          MBKCS112
02852                                                                   MBKCS112
02853 ******************************************************************MBKCS112
02854 *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.     MBKCS112
02855 *                                                                 MBKCS112
02856 *    PERFORMED FROM H200-FORMAT-PANEL-BODY.                          CL*93
02857 ******************************************************************MBKCS112
02858                                                                   MBKCS112
02859      COMPUTE PV-ENDING-ITEM-ON-PANEL =                            MBKCS112
02860          (MB012-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.      CL183
02861                                                                   MBKCS112
02862      MOVE MB012-ITEM-AT-TOP-OF-PANEL TO PV-ITEM-IN-USE.              CL183
02863      MOVE +1                         TO PS-SUB.                   MBKCS112
02864                                                                   MBKCS112
02865      PERFORM J100-PROCESS-ITEMS                                   MBKCS112
02866          UNTIL PV-ITEM-IN-USE > PV-ENDING-ITEM-ON-PANEL.          MBKCS112
02867  EJECT                                                            MBKCS112
02868  I300-FORMAT-NR-SELECTED.                                         MBKCS112
02869                                                                   MBKCS112
02870 ******************************************************************MBKCS112
02871 *    FORMAT THE FIELD WHICH WILL SHOW THE NUMBER OF ITEMS THAT    MBKCS112
02872 *    WERE SELECTED BY THE USER.                                   MBKCS112
02873 *                                                                 MBKCS112
02874 *    PERFORMED FROM H200-FORMAT-PANEL-BODY.                          CL*93
02875 ******************************************************************MBKCS112
02876                                                                   MBKCS112
02877      MOVE MB012-NUMBER-OF-ITEMS-SELECTED TO PV-DISPLAY-7.            CL183
02878      MOVE PV-DISPLAY-7                   TO ATOTSELO.                CL178
02879  EJECT                                                            MBKCS112
02880  I700-FORMAT-FROM-TO-LINE.                                        MBKCS112
02881                                                                   MBKCS112
02882 ******************************************************************MBKCS112
02883 *    FORMAT THE LINE THAT WILL SHOW THE TOTAL NUMBER OF ITEMS WE  MBKCS112
02884 *    HAVE ON OUR LIST AND SHOW THE NUMBERS ASSOCIATED WITH THE    MBKCS112
02885 *    FIRST AND LAST ITEMS ON CURRENT PAGE.  THIS WILL HELP THE    MBKCS112
02886 *    USER DETERMINE HOW FAR THEY ARE INTO THE LIST.               MBKCS112
02887 *                                                                 MBKCS112
02888 *    PERFORMED FROM H200-FORMAT-PANEL-BODY.                          CL*93
02889 ******************************************************************MBKCS112
02890                                                                   MBKCS112
02891 ******************************************************************   CL**3
02892 *    ON MAP      ITEMS XXXXXXX TO XXXXXXX OF XXXXXXX                 CL201
02893 *                        BEG   TO   END   OF   TOT                   CL**3
02894 ******************************************************************   CL**3
02895                                                                      CL**3
02896      MOVE MB012-CO-NUMBER                TO MB013O-AOPERCOO.         CL185
02897                                                                      CL*68
02898      IF MB012-NUMBER-OF-ITEMS-READ > +0                              CL183
02899          MOVE MB012-ITEM-AT-TOP-OF-PANEL TO MB013O-ABEGITMO          CL185
02900      ELSE                                                         MBKCS112
02901          MOVE 0                          TO MB013O-ABEGITMO          CL185
02902      END-IF.                                                      MBKCS112
02903                                                                   MBKCS112
02904      MOVE DP015-GREEN                    TO ABEGITMC.                CL*22
02905      MOVE DP015-UNDERLINE                TO ABEGITMH.                CL*56
02906      MOVE DP015-UNP-NUM-BRT-MDT          TO ABEGITMA.                CL*22
02907                                                                      CL*22
02908      COMPUTE PV-ENDING-ITEM-ON-PANEL =                            MBKCS112
02909          (MB012-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.      CL183
02910                                                                   MBKCS112
02911      IF PV-ENDING-ITEM-ON-PANEL < MB012-NUMBER-OF-ITEMS-READ         CL183
02912          MOVE PV-ENDING-ITEM-ON-PANEL    TO MB013O-AENDITMO          CL185
02913      ELSE                                                         MBKCS112
02914          MOVE MB012-NUMBER-OF-ITEMS-READ TO MB013O-AENDITMO          CL185
02915      END-IF.                                                      MBKCS112
02916                                                                   MBKCS112
02917      MOVE SPACES TO MB013O-AOFO.                                     CL185
02918      MOVE ZEROES TO MB013O-ATOTITMO.                                 CL185
02919                                                                   MBKCS112
02920      IF (MB012-NUMBER-OF-ITEMS-READ > +0) AND                        CL183
02921         (MB012-NO-ITEMS-LEFT-ON-DB)                                  CL183
02922          MOVE 'OF'                       TO MB013O-AOFO              CL185
02923                                                                      CL**3
02924          MOVE MB012-NUMBER-OF-ITEMS-READ TO MB013O-ATOTITMO          CL185
02925      END-IF.                                                      MBKCS112
02926  EJECT                                                            MBKCS112
02927  I800-SET-CURSOR-POSITION.                                        MBKCS112
02928                                                                   MBKCS112
02929 ******************************************************************MBKCS112
02930 *    IF NO ITEMS WERE READ FROM THE DATA BASE, POINT THE CURSOR   MBKCS112
02931 *    TO THE COMMAND LINE, OTHERWISE POINT THE CURSOR TO ONE OF    MBKCS112
02932 *    THE SELECTION ENTRIES.                                       MBKCS112
02933 *                                                                 MBKCS112
02934 *    PERFORMED FROM H200-FORMAT-PANEL-BODY.                          CL*93
02935 ******************************************************************MBKCS112
02936                                                                   MBKCS112
02937      IF (PS-CURSOR-NOT-POSITIONED  OR                                CL201
02938          PS-INFORMATIONAL          OR                                CL201
02939          PS-WARNING)                                                 CL201
02940          PERFORM J200-FIND-LAST-SELECTION                            CL157
02941      END-IF.                                                      MBKCS112
02942  EJECT                                                            MBKCS112
02943  J100-PROCESS-ITEMS.                                              MBKCS112
02944                                                                   MBKCS112
02945 ******************************************************************MBKCS112
02946 *    PROCESS EACH ITEM IN THE ARRAY AND MOVE IT TO THE MAP.       MBKCS112
02947 *    FILL THE MAP UNTIL WE HAVE REACHED THE MAXIMUM NUMBER OF     MBKCS112
02948 *    MAP ENTRIES.                                                 MBKCS112
02949 *                                                                 MBKCS112
02950 *    PERFORMED BY I200-FORMAT-LIST-LINES.                            CL*93
02951 ******************************************************************MBKCS112
02952                                                                   MBKCS112
02953      IF (PV-ITEM-IN-USE < MB012-NUMBER-OF-ITEMS-READ) OR             CL183
02954         (PV-ITEM-IN-USE = MB012-NUMBER-OF-ITEMS-READ)                CL183
02955          PERFORM K100-SET-SUBSCRIPTS                              MBKCS112
02956                                                                   MBKCS112
02957          MOVE DP015-UNP-ALP-BRT-MDT TO MB013I-ASELA(PS-SUB)          CL185
02958                                                                   MBKCS112
02959          IF (MB012-SELECT(MB012-BLOCK-SUB MB012-ITEM-SUB))    OR     CL183
02960             (MB012-NO-SELECT(MB012-BLOCK-SUB MB012-ITEM-SUB))        CL183
02961              MOVE DP015-UNDERLINE TO MB013O-ASELH(PS-SUB)            CL185
02962              MOVE DP015-GREEN     TO MB013O-ASELC(PS-SUB)            CL185
02963          ELSE                                                        CL*44
02964              IF (MB012-BEGIN(MB012-BLOCK-SUB MB012-ITEM-SUB)) OR     CL183
02965                 (MB012-END(MB012-BLOCK-SUB MB012-ITEM-SUB))          CL183
02966                                                                      CL*44
02967                  IF (DP020-MSG-NUMBER = PC-NO-START-OF-RANGE) OR     CL*44
02968                     (DP020-MSG-NUMBER = PC-NO-END-OF-RANGE)          CL*44
02969                      MOVE DP015-REVERSE TO MB013O-ASELH(PS-SUB)      CL185
02970                      MOVE DP015-YELLOW  TO MB013O-ASELC(PS-SUB)      CL185
02971                  END-IF                                              CL*44
02972                                                                      CL*45
02973                  IF (DP020-MSG-NUMBER = PC-MULTIPLE-START-RANGES)    CL*70
02974                 AND (MB012-BEGIN(MB012-BLOCK-SUB MB012-ITEM-SUB))    CL183
02975                      MOVE DP015-REVERSE TO MB013O-ASELH(PS-SUB)      CL185
02976                      MOVE DP015-RED     TO MB013O-ASELC(PS-SUB)      CL185
02977                  END-IF                                              CL*45
02978                  IF (DP020-MSG-NUMBER = PC-MULTIPLE-END-RANGES)      CL*70
02979                 AND (MB012-END(MB012-BLOCK-SUB MB012-ITEM-SUB))      CL183
02980                      MOVE DP015-REVERSE TO MB013O-ASELH(PS-SUB)      CL185
02981                      MOVE DP015-RED     TO MB013O-ASELC(PS-SUB)      CL185
02982                  END-IF                                              CL*70
02983                  IF DP020-MSG-NUMBER = PC-END-BEFORE-START           CL*56
02984                      MOVE DP015-REVERSE TO MB013O-ASELH(PS-SUB)      CL185
02985                      MOVE DP015-RED     TO MB013O-ASELC(PS-SUB)      CL185
02986                  END-IF                                              CL*45
02987              END-IF                                                  CL*45
02988          END-IF                                                      CL*45
02989                                                                   MBKCS112
02990          MOVE MB012-SELECTED-SW(MB012-BLOCK-SUB MB012-ITEM-SUB) TO   CL183
02991               MB013O-ASELO(PS-SUB)                                   CL185
02992                                                                      CL*45
02993          IF (PS-SEND-MAP-N-DATA-FRSET)     OR                        CL*45
02994             (PS-DIFFERENT-ITEMS-DISPLAYED)                           CL*45
02995             MOVE MB012-ITEM-GMA (MB012-BLOCK-SUB                     CL183
02996                                  MB012-ITEM-SUB)           TO        CL183
02997                  MB013O-AGMAO(PS-SUB)                                CL185
02998             MOVE MB012-ITEM-DMA (MB012-BLOCK-SUB                     CL183
02999                                  MB012-ITEM-SUB)           TO        CL183
03000                  MB013O-ADMAO(PS-SUB)                                CL185
03001             MOVE MB012-ITEM-DEPT (MB012-BLOCK-SUB                    CL183
03002                                  MB012-ITEM-SUB)           TO        CL183
03003                  MB013O-ADEPTO(PS-SUB)                               CL185
03004             MOVE MB012-ITEM-DESC (MB012-BLOCK-SUB                    CL183
03005                                   MB012-ITEM-SUB)          TO        CL183
03006                  MB013O-ADESCO(PS-SUB)                               CL185
03007             MOVE SPACES                                    TO        CL*45
03008                  MB013O-AACTNO(PS-SUB)                               CL185
03009             MOVE DP015-HL-OFF                              TO        CL*45
03010                  MB013O-AGMAH(PS-SUB)                                CL185
03011             MOVE DP015-HL-OFF                              TO        CL*45
03012                  MB013O-ADMAH(PS-SUB)                                CL185
03013             MOVE DP015-HL-OFF                              TO        CL*45
03014                  MB013O-ADEPTH(PS-SUB)                               CL185
03015             MOVE DP015-HL-OFF                              TO        CL*45
03016                  MB013O-ADESCH(PS-SUB)                               CL185
03017             MOVE DP015-HL-OFF                              TO        CL*45
03018                  MB013O-AACTNH(PS-SUB)                               CL185
03019 *                                                                    CL242
03020             MOVE MB012-COLOR (MB012-BLOCK-SUB MB012-ITEM-SUB)        CL242
03021                                TO  MB013O-AGMAC(PS-SUB)              CL242
03022                                    MB013O-ADAMC(PS-SUB)              CL243
03023                                    MB013O-ADEPTC(PS-SUB)             CL242
03024                                    MB013O-ADESCC(PS-SUB)             CL242
03025                                    MB013O-AACTNC(PS-SUB)             CL242
03026 *                                                                    CL242
03027          END-IF                                                      CL*45
03028      ELSE                                                            CL*45
03029          MOVE DP015-ASK-DRK-OFF                            TO        CL170
03030               MB013I-ASELA(PS-SUB)                                   CL185
03031          MOVE SPACES                                       TO        CL*56
03032               MB013O-ASELO(PS-SUB)                                   CL185
03033          MOVE SPACES                                       TO        CL*45
03034               MB013O-AGMAO(PS-SUB)                                   CL185
03035          MOVE SPACES                                       TO        CL*45
03036               MB013O-ADMAO(PS-SUB)                                   CL185
03037          MOVE SPACES                                       TO        CL*45
03038               MB013O-ADEPTO(PS-SUB)                                  CL185
03039          MOVE SPACES                                       TO        CL*45
03040               MB013O-ADESCO(PS-SUB)                                  CL185
03041         MOVE SPACES                                        TO        CL*93
03042              MB013O-AACTNO(PS-SUB)                                   CL185
03043      END-IF.                                                         CL*45
03044                                                                      CL*45
03045      ADD +1 TO PV-ITEM-IN-USE.                                    MBKCS112
03046      ADD +1 TO PS-SUB.                                            MBKCS112
03047  EJECT                                                            MBKCS112
03048  J200-FIND-LAST-SELECTION.                                        MBKCS112
03049                                                                   MBKCS112
03050 ******************************************************************MBKCS112
03051 *    FIND THE LAST SELECTION ENTRY IN THE MAP THAT IS NOT         MBKCS112
03052 *    SPACES OR LOW VALUES AND POINT THE CURSOR TO THIS ENTRY.     MBKCS112
03053 *    IF ALL ENTRIES ARE SPACES OR LOW VALUES, POINT THE CURSOR    MBKCS112
03054 *    TO THE 1ST SELECTION ENTRY IN THE MAP.                       MBKCS112
03055 *                                                                 MBKCS112
03056 *    PERFORMED BY I800-SET-CURSOR-POSITION.                          CL*93
03057 ******************************************************************MBKCS112
03058                                                                   MBKCS112
03059      MOVE PC-ITEMS-PER-PANEL TO PS-SUB.                           MBKCS112
03060                                                                   MBKCS112
03061      SET PS-NO-SELECTION-FOUND TO TRUE.                           MBKCS112
03062                                                                   MBKCS112
03063      PERFORM                                                      MBKCS112
03064          UNTIL (PS-SUB = +0)        OR                            MBKCS112
03065                (PS-SELECTION-FOUND)                               MBKCS112
03066                                                                   MBKCS112
03067          IF (MB013O-ASELO(PS-SUB) NOT = SPACES)     AND              CL185
03068             (MB013O-ASELO(PS-SUB) NOT = LOW-VALUES)                  CL185
03069              SET PS-SELECTION-FOUND TO TRUE                       MBKCS112
03070          ELSE                                                     MBKCS112
03071              SUBTRACT +1 FROM PS-SUB                              MBKCS112
03072          END-IF                                                   MBKCS112
03073                                                                   MBKCS112
03074      END-PERFORM.                                                 MBKCS112
03075                                                                   MBKCS112
03076      IF PS-SELECTION-FOUND                                        MBKCS112
03077          MOVE -1 TO MB013I-ASELL(PS-SUB)                             CL185
03078      ELSE                                                         MBKCS112
03079          MOVE -1 TO MB013I-ASELL(1)                                  CL185
03080      END-IF.                                                      MBKCS112
03081  EJECT                                                            MBKCS112
03082  K100-SET-SUBSCRIPTS.                                             MBKCS112
03083                                                                   MBKCS112
03084 ******************************************************************MBKCS112
03085 *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM  MBKCS112
03086 *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL MBKCS112
03087 *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                   MBKCS112
03088 *                                                                 MBKCS112
03089 *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS    MBKCS112
03090 *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT     MBKCS112
03091 *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION   MBKCS112
03092 *    US IN THE 2ND BLOCK OF THE ARRAY.                            MBKCS112
03093 *                                                                    CL*92
03094 *    PERFORMED BY F300-CHECK-SELECTIONS                              CL*92
03095 ******************************************************************MBKCS112
03096                                                                   MBKCS112
03097      COMPUTE PS-ITEM-SUB =                                        MBKCS112
03098          (PV-ITEM-IN-USE - MB012-LIST-ITEM-NUMBER(1 1)) + 1.         CL183
03099                                                                   MBKCS112
03100      MOVE PS-ITEM-SUB TO MB012-ITEM-SUB.                             CL183
03101                                                                   MBKCS112
03102      IF MB012-ITEM-SUB > PC-ITEMS-PER-BLOCK                          CL183
03103          SUBTRACT PC-ITEMS-PER-BLOCK FROM MB012-ITEM-SUB             CL183
03104                                                                   MBKCS112
03105          MOVE PC-MAX-BLOCKS-IN-ARRAY TO MB012-BLOCK-SUB              CL183
03106      ELSE                                                         MBKCS112
03107          MOVE +1                     TO MB012-BLOCK-SUB              CL183
03108      END-IF.                                                      MBKCS112
03109 *                                                                    CL242
03110  EJECT                                                               CL242
03111 ******************************************************************   CL242
03112 *    PREPARE THE TEMPORARY STORAGE QUEUE.  MOVE ALL SELECTED         CL242
03113 *    GROUPS FROM EITHER TEMP STORAGE OR THE ARRAY IN THE COMM        CL242
03114 *    AREA TO THE LIST TS QUEUE RECORD(S).                            CL242
03115 *                                                                    CL242
03116 *    PERFORMED BY D300                                               CL242
03117 ******************************************************************   CL242
03118 *                                                                    CL242
03119  X100-PREPARE-LIST-QUEUE.                                            CL242
03120 *                                                                    CL242
03121      MOVE +1                   TO  MB012-BLOCK-SUB.                  CL245
03122      PERFORM Z150-WRITE-TEMP-STORAGE.                                CL242
03123      ADD +1                    TO  MB012-BLOCK-SUB.                  CL245
03124      PERFORM Z150-WRITE-TEMP-STORAGE.                                CL242
03125 *                                                                    CL242
03126      IF  MB012-HIGHEST-BLOCK-WRITTEN = ZERO                          CL245
03127          MOVE EIBTRMID         TO  PV-TERM-ID                        CL242
03128          MOVE EIBTASKN         TO  PV-TASK-NUMBER                    CL242
03129      ELSE                                                            CL242
03130          MOVE MB012-TERM-ID    TO  PV-TERM-ID                        CL245
03131          MOVE MB012-TASK-NUMBER                                      CL245
03132                                TO  PV-TASK-NUMBER                    CL242
03133      END-IF.                                                         CL242
03134      SUBTRACT 1              FROM  PV-TASK-NUMBER.                   CL242
03135 *                                                                    CL242
03136      MOVE PV-QUEUE-NAME        TO  MB005-LIST-QUEUE-NAME             CL242
03137                                    MB012-LIST-QUEUE-NAME.            CL245
03138      MOVE LENGTH OF SELECTED-LIST-RECORD                             CL242
03139                                TO  MB005-LIST-QUEUE-LENGTH.          CL242
03140      PERFORM X900-DELETE-LIST-QUEUE.                                 CL242
03141 *                                                                    CL242
03142      SET MB003-IDX             TO  1.                                CL255
03143      INITIALIZE SELECTED-LIST-RECORD.                                CL242
03144      PERFORM X200-PROCESS-TS-QUEUE                                   CL242
03145              VARYING PV-ITEM                                         CL242
03146              FROM 1 BY 1                                             CL242
03147              UNTIL PV-ITEM GREATER MB012-HIGHEST-BLOCK-WRITTEN.      CL245
03148 *                                                                    CL242
03149      PERFORM X500-WRITE-LIST-QUEUE.                                  CL242
03150 *                                                                    CL242
03151  EJECT                                                               CL242
03152 ******************************************************************   CL242
03153 *    OBTAIN A TS QUEUE RECORD, MOVE SELECTED RECORDS TO THE          CL242
03154 *    LIST QUEUE RECORD.                                              CL242
03155 *                                                                    CL242
03156 *    PERFORMED BY X100                                               CL242
03157 ******************************************************************   CL242
03158 *                                                                    CL242
03159  X200-PROCESS-TS-QUEUE.                                              CL242
03160 *                                                                    CL242
03161      PERFORM X250-READ-TS-QUEUE.                                     CL242
03162      IF  PV-RESP EQUAL DFHRESP(NORMAL)                               CL242
03163          PERFORM                                                     CL242
03164          VARYING PV-TW-IDX                                           CL242
03165          FROM 1 BY 1                                                 CL242
03166          UNTIL PV-TW-IDX GREATER THAN PC-ITEMS-PER-BLOCK             CL242
03167            OR  PV-TW-LIST-ITEM-NUMBER (PV-TW-IDX) EQUAL ZERO         CL242
03168 *                                                                    CL242
03169              IF  PV-TW-SELECT (PV-TW-IDX)                            CL242
03170                  MOVE PV-TW-KEY-DATA (PV-TW-IDX)                     CL242
03171                                TO  MB003-KEY-DATA (MB003-IDX)        CL255
03172                  MOVE PV-TW-LIST-ITEM-NUMBER (PV-TW-IDX)             CL242
03173                                TO  MB003-ITEM       (MB003-IDX)      CL255
03174                  IF  PV-TW-COLOR (PV-TW-IDX) = DP015-TURQUOISE       CL245
03175                      SET PS-SELECTED-HISTORY                         CL244
03176                                TO  TRUE                              CL244
03177                  END-IF                                              CL245
03178 *                                                                    CL245
03179                  SET MB003-IDX UP BY 1                               CL255
03180                  IF  MB003-IDX GREATER PC-MAX-QUEUE-ITEMS            CL255
03181                      PERFORM X500-WRITE-LIST-QUEUE                   CL242
03182                  END-IF                                              CL242
03183              END-IF                                                  CL242
03184 *                                                                    CL242
03185          END-PERFORM                                                 CL242
03186      END-IF.                                                         CL242
03187 *                                                                    CL242
03188  EJECT                                                               CL242
03189 ******************************************************************   CL242
03190 *    READ A TS QUEUE RECORD.                                         CL242
03191 *                                                                    CL242
03192 *    PERFORMED BY X200                                               CL242
03193 ******************************************************************   CL242
03194 *                                                                    CL242
03195  X250-READ-TS-QUEUE.                                                 CL242
03196 *                                                                    CL242
03197      EXEC CICS                                                       CL242
03198          READQ TS                                                    CL242
03199          QUEUE(MB012-TEMP-QUEUE-NAME)                                CL245
03200          INTO(PV-TEMP-WORK-AREA)                                     CL242
03201          LENGTH(PC-BLOCK-LENGTH)                                     CL242
03202          ITEM(PV-ITEM)                                               CL242
03203          RESP(PV-RESP)                                               CL242
03204      END-EXEC.                                                       CL242
03205 *                                                                    CL242
03206  EJECT                                                               CL242
03207 ******************************************************************   CL242
03208 *    WRITE THE LIST QUEUE RECORD.                                    CL242
03209 *                                                                    CL242
03210 *    PERFORMED BY X100                                               CL243
03211 ******************************************************************   CL242
03212 *                                                                    CL242
03213  X500-WRITE-LIST-QUEUE.                                              CL242
03214 *                                                                    CL242
03215      IF  MB003-IDX GREATER THAN 1                                    CL255
03216          MOVE PC-MAX-QUEUE-ITEMS                                     CL242
03217                                TO  MB003-MAX-ITEMS                   CL255
03218 *        MOVE MB012-NUMBER-OF-ITEMS-SELECTED                         CL245
03219 *                              TO  MB003-ITEMS-SELECTED              CL255
03220 *                                                                    CL242
03221          IF  DP020-SRC-AID EQUAL DP016-PF14                          CL242
03222          OR  DP020-SRC-AID EQUAL DP016-PF18                          CL242
03223              SET MB003-INQUIRY TO    TRUE                            CL255
03224          END-IF                                                      CL242
03225          IF  DP020-SRC-AID EQUAL DP016-PF15                          CL242
03226          OR  DP020-SRC-AID EQUAL DP016-PF19                          CL242
03227              SET MB003-UPDATE    TO  TRUE                            CL255
03228          END-IF                                                      CL242
03229 *                                                                    CL242
03230          EXEC CICS                                                   CL242
03231               WRITEQ TS                                              CL242
03232               QUEUE(MB005-LIST-QUEUE-NAME)                           CL242
03233               FROM(SELECTED-LIST-RECORD)                             CL242
03234               LENGTH(MB005-LIST-QUEUE-LENGTH)                        CL242
03235               ITEM(PV-MA-ITEM)                                       CL242
03236               RESP(PV-RESP)                                          CL242
03237          END-EXEC                                                    CL242
03238 *                                                                    CL242
03239          IF  PV-RESP NOT EQUAL DFHRESP(NORMAL)                       CL242
03240              SET DP013-LOGIC-ABEND TO TRUE                           CL242
03241              SET DP013-NO-ROLLBACK TO TRUE                           CL242
03242              MOVE 'X500-WRITE-LIST-QUEUE'   TO                       CL242
03243                   DP013-PARAGRAPH                                    CL242
03244              MOVE PV-BLOCK-NUMBER           TO                       CL242
03245                   AM-BLOCK-NUMBER-11                                 CL242
03246              MOVE AM-ABEND-MESSAGE-11       TO                       CL242
03247                   DP013-MESSAGE-TEXT(1)                              CL242
03248              PERFORM DP013-0000-PROCESS-ABEND                        CL242
03249          END-IF                                                      CL242
03250 *                                                                    CL242
03251          INITIALIZE SELECTED-LIST-RECORD                             CL242
03252          SET MB003-IDX         TO  1                                 CL255
03253      END-IF.                                                         CL242
03254 *                                                                    CL242
03255  EJECT                                                               CL242
03256 ******************************************************************   CL242
03257 *    WRITE THE TS QUEUE RECORD JUST UPDATED IN C700.                 CL242
03258 *                                                                    CL242
03259 *    PERFORMED BY C600, C700                                         CL242
03260 ******************************************************************   CL242
03261 *                                                                    CL242
03262  X550-WRITE-TS-QUEUE.                                                CL242
03263 *                                                                    CL242
03264      EXEC CICS                                                       CL242
03265          WRITEQ TS                                                   CL242
03266          REWRITE                                                     CL242
03267          QUEUE(MB012-TEMP-QUEUE-NAME)                                CL245
03268          FROM(PV-TEMP-WORK-AREA)                                     CL242
03269          LENGTH(PC-BLOCK-LENGTH)                                     CL242
03270          ITEM(PV-SAVE-ITEM)                                          CL242
03271          RESP(PV-RESP)                                               CL242
03272      END-EXEC.                                                       CL242
03273 *                                                                    CL242
03274  EJECT                                                               CL242
03275 ******************************************************************   CL242
03276 *    READ THE LIST QUEUE RECORD.                                     CL242
03277 *                                                                    CL242
03278 *    PERFORMED BY C600                                               CL242
03279 ******************************************************************   CL242
03280 *                                                                    CL242
03281  X600-READ-LIST-QUEUE.                                               CL242
03282 *                                                                    CL242
03283      EXEC CICS                                                       CL242
03284           READQ TS                                                   CL242
03285           QUEUE(MB005-LIST-QUEUE-NAME)                               CL242
03286           INTO(SELECTED-LIST-RECORD)                                 CL242
03287           LENGTH(MB005-LIST-QUEUE-LENGTH)                            CL242
03288           ITEM(PV-MA-ITEM)                                           CL242
03289           RESP(PV-RESP)                                              CL242
03290      END-EXEC.                                                       CL242
03291 *                                                                    CL242
03292  EJECT                                                               CL242
03293 ******************************************************************   CL242
03294 *    DELETE THE SKU LIST TEMP STORAGE QUEUE.                         CL242
03295 *                                                                    CL242
03296 *    PERFORMED BY X100                                               CL242
03297 ******************************************************************   CL242
03298 *                                                                    CL242
03299  X900-DELETE-LIST-QUEUE.                                             CL242
03300 *                                                                    CL242
03301      EXEC CICS                                                       CL242
03302          DELETEQ TS                                                  CL242
03303          QUEUE(MB005-LIST-QUEUE-NAME)                                CL242
03304          RESP(PV-RESP)                                               CL242
03305      END-EXEC.                                                       CL242
03306 *                                                                    CL242
03307  EJECT                                                            MBKCS112
03308  Z050-DELETE-TEMP-STORAGE.                                        MBKCS112
03309                                                                   MBKCS112
03310 ******************************************************************MBKCS112
03311 *    DELETE THE TEMPORARY STORAGE QUEUE.                          MBKCS112
03312 *                                                                 MBKCS112
03313 *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                           CL*93
03314 ******************************************************************MBKCS112
03315                                                                   MBKCS112
03316      EXEC CICS                                                    MBKCS112
03317          DELETEQ TS                                               MBKCS112
03318          QUEUE(MB012-TEMP-QUEUE-NAME)                                CL183
03319          RESP(PV-CICS-RESPONSE)                                      CL**4
03320      END-EXEC.                                                    MBKCS112
03321                                                                   MBKCS112
03322      IF (PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)) AND                 CL**4
03323         (PV-CICS-RESPONSE NOT = DFHRESP(QIDERR))                     CL**4
03324          SET DP013-LOGIC-ABEND           TO TRUE                     CL*22
03325          SET DP013-NO-ROLLBACK           TO TRUE                     CL*22
03326          MOVE 'Z050-DELETE-TEMP-STORAGE' TO DP013-PARAGRAPH          CL*22
03327          MOVE AM-ABEND-MESSAGE-8         TO DP013-MESSAGE-TEXT(1)    CL*22
03328      END-IF.                                                      MBKCS112
03329                                                                   MBKCS112
03330      MOVE +0 TO MB012-HIGHEST-BLOCK-WRITTEN.                         CL183
03331  EJECT                                                            MBKCS112
03332  Z100-READ-TEMP-STORAGE.                                          MBKCS112
03333                                                                   MBKCS112
03334 ******************************************************************MBKCS112
03335 *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.          MBKCS112
03336 *                                                                 MBKCS112
03337 *    PERFORMED BY F200-MARK-RANGE-AS-SELECTED.                       CL*92
03338 ******************************************************************MBKCS112
03339                                                                   MBKCS112
03340      EXEC CICS                                                    MBKCS112
03341          READQ TS                                                 MBKCS112
03342          QUEUE(MB012-TEMP-QUEUE-NAME)                                CL183
03343          INTO(MB012-BLOCK-ENTRIES(MB012-BLOCK-SUB))                  CL183
03344          LENGTH(PC-BLOCK-LENGTH)                                  MBKCS112
03345          ITEM(PV-BLOCK-NUMBER)                                    MBKCS112
03346          RESP(PV-CICS-RESPONSE)                                      CL**4
03347      END-EXEC.                                                    MBKCS112
03348                                                                   MBKCS112
03349      IF (PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)) AND                 CL*69
03350         (PV-CICS-RESPONSE NOT = DFHRESP(ITEMERR))                    CL*69
03351                                                                   MBKCS112
03352          EXEC CICS                                                MBKCS112
03353               DELETEQ TS                                          MBKCS112
03354               QUEUE(MB012-TEMP-QUEUE-NAME)                           CL183
03355               RESP(PV-CICS-RESPONSE)                                 CL**4
03356          END-EXEC                                                 MBKCS112
03357                                                                   MBKCS112
03358          SET DP013-LOGIC-ABEND         TO TRUE                       CL*28
03359          SET DP013-NO-ROLLBACK         TO TRUE                       CL*28
03360          MOVE 'Z100-READ-TEMP-STORAGE' TO DP013-PARAGRAPH            CL*28
03361          MOVE AM-ABEND-MESSAGE-12      TO DP013-MESSAGE-TEXT(1)      CL*57
03362          PERFORM DP013-0000-PROCESS-ABEND                            CL*28
03363                                                                      CL*28
03364      END-IF.                                                      MBKCS112
03365                                                                   MBKCS112
03366      SET MB012-BLOCK-NOT-MODIFIED(MB012-BLOCK-SUB) TO TRUE.          CL183
03367  EJECT                                                            MBKCS112
03368  Z150-WRITE-TEMP-STORAGE.                                         MBKCS112
03369                                                                   MBKCS112
03370 ******************************************************************MBKCS112
03371 *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.       MBKCS112
03372 *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.     MBKCS112
03373 *                                                                 MBKCS112
03374 *    PERFORMED BY F200-MARK-RANGE-AS-SELECTED                        CL*92
03375 ******************************************************************MBKCS112
03376                                                                   MBKCS112
03377      IF MB012-HIGHEST-BLOCK-WRITTEN = +0                             CL183
03378          MOVE EIBTASKN TO DP020-SRC-TASK-NUMBER                   MBKCS112
03379          MOVE EIBTASKN TO MB012-TASK-NUMBER                          CL183
03380          MOVE EIBTRMID TO MB012-TERM-ID                              CL183
03381      END-IF.                                                      MBKCS112
03382                                                                   MBKCS112
03383      MOVE MB012-LIST-ITEM-NUMBER(MB012-BLOCK-SUB 1) TO               CL183
03384           PV-LIST-ITEM-NUMBER.                                    MBKCS112
03385                                                                   MBKCS112
03386      COMPUTE PV-BLOCK-NUMBER =                                    MBKCS112
03387          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-BLOCK).    MBKCS112
03388                                                                   MBKCS112
03389      IF (PV-BLOCK-NUMBER < MB012-HIGHEST-BLOCK-WRITTEN) OR           CL183
03390         (PV-BLOCK-NUMBER = MB012-HIGHEST-BLOCK-WRITTEN)              CL183
03391                                                                   MBKCS112
03392          IF MB012-BLOCK-MODIFIED(MB012-BLOCK-SUB)                    CL183
03393                                                                   MBKCS112
03394              EXEC CICS                                            MBKCS112
03395                  WRITEQ TS                                        MBKCS112
03396                  QUEUE(MB012-TEMP-QUEUE-NAME)                        CL183
03397                  FROM(MB012-BLOCK-ENTRIES(MB012-BLOCK-SUB))          CL183
03398                  LENGTH(PC-BLOCK-LENGTH)                          MBKCS112
03399                  ITEM(PV-BLOCK-NUMBER)                            MBKCS112
03400                  REWRITE                                          MBKCS112
03401                  RESP(PV-CICS-RESPONSE)                              CL**4
03402              END-EXEC                                             MBKCS112
03403                                                                   MBKCS112
03404              IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)               CL**4
03405                  EXEC CICS                                        MBKCS112
03406                      DELETEQ TS                                   MBKCS112
03407                      QUEUE(MB012-TEMP-QUEUE-NAME)                    CL183
03408                      RESP(PV-CICS-RESPONSE)                          CL**4
03409                  END-EXEC                                         MBKCS112
03410                                                                   MBKCS112
03411                  SET DP013-LOGIC-ABEND TO TRUE                       CL*26
03412                  SET DP013-NO-ROLLBACK TO TRUE                       CL*26
03413                  MOVE 'Z150-WRITE-TEMP-STORAGE' TO                   CL*26
03414                       DP013-PARAGRAPH                                CL*26
03415                  MOVE AM-ABEND-MESSAGE-9        TO                   CL*26
03416                       DP013-MESSAGE-TEXT(1)                          CL*26
03417                  PERFORM DP013-0000-PROCESS-ABEND                    CL*26
03418              END-IF                                               MBKCS112
03419                                                                   MBKCS112
03420          END-IF                                                   MBKCS112
03421                                                                   MBKCS112
03422      ELSE                                                         MBKCS112
03423                                                                   MBKCS112
03424          COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                       MBKCS112
03425              MB012-HIGHEST-BLOCK-WRITTEN + 1                         CL183
03426                                                                   MBKCS112
03427          IF PV-BLOCK-NUMBER = PV-HIGHEST-BLOCK-WRITTEN            MBKCS112
03428                                                                   MBKCS112
03429              EXEC CICS                                            MBKCS112
03430                  WRITEQ TS                                        MBKCS112
03431                  QUEUE(MB012-TEMP-QUEUE-NAME)                        CL183
03432                  FROM(MB012-BLOCK-ENTRIES(MB012-BLOCK-SUB))          CL183
03433                  LENGTH(PC-BLOCK-LENGTH)                          MBKCS112
03434                  ITEM(PV-BLOCK-NUMBER)                            MBKCS112
03435                  RESP(PV-CICS-RESPONSE)                              CL**4
03436              END-EXEC                                             MBKCS112
03437                                                                   MBKCS112
03438              IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)               CL**4
03439                                                                      CL**3
03440                  EXEC CICS                                           CL**3
03441                      DELETEQ TS                                   MBKCS112
03442                      QUEUE(MB012-TEMP-QUEUE-NAME)                    CL183
03443                      RESP(PV-CICS-RESPONSE)                          CL**4
03444                  END-EXEC                                         MBKCS112
03445                                                                   MBKCS112
03446                  SET DP013-LOGIC-ABEND TO TRUE                       CL*26
03447                  SET DP013-NO-ROLLBACK TO TRUE                       CL*26
03448                  MOVE 'Z150-WRITE-TEMP-STORAGE' TO                   CL*26
03449                       DP013-PARAGRAPH                                CL*26
03450                  MOVE AM-ABEND-MESSAGE-10       TO                   CL*26
03451                       DP013-MESSAGE-TEXT(1)                          CL*26
03452                  PERFORM DP013-0000-PROCESS-ABEND                    CL*26
03453              END-IF                                               MBKCS112
03454                                                                   MBKCS112
03455              ADD +1 TO MB012-HIGHEST-BLOCK-WRITTEN                   CL183
03456          ELSE                                                     MBKCS112
03457              SET DP013-LOGIC-ABEND TO TRUE                           CL*26
03458              SET DP013-NO-ROLLBACK TO TRUE                           CL*26
03459              MOVE 'Z150-WRITE-TEMP-STORAGE' TO                       CL*26
03460                   DP013-PARAGRAPH                                    CL*26
03461              MOVE PV-BLOCK-NUMBER           TO                       CL*26
03462                   AM-BLOCK-NUMBER-11                                 CL*26
03463              MOVE AM-ABEND-MESSAGE-11       TO                       CL*26
03464                   DP013-MESSAGE-TEXT(1)                              CL*26
03465              PERFORM DP013-0000-PROCESS-ABEND                        CL*26
03466          END-IF                                                   MBKCS112
03467                                                                   MBKCS112
03468      END-IF.                                                      MBKCS112
03469                                                                   MBKCS112
03470      SET MB012-BLOCK-NOT-MODIFIED(MB012-BLOCK-SUB) TO TRUE.          CL183
03471  EJECT                                                            MBKCS112
03472  Z200-OPEN-MERCH-CURSOR.                                             CL*92
03473                                                                      CL*92
03474 ******************************************************************   CL*39
03475 *    OPEN MERCHANDISE AREA CURSOR                                    CL*39
03476 ******************************************************************   CL*39
03477                                                                      CL*39
03478      EXEC SQL                                                        CL*39
03479          OPEN MERCH-CURSOR                                           CL*39
03480      END-EXEC.                                                       CL*39
03481                                                                      CL*58
03482      IF (SQLCODE = +0)   OR                                          CL*58
03483         (SQLCODE = +100)                                             CL*58
03484          SET PS-MERCH-CURSOR-OPENED     TO TRUE                      CL*84
03485      ELSE                                                            CL*58
03486          SET DP013-DB2-ABEND            TO TRUE                      CL*58
03487          SET DP013-NO-ROLLBACK          TO TRUE                      CL*58
03488          MOVE 'Z200-OPEN-MERCH-CURSOR'  TO                           CL*58
03489               DP013-PARAGRAPH                                        CL*58
03490          MOVE AM-ABEND-MESSAGE-21       TO                           CL*58
03491               DP013-MESSAGE-TEXT(1)                                  CL*58
03492          MOVE SQLCA                     TO                           CL*58
03493               DP013-SQLCA                                            CL*58
03494          PERFORM DP013-0000-PROCESS-ABEND                            CL*58
03495      END-IF.                                                         CL*58
03496  EJECT                                                               CL*39
03497  Z210-READ-MERCH-AREA.                                               CL*39
03498                                                                      CL137
03499 ******************************************************************   CL137
03500 *    READ MERCHANDISE AREA CURSOR                                    CL137
03501 ******************************************************************   CL137
03502      EXEC SQL                                                        CL218
03503          FETCH MERCH-CURSOR                                          CL218
03504           INTO :MDSEAR-OPERCO-NBR,                                   CL218
03505                :MDSEAR-GMA-NBR,                                      CL218
03506                :MDSEAR-DMA-NBR,                                      CL218
03507                :MDSEAR-DEPT-NBR,                                     CL218
03508                :MDSEAR-MAJ-CL-NBR,                                   CL218
03509                :MDSEAR-SUB-CL-NBR,                                   CL218
03510                :MDSEAR-MDSELV-CDE,                                   CL218
03511                :MDSEAR-CHG-DTE,                                      CL218
03512                :MDSEAR-CRE-DTE,                                      CL218
03513                :MDSEAR-STRT-DTE,                                     CL218
03514                :MDSEAR-END-DTE,                                      CL218
03515                :MDSEAR-GEN-MDSE-TYP-CDE,                             CL218
03516                :MDSEAR-DESC,                                         CL218
03517                :MDSEAR-BYR-NBR,                                      CL218
03518                :MDSEAR-LIST-SEQ-NBR                                  CL218
03519      END-EXEC.                                                       CL218
03520                                                                      CL218
03521      SET PS-DB2-OK TO TRUE                                           CL218
03522                                                                      CL218
03523      IF (SQLCODE = -904) OR                                          CL218
03524         (SQLCODE = -913)                                             CL218
03525          SET PS-DB2-RETRY TO TRUE                                    CL218
03526          ADD 1 TO PC-RETRY-CNTR                                      CL218
03527          IF PC-RETRY-CNTR > PC-MAX-RETRYS                            CL218
03528              SET PS-DB2-OK TO TRUE                                   CL218
03529          END-IF                                                      CL218
03530      END-IF.                                                         CL218
03531                                                                      CL218
03532      IF PS-DB2-OK                                                    CL218
03533          IF SQLCODE = +0                                             CL218
03534              CONTINUE                                                CL218
03535          ELSE                                                        CL218
03536              IF SQLCODE = +100                                       CL218
03537                  SET MB012-NO-ITEMS-LEFT-ON-DB TO TRUE               CL218
03538              ELSE                                                    CL218
03539                  SET DP013-DB2-ABEND   TO TRUE                       CL218
03540                  SET DP013-NO-ROLLBACK TO TRUE                       CL218
03541                  MOVE 'Z210-READ-MERCH-CURSOR' TO DP013-PARAGRAPH    CL218
03542                  MOVE PC-OPERCO-NBR      TO AM-OPERCO-NBR-13         CL218
03543                  MOVE MB012-SAVE-GMA     TO AM-GMA-NBR-13            CL218
03544                  MOVE MB012-SAVE-DMA     TO AM-DMA-NBR-13            CL218
03545                  MOVE MB012-SAVE-DPT     TO AM-DEPT-NBR-13           CL218
03546                  MOVE AM-ABEND-MESSAGE-13 TO DP013-MESSAGE-TEXT(1)   CL218
03547                  MOVE SQLCA              TO DP013-SQLCA              CL218
03548                  MOVE 'TMDSEAR'  TO DP013-DB2-TABLE-NAME(1)          CL221
03549                  PERFORM DP013-0000-PROCESS-ABEND                    CL218
03550              END-IF                                                  CL218
03551          END-IF                                                      CL218
03552      END-IF.                                                         CL218
03553  EJECT                                                               CL*39
03554  Z220-CLOSE-MERCH-CURSOR.                                            CL*42
03555                                                                      CL*42
03556 ******************************************************************MBKCS112
03557 *    CLOSE THE MERCH-CURSOR                                          CL*42
03558 ******************************************************************MBKCS112
03559                                                                   MBKCS112
03560      EXEC SQL                                                        CL*42
03561          CLOSE MERCH-CURSOR                                          CL*42
03562      END-EXEC.                                                       CL*42
03563                                                                      CL*42
03564      IF (SQLCODE = +0)   OR                                          CL*42
03565         (SQLCODE = +100)                                             CL*42
03566          SET PS-MERCH-CURSOR-NOT-OPENED TO TRUE                      CL*84
03567      ELSE                                                            CL*42
03568          SET DP013-DB2-ABEND            TO TRUE                      CL*42
03569          SET DP013-NO-ROLLBACK          TO TRUE                      CL*42
03570          MOVE 'Z210-CLOSE-MERCH-CURSOR' TO                           CL*42
03571               DP013-PARAGRAPH                                        CL*42
03572          MOVE AM-ABEND-MESSAGE-22       TO                           CL*42
03573               DP013-MESSAGE-TEXT(1)                                  CL*42
03574          MOVE SQLCA                     TO                           CL*42
03575               DP013-SQLCA                                            CL*42
03576          PERFORM DP013-0000-PROCESS-ABEND                            CL*42
03577      END-IF.                                                         CL*42
03578  EJECT                                                            MBKCS112
03579  Z300-OPEN-GMA-CURSOR.                                               CL*97
03580                                                                      CL*97
03581 ******************************************************************   CL*97
03582 *    OPEN MERCHANDISE AREA CURSOR -- GMA ONLY                        CL*97
03583 ******************************************************************   CL*97
03584                                                                      CL*97
03585      EXEC SQL                                                        CL*97
03586          OPEN GMA-CURSOR                                             CL*97
03587      END-EXEC.                                                       CL*97
03588                                                                      CL*97
03589      IF (SQLCODE = +0)   OR                                          CL*97
03590         (SQLCODE = +100)                                             CL*97
03591          SET PS-GMA-CURSOR-OPENED       TO TRUE                      CL112
03592      ELSE                                                            CL*97
03593          SET DP013-DB2-ABEND            TO TRUE                      CL*97
03594          SET DP013-NO-ROLLBACK          TO TRUE                      CL*97
03595          MOVE 'Z300-OPEN-GMA-CURSOR'    TO                           CL*98
03596               DP013-PARAGRAPH                                        CL*97
03597          MOVE AM-ABEND-MESSAGE-21       TO                           CL*97
03598               DP013-MESSAGE-TEXT(1)                                  CL*97
03599          MOVE SQLCA                     TO                           CL*97
03600               DP013-SQLCA                                            CL*97
03601          PERFORM DP013-0000-PROCESS-ABEND                            CL*97
03602      END-IF.                                                         CL*97
03603  EJECT                                                               CL*97
03604  Z310-READ-GMA-AREA.                                                 CL*98
03605                                                                      CL137
03606 ******************************************************************   CL137
03607 *    READ MERCHANDISE AREA CURSOR -- GMA ONLY                        CL137
03608 ******************************************************************   CL137
03609                                                                      CL*97
03610      EXEC SQL                                                        CL*97
03611          FETCH GMA-CURSOR                                            CL111
03612           INTO :MDSEAR-OPERCO-NBR,                                   CL*97
03613                :MDSEAR-GMA-NBR,                                      CL*97
03614                :MDSEAR-DMA-NBR,                                      CL*97
03615                :MDSEAR-DEPT-NBR,                                     CL*97
03616                :MDSEAR-MAJ-CL-NBR,                                   CL*97
03617                :MDSEAR-SUB-CL-NBR,                                   CL*97
03618                :MDSEAR-MDSELV-CDE,                                   CL*97
03619                :MDSEAR-CHG-DTE,                                      CL*97
03620                :MDSEAR-CRE-DTE,                                      CL*97
03621                :MDSEAR-STRT-DTE,                                     CL*97
03622                :MDSEAR-END-DTE,                                      CL*97
03623                :MDSEAR-GEN-MDSE-TYP-CDE,                             CL*97
03624                :MDSEAR-DESC,                                         CL*97
03625                :MDSEAR-BYR-NBR,                                      CL108
03626                :MDSEAR-LIST-SEQ-NBR                                  CL108
03627      END-EXEC.                                                       CL*97
03628                                                                      CL*97
03629      SET PS-DB2-OK TO TRUE                                           CL*97
03630                                                                      CL*97
03631      IF (SQLCODE = -904) OR                                          CL*97
03632         (SQLCODE = -913)                                             CL*97
03633          SET PS-DB2-RETRY TO TRUE                                    CL*97
03634          ADD 1 TO PC-RETRY-CNTR                                      CL*97
03635          IF PC-RETRY-CNTR > PC-MAX-RETRYS                            CL*97
03636              SET PS-DB2-OK TO TRUE                                   CL*97
03637          END-IF                                                      CL*97
03638      END-IF.                                                         CL*97
03639                                                                      CL*97
03640      IF PS-DB2-OK                                                    CL*97
03641          IF SQLCODE = +0                                             CL*97
03642              CONTINUE                                                CL*97
03643          ELSE                                                        CL*97
03644              IF SQLCODE = +100                                       CL*97
03645                  SET MB012-NO-ITEMS-LEFT-ON-DB TO TRUE               CL183
03646              ELSE                                                    CL*97
03647                  SET DP013-DB2-ABEND   TO TRUE                       CL*97
03648                  SET DP013-NO-ROLLBACK TO TRUE                       CL*97
03649                  MOVE 'Z310-READ-GMA-CURSOR'   TO                    CL119
03650                       DP013-PARAGRAPH                                CL*97
03651                  MOVE PC-OPERCO-NBR            TO                    CL*97
03652                       AM-OPERCO-NBR-13                               CL*97
03653                  MOVE MB012-SAVE-GMA           TO                    CL183
03654                       AM-GMA-NBR-13                                  CL*97
03655                  MOVE MB012-SAVE-DMA           TO                    CL183
03656                       AM-DMA-NBR-13                                  CL*97
03657                  MOVE MB012-SAVE-DPT           TO                    CL183
03658                       AM-DEPT-NBR-13                                 CL*97
03659                  MOVE AM-ABEND-MESSAGE-13      TO                    CL*97
03660                       DP013-MESSAGE-TEXT(1)                          CL*97
03661                  MOVE SQLCA                    TO                    CL*97
03662                       DP013-SQLCA                                    CL*97
03663                  PERFORM DP013-0000-PROCESS-ABEND                    CL*97
03664              END-IF                                                  CL*97
03665          END-IF                                                      CL*97
03666      END-IF.                                                         CL*97
03667  EJECT                                                               CL*97
03668  Z320-CLOSE-GMA-CURSOR.                                              CL112
03669                                                                      CL*97
03670 ******************************************************************   CL*97
03671 *    CLOSE THE GMA-CURSOR                                            CL*98
03672 ******************************************************************   CL*97
03673                                                                      CL*97
03674      EXEC SQL                                                        CL*97
03675          CLOSE GMA-CURSOR                                            CL*98
03676      END-EXEC.                                                       CL*97
03677                                                                      CL*97
03678      IF (SQLCODE = +0)   OR                                          CL*97
03679         (SQLCODE = +100)                                             CL*97
03680          SET PS-GMA-CURSOR-NOT-OPENED TO TRUE                        CL*98
03681      ELSE                                                            CL*97
03682          SET DP013-DB2-ABEND            TO TRUE                      CL*97
03683          SET DP013-NO-ROLLBACK          TO TRUE                      CL*97
03684          MOVE 'Z310-CLOSE-GMA-CURSOR'   TO                           CL*98
03685               DP013-PARAGRAPH                                        CL*97
03686          MOVE AM-ABEND-MESSAGE-22       TO                           CL*97
03687               DP013-MESSAGE-TEXT(1)                                  CL*97
03688          MOVE SQLCA                     TO                           CL*97
03689               DP013-SQLCA                                            CL*97
03690          PERFORM DP013-0000-PROCESS-ABEND                            CL*97
03691      END-IF.                                                         CL*97
03692  EJECT                                                               CL*97
03693  Z400-OPEN-DMA-CURSOR.                                               CL119
03694                                                                      CL119
03695 ******************************************************************   CL119
03696 *    OPEN MERCHANDISE AREA CURSOR -- DMA ONLY                        CL119
03697 ******************************************************************   CL119
03698                                                                      CL119
03699      EXEC SQL                                                        CL119
03700          OPEN DMA-CURSOR                                             CL119
03701      END-EXEC.                                                       CL119
03702                                                                      CL119
03703      IF (SQLCODE = +0)   OR                                          CL119
03704         (SQLCODE = +100)                                             CL119
03705          SET PS-DMA-CURSOR-OPENED       TO TRUE                      CL119
03706      ELSE                                                            CL119
03707          SET DP013-DB2-ABEND            TO TRUE                      CL119
03708          SET DP013-NO-ROLLBACK          TO TRUE                      CL119
03709          MOVE 'Z400-OPEN-DMA-CURSOR'    TO                           CL119
03710               DP013-PARAGRAPH                                        CL119
03711          MOVE AM-ABEND-MESSAGE-21       TO                           CL119
03712               DP013-MESSAGE-TEXT(1)                                  CL119
03713          MOVE SQLCA                     TO                           CL119
03714               DP013-SQLCA                                            CL119
03715          PERFORM DP013-0000-PROCESS-ABEND                            CL119
03716      END-IF.                                                         CL119
03717  EJECT                                                               CL119
03718  Z410-READ-DMA-AREA.                                                 CL119
03719                                                                      CL137
03720 ******************************************************************   CL137
03721 *    READ MERCHANDISE AREA CURSOR -- DMA ONLY                        CL137
03722 ******************************************************************   CL137
03723                                                                      CL119
03724      EXEC SQL                                                        CL119
03725          FETCH DMA-CURSOR                                            CL119
03726           INTO :MDSEAR-OPERCO-NBR,                                   CL119
03727                :MDSEAR-GMA-NBR,                                      CL119
03728                :MDSEAR-DMA-NBR,                                      CL119
03729                :MDSEAR-DEPT-NBR,                                     CL119
03730                :MDSEAR-MAJ-CL-NBR,                                   CL119
03731                :MDSEAR-SUB-CL-NBR,                                   CL119
03732                :MDSEAR-MDSELV-CDE,                                   CL119
03733                :MDSEAR-CHG-DTE,                                      CL119
03734                :MDSEAR-CRE-DTE,                                      CL119
03735                :MDSEAR-STRT-DTE,                                     CL119
03736                :MDSEAR-END-DTE,                                      CL119
03737                :MDSEAR-GEN-MDSE-TYP-CDE,                             CL119
03738                :MDSEAR-DESC,                                         CL119
03739                :MDSEAR-BYR-NBR,                                      CL119
03740                :MDSEAR-LIST-SEQ-NBR                                  CL119
03741      END-EXEC.                                                       CL119
03742                                                                      CL119
03743      SET PS-DB2-OK TO TRUE                                           CL119
03744                                                                      CL119
03745      IF (SQLCODE = -904) OR                                          CL119
03746         (SQLCODE = -913)                                             CL119
03747          SET PS-DB2-RETRY TO TRUE                                    CL119
03748          ADD 1 TO PC-RETRY-CNTR                                      CL119
03749          IF PC-RETRY-CNTR > PC-MAX-RETRYS                            CL119
03750              SET PS-DB2-OK TO TRUE                                   CL119
03751          END-IF                                                      CL119
03752      END-IF.                                                         CL119
03753                                                                      CL119
03754      IF PS-DB2-OK                                                    CL119
03755          IF SQLCODE = +0                                             CL119
03756              CONTINUE                                                CL119
03757          ELSE                                                        CL119
03758              IF SQLCODE = +100                                       CL119
03759                  SET MB012-NO-ITEMS-LEFT-ON-DB TO TRUE               CL183
03760              ELSE                                                    CL119
03761                  SET DP013-DB2-ABEND   TO TRUE                       CL119
03762                  SET DP013-NO-ROLLBACK TO TRUE                       CL119
03763                  MOVE 'Z410-READ-DMA-CURSOR'   TO                    CL119
03764                       DP013-PARAGRAPH                                CL119
03765                  MOVE PC-OPERCO-NBR            TO                    CL119
03766                       AM-OPERCO-NBR-13                               CL119
03767                  MOVE MB012-SAVE-GMA           TO                    CL183
03768                       AM-GMA-NBR-13                                  CL119
03769                  MOVE MB012-SAVE-DMA           TO                    CL183
03770                       AM-DMA-NBR-13                                  CL119
03771                  MOVE MB012-SAVE-DPT           TO                    CL183
03772                       AM-DEPT-NBR-13                                 CL119
03773                  MOVE AM-ABEND-MESSAGE-13      TO                    CL119
03774                       DP013-MESSAGE-TEXT(1)                          CL119
03775                  MOVE SQLCA                    TO                    CL119
03776                       DP013-SQLCA                                    CL119
03777                  PERFORM DP013-0000-PROCESS-ABEND                    CL119
03778              END-IF                                                  CL119
03779          END-IF                                                      CL119
03780      END-IF.                                                         CL119
03781  EJECT                                                               CL119
03782  Z420-CLOSE-DMA-CURSOR.                                              CL119
03783                                                                      CL119
03784 ******************************************************************   CL119
03785 *    CLOSE THE DMA-CURSOR                                            CL119
03786 ******************************************************************   CL119
03787                                                                      CL119
03788      EXEC SQL                                                        CL119
03789          CLOSE DMA-CURSOR                                            CL119
03790      END-EXEC.                                                       CL119
03791                                                                      CL119
03792      IF (SQLCODE = +0)   OR                                          CL119
03793         (SQLCODE = +100)                                             CL119
03794          SET PS-DMA-CURSOR-NOT-OPENED TO TRUE                        CL119
03795      ELSE                                                            CL119
03796          SET DP013-DB2-ABEND            TO TRUE                      CL119
03797          SET DP013-NO-ROLLBACK          TO TRUE                      CL119
03798          MOVE 'Z410-CLOSE-DMA-CURSOR'   TO                           CL119
03799               DP013-PARAGRAPH                                        CL119
03800          MOVE AM-ABEND-MESSAGE-22       TO                           CL119
03801               DP013-MESSAGE-TEXT(1)                                  CL119
03802          MOVE SQLCA                     TO                           CL119
03803               DP013-SQLCA                                            CL119
03804          PERFORM DP013-0000-PROCESS-ABEND                            CL119
03805      END-IF.                                                         CL119
03806  EJECT                                                               CL119
03807  Z500-OPEN-DPT-CURSOR.                                               CL131
03808                                                                      CL129
03809 ******************************************************************   CL129
03810 *    OPEN MERCHANDISE AREA CURSOR -- DPT ONLY                        CL129
03811 ******************************************************************   CL129
03812                                                                      CL129
03813      EXEC SQL                                                        CL129
03814          OPEN DPT-CURSOR                                             CL129
03815      END-EXEC.                                                       CL129
03816                                                                      CL129
03817      IF (SQLCODE = +0)   OR                                          CL129
03818         (SQLCODE = +100)                                             CL129
03819          SET PS-DPT-CURSOR-OPENED       TO TRUE                      CL129
03820      ELSE                                                            CL129
03821          SET DP013-DB2-ABEND            TO TRUE                      CL129
03822          SET DP013-NO-ROLLBACK          TO TRUE                      CL129
03823          MOVE 'Z500-OPEN-DPT-CURSOR'    TO                           CL129
03824               DP013-PARAGRAPH                                        CL129
03825          MOVE AM-ABEND-MESSAGE-21       TO                           CL129
03826               DP013-MESSAGE-TEXT(1)                                  CL129
03827          MOVE SQLCA                     TO                           CL129
03828               DP013-SQLCA                                            CL129
03829          PERFORM DP013-0000-PROCESS-ABEND                            CL129
03830      END-IF.                                                         CL129
03831  EJECT                                                               CL129
03832  Z510-READ-DPT-AREA.                                                 CL129
03833                                                                      CL137
03834 ******************************************************************   CL137
03835 *    READ MERCHANDISE AREA CURSOR -- DPT ONLY                        CL137
03836 ******************************************************************   CL137
03837                                                                      CL129
03838      EXEC SQL                                                        CL129
03839          FETCH DPT-CURSOR                                            CL129
03840           INTO :MDSEAR-OPERCO-NBR,                                   CL129
03841                :MDSEAR-GMA-NBR,                                      CL129
03842                :MDSEAR-DMA-NBR,                                      CL129
03843                :MDSEAR-DEPT-NBR,                                     CL129
03844                :MDSEAR-MAJ-CL-NBR,                                   CL129
03845                :MDSEAR-SUB-CL-NBR,                                   CL129
03846                :MDSEAR-MDSELV-CDE,                                   CL129
03847                :MDSEAR-CHG-DTE,                                      CL129
03848                :MDSEAR-CRE-DTE,                                      CL129
03849                :MDSEAR-STRT-DTE,                                     CL129
03850                :MDSEAR-END-DTE,                                      CL129
03851                :MDSEAR-GEN-MDSE-TYP-CDE,                             CL129
03852                :MDSEAR-DESC,                                         CL129
03853                :MDSEAR-BYR-NBR,                                      CL129
03854                :MDSEAR-LIST-SEQ-NBR                                  CL129
03855      END-EXEC.                                                       CL129
03856                                                                      CL129
03857      SET PS-DB2-OK TO TRUE                                           CL129
03858                                                                      CL129
03859      IF (SQLCODE = -904) OR                                          CL129
03860         (SQLCODE = -913)                                             CL129
03861          SET PS-DB2-RETRY TO TRUE                                    CL129
03862          ADD 1 TO PC-RETRY-CNTR                                      CL129
03863          IF PC-RETRY-CNTR > PC-MAX-RETRYS                            CL129
03864              SET PS-DB2-OK TO TRUE                                   CL129
03865          END-IF                                                      CL129
03866      END-IF.                                                         CL129
03867                                                                      CL129
03868      IF PS-DB2-OK                                                    CL129
03869          IF SQLCODE = +0                                             CL129
03870              CONTINUE                                                CL129
03871          ELSE                                                        CL129
03872              IF SQLCODE = +100                                       CL129
03873                  SET MB012-NO-ITEMS-LEFT-ON-DB TO TRUE               CL183
03874              ELSE                                                    CL129
03875                  SET DP013-DB2-ABEND   TO TRUE                       CL129
03876                  SET DP013-NO-ROLLBACK TO TRUE                       CL129
03877                  MOVE 'Z510-READ-DPT-CURSOR'   TO                    CL129
03878                       DP013-PARAGRAPH                                CL129
03879                  MOVE PC-OPERCO-NBR            TO                    CL129
03880                       AM-OPERCO-NBR-13                               CL129
03881                  MOVE MB012-SAVE-GMA           TO                    CL183
03882                       AM-GMA-NBR-13                                  CL129
03883                  MOVE MB012-SAVE-DMA           TO                    CL183
03884                       AM-DMA-NBR-13                                  CL129
03885                  MOVE MB012-SAVE-DPT           TO                    CL183
03886                       AM-DEPT-NBR-13                                 CL129
03887                  MOVE AM-ABEND-MESSAGE-13      TO                    CL129
03888                       DP013-MESSAGE-TEXT(1)                          CL129
03889                  MOVE SQLCA                    TO                    CL129
03890                       DP013-SQLCA                                    CL129
03891                  PERFORM DP013-0000-PROCESS-ABEND                    CL129
03892              END-IF                                                  CL129
03893          END-IF                                                      CL129
03894      END-IF.                                                         CL129
03895  EJECT                                                               CL129
03896  Z520-CLOSE-DPT-CURSOR.                                              CL129
03897                                                                      CL129
03898 ******************************************************************   CL129
03899 *    CLOSE THE DPT-CURSOR                                            CL148
03900 ******************************************************************   CL129
03901                                                                      CL129
03902      EXEC SQL                                                        CL129
03903          CLOSE DPT-CURSOR                                            CL129
03904      END-EXEC.                                                       CL129
03905                                                                      CL129
03906      IF (SQLCODE = +0)   OR                                          CL129
03907         (SQLCODE = +100)                                             CL129
03908          SET PS-DPT-CURSOR-NOT-OPENED TO TRUE                        CL129
03909      ELSE                                                            CL129
03910          SET DP013-DB2-ABEND            TO TRUE                      CL129
03911          SET DP013-NO-ROLLBACK          TO TRUE                      CL129
03912          MOVE 'Z520-CLOSE-DPT-CURSOR'   TO                           CL148
03913               DP013-PARAGRAPH                                        CL129
03914          MOVE AM-ABEND-MESSAGE-22       TO                           CL129
03915               DP013-MESSAGE-TEXT(1)                                  CL129
03916          MOVE SQLCA                     TO                           CL129
03917               DP013-SQLCA                                            CL129
03918          PERFORM DP013-0000-PROCESS-ABEND                            CL129
03919      END-IF.                                                         CL129
03920  EJECT                                                               CL129
03921 ******************************************************************   CL*42
03922 *    FILL IN SCREEN HEADER MODULE                                    CL*42
03923 ******************************************************************   CL*42
03924                                                                      CL*42
03925      COPY DPPD002.                                                   CL*42
03926  EJECT                                                               CL*42
03927 ******************************************************************MBKCS112
03928 *    RETURN TO CICS MODULE                                        MBKCS112
03929 ******************************************************************MBKCS112
03930                                                                   MBKCS112
03931      COPY DPPD003.                                                   CL**3
03932  EJECT                                                            MBKCS112
03933 ******************************************************************MBKCS112
03934 *    FORMAT MESSAGE LINES MODULE                                  MBKCS112
03935 ******************************************************************MBKCS112
03936                                                                   MBKCS112
03937      COPY DPPD005.                                                   CL194
03938  EJECT                                                            MBKCS112
03939 ******************************************************************MBKCS112
03940 *    GET PANEL MODULE                                             MBKCS112
03941 ******************************************************************MBKCS112
03942                                                                   MBKCS112
03943      COPY DPPD007.                                                   CL**3
03944  EJECT                                                            MBKCS112
03945 ******************************************************************MBKCS112
03946 *    WRITE PANEL MODULE                                           MBKCS112
03947 ******************************************************************MBKCS112
03948                                                                   MBKCS112
03949      COPY DPPD008.                                                   CL**3
03950  EJECT                                                            MBKCS112
03951 ******************************************************************MBKCS112
03952 *    TOGGLE THE FUNCTION KEY LINE MODULE                          MBKCS112
03953 ******************************************************************MBKCS112
03954                                                                   MBKCS112
03955      COPY DPPD009.                                                   CL**3
03956  EJECT                                                            MBKCS112
03957 ******************************************************************MBKCS112
03958 *    ABEND PROCESSOR MODULE                                       MBKCS112
03959 ******************************************************************MBKCS112
03960                                                                   MBKCS112
03961      COPY DPPD013.                                                   CL**3
03962  EJECT                                                            MBKCS112
03963 ******************************************************************MBKCS112
03964 *    CHECK COMMUNICATION AREA MODULE                              MBKCS112
03965 ******************************************************************MBKCS112
03966                                                                   MBKCS112
03967      COPY DPPD015.                                                   CL**3
03968  EJECT                                                            MBKCS112
03969 ******************************************************************MBKCS112
03970 *    UPDATE TIME AND DATE MODULE                                  MBKCS112
03971 ******************************************************************MBKCS112
03972                                                                   MBKCS112
03973      COPY DPPD017.                                                   CL**3
03974  EJECT                                                            MBKCS112
