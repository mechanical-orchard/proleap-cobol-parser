       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      XSKUT018.                                       00020000
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040000
       DATE-WRITTEN.    10/02/2002.                                     00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **XS DOMAIN TABLE INTEGRITY PROCESS - FOR TABLE XST_SUB_CL        00080000
      ******************************************************************00090000
      **THIS PROGRAM VALIDATES THE DATA IN THE XS DOMAIN IS THE SAME    00100000
      **AS THE DATA IN THE SOR - VSM.                                   00110000
      ******************************************************************00120000
      *DATE:10/02/2002   MADE BY:JULIE SMITH INITIAL PROGRAM WRITE      00130000
032803*DATE:03/28/2003   MADE BY:JULIE SMITH CORRECT MOVES IF ERROR     00140000
      ******************************************************************00150000
       ENVIRONMENT DIVISION.                                            00160000
       CONFIGURATION SECTION.                                           00170000
       SOURCE-COMPUTER.        IBM-3090.                                00180000
       OBJECT-COMPUTER.        IBM-3090.                                00190000
       INPUT-OUTPUT SECTION.                                            00200000
       FILE-CONTROL.                                                    00210000
                                                                        00220000
           SELECT CONTROL-CARD                                          00230000
                  ASSIGN TO INP00.                                      00240000
           SELECT VSM-EXTRACT                                           00250000
                  ASSIGN TO INP01.                                      00260000
           SELECT XS-EXTRACT                                            00270000
                  ASSIGN TO INP02.                                      00280000
           SELECT DIFFERENCES-FILE                                      00290000
                  ASSIGN TO OUT01.                                      00300000
                                                                        00310000
       DATA DIVISION.                                                   00320000
       FILE SECTION.                                                    00330000
                                                                        00340000
       FD  CONTROL-CARD                                                 00350000
           RECORDING MODE F                                             00360000
           BLOCK CONTAINS 0 RECORDS                                     00370000
           LABEL RECORDS ARE OMITTED                                    00380000
           DATA RECORD IS CONTROL-CARD-RECORD.                          00390000
       01  CONTROL-CARD-RECORD             PIC X(080).                  00400000
                                                                        00410000
       FD  VSM-EXTRACT                                                  00420000
           RECORDING MODE F                                             00430000
           BLOCK CONTAINS 0 RECORDS                                     00440000
           LABEL RECORDS ARE OMITTED                                    00450000
           DATA RECORD IS VSM-RECORD.                                   00460000
       01  VSM-RECORD                      PIC X(073).                  00470000
                                                                        00480000
       FD  XS-EXTRACT                                                   00490000
           RECORDING MODE F                                             00500000
           BLOCK CONTAINS 0 RECORDS                                     00510000
           LABEL RECORDS ARE OMITTED                                    00520000
           DATA RECORD IS XS-RECORD.                                    00530000
       01  XS-RECORD                       PIC X(061).                  00540000
                                                                        00550000
       FD  DIFFERENCES-FILE                                             00560000
           RECORDING MODE F                                             00570000
           BLOCK CONTAINS 0 RECORDS                                     00580000
           LABEL RECORDS ARE OMITTED                                    00590000
           DATA RECORD IS DIFFERENCES-RECORD.                           00600000
       01  DIFFERENCES-RECORD              PIC X(200).                  00610000
                                                                        00620000
       WORKING-STORAGE SECTION.                                         00630000
      ******************************************************************00640000
      **SWITCHES                                                        00650000
      ******************************************************************00660000
       01  PS-PROGRAM-SWITCHES.                                         00670000
           05  PS-END-OF-VSM-EXTRACT-SW        PIC X(01) VALUE 'N'.     00680000
               88  PS-END-OF-VSM-EXTRACT                 VALUE 'Y'.     00690000
               88  PS-NOT-END-OF-VSM-EXTRACT             VALUE 'N'.     00700000
           05  PS-END-OF-XS-EXTRACT-SW         PIC X(01) VALUE 'N'.     00710000
               88  PS-END-OF-XS-EXTRACT                  VALUE 'Y'.     00720000
               88  PS-NOT-END-OF-XS-EXTRACT              VALUE 'N'.     00730000
           05  PS-ERROR-FOUND-SW               PIC X(01) VALUE 'N'.     00740000
               88  PS-ERROR-FOUND                        VALUE 'Y'.     00750000
               88  PS-ERROR-NOT-FOUND                    VALUE 'N'.     00760000
      ******************************************************************00770000
      **CONTROL CARD LAYOUT                                             00780000
      ******************************************************************00790000
       01  CONTROL-CARD-LAYOUT.                                         00800000
           05  CC-START-POINT                  PIC 9(05) VALUE ZEROS.   00810000
           05  FILLER                          PIC X(01) VALUE SPACES.  00820000
           05  CC-READ-FREQUENCY               PIC 9(05) VALUE ZEROS.   00830000
           05  FILLER                          PIC X(69) VALUE SPACES.  00840000
      ******************************************************************00850000
      **TIMESTAMP VARIABLES                                             00860000
      ******************************************************************00870000
       01  SD-TIMESTAMP                        PIC X(26) VALUE SPACES.  00880000
       01  FILLER REDEFINES SD-TIMESTAMP.                               00890000
           05  SD-CURR-DTE                     PIC X(10).               00900000
           05  FILLER                          PIC X(01).               00910000
           05  SD-HOUR                         PIC X(02).               00920000
           05  FILLER                          PIC X(01).               00930000
           05  SD-MINUTE                       PIC X(02).               00940000
           05  FILLER                          PIC X(01).               00950000
           05  SD-SECOND                       PIC X(02).               00960000
           05  FILLER                          PIC X(01).               00970000
           05  SD-MICROSEC                     PIC X(06).               00980000
      ******************************************************************00990000
      **PROGRAM VARIABLES                                               01000000
      ******************************************************************01010000
       01  PC-PROGRAM-VARIABLES.                                        01020000
           05  PV-BUS-GP-NBR                   PIC 9(03) VALUE ZEROS.   01030000
      ******************************************************************01040000
      **PROGRAM CONSTANTS                                               01050000
      ******************************************************************01060000
       01  PC-PROGRAM-CONSTANTS.                                        01070000
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)                01080000
                                                       VALUE 'XSKUT018'.01090000
      ******************************************************************01100000
      **PROGRAM COUNTERS                                                01110000
      ******************************************************************01120000
       01  PC-PROGRAM-COUNTER.                                          01130000
           05  PC-RECORD-COUNT                 PIC S9(09) VALUE ZEROS   01140000
                                                          COMP-3.       01150000
           05  PC-VSM-EXTRACT-RECS-READ        PIC 9(09) VALUE ZEROS.   01160000
           05  PC-XS-EXTRACT-RECS-READ         PIC 9(09) VALUE ZEROS.   01170000
           05  PC-ERROR-RECORD-WRITTEN         PIC 9(09) VALUE ZEROS.   01180000
      ******************************************************************01190000
      **ERROR HANDLING                                                  01200000
      ******************************************************************01210000
       01  PV-MISC-ABEND-FIELDS.                                        01220000
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.      01230000
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.      01240000
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.      01250000
           05  PV-DB2-OPERATION             PIC X(30) VALUE SPACE.      01260000
       01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 01270000
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           01280000
      ******************************************************************01290000
      **COPYBOOKS                                                       01300000
      ******************************************************************01310000
      ***VSM EXTRACT FILE LAYOUT                                        01320000
           COPY HMRD010.                                                01330000
      ***XS EXTRACT FILE LAYOUT                                         01340000
           COPY XSRD007.                                                01350000
      ***XS ERROR FILE LAYOUT                                           01360000
           COPY XSRD008.                                                01370000
      ***VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        01380000
           COPY DPWS004.                                                01390000
      ***DB2 ABEND COPYBOOK                                             01400000
           COPY DPWS900.                                                01410000
      ***DB2 MESSAGE AREA                                               01420000
           COPY SQLCA2.                                                 01430000
      ******************************************************************01440000
      **TABLE INCLUSIONS                                                01450000
      ******************************************************************01460000
      ***DB2 COMMUNICATIONS AREA                                        01470000
           EXEC SQL                                                     01480000
                INCLUDE SQLCA                                           01490000
           END-EXEC.                                                    01500000
      ***TABLE XST_SUB_CL                                               01510000
           EXEC SQL                                                     01520000
                INCLUDE XST00014                                        01530000
           END-EXEC.                                                    01540000
                                                                        01550000
       PROCEDURE DIVISION.                                              01560000
      ***************************************************************** 01570000
       000-MAINLINE.                                                    01580000
                                                                        01590000
           PERFORM 1000-INITIALIZATION.                                 01600000
           PERFORM 2000-PROCESS-RECORDS                                 01610000
                   UNTIL PS-END-OF-VSM-EXTRACT                          01620000
                      OR PS-END-OF-XS-EXTRACT.                          01630000
           PERFORM 3000-PROCESS-EXTRA-RECORDS                           01640000
                   UNTIL PS-END-OF-VSM-EXTRACT                          01650000
                     AND PS-END-OF-XS-EXTRACT.                          01660000
           PERFORM 9999-WRAPUP.                                         01670000
           GOBACK.                                                      01680000
                                                                        01690000
       EJECT.                                                           01700000
      ******************************************************************01710000
      **INITIALIZATION AREA                                             01720000
      ******************************************************************01730000
       1000-INITIALIZATION.                                             01740000
                                                                        01750000
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01760000
                                                                        01770000
           INITIALIZE PC-PROGRAM-COUNTER.                               01780000
                                                                        01790000
           OPEN INPUT  CONTROL-CARD                                     01800000
                       VSM-EXTRACT                                      01810000
                       XS-EXTRACT                                       01820000
                OUTPUT DIFFERENCES-FILE.                                01830000
                                                                        01840000
           PERFORM 1100-SELECT-DB2-TIME.                                01850000
           PERFORM 1200-SELECT-RECORD-COUNT.                            01860000
                                                                        01870000
           PERFORM 9999-READ-CONTROL-CARD.                              01880000
           PERFORM 9999-READ-VSM-EXTRACT CC-START-POINT TIMES.          01890000
           PERFORM 9999-READ-XS-EXTRACT  CC-START-POINT TIMES.          01900000
                                                                        01910000
       EJECT.                                                           01920000
      ******************************************************************01930000
      **RETRIEVE TIME FROM DB2                                          01940000
      ******************************************************************01950000
       1100-SELECT-DB2-TIME.                                            01960000
                                                                        01970000
           MOVE '1100-SELECT-DB2-TIME' TO PV-PARAGRAPH-NAME.            01980000
                                                                        01990000
           EXEC SQL                                                     02000000
                SELECT CURRENT TIMESTAMP                                02010000
                INTO  :SD-TIMESTAMP                                     02020000
                FROM   SYSIBM.SYSDUMMY1                                 02030000
           END-EXEC.                                                    02040000
                                                                        02050000
           EVALUATE TRUE                                                02060000
              WHEN SQLCODE = ZERO                                       02070000
                   CONTINUE                                             02080000
              WHEN OTHER                                                02090000
               PERFORM 9999-ABEND-ROUTINE                               02100000
           END-EVALUATE.                                                02110000
                                                                        02120000
       EJECT.                                                           02130000
      ******************************************************************02140000
      **RETRIEVE TIME FROM DB2                                          02150000
      ******************************************************************02160000
       1200-SELECT-RECORD-COUNT.                                        02170000
                                                                        02180000
           MOVE '1200-SELECT-RECORD-COUNT' TO PV-PARAGRAPH-NAME.        02190000
                                                                        02200000
           EXEC SQL                                                     02210000
                SELECT COUNT(*)                                         02220000
                INTO  :PC-RECORD-COUNT                                  02230000
                FROM   XST_SUB_CL                                       02240000
           END-EXEC.                                                    02250000
                                                                        02260000
           EVALUATE TRUE                                                02270000
              WHEN SQLCODE = ZERO                                       02280000
                   CONTINUE                                             02290000
              WHEN OTHER                                                02300000
                   PERFORM 9999-ABEND-ROUTINE                           02310000
           END-EVALUATE.                                                02320000
                                                                        02330000
       EJECT.                                                           02340000
      ******************************************************************02350000
      **PROCESSES INPUT FILE                                            02360000
      ******************************************************************02370000
       2000-PROCESS-RECORDS.                                            02380000
                                                                        02390000
           MOVE '2000-PROCESS-RECORDS' TO PV-PARAGRAPH-NAME.            02400000
                                                                        02410000
           INITIALIZE XSRD008-SUBCLASS-ERR-RECORD.                      02420000
                                                                        02430000
           EVALUATE TRUE                                                02440000
              WHEN HM010-DEPT-NBR = XSRD007-DEPT-NBR                    02450000
                   PERFORM 2100-COMPARE-MAJ-CLASS                       02460000
              WHEN HM010-DEPT-NBR > XSRD007-DEPT-NBR                    02470000
                   PERFORM 9999-MOVE-XS-VALUES                          02480000
                   PERFORM 9999-WRITE-ERROR-RECORD                      02490000
                   PERFORM 9999-READ-XS-EXTRACT                         02500000
              WHEN HM010-DEPT-NBR < XSRD007-DEPT-NBR                    02510000
                   PERFORM 9999-MOVE-VSM-VALUES                         02520000
                   PERFORM 9999-WRITE-ERROR-RECORD                      02530000
                   PERFORM 9999-READ-VSM-EXTRACT                        02540000
           END-EVALUATE.                                                02550000
                                                                        02560000
       EJECT.                                                           02570000
      ******************************************************************02580000
      **COMPARES RECORDS MAJOR CLASS FIELDS                             02590000
      ******************************************************************02600000
       2100-COMPARE-MAJ-CLASS.                                          02610000
                                                                        02620000
           MOVE '2100-COMPARE-MAJ-CLASS' TO PV-PARAGRAPH-NAME.          02630000
                                                                        02640000
           EVALUATE TRUE                                                02650000
              WHEN HM010-MAJ-CL-NBR = XSRD007-MAJ-CL-NBR                02660000
                   PERFORM 2200-COMPARE-SUB-CLASS                       02670000
              WHEN HM010-MAJ-CL-NBR > XSRD007-MAJ-CL-NBR                02680000
                   PERFORM 9999-MOVE-XS-VALUES                          02690000
                   PERFORM 9999-WRITE-ERROR-RECORD                      02700000
                   PERFORM 9999-READ-XS-EXTRACT                         02710000
              WHEN HM010-MAJ-CL-NBR < XSRD007-MAJ-CL-NBR                02720000
                   PERFORM 9999-MOVE-VSM-VALUES                         02730000
                   PERFORM 9999-WRITE-ERROR-RECORD                      02740000
                   PERFORM 9999-READ-VSM-EXTRACT                        02750000
           END-EVALUATE.                                                02760000
                                                                        02770000
       EJECT.                                                           02780000
      ******************************************************************02790000
      **COMPARES RECORDS MAJOR CLASS FIELDS                             02800000
      ******************************************************************02810000
       2200-COMPARE-SUB-CLASS.                                          02820000
                                                                        02830000
           MOVE '2200-COMPARE-SUB-CLASS' TO PV-PARAGRAPH-NAME.          02840000
                                                                        02850000
           EVALUATE TRUE                                                02860000
              WHEN HM010-SUB-CL-NBR = XSRD007-SUB-CL-NBR                02870000
                   PERFORM 2300-COMPARE-FIELDS                          02880000
                   PERFORM 9999-READ-XS-EXTRACT  CC-READ-FREQUENCY TIMES02890000
                   PERFORM 9999-READ-VSM-EXTRACT CC-READ-FREQUENCY TIMES02900000
              WHEN HM010-SUB-CL-NBR > XSRD007-SUB-CL-NBR                02910000
                   PERFORM 9999-MOVE-XS-VALUES                          02920000
                   PERFORM 9999-WRITE-ERROR-RECORD                      02930000
                   PERFORM 9999-READ-XS-EXTRACT                         02940000
              WHEN HM010-SUB-CL-NBR < XSRD007-SUB-CL-NBR                02950000
                   PERFORM 9999-MOVE-VSM-VALUES                         02960000
                   PERFORM 9999-WRITE-ERROR-RECORD                      02970000
                   PERFORM 9999-READ-VSM-EXTRACT                        02980000
           END-EVALUATE.                                                02990000
                                                                        03000000
       EJECT.                                                           03010000
      ******************************************************************03020000
      **COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   03030000
      ******************************************************************03040000
       2300-COMPARE-FIELDS.                                             03050000
                                                                        03060000
           MOVE '2300-COMPARE-FIELDS' TO PV-PARAGRAPH-NAME.             03070000
                                                                        03080000
           INITIALIZE PS-ERROR-FOUND-SW.                                03090000
                                                                        03100000
           IF  XSRD007-SUB-CL-NM    NOT EQUAL HM010-SUB-CL-NM           03110000
               SET PS-ERROR-FOUND     TO TRUE                           03120000
           END-IF.                                                      03130000
                                                                        03140000
           IF  XSRD007-POS-MDSE-DESC NOT EQUAL HM010-POS-MDSE-DESC      03150000
               SET PS-ERROR-FOUND     TO TRUE                           03160000
           END-IF.                                                      03170000
                                                                        03180000
           IF  XSRD007-TAX-DFLT-IND NOT EQUAL HM010-TAX-DFLT-IND        03190000
               SET PS-ERROR-FOUND     TO TRUE                           03200000
           END-IF.                                                      03210000
                                                                        03220000
           IF  XSRD007-DISC-ELIG-IND NOT EQUAL HM010-DISC-ELIG-IND      03230000
               SET PS-ERROR-FOUND     TO TRUE                           03240000
           END-IF.                                                      03250000
                                                                        03260000
           IF  PS-ERROR-FOUND                                           03270000
               PERFORM 9999-MOVE-XS-VALUES                              03280000
               PERFORM 9999-MOVE-VSM-VALUES                             03290000
               PERFORM 9999-WRITE-ERROR-RECORD                          03300000
           END-IF.                                                      03310000
                                                                        03320000
       EJECT.                                                           03330000
      ******************************************************************03340000
      **COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   03350000
      ******************************************************************03360000
       3000-PROCESS-EXTRA-RECORDS.                                      03370000
                                                                        03380000
           MOVE '3000-PROCESS-EXTRA-RECORDS' TO PV-PARAGRAPH-NAME.      03390000
                                                                        03400000
           INITIALIZE DIFFERENCES-RECORD.                               03410000
                                                                        03420000
           IF  PS-NOT-END-OF-VSM-EXTRACT                                03430000
               PERFORM 9999-MOVE-VSM-VALUES                             03440000
               PERFORM 9999-WRITE-ERROR-RECORD                          03450000
               PERFORM 9999-READ-VSM-EXTRACT                            03460000
           END-IF.                                                      03470000
                                                                        03480000
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03490000
               PERFORM 9999-MOVE-XS-VALUES                              03500000
               PERFORM 9999-WRITE-ERROR-RECORD                          03510000
               PERFORM 9999-READ-XS-EXTRACT                             03520000
           END-IF.                                                      03530000
                                                                        03540000
       EJECT.                                                           03550000
      ***************************************************************** 03560000
      **READ CONTROL CARD FOR FREQUENCY OF COMPARE                      03570000
      ***************************************************************** 03580000
       9999-READ-CONTROL-CARD.                                          03590000
                                                                        03600000
           READ CONTROL-CARD INTO CONTROL-CARD-LAYOUT.                  03610000
                                                                        03620000
       EJECT.                                                           03630000
      ***************************************************************** 03640000
      **READ VSM EXTRACT FILE                                           03650000
      ***************************************************************** 03660000
       9999-READ-VSM-EXTRACT.                                           03670000
                                                                        03680000
           IF  PS-NOT-END-OF-VSM-EXTRACT                                03690000
               READ VSM-EXTRACT INTO HMRD010-RECORD                     03700000
                    AT END                                              03710000
                       SET PS-END-OF-VSM-EXTRACT TO TRUE                03720000
               END-READ                                                 03730000
           END-IF.                                                      03740000
                                                                        03750000
           IF  PS-NOT-END-OF-VSM-EXTRACT                                03760000
               ADD +1                       TO PC-VSM-EXTRACT-RECS-READ 03770000
           END-IF.                                                      03780000
                                                                        03790000
       EJECT.                                                           03800000
      ***************************************************************** 03810000
      **READ XS EXTRACT FILE                                            03820000
      ***************************************************************** 03830000
       9999-READ-XS-EXTRACT.                                            03840000
                                                                        03850000
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03860000
               READ XS-EXTRACT INTO XSRD007-SUBCLASS-UNLOAD-FILE        03870000
                    AT END                                              03880000
                       SET PS-END-OF-XS-EXTRACT TO TRUE                 03890000
               END-READ                                                 03900000
           END-IF.                                                      03910000
                                                                        03920000
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03930000
               ADD +1                       TO PC-XS-EXTRACT-RECS-READ  03940000
           END-IF.                                                      03950000
                                                                        03960000
       EJECT.                                                           03970000
      ***************************************************************** 03980000
      **MOVES XS EXTRACT VALUES TO ERROR RECORD                         03990000
      ***************************************************************** 04000000
       9999-MOVE-XS-VALUES.                                             04010000
                                                                        04020000
           MOVE XSRD007-DEPT-NBR      TO XSRD006-XS-DEPT-NBR.           04030000
           MOVE XSRD007-MAJ-CL-NBR    TO XSRD006-XS-MAJ-CL-NBR.         04040000
           MOVE XSRD007-SUB-CL-NBR    TO XSRD006-XS-SUB-CL-NBR.         04050000
           MOVE XSRD007-SUB-CL-NM     TO XSRD006-XS-SUB-CL-NME.         04060000
           MOVE XSRD007-POS-MDSE-DESC TO XSRD006-XS-POS-MDSE-DESC.      04070000
           MOVE XSRD007-TAX-DFLT-IND  TO XSRD006-XS-TAX-DFLT-IND.       04080000
           MOVE XSRD007-DISC-ELIG-IND TO XSRD006-XS-DISC-ELIG-IND.      04090000
                                                                        04100000
       EJECT.                                                           04110000
      ***************************************************************** 04120000
      **MOVES VSM EXTRACT VALUES TO ERROR RECORD                        04130000
      ***************************************************************** 04140000
       9999-MOVE-VSM-VALUES.                                            04150000
                                                                        04160000
032803     MOVE HM010-DEPT-NBR        TO XSRD006-VSM-DEPT-NBR.          04170000
032803     MOVE HM010-MAJ-CL-NBR      TO XSRD006-VSM-MAJ-CL-NBR.        04180000
032803     MOVE HM010-SUB-CL-NBR      TO XSRD006-VSM-SUB-CL-NBR.        04190000
032803     MOVE HM010-SUB-CL-NM       TO XSRD006-VSM-SUB-CL-NME.        04200000
032803     MOVE HM010-POS-MDSE-DESC   TO XSRD006-VSM-POS-MDSE-DESC.     04210000
032803     MOVE HM010-TAX-DFLT-IND    TO XSRD006-VSM-TAX-DFLT-IND.      04220000
032803     MOVE HM010-DISC-ELIG-IND   TO XSRD006-VSM-DISC-ELIG-IND.     04230000
                                                                        04240000
       EJECT.                                                           04250000
      ***************************************************************** 04260000
      **ERROR FILE                                                      04270000
      ***************************************************************** 04280000
       9999-WRITE-ERROR-RECORD.                                         04290000
                                                                        04300000
           WRITE DIFFERENCES-RECORD FROM XSRD008-SUBCLASS-ERR-RECORD.   04310000
                                                                        04320000
           ADD +1                       TO PC-ERROR-RECORD-WRITTEN.     04330000
                                                                        04340000
       EJECT.                                                           04350000
      ****************************************************************  04360000
      *END OF PROGRAM ROUTINE                                           04370000
      ****************************************************************  04380000
       9999-WRAPUP.                                                     04390000
                                                                        04400000
           CLOSE CONTROL-CARD                                           04410000
                 VSM-EXTRACT                                            04420000
                 XS-EXTRACT                                             04430000
                 DIFFERENCES-FILE.                                      04440000
                                                                        04450000
           PERFORM 9999-DISPLAY-STATISTICS.                             04460000
                                                                        04470000
           IF  PC-ERROR-RECORD-WRITTEN > 0                              04480000
               MOVE 04               TO RETURN-CODE                     04490000
           END-IF.                                                      04500000
                                                                        04510000
       EJECT.                                                           04520000
      ****************************************************************  04530000
      *DISPLAY PROGRAM STATISTICS                                       04540000
      ****************************************************************  04550000
       9999-DISPLAY-STATISTICS.                                         04560000
                                                                        04570000
           DISPLAY '*************************************************'. 04580000
           DISPLAY '* PROGRAM..............: ' PC-CURRENT-PROGRAM-NAME. 04590000
           DISPLAY '* DATE/TIMESTAMP.......: ' SD-TIMESTAMP.            04600000
           DISPLAY '* RECORDS IN TABLE.....: ' PC-RECORD-COUNT.         04610000
           DISPLAY '* VSM EXTRACT RECORDS..: ' PC-VSM-EXTRACT-RECS-READ.04620000
           DISPLAY '* XS EXTRACT RECORDS...: ' PC-XS-EXTRACT-RECS-READ. 04630000
           DISPLAY '* ERROR RECORDS WRITTEN: ' PC-ERROR-RECORD-WRITTEN. 04640000
           DISPLAY '*************************************************'. 04650000
                                                                        04660000
       EJECT.                                                           04670000
      ******************************************************************04680000
      **SQL ABEND ROUTINE                                               04690000
      ******************************************************************04700000
       9999-ABEND-ROUTINE.                                              04710000
                                                                        04720000
           DISPLAY '** ABEND     **'.                                   04730000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        04740000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              04750000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               04760000
      ******************************************************************04770000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     04780000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      04790000
      ******************************************************************04800000
           COPY DPPD004.                                                04810000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            04820000
           CALL 'ILBOABN0' USING ABEND-CODE.                            04830000
                                                                        04840000
       EJECT                                                            04850000
