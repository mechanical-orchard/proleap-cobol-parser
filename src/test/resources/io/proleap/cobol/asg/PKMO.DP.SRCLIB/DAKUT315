       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.           DAKUT315.                                          
       AUTHOR.               RICH KELLEN.                                       
       INSTALLATION.         KOHLS DEPARTMENT STORES.                           
       DATE-WRITTEN          06/15/2006.                                        
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *   DAKUT315 - DEBIT ALLOWANCE DEBIT EXTRACTS                    *        
      *                                                                *        
      *   THIS PROGRAM READS AN INPUT FILE FROM DAKUT314 AND EXTRACTS  *        
      *   DEBIT ALLOWANCE DATA BY ENTITY ID/DEPT WHERE THE TYP_CD =    *        
      *   ('AC','MA','MC','PC','PR') FROM THE TDAALW ORACLE TABLE.     *        
      *   AN OUTPUT RECORD FOR EACH OF THESE IS CREATED AS INPUT TO    *        
      *   DAKRP114 TO CREATE THE MONTHLY PROFIT ASSISTANCE / CO-OP     *        
      *   STATEMENT.                                                   *        
      *   THE ORACLE TABLES USED IN THIS PROGRAM ARE:                  *        
      *   TDAALW - DEBIT ALLOWANCE TABLE                               *        
      *   TDACDE - DEBIT ALLOWANCE TYPE CODE DESCRIPTION               *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * WR30812  07/11/2006  INITIAL IMPLEMENTATION                    *        
      * WR30812  09/27/2006  CHG CALC OF DA110-TTL-AVL-COST-AMT-TOT =  *        
      *                      DA109-TTL-AVL-COST-AMT-TOT - DA VALUE FOR *        
      *                      PREVIOUS PERIODS BASED ON NEW DATE PARMS  *        
      *                      ON INPUT FILE. CALC AND CREATE NEW FIELD  *        
      *                      ON OUTPUT FILE WHICH CONTAINS THE TOTAL   *        
      *                      DA VALUE FOR THE CURRENT PERIOD SO        *        
      *                      DAKRP114 CAN COMPUTE STARTING AND ENDING  *        
      *                      BALANCE CORRECLY.                         *        
      * 8613F    07/17/2015  RMS 9 REMEDIATION. CHANGED FROM           *        
      *                      AUTO LOGON TO EXTERNAL SIGN ON                     
208057* CHG0208057 9/7/2018  JIM HUNTER MAINFRAME REFACTORING -        *        
208057*                                 REMOVE MLWS502, MLLS502 AND    *        
208057*                                 MLPD502 COPYBOOKS WHICH GET THE*        
208057*                                 JOB NAME SINCE THE JOB NAME IS *        
208057*                                 NOT USED.                      *        
      ******************************************************************        
      ******************************************************************        
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT CONTROL-CARD-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT DEBIT-ALLOWANCE-FILE                                          
               ASSIGN TO INP02.                                                 
                                                                                
8613F      SELECT ORACLE-LOGIN-FILE                                             
8613F          ASSIGN TO ORALOGON.                                              
                                                                                
           SELECT DETAIL-DEBIT-ALLWNCE-FILE                                     
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
       FD  CONTROL-CARD-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CONTROL-CARD-RECORD             PIC X(80).                           
                                                                                
       FD  DEBIT-ALLOWANCE-FILE                                                 
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DEBIT-ALLOWANCE-RECORD          PIC X(188).                          
                                                                                
8613F  FD  ORACLE-LOGIN-FILE                                                    
8613F      RECORDING MODE IS F                                                  
8613F      LABEL RECORDS ARE STANDARD                                           
8613F      BLOCK CONTAINS 0 RECORDS                                             
8613F      DATA RECORD IS ORACLE-LOGIN-REC.                                     
8613F  01  ORACLE-LOGIN-REC.                                                    
8613F      05  FILLER                  PIC X(80).                               
                                                                                
       FD  DETAIL-DEBIT-ALLWNCE-FILE                                            
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DETAIL-DEBIT-ALLWNCE-RECORD     PIC X(275).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
8613F  01  CC-ORACLE-CONNECTION.                                                
8613F      05  CC-ORACLE-USERID           PIC X(40).                            
8613F          88 CC-AUTO-LOGON                           VALUE '/'.            
8613F      05  CC-ORACLE-PASSWORD         PIC X(40).                            
8613F *                                                                         
      ****************************************************************          
      * RECORD LAYOUT FOR CONTROL CARD FILE.                                    
      ****************************************************************          
       01  CC-CONTROL-CARD.                                                     
           05  FILLER                      PIC X(01)       VALUE SPACES.        
               88  CC-COMMENT                              VALUE '*'.           
               88  CC-NOT-COMMENT                          VALUE SPACES.        
           05  CC-PROCESS-DTE              PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(08)       VALUE SPACES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC 9(02)       VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(08)       VALUE SPACES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC 9(02)       VALUE ZEROES.        
           05  FILLER                      PIC X(40)       VALUE SPACES.        
                                                                                
      *****************************************************************         
      *  KOHLS CALENDAR ROUTINE COPYBOOK                              *         
      *****************************************************************         
      *01  DP500-KOHLS-CALENDAR-ROUTINE.                                        
      *01  DPG51.                                                               
      *01  DPG52.                                                               
      *01  DPG53.                                                               
      *01  DPG54.                                                               
      *01  DPG55.                                                               
      *01  DPG56.                                                               
           COPY DPWS500.                                                        
                                                                                
      ******************************************************************        
      *  PARAMETER LIST FOR THE DYNAMIC ALLOCATION ROUTINE                      
      ******************************************************************        
      *01  DP023-DYNALC.                                                        
      *01  DP023-DYNALC-PARMS.                                                  
      *01  DP023-DYNALC-PD-PARMS.                                               
      *01  DP023-ABEND-CODE                                                     
      *01  DP023-SUB                                                            
      *01  DP023-MAX                                                            
      *01  DP023-RETURN-CODES.                                                  
      *01  DP023-RETURN-CODE-CONSTANTS.                                         
           COPY DPWS023.                                                        
                                                                                
      *****************************************************************         
      *  PROGRAM'S ACCUMULATORS                                       *         
      *****************************************************************         
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-DA-RECORDS-READ          PIC 9(09)       VALUE ZEROES.        
           05  PA-DA-RECORD-WRITTEN        PIC 9(09)       VALUE ZEROES.        
                                                                                
      *****************************************************************         
      *  PROGRAM'S CONSTANTS                                          *         
      *****************************************************************         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME             PIC X(08)       VALUE                
               'DAKUT315'.                                                      
           05  PC-ORACLE-PROD-ID           PIC X(1)        VALUE '/'.           
           05  PC-ORACLE-PROD-ID-LGTH      PIC 9(02)       VALUE 1.             
           05  PC-PROCESS-STAT-CDE         PIC X(02)       VALUE '60'.          
8613F      05  PC-AUTO-LOGON               PIC X(01)       VALUE '/'.           
                                                                                
      *****************************************************************         
      *  PROGRAM'S SWITCHES                                           *         
      *****************************************************************         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-TDAALW-SW         PIC X(01)       VALUE 'N'.           
               88  PS-END-OF-TDAALW                        VALUE 'Y'.           
               88  PS-NOT-END-OF-TDAALW                    VALUE 'N'.           
           05  PS-END-OF-DA-FILE-SW        PIC X(01)       VALUE 'N'.           
               88  PS-END-OF-DA-FILE                       VALUE 'Y'.           
               88  PS-NOT-END-OF-DA-FILE                   VALUE 'N'.           
           05  PS-END-OF-CONTROL-CARDS-SW  PIC X(01)       VALUE 'N'.           
               88  PS-END-OF-CONTROL-CARDS                 VALUE 'Y'.           
               88  PS-NOT-END-OF-CONTROL-CARDS             VALUE 'N'.           
8613F      05  PS-ORACLE-LOGIN-EOF-SW      PIC  X          VALUE 'N'.           
8613F          88  ORACLE-LOGIN-EOF                       VALUE 'Y'.            
                                                                                
      *****************************************************************         
      *  PROGRAM'S VARIABLES                                          *         
      *****************************************************************         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-JOBNAME                  PIC X(08)       VALUE SPACES.        
           05  FILLER REDEFINES PV-JOBNAME.                                     
               10  PV-JOBNAME-APPL         PIC X(02).                           
                   88  PV-PROD-JOBS                        VALUES ARE           
                       'DA' 'IN' 'MJ' 'ML'.                                     
               10  FILLER                  PIC X(06).                           
           05  PV-PARAGRAPH-NAME           PIC X(40)       VALUE SPACES.        
           05  PV-NULL-RETURN              PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
               88 PV-NULL                  VALUE -1.                            
           05  PV-BEG-FSCL-DATE            PIC X(10)       VALUE SPACES.        
           05  PV-END-FSCL-DATE            PIC X(10)       VALUE SPACES.        
           05  PV-OFF-INVC-TOT             PIC 9(11)V99    VALUE ZEROES.        
8613F      05  PV-ORACLE-USERID-LEN        PIC S9(04)      VALUE ZEROS.         
8613F      05  PV-ORACLE-PASSWORD-LEN      PIC S9(04)      VALUE ZEROS.         
       01  PV-ABEND-CODE                   PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  ABEND-4001-INV-WRITE-KEY                    VALUE +4001.         
           88  ABEND-4002-INV-READ-KEY                     VALUE +4002.         
           88  ABEND-4003-CNTL-CARD-ERRORS                 VALUE +4003.         
           88  ABEND-4004-INTERNAL-ERROR                   VALUE +4004.         
           88  ABEND-4005-OPEN-FILE-ERROR                  VALUE +4005.         
           88  ABEND-4006-CLOSE-FILE-ERROR                 VALUE +4006.         
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
           88  ABEND-4009-INVALID-DATA                     VALUE +4009.         
           88  ABEND-4012-BAD-INTR-SORT                    VALUE +4012.         
           88  ABEND-4013-DB-ERROR                         VALUE +4013.         
                                                                                
       01  WS-MSG-TEXT                     PIC X(512)      VALUE SPACES.        
       01  WS-MAX-SIZE                     PIC S9(09)      VALUE +512           
                               COMP.                                            
       01  WS-MSG-LENGTH                   PIC S9(09)      VALUE ZEROES         
                               COMP.                                            
                                                                                
                                                                                
      *****************************************************************         
      * DEBIT ALLOWANCE OFF INVOICE EXTRACT LAYOUT                              
      *****************************************************************         
           COPY DARD109.                                                        
      *****************************************************************         
      *****************************************************************         
      * DEBIT ALLOWANCE DETAIL LAYOUT                                           
      *****************************************************************         
           COPY DARD110.                                                        
      *****************************************************************         
      ******************************************************************        
      *    ORACLE DECLARE SECTION                                               
      ******************************************************************        
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
       01  USERNAME                        PIC X(10)      VARYING.              
       01  PASSWD                          PIC X(10)      VARYING.              
                                                                                
      ******************************************************************        
      * COBOL DECLARATION FOR DEBIT ALLOWANCE TRANSACTIONS TABLE                
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TDAALW                                                   
           END-EXEC.                                                            
           10  DAALW-RMNG-AVL-COST-AMT     PIC S9(11)V9(2) COMP-3.              
           10  DAALW-EMAIL-ADDR            PIC X(250).                          
           10  DAALW-EMAIL-ADDR-ORGN-CDE   PIC X(02).                           
           10  DAALW-EMAIL-ADDR-COM-STAT-CDE PIC X(01).                         
                                                                                
      ******************************************************************        
      * COBOL DECLARATION FOR DEBIT ALLOWANCE AVAIL VENDOR FUNDING              
      ******************************************************************        
      *    EXEC SQL                                                             
      *        INCLUDE DAT00002                                                 
      *    END-EXEC.                                                            
      ******************************************************************        
      ******************************************************************        
      * COBOL DECLARATION FOR DEBIT ALLOWANCE CODE DESCRIPTION TABLE            
      ******************************************************************        
      *    EXEC SQL                                                             
      *        INCLUDE TDACDE                                                   
      *    END-EXEC.                                                            
      ******************************************************************        
      * COBOL DECLARATION FOR TABLE TDACDE                             *        
      ******************************************************************        
       01  DCLTDACDE.                                                           
           10  DACDE-CDE-TYP-DESC          PIC X(26).                           
           10  DACDE-CDE-VAL-CDE           PIC X(2).                            
           10  DACDE-CDE-DESC              PIC X(30).                           
           10  DACDE-ACTV-IND              PIC X(1).                            
      ******************************************************************        
      * DEBIT ALLOWANCE CURSOR                                                  
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               DECLARE TDAALW_INFO CURSOR FOR                                   
                   SELECT                                                       
                         DR_ALW_NBR                                             
                        ,TYP_CDE                                                
                        ,DEPT_NBR                                               
                        ,ENT_ID                                                 
                        ,TO_CHAR(CPLT_DTE, 'YYYY-MM-DD-HH24.MI.SS')             
                        ,NET_CST_AMT                                            
                   FROM                                                         
                         TDAALW                                                 
                   WHERE DEPT_NBR = :DAALW-DEPT-NBR                             
                     AND ENT_ID   = :DAALW-ENT-ID                               
                     AND TYP_CDE IN ('AC','MA','MC','PC','PR')                  
                     AND CPLT_DTE IS NOT NULL                                   
                     AND FSCL_MN_NBR = :DA109-FSCL-MN-NBR                       
                     AND FSCL_YR_NBR = :DA109-FSCL-YR-NBR                       
           END-EXEC.                                                            
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
      *                                                                         
      * ORA COMMUNICATION AREA                                                  
      *                                                                         
        01       SQLCA.                                                         
                 05  SQLCAID               PIC X(8).                            
                 05  SQLCABC               PIC S9(9) COMPUTATIONAL.             
                 05  SQLCODE               PIC S9(9) COMPUTATIONAL.             
                 05  SQLERRM.                                                   
                     49 SQLERRML           PIC S9(4) COMPUTATIONAL.             
                     49 SQLERRMC           PIC X(70).                           
                 05  SQLERRP               PIC X(8).                            
                 05  SQLERRD OCCURS 6 TIMES                                     
                                           PIC S9(9) COMPUTATIONAL.             
                 05  SQLWARN.                                                   
                     10 SQLWARN0           PIC X(1).                            
                     10 SQLWARN1           PIC X(1).                            
                     10 SQLWARN2           PIC X(1).                            
                     10 SQLWARN3           PIC X(1).                            
                     10 SQLWARN4           PIC X(1).                            
                     10 SQLWARN5           PIC X(1).                            
                     10 SQLWARN6           PIC X(1).                            
                     10 SQLWARN7           PIC X(1).                            
                 05  SQLEXT                PIC X(8).                            
      ******************************************************************        
      * ORA COMMUNICATION AREA2                                                 
           COPY SQLCA2.                                                         
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      *    MAINLINE LOGIC                                              *        
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-TDAALW                                          
               UNTIL PS-END-OF-DA-FILE.                                         
                                                                                
           PERFORM 9900-EOJ-LOGIC.                                              
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *    LOGON TO ORACLE, AND DECLARE THE ORACLE CURSORS.            *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           INITIALIZE DPG51                                                     
                      DPG52                                                     
                      DPG53                                                     
                      DPG54                                                     
                      DPG55                                                     
                      DPG56.                                                    
                                                                                
           OPEN INPUT  CONTROL-CARD-FILE                                        
                       DEBIT-ALLOWANCE-FILE                                     
8613F                  ORACLE-LOGIN-FILE                                        
                OUTPUT DETAIL-DEBIT-ALLWNCE-FILE.                               
                                                                                
           PERFORM 7000-READ-CONTROL-CARD-FILE.                                 
           PERFORM 7000-READ-CONTROL-CARD-FILE                                  
                   UNTIL PS-END-OF-CONTROL-CARDS                                
                       OR CC-NOT-COMMENT.                                       
                                                                                
           IF PS-END-OF-CONTROL-CARDS                                           
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM ' PC-PROGRAM-NAME ' **'                      
               DISPLAY '** CONTROL CARD DATE EMPTY'                             
               SET ABEND-4003-CNTL-CARD-ERRORS TO TRUE                          
               PERFORM 9999-ABEND                                               
           ELSE                                                                 
               DISPLAY ' EXECUTING WITH THIS CONTROL CARD:'                     
               DISPLAY ' ----+----1----+----2----+----3----+----4'              
                       '----+----5----+----6----+----7'                         
               DISPLAY ' ' CC-CONTROL-CARD                                      
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
           PERFORM 7100-READ-DEBIT-ALLOWANCE-FILE.                              
           IF PS-END-OF-DA-FILE                                                 
               DISPLAY ' *** NO INPUT TO PROCESS ***'                           
               DISPLAY ' '                                                      
           ELSE                                                                 
               PERFORM 1100-LOGON-TO-ORACLE                                     
               MOVE DA109-BEG-FISCAL-DATE TO PV-BEG-FSCL-DATE                   
               MOVE DA109-END-FISCAL-DATE TO PV-END-FSCL-DATE                   
               DISPLAY 'FISCAL DATES USED - ' PV-BEG-FSCL-DATE ' - '            
                                              PV-END-FSCL-DATE                  
                                              UPON CONSOLE                      
           END-IF.                                                              
                                                                                
      ****************************************************************          
      * LOGON                                                        *          
      ****************************************************************          
       1100-LOGON-TO-ORACLE.                                                    
                                                                                
           DISPLAY '**------ LOGGING ON TO ORACLE ------**'.                    
                                                                                
8613F      MOVE '1100-LOGON-TO-ORACLE'   TO PV-PARAGRAPH-NAME.                  
                                                                                
8613F      READ ORACLE-LOGIN-FILE INTO CC-ORACLE-CONNECTION                     
8613F         AT END                                                            
8613F            SET ORACLE-LOGIN-EOF      TO TRUE.                             
                                                                                
8613F      IF CC-AUTO-LOGON                                                     
8613F         DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                 
8613F         EXEC SQL                                                          
8613F              CONNECT :PC-AUTO-LOGON                                       
8613F         END-EXEC                                                          
8613F      ELSE                                                                 
8613F         INITIALIZE PV-ORACLE-USERID-LEN                                   
8613F                    PV-ORACLE-PASSWORD-LEN                                 
8613F         INSPECT CC-ORACLE-USERID                                          
8613F                 TALLYING PV-ORACLE-USERID-LEN                             
8613F                 FOR CHARACTERS BEFORE INITIAL SPACE                       
8613F         INSPECT CC-ORACLE-PASSWORD                                        
8613F                 TALLYING PV-ORACLE-PASSWORD-LEN                           
8613F                 FOR CHARACTERS BEFORE INITIAL SPACE                       
8613F         MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN)     TO             
8613F                 USERNAME-ARR                                              
8613F         MOVE PV-ORACLE-USERID-LEN    TO USERNAME-LEN                      
8613F         MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO             
8613F                 PASSWD-ARR                                                
8613F         MOVE PV-ORACLE-PASSWORD-LEN  TO PASSWD-LEN                        
8613F         MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO             
8613F              PASSWD-ARR                                                   
8613F         MOVE PV-ORACLE-PASSWORD-LEN  TO PASSWD-LEN                        
8613F         MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN)     TO             
8613F              USERNAME-ARR                                                 
8613F         MOVE PV-ORACLE-USERID-LEN    TO USERNAME-LEN                      
8613F         MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO             
8613F              PASSWD-ARR                                                   
8613F         MOVE PV-ORACLE-PASSWORD-LEN  TO PASSWD-LEN                        
8613F         DISPLAY PC-PROGRAM-NAME  ' - LOGON TO ORACLE AS USER '            
8613F              USERNAME-ARR                                                 
8613F         EXEC SQL                                                          
8613F              CONNECT :USERNAME IDENTIFIED BY :PASSWD                      
8613F         END-EXEC                                                          
8613F         INITIALIZE CC-ORACLE-PASSWORD                                     
8613F                    PASSWD                                                 
8613F      END-IF.                                                              
8613F                                                                           
8613F      IF (SQLCODE NOT = 0)                                                 
8613F          PERFORM 9990-SQL-ERROR                                           
8613F      END-IF.                                                              
                                                                                
      ******************************************************************        
      *    2000-PROCESS-TDAALW                                         *        
      *      THIS IS PERFORMED ONLY VIA THE 0000-MAINLINE              *        
      *      PURPOSE: READ A RECORD FRO  DEBIT ALLOWANCE FILE          *        
      *               BUILD DETAIL DEBIT ALLOWANCE DETAIL RECORD       *        
      *               TO BE USED BY PROGRAM DAKRP114 TO CREATE A REPORT*        
      ******************************************************************        
       2000-PROCESS-TDAALW.                                                     
                                                                                
           MOVE DA109-DMM-NM                 TO DA110-DMM-NM.                   
           MOVE DA109-DMM-NBR                TO DA110-DMM-NBR.                  
           MOVE DA109-BUYER-NM               TO DA110-BUYER-NM.                 
           MOVE DA109-BUYER-NBR              TO DA110-BUYER-NBR.                
           MOVE DA109-VENDOR-NM              TO DA110-VENDOR-NM.                
           MOVE DA109-VENDOR-NBR             TO DA110-VENDOR-NBR.               
           MOVE DA109-ENTITY-ID              TO DA110-ENTITY-ID.                
           MOVE DA109-DEPT-NBR               TO DA110-DEPT-NBR.                 
           MOVE DA109-RMNG-AVL-COST-AMT-TOT TO                                  
                                            DA110-RMNG-AVL-COST-AMT-TOT.        
           MOVE DA109-FSCL-MN-NBR            TO DA110-FSCL-MN-NBR.              
           MOVE DA109-FSCL-YR-NBR            TO DA110-FSCL-YR-NBR.              
           MOVE DA109-BEG-FISCAL-DATE        TO DA110-BEG-FISCAL-DATE.          
           MOVE DA109-END-FISCAL-DATE        TO DA110-END-FISCAL-DATE.          
           MOVE DA109-PREV-PERIOD-IND        TO DA110-PREV-PERIOD-IND.          
           MOVE DA109-PREV-BAL               TO DA110-PREV-BAL                  
           MOVE DA109-DEPT-NBR  TO DAALW-DEPT-NBR.                              
           MOVE DA109-ENTITY-ID TO DAALW-ENT-ID.                                
           MOVE 0 TO PV-OFF-INVC-TOT.                                           
           PERFORM 8400-GET-PREV-TDAALW.                                        
           MOVE PV-OFF-INVC-TOT TO DA110-PREV-OFF-INVC-TOT.                     
           COMPUTE DA110-TTL-AVL-COST-AMT-TOT =                                 
             DA109-TTL-AVL-COST-AMT-TOT - PV-OFF-INVC-TOT.                      
           SET PS-NOT-END-OF-TDAALW TO TRUE                                     
           MOVE 0 TO PV-OFF-INVC-TOT.                                           
           PERFORM 8000-OPEN-TDAALW-CSR.                                        
           PERFORM 8200-FETCH-TDAALW.                                           
           IF PS-END-OF-TDAALW                                                  
             MOVE SPACE                 TO DA110-DR-ALW-NBR                     
                                           DA110-CPLT-DTE                       
             MOVE 0                     TO DA110-NET-CST-AMT                    
             MOVE 0                     TO DA110-OFF-INVC-TOT                   
             MOVE '00'                  TO DA110-SORT-SEQ                       
             MOVE 'NO DEBIT ALLOWANCES' TO DA110-CDE-DESC                       
             PERFORM 7200-WRITE-DA-DETAIL-REC                                   
           ELSE                                                                 
             PERFORM 8300-GET-OFF-INVC-TOT                                      
             MOVE PV-OFF-INVC-TOT TO DA110-OFF-INVC-TOT                         
             PERFORM UNTIL PS-END-OF-TDAALW                                     
               MOVE DAALW-DR-ALW-NBR      TO DA110-DR-ALW-NBR                   
               MOVE DAALW-CPLT-DTE (1:10) TO DA110-CPLT-DTE                     
               MOVE DAALW-NET-CST-AMT     TO DA110-NET-CST-AMT                  
      * THESE CODES ARE ASSIGNED BECAUSE THE USERS WANTED THE DA TYPES          
      * TO DISPLAY IN A PARTICULAR SEQUENCE THAT CANNOT BE ATTAINED BY          
      * USING ANY EXISTING FIELD.                                               
               IF DAALW-TYP-CDE  = 'AC'                                         
                 MOVE '10'               TO DA110-SORT-SEQ                      
               ELSE                                                             
                IF DAALW-TYP-CDE  = 'PC'                                        
                  MOVE '20'               TO DA110-SORT-SEQ                     
                ELSE                                                            
                  IF DAALW-TYP-CDE  = 'PR'                                      
                   MOVE '30'               TO DA110-SORT-SEQ                    
                 ELSE                                                           
                  IF DAALW-TYP-CDE  = 'MA'                                      
                   MOVE '40'               TO DA110-SORT-SEQ                    
                  ELSE                                                          
                   IF DAALW-TYP-CDE  = 'MC'                                     
                    MOVE '50'               TO DA110-SORT-SEQ                   
                   END-IF                                                       
                  END-IF                                                        
                 END-IF                                                         
                END-IF                                                          
               END-IF                                                           
      *                                                                         
               ADD +1                   TO PA-DA-RECORD-WRITTEN                 
               PERFORM 8800-GET-DA-TYPE-DESC                                    
               MOVE DACDE-CDE-DESC         TO DA110-CDE-DESC                    
               PERFORM 7200-WRITE-DA-DETAIL-REC                                 
               PERFORM 8200-FETCH-TDAALW                                        
             END-PERFORM                                                        
           END-IF.                                                              
           PERFORM 8100-CLOSE-TDAALW-CSR.                                       
                                                                                
                                                                                
           PERFORM 7100-READ-DEBIT-ALLOWANCE-FILE.                              
                                                                                
      ******************************************************************        
      *  READ CONTROL CARD FILE                                        *        
      ******************************************************************        
       7000-READ-CONTROL-CARD-FILE.                                             
                                                                                
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                          
               AT END SET PS-END-OF-CONTROL-CARDS TO TRUE.                      
                                                                                
      ******************************************************************        
      *  READ DEBIT ALLOWANCE FILE                                     *        
      ******************************************************************        
       7100-READ-DEBIT-ALLOWANCE-FILE.                                          
                                                                                
           ADD +1 TO PA-DA-RECORDS-READ.                                        
           READ DEBIT-ALLOWANCE-FILE INTO DA109-AVL-VND-FUND-REC                
               AT END SET PS-END-OF-DA-FILE TO TRUE.                            
                                                                                
      ******************************************************************        
      *  WRITE THE DEBIT ALLOWANCE DETAIL FILE                         *        
      ******************************************************************        
       7200-WRITE-DA-DETAIL-REC.                                                
                                                                                
           WRITE DETAIL-DEBIT-ALLWNCE-RECORD FROM                               
                 DA110-AVL-VND-FUND-DET-REC.                                    
                                                                                
      ******************************************************************        
      *  OPEN DEBIT ALLOWANCE CURSOR                                   *        
      ******************************************************************        
       8000-OPEN-TDAALW-CSR.                                                    
                                                                                
           MOVE '8000-OPEN-TDAALW-CSR  ' TO PV-PARAGRAPH-NAME.                  
                                                                                
           EXEC SQL                                                             
               OPEN TDAALW_INFO                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   SET PS-NOT-END-OF-TDAALW    TO TRUE                          
               WHEN SQLCODE = 1403                                              
                   SET PS-END-OF-TDAALW        TO TRUE                          
               WHEN OTHER                                                       
                   PERFORM 9990-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  CLOSE DEBIT ALLOWANCE CURSOR                                  *        
      ******************************************************************        
       8100-CLOSE-TDAALW-CSR.                                                   
                                                                                
           MOVE '8100-CLOSE-TDAALW-CSR ' TO PV-PARAGRAPH-NAME.                  
                                                                                
           EXEC SQL                                                             
               CLOSE TDAALW_INFO                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9990-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  FETCH THE NEXT ROW                                            *        
      ******************************************************************        
       8200-FETCH-TDAALW.                                                       
                                                                                
           MOVE '8200-FETCH-TDAALW   ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           EXEC SQL                                                             
               FETCH TDAALW_INFO                                                
               INTO                                                             
                    :DAALW-DR-ALW-NBR                                           
                   ,:DAALW-TYP-CDE                                              
                   ,:DAALW-DEPT-NBR                                             
                   ,:DAALW-ENT-ID                                               
                   ,:DAALW-CPLT-DTE      :PV-NULL-RETURN                        
                   ,:DAALW-NET-CST-AMT                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE = 1403                                              
                   SET PS-END-OF-TDAALW        TO TRUE                          
               WHEN OTHER                                                       
                   PERFORM 9990-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  GET DEBIT ALLOWANCE TYPE DESCRIPTION                          *        
      ******************************************************************        
       8300-GET-OFF-INVC-TOT.                                                   
                                                                                
           MOVE '8300-GET-OFF-INVC-TOT' TO PV-PARAGRAPH-NAME.                   
                                                                                
                                                                                
           EXEC SQL                                                             
                   SELECT                                                       
                         SUM(NET_CST_AMT)                                       
                   INTO  :PV-OFF-INVC-TOT                                       
                   FROM                                                         
                         TDAALW                                                 
                   WHERE DEPT_NBR = :DAALW-DEPT-NBR                             
                     AND ENT_ID   = :DAALW-ENT-ID                               
                     AND TYP_CDE IN ('AC','PC','PR')                            
                     AND CPLT_DTE IS NOT NULL                                   
                     AND NET_CST_AMT IS NOT NULL                                
                     AND FSCL_MN_NBR = :DA109-FSCL-MN-NBR                       
                     AND FSCL_YR_NBR = :DA109-FSCL-YR-NBR                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE = -1405                                             
               WHEN SQLCODE = -1403                                             
                 CONTINUE                                                       
               WHEN OTHER                                                       
                   PERFORM 9995-GET-SQL-MSG                                     
                   IF WS-MSG-LENGTH > ZEROES                                    
                       DISPLAY '   *** SQL CODE: ' SQLCODE                      
                               ' MESSAGE: ' WS-MSG-TEXT(1:WS-MSG-LENGTH)        
                   END-IF                                                       
                   MOVE SPACES                TO DACDE-CDE-DESC                 
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  GET PREVIOUS DEBIT ALLOWANCES                                 *        
      ******************************************************************        
       8400-GET-PREV-TDAALW.                                                    
                                                                                
           MOVE '8400-GET-PREV-TDAALW' TO PV-PARAGRAPH-NAME.                    
                                                                                
           EXEC SQL                                                             
                   SELECT                                                       
                         SUM(NET_CST_AMT)                                       
                   INTO                                                         
                         PV-OFF-INVC-TOT                                        
                   FROM                                                         
                         TDAALW                                                 
                   WHERE DEPT_NBR = :DAALW-DEPT-NBR                             
                     AND ENT_ID   = :DAALW-ENT-ID                               
                     AND TYP_CDE IN ('AC','PC','PR')                            
                     AND CPLT_DTE IS NOT NULL                                   
                     AND ((FSCL_MN_NBR < :DA109-FSCL-MN-NBR                     
                     AND FSCL_YR_NBR = :DA109-FSCL-YR-NBR)                      
                      OR FSCL_YR_NBR < :DA109-FSCL-YR-NBR)                      
                     AND ((FSCL_MN_NBR > :DA109-BEG-FSCL-MN-NBR                 
                     AND FSCL_YR_NBR = :DA109-BEG-FSCL-YR-NBR)                  
                      OR FSCL_YR_NBR > :DA109-BEG-FSCL-YR-NBR)                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE = -1405                                             
               WHEN SQLCODE = -1403                                             
                 CONTINUE                                                       
               WHEN OTHER                                                       
                   PERFORM 9995-GET-SQL-MSG                                     
                   IF WS-MSG-LENGTH > ZEROES                                    
                       DISPLAY '   *** SQL CODE: ' SQLCODE                      
                               ' MESSAGE: ' WS-MSG-TEXT(1:WS-MSG-LENGTH)        
                   END-IF                                                       
                   MOVE SPACES                TO DACDE-CDE-DESC                 
           END-EVALUATE.                                                        
      ******************************************************************        
      *  GET DEBIT ALLOWANCE TYPE DESCRIPTION                          *        
      ******************************************************************        
       8800-GET-DA-TYPE-DESC.                                                   
                                                                                
           MOVE '8800-GET-DA-TYPE-DESC' TO PV-PARAGRAPH-NAME.                   
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                       UPPER(CDE_DESC)                                          
                 INTO                                                           
                      :DACDE-CDE-DESC                                           
                 FROM                                                           
                       TDACDE                                                   
                 WHERE                                                          
                       CDE_VAL_CDE = :DAALW-TYP-CDE                             
                   AND CDE_TYP_DESC = 'DEBIT TYPE'                              
                   AND ACTV_IND = 'Y'                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY '  *** UNABLE TO DETERMINE DESC OF DA-TYPE '         
                           DAALW-TYP-CDE                                        
                   MOVE 'DESC NOT FOUND' TO DACDE-CDE-DESC                      
                   PERFORM 9995-GET-SQL-MSG                                     
                   IF WS-MSG-LENGTH > ZEROES                                    
                       DISPLAY '   *** SQL CODE: ' SQLCODE                      
                               ' MESSAGE: ' WS-MSG-TEXT(1:WS-MSG-LENGTH)        
                   END-IF                                                       
                   MOVE SPACES                TO DACDE-CDE-DESC                 
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    9900-EOJ-LOGIC.                                             *        
      *      THIS IS PERFORMED AT END OF INPUT FILE.                   *        
      *      PURPOSE: CLOSE INPUT FILE, COMMIT CHANGES AND DISPLAY     *        
      *      RECORD COUNTS.                                            *        
      ******************************************************************        
       9900-EOJ-LOGIC.                                                          
           CLOSE CONTROL-CARD-FILE                                              
                 DEBIT-ALLOWANCE-FILE                                           
                 DETAIL-DEBIT-ALLWNCE-FILE.                                     
           DISPLAY ' '.                                                         
           DISPLAY 'DA DETAIL RECORDS READ    ' PA-DA-RECORDS-READ.             
           DISPLAY 'DA DETAIL RECORDS WRITTEN ' PA-DA-RECORD-WRITTEN.           
           DISPLAY '**------ END ' PC-PROGRAM-NAME ' ------**'.                 
                                                                                
                                                                                
      ******************************************************************        
      * DISPLAYS ERRORS ENCOUNTERED ON SQL REQUEST                              
      ******************************************************************        
       9990-SQL-ERROR.                                                          
                                                                                
           MOVE SQLCODE    TO SQLCODE2.                                         
           MOVE SQLERRD(3) TO SQLERRD3.                                         
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
                                                                                
           PERFORM 9995-GET-SQL-MSG.                                            
           IF WS-MSG-LENGTH > ZEROES                                            
               DISPLAY ' ** MESSAGE TEXT      ** = '                            
                       WS-MSG-TEXT(1:WS-MSG-LENGTH)                             
           END-IF.                                                              
                                                                                
           SET ABEND-4013-DB-ERROR  TO TRUE.                                    
                                                                                
           PERFORM 9999-ABEND.                                                  
                                                                                
      ******************************************************************        
      * GET ORACLE SQL ERROR MESSAGE                                            
      ******************************************************************        
       9995-GET-SQL-MSG.                                                        
                                                                                
           MOVE SPACES TO WS-MSG-TEXT.                                          
                                                                                
           CALL 'SQLGLM' USING WS-MSG-TEXT,                                     
                               WS-MAX-SIZE,                                     
                               WS-MSG-LENGTH.                                   
                                                                                
      ******************************************************************        
      * PROGRAM ABEND PARAGRAPH                                                 
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING PV-ABEND-CODE.                                
                                                                                
                                                                                
      *----------------------------------------------------------               
      *  DYNAMIC ALLOCATION ROUTINE                                             
      *----------------------------------------------------------               
      *DP023-0000-DYNALC.                                                       
           COPY DPPD023.                                                        
                                                                                
      ******************************************************************        
      *    END OF SOURCE CODE FOR DAKUT315                            *         
      ******************************************************************        
