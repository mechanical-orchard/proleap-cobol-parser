       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    ETKUT144.                                                 
       AUTHOR.        SUE PARI.                                                 
       DATE-WRITTEN.  04/29/2002.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  THIS PROGRAM REFORMATS THE STORE ADDRESS FILE INTO A FORMAT   *        
      *  THAT CAN BE USED BY THE APPLICATION INTEGRATOR MAP.           *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS INPUT-RECORD.                                         
                                                                                
           COPY ETRD033.                                                        
                                                                                
                                                                                
       FD  OUTPUT-FILE                                                          
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 60 CHARACTERS                                        
           DATA RECORD IS ET032-RELATIONSHIP-RECORD.                            
                                                                                
           COPY ETRD032.                                                        
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-HL-LEVEL                    PIC 9(5)    VALUE ZERO.               
                                                                                
       01  WS-ZIP-CODE                    PIC X(10).                            
       01  FILLER REDEFINES WS-ZIP-CODE.                                        
           05  WS-ZIP-CODE1               PIC X(5).                             
           05  WS-ZIP-CODE2               PIC X(5).                             
                                                                                
       01  WS-FORMATTED-DATE              PIC X(10).                            
       01  FILLER REDEFINES WS-FORMATTED-DATE.                                  
           05  WS-FORM-DATE-YEAR          PIC X(4).                             
           05  FILLER                     PIC X(1).                             
           05  WS-FORM-DATE-MONTH         PIC X(2).                             
           05  FILLER                     PIC X(1).                             
           05  WS-FORM-DATE-DAY           PIC X(2).                             
                                                                                
       01  WS-DATE                        PIC X(8).                             
       01  FILLER REDEFINES WS-DATE.                                            
           05  WS-DATE-YEAR               PIC X(4).                             
           05  WS-DATE-MONTH              PIC X(2).                             
           05  WS-DATE-DAY                PIC X(2).                             
                                                                                
       01  WS-CURRENT-DATE                PIC X(6).                             
                                                                                
       01  END-OF-INPUT-FILE-SW           PIC X(1)    VALUE 'N'.                
           88  END-OF-INPUT-FILE                      VALUE 'Y'.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT  INPUT-FILE.                                              
           OPEN OUTPUT OUTPUT-FILE.                                             
                                                                                
           ACCEPT WS-CURRENT-DATE FROM DATE.                                    
                                                                                
           PERFORM 9000-READ-INPUT-FILE                                         
              THRU 9000-READ-INPUT-FILE-EXIT.                                   
                                                                                
           PERFORM 1000-REFORMAT-FILE                                           
              THRU 1000-REFORMAT-FILE-EXIT                                      
                 UNTIL END-OF-INPUT-FILE.                                       
                                                                                
           CLOSE INPUT-FILE.                                                    
           CLOSE OUTPUT-FILE.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *  BEFORE THE FIRST RECORD IS PROCESSED, CREATE HEADER LEVEL     *        
      *  INFORMATION, WHICH CONSISTS OF:                               *        
      *    1.  RECORD TYPE 01 - HEADER LEVEL BHT & N1 SEGMENTS         *        
      *    2.  RECORD TYPE 02 - FIRST HL SEGMENT                       *        
      *    2.  RECORD TYPE 03 - FIRST N1 SEGMENT                       *        
      *                                                                *        
      *  FOR EVERY RECORD ON THE INPUT FILE, CREATE THE FOLLOWING:     *        
      *    1.  RECORD TYPE 02 - HL SEGMENT                             *        
      *    2.  RECORD TYPE 03 - N1 SEGMENT                             *        
      *    3.  RECORD TYPE 04 - N3 SEGMENT                             *        
      *    4.  RECORD TYPE 05 - N4 SEGMENT                             *        
      *    5.  RECORD TYPE 06 - DTM SEGMENT - CREATE IF THERE IS AN    *        
      *        OPENED DATE OR A CLOSED DATE                            *        
      *    6.  RECORD TYPE 07 - ASI SEGMENT - CREATE IF THERE IS A     *        
      *        VALUE IN THE ACTION CODE FIELD.                         *        
      ******************************************************************        
       1000-REFORMAT-FILE.                                                      
                                                                                
           IF WS-HL-LEVEL = ZERO                                                
              PERFORM 2000-CREATE-RECORD-01                                     
                 THRU 2000-CREATE-RECORD-01-EXIT                                
              PERFORM 2500-FIRST-HL-LEVEL                                       
                 THRU 2500-FIRST-HL-LEVEL-EXIT.                                 
                                                                                
           PERFORM 3000-CREATE-RECORD-02                                        
              THRU 3000-CREATE-RECORD-02-EXIT.                                  
                                                                                
           PERFORM 4000-CREATE-RECORD-03                                        
              THRU 4000-CREATE-RECORD-03-EXIT.                                  
                                                                                
           PERFORM 5000-CREATE-RECORD-04                                        
              THRU 5000-CREATE-RECORD-04-EXIT.                                  
                                                                                
           PERFORM 6000-CREATE-RECORD-05                                        
              THRU 6000-CREATE-RECORD-05-EXIT.                                  
                                                                                
           IF ET033-OPENED-DATE NOT = SPACES OR                                 
              ET033-CLOSED-DATE NOT = SPACES                                    
                 PERFORM 7000-CREATE-RECORD-06                                  
                    THRU 7000-CREATE-RECORD-06-EXIT.                            
                                                                                
           IF ET033-ACTION-CODE = 'A' OR 'D' OR 'C'                             
              PERFORM 8000-CREATE-RECORD-07                                     
                 THRU 8000-CREATE-RECORD-07-EXIT.                               
                                                                                
           PERFORM 9000-READ-INPUT-FILE                                         
              THRU 9000-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-REFORMAT-FILE-EXIT.                                                 
           EXIT.                                                                
                                                                                
                                                                                
       2000-CREATE-RECORD-01.                                                   
                                                                                
           INITIALIZE ET032-RELATIONSHIP-RECORD.                                
                                                                                
           MOVE '01'                     TO ET032-RECORD-TYPE-CODE.             
           MOVE SPACES                   TO ET032-HDR-ENT-ID.                   
           MOVE '0065'                   TO ET032-HDR-STRUCTURE-CODE.           
           MOVE 'FR'                     TO ET032-HDR-ID-CODE.                  
           MOVE 'KOHLS DEPARTMENT STORE' TO ET032-HDR-NAME.                     
                                                                                
           IF ET033-ACTION-CODE = 'C' OR                                        
              ET033-ACTION-CODE = 'D' OR                                        
              ET033-ACTION-CODE = 'A'                                           
                 MOVE '04' TO ET032-HDR-PURPOSE-CODE                            
           ELSE                                                                 
              MOVE '00' TO ET032-HDR-PURPOSE-CODE.                              
                                                                                
           STRING '20'            DELIMITED BY SIZE                             
                  WS-CURRENT-DATE DELIMITED BY SIZE                             
                    INTO ET032-HDR-DATE.                                        
                                                                                
           WRITE ET032-RELATIONSHIP-RECORD.                                     
                                                                                
       2000-CREATE-RECORD-01-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
       2500-FIRST-HL-LEVEL.                                                     
                                                                                
           INITIALIZE ET032-RELATIONSHIP-RECORD.                                
                                                                                
           ADD 1 TO WS-HL-LEVEL.                                                
                                                                                
           MOVE '02'        TO ET032-RECORD-TYPE-CODE.                          
           MOVE WS-HL-LEVEL TO ET032-HL-ID-NUMBER.                              
           MOVE 0           TO ET032-HL-PARENT-ID.                              
           MOVE '35'        TO ET032-HL-LEVEL-CODE.                             
                                                                                
           WRITE ET032-RELATIONSHIP-RECORD.                                     
                                                                                
           INITIALIZE ET032-RELATIONSHIP-RECORD.                                
                                                                                
           MOVE '03'                     TO ET032-RECORD-TYPE-CODE.             
           MOVE 'CQ'                     TO ET032-N1-ID-CODE1.                  
           MOVE 'KOHLS DEPARTMENT STORE' TO ET032-N1-NAME.                      
                                                                                
           WRITE ET032-RELATIONSHIP-RECORD.                                     
                                                                                
       2500-FIRST-HL-LEVEL-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       3000-CREATE-RECORD-02.                                                   
                                                                                
           INITIALIZE ET032-RELATIONSHIP-RECORD.                                
                                                                                
           ADD 1 TO WS-HL-LEVEL.                                                
                                                                                
           MOVE '02'        TO ET032-RECORD-TYPE-CODE.                          
           MOVE WS-HL-LEVEL TO ET032-HL-ID-NUMBER.                              
           MOVE 1           TO ET032-HL-PARENT-ID.                              
           MOVE '36'        TO ET032-HL-LEVEL-CODE.                             
                                                                                
           WRITE ET032-RELATIONSHIP-RECORD.                                     
                                                                                
       3000-CREATE-RECORD-02-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
       4000-CREATE-RECORD-03.                                                   
                                                                                
           INITIALIZE ET032-RELATIONSHIP-RECORD.                                
                                                                                
           MOVE '03'             TO ET032-RECORD-TYPE-CODE.                     
           MOVE 'BU'             TO ET032-N1-ID-CODE1.                          
           MOVE ET033-STORE-NAME TO ET032-N1-NAME.                              
           MOVE '93'             TO ET032-N1-ID-QUALIFIER.                      
           MOVE ET033-STORE-NBR  TO ET032-N1-LOCATION.                          
                                                                                
           IF ET033-STORE                                                       
              MOVE 'SN' TO ET032-N1-ID-CODE2                                    
           ELSE                                                                 
              IF ET033-WAREHOUSE                                                
                 MOVE 'WH' TO ET032-N1-ID-CODE2                                 
              ELSE                                                              
                 MOVE 'CQ' TO ET032-N1-ID-CODE2.                                
                                                                                
           WRITE ET032-RELATIONSHIP-RECORD.                                     
                                                                                
       4000-CREATE-RECORD-03-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
       5000-CREATE-RECORD-04.                                                   
                                                                                
           INITIALIZE ET032-RELATIONSHIP-RECORD.                                
                                                                                
           MOVE '04'           TO ET032-RECORD-TYPE-CODE.                       
           MOVE ET033-ADDRESS1 TO ET032-N3-ADDRESS1.                            
           MOVE ET033-ADDRESS2 TO ET032-N3-ADDRESS2.                            
                                                                                
           WRITE ET032-RELATIONSHIP-RECORD.                                     
                                                                                
       5000-CREATE-RECORD-04-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
       6000-CREATE-RECORD-05.                                                   
                                                                                
           INITIALIZE ET032-RELATIONSHIP-RECORD.                                
                                                                                
           MOVE '05'        TO ET032-RECORD-TYPE-CODE.                          
           MOVE ET033-CITY  TO ET032-N4-CITY.                                   
           MOVE ET033-STATE TO ET032-N4-STATE.                                  
                                                                                
           IF      ET033-ZIP-CODE2 = '0000'  OR                                 
                   ET033-ZIP-CODE2 = '0000 ' OR                                 
                   ET033-ZIP-CODE2 = '00000'                                    
              MOVE ET033-ZIP-CODE1 TO ET032-N4-ZIP                              
           ELSE                                                                 
              MOVE ET033-ZIP-CODE1 TO WS-ZIP-CODE1                              
              MOVE ET033-ZIP-CODE2 TO WS-ZIP-CODE2                              
              MOVE WS-ZIP-CODE     TO ET032-N4-ZIP.                             
                                                                                
           WRITE ET032-RELATIONSHIP-RECORD.                                     
                                                                                
       6000-CREATE-RECORD-05-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
       7000-CREATE-RECORD-06.                                                   
                                                                                
           IF ET033-OPENED-DATE NOT = SPACES                                    
              INITIALIZE ET032-RELATIONSHIP-RECORD                              
              MOVE '06'               TO ET032-RECORD-TYPE-CODE                 
              MOVE '145'              TO ET032-DTM-QUALIFIER                    
              MOVE ET033-OPENED-DATE  TO WS-FORMATTED-DATE                      
              MOVE WS-FORM-DATE-YEAR  TO WS-DATE-YEAR                           
              MOVE WS-FORM-DATE-MONTH TO WS-DATE-MONTH                          
              MOVE WS-FORM-DATE-DAY   TO WS-DATE-DAY                            
              MOVE WS-DATE            TO ET032-DTM-DATE                         
              WRITE ET032-RELATIONSHIP-RECORD.                                  
                                                                                
           IF ET033-CLOSED-DATE NOT = SPACES                                    
              INITIALIZE ET032-RELATIONSHIP-RECORD                              
              MOVE '06'               TO ET032-RECORD-TYPE-CODE                 
              MOVE '146'              TO ET032-DTM-QUALIFIER                    
              MOVE ET033-CLOSED-DATE  TO WS-FORMATTED-DATE                      
              MOVE WS-FORM-DATE-YEAR  TO WS-DATE-YEAR                           
              MOVE WS-FORM-DATE-MONTH TO WS-DATE-MONTH                          
              MOVE WS-FORM-DATE-DAY   TO WS-DATE-DAY                            
              MOVE WS-DATE            TO ET032-DTM-DATE                         
              WRITE ET032-RELATIONSHIP-RECORD.                                  
                                                                                
       7000-CREATE-RECORD-06-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
       8000-CREATE-RECORD-07.                                                   
                                                                                
           INITIALIZE ET032-RELATIONSHIP-RECORD.                                
                                                                                
           MOVE '07' TO ET032-RECORD-TYPE-CODE.                                 
           MOVE '2 ' TO ET032-ASI-ACTION-CODE.                                  
                                                                                
           IF ET033-ADD                                                         
              MOVE '021' TO ET032-ASI-TYPE-CODE                                 
           ELSE                                                                 
              IF ET033-DELETE                                                   
                 MOVE '002' TO ET032-ASI-TYPE-CODE                              
              ELSE                                                              
                 MOVE '001' TO ET032-ASI-TYPE-CODE.                             
                                                                                
           WRITE ET032-RELATIONSHIP-RECORD.                                     
                                                                                
       8000-CREATE-RECORD-07-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
       9000-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE                                                      
              AT END                                                            
                 SET END-OF-INPUT-FILE TO TRUE.                                 
                                                                                
       9000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
