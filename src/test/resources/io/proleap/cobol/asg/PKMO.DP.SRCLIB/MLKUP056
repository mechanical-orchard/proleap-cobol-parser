       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP056.                                         00030000
       AUTHOR.        JACK KRAMER.                                      00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  02/17/2010.                                       00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *           MLKUP056 - PURCHASE ADJUSTMENT TRANSACTION UPDATE    *00100000
      *                      TABLE UPDATE (MLT_PADJ_TRN_DTL)           *00110000
      ******************************************************************00110100
      ******************************************************************00110200
      *    WR/PITS#     DATE        DESCRIPTION                        *00110300
      *    --------   --------     ---------------------------------   *00110400
      *    WR37965    02/17/10     INITIAL CREATION                    *00110500
      ******************************************************************00110600
                                                                        00110700
       ENVIRONMENT DIVISION.                                            00110800
                                                                        00110900
       CONFIGURATION SECTION.                                           00111000
       SOURCE-COMPUTER.        IBM-3090.                                00112000
       OBJECT-COMPUTER.        IBM-3090.                                00113000
       INPUT-OUTPUT SECTION.                                            00114000
       FILE-CONTROL.                                                    00115000
           SELECT PADJ-TRN-DTL-FILE                                     00116000
               ASSIGN TO INP01.                                         00117000
                                                                        00118000
           SELECT CNTL-DATE-FILE                                        00118107
               ASSIGN TO INP02.                                         00118207
                                                                        00118307
       DATA DIVISION.                                                   00119000
       FILE SECTION.                                                    00120000
                                                                        00130000
       FD  PADJ-TRN-DTL-FILE                                            00140000
           LABEL RECORDS ARE STANDARD                                   00150000
           RECORDING MODE IS F                                          00160000
           BLOCK CONTAINS 0 RECORDS.                                    00170000
       01  PADJ-TRN-DTL-REC            PIC X(200).                      00180000
                                                                        00180107
       FD  CNTL-DATE-FILE                                               00181007
           LABEL RECORDS ARE STANDARD                                   00182007
           RECORDING MODE IS F                                          00183007
           BLOCK CONTAINS 0 RECORDS.                                    00184007
       01  CNTL-DATE-REC               PIC X(80).                       00185007
      *                                                                 00190000
                                                                        00200000
       WORKING-STORAGE SECTION.                                         00210000
                                                                        00220000
       01  FILLER                      PIC X(28)   VALUE                00230000
                'BEGINNING OF WORKING STORAGE'.                         00240000
                                                                        00250000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00260000
                                                                        00270000
       01  PC-PROGRAM-CONSTANTS.                                        00280000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK056'.    00290006
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP056'.  00300000
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00310000
                                                                        00320000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00330000
           05  PE-MSG-01                       PIC X(50) VALUE          00340000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00350000
           05  PE-MSG-02                       PIC X(50) VALUE          00360000
                'ATTEMPT TO UPDATE ROW ON MLT_PADJ_TRN_DTL FAILED'.     00370000
           05  PE-MSG-03                       PIC X(50) VALUE          00380000
                'ATTEMPT TO INSERT ROW ON MLT_PADJ_TRN_DTL FAILED'.     00390000
           05  PE-MSG-04                       PIC X(50) VALUE          00400000
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00410000
           05  PE-MSG-05                       PIC X(50) VALUE          00420000
                'UPDATE TO TCKRSTINF FAILED                      '.     00430000
           05  PE-MSG-06                       PIC X(50) VALUE          00440000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00450000
           05  PE-MSG-07                       PIC X(50) VALUE          00460000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00470000
           05  PE-MSG-08                       PIC X(50) VALUE          00471007
                'ERROR WHILE VALIDATING CONTROL DATE'.                  00472008
      *                                                                 00480000
       01  PS-PROGRAM-SWITCHES.                                         00490000
           05  PS-EOF-PADJ-DTL-FILE-SW      PIC X(01)     VALUE 'N'.    00500000
               88  PS-EOF-PADJ-DTL-FILE                   VALUE 'Y'.    00510000
           05  PS-MLT00039-CURSOR-EOF-SW    PIC X(01)     VALUE 'N'.    00520000
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00530000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00540000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00550000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00560000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00570000
           05  PS-CNTL-DATE-FILE-SW         PIC X(01)     VALUE 'N'.    00571007
               88  PS-END-OF-CNTL-DTE-FILE                VALUE 'Y'.    00572007
      *                                                                 00572107
       01  CC-CONTROL-CARD.                                             00573007
           05  CC-CNTL-DATE                 PIC X(10)     VALUE SPACES. 00574007
           05  FILLER                       PIC X(70)     VALUE SPACES. 00575007
      *                                                                 00580000
       01  PROGRAM-VARIABLES.                                           00640000
           05  PV-RETRY-COUNT            PIC S9(04) VALUE +0.           00650000
           05  PV-DEADLOCK-COUNT         PIC S9(04) VALUE +0.           00660000
           05  PV-PARAGRAPH-NAME         PIC  X(30) VALUE SPACES.       00673000
           05  PV-ABEND-MESSAGE          PIC  X(75) VALUE SPACES.       00674000
           05  PV-OPERATION-ATTEMPTED    PIC  X(50) VALUE SPACES.       00675000
           05  PV-CURRENT-TIMESTAMP      PIC  X(26) VALUE SPACES.       00676000
           05  PV-COMMIT-TIMESTAMP       PIC  X(26) VALUE SPACES.       00677000
           05  PV-DISPLAY-FIELD          PIC ZZZZZZZZ9  VALUE ZERO.     00677100
           05  PV-DB2-DATE-EXISTS        PIC   X(1) VALUE SPACES.       00677207
      *                                                                 00678000
       01  PA-PROGRAM-ACCUMULATORS.                                     00679000
           05  PA-RECORD-COUNTS.                                        00680000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      00690000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      00700000
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      00710000
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      00720000
      *                                                                 00730000
       01  PD-PROGRAM-DISPLAYS.                                         00740000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            00750000
      *                                                                 00960000
      ******************************************************************00970000
      * MLRD090 - AUDIT DETAIL TRANSACTIONS                             00980000
      ******************************************************************00990000
           COPY MLRD090.                                                01000020
      *                                                                 01010000
      ******************************************************************01020000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01030000
      ******************************************************************01040000
           COPY DPWS004.                                                01050000
      *                                                                 01060000
      ******************************************************************01070000
      *  USED FOR DB2 ERRORS                                            01080000
      ******************************************************************01090000
           EXEC SQL                                                     01091000
               INCLUDE SQLCA                                            01092000
           END-EXEC.                                                    01093000
      *                                                                 01094000
      ******************************************************************01094109
      * DATE TABLE                                                      01094209
      ******************************************************************01094309
           EXEC SQL                                                     01094509
             INCLUDE TDTATTR                                            01094609
           END-EXEC.                                                    01094709
      *                                                                 01094809
      ******************************************************************01095000
      *  MERCHANDISE LEDGER PURCHASE ADJUSTMENT AUDIT DETAIL            01096000
      ******************************************************************01097000
           EXEC SQL                                                     01098000
               INCLUDE MLT00039                                         01099000
           END-EXEC.                                                    01100000
      *                                                                 01110000
      ***************************************************************** 01120000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01130000
      ***************************************************************** 01140000
           EXEC SQL                                                     01150000
               INCLUDE TCKRSTCN                                         01160000
           END-EXEC.                                                    01170000
      *                                                                 01180000
           EXEC SQL                                                     01190000
               INCLUDE TCKRSTIN                                         01200000
           END-EXEC.                                                    01210000
      *                                                                 01220000
      ******************************************************************01230000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01240000
      ******************************************************************01250000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01260000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +76 COMP.  01270005
           05  CR-CHECKPOINT-RESTART-VAR.                               01280000
               10  CR-PREV-DTL-KEY.                                     01290000
                   15  CR-FSCL-WK-END-DTE   PIC X(10)  VALUE SPACES.    01310000
                   15  CR-PRCG-DTE          PIC X(10)  VALUE SPACES.    01330000
                   15  CR-DEPT-NBR          PIC S9(4)V COMP-3 VALUE +0. 01350000
                   15  CR-MAJ-CL-NBR        PIC S9(4)V COMP-3 VALUE +0. 01351000
                   15  CR-SUB-CL-NBR        PIC S9(4)V COMP-3 VALUE +0. 01352000
                   15  CR-LOC-NBR           PIC S9(4)  COMP   VALUE +0. 01353000
                   15  CR-ML-LO-MDSE-LVL-ID PIC S9(9)  COMP   VALUE +0. 01354000
                   15  CR-ORIG-ENT-ID       PIC S9(9)  COMP   VALUE +0. 01355000
                   15  CR-ORIG-LBL-SEQ-NBR  PIC S9(9)  COMP   VALUE +0. 01356000
                   15  CR-DUMMY-SKU-IND     PIC X(01) VALUE SPACES.     01357000
                   15  CR-FINCL-PRC-GP-CDE  PIC X(06) VALUE SPACES.     01358000
                   15  CR-SRC-SYS-ID        PIC S9(4)  COMP   VALUE +0. 01359000
                   15  CR-TRN-TYP-CDE       PIC X(03) VALUE SPACES.     01359100
                   15  CR-AP-TRN-CDE        PIC X(02) VALUE SPACES.     01359200
                   15  CR-DR-CR-CDE         PIC X(02) VALUE SPACES.     01359300
                   15  CR-ENT-ID            PIC S9(9)  COMP   VALUE +0. 01359404
                   15  CR-LBL-SEQ-NBR       PIC S9(4)  COMP   VALUE +0. 01359504
               10  CR-INPUT-READ-COUNT      PIC 9(09) VALUE ZEROES.     01360000
      *                                                                 01370000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01380000
           05  CK-SAVE-PREV-KEY.                                        01390000
               10  CK-FSCL-WK-END-DTE     PIC X(10)  VALUE SPACES.      01391000
               10  CK-PRCG-DTE            PIC X(10)  VALUE SPACES.      01392000
               10  CK-DEPT-NBR            PIC S9(4)V COMP-3 VALUE +0.   01393000
               10  CK-MAJ-CL-NBR          PIC S9(4)V COMP-3 VALUE +0.   01394000
               10  CK-SUB-CL-NBR          PIC S9(4)V COMP-3 VALUE +0.   01395000
               10  CK-LOC-NBR             PIC S9(4)  COMP   VALUE +0.   01396000
               10  CK-ML-LO-MDSE-LVL-ID   PIC S9(9)  COMP   VALUE +0.   01397000
               10  CK-ORIG-ENT-ID         PIC S9(9)  COMP   VALUE +0.   01398000
               10  CK-ORIG-LBL-SEQ-NBR    PIC S9(9)  COMP   VALUE +0.   01399000
               10  CK-DUMMY-SKU-IND       PIC X(01) VALUE SPACES.       01399100
               10  CK-FINCL-PRC-GP-CDE    PIC X(06) VALUE SPACES.       01399200
               10  CK-SRC-SYS-ID          PIC S9(4)  COMP   VALUE +0.   01399300
               10  CK-TRN-TYP-CDE         PIC X(03) VALUE SPACES.       01399400
               10  CK-AP-TRN-CDE          PIC X(02) VALUE SPACES.       01399500
               10  CK-DR-CR-CDE           PIC X(02) VALUE SPACES.       01399600
               10  CK-ENT-ID              PIC S9(9)  COMP   VALUE +0.   01399704
               10  CK-LBL-SEQ-NBR         PIC S9(4)  COMP   VALUE +0.   01399804
                                                                        01440000
      *-------------------*                                             01450000
       PROCEDURE DIVISION.                                              01460000
      *-------------------*                                             01470000
                                                                        01480000
       0000-MLKUP056.                                                   01490000
                                                                        01500000
           PERFORM 1000-INITIALIZATION.                                 01510000
      *                                                                 01520000
           PERFORM 2000-PROCESS-INPUT                                   01530000
                UNTIL PS-EOF-PADJ-DTL-FILE.                             01540000
      *                                                                 01550000
           PERFORM 8000-EOJ-ROUTINE.                                    01560000
                                                                        01570000
           STOP RUN.                                                    01580000
                                                                        01590000
      ******************************************************************01600000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 01610000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      01620000
      ******************************************************************01630000
      *                                                                 01640000
       1000-INITIALIZATION.                                             01650000
      *                                                                 01661008
           OPEN INPUT PADJ-TRN-DTL-FILE                                 01670007
                      CNTL-DATE-FILE.                                   01680007
                                                                        01681007
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         01690000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              01710000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           01720000
               PERFORM 7500-SELECT-RESTART-INFO                         01730000
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    01740000
                                            CR-CHECKPOINT-RESTART-VAR   01750000
               MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY        01760000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                01770000
               DISPLAY 'FSCL WK END DTE ' CR-FSCL-WK-END-DTE            01780000
               DISPLAY 'PRCG DATE       ' CR-PRCG-DTE                   01790000
               MOVE CR-DEPT-NBR           TO PV-DISPLAY-FIELD           01791000
               DISPLAY 'DEPARTMENT NBR  ' PV-DISPLAY-FIELD              01800000
               MOVE CR-MAJ-CL-NBR         TO PV-DISPLAY-FIELD           01800100
               DISPLAY 'MAJOR CLASS     ' PV-DISPLAY-FIELD              01801000
               MOVE CR-SUB-CL-NBR         TO PV-DISPLAY-FIELD           01801100
               DISPLAY 'SUB CLASS       ' PV-DISPLAY-FIELD              01802000
               MOVE CR-LOC-NBR            TO PV-DISPLAY-FIELD           01802100
               DISPLAY 'LOC NBR         ' PV-DISPLAY-FIELD              01803000
               MOVE CR-ML-LO-MDSE-LVL-ID  TO PV-DISPLAY-FIELD           01803100
               DISPLAY 'ML LWST ID      ' PV-DISPLAY-FIELD              01804000
               MOVE CR-ORIG-ENT-ID        TO PV-DISPLAY-FIELD           01804100
               DISPLAY 'ORIG ENT ID     ' PV-DISPLAY-FIELD              01805000
               MOVE CR-ORIG-LBL-SEQ-NBR   TO PV-DISPLAY-FIELD           01805100
               DISPLAY 'ORIG LBL SEQ    ' PV-DISPLAY-FIELD              01806000
               DISPLAY 'DUMMY SKU IND   ' CR-DUMMY-SKU-IND              01807000
               DISPLAY 'FINCL PRC CDE   ' CR-FINCL-PRC-GP-CDE           01808000
               MOVE CR-SRC-SYS-ID         TO PV-DISPLAY-FIELD           01808100
               DISPLAY 'SRC SYS ID      ' PV-DISPLAY-FIELD              01809000
               DISPLAY 'TRN TYP CDE     ' CR-TRN-TYP-CDE                01809100
               DISPLAY 'AP TRN CDE      ' CR-AP-TRN-CDE                 01809200
               DISPLAY 'DR CR CDE       ' CR-DR-CR-CDE                  01809300
               MOVE CR-ENT-ID             TO PV-DISPLAY-FIELD           01809404
               DISPLAY 'ENT-ID          ' PV-DISPLAY-FIELD              01809504
               MOVE CR-LBL-SEQ-NBR        TO PV-DISPLAY-FIELD           01809604
               DISPLAY 'LBL SEQ NBR     ' PV-DISPLAY-FIELD              01809704
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           01810000
           ELSE                                                         01820000
               SET PS-FIRST-COMMIT TO TRUE                              01830000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     01840000
           END-IF.                                                      01850000
      *                                                                 01860000
           PERFORM 5000-READ-DATE-CNTL-FILE                             01861007
           PERFORM 4000-READ-PADJ-DTL-FILE UNTIL                        01870000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     01880000
            OR PS-EOF-PADJ-DTL-FILE.                                    01890000
      *                                                                 01900000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           01910000
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                01920000
                        PA-INPUT-COUNT                                  01930000
               DISPLAY 'FSCL WK END DTE ' ML090-FSCL-WK-END-DTE         01981000
               DISPLAY 'PRCG DATE       ' ML090-PRCG-DTE                01982000
               MOVE ML090-DEPT-NBR        TO PV-DISPLAY-FIELD           01983000
               DISPLAY 'DEPARTMENT NBR  ' PV-DISPLAY-FIELD              01984000
               MOVE ML090-MAJ-CL-NBR      TO PV-DISPLAY-FIELD           01985000
               DISPLAY 'MAJOR CLASS     ' PV-DISPLAY-FIELD              01986000
               MOVE ML090-SUB-CL-NBR      TO PV-DISPLAY-FIELD           01987000
               DISPLAY 'SUB CLASS       ' PV-DISPLAY-FIELD              01988000
               MOVE ML090-LOC-NBR         TO PV-DISPLAY-FIELD           01989000
               DISPLAY 'LOC NBR         ' PV-DISPLAY-FIELD              01989100
               MOVE ML090-ML-LO-MDSE-LVL-ID TO PV-DISPLAY-FIELD         01989200
               DISPLAY 'ML LWST ID      ' PV-DISPLAY-FIELD              01989300
               MOVE ML090-ORIG-ENT-ID     TO PV-DISPLAY-FIELD           01989400
               DISPLAY 'ORIG ENT ID     ' PV-DISPLAY-FIELD              01989500
               MOVE ML090-ORIG-LBL-SEQ-NBR TO PV-DISPLAY-FIELD          01989600
               DISPLAY 'ORIG LBL SEQ    ' PV-DISPLAY-FIELD              01989700
               DISPLAY 'DUMMY SKU IND   ' ML090-DUMMY-SKU-IND           01989800
               DISPLAY 'FINCL PRC CDE   ' ML090-FINCL-PRC-GP-CDE        01989900
               MOVE ML090-SRC-SYS-ID      TO PV-DISPLAY-FIELD           01990000
               DISPLAY 'SRC SYS ID      ' PV-DISPLAY-FIELD              01990100
               DISPLAY 'TRN TYP CDE     ' ML090-TRN-TYP-CDE             01990200
               DISPLAY 'AP TRN TYP      ' ML090-AP-TRN-CDE              01990300
               DISPLAY 'DR CR CDE       ' ML090-DR-CR-CDE               01990400
               MOVE ML090-ENT-ID          TO PV-DISPLAY-FIELD           01990504
               DISPLAY 'ENT-ID          ' PV-DISPLAY-FIELD              01990604
               MOVE ML090-LBL-SEQ-NBR     TO PV-DISPLAY-FIELD           01990704
               DISPLAY 'LBL SEQ NBR     ' PV-DISPLAY-FIELD              01990804
           END-IF.                                                      01991000
                                                                        02000000
           IF PS-EOF-PADJ-DTL-FILE                                      02010000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02020000
                   CONTINUE                                             02030000
               ELSE                                                     02040000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02050000
               END-IF                                                   02060000
           ELSE                                                         02070000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02080000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02090000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02100000
           END-IF.                                                      02110000
                                                                        02120000
      ******************************************************************02130000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02140000
      ******************************************************************02150000
       2000-PROCESS-INPUT.                                              02160000
      *                                                                 02170000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02180000
      *                                                                 02190000
           IF CC-CNTL-DATE = SPACES                                     02190308
             MOVE ML090-PRCG-DTE        TO MLT00039-PRCG-DTE            02190508
           ELSE                                                         02190608
             MOVE CC-CNTL-DATE          TO MLT00039-PRCG-DTE            02190808
           END-IF                                                       02190908
                                                                        02191010
           PERFORM 7200-INSERT-MLT00039-ROW.                            02200012
      *                                                                 02360000
           IF PS-PROGRAM-DEADLOCKED                                     02370000
               CONTINUE                                                 02380000
           ELSE                                                         02390000
               PERFORM 7700-COMMIT-PROCESSING                           02400000
               PERFORM 4000-READ-PADJ-DTL-FILE                          02410000
           END-IF.                                                      02420000
      *                                                                 02430000
      ******************************************************************02820000
      * READS THE CONDENSED FILE INTO THE MLT_PADJ_TRN_DTL LAYOUT       02830000
      ******************************************************************02840000
       4000-READ-PADJ-DTL-FILE.                                         02850000
           READ PADJ-TRN-DTL-FILE INTO ML090-AUDIT-REC                  02860000
              AT END                                                    02870000
                MOVE 'Y' TO PS-EOF-PADJ-DTL-FILE-SW.                    02880000
      *                                                                 02890000
           IF PS-EOF-PADJ-DTL-FILE                                      02900000
               IF PA-INPUT-COUNT=0                                      02910019
                  INITIALIZE ML090-AUDIT-REC                            02920019
               END-IF                                                   02930019
               MOVE ML090-FSCL-WK-END-DTE     TO CR-FSCL-WK-END-DTE     02941000
               MOVE ML090-PRCG-DTE            TO CR-PRCG-DTE            02942000
               MOVE ML090-DEPT-NBR            TO CR-DEPT-NBR            02943000
               MOVE ML090-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          02945000
               MOVE ML090-SUB-CL-NBR          TO CR-SUB-CL-NBR          02947000
               MOVE ML090-LOC-NBR             TO CR-LOC-NBR             02949000
               MOVE ML090-ML-LO-MDSE-LVL-ID   TO CR-ML-LO-MDSE-LVL-ID   02949200
               MOVE ML090-ORIG-ENT-ID         TO CR-ORIG-ENT-ID         02949400
               MOVE ML090-ORIG-LBL-SEQ-NBR    TO CR-ORIG-LBL-SEQ-NBR    02949600
               MOVE ML090-DUMMY-SKU-IND       TO CR-DUMMY-SKU-IND       02949800
               MOVE ML090-FINCL-PRC-GP-CDE    TO CR-FINCL-PRC-GP-CDE    02950000
               MOVE ML090-SRC-SYS-ID          TO CR-SRC-SYS-ID          02950100
               MOVE ML090-TRN-TYP-CDE         TO CR-TRN-TYP-CDE         02950200
               MOVE ML090-AP-TRN-CDE          TO CR-AP-TRN-CDE          02950300
               MOVE ML090-DR-CR-CDE           TO CR-DR-CR-CDE           02950400
               MOVE ML090-ENT-ID              TO CR-ENT-ID              02950504
               MOVE ML090-LBL-SEQ-NBR         TO CR-LBL-SEQ-NBR         02950604
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    02951000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  02960000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       02970000
                                             TCKRSTINF-WORK-STRG-DESC   02980000
               EXEC SQL                                                 02990000
                 UPDATE TCKRSTINF                                       03000000
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03010000
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03020000
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03030000
               END-EXEC                                                 03040000
               IF SQLCODE = 0                                           03050000
                   CONTINUE                                             03060000
               ELSE                                                     03070000
                   MOVE PE-MSG-05          TO PV-OPERATION-ATTEMPTED    03080000
                   MOVE '* 4000-READ-PADJ-DTL  *' TO PV-PARAGRAPH-NAME  03090000
                   PERFORM 9999-SQL-ABEND                               03100000
               END-IF                                                   03110000
            ELSE                                                        03120000
                ADD 1                        TO PA-INPUT-COUNT          03130000
            END-IF.                                                     03140000
      *                                                                 03150000
      ******************************************************************03151007
      * READ DATE CONTROL FILE                                          03152007
      ******************************************************************03154007
       5000-READ-DATE-CNTL-FILE.                                        03155007
           READ CNTL-DATE-FILE INTO CC-CONTROL-CARD                     03156007
               AT END                                                   03157007
                   MOVE SPACES TO CC-CONTROL-CARD                       03158007
                   SET PS-END-OF-CNTL-DTE-FILE TO TRUE                  03158107
                   DISPLAY  'PROCESSING DATE CONTROL CARD EMPTY'.       03159010
           IF NOT PS-END-OF-CNTL-DTE-FILE                               03159107
               PERFORM 5001-VALIDATE-CNTL-DATE                          03159207
           END-IF.                                                      03159307
      *                                                                 03159407
      ******************************************************************03159507
      * VALIDATE CONTROL DATE                                           03159607
      ******************************************************************03159707
       5001-VALIDATE-CNTL-DATE.                                         03159807
                  EXEC SQL                                              03159907
                     SELECT 'Y'                                         03160007
                       INTO :PV-DB2-DATE-EXISTS                         03160107
                       FROM TDTATTR                                     03160207
                      WHERE KY_DTE = :CC-CNTL-DATE                      03160308
                  END-EXEC                                              03160407
                                                                        03160507
                  EVALUATE TRUE                                         03160607
                      WHEN SQLCODE = 0                                  03160707
                           CONTINUE                                     03160807
                      WHEN SQLCODE = 100                                03160907
                           DISPLAY 'INVALID DATE IN CONTROL CARD'       03161007
                           MOVE SPACES TO CC-CNTL-DATE                  03161107
                      WHEN OTHER                                        03161207
                           CONTINUE                                     03161307
                           MOVE '5001-VALIDATE-CNTL-DATE' TO            03161407
                                              PV-PARAGRAPH-NAME         03161507
                           MOVE PE-MSG-08 TO PV-OPERATION-ATTEMPTED     03161607
                           PERFORM 9999-SQL-ABEND                       03162207
                  END-EVALUATE.                                         03162308
      *                                                                 03770000
      ***************************************************************** 03780000
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON KEY. CURRENT ROW 03790015
      * AMOUNT FIELDS UPDATED TO ADD VALUES FROM INPUT REC.             03800016
      ***************************************************************** 03820000
       7100-UPDATE-MLT00039-ROW.                                        03830000
      *                                                                 03840012
           EXEC SQL                                                     03870000
               UPDATE MLT_PADJ_TRN_DTL                                  03880000
                   SET PADJ_GRO_COST_AMT  = PADJ_GRO_COST_AMT +         03920012
                                             :ML090-PADJ-GRO-COST-AMT   03930012
                     , PADJ_NET_COST_AMT  = PADJ_NET_COST_AMT +         03940012
                                             :ML090-PADJ-NET-COST-AMT   03950012
                     , PADJ_RTL_AMT       = PADJ_RTL_AMT      +         03960012
                                             :ML090-PADJ-RTL-AMT        03970012
                     , PADJ_DISC_COST_AMT = PADJ_DISC_COST_AMT +        03980017
                                             :ML090-PADJ-DISC-COST-AMT  03990017
              WHERE   FSCL_WK_END_DTE     = :ML090-FSCL-WK-END-DTE      04061008
                AND   PRCG_DTE            = :MLT00039-PRCG-DTE          04062008
                AND   DEPT_NBR            = :ML090-DEPT-NBR             04063008
                AND   MAJ_CL_NBR          = :ML090-MAJ-CL-NBR           04064008
                AND   SUB_CL_NBR          = :ML090-SUB-CL-NBR           04065008
                AND   LOC_NBR             = :ML090-LOC-NBR              04066008
                AND   ML_LO_MDSE_LVL_ID   = :ML090-ML-LO-MDSE-LVL-ID    04067008
                AND   ORIG_ENT_ID         = :ML090-ORIG-ENT-ID          04068008
                AND   ORIG_LBL_SEQ_NBR    = :ML090-ORIG-LBL-SEQ-NBR     04069008
                AND   DMY_SKU_IND         = :ML090-DUMMY-SKU-IND        04069108
                AND   FINCL_PRC_GP_CDE    = :ML090-FINCL-PRC-GP-CDE     04069208
                AND   SRC_SYS_ID          = :ML090-SRC-SYS-ID           04069308
                AND   TRN_TYP_CDE         = :ML090-TRN-TYP-CDE          04069408
                AND   AP_TRN_CDE          = :ML090-AP-TRN-CDE           04069508
                AND   DRCR_CDE            = :ML090-DR-CR-CDE            04069608
                AND   ENT_ID              = :ML090-ENT-ID               04069708
                AND   LBL_SEQ_NBR         = :ML090-LBL-SEQ-NBR          04069808
           END-EXEC                                                     04070000
      *                                                                 04080000
           EVALUATE SQLCODE                                             04090000
               WHEN 0                                                   04100000
                   ADD 1 TO PA-UPDATE-COUNT                             04110012
               WHEN -911                                                04120000
                   ADD +1             TO PV-DEADLOCK-COUNT              04130000
                   DISPLAY '*** -911 OCCURRED ON UPDATE ***'            04140000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04150000
                       MOVE '7100-UPDATE-MLT00039-ROW' TO               04160000
                                                    PV-PARAGRAPH-NAME   04170000
                       PERFORM 9000-DEADLOCK-ERROR                      04180000
                   ELSE                                                 04190000
                       PERFORM 7999-RESTART-PROCESS                     04200000
                   END-IF                                               04210000
               WHEN OTHER                                               04220000
                   MOVE '7100-UPDATE-MLT00039-ROW' TO PV-PARAGRAPH-NAME 04230000
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED04240000
                   PERFORM 9999-SQL-ABEND                               04250000
           END-EVALUATE.                                                04260000
      *                                                                 04270000
      ***************************************************************** 04300000
      * THERE WAS NO MATCHING ROW ON THE MONTHLY DTL TABLE. INITIAL   * 04310000
      * ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW.             * 04320000
      ***************************************************************** 04340000
       7200-INSERT-MLT00039-ROW.                                        04350000
      *                                                                 04360000
           EXEC SQL                                                     04370000
               INSERT INTO MLT_PADJ_TRN_DTL                             04380000
                    (  FSCL_WK_END_DTE                                  04390000
                     , PRCG_DTE                                         04400000
                     , DEPT_NBR                                         04410000
                     , MAJ_CL_NBR                                       04420000
                     , SUB_CL_NBR                                       04430000
                     , LOC_NBR                                          04440000
                     , ML_LO_MDSE_LVL_ID                                04450000
                     , ORIG_ENT_ID                                      04460000
                     , ORIG_LBL_SEQ_NBR                                 04470000
                     , DMY_SKU_IND                                      04480000
                     , FINCL_PRC_GP_CDE                                 04490000
                     , SRC_SYS_ID                                       04500000
                     , TRN_TYP_CDE                                      04510000
                     , AP_TRN_CDE                                       04520000
                     , DRCR_CDE                                         04530000
                     , ENT_ID                                           04540000
                     , LBL_SEQ_NBR                                      04550000
                     , PADJ_GRO_COST_AMT                                04560000
                     , PADJ_NET_COST_AMT                                04570000
                     , PADJ_RTL_AMT                                     04580017
                     , PADJ_DISC_COST_AMT)                              04590018
               VALUES                                                   04740000
                   (   :ML090-FSCL-WK-END-DTE                           04750000
                     , :MLT00039-PRCG-DTE                               04760008
                     , :ML090-DEPT-NBR                                  04770000
                     , :ML090-MAJ-CL-NBR                                04780000
                     , :ML090-SUB-CL-NBR                                04790000
                     , :ML090-LOC-NBR                                   04800000
                     , :ML090-ML-LO-MDSE-LVL-ID                         04810000
                     , :ML090-ORIG-ENT-ID                               04820000
                     , :ML090-ORIG-LBL-SEQ-NBR                          04830000
                     , :ML090-DUMMY-SKU-IND                             04840000
                     , :ML090-FINCL-PRC-GP-CDE                          04850000
                     , :ML090-SRC-SYS-ID                                04860000
                     , :ML090-TRN-TYP-CDE                               04870000
                     , :ML090-AP-TRN-CDE                                04880000
                     , :ML090-DR-CR-CDE                                 04890000
                     , :ML090-ENT-ID                                    04891000
                     , :ML090-LBL-SEQ-NBR                               04892000
                     , :ML090-PADJ-GRO-COST-AMT                         04893000
                     , :ML090-PADJ-NET-COST-AMT                         04894000
                     , :ML090-PADJ-RTL-AMT                              04895017
                     , :ML090-PADJ-DISC-COST-AMT)                       04896017
           END-EXEC                                                     05100000
      *                                                                 05110000
           EVALUATE SQLCODE                                             05120000
               WHEN 0                                                   05130000
                   ADD 1 TO PA-INSERT-COUNT                             05140012
               WHEN -803                                                05141012
                   PERFORM 7100-UPDATE-MLT00039-ROW                     05143012
               WHEN -911                                                05150000
                   ADD +1             TO PV-DEADLOCK-COUNT              05160000
                   DISPLAY '*** -911 OCCURRED ON INSERT ***'            05170000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05180000
                       MOVE '7200-INSERT-MLT00039-ROW' TO               05190000
                                                    PV-PARAGRAPH-NAME   05200000
                       PERFORM 9000-DEADLOCK-ERROR                      05210000
                   ELSE                                                 05220000
                       PERFORM 7999-RESTART-PROCESS                     05230000
                   END-IF                                               05240000
               WHEN OTHER                                               05250000
                   MOVE '7200-INSERT-MLT00039-ROW' TO PV-PARAGRAPH-NAME 05260000
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED05270000
                   PERFORM 9999-SQL-ABEND                               05280000
           END-EVALUATE.                                                05290000
                                                                        05300000
      ******************************************************************05330000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05340000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05350000
      *            CONTROL TABLE                                       *05360000
      ******************************************************************05370000
       7300-GET-COMMIT-TIMESTAMP.                                       05380000
                                                                        05390000
           EXEC SQL                                                     05400000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        05410000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05420000
               INTO :PV-COMMIT-TIMESTAMP                                05430000
               FROM TCKRSTCNTL                                          05440000
              WHERE JOB_NAME  = :PC-JOB-NAME                            05450000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05460000
           END-EXEC.                                                    05470000
                                                                        05480000
           EVALUATE TRUE                                                05490000
               WHEN SQLCODE = 0                                         05500000
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  05510000
                   CONTINUE                                             05520000
               WHEN SQLCODE NOT EQUAL ZERO                              05530000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME05540000
                   PERFORM 9999-SQL-ABEND                               05550000
           END-EVALUATE.                                                05560000
      *                                                                 05570000
      *                                                                 05580000
      ******************************************************************05590000
      * 7400-INIT-CHECKPOINT-SELECT                                    *05600000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05610000
      *            IF WE ARE IN A RESTART CONDITION.                   *05620000
      ******************************************************************05630000
       7400-INIT-CHECKPOINT-SELECT.                                     05640000
           EXEC SQL                                                     05660000
             SELECT  CKPT_STAT_CODE                                     05670000
                    ,CKPT_FRQNCY_QTY                                    05680000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         05690000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05700000
               FROM  TCKRSTCNTL                                         05710000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05720000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05730000
           END-EXEC.                                                    05740000
                                                                        05750000
           IF SQLCODE = 0                                               05760000
               CONTINUE                                                 05770000
           ELSE                                                         05780000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  05790000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     05800000
               PERFORM 9999-SQL-ABEND                                   05810000
           END-IF.                                                      05820000
      *                                                                 05830000
      *                                                                 05840000
      ******************************************************************05850000
      * 7500-SELECT-RESTART-INFO                                       *05860000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *05870000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *05880000
      ******************************************************************05890000
       7500-SELECT-RESTART-INFO.                                        05900000
           EXEC SQL                                                     05920000
             SELECT  WORK_STRG_DESC                                     05930000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          05940000
               FROM  TCKRSTINF                                          05950000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05960000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05970000
           END-EXEC.                                                    05980000
                                                                        05990000
           IF SQLCODE = 0                                               06000000
               CONTINUE                                                 06010000
           ELSE                                                         06020000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   06030000
               PERFORM 9999-SQL-ABEND                                   06040000
           END-IF.                                                      06050000
                                                                        06060000
      ******************************************************************06070000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *06080000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *06090000
      ******************************************************************06100000
       7600-SET-CURRENT-TIMESTAMP.                                      06110000
                                                                        06120000
           EXEC SQL                                                     06130000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           06140000
           END-EXEC.                                                    06150000
                                                                        06160000
           IF SQLCODE = 0                                               06170000
               CONTINUE                                                 06180000
           ELSE                                                         06190000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 06200000
               PERFORM 9999-SQL-ABEND                                   06210000
           END-IF.                                                      06220000
      *                                                                 06230000
      *                                                                 06240000
      ******************************************************************06250000
      * 7700-COMMIT-PROCESSING.                                        *06260000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *06270000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*06280000
      ******************************************************************06290000
       7700-COMMIT-PROCESSING.                                          06300000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06310000
                                                                        06320000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06330000
                                                                        06340000
      * PREPARE COMMIT RECORD FOR UPDATE                                06350000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                06360000
               MOVE ML090-FSCL-WK-END-DTE     TO CR-FSCL-WK-END-DTE     06371000
               MOVE ML090-PRCG-DTE            TO CR-PRCG-DTE            06372000
               MOVE ML090-DEPT-NBR            TO CR-DEPT-NBR            06373000
               MOVE ML090-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          06374000
               MOVE ML090-SUB-CL-NBR          TO CR-SUB-CL-NBR          06375000
               MOVE ML090-LOC-NBR             TO CR-LOC-NBR             06376000
               MOVE ML090-ML-LO-MDSE-LVL-ID   TO CR-ML-LO-MDSE-LVL-ID   06377000
               MOVE ML090-ORIG-ENT-ID         TO CR-ORIG-ENT-ID         06378000
               MOVE ML090-ORIG-LBL-SEQ-NBR    TO CR-ORIG-LBL-SEQ-NBR    06379000
               MOVE ML090-DUMMY-SKU-IND       TO CR-DUMMY-SKU-IND       06379100
               MOVE ML090-FINCL-PRC-GP-CDE    TO CR-FINCL-PRC-GP-CDE    06379200
               MOVE ML090-SRC-SYS-ID          TO CR-SRC-SYS-ID          06379300
               MOVE ML090-TRN-TYP-CDE         TO CR-TRN-TYP-CDE         06379400
               MOVE ML090-AP-TRN-CDE          TO CR-AP-TRN-CDE          06379500
               MOVE ML090-DR-CR-CDE           TO CR-DR-CR-CDE           06379600
               MOVE ML090-ENT-ID              TO CR-ENT-ID              06379704
               MOVE ML090-LBL-SEQ-NBR         TO CR-LBL-SEQ-NBR         06379804
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    06410000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  06420000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       06430000
                                             TCKRSTINF-WORK-STRG-DESC   06440000
               IF PS-FIRST-COMMIT                                       06450000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                06460000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                06470000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       06480000
               END-IF                                                   06490000
               PERFORM 7800-COMMIT-CHANGES                              06500000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        06510000
           END-IF.                                                      06520000
      *                                                                 06530000
      *                                                                 06540000
      ******************************************************************06550000
      * 7800-COMMIT-CHANGES.                                            06560000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      06570000
      *            TAKE A COMMIT.                                       06580000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    06590000
      ******************************************************************06600000
       7800-COMMIT-CHANGES.                                             06610000
                                                                        06620000
           EXEC SQL                                                     06630000
             UPDATE TCKRSTINF                                           06640000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          06650000
              WHERE JOB_NAME       = :PC-JOB-NAME                       06660000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   06670000
           END-EXEC.                                                    06680000
                                                                        06690000
           IF SQLCODE = 0                                               06700000
               CONTINUE                                                 06710000
           ELSE                                                         06720000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED06730000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06740000
               PERFORM 9999-SQL-ABEND                                   06750000
           END-IF.                                                      06760000
                                                                        06770000
           EXEC SQL                                                     06780000
               COMMIT                                                   06790000
           END-EXEC.                                                    06800000
                                                                        06810000
           IF SQLCODE = 0                                               06820000
               ADD 1 TO PA-COMMIT-COUNT                                 06830000
           ELSE                                                         06840000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED06850000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06860000
               PERFORM 9999-SQL-ABEND                                   06870000
           END-IF.                                                      06880000
      *                                                                 06890000
      ******************************************************************06900000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   06910000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   06920000
      ******************************************************************06930000
       7900-UPDATE-CHECKPOINT-STATUS.                                   06940000
                                                                        06950000
           EXEC SQL                                                     06960000
             UPDATE  TCKRSTCNTL                                         06970000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        06980000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06990000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07000000
           END-EXEC.                                                    07010000
                                                                        07020000
           IF SQLCODE = 0                                               07030000
               CONTINUE                                                 07040000
           ELSE                                                         07050000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME07060000
               PERFORM 9999-SQL-ABEND                                   07070000
           END-IF.                                                      07080000
                                                                        07090000
           EJECT                                                        07100000
      *                                                                 07110000
      *                                                                 07120000
      ******************************************************************07130000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST MLT_PADJ_TRN_DTL      07140000
      * ROW FOR UPDATE.  FIND RESTART RECORD AND CKPT RESTART AT POINT  07150000
      * OF LAST COMMIT.                                                 07160000
      ******************************************************************07170000
       7999-RESTART-PROCESS.                                            07180000
      *                                                                 07190000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           07200000
      *                                                                 07210000
           CLOSE PADJ-TRN-DTL-FILE.                                     07220000
      *                                                                 07230000
           PERFORM 7500-SELECT-RESTART-INFO.                            07240000
      *                                                                 07250000
           DISPLAY '**** RESTART **** '.                                07260000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 07270000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   07280000
                                                                        07290000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      07300000
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        07310000
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       07320000
           IF PS-FIRST-COMMIT                                           07330000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:76)               07340005
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     07350000
                      ' BEGINNING'                                      07360000
           ELSE                                                         07370000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           07380000
                        TCKRSTINF-WORK-STRG-DESC (1:76)                 07390005
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 07400000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         07410000
                    CR-CHECKPOINT-RESTART-AREA                          07420000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                07430000
               DISPLAY 'FSCL WK END DTE ' CR-FSCL-WK-END-DTE            07440001
               DISPLAY 'PRCG DATE       ' CR-PRCG-DTE                   07441001
               MOVE CR-DEPT-NBR           TO PV-DISPLAY-FIELD           07442001
               DISPLAY 'DEPARTMENT NBR  ' PV-DISPLAY-FIELD              07443001
               MOVE CR-MAJ-CL-NBR         TO PV-DISPLAY-FIELD           07444001
               DISPLAY 'MAJOR CLASS     ' PV-DISPLAY-FIELD              07445001
               MOVE CR-SUB-CL-NBR         TO PV-DISPLAY-FIELD           07446001
               DISPLAY 'SUB CLASS       ' PV-DISPLAY-FIELD              07447001
               MOVE CR-LOC-NBR            TO PV-DISPLAY-FIELD           07448001
               DISPLAY 'LOC NBR         ' PV-DISPLAY-FIELD              07449001
               MOVE CR-ML-LO-MDSE-LVL-ID  TO PV-DISPLAY-FIELD           07449101
               DISPLAY 'ML LWST ID      ' PV-DISPLAY-FIELD              07449201
               MOVE CR-ORIG-ENT-ID        TO PV-DISPLAY-FIELD           07449301
               DISPLAY 'ORIG ENT ID     ' PV-DISPLAY-FIELD              07449401
               MOVE CR-ORIG-LBL-SEQ-NBR   TO PV-DISPLAY-FIELD           07449501
               DISPLAY 'ORIG LBL SEQ    ' PV-DISPLAY-FIELD              07449601
               DISPLAY 'DUMMY SKU IND   ' CR-DUMMY-SKU-IND              07449701
               DISPLAY 'FINCL PRC CDE   ' CR-FINCL-PRC-GP-CDE           07449801
               MOVE CR-SRC-SYS-ID         TO PV-DISPLAY-FIELD           07449901
               DISPLAY 'SRC SYS ID      ' PV-DISPLAY-FIELD              07450001
               DISPLAY 'TRN TYP CDE     ' CR-TRN-TYP-CDE                07451001
               DISPLAY 'AP TRN CDE      ' CR-AP-TRN-CDE                 07452001
               DISPLAY 'DR CR CDE       ' CR-DR-CR-CDE                  07453001
               MOVE CR-ENT-ID             TO PV-DISPLAY-FIELD           07454004
               DISPLAY 'ENT-ID          ' PV-DISPLAY-FIELD              07455004
               MOVE CR-LBL-SEQ-NBR        TO PV-DISPLAY-FIELD           07456004
               DISPLAY 'LBL SEQ NBR     ' PV-DISPLAY-FIELD              07457004
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           07470000
           END-IF.                                                      07480000
                                                                        07490000
           OPEN INPUT PADJ-TRN-DTL-FILE.                                07500000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      07510000
      *                                                                 07520000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          07530000
           PERFORM 4000-READ-PADJ-DTL-FILE                              07540000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              07550000
           DISPLAY 'INPUT RECORD RESTARTED ON RECORD '                  07560000
                    PA-INPUT-COUNT.                                     07570000
           DISPLAY 'FSCL WK END DTE '     ML090-FSCL-WK-END-DTE.        07581000
           DISPLAY 'PRCG DATE       '     ML090-PRCG-DTE.               07582000
           MOVE ML090-DEPT-NBR          TO PV-DISPLAY-FIELD.            07583000
           DISPLAY 'DEPARTMENT NBR  '      PV-DISPLAY-FIELD.            07584000
           MOVE ML090-MAJ-CL-NBR        TO PV-DISPLAY-FIELD.            07585000
           DISPLAY 'MAJOR CLASS     '      PV-DISPLAY-FIELD.            07586000
           MOVE ML090-SUB-CL-NBR        TO PV-DISPLAY-FIELD.            07587000
           DISPLAY 'SUB CLASS       '      PV-DISPLAY-FIELD.            07588000
           MOVE ML090-LOC-NBR           TO PV-DISPLAY-FIELD.            07589000
           DISPLAY 'LOC NBR         '      PV-DISPLAY-FIELD.            07589100
           MOVE ML090-ML-LO-MDSE-LVL-ID TO PV-DISPLAY-FIELD.            07589200
           DISPLAY 'ML LWST ID      '      PV-DISPLAY-FIELD.            07589300
           MOVE ML090-ORIG-ENT-ID       TO PV-DISPLAY-FIELD.            07589400
           DISPLAY 'ORIG ENT ID     '      PV-DISPLAY-FIELD.            07589500
           MOVE ML090-ORIG-LBL-SEQ-NBR  TO PV-DISPLAY-FIELD.            07589600
           DISPLAY 'ORIG LBL SEQ    '      PV-DISPLAY-FIELD.            07589700
           DISPLAY 'DUMMY SKU IND   '      ML090-DUMMY-SKU-IND.         07589800
           DISPLAY 'FINCL PRC CDE   '      ML090-FINCL-PRC-GP-CDE.      07589900
           MOVE ML090-SRC-SYS-ID        TO PV-DISPLAY-FIELD.            07590000
           DISPLAY 'SRC SYS ID      '      PV-DISPLAY-FIELD.            07590100
           DISPLAY 'TRN TYP CDE     '      ML090-TRN-TYP-CDE.           07590200
           DISPLAY 'AP TRN CDE      '      ML090-AP-TRN-CDE.            07590300
           DISPLAY 'DR CR CDE       '      ML090-DR-CR-CDE.             07590400
           MOVE ML090-ENT-ID            TO PV-DISPLAY-FIELD.            07590504
           DISPLAY 'ENT-ID          '      PV-DISPLAY-FIELD.            07590604
           MOVE ML090-LBL-SEQ-NBR       TO PV-DISPLAY-FIELD.            07590704
           DISPLAY 'LBL SEQ NBR     '      PV-DISPLAY-FIELD.            07590804
      ******************************************************************07630000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *07640000
      ******************************************************************07650000
       8000-EOJ-ROUTINE.                                                07660000
      *                                                                 07670000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       07680000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       07690000
                                                                        07700000
      *                                                                 07710000
           DISPLAY SPACES.                                              07720000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         07730000
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        07740000
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        07750000
           DISPLAY SPACES.                                              07760000
      *                                                                 07770000
           CLOSE PADJ-TRN-DTL-FILE.                                     07780000
      *                                                                 07790000
      *                                                                 07800000
      ******************************************************************07810000
      *  PERFORMED BY 7100-UPDATE AND 7200-INSERT                       07820000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   07830000
      ******************************************************************07840000
       9000-DEADLOCK-ERROR.                                             07850000
      *                                                                 07860000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     07870000
               PERFORM 9999-SQL-ABEND.                                  07880000
      *                                                                 07890000
      ******************************************************************07900000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               07910000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             07920000
      ******************************************************************07930000
       9999-SQL-ABEND.                                                  07940000
                                                                        07950000
           MOVE +4013                 TO ABEND-CODE.                    07960000
           DISPLAY '**  ABEND     **'.                                  07970000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              07980000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            07990000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       08000000
                                                                        08010000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         08020000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        08030000
                                                                        08040000
           COPY DPPD004.                                                08050000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           08060000
