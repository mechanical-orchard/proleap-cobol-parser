       IDENTIFICATION DIVISION.                                         00010015
       PROGRAM-ID.    ICKUP054.                                         00020015
       AUTHOR.        R. KELLEN.                                        00030015
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040015
       DATE-WRITTEN.  02/01/2008.                                       00050015
       DATE-COMPILED.                                                   00060015
                                                                        00070015
      ***************************************************************** 00080015
      * ICKUP054 - UPDATE ICT_SKU_LOC_CNT AND ICT_LOC_CNT_STAT TABLES   00090015
      ***************************************************************** 00100015
      *        THIS PROGRAM WILL READ A FILE CONTAINING ACTIVE AND      00110045
      *        CLEARANCE COUNT/LOC/SKU DATA AND ADD THESE TO THE        00120015
      *        ICT_SKU_LOC_CNT AND ICT_LOC_CNT_STAT TABLES. IT WILL ALSO00130015
      *        READ A FILE OF CANCELLED IC COUNTS AND ADD A CANCELLED   00140015
      *        ROW TO THE ICT_LOC_CNT_STAT TABLE.                       00150015
      *                                                                 00160015
      ***************************************************************** 00170015
      *                                                                 00180015
      *  TABLES UPDATED ARE:                                            00190015
      *      ICT_LOC_CNT_STAT - IC COUNT EXECUTION STATUS TABLE         00200015
      *      ICT_SKU_LOC_CNT  - IC COUNT EXECUTION TABLE                00210015
      *  FILES READ ARE:                                                00220015
      *      NONE                                                       00230015
      ***************************************************************** 00240015
      * 02/02/06 - R KELLEN         -  WR NO. WR30388                   00250015
      *    - NEW PROGRAM.                                               00260015
      ***************************************************************** 00270015
      * 04/07/10 - COGNIZANT        -  PKE#2823                         00272084
      *    - CODE CHANGE TO HANDLE CANCELLED COUNT ISSUE                00273084
      *    - REMOVED THE 'NOT' IN THE QUERY IN THE PARAGRAPH            00273185
      *    - 8530-CHECK-COMP-CANC-COUNT TO ONLY PICK UP THE COUNTS IN   00273285
      *    - CAN OR COM STATUS AND BYPASS THE INSERT.                   00273385
      *    - IF THE COUNT IS NOT IN CAN OR COM STATUS , CAN STATUS      00273485
      *    - WILL BE UPDATED.                                           00273585
      ***************************************************************** 00274084
C82402* 05/21/14 - COGNIZANT        -  CHG0082402                       00275099
C82402* PERFORMANCE IMPROVEMENT CHANGES                                 00276099
C82402*    - INTRODUCED MULTI ROW INSERTS TO ICT_SKU_LOC_CNT TABLE.     00277099
C82402*    - FIND THE TAG C82402 FOR CHANGES.                           00279299
C82402***************************************************************** 00279499
                                                                        00280015
       ENVIRONMENT DIVISION.                                            00290015
       CONFIGURATION SECTION.                                           00300015
       SOURCE-COMPUTER. IBM-3090.                                       00310015
       OBJECT-COMPUTER. IBM-3090.                                       00320015
       INPUT-OUTPUT SECTION.                                            00330015
                                                                        00340015
       FILE-CONTROL.                                                    00350015
           SELECT CNT-SKU-EXP-FILE           ASSIGN TO INP01.           00360044
                                                                        00370015
           SELECT CNT-CAN-COUNT-FILE         ASSIGN TO INP02.           00380044
                                                                        00390015
           SELECT CONTROL-FILE-1             ASSIGN TO CTL01.           00400015
                                                                        00410015
      ***************************************************************** 00420015
       DATA DIVISION.                                                   00430015
      ***************************************************************** 00440015
       FILE SECTION.                                                    00450015
                                                                        00460015
       FD  CNT-SKU-EXP-FILE                                             00470044
           LABEL RECORDS ARE STANDARD                                   00480015
           RECORDING MODE IS F                                          00490015
           BLOCK CONTAINS 0 RECORDS                                     00500015
           DATA RECORD IS CNT-ACT-SKU-EXP-REC.                          00510015
       01  CNT-SKU-EXP-REC        PIC  X(53).                           00520044
                                                                        00530015
       FD  CNT-CAN-COUNT-FILE                                           00540015
           LABEL RECORDS ARE STANDARD                                   00550015
           RECORDING MODE IS F                                          00560015
           BLOCK CONTAINS 0 RECORDS                                     00570015
           DATA RECORD IS CNT-CAN-COUNT-REC.                            00580015
       01  CNT-CAN-COUNT-REC          PIC  X(14).                       00590024
                                                                        00600015
       FD  CONTROL-FILE-1                                               00610015
           LABEL RECORDS ARE STANDARD                                   00620015
           RECORDING MODE IS F                                          00630015
           BLOCK CONTAINS 0 RECORDS                                     00640015
           DATA RECORD IS CONTROL-FILE-1-REC.                           00650015
       01  CONTROL-FILE-1-REC.                                          00660015
           05  CTL1-JOB-NAME          PIC X(8).                         00670015
           05  FILLER                 PIC X(72).                        00680015
                                                                        00710015
       WORKING-STORAGE SECTION.                                         00720015
      ******************************************************************00730015
      * PC PROGRAM CONSTANTS                                           *00740015
      ******************************************************************00750015
       01  PC-PROGRAM-CONSTANTS.                                        00760015
           05  PC-PROGRAM-NAME                PIC X(08)    VALUE        00770015
                                                 'ICKUP054'.            00780015
           05  PC-JOB-NAME               PIC X(08)     VALUE 'ICK054  '.00790020
           05  PC-MAXIMUM-RETRY-COUNT         PIC S9(04)   VALUE +2     00800015
                                                           COMP SYNC.   00810015
           05  PC-DEFAULT-TMST                PIC X(26)    VALUE        00820015
               '2007-01-01-00.00.00.000000'.                            00830015
                                                                        00840015
      ******************************************************************00850015
      * PV PROGRAM VARIABLES                                           *00860015
      ******************************************************************00870015
       01  PV-WORK-VARIABLES.                                           00880015
           05  PV-RETRIES                     PIC S9(04)   VALUE +0     00890015
                                                           COMP.        00900015
           05  PV-PARAGRAPH-NAME              PIC  X(30)   VALUE SPACES.00910015
           05  PV-SEQ-NBR                     PIC  S9(4) COMP VALUE 0.  00920015
           05  PV-HOLD-LOC-NBR                PIC  S9(4) COMP VALUE 0.  00921051
           05  PV-HOLD-CNT-STUP-ID            PIC  S9(9) COMP VALUE 0.  00922051
           05  PV-COMPLETED-COUNT-FLG         PIC  X       VALUE SPACES.00930015
           05  PV-DB2-OPERATION-ATTEMPTED     PIC  X(30)   VALUE SPACES.00940015
           05  PV-SQLCODE-DISPLAY             PIC +(8)9    VALUE ZEROES.00950000
           05  PV-COMP-CAN-COUNT-SW           PIC X        VALUE SPACE. 00951081
               88 PV-COMP-CAN-COUNT           VALUE 'Y'.                00952081
           05  PV-STD-TM-ZN-CDE-NI            PIC S9(04)  COMP          00960033
                                                         VALUE ZERO.    00970015
               88 PV-STD-TM-ZN-CDE-IS-NULL    VALUE -1.                 00980033
           05  PV-DST-IND-NI                  PIC S9(04)  COMP          00990033
                                                         VALUE ZERO.    01000033
               88 PV-DST-IND-IS-NULL          VALUE -1.                 01010033
           05  PV-IN-TRNST-UNT-QTY-NI         PIC S9(04)  COMP          01050015
                                                         VALUE ZERO.    01060015
               88 PV-IN-TRNST-UNT-QTY-NULL    VALUE -1.                 01070015
           05  PV-ACPTD-TRUK-UNT-QTY-NI       PIC S9(04)  COMP          01080015
                                                         VALUE ZERO.    01090015
               88 PV-ACPTD-TRUK-UNT-QTY-IS-NULL VALUE -1.               01100015
           05  PV-RTL-SAL-UNT-QTY-NI          PIC S9(04)  COMP          01110015
                                                         VALUE ZERO.    01120015
               88 RTL-SAL-UNT-QTY-IS-NULL     VALUE -1.                 01130015
           05  PV-RTL-RET-UNT-QTY-NI          PIC S9(04)  COMP          01140015
                                                         VALUE ZERO.    01150015
               88 RTL-RET-UNT-QTY-IS-NULL     VALUE -1.                 01160015
           05  PV-XFER-IN-UNT-QTY-NI          PIC S9(04)  COMP          01170015
                                                         VALUE ZERO.    01180015
               88 RTL-XFER-IN-UNT-QTY-IS-NULL VALUE -1.                 01190015
           05  PV-XFER-OUT-UNT-QTY-NI         PIC S9(04)  COMP          01200015
                                                         VALUE ZERO.    01210015
               88 RTL-XFER-OUT-UNT-QTY-IS-NULL VALUE -1.                01220015
           05  PV-SCND-UPC-NBR-NI              PIC S9(04)  COMP         01230015
                                                         VALUE ZERO.    01240015
               88 RTL-SCND-UPC-NBR-IS-NULL     VALUE -1.                01250015
           05  PV-SCND-UNT-QTY-NI              PIC S9(04)  COMP         01260015
                                                         VALUE ZERO.    01270015
               88 RTL-SCND-UNT-QTY-IS-NULL     VALUE -1.                01280015
           05  PV-CNT-GP-CDE-NI                PIC S9(04)  COMP         01290015
                                                         VALUE ZERO.    01300015
               88 RTL-CNT-GP-CDE-IS-NULL       VALUE -1.                01310015
           05  PV-MDSE-EXPRN-DTE-NI            PIC S9(04)  COMP         01360015
                                                         VALUE ZERO.    01370015
               88 RTL-MDSE-EXPRN-DTE-IS-NULL   VALUE -1.                01380015
           05  PV-COMMITS-TAKEN                PIC S9(09)  COMP-3       01420015
                                                         VALUE ZERO.    01430015
           05  PV-COMMIT-INTERVAL          PIC S9(09)  COMP-3           01440099
C82402                                                VALUE +50.        01451099
C82402     05  PV-COMMIT-CNTR              PIC S9(09) VALUE +1 COMP-3.  01461099
           05  PV-CURRENT-WEEKDAY          PIC 9.                       01480015
           05  PV-CURRENT-WEEK-NBR         PIC 9.                       01490015
           05  PV-DECREMENT-1ST            PIC 9.                       01500015
           05  PV-CURRENT-DATE             PIC X(10).                   01510015
           05  PV-CURRENT-DATE-PLUS-2      PIC X(10).                   01511071
           05  PV-CURRENT-TMST             PIC X(26).                   01520015
      *    05  PV-LOCATION-TMST            PIC X(26).                   01530015
           05  PV-LAST-RUN-TMST            PIC X(26).                   01540015
           05  PV-LAST-IMM-TMST            PIC X(26).                   01550015
C82402                                                                  01581087
C82402     05  PV-INDX1                    PIC S9(4) COMP VALUE ZERO.   01582099
C82402     05  PV-INDX2                    PIC S9(4) COMP VALUE ZERO.   01582099
C82402     05  PV-CNTR                     PIC S9(4) COMP VALUE ZERO.   01582099
C82402     05  PV-ROW-NUM                  PIC S9(4) COMP VALUE ZERO.   01583099
C82402     05  PV-NUM-CONDNS               PIC S9(9) COMP VALUE ZERO.   01583099
C82402     05  PV-RET-SQLCODE              PIC S9(9) COMP VALUE ZERO.   01583099
C82402     05  PV-I-CNTR                   PIC 9(10) COMP VALUE 0.              
C82402     05  PV-J-CNTR                   PIC 9(10) COMP VALUE 0.              
C82402     05  PV-J-MSD-INS-CNTR           PIC 9(10) COMP VALUE 0.              
                                                                        01590015
      ******************************************************************01600015
      * PS PROGRAM SWITCHES                                            *01610015
      ******************************************************************01620015
       01  PS-PROGRAM-SWITCHES.                                         01630015
           05  PS-END-OF-ACT-INPUT-FL-SW      PIC  X(01)   VALUE 'N'.   01640015
               88  PS-END-OF-ACT-INPUT-FL                  VALUE 'Y'.   01650015
           05  PS-END-OF-CLR-INPUT-FL-SW      PIC  X(01)   VALUE 'N'.   01660015
               88  PS-END-OF-CLR-INPUT-FL                  VALUE 'Y'.   01670015
           05  PS-END-OF-CAN-INPUT-FL-SW      PIC  X(01)   VALUE 'N'.   01680015
               88  PS-END-OF-CAN-INPUT-FL                  VALUE 'Y'.   01690015
           05  PS-NO-MORE-CANC-CNT-SW         PIC  X(01)   VALUE 'N'.   01700015
               88  PS-NO-MORE-CANC-CNT                     VALUE 'Y'.   01710015
               88  PS-MORE-CANC-CNT                        VALUE 'N'.   01720015
           05  PS-NO-MORE-TMST-UPD-SW         PIC  X(01)   VALUE 'N'.   01730030
               88  PS-NO-MORE-TMST-UPD                     VALUE 'Y'.   01740030
               88  PS-MORE-TMST-UPD                        VALUE 'N'.   01750030
           05  PS-CLR-ROW-NOT-FOUND-SW        PIC  X(01)   VALUE 'Y'.   01760015
               88  PS-CLR-ROW-NOT-FOUND                    VALUE 'N'.   01770015
               88  PS-CLR-ROW-FOUND                        VALUE 'Y'.   01780015
           05  PS-PROCESS-STOP-SW             PIC  X(01)   VALUE 'N'.   01790015
               88  PS-PROCESS-STOP                         VALUE 'Y'.   01800015
           05  PS-FIRST-COMMIT-SW             PIC  X(01)   VALUE 'Y'.   01810015
               88  PS-FIRST-COMMIT                         VALUE 'Y'.   01820015
                                                                        01830015
       01  ABEND-CODE                         PIC S9(04)   VALUE +0     01840015
                                                           COMP SYNC.   01850015
       01  CR-CHECKPOINT-RESTART-AREA.                                  01860015
           05  CR-AREA-LEN               PIC S9(04)    VALUE +35  COMP. 01870015
                                                                        01880015
           05  CR-CHECKPOINT-RESTART-VAR.                               01890015
               10  CR-ACT-READ-CNT       PIC S9(09)    VALUE +0 COMP-3. 01900015
               10  CR-CAN-READ-CNT       PIC S9(09)    VALUE +0 COMP-3. 01910015
               10  CR-ACT-ACT-READ-CNT   PIC S9(09)    VALUE +0 COMP-3. 01920015
               10  CR-ACT-CLR-READ-CNT   PIC S9(09)    VALUE +0 COMP-3. 01930015
               10  CR-ACT-CAN-READ-CNT   PIC S9(09)    VALUE +0 COMP-3. 01940015
               10  CR-ACT-DONE-SW        PIC X         VALUE 'N'.       01950015
                 88 CR-ACT-DONE                        VALUE 'Y'.       01960015
                 88 CR-ACT-NOT-DONE                    VALUE 'N'.       01970015
               10  CR-CLR-DONE-SW        PIC X         VALUE 'N'.       01980015
                 88 CR-CLR-DONE                        VALUE 'Y'.       01990015
                 88 CR-CLR-NOT-DONE                    VALUE 'N'.       02000015
               10  CR-CAN-DONE-SW        PIC X         VALUE 'N'.       02010015
                 88 CR-CAN-DONE                        VALUE 'Y'.       02020015
                 88 CR-CAN-NOT-DONE                    VALUE 'N'.       02030015
                                                                        02040015
       01  DISPLAY-EDITS.                                               02050015
           05  DI-ACT-CNTR                  PIC  9(09)  VALUE ZEROS.    02060015
           05  DI-CLR-CNTR                  PIC  9(09)  VALUE ZEROS.    02070015
           05  DI-CAN-CNTR                  PIC  9(09)  VALUE ZEROS.    02080015
           05  DI-COMMIT-CNTR               PIC  9(09)  VALUE ZEROS.    02090015
      *                                                                 02091087
      *                                                                 02110015
      ***************************************************************** 02120015
      *    INPUT/OUTPUT COPYBOOKS                                       02130015
      ***************************************************************** 02140015
           COPY ICRD053.                                                02150015
                                                                        02160015
                                                                        02180015
           COPY ICRD056.                                                02190015
                                                                        02200015
C82402***************************************************************** 02201093
C82402     COPY ICRD054.                                                02202099
      ************************************************************      02210018
      *    TIMEZONE CONVERSION ROUTINE COPYBOOK                         02220018
      ************************************************************      02230018
           COPY ICWS010.                                                02240068
      ***************************************************************** 02250015
      *    DB2 COMMUNICATION AREA                                       02260015
      ***************************************************************** 02270015
           COPY DPWS004.                                                02280015
                                                                        02290015
      ******************************************************************02300015
      *  DCLGEN FOR CHECKPOINT RESTART CONTROL TABLE                    02310015
      ******************************************************************02320015
           EXEC SQL                                                     02330015
               INCLUDE TCKRSTCN                                         02340015
           END-EXEC.                                                    02350015
                                                                        02360015
      ******************************************************************02370015
      *  DCLGEN FOR CHECKPOINT RESTART INFORMATION TABLE                02380015
      ******************************************************************02390015
           EXEC SQL                                                     02400015
               INCLUDE TCKRSTIN                                         02410015
           END-EXEC.                                                    02420015
      ***************************************************************** 02430015
      *  XST_SKU TABLE                                                  02440015
      ***************************************************************** 02450015
           EXEC SQL                                                     02460015
                INCLUDE XST00010                                        02470015
           END-EXEC.                                                    02480015
                                                                        02490015
      ***************************************************************** 02500018
      *  XST_LOC TABLE                                                  02510018
      ***************************************************************** 02520018
           EXEC SQL                                                     02530018
                INCLUDE XST00001                                        02540018
           END-EXEC.                                                    02550018
                                                                        02560018
      ***************************************************************** 02570015
      *  XST_SKU_LOC TABLE                                              02580015
      ***************************************************************** 02590015
           EXEC SQL                                                     02600015
                INCLUDE XST00008                                        02610015
           END-EXEC.                                                    02620015
                                                                        02630015
      ***************************************************************** 02640015
      *  ICT_SKU_LOC_CNT TABLE                                          02650015
      ***************************************************************** 02660015
           EXEC SQL                                                     02670015
                INCLUDE ICT00011                                        02680015
           END-EXEC.                                                    02690015
                                                                        02700015
      ***************************************************************** 02710015
      *  ICT_LOC_CNT_STAT TABLE                                         02720015
      ***************************************************************** 02730015
           EXEC SQL                                                     02740015
                INCLUDE ICT00020                                        02750015
           END-EXEC.                                                    02760015
                                                                        02770015
      ***************************************************************** 02780015
      *  TDTATTR DATE TABLE                                             02790015
      ***************************************************************** 02800015
           EXEC SQL                                                     02810015
                INCLUDE TDTATTR                                         02820015
           END-EXEC.                                                    02830015
                                                                        02840015
           EXEC SQL                                                     02850015
               INCLUDE SQLCA                                            02860015
           END-EXEC.                                                    02870015
                                                                        02880015
           COPY SQLCA2.                                                 02890015
                                                                        02900015
      *---------------------------------------------------------------- 02910015
      *  USA IC COUNTS CURSOR WILL FETCH COUNTS TO BE CANCELLED         02920015
      *---------------------------------------------------------------- 02930015
           EXEC SQL                                                     02940015
             DECLARE CANC-CNT-CSR CURSOR FOR                            02950032
              SELECT  A.CNT_STAT_CDE                                    02960015
                     ,A.LOC_NBR                                         02970015
                     ,A.CNT_STUP_ID                                     02980015
                     ,A.CNT_SEQ_NBR                                     02990041
                     ,A.LAST_UPD_TMST                                   03000015
              FROM ICT_LOC_CNT_STAT    A                                03010015
              WHERE CNT_STUP_ID         = :ICT00020-CNT-STUP-ID         03020015
              AND DATE(A.LAST_UPD_TMST) < :PV-CURRENT-DATE              03030042
              AND A.LAST_UPD_TMST     =                                 03040042
      *** GET MAXIMUM TMST WITHIN...                                    03050042
              (SELECT MAX(LAST_UPD_TMST)                                03060015
               FROM ICT_LOC_CNT_STAT   B                                03070015
               WHERE  B.CNT_STUP_ID     = A.CNT_STUP_ID                 03080042
               AND    B.LOC_NBR         = A.LOC_NBR                     03090042
               AND    B.CNT_SEQ_NBR     =                               03100042
      *** MAXIMUM SEQ NBR                                               03110042
                (SELECT MAX(D.CNT_SEQ_NBR)                              03120047
                 FROM ICT_LOC_CNT_STAT   D                              03130042
                 WHERE  D.CNT_STUP_ID     = A.CNT_STUP_ID               03140042
                 AND    D.LOC_NBR         = A.LOC_NBR))                 03150047
      *** EXCLUDE ALREADY CANCELLED AND COMPLETED COUNTS                03160081
               AND NOT EXISTS                                           03170015
               (SELECT 'Y'                                              03180015
                FROM ICT_LOC_CNT_STAT C                                 03190015
                WHERE C.CNT_STAT_CDE IN ('CAN','COM')                   03200081
               AND    A.CNT_STUP_ID     = C.CNT_STUP_ID                 03210015
               AND    A.CNT_STAT_CDE    = C.CNT_STAT_CDE                03220015
               AND    A.CNT_SEQ_NBR     = C.CNT_SEQ_NBR                 03221081
               AND    A.LOC_NBR         = C.LOC_NBR)                    03230015
             WITH UR                                                    03240015
           END-EXEC.                                                    03250015
                                                                        03260015
       PROCEDURE DIVISION.                                              03640015
      ******************************************************************03650015
      * 0000-MAIN-MODULE.                                              *03660015
      *  ==> PROCESSES:                                                *03670015
      *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *03680015
      ******************************************************************03690015
       0000-MAIN-MODULE.                                                03700015
           MOVE '0000-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                03710015
                                                                        03720015
           PERFORM 1000-INITIALIZATION.                                 03730015
                                                                        03740015
           IF CR-ACT-NOT-DONE                                           03751015
             PERFORM 7000-READ-CNT-SKU-EXP-FL                           03760044
             IF  TCKRSTCNTL-CKPT-STAT-CODE = '9'                        03770015
               DISPLAY 'STARTING RECORD - ' ICRD053-IC-COUNTS-FILE      03780015
             END-IF                                                     03790015
             PERFORM 2000-PROCESS-COUNT                                 03800044
               UNTIL PS-END-OF-ACT-INPUT-FL                             03810015
C82402       PERFORM 2300-MISSED-INS                                    03811099
             PERFORM 8000-SQL-COMMIT                                    03820015
             DISPLAY 'ACTIVE/CLR COUNT PROCESSING DONE'.                03830044
                                                                        03840015
           IF CR-CAN-NOT-DONE                                           03850015
             PERFORM 7200-READ-CNT-CAN-COUNT-FILE                       03860015
             IF  TCKRSTCNTL-CKPT-STAT-CODE = '9'                        03870015
               DISPLAY 'STARTING RECORD - ' ICRD056-CANC-IC-COUNTS-FILE 03880015
             END-IF                                                     03890015
             PERFORM 2200-PROCESS-CAN-COUNT                             03900015
               UNTIL PS-END-OF-CAN-INPUT-FL                             03910015
             DISPLAY 'CLEARANCE COUNT PROCESSING DONE'.                 03920015
                                                                        03930015
           PERFORM 3000-EOJ-PROCESSING.                                 03940015
                                                                        03950015
           STOP RUN.                                                    03960015
                                                                        03970015
      ******************************************************************03980015
      * 1000-INITIALIZATION.                                           *03990015
      ******************************************************************04000015
       1000-INITIALIZATION.                                             04010015
           MOVE '1000-INITIALIZATION ' TO PV-PARAGRAPH-NAME.            04020015
                                                                        04030015
           OPEN INPUT  CONTROL-FILE-1.                                  04040015
           READ CONTROL-FILE-1                                          04050015
             AT END                                                     04060015
               DISPLAY 'NO CTL CARD FOUND' UPON CONSOLE                 04070015
               PERFORM 9999-SQL-ABEND.                                  04080015
           MOVE CTL1-JOB-NAME TO  PC-JOB-NAME.                          04090015
           DISPLAY 'JOB/PGM NAME - ' PC-JOB-NAME  '/'                   04100015
                                 PC-PROGRAM-NAME.                       04110015
           CLOSE CONTROL-FILE-1.                                        04120015
           PERFORM 8100-INIT-CHECKPOINT-SELECT.                         04130015
           MOVE TCKRSTCNTL-CKPT-FRQNCY-QTY TO PV-COMMIT-INTERVAL.       04140022
      ******************************************************************04150015
      *  IF THE PROGRAM IS IN A RESTART STATUS, SELECT SAVED DATA       04160015
      *  FROM THE RESTART INFO TABLE AND ONLY PROCESS NON-COMPLETED     04170015
      *  RECORDS AS OF THE LAST COMMIT. IF NOT, THEN THE VARIABLES WILL 04180015
      *  CONTAIN THEIR INITIAL VALUES AND THE PROGRAM WILL PROCESS ALL  04190015
      *  RECORDS ON ALL INPUT FILES. IF A FILE WAS COMPLETELY PROCESSED 04200015
      *  IN THE PREVIOUS RUN BEFORE AN ABEND, IT WILL NOT BE OPENED OR  04210015
      *  READ AT ALL.                                                   04220015
      ******************************************************************04230015
           IF  TCKRSTCNTL-CKPT-STAT-CODE = '9'                          04240015
               PERFORM 8150-SELECT-RESTART-INFO                         04250015
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT                       04260015
                 TO CR-CHECKPOINT-RESTART-VAR                           04270015
               DISPLAY SPACES                                           04340015
               DISPLAY '** ICKUP054 IS BEING RESTARTED'.                04350015
                                                                        04360015
           IF CR-ACT-NOT-DONE                                           04370015
             OPEN INPUT  CNT-SKU-EXP-FILE                               04380044
             PERFORM 7000-READ-CNT-SKU-EXP-FL CR-ACT-READ-CNT TIMES.    04390099
           IF CR-CAN-NOT-DONE                                           04400015
             OPEN INPUT  CNT-CAN-COUNT-FILE                             04410015
             PERFORM 7200-READ-CNT-CAN-COUNT-FILE CR-CAN-READ-CNT TIMES.04420015
                                                                        04430015
           EXEC SQL                                                     04440015
               SET :PV-CURRENT-DATE = CURRENT DATE                      04450015
           END-EXEC.                                                    04460015
                                                                        04470015
           EXEC SQL                                                     04471071
               SET :PV-CURRENT-DATE-PLUS-2 = CURRENT DATE + 2 DAYS      04472071
           END-EXEC.                                                    04473071
                                                                        04474071
           EXEC SQL                                                     04480015
               SET :PV-CURRENT-TMST = CURRENT TIMESTAMP                 04490015
           END-EXEC.                                                    04500015
           PERFORM 9000-TIMEZONE-CONVERSION-SETUP.                      04511068
TEST  **   MOVE '2008-02-27-13.57.34.400198' TO PV-CURRENT-TMST.        04540057
TEST  **   MOVE '2008-02-27' TO PV-CURRENT-DATE.                        04550057
      *    PERFORM 8170-GET-COMMIT-TIMESTAMP.                           04560015
                                                                        04570015
                                                                        04578094
      ******************************************************************04580015
      * 2000-PROCESS-COUNT.                                            *04590044
      *  ==> INSERT  ROWS FOR EACH ACTIVE/CLR COUNT/LOC/SKU RECORD     *04600044
      ******************************************************************04610015
       2000-PROCESS-COUNT.                                              04620044
           MOVE '2000-PROCESS-COUNT' TO PV-PARAGRAPH-NAME.              04630044
      *** LOGIC TO DETERMINE NEW OR EXISTING SEQ NBR********************04631046
      *** POSSIBLE VALUES FOR ICRD053-RPET-CNT-IND***                   04632053
      ***   'N' - NOT REPEATING COUNT                                   04633053
      ***   'W' - WEEKLY REPEATING COUNT                                04634053
      ***   'M' - MONTHLY REPEATING COUNT                               04635053
      ***   'D' - DAILY REPEATING COUNT                                 04635153
      ***********************************************                   04635253
           IF ICRD053-CNT-STUP-ID NOT = ICT00011-CNT-STUP-ID            04640044
             IF ICRD053-RPET-CNT-IND = 'N'                              04641075
      *** ALL NON-REPEATING COUNTS HAVE A SEQ-NBR OF 1                  04660043
               MOVE 1 TO PV-SEQ-NBR                                     04670044
             ELSE                                                       04680044
             IF ICRD053-RPET-CNT-IND = 'W' OR 'M'                       04690044
      *** WEEKLY OR DAILY COUNTS CAN BE LOADED IN ADVANCE OF THEIR      04700075
      *** EXEC DATE (UNLIKE DAILY COUNTS),SO, AN UPDATE OF AN EXISTING  04710075
      *** COUNT MAY BE PERFORMED OR A NEW COUNT (SEQ + 1) MAY BE DONE.  04720075
                 MOVE ICRD053-CNT-STUP-ID TO ICT00020-CNT-STUP-ID       04721572
      *** THIS SELECT COMPUTES THE NEXT SEQ-NBR FOR NEW COUNTS BY       04721675
      *** ADDING 1 TO THE CURRENT HIGHEST SEQ-NBR AND IT DEFAULTS TO    04721775
      *** ONE IF THIS THE FIRST TIME THIS COUNT HAS BEEN PROCESSED.     04721875
                 PERFORM 8510-SELECT-SEQ-NBR                            04721972
               IF ICRD053-RPET-NEW                                      04722075
                 MOVE ICT00020-CNT-SEQ-NBR TO PV-SEQ-NBR                04722776
               ELSE                                                     04722875
      *** IF NOT A NEW COUNT, IT MUST BE AN UPD TO AN EXISTING COUNT.   04722975
                 IF ICT00020-CNT-SEQ-NBR > 1                            04723675
      *** IF THIS THE FIRST TIME A COUNT HAS BEEN PROCESSED WE DON'T    04723775
      *** WANT TO SUBTRACT ONE AS THAT WOULD GIVE A SEQ-NBR = 0.        04723875
                   SUBTRACT 1 FROM ICT00020-CNT-SEQ-NBR                 04723975
                   MOVE ICT00020-CNT-SEQ-NBR TO PV-SEQ-NBR              04724675
                 ELSE                                                   04724775
                   MOVE ICT00020-CNT-SEQ-NBR TO PV-SEQ-NBR              04725476
                 END-IF                                                 04726075
               END-IF                                                   04751075
             ELSE                                                       04880044
      *** IF NOT MONTHLY OR WEEKLY THEN THIS IS A DAILY REPEATING COUNT 04890046
      *** AND THE SEQ NBR IS ALWAYS INCREMENTED FOR A DAILY COUNT.      04900046
               MOVE ICT00020-CNT-STUP-ID TO PV-HOLD-CNT-STUP-ID         04910070
               MOVE ICRD053-CNT-STUP-ID TO ICT00020-CNT-STUP-ID         04911070
               PERFORM 8510-SELECT-SEQ-NBR                              04920044
               MOVE PV-HOLD-CNT-STUP-ID TO ICT00020-CNT-STUP-ID         04921070
               MOVE ICT00020-CNT-SEQ-NBR TO PV-SEQ-NBR.                 04930044
      ******************************************************************04931046
           IF ICRD053-ORIG-CLR-DTE > SPACE                              04950044
             PERFORM 2100-PROCESS-CLR-COUNT                             04960044
           ELSE                                                         04970044
             PERFORM 2050-PROCESS-ACT-COUNT.                            04980087
C82402     PERFORM 2400-CHK-FOR-INS                                     04982099
C82402                                                                  04983099
C82402     IF PV-COMMIT-CNTR > PV-COMMIT-INTERVAL                       04984199
C82402       ADD PV-COMMIT-INTERVAL TO CR-ACT-READ-CNT                  04984399
C82402       PERFORM 8000-SQL-COMMIT.                                   04985099
C82402     PERFORM 7000-READ-CNT-SKU-EXP-FL.                            04986099
C82402                                                                  04987099
      ******************************************************************04990044
      * 2050-PROCESS-ACT-COUNT.                                        *05000044
      *  ==> INSERT  ROWS FOR EACH ACTIVE COUNT/LOC/SKU RECORD         *05010044
      ******************************************************************05020044
       2050-PROCESS-ACT-COUNT.                                          05030044
           MOVE '2050-PROCESS-ACT-COUNT' TO PV-PARAGRAPH-NAME.          05040046
           MOVE ICRD053-LOC-NBR     TO ICT00011-LOC-NBR                 05050044
                                       XST00001-LOC-NBR.                05060044
           MOVE ICRD053-SKU-NBR     TO ICT00011-SKU-NBR.                05070044
           MOVE PV-SEQ-NBR          TO ICT00011-CNT-SEQ-NBR.            05071048
           MOVE ICRD053-CNT-STUP-ID TO ICT00011-CNT-STUP-ID.            05080044
           MOVE ICRD053-UNT-RTL-AMT TO ICT00011-UNT-RTL-AMT.            05090044
           MOVE ICRD053-EXEC-DTE       TO ICT00011-EXEC-TMST.           05180029
           MOVE '-00.00.01.000000'     TO ICT00011-EXEC-TMST (11:16).   05190027
C82402     PERFORM 8500-SELECT-XST-LOC.                                 05140018
           MOVE ICWS010-TZC-LOCATION-TMST TO ICT00011-LAST-UPD-TMST.    05210068
C82402     PERFORM 2500-MOVE-ACT-SKU-LOC-CNT.                           05221099
      * ONLY 1 ROW PER COUNT/LOC IS CREATED, NOT COUNT/LOC/SKU          05230015
           IF  ICRD053-LOC-NBR  NOT = PV-HOLD-LOC-NBR                   05240051
             OR ICRD053-CNT-STUP-ID NOT = PV-HOLD-CNT-STUP-ID           05250051
             MOVE ICRD053-LOC-NBR     TO PV-HOLD-LOC-NBR                05260077
             MOVE ICRD053-CNT-STUP-ID TO PV-HOLD-CNT-STUP-ID            05270077
             MOVE 'AVA'               TO ICT00020-CNT-STAT-CDE          05300015
             MOVE PV-SEQ-NBR          TO ICT00020-CNT-SEQ-NBR           05310041
             MOVE ICRD053-LOC-NBR     TO ICT00020-LOC-NBR               05320015
             MOVE ICRD053-CNT-STUP-ID TO ICT00020-CNT-STUP-ID           05330015
C82402       MOVE ICWS010-TZC-LOCATION-TMST TO ICT00020-LAST-UPD-TMST   05210068
C82402         PERFORM 8350-INS-SKU-LOC-CNT-ST                          06301099
C82402         ADD 1 TO PV-I-CNTR                                               
             .                                                          05460015
                                                                        05470015
      ******************************************************************05480015
      * 2100-PROCESS-CLR-COUNT.                                        *05490015
      *  ==> INSERT ROWS FOR EACH CLEARANCE COUNT/LOC/SKU RECORD       *05500044
      ******************************************************************05510015
       2100-PROCESS-CLR-COUNT.                                          05520015
           MOVE '2100-PROCESS-CLR-COUNT' TO PV-PARAGRAPH-NAME.          05530015
           SET PS-CLR-ROW-FOUND TO TRUE.                                05590015
           MOVE PV-SEQ-NBR           TO ICT00011-CNT-SEQ-NBR.           05600041
C82402     MOVE ICRD053-LOC-NBR      TO ICT00011-LOC-NBR                05610099
C82402                                  XST00001-LOC-NBR.               05611099
           MOVE ICRD053-SKU-NBR      TO ICT00011-SKU-NBR.               05620044
           MOVE ICRD053-CNT-STUP-ID  TO ICT00011-CNT-STUP-ID.           05630044
           MOVE ICRD053-ORIG-CLR-DTE TO ICT00011-MDSE-EXPRN-DTE.        05640099
           MOVE ICRD053-EXEC-DTE     TO ICT00011-EXEC-TMST.             05650099
           MOVE '-00.00.01.000000'   TO ICT00011-EXEC-TMST (11:16).     05660099
           PERFORM 8260-SEL-SKU-LOC-CNT.                                05670015
      * IF A ROW DOESN'T EXIST IT IS ADDED WITH THE ORIG-CLR-DTE        05680015
      * POPULATED                                                       05690015
           IF PS-CLR-ROW-NOT-FOUND                                      05700015
             MOVE 0                    TO ICT00011-UNT-RTL-AMT          05780015
             MOVE 0                    TO ICT00011-LOC-EXPTED-UNT-QTY   05790015
             PERFORM 8500-SELECT-XST-LOC                                05800018
             MOVE ICWS010-TZC-LOCATION-TMST TO ICT00011-LAST-UPD-TMST   05850068
C82402       PERFORM 2700-MOVE-NEW-CLR-SKU-LOC-CNT                      05861099
      * ONLY 1 ROW PER COUNT/LOC IS CREATED, NOT COUNT/LOC/SKU          05870036
             IF  ICRD053-LOC-NBR  NOT = PV-HOLD-LOC-NBR                 05900077
               OR ICRD053-CNT-STUP-ID NOT = PV-HOLD-CNT-STUP-ID         05910077
               MOVE ICRD053-LOC-NBR     TO PV-HOLD-LOC-NBR              05920077
               MOVE ICRD053-CNT-STUP-ID TO PV-HOLD-CNT-STUP-ID          05930077
               MOVE 'AVA'               TO ICT00020-CNT-STAT-CDE        05940036
               MOVE PV-SEQ-NBR          TO ICT00020-CNT-SEQ-NBR         05950041
               MOVE ICRD053-LOC-NBR     TO ICT00020-LOC-NBR             05960044
               MOVE ICRD053-CNT-STUP-ID TO ICT00020-CNT-STUP-ID         05970044
             MOVE ICWS010-TZC-LOCATION-TMST TO ICT00020-LAST-UPD-TMST   05210068
C82402         PERFORM 8350-INS-SKU-LOC-CNT-ST                          06001099
C82402         ADD 1 TO PV-I-CNTR                                               
             END-IF                                                     06010036
           ELSE                                                         06020015
      * A ROW DOES EXIST, SO, A NEW ROW WITH EXISTING DATA AND THE      06030015
      * ORIG-CLR-DTE DATA IS ADDED. DATA FROM THE SELECT ABOVE          06040015
      * IS USED AND THE ORIG-CLR-DTE IS UPDATED FROM THE INPUT RECORD.  06050015
             PERFORM 8500-SELECT-XST-LOC                                06110099
             MOVE ICWS010-TZC-LOCATION-TMST TO ICT00011-LAST-UPD-TMST   06150068
C82402       PERFORM 2800-MOVE-CLR-SKU-LOC-CNT                          06161099
      * ONLY 1 ROW PER COUNT/LOC IS CREATED, NOT COUNT/LOC/SKU          06170036
             IF  ICRD053-LOC-NBR  NOT = PV-HOLD-LOC-NBR                 06200077
               OR ICRD053-CNT-STUP-ID NOT = PV-HOLD-CNT-STUP-ID         06210077
               MOVE ICRD053-LOC-NBR     TO PV-HOLD-LOC-NBR              06220077
               MOVE ICRD053-CNT-STUP-ID TO PV-HOLD-CNT-STUP-ID          06230077
               MOVE 'AVA'               TO ICT00020-CNT-STAT-CDE        06240036
               MOVE PV-SEQ-NBR          TO ICT00020-CNT-SEQ-NBR         06250041
               MOVE ICRD053-LOC-NBR     TO ICT00020-LOC-NBR             06260044
               MOVE ICRD053-CNT-STUP-ID TO ICT00020-CNT-STUP-ID         06270044
             MOVE ICWS010-TZC-LOCATION-TMST TO ICT00020-LAST-UPD-TMST   05210068
C82402         PERFORM 8350-INS-SKU-LOC-CNT-ST                          06301099
C82402         ADD 1 TO PV-I-CNTR                                               
             END-IF.                                                    06310041
                                                                        06370015
      ******************************************************************06380015
      * 2200-PROCESS-CAN-COUNT.                                        *06390015
      *  ==> INSERT CANCELLED ROWS INTO ICT_LOC_CNT_STAT TABLE         *06400015
      ******************************************************************06410015
       2200-PROCESS-CAN-COUNT.                                          06420015
           MOVE '2200-PROCESS-CAN-COUNT' TO PV-PARAGRAPH-NAME.          06430015
           SET PS-MORE-CANC-CNT TO TRUE                                 06440015
           MOVE ICRD056-CNT-STUP-ID TO ICT00020-CNT-STUP-ID.            06450015
           MOVE ICRD056-LOC-NBR     TO ICT00020-LOC-NBR.                06451064
      *** IF ICRD056-LOC-NBR = 0, THEN, THIS A CANCEL FOR ALL STORES    06470024
           IF ICRD056-LOC-NBR = 0                                       06480024
             PERFORM 8400-OPEN-CANC-CNT                                 06490024
             PERFORM 8410-FETCH-CANC-CNT                                06500024
             PERFORM                                                    06510024
               UNTIL PS-NO-MORE-CANC-CNT                                06520024
               MOVE 'CAN'               TO ICT00020-CNT-STAT-CDE        06530024
               MOVE ICT00020-LOC-NBR    TO XST00001-LOC-NBR             06540024
               PERFORM 8500-SELECT-XST-LOC                              06550024
               MOVE XST00001-STD-TM-ZN-CDE TO                                   
                    ICWS010-TZC-TZ-IN-TM-ZN-CDE                                 
               MOVE XST00001-DST-IND       TO ICWS010-TZC-TZ-IND-ST-IND         
               PERFORM 9000-TIMEZONE-CONVERSION                                 
               MOVE ICWS010-TZC-LOCATION-TMST TO ICT00020-LAST-UPD-TMST         
               PERFORM 8520-SELECT-CURR-SEQ-NBR                         06591080
C82402         PERFORM 8350-INS-SKU-LOC-CNT-ST                          06601099
C82402         ADD 1 TO PV-I-CNTR                                               
               PERFORM 8410-FETCH-CANC-CNT                              06610024
             END-PERFORM                                                06620024
             PERFORM 8420-CLOSE-CANC-CNT                                06630024
           ELSE                                                         06640024
      *** ELSE, ONLY THE COUNT FOR THE STORE SPECIFIED ON THE RECORD    06650024
      *** WILL BE CANCELLED.                                            06660024
             MOVE 'CAN'               TO ICT00020-CNT-STAT-CDE          06670024
             MOVE ICRD056-LOC-NBR     TO XST00001-LOC-NBR               06680024
                                         ICT00020-LOC-NBR               06690030
             PERFORM 8520-SELECT-CURR-SEQ-NBR                           06691080
             PERFORM 8530-CHECK-COMP-CANC-COUNT                         06692081
             IF PV-COMP-CAN-COUNT                                       06693081
               DISPLAY 'COUNT ALREADY CANCELLED OR COMPLETED - '        06694085
                        ICT00020-CNT-STUP-ID  '-'                       06694181
                        ICT00020-CNT-STAT-CDE '-'                       06694281
                        ICT00020-CNT-SEQ-NBR  '-'                       06694381
                        ICT00020-LOC-NBR                                06694481
             ELSE                                                       06695081
               PERFORM 8500-SELECT-XST-LOC                              06700081
               MOVE XST00001-STD-TM-ZN-CDE TO                                   
                    ICWS010-TZC-TZ-IN-TM-ZN-CDE                                 
               MOVE XST00001-DST-IND     TO ICWS010-TZC-TZ-IND-ST-IND           
               PERFORM 9000-TIMEZONE-CONVERSION                                 
               MOVE ICWS010-TZC-LOCATION-TMST TO                                
                     ICT00020-LAST-UPD-TMST                                     
C82402         PERFORM 8350-INS-SKU-LOC-CNT-ST                          06750199
C82402         ADD 1 TO PV-I-CNTR                                               
               DISPLAY 'COUNT CANCELLED OR COMPLETED TODAY - '          06750399
                        ICT00020-CNT-STUP-ID  '-'                       06750499
                        ICT00020-CNT-STAT-CDE '-'                       06750599
                        ICT00020-CNT-SEQ-NBR  '-'                       06750699
                        ICT00020-LOC-NBR                                06750799
             END-IF.                                                    06752081
           ADD 1 TO PV-COMMIT-CNTR.                                     06760015
           IF PV-COMMIT-CNTR > PV-COMMIT-INTERVAL                       06770015
             ADD PV-COMMIT-INTERVAL TO CR-CAN-READ-CNT                  06780015
             PERFORM 8000-SQL-COMMIT.                                   06790015
           PERFORM 7200-READ-CNT-CAN-COUNT-FILE.                        06800015
                                                                        06810015
C82402******************************************************************06811099
C82402* 2300-MISSED-INS.                                               *06812099
C82402******************************************************************06814099
C82402 2300-MISSED-INS.                                                 06814199
C82402     IF PV-INDX1 > 0                                              06814299
C82402       ADD PV-INDX1 TO PV-J-MSD-INS-CNTR                                  
C82402       PERFORM 8200-INS-ACT-SKU-LOC-CNT                           06814399
C82402     END-IF.                                                      06814699
C82402                                                                  06816299
C82402******************************************************************06816399
C82402* 2400-CHK-FOR-INS.                                              *06816499
C82402******************************************************************06816599
C82402 2400-CHK-FOR-INS.                                                06816699
C82402     IF PV-INDX1 = 100                                            06816799
C82402       PERFORM 8200-INS-ACT-SKU-LOC-CNT                           06816899
C82402       ADD 1 TO PV-J-CNTR                                                 
C82402     END-IF.                                                      06817199
C82402                                                                  06817199
C82402******************************************************************06818899
C82402* 2500-MOVE-ACT-SKU-LOC-CNT.                                     *06818999
C82402******************************************************************06819099
C82402 2500-MOVE-ACT-SKU-LOC-CNT.                                       06819199
C82402     ADD +1                      TO PV-INDX1                      06819299
C82402     MOVE ICT00011-LOC-NBR       TO IC054-LOC-NBR(PV-INDX1)       06819399
C82402     MOVE ICT00011-SKU-NBR       TO IC054-SKU-NBR(PV-INDX1)       06819499
C82402     MOVE ICT00011-CNT-STUP-ID   TO IC054-CNT-STUP-ID(PV-INDX1)   06819599
C82402     MOVE ICT00011-LAST-UPD-TMST TO IC054-LAST-UPD-TMST(PV-INDX1) 06819699
C82402     MOVE ICT00011-CNT-SEQ-NBR   TO IC054-CNT-SEQ-NBR(PV-INDX1)   06819899
C82402     MOVE 'A'                    TO IC054-LAST-ACTN-CDE(PV-INDX1) 06820199
C82402     MOVE ICT00011-UNT-RTL-AMT   TO IC054-UNT-RTL-AMT(PV-INDX1)   06820399
C82402     MOVE ZEROS                  TO                               06820499
C82402                            IC054-LOC-EXPTED-UNT-QTY(PV-INDX1)    06820599
C82402     MOVE ICT00011-EXEC-TMST     TO IC054-EXEC-TMST(PV-INDX1)     06820799
C82402     MOVE -1                     TO                               06820999
C82402                             IC054-TRNST-UNT-QTY-NI(PV-INDX1)     06821099
C82402                             IC054-TRUK-UNT-QTY-NI(PV-INDX1)      06821399
C82402                             IC054-RTL-SAL-UNT-QTY-NI(PV-INDX1)   06821599
C82402                             IC054-RTL-RET-UNT-QTY-NI(PV-INDX1)   06821799
C82402                             IC054-XFER-IN-UNT-QTY-NI(PV-INDX1)   06821999
C82402                             IC054-XFER-OUT-UNT-QTY-NI(PV-INDX1)  06822199
C82402                             IC054-SCND-UPC-NBR-NI(PV-INDX1)      06822399
C82402                             IC054-SCND-UNT-QTY-NI(PV-INDX1)      06822599
C82402                             IC054-CNT-GP-CDE-NI(PV-INDX1)        06822799
C82402                             IC054-MDSE-EXPRN-DTE-NI(PV-INDX1).   06823399
C82402                                                                  06824999
C82402******************************************************************06825099
C82402* 2700-MOVE-NEW-CLR-SKU-LOC-CNT.                                 *06825199
C82402******************************************************************06825299
C82402 2700-MOVE-NEW-CLR-SKU-LOC-CNT.                                   06825399
C82402     ADD +1                      TO PV-INDX1                      06825499
C82402     MOVE ICT00011-LOC-NBR       TO IC054-LOC-NBR(PV-INDX1)       06825599
C82402     MOVE ICT00011-SKU-NBR       TO IC054-SKU-NBR(PV-INDX1)       06825699
C82402     MOVE ICT00011-CNT-STUP-ID   TO IC054-CNT-STUP-ID(PV-INDX1)   06825799
C82402     MOVE ICT00011-LAST-UPD-TMST TO IC054-LAST-UPD-TMST(PV-INDX1) 06825899
C82402     MOVE ICT00011-CNT-SEQ-NBR   TO IC054-CNT-SEQ-NBR(PV-INDX1)   06825999
C82402     MOVE 'A'                    TO IC054-LAST-ACTN-CDE(PV-INDX1) 06826299
C82402     MOVE ICT00011-UNT-RTL-AMT   TO IC054-UNT-RTL-AMT(PV-INDX1)   06826399
C82402     MOVE ICT00011-LOC-EXPTED-UNT-QTY TO                          06826499
C82402                            IC054-LOC-EXPTED-UNT-QTY(PV-INDX1)    06826599
C82402     MOVE ICT00011-EXEC-TMST     TO IC054-EXEC-TMST(PV-INDX1)     06826799
C82402     MOVE ICT00011-MDSE-EXPRN-DTE TO                              06826999
C82402                             IC054-MDSE-EXPRN-DTE(PV-INDX1)       06827099
C82402                                                                  06827199
C82402     MOVE -1              TO IC054-TRNST-UNT-QTY-NI(PV-INDX1)     06827299
C82402                             IC054-TRUK-UNT-QTY-NI(PV-INDX1)      06827599
C82402                             IC054-RTL-SAL-UNT-QTY-NI(PV-INDX1)   06827799
C82402                             IC054-RTL-RET-UNT-QTY-NI(PV-INDX1)   06827999
C82402                             IC054-XFER-IN-UNT-QTY-NI(PV-INDX1)   06828199
C82402                             IC054-XFER-OUT-UNT-QTY-NI(PV-INDX1)  06828399
C82402                             IC054-SCND-UPC-NBR-NI(PV-INDX1)      06828599
C82402                             IC054-SCND-UNT-QTY-NI(PV-INDX1)      06828799
C82402                             IC054-CNT-GP-CDE-NI(PV-INDX1)        06828999
C82402     MOVE 0               TO IC054-MDSE-EXPRN-DTE-NI(PV-INDX1).   06829399
C82402                                                                  06829599
C82402******************************************************************06829699
C82402* 2800-MOVE-CLR-SKU-LOC-CNT.                                     *06829799
C82402******************************************************************06829899
C82402 2800-MOVE-CLR-SKU-LOC-CNT.                                       06829999
C82402     ADD +1                      TO PV-INDX1                      06830099
C82402     MOVE ICT00011-LOC-NBR       TO IC054-LOC-NBR(PV-INDX1)       06830199
C82402     MOVE ICT00011-SKU-NBR       TO IC054-SKU-NBR(PV-INDX1)       06830299
C82402     MOVE ICT00011-CNT-STUP-ID   TO IC054-CNT-STUP-ID(PV-INDX1)   06830399
C82402     MOVE ICT00011-LAST-UPD-TMST TO IC054-LAST-UPD-TMST(PV-INDX1) 06830499
C82402     MOVE ICT00011-CNT-SEQ-NBR   TO IC054-CNT-SEQ-NBR(PV-INDX1)   06830599
C82402     MOVE ICT00011-LAST-ACTN-CDE TO IC054-LAST-ACTN-CDE(PV-INDX1) 06830899
C82402     MOVE ICT00011-UNT-RTL-AMT   TO IC054-UNT-RTL-AMT(PV-INDX1)   06830999
C82402     MOVE ICT00011-LOC-EXPTED-UNT-QTY TO                          06831099
C82402                             IC054-LOC-EXPTED-UNT-QTY(PV-INDX1)   06831199
C82402     IF PV-IN-TRNST-UNT-QTY-NI = 0                                06831299
C82402        MOVE ICT00011-IN-TRNST-UNT-QTY                            06831399
C82402                        TO IC054-IN-TRNST-UNT-QTY(PV-INDX1)       06831499
C82402     END-IF                                                       06831599
C82402     IF PV-ACPTD-TRUK-UNT-QTY-NI = 0                              06831699
C82402        MOVE ICT00011-ACPTD-TRUK-UNT-QTY                          06831799
C82402                        TO IC054-ACPTD-TRUK-UNT-QTY(PV-INDX1)     06831899
C82402     END-IF                                                       06831999
C82402     IF PV-RTL-SAL-UNT-QTY-NI = 0                                 06832099
C82402        MOVE ICT00011-RTL-SAL-UNT-QTY                             06832199
C82402                        TO IC054-RTL-SAL-UNT-QTY(PV-INDX1)        06832299
C82402     END-IF                                                       06832399
C82402     IF PV-RTL-RET-UNT-QTY-NI = 0                                 06832499
C82402        MOVE ICT00011-RTL-RET-UNT-QTY                             06832599
C82402                        TO IC054-RTL-RET-UNT-QTY(PV-INDX1)        06832699
C82402     END-IF                                                       06832799
C82402     IF PV-XFER-IN-UNT-QTY-NI = 0                                 06832899
C82402        MOVE ICT00011-XFER-IN-UNT-QTY                             06832999
C82402                        TO IC054-XFER-IN-UNT-QTY(PV-INDX1)        06833099
C82402     END-IF                                                       06833199
C82402     IF PV-XFER-OUT-UNT-QTY-NI = 0                                06833299
C82402        MOVE ICT00011-XFER-OUT-UNT-QTY                            06833399
C82402                        TO IC054-XFER-OUT-UNT-QTY(PV-INDX1)       06833499
C82402     END-IF                                                       06833599
C82402     IF PV-SCND-UPC-NBR-NI = 0                                    06833699
C82402        MOVE ICT00011-SCND-UPC-NBR                                06833799
C82402                        TO IC054-SCND-UPC-NBR(PV-INDX1)           06833899
C82402     END-IF                                                       06833999
C82402     IF PV-SCND-UNT-QTY-NI = 0                                    06834099
C82402        MOVE ICT00011-SCND-UNT-QTY                                06834199
C82402                        TO IC054-SCND-UNT-QTY(PV-INDX1)           06834299
C82402     END-IF                                                       06834399
C82402     IF PV-CNT-GP-CDE-NI = 0                                      06834499
C82402        MOVE ICT00011-CNT-GP-CDE                                  06834599
C82402                        TO IC054-CNT-GP-CDE(PV-INDX1)             06834699
C82402     END-IF                                                       06834799
C82402     MOVE ICT00011-EXEC-TMST  TO IC054-EXEC-TMST(PV-INDX1)        06834999
C82402     MOVE ICT00011-MDSE-EXPRN-DTE                                 06835199
C82402                        TO IC054-MDSE-EXPRN-DTE(PV-INDX1)         06835299
C82402*                                                                 06835399
C82402     MOVE PV-IN-TRNST-UNT-QTY-NI                                  06835499
C82402                        TO IC054-TRNST-UNT-QTY-NI(PV-INDX1)       06835599
C82402     MOVE PV-ACPTD-TRUK-UNT-QTY-NI                                06835699
C82402                      TO IC054-TRUK-UNT-QTY-NI(PV-INDX1)          06835799
C82402     MOVE PV-RTL-SAL-UNT-QTY-NI                                   06835899
C82402                        TO IC054-RTL-SAL-UNT-QTY-NI(PV-INDX1)     06835999
C82402     MOVE PV-RTL-RET-UNT-QTY-NI                                   06836099
C82402                        TO IC054-RTL-RET-UNT-QTY-NI(PV-INDX1)     06836199
C82402     MOVE PV-XFER-IN-UNT-QTY-NI                                   06836299
C82402                        TO IC054-XFER-IN-UNT-QTY-NI(PV-INDX1)     06836399
C82402     MOVE PV-XFER-OUT-UNT-QTY-NI                                  06836499
C82402                        TO IC054-XFER-OUT-UNT-QTY-NI(PV-INDX1)    06836599
C82402     MOVE PV-SCND-UPC-NBR-NI TO IC054-SCND-UPC-NBR-NI(PV-INDX1)   06836699
C82402     MOVE PV-SCND-UNT-QTY-NI TO IC054-SCND-UNT-QTY-NI(PV-INDX1)   06836799
C82402     MOVE PV-CNT-GP-CDE-NI   TO IC054-CNT-GP-CDE-NI(PV-INDX1)     06836899
C82402     MOVE 0                  TO IC054-MDSE-EXPRN-DTE-NI(PV-INDX1).06837099
                                                                        06837199
      ******************************************************************06837299
      * 3000-EOJ-PROCESSING.                                           *06838089
      *  ==> PROCESSES:                                                *06840015
      *    - CLOSE FILES THAT WERE OPENED IN PARA 1000. IF A READ CNT  *06850015
      *      > 0, THEN, IT WAS OPENED AND READ AT LEAST ONCE.           06860015
      *    - DISPLAYS PROCESSING TOTALS                                *06870015
      *    - SETS CHEKPOINT STATUS CODE TO 0 (PROGRAM NOT IN PROCESS OR*06880015
      *      IN RESTART STATUS.                                        *06890015
      ******************************************************************06900015
       3000-EOJ-PROCESSING.                                             06910015
           MOVE '3000-EOJ-PROCESSING' TO PV-PARAGRAPH-NAME.             06920015
                                                                        06930015
C82402     IF DI-ACT-CNTR > 0 OR DI-CLR-CNTR > 0                        06940015
             CLOSE CNT-SKU-EXP-FILE.                                    06950044
C82402     IF DI-CAN-CNTR > 0                                           06960015
             CLOSE CNT-CAN-COUNT-FILE.                                  06970015
                                                                        06980015
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       06990015
           PERFORM 8160-UPDATE-CHECKPOINT-STATUS.                       07000015
           PERFORM 9990-DISPLAY-CONTROL-INFO.                           07010015
                                                                        07030015
      *----------------------------------------------------------       07040015
      * 7000-READ-CNT-SKU-EXP-FL.                                       07050044
      *----------------------------------------------------------       07060015
       7000-READ-CNT-SKU-EXP-FL.                                        07070044
           MOVE '7000-READ-CNT-SKU-EXP-FL'   TO PV-PARAGRAPH-NAME.      07080044
           READ CNT-SKU-EXP-FILE                                        07090044
             INTO ICRD053-IC-COUNTS-FILE                                07100015
           AT END SET PS-END-OF-ACT-INPUT-FL TO TRUE                    07110015
                  SET CR-ACT-DONE TO TRUE                               07120059
           NOT AT END                                                   07120159
             IF ICRD053-ORIG-CLR-DTE > SPACE                            07121059
               ADD +1 TO DI-CLR-CNTR                                    07130059
             ELSE                                                       07140159
               ADD +1 TO DI-ACT-CNTR                                    07141059
             END-IF                                                     07142059
           END-READ.                                                    07143059
                                                                        07160015
      *----------------------------------------------------------       07170015
      * 7200-READ-CNT-CAN-COUNT-FILE.                                   07180015
      *----------------------------------------------------------       07190015
       7200-READ-CNT-CAN-COUNT-FILE.                                    07200015
           MOVE '7200-READ-CNT-CAN-COUNT-FILE'   TO PV-PARAGRAPH-NAME.  07210015
           READ CNT-CAN-COUNT-FILE                                      07220015
             INTO ICRD056-CANC-IC-COUNTS-FILE                           07230015
           AT END SET PS-END-OF-CAN-INPUT-FL TO TRUE                    07240015
C82402            SET CR-CAN-DONE TO TRUE                               07250099
C82402     NOT AT END                                                   07251099
             ADD +1 TO DI-CAN-CNTR                                      07260099
C82402     END-READ.                                                    07271099
                                                                        07290015
      *----------------------------------------------------------       07300015
      * 8000-SQL-COMMIT.                                                07310015
      *----------------------------------------------------------       07320015
       8000-SQL-COMMIT.                                                 07330015
           MOVE '8000-SQL-COMMIT' TO PV-PARAGRAPH-NAME.                 07340015
                                                                        07350015
           MOVE DI-ACT-CNTR   TO CR-ACT-ACT-READ-CNT.                   07360015
           MOVE DI-CLR-CNTR   TO CR-ACT-CLR-READ-CNT.                   07370015
           MOVE DI-CAN-CNTR   TO CR-ACT-CAN-READ-CNT.                   07380015
           MOVE CR-CHECKPOINT-RESTART-AREA TO TCKRSTINF-WORK-STRG-DESC. 07390021
                                                                        07400015
           IF PS-FIRST-COMMIT                                           07410015
               MOVE '9'        TO TCKRSTCNTL-CKPT-STAT-CODE             07420015
               PERFORM 8160-UPDATE-CHECKPOINT-STATUS                    07430015
               MOVE 'N'        TO PS-FIRST-COMMIT-SW                    07440015
           END-IF.                                                      07450015
           EXEC SQL                                                     07460015
             UPDATE TCKRSTINF                                           07470015
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          07480015
              WHERE JOB_NAME  = :PC-JOB-NAME                            07490015
                AND PLAN_NAME = :PC-PROGRAM-NAME                        07500015
           END-EXEC.                                                    07510015
                                                                        07520015
           EVALUATE TRUE                                                07530015
              WHEN SQLCODE = 0                                          07540015
                  CONTINUE                                              07550015
              WHEN OTHER                                                07560015
               SET PS-PROCESS-STOP    TO TRUE                           07570015
               DISPLAY '** ICKUP054 CHECKPOINT'                         07580015
                       ' HAD SQLCODE OF ' SQLCODE                       07590015
               PERFORM 9999-SQL-ABEND                                   07600015
           END-EVALUATE.                                                07610015
                                                                        07620015
           EXEC SQL                                                     07630015
               COMMIT WORK                                              07640015
           END-EXEC.                                                    07650015
           DISPLAY 'COMMIT COMPLETED'.                                  07660015
           ADD +1 TO DI-COMMIT-CNTR.                                    07670015
           MOVE 1 TO PV-COMMIT-CNTR.                                    07680015
                                                                        07700591
      *----------------------------------------------------------       07701015
      *  SELECT FROM THE CHECKPOINT RESTART CONTROL TABLE TO SEE IF     07710015
      *  THE PROGRAM IS IN A RESTART CONDITION.                         07720015
      *----------------------------------------------------------       07730015
       8100-INIT-CHECKPOINT-SELECT.                                     07740015
           MOVE '8100-INIT-CHECKPOINT-SELECT' TO  PV-PARAGRAPH-NAME.    07750015
                                                                        07760015
           EXEC SQL                                                     07770015
             SELECT                                                     07780015
                  CKPT_STAT_CODE                                        07790015
                 ,CKPT_FRQNCY_QTY                                       07800023
               INTO                                                     07810015
                 :TCKRSTCNTL-CKPT-STAT-CODE                             07820015
                ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                            07830023
               FROM  TCKRSTCNTL                                         07840015
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07850015
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07860015
           END-EXEC.                                                    07870015
                                                                        07880015
           EVALUATE TRUE                                                07890015
              WHEN SQLCODE = 0                                          07900015
                  CONTINUE                                              07910015
              WHEN OTHER                                                07920015
               SET PS-PROCESS-STOP    TO TRUE                           07930015
               DISPLAY '** ICKUP054 SELECT CKPT'                        07940015
                       ' HAD SQLCODE OF ' SQLCODE                       07950015
               PERFORM 9999-SQL-ABEND                                   07980015
           END-EVALUATE.                                                07990015
                                                                        08000015
      ******************************************************************08010015
      *  SELECT FROM THE CHECKPOINT RESTART INFORMATION TABLE TO        08020015
      *  OBTAIN THE PROGRAMS SAVED VARIABLES.                           08030015
      ******************************************************************08040015
       8150-SELECT-RESTART-INFO.                                        08050015
           MOVE '8150-SELECT-RESTART-INFO ' TO  PV-PARAGRAPH-NAME.      08060015
                                                                        08070015
           EXEC SQL                                                     08080015
               SELECT                                                   08090015
                  WORK_STRG_DESC                                        08100015
               INTO                                                     08110015
                 :TCKRSTINF-WORK-STRG-DESC                              08120015
               FROM  TCKRSTINF                                          08130015
              WHERE  JOB_NAME  = :PC-JOB-NAME                           08140015
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       08150015
           END-EXEC.                                                    08160015
                                                                        08170015
           EVALUATE TRUE                                                08180015
              WHEN SQLCODE = 0                                          08190015
                  CONTINUE                                              08200015
              WHEN OTHER                                                08210015
               SET PS-PROCESS-STOP    TO TRUE                           08220015
               DISPLAY '** ICKUP054 SELECT XRST'                        08230015
                       ' HAD SQLCODE OF ' SQLCODE                       08240015
               PERFORM 9999-SQL-ABEND                                   08250015
           END-EVALUATE.                                                08260015
                                                                        08270015
      ******************************************************************08280015
      *  UPDATE THE STATUS ON THE CHECKPOINT CONTROL TABLE              08290015
      ******************************************************************08300015
       8160-UPDATE-CHECKPOINT-STATUS.                                   08310015
           MOVE '8160-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME.   08320015
                                                                        08330015
           EXEC SQL                                                     08340015
             UPDATE  TCKRSTCNTL                                         08350015
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        08360015
              WHERE  JOB_NAME  = :PC-JOB-NAME                           08370015
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       08380015
           END-EXEC.                                                    08390015
                                                                        08400015
           EVALUATE TRUE                                                08410015
              WHEN SQLCODE = 0                                          08420015
                  CONTINUE                                              08430015
              WHEN OTHER                                                08440015
               SET PS-PROCESS-STOP    TO TRUE                           08450015
               DISPLAY '** ICKUP054 UPDATE CKPT'                        08460015
                       ' HAD SQLCODE OF ' SQLCODE                       08470015
               PERFORM 9999-SQL-ABEND                                   08480015
           END-EVALUATE.                                                08490015
                                                                        08500015
                                                                        08510015
      ******************************************************************08520015
      *  GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT CONTROL TABLE     08530015
      ******************************************************************08540015
      *8170-GET-COMMIT-TIMESTAMP.                                       08550015
      *                                                                 08560015
      *    EXEC SQL                                                     08570015
      *      SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       08580015
      *        INTO :PV-COMMIT-TIMESTAMP                                08590015
      *        FROM TCKRSTCNTL                                          08600015
      *       WHERE JOB_NAME  = :PC-JOB-NAME                            08610015
      *         AND PLAN_NAME = :PC-PROGRAM-NAME                        08620015
      *    END-EXEC.                                                    08630015
      *                                                                 08640015
      *    EVALUATE TRUE                                                08650015
      *       WHEN SQLCODE = 0                                          08660015
      *           CONTINUE                                              08670015
      *       WHEN OTHER                                                08680015
      *        SET PS-PROCESS-STOP    TO TRUE                           08690015
      *        DISPLAY '** ICKUP054 SELECT COMMIT TMST'                 08700015
      *                ' HAD SQLCODE OF ' SQLCODE                       08710015
      *        PERFORM 9999-SQL-ABEND                                   08720015
      *    END-EVALUATE.                                                08730015
                                                                        08740015
      *-------------------------------------------------------------    08750015
      * 8200-INST-ACT-SKU-LOC-CNT.                                      08760015
      *      INSERT LAST RUN TIMESTAMP OF TO ICT_CNT_STUP_STAT TABLE    08770015
      *-------------------------------------------------------------    08780015
       8200-INS-ACT-SKU-LOC-CNT.                                        08790015
           MOVE '8200-INS-ACT-SKU-LOC-CNT' TO PV-PARAGRAPH-NAME.        08800015
           EXEC SQL                                                     08810015
            INSERT INTO ICT_SKU_LOC_CNT                                 08811099
                  (LOC_NBR                                              08812099
                  ,SKU_NBR                                              08813099
                  ,CNT_STUP_ID                                          08814099
                  ,LAST_UPD_TMST                                        08815099
                  ,CNT_SEQ_NBR                                          08816099
                  ,LAST_UPD_USR_ID                                      08817099
                  ,LAST_ACTN_CDE                                        08818099
                  ,UNT_RTL_AMT                                          08819099
                  ,LOC_EXPTED_UNT_QTY                                   08819199
                  ,IN_TRNST_UNT_QTY                                     08819299
                  ,ACPTD_TRUK_UNT_QTY                                   08819399
                  ,RTL_SAL_UNT_QTY                                      08819499
                  ,RTL_RET_UNT_QTY                                      08819599
                  ,XFER_IN_UNT_QTY                                      08819699
                  ,XFER_OUT_UNT_QTY                                     08819799
                  ,SCND_UPC_NBR                                         08819899
                  ,SCND_UNT_QTY                                         08819999
                  ,CNT_GP_CDE                                           08820099
                  ,EXEC_TMST                                            08820199
                  ,MDSE_EXPRN_DTE)                                      08820299
            VALUES                                                      08820399
C82402          (:IC054-LOC-NBR                                         08820499
C82402          ,:IC054-SKU-NBR                                         08820599
C82402          ,:IC054-CNT-STUP-ID                                     08820699
C82402          ,:IC054-LAST-UPD-TMST                                   08820799
C82402          ,:IC054-CNT-SEQ-NBR                                     08820899
C82402          ,'SYSTEM'                                               08820999
C82402          ,:IC054-LAST-ACTN-CDE                                   08821099
C82402          ,:IC054-UNT-RTL-AMT                                     08821199
C82402          ,:IC054-LOC-EXPTED-UNT-QTY                              08821299
C82402          ,:IC054-IN-TRNST-UNT-QTY:IC054-TRNST-UNT-QTY-NI         08821399
C82402          ,:IC054-ACPTD-TRUK-UNT-QTY:IC054-TRUK-UNT-QTY-NI        08821499
C82402          ,:IC054-RTL-SAL-UNT-QTY:IC054-RTL-SAL-UNT-QTY-NI        08821599
C82402          ,:IC054-RTL-RET-UNT-QTY:IC054-RTL-RET-UNT-QTY-NI        08821699
C82402          ,:IC054-XFER-IN-UNT-QTY:IC054-XFER-IN-UNT-QTY-NI        08821799
C82402          ,:IC054-XFER-OUT-UNT-QTY:IC054-XFER-OUT-UNT-QTY-NI      08821899
C82402          ,:IC054-SCND-UPC-NBR:IC054-SCND-UPC-NBR-NI              08821999
C82402          ,:IC054-SCND-UNT-QTY:IC054-SCND-UNT-QTY-NI              08822099
C82402          ,:IC054-CNT-GP-CDE:IC054-CNT-GP-CDE-NI                  08822199
C82402          ,:IC054-EXEC-TMST                                       08822299
C82402          ,:IC054-MDSE-EXPRN-DTE:IC054-MDSE-EXPRN-DTE-NI)         08822399
C82402          FOR :PV-INDX1 ROWS                                      08822499
C82402          ATOMIC                                                  08822599
           END-EXEC.                                                    09040015
           EVALUATE TRUE                                                09050015
              WHEN SQLCODE = 0                                          09060015
C82402            ADD  1 TO  PV-COMMIT-CNTR                                     
C82402            MOVE 0 TO  PV-INDX1                                   09071099
              WHEN OTHER                                                09080015
C82402            EXEC SQL                                                      
C82402            GET DIAGNOSTICS                                               
C82402                :PV-NUM-CONDNS = NUMBER                                   
C82402            END-EXEC                                                      
C82402                                                                  12170015
C82402            PERFORM VARYING PV-CNTR FROM 1 BY 1                           
C82402            UNTIL PV-CNTR > PV-NUM-CONDNS                                 
C82402                                                                  12170015
C82402              EXEC SQL                                                    
C82402                GET DIAGNOSTICS CONDITION :PV-CNTR                        
C82402                  :PV-RET-SQLCODE  = DB2_RETURNED_SQLCODE,                
C82402                  :PV-ROW-NUM      = DB2_ROW_NUMBER                       
C82402              END-EXEC                                                    
C82402                                                                          
C82402              IF PV-ROW-NUM NOT = 0                                       
C82402                MOVE PV-RET-SQLCODE TO SQLCODE                            
C82402                DISPLAY 'ROW-NUM       = ' PV-ROW-NUM                     
C82402                DISPLAY 'LOC-NBR       = '                                
C82402                             IC054-LOC-NBR(PV-ROW-NUM)                    
C82402                DISPLAY 'SKU-NBR       = '                                
C82402                             IC054-SKU-NBR(PV-ROW-NUM)                    
C82402                DISPLAY 'CNT-STUP-ID   = '                                
C82402                             IC054-CNT-STUP-ID(PV-ROW-NUM)                
C82402                DISPLAY 'LAST-UPD-TMST = '                                
C82402                             IC054-LAST-UPD-TMST(PV-ROW-NUM)              
C82402                DISPLAY 'CNT-SEQ-NBR   = '                                
C82402                             IC054-CNT-SEQ-NBR(PV-ROW-NUM)                
C82402                DISPLAY 'LAST-ACTN-CDE = '                                
C82402                             IC054-LAST-ACTN-CDE(PV-ROW-NUM)              
C82402                DISPLAY 'UNT-RTL-AMT   = '                                
C82402                             IC054-UNT-RTL-AMT(PV-ROW-NUM)                
C82402                DISPLAY 'EXPTD-UNT-QTY = '                                
C82402                             IC054-LOC-EXPTED-UNT-QTY(PV-ROW-NUM)         
C82402                DISPLAY 'EXEC-TMST     = '                                
C82402                             IC054-EXEC-TMST(PV-ROW-NUM)                  
C82402              END-IF                                                      
C82402            END-PERFORM                                                   
                  SET PS-PROCESS-STOP    TO TRUE                        09140015
                  DISPLAY '** ICKUP054 INSERT ACT ICT_SKU_LOC_CNT'      09150015
                       ' HAD SQLCODE OF ' SQLCODE                       09160015
                  PERFORM 9999-SQL-ABEND                                09170015
           END-EVALUATE.                                                09180015
                                                                        09190015
      *-------------------------------------------------------------    10260015
      * 8260-SEL-SKU-LOC-CNT.                                           10270015
      *      SELECT CURRENT ICT_SKU_LOC_CNT ROW.                        10280015
      *-------------------------------------------------------------    10290015
       8260-SEL-SKU-LOC-CNT.                                            10300015
           MOVE '8260-SEL-SKU-LOC-CNT' TO PV-PARAGRAPH-NAME.            10310015
           EXEC SQL                                                     10320015
C82402      SELECT LAST_ACTN_CDE                                        10381099
                  ,UNT_RTL_AMT                                          10400015
                  ,LOC_EXPTED_UNT_QTY                                   10410015
                  ,IN_TRNST_UNT_QTY                                     10420015
                  ,ACPTD_TRUK_UNT_QTY                                   10430015
                  ,RTL_SAL_UNT_QTY                                      10440015
                  ,RTL_RET_UNT_QTY                                      10450015
                  ,XFER_IN_UNT_QTY                                      10460015
                  ,XFER_OUT_UNT_QTY                                     10470015
                  ,SCND_UPC_NBR                                         10480015
                  ,SCND_UNT_QTY                                         10490015
                  ,CNT_GP_CDE                                           10500015
C82402      INTO :ICT00011-LAST-ACTN-CDE                                10530015
                ,:ICT00011-UNT-RTL-AMT                                  10610015
                ,:ICT00011-LOC-EXPTED-UNT-QTY                           10620015
                ,:ICT00011-IN-TRNST-UNT-QTY:PV-IN-TRNST-UNT-QTY-NI      10630015
                ,:ICT00011-ACPTD-TRUK-UNT-QTY:PV-ACPTD-TRUK-UNT-QTY-NI  10640015
                ,:ICT00011-RTL-SAL-UNT-QTY:PV-RTL-SAL-UNT-QTY-NI        10650015
                ,:ICT00011-RTL-RET-UNT-QTY:PV-RTL-RET-UNT-QTY-NI        10660015
                ,:ICT00011-XFER-IN-UNT-QTY:PV-XFER-IN-UNT-QTY-NI        10670015
                ,:ICT00011-XFER-OUT-UNT-QTY:PV-XFER-OUT-UNT-QTY-NI      10680015
                ,:ICT00011-SCND-UPC-NBR:PV-SCND-UPC-NBR-NI              10690015
                ,:ICT00011-SCND-UNT-QTY:PV-SCND-UNT-QTY-NI              10700015
                ,:ICT00011-CNT-GP-CDE:PV-CNT-GP-CDE-NI                  10710015
            FROM ICT_SKU_LOC_CNT                                        10740099
C82402      WHERE   LOC_NBR       = :ICT00011-LOC-NBR                   10750099
C82402        AND   SKU_NBR       = :ICT00011-SKU-NBR                   10760099
C82402        AND   CNT_STUP_ID   = :ICT00011-CNT-STUP-ID               10770099
C82402        AND   CNT_SEQ_NBR   = :ICT00011-CNT-SEQ-NBR               10780099
C82402      ORDER BY LAST_UPD_TMST DESC                                 10781099
C82402      FETCH FIRST 1 ROW ONLY                                      10782099
           END-EXEC.                                                    10850015
           EVALUATE TRUE                                                10860015
              WHEN SQLCODE = 0                                          10870015
                  CONTINUE                                              10880015
              WHEN SQLCODE = +100                                       10890015
                  SET PS-CLR-ROW-NOT-FOUND TO TRUE                      10900015
              WHEN OTHER                                                10910015
               SET PS-PROCESS-STOP    TO TRUE                           10920015
               DISPLAY '** ICKUP054 SELECT ICT_SKU_LOC_CNT'             10930015
                       ' HAD SQLCODE OF ' SQLCODE                       10940015
               PERFORM 9999-SQL-ABEND                                   10980015
           END-EVALUATE.                                                10990015
                                                                        11000015
      *-------------------------------------------------------------    11660015
C82402* 8350-INS-SKU-LOC-CNT-ST.                                        11670015
      *      INSERT LAST RUN TIMESTAMP OF TO ICT_CNT_STUP_STAT TABLE    11680015
      *-------------------------------------------------------------    11690015
C82402 8350-INS-SKU-LOC-CNT-ST.                                         11700015
C82402     MOVE '8350-INS-SKU-LOC-CNT-ST' TO PV-PARAGRAPH-NAME.         11710015
           EXEC SQL                                                     11720015
             INSERT INTO ICT_LOC_CNT_STAT                               11730015
               (CNT_STAT_CDE                                            11740015
               ,LOC_NBR                                                 11750015
               ,CNT_STUP_ID                                             11760015
               ,CNT_SEQ_NBR                                             11770041
               ,LAST_UPD_TMST                                           11780015
               ,LAST_UPD_USR_ID                                         11790015
               ,LAST_ACTN_CDE)                                          11800015
             VALUES                                                     11810015
               (:ICT00020-CNT-STAT-CDE                                  11881099
               ,:ICT00020-LOC-NBR                                       11882099
               ,:ICT00020-CNT-STUP-ID                                   11883099
               ,:ICT00020-CNT-SEQ-NBR                                   11884099
               ,:ICT00020-LAST-UPD-TMST                                 11885099
               ,'SYSTEM'                                                11886099
               ,'A')                                                    11887099
           END-EXEC.                                                    11890015
           EVALUATE TRUE                                                11900015
              WHEN SQLCODE = 0                                          11910015
                  CONTINUE                                              11930099
              WHEN SQLCODE = -803                                               
      *** THIS CONDITION CAN HAPPEN IF ACTIVE AND CLEARANCE SKUS ARE            
      *** PROCESSED FOR THE SAME IC COUNT AND LOCATION AS THEY ARE NOT          
      *** PROCESSED AT THE SAME TIME.  THIS CONDITION CAN BE IGNORED IN         
      *** THIS CASE.                                                            
               CONTINUE                                                         
              WHEN OTHER                                                12070015
                  SET PS-PROCESS-STOP    TO TRUE                        12080015
                  DISPLAY '** ICKUP054 INSERT ICT_LOC_CNT_STAT'         12090015
                       ' HAD SQLCODE OF ' SQLCODE                       12100015
                  PERFORM 9999-SQL-ABEND                                12150015
           END-EVALUATE.                                                12160015
                                                                        12170015
      *----------------------------------------------------------       12180015
      * 8400-OPEN-CANC-CNT.                                             12190015
      *      OPEN THE CURSOR OF CANCELLED COUNTS.                       12200015
      *----------------------------------------------------------       12210015
       8400-OPEN-CANC-CNT.                                              12220015
           MOVE '8400-OPEN-CANC-CNT'  TO PV-PARAGRAPH-NAME.             12240015
                                                                        12250015
           PERFORM                                                      12260015
               WITH TEST AFTER                                          12270015
               VARYING PV-RETRIES                                       12280015
               FROM 1 BY 1                                              12290015
               UNTIL (PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR      12300015
                      PS-PROCESS-STOP                    OR             12310015
                      SQLCODE = 0)                                      12320015
                                                                        12330015
                 EXEC SQL                                               12340015
                    OPEN CANC-CNT-CSR                                   12350015
                 END-EXEC                                               12360015
                                                                        12370015
               EVALUATE TRUE                                            12380015
                   WHEN SQLCODE = -904                                  12390015
                   WHEN SQLCODE = -911                                  12400015
                   WHEN SQLCODE = -913                                  12410015
                       CONTINUE                                         12420015
                   WHEN SQLCODE = 0                                     12430015
                       CONTINUE                                         12440015
                   WHEN OTHER                                           12450015
                       SET PS-PROCESS-STOP TO TRUE                      12460015
               END-EVALUATE                                             12470015
           END-PERFORM.                                                 12480015
                                                                        12490015
           IF PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR              12500015
              PS-PROCESS-STOP                                           12510015
               SET PS-PROCESS-STOP    TO TRUE                           12520015
               DISPLAY '** ICKUP054 OPEN OF CANC-CNT CURSOR FOR '       12530015
                       ' HAD SQLCODE OF ' SQLCODE                       12540015
               PERFORM 9999-SQL-ABEND                                   12550015
           END-IF.                                                      12560015
                                                                        12570015
      *----------------------------------------------------------       12580015
      * 8410-FETCH-CANC-CNT.                                            12590015
      *      FETCH A CANCELLED COUNTS ROW.                              12600015
      *----------------------------------------------------------       12610015
       8410-FETCH-CANC-CNT.                                             12620015
           MOVE '8410-FETCH-CANC-CNT'  TO PV-PARAGRAPH-NAME.            12640015
                                                                        12650015
           PERFORM                                                      12660015
               WITH TEST AFTER                                          12670015
               VARYING PV-RETRIES                                       12680015
               FROM 1 BY 1                                              12690015
               UNTIL (PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR      12700015
                      PS-PROCESS-STOP                   OR              12710015
                      SQLCODE = 0                       OR              12720015
                      SQLCODE = +100)                                   12730015
                                                                        12740015
                 EXEC SQL                                               12750015
                    FETCH CANC-CNT-CSR                                  12760015
                    INTO  :ICT00020-CNT-STAT-CDE                        12770015
                         ,:ICT00020-LOC-NBR                             12780015
                         ,:ICT00020-CNT-STUP-ID                         12800042
                         ,:ICT00020-CNT-SEQ-NBR                         12801049
                         ,:ICT00020-LAST-UPD-TMST                       12810032
                 END-EXEC                                               12820015
                                                                        12830015
                EVALUATE TRUE                                           12840015
                    WHEN SQLCODE = -904                                 12850015
                    WHEN SQLCODE = -911                                 12860015
                    WHEN SQLCODE = -913                                 12870015
                        CONTINUE                                        12880015
                    WHEN SQLCODE = ZERO                                 12890015
                        CONTINUE                                        12891065
                    WHEN SQLCODE = +100                                 12950015
                        SET PS-NO-MORE-CANC-CNT    TO TRUE              12960015
                    WHEN OTHER                                          12970015
                        SET PS-PROCESS-STOP        TO TRUE              12980015
                END-EVALUATE                                            12990015
           END-PERFORM.                                                 13000015
                                                                        13010015
           IF PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR              13020015
              PS-PROCESS-STOP                                           13030015
               SET PS-PROCESS-STOP    TO TRUE                           13040015
               DISPLAY '** ICKUP054 FETCH OF CANC-CNT CURSOR'           13050015
                       ' HAD SQLCODE OF ' SQLCODE                       13060015
               PERFORM 9999-SQL-ABEND                                   13070015
           END-IF.                                                      13080015
                                                                        13090015
      *-------------------------------------------------------------    13100015
      * 8420-CLOSE-CANC-CNT.                                            13110015
      *      CLOSE THE CANCELLED COUNTS CURSOR.                         13120015
      *-------------------------------------------------------------    13130015
       8420-CLOSE-CANC-CNT.                                             13140015
           MOVE '8420-CLOSE-CANC-CNT'  TO PV-PARAGRAPH-NAME.            13160015
                                                                        13170015
           PERFORM                                                      13180015
               WITH TEST AFTER                                          13190015
               VARYING PV-RETRIES                                       13200015
               FROM 1 BY 1                                              13210015
               UNTIL (PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR      13220015
                      PS-PROCESS-STOP                    OR             13230015
                      SQLCODE = 0)                                      13240015
                                                                        13250015
                 EXEC SQL                                               13260015
                    CLOSE CANC-CNT-CSR                                  13270015
                 END-EXEC                                               13280015
                                                                        13290015
                EVALUATE TRUE                                           13300015
                   WHEN SQLCODE = -904                                  13310015
                   WHEN SQLCODE = -911                                  13320015
                   WHEN SQLCODE = -913                                  13330015
                       CONTINUE                                         13340015
                   WHEN SQLCODE = 0                                     13350015
                       CONTINUE                                         13360015
                   WHEN OTHER                                           13370015
                       SET PS-PROCESS-STOP TO TRUE                      13380015
                END-EVALUATE                                            13390015
           END-PERFORM.                                                 13400015
                                                                        13410015
           IF PV-RETRIES GREATER PC-MAXIMUM-RETRY-COUNT OR              13420015
              PS-PROCESS-STOP                                           13430015
               SET PS-PROCESS-STOP    TO TRUE                           13440015
               DISPLAY '** ICKUP054 CLOSE OF CANC-CNT CURSOR'           13450015
                       ' HAD SQLCODE OF ' SQLCODE                       13460015
               PERFORM 9999-SQL-ABEND                                   13470015
           END-IF.                                                      13480015
                                                                        14980032
      *----------------------------------------------------------       14990032
      *  SELECT FROM XST_LOC TO GET A STORE TIMEZONE.                   15000032
      *----------------------------------------------------------       15010032
       8500-SELECT-XST-LOC.                                             15020032
C82402     MOVE '8500-XST-LOC' TO  PV-PARAGRAPH-NAME.                   15030032
           EXEC SQL                                                     15050032
C82402       SELECT STD_TM_ZN_CDE                                       15060032
                   ,DST_IND                                             15080032
C82402       INTO  :XST00001-STD-TM-ZN-CDE:PV-STD-TM-ZN-CDE-NI          15090032
                  ,:XST00001-DST-IND:PV-DST-IND-NI                      15110033
             FROM   XST_LOC                                             15120032
             WHERE  LOC_NBR   = :XST00001-LOC-NBR                       15130032
           END-EXEC .                                                   15140032
                                                                        15150032
           EVALUATE TRUE                                                15160032
            WHEN SQLCODE = 0                                            15170032
              IF PV-DST-IND-IS-NULL                                     15180033
                MOVE 'Y' TO XST00001-DST-IND                            15190033
              END-IF                                                    15200033
              IF PV-STD-TM-ZN-CDE-IS-NULL                               15210033
                MOVE 'CST' TO XST00001-STD-TM-ZN-CDE                    15220033
              END-IF                                                    15230033
C82402        MOVE XST00001-STD-TM-ZN-CDE TO ICWS010-TZC-TZ-IN-TM-ZN-CDE        
C82402        MOVE XST00001-DST-IND       TO ICWS010-TZC-TZ-IND-ST-IND          
C82402        PERFORM 9000-TIMEZONE-CONVERSION                                  
            WHEN OTHER                                                  15240032
              SET PS-PROCESS-STOP    TO TRUE                            15250032
              DISPLAY '** ICKUP054 SELECT XST_LOC'                      15260032
                      ' HAD SQLCODE OF ' SQLCODE                        15270032
              PERFORM 9999-SQL-ABEND                                    15280032
           END-EVALUATE.                                                15290032
                                                                        15300032
      *----------------------------------------------------------       15310039
      *  SELECT TO GET NEXT SEQ-NBR FOR ICT-LOC-CNT-STAT TABLE.         15320043
      *----------------------------------------------------------       15330039
       8510-SELECT-SEQ-NBR.                                             15340039
           MOVE '8510-SELECT-SEQ-NBR' TO  PV-PARAGRAPH-NAME.            15350039
                                                                        15360039
           EXEC SQL                                                     15370039
            SELECT COALESCE(MAX(CNT_SEQ_NBR)+1,1)                       15380039
               INTO                                                     15390039
                  :ICT00020-CNT-SEQ-NBR                                 15400041
            FROM ICT_LOC_CNT_STAT A                                     15410041
            WHERE A.CNT_STUP_ID   = :ICT00020-CNT-STUP-ID               15420041
           END-EXEC.                                                    15430039
                                                                        15440039
           EVALUATE TRUE                                                15450039
              WHEN SQLCODE = 0                                          15460039
                CONTINUE                                                15470039
              WHEN OTHER                                                15480039
               SET PS-PROCESS-STOP    TO TRUE                           15490039
               DISPLAY '** ICKUP054 SELECT SEQ NBR'                     15500039
                       ' HAD SQLCODE OF ' SQLCODE                       15510039
               PERFORM 9999-SQL-ABEND                                   15520039
           END-EVALUATE.                                                15530039
                                                                        15540039
      *----------------------------------------------------------       15550079
      *  SELECT TO GET CURR SEQ-NBR FOR ICT-LOC-CNT-STAT TABLE.         15550181
      *----------------------------------------------------------       15550279
       8520-SELECT-CURR-SEQ-NBR.                                        15550379
           MOVE '8520-SELECT-CURR-SEQ-NBR' TO  PV-PARAGRAPH-NAME.       15550479
                                                                        15550579
           EXEC SQL                                                     15550679
            SELECT COALESCE(MAX(CNT_SEQ_NBR),1)                         15550779
               INTO                                                     15550879
                  :ICT00020-CNT-SEQ-NBR                                 15550979
            FROM ICT_LOC_CNT_STAT A                                     15551079
            WHERE A.CNT_STUP_ID   = :ICT00020-CNT-STUP-ID               15551179
           END-EXEC.                                                    15551279
                                                                        15551379
           EVALUATE TRUE                                                15551479
              WHEN SQLCODE = 0                                          15551579
                CONTINUE                                                15551679
              WHEN OTHER                                                15551779
               SET PS-PROCESS-STOP    TO TRUE                           15551879
               DISPLAY '** ICKUP054 SELECT CURR SEQ NBR'                15551979
                       ' HAD SQLCODE OF ' SQLCODE                       15552079
               PERFORM 9999-SQL-ABEND                                   15552179
           END-EVALUATE.                                                15552279
                                                                        15552379
      *----------------------------------------------------------       15552481
      *  SELECT TO CHECK FOR COMPLETED OR CANCELLED COUNT.              15552581
      *----------------------------------------------------------       15552681
       8530-CHECK-COMP-CANC-COUNT.                                      15552781
           MOVE '8530-CHECK-COMP-CANC-COUNT' TO  PV-PARAGRAPH-NAME.     15552881
                                                                        15552981
           EXEC SQL                                                     15553081
            SELECT 'Y'                                                  15553181
            INTO :PV-COMP-CAN-COUNT-SW                                  15553481
            FROM ICT_LOC_CNT_STAT  A                                    15553581
PK2823      WHERE A.CNT_STAT_CDE IN ('CAN','COM')                       15553684
              AND    A.CNT_STUP_ID     = :ICT00020-CNT-STUP-ID          15553781
              AND    A.CNT_SEQ_NBR     = :ICT00020-CNT-SEQ-NBR          15553981
              AND    A.LOC_NBR         = :ICT00020-LOC-NBR              15554081
            FETCH FIRST ROW ONLY                                        15554182
           END-EXEC.                                                    15554381
                                                                        15554481
           EVALUATE TRUE                                                15554581
              WHEN SQLCODE = 0                                          15554681
                CONTINUE                                                15554781
              WHEN SQLCODE = +100                                       15554881
               MOVE 'N' TO PV-COMP-CAN-COUNT-SW                         15554981
              WHEN OTHER                                                15555481
               SET PS-PROCESS-STOP    TO TRUE                           15555581
               DISPLAY '** ICKUP054 CHECK FOR COMP/CAN COUNT'           15555681
                       ' HAD SQLCODE OF ' SQLCODE                       15555781
               PERFORM 9999-SQL-ABEND                                   15555881
           END-EVALUATE.                                                15555981
                                                                        15556081
      ******************************************************************15556181
           COPY ICPD010.                                                15557081
      ******************************************************************15570039
      ******************************************************************15580039
      * 9990-DISPLAY-CONTROL-INFO.                                     *15590039
      *  ==> PROCESSES:                                                *15600015
      *    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *15610015
      ******************************************************************15620015
       9990-DISPLAY-CONTROL-INFO.                                       15630015
                                                                        15640015
           DISPLAY '********************************************'.      15680015
           DISPLAY '**** RUN STATISTICS FOR PROGRAM ICKUP054 ***'.      15690015
           DISPLAY '****'.                                              15700015
           DISPLAY 'ACTIVE COUNTS PROCESSED    :' DI-ACT-CNTR.          15710015
           DISPLAY 'CLEARANCE COUNTS PROCESSED :' DI-CLR-CNTR.          15720015
           DISPLAY 'CANCELLED COUNTS PROCESSED :' DI-CAN-CNTR.          15730015
C82402     DISPLAY 'NO OF INSERTS HAPPENED IN ICT_LOC_CNT_STAT TABLE            
C82402-            '      :' PV-I-CNTR.                                         
C82402     DISPLAY 'NO OF MULTI-ROW INSERTS HAPPENED IN ICT_SKU_LOC_CNT         
C82402-            'TABLE :' PV-J-CNTR.                                         
C82402     DISPLAY 'MISSED INSERTS HAPPENED IN ICT_SKU_LOC_CNT TABLE            
C82402-            '      :' PV-J-MSD-INS-CNTR.                                 
           DISPLAY 'COMMITS TAKEN              :' DI-COMMIT-CNTR.       15740015
           DISPLAY '****'.                                              15750015
           DISPLAY '**** RUN STATS END FOR PROGRAM ICKUP054  ***'.      15760015
           DISPLAY '********************************************'.      15770015
                                                                        15780015
      ******************************************************************15790015
      * 9999-SQL-ABEND.                                                *15800015
      *  ==> PROCESSES:                                                *15810015
      *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *15820015
      ******************************************************************15830015
       9999-SQL-ABEND.                                                  15840015
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                          15860015
                                                                        15870015
           DISPLAY '** ABEND **'.                                       15880015
           DISPLAY 'ICKUP054 - ABEND'.                                  15890015
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                    15900015
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.           15910015
           DISPLAY 'SQL CODE  = ' PV-SQLCODE-DISPLAY.                   15920015
           PERFORM 9990-DISPLAY-CONTROL-INFO.                           15930015
                                                                        15940015
           COPY DPPD004.                                                15950015
                                                                        15960015
           MOVE +4013 TO ABEND-CODE.                                    15970015
           CALL 'ILBOABN0' USING ABEND-CODE.                            15980015
                                                                        15990015
