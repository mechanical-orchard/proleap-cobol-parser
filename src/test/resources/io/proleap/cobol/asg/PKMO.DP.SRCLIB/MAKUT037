       IDENTIFICATION DIVISION.                                         12/18/95
       PROGRAM-ID.    MAKUT037.                                         MAKUT037
       AUTHOR.        LINDA GRIESBACH.                                     LV005
      ******************************************************************      32
      * THE PURPOSE OF THIS PROGRAM IS TO CREATE OH/OO RECORDS THAT    *      32
      * WILL BE LOADED INTO THE TSKUINRU ORACLE TABLE.  THE INVENTORY  *      32
      * LOAD FILE CONTAINS A RECORD FOR EACH VND/VST/STORE             *      32
      * COMBINATION.  ALONG WITH THE VND/VST/STORE, THE RECORD         *      32
      * CONTAINS THE OH AND OO QUANTITIES FOR THAT VND/VST/STORE.      *      32
      *                                                                *      32
      * THIS PROGRAM DETERMINES THE CORRESPONDING ITEM CLUSTER AND     *      32
      * AND ORG UNIT ID'S FOR EACH VND/VST/STORE RECORD.  IT THEN      *      32
      * CREATES AN OUTPUT RECORD CONTAINING THE ITEM/ORG CLUSTER       *      32
      * ID'S ALONG WITH THE OH/OO QUANTITIES.                          *      32
      ******************************************************************      32
      *  WR25478 - LINDA GRIESBACH                                     *      32
      *            STORE NBR EXPANSION                                 *      32
      ******************************************************************      32
221500*  04/08/2019 - CHG0221500 - JIM ARENDT                          *      32
221500*            MAINFRAME REFACTORING - INCREASING OCCURS OF        *      32
221500*            STORE TABLE (ORG-CLUSTR-ID) FROM 2000 TO 9999       *      32
221500*            TO ALLOW FOR 6666 (STOREFRONT) AND TEST LAB STORES. *      32
221500******************************************************************      32
                                                                        MAKUT037
       ENVIRONMENT DIVISION.                                            MAKUT037
       INPUT-OUTPUT SECTION.                                            MAKUT037
       FILE-CONTROL.                                                    MAKUT037
                                                                        MAKUT037
           SELECT VST-STORE-INVENTORY-FILE                              MAKUT037
               ASSIGN TO INP01.                                         MAKUT037
                                                                        MAKUT037
           SELECT ORG-CLUSTR-FILE                                       MAKUT037
               ASSIGN TO INP02.                                         MAKUT037
                                                                        MAKUT037
           SELECT ITEM-CLUSTR-FILE                                      MAKUT037
               ASSIGN TO INP03.                                         MAKUT037
                                                                        MAKUT037
           SELECT INVENTORY-LOAD-FILE                                   MAKUT037
               ASSIGN TO OUT01.                                         MAKUT037
                                                                        MAKUT037
       DATA DIVISION.                                                   MAKUT037
       FILE SECTION.                                                    MAKUT037
       FD  VST-STORE-INVENTORY-FILE                                     MAKUT037
           RECORDING MODE IS F                                          MAKUT037
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT037
       01  VST-STORE-INVENTORY-RECORD  PIC X(109).                         CL**5
                                                                        MAKUT037
       FD  ORG-CLUSTR-FILE                                              MAKUT037
           RECORDING MODE IS F                                          MAKUT037
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT037
       01  ORG-CLUSTR-RECORD           PIC X(13).                          CL**5
                                                                        MAKUT037
       FD  ITEM-CLUSTR-FILE                                             MAKUT037
           RECORDING MODE IS F                                          MAKUT037
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT037
       01  ITEM-CLUSTR-RECORD          PIC X(55).                          CL**5
                                                                        MAKUT037
       FD  INVENTORY-LOAD-FILE                                          MAKUT037
           RECORDING MODE IS F                                          MAKUT037
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT037
       01  INVENTORY-LOAD-RECORD       PIC X(41).                          CL**5
                                                                        MAKUT037
                                                                        MAKUT037
       WORKING-STORAGE SECTION.                                         MAKUT037
                                                                        MAKUT037
       01  PROGRAM-VARIABLES.                                           MAKUT037
           05  PV-INV-READ-COUNT       PIC S9(13) VALUE +0 COMP.        MAKUT037
           05  PV-INV-WRITTEN-COUNT    PIC S9(13) VALUE +0 COMP.        MAKUT037
           05  PV-NO-ITEM-COUNT        PIC S9(9)  VALUE +0 COMP.        MAKUT037
           05  PV-NO-ORG-COUNT         PIC S9(9)  VALUE +0 COMP.        MAKUT037
           05  PV-SUB                  PIC S9(4)  COMP-3.                       
           05  PV-ABEND-CODE           PIC S9(4)  VALUE +0 COMP.                
           05  PV-LAST-DEPT            PIC X(3)   VALUE SPACES.                 
           05  PV-LAST-VND             PIC X(20)  VALUE SPACES.                 
           05  PV-PREV-CLUSTR-KEY.                                              
               10  PV-PREV-OWNER-DEPT  PIC X(3)   VALUE SPACES.                 
               10  FILLER              PIC X(1)   VALUE SPACE.                  
               10  PV-PREV-CLUSTR-VND                                           
                                       PIC X(20)  VALUE SPACES.                 
               10  FILLER              PIC X(1)   VALUE SPACE.                  
               10  PV-PREV-CLUSTR-VST                                           
                                       PIC X(20)  VALUE SPACES.                 
           05  PV-VST-CLUSTR-KEY.                                               
               10  PV-VST-CLUSTR-DEPT  PIC X(3).                                
               10  FILLER              PIC X(1)   VALUE SPACE.                  
               10  PV-VST-CLUSTR-OWNER-VENDOR                                   
                                       PIC X(20).                               
               10  FILLER              PIC X(1)   VALUE SPACE.                  
               10  PV-VST-CLUSTR-ITEM-NAME                                      
                                       PIC X(20).                               
                                                                                
       01  PROGRAM-SWITCHES.                                            MAKUT037
           05  PS-VST-STORE-INV-EOF-SW PIC X(1)   VALUE 'N'.            MAKUT037
               88 PS-VST-STORE-INV-EOF            VALUE 'Y'.            MAKUT037
               88 PS-VST-STORE-INV-NOT-EOF        VALUE 'N'.            MAKUT037
           05  PS-ORG-CLUSTR-EOF-SW    PIC X(1)   VALUE 'N'.            MAKUT037
               88 PS-ORG-CLUSTR-EOF               VALUE 'Y'.            MAKUT037
               88 PS-ORG-CLUSTR-NOT-EOF           VALUE 'N'.            MAKUT037
           05  PS-ITEM-CLUSTR-EOF-SW   PIC X(1)   VALUE 'N'.            MAKUT037
               88 PS-ITEM-CLUSTR-EOF              VALUE 'Y'.            MAKUT037
               88 PS-ITEM-CLUSTR-NOT-EOF          VALUE 'N'.            MAKUT037
           05  PS-FIRST-ORG-CLUSTR-SW  PIC X(1)   VALUE 'Y'.            MAKUT037
               88 PS-FIRST-ORG-CLUSTR             VALUE 'Y'.            MAKUT037
               88 PS-NOT-FIRST-ORG-CLUSTR         VALUE 'N'.            MAKUT037
           05  PS-FIRST-ITEM-CLUSTR-SW PIC X(1)   VALUE 'Y'.            MAKUT037
               88 PS-FIRST-ITEM-CLUSTR            VALUE 'Y'.            MAKUT037
               88 PS-NOT-FIRST-ITEM-CLUSTR        VALUE 'N'.            MAKUT037
           05  PS-ITEM-CLUSTR-MATCH-SW PIC X(1)   VALUE 'N'.            MAKUT037
               88 PS-ITEM-CLUSTR-MATCH            VALUE 'Y'.            MAKUT037
               88 PS-ITEM-CLUSTR-NO-MATCH         VALUE 'N'.            MAKUT037
           05  PS-ITEM-CLUSTR-FILE-SW  PIC X(1)   VALUE 'Y'.            MAKUT037
               88 PS-ITEM-CLUSTR-IN-FILE          VALUE 'Y'.            MAKUT037
               88 PS-ITEM-CLUSTR-NOT-IN-FILE      VALUE 'N'.            MAKUT037
                                                                        MAKUT037
       01  ORG-CLUSTR-TABLE.                                                    
221500*    05  ORG-CLUSTR-ID           OCCURS 2000 TIMES                        
221500     05  ORG-CLUSTR-ID           OCCURS 9999 TIMES                        
                                       PIC 9(9).                                
                                                                                
       01  ERROR-MESSAGE.                                                       
           05  FILLER                  PIC X(29)                                
                  VALUE '*** ERROR IN PROGRAM MAKUT037'.                        
           05  FILLER                  PIC X(14)                                
                  VALUE '; ERROR CODE ='.                                       
           05  EM-ERROR-CODE           PIC --------9                            
                                                  VALUE ZERO.                   
           05  FILLER                  PIC X(15)                                
                  VALUE '; ERROR DESC ='.                                       
           05  EM-ERROR-DESC           PIC X(65)  VALUE SPACES.                 
                                                                                
       01  LOG-MESSAGE.                                                 MAKUT037
           05  FILLER                  PIC X(23)                        MAKUT037
                   VALUE '*** PROGRAM MAKUT037 - '.                     MAKUT037
           05  LM-MESSAGE              PIC X(109) VALUE SPACES.         MAKUT037
                                                                        MAKUT037
       01  OWNER-NOT-FOUND-LOG-MSG.                                     MAKUT037
           05  FILLER                  PIC X(29)                        MAKUT037
                   VALUE 'OWNER ID ROW NOT FOUND FOR = '.               MAKUT037
           05  INF-LM-OWNER            PIC X(3)   VALUE SPACES.         MAKUT037
                                                                        MAKUT037
       01  VND-NOT-FOUND-LOG-MSG.                                       MAKUT037
           05  FILLER                  PIC X(27)                        MAKUT037
                   VALUE 'VND ID ROW NOT FOUND FOR = '.                 MAKUT037
           05  INF-LM-VND              PIC X(20)  VALUE SPACES.         MAKUT037
           05  FILLER                  PIC X(14)                                
                   VALUE ' - DEPT NBR = '.                                      
           05  INF-LM-VND-OWNER        PIC X(3)  VALUE SPACES.          MAKUT037
                                                                        MAKUT037
       01  ITEM-NOT-FOUND-LOG-MSG.                                      MAKUT037
           05  FILLER                  PIC X(37)                        MAKUT037
                   VALUE 'ITEM CLUSTER ROW NOT FOUND FOR = '.           MAKUT037
           05  INF-LM-ITEM-NAME        PIC X(20)  VALUE SPACES.         MAKUT037
           05  FILLER                  PIC X(12)                                
                   VALUE ' - VENDOR = '.                                        
           05  INF-LM-ITEM-VND         PIC X(20)  VALUE SPACES.         MAKUT037
           05  FILLER                  PIC X(14)                                
                   VALUE ' - DEPT NBR = '.                                      
           05  INF-LM-ITEM-OWNER       PIC X(3)  VALUE SPACES.          MAKUT037
                                                                                
       01  RUN-SUMMARY-LOG-MSG1.                                        MAKUT037
           05  FILLER                  PIC X(35)                        MAKUT037
                   VALUE 'VST STORE INVENTORY RECORDS READ = '.         MAKUT037
           05  RSLM1-VST-STOR-INV-RECS PIC Z(12)9 VALUE ZERO.           MAKUT037
                                                                        MAKUT037
       01  RUN-SUMMARY-LOG-MSG2.                                        MAKUT037
           05  FILLER                  PIC X(33)                        MAKUT037
                   VALUE 'INVENTORY LOAD RECORDS WRITTEN = '.           MAKUT037
           05  RSLM2-INV-LOAD-RECS     PIC Z(12)9 VALUE ZERO.           MAKUT037
                                                                        MAKUT037
       01  RUN-SUMMARY-LOG-MSG3.                                        MAKUT037
           05  FILLER                  PIC X(43)                        MAKUT037
                   VALUE 'NUMBER OF VST STORE INVENTORY RECORDS DROPP'. MAKUT037
           05  FILLER                  PIC X(39)                        MAKUT037
                   VALUE 'ED DUE TO NO MATCHING ITEM CLUSTER ID= '.     MAKUT037
           05  RSLM3-EXTRACT-NO-ITEM-RECS                               MAKUT037
                                       PIC Z(8)9   VALUE ZERO.                  
                                                                        MAKUT037
       01  RUN-SUMMARY-LOG-MSG4.                                        MAKUT037
           05  FILLER                  PIC X(38)                        MAKUT037
                   VALUE 'NUMBER OF VST STORE INVENTORY RECORDS '.      MAKUT037
           05  FILLER                  PIC X(38)                        MAKUT037
                   VALUE 'DROPPED DUE TO NO MATCHING ORG UNIT = '.      MAKUT037
           05  RSLM4-STORE-NO-ORG-RECS PIC Z(8)9   VALUE ZERO.          MAKUT037
                                                                        MAKUT037
       01  PV-ORG-CLUSTR-RECORD.                                                
           05  PV-ORG-CLUSTR-STORE     PIC X(4).                                
           05  PV-ORG-CLUSTR-ID        PIC 9(9).                                
                                                                        MAKUT037
       01  PV-ITEM-CLUSTR-RECORD.                                               
           05  PV-ITEM-CLUSTR-KEY.                                              
               10  PV-ITEM-CLUSTR-DEPT PIC X(3).                                
               10  FILLER              PIC X(1).                                
               10  PV-ITEM-CLUSTR-VND  PIC X(20).                               
               10  FILLER              PIC X(1).                                
               10  PV-ITEM-CLUSTR-VST  PIC X(20).                               
           05  FILLER                  PIC X(1).                                
           05  PV-ITEM-CLUSTR-ID       PIC 9(9).                                
                                                                        MAKUT037
       01  PV-PREV-ITEM-CLUSTR-RECORD.                                          
           05  PV-PREV-ITEM-CLUSTR-KEY.                                         
               10  PV-PREV-ITEM-CLUSTR-DEPT                                     
                                       PIC X(3).                                
               10  FILLER              PIC X(1).                                
               10  PV-PREV-ITEM-CLUSTR-VND                                      
                                       PIC X(20).                               
               10  FILLER              PIC X(1).                                
               10  PV-PREV-ITEM-CLUSTR-VST                                      
                                       PIC X(20).                               
           05  FILLER                  PIC X(1).                                
           05  PV-PREV-ITEM-CLUSTR-ID  PIC 9(9).                                
                                                                                
      * FIELDS FOR THE VST STORE INVENTORY EXTRACT FILE                 MAKUT037
           COPY MARD015.                                                MAKUT037
                                                                        MAKUT037
      * FIELDS FOR THE INVENTORY LOAD FILE                              MAKUT037
           COPY MARD016.                                                MAKUT037
                                                                        MAKUT037
                                                                        MAKUT037
       EJECT                                                            MAKUT037
       PROCEDURE DIVISION.                                              MAKUT037
                                                                        MAKUT037
       0000-MAINLINE.                                                   MAKUT037
                                                                        MAKUT037
           PERFORM 1000-INITIALIZE                                      MAKUT037
              THRU 1000-EXIT.                                                   
                                                                        MAKUT037
           PERFORM 2000-CREATE-OH-OO-FILE                               MAKUT037
              THRU 2000-EXIT                                                    
              UNTIL PS-VST-STORE-INV-EOF.                                       
                                                                        MAKUT037
           PERFORM 3000-CLOSE                                           MAKUT037
              THRU 3000-EXIT.                                                   
                                                                        MAKUT037
           DISPLAY 'MAKUT037 - SUCCESSFUL END'.                         MAKUT037
           MOVE ZERO TO RETURN-CODE.                                    MAKUT037
           GOBACK.                                                      MAKUT037
                                                                        MAKUT037
       0000-EXIT.                                                       MAKUT037
           EXIT.                                                                
                                                                        MAKUT037
                                                                        MAKUT037
       1000-INITIALIZE.                                                 MAKUT037
                                                                        MAKUT037
           OPEN INPUT  VST-STORE-INVENTORY-FILE                         MAKUT037
                       ORG-CLUSTR-FILE                                  MAKUT037
                       ITEM-CLUSTR-FILE                                 MAKUT037
                OUTPUT INVENTORY-LOAD-FILE.                                     
                                                                        MAKUT037
           INITIALIZE ORG-CLUSTR-TABLE.                                         
                                                                                
           PERFORM 1100-LOAD-ORG-CLUSTR-FILE                                    
              THRU 1100-EXIT                                                    
             UNTIL PS-ORG-CLUSTR-EOF.                                           
                                                                                
           PERFORM 9997-READ-ITEM-CLUSTR-FILE                                   
              THRU 9997-EXIT.                                                   
                                                                                
           PERFORM 9998-READ-VST-STORE-INV-FILE                                 
              THRU 9998-EXIT.                                                   
                                                                        MAKUT037
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       1100-LOAD-ORG-CLUSTR-FILE.                                               
                                                                        MAKUT037
           READ ORG-CLUSTR-FILE                                                 
               INTO PV-ORG-CLUSTR-RECORD                                        
                   AT END                                                       
                       IF PS-FIRST-ORG-CLUSTR                                   
                           MOVE -1003 TO EM-ERROR-CODE                          
                                         PV-ABEND-CODE                          
                           MOVE 'EMPTY ORG CLUSTER TABLE'                       
                                      TO EM-ERROR-DESC                          
                           PERFORM 9999-ABEND                                   
                              THRU 9999-EXIT                                    
                       ELSE                                                     
                           SET PS-ORG-CLUSTR-EOF TO TRUE                        
                           GO TO 1100-EXIT                                      
                       END-IF                                                   
           END-READ.                                                            
                                                                        MAKUT037
           IF PS-FIRST-ORG-CLUSTR                                               
               SET PS-NOT-FIRST-ORG-CLUSTR TO TRUE                              
           END-IF.                                                              
                                                                                
           IF PV-ORG-CLUSTR-STORE IS NUMERIC                                    
             AND PV-ORG-CLUSTR-STORE > ZERO                                     
               MOVE PV-ORG-CLUSTR-STORE TO PV-SUB                               
               MOVE PV-ORG-CLUSTR-ID    TO ORG-CLUSTR-ID (PV-SUB)               
           END-IF.                                                              
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                        MAKUT037
                                                                        MAKUT037
       2000-CREATE-OH-OO-FILE.                                          MAKUT037
                                                                        MAKUT037
      * THE OH/OO INPUT FILE IS SORTED BY VND/VST/STORE -               MAKUT037
      * IT MAY CONTAIN THE SAME VND/VST FOR MULTIPLE STORES -           MAKUT037
      * SO MULTIPLE INPUT RECORDS COULD BE BYPASSED                     MAKUT037
      * IF THE VND/VST IS NOT IN THE ITEM-CLUSTR-FILE                   MAKUT037
                                                                        MAKUT037
           IF PV-VST-CLUSTR-KEY = PV-ITEM-CLUSTR-KEY                    MAKUT037
             OR PV-VST-CLUSTR-KEY < PV-ITEM-CLUSTR-KEY                          
             OR PS-ITEM-CLUSTR-EOF                                              
               NEXT SENTENCE                                                    
           ELSE                                                                 
               SET PS-ITEM-CLUSTR-NO-MATCH TO TRUE                              
               SET PS-ITEM-CLUSTR-IN-FILE  TO TRUE                              
               PERFORM 9997-READ-ITEM-CLUSTR-FILE                               
                  THRU 9997-EXIT                                                
                 UNTIL PS-ITEM-CLUSTR-MATCH                                     
                    OR PS-ITEM-CLUSTR-NOT-IN-FILE                               
                    OR PS-ITEM-CLUSTR-EOF                                       
           END-IF.                                                              
                                                                                
                                                                        MAKUT037
           IF PV-VST-CLUSTR-KEY = PV-ITEM-CLUSTR-KEY                            
               MOVE MA015-STORE TO PV-SUB                                       
               MOVE ORG-CLUSTR-ID (PV-SUB)                                      
                                TO MA016-ORG-UNIT-CLUSTER-ID                    
               IF MA016-ORG-UNIT-CLUSTER-ID NOT = ZERO                          
                   MOVE PV-ITEM-CLUSTR-ID TO MA016-ITEM-CLUSTER-ID              
                   MOVE MA015-ONHAND-QTY  TO MA016-ONHAND-QTY                   
                   MOVE MA015-ONORD-QTY   TO MA016-ONORD-QTY                    
                                                                                
                   WRITE INVENTORY-LOAD-RECORD                                  
                       FROM MA016-INVENTORY-LOAD                                
                   ADD +1 TO PV-INV-WRITTEN-COUNT                               
               ELSE                                                             
                   ADD +1 TO PV-NO-ORG-COUNT                                    
               END-IF                                                           
           ELSE                                                                 
               IF PV-VST-CLUSTR-KEY NOT = PV-PREV-CLUSTR-KEY                    
                   PERFORM 2010-DETERMINE-ERROR                                 
                      THRU 2010-EXIT                                            
                   ADD +1 TO PV-NO-ITEM-COUNT                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 9998-READ-VST-STORE-INV-FILE                                 
              THRU 9998-EXIT.                                                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2010-DETERMINE-ERROR.                                                    
                                                                                
      ***  DETERMINE WHAT FIELDS DIDN'T HAVE A MATCH                            
      ***  I.E. THEY DIDN'T EXIST ON THE ITEM-CLUSTR-FILE                       
                                                                                
           IF (PS-ITEM-CLUSTR-EOF                                               
               AND MA015-OWNER-DEPT NOT = PV-LAST-DEPT)                         
             OR (MA015-OWNER-DEPT NOT = PV-LAST-DEPT                            
                 AND MA015-OWNER-DEPT NOT = PV-ITEM-CLUSTR-DEPT)                
                                                                                
      ***  HAS THIS ERROR MESSAGE BEEN DISPLAYED YET                            
               IF MA015-OWNER-DEPT = INF-LM-OWNER                               
                   GO TO 2010-EXIT                                              
               ELSE                                                             
                   MOVE MA015-OWNER-DEPT        TO INF-LM-OWNER                 
                   MOVE OWNER-NOT-FOUND-LOG-MSG TO LM-MESSAGE                   
               END-IF                                                           
                                                                                
           ELSE                                                                 
               IF (PS-ITEM-CLUSTR-EOF                                           
                   AND MA015-OWNER-VENDOR NOT = PV-LAST-VND)                    
                 OR (MA015-OWNER-VENDOR NOT = PV-LAST-VND                       
                     AND MA015-OWNER-VENDOR NOT = PV-ITEM-CLUSTR-VND)           
                                                                                
      ***  HAS THIS ERROR MESSAGE BEEN DISPLAYED YET                            
                   IF MA015-OWNER-DEPT = INF-LM-VND-OWNER                       
                     AND MA015-OWNER-VENDOR = INF-LM-VND                        
                       GO TO 2010-EXIT                                          
                   ELSE                                                         
                       MOVE MA015-OWNER-DEPT   TO INF-LM-VND-OWNER              
                       MOVE MA015-OWNER-VENDOR TO INF-LM-VND                    
                       MOVE VND-NOT-FOUND-LOG-MSG TO LM-MESSAGE                 
                   END-IF                                                       
                                                                                
               ELSE                                                             
                   MOVE MA015-OWNER-DEPT   TO INF-LM-ITEM-OWNER                 
                   MOVE MA015-OWNER-VENDOR TO INF-LM-ITEM-VND                   
                   MOVE MA015-ITEM-NAME    TO INF-LM-ITEM-NAME                  
                   MOVE ITEM-NOT-FOUND-LOG-MSG TO LM-MESSAGE                    
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
           DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3000-CLOSE.                                                              
                                                                                
           CLOSE VST-STORE-INVENTORY-FILE                                       
                 ORG-CLUSTR-FILE                                                
                 ITEM-CLUSTR-FILE                                               
                 INVENTORY-LOAD-FILE.                                           
                                                                                
           MOVE PV-INV-READ-COUNT       TO RSLM1-VST-STOR-INV-RECS.             
           MOVE RUN-SUMMARY-LOG-MSG1    TO LM-MESSAGE.                          
           DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
                                                                                
           MOVE PV-INV-WRITTEN-COUNT    TO RSLM2-INV-LOAD-RECS.                 
           MOVE RUN-SUMMARY-LOG-MSG2    TO LM-MESSAGE.                          
           DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
                                                                                
           MOVE PV-NO-ITEM-COUNT        TO RSLM3-EXTRACT-NO-ITEM-RECS.  MAKUT037
           MOVE RUN-SUMMARY-LOG-MSG3    TO LM-MESSAGE.                  MAKUT037
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT037
                                                                        MAKUT037
           MOVE PV-NO-ORG-COUNT         TO RSLM4-STORE-NO-ORG-RECS.     MAKUT037
           MOVE RUN-SUMMARY-LOG-MSG4    TO LM-MESSAGE.                  MAKUT037
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT037
                                                                        MAKUT037
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                        MAKUT037
                                                                        MAKUT037
       9997-READ-ITEM-CLUSTR-FILE.                                              
                                                                        MAKUT037
      *  SAVE THE PREVIOUSLY READ RECORD FOR LATER USE TO DETERMINE     MAKUT037
      *  WHETHER OR NOT THE DEPT/VENDOR HAVE CHANGED                            
                                                                        MAKUT037
           IF NOT PS-FIRST-ITEM-CLUSTR                                          
               MOVE PV-ITEM-CLUSTR-RECORD TO                                    
                           PV-PREV-ITEM-CLUSTR-RECORD                           
           END-IF.                                                              
                                                                        MAKUT037
           READ ITEM-CLUSTR-FILE                                                
               INTO PV-ITEM-CLUSTR-RECORD                                       
                   AT END                                                       
                       IF PS-FIRST-ITEM-CLUSTR                                  
                           MOVE -1003 TO EM-ERROR-CODE                          
                                         PV-ABEND-CODE                          
                           MOVE 'EMPTY ITEM CLUSTER TABLE'                      
                                      TO EM-ERROR-DESC                          
                           PERFORM 9999-ABEND                                   
                              THRU 9999-EXIT                                    
                       ELSE                                                     
                           MOVE SPACES TO PV-ITEM-CLUSTR-VST                    
                           SET PS-ITEM-CLUSTR-EOF TO TRUE                       
                           GO TO 9997-EXIT                                      
                       END-IF                                                   
           END-READ.                                                            
                                                                        MAKUT037
           IF PS-FIRST-ITEM-CLUSTR                                              
               SET PS-NOT-FIRST-ITEM-CLUSTR TO TRUE                             
               GO TO 9997-EXIT                                                  
           END-IF.                                                              
                                                                        MAKUT037
      *  PV-LAST FIELDS ARE USED TO CONTROL THE ERROR DISPLAYS          MAKUT037
           IF PV-PREV-ITEM-CLUSTR-DEPT NOT = PV-ITEM-CLUSTR-DEPT                
               MOVE PV-PREV-ITEM-CLUSTR-DEPT TO PV-LAST-DEPT                    
               MOVE PV-PREV-ITEM-CLUSTR-VND  TO PV-LAST-VND                     
           ELSE                                                                 
               IF PV-PREV-ITEM-CLUSTR-VND NOT = PV-ITEM-CLUSTR-VND              
                   MOVE PV-PREV-ITEM-CLUSTR-VND TO PV-LAST-VND                  
               END-IF                                                           
           END-IF.                                                              
                                                                        MAKUT037
           IF PV-VST-CLUSTR-KEY = PV-ITEM-CLUSTR-KEY                    MAKUT037
               SET PS-ITEM-CLUSTR-MATCH TO TRUE                                 
               GO TO 9997-EXIT                                                  
           END-IF.                                                              
                                                                        MAKUT037
           IF PV-VST-CLUSTR-KEY < PV-ITEM-CLUSTR-KEY                    MAKUT037
               SET PS-ITEM-CLUSTR-NOT-IN-FILE TO TRUE                           
           END-IF.                                                              
                                                                        MAKUT037
       9997-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9998-READ-VST-STORE-INV-FILE.                                            
                                                                        MAKUT037
           READ VST-STORE-INVENTORY-FILE                                MAKUT037
               INTO MA015-ROLLUP-INVENTORY                              MAKUT037
                   AT END                                               MAKUT037
                       SET PS-VST-STORE-INV-EOF TO TRUE                 MAKUT037
                       GO TO 9998-EXIT                                          
           END-READ.                                                            
                                                                                
           MOVE MA015-ITEM-NAME    TO PV-VST-CLUSTR-ITEM-NAME.                  
           MOVE MA015-OWNER-DEPT   TO PV-VST-CLUSTR-DEPT.                       
           MOVE MA015-OWNER-VENDOR TO PV-VST-CLUSTR-OWNER-VENDOR.               
           ADD +1 TO PV-INV-READ-COUNT.                                         
                                                                                
       9998-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9999-ABEND.                                                              
                                                                        MAKUT037
           MOVE ERROR-MESSAGE TO LOG-MESSAGE.                           MAKUT037
                                                                        MAKUT037
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT037
                                                                        MAKUT037
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         MAKUT037
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
