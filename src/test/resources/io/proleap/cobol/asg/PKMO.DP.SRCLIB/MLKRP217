       IDENTIFICATION DIVISION.                                         05/14/99
                                                                        MLKRP217
       PROGRAM-ID.    MLKRP217                                             LV001
       AUTHOR.        PAUL KEBER - STRATAGEM.                           MLKRP217
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          MLKRP217
       DATE-WRITTEN.  05/14/99.                                         MLKRP217
       DATE-COMPILED.                                                   MLKRP217
                                                                        MLKRP217
      ******************************************************************MLKRP217
      *           MLKRP217 - BLOCKED ML TRANSACTIONS REPORT             MLKRP217
      *                                                                 MLKRP217
      *      THIS PROGRAM READS A FILE OF MERCHANDISE LEDGER            MLKRP217
      * TRANSACTIONS WHOSE DEPARTMENT/CLASSES WERE SKIPPED, AND         MLKRP217
      * CREATES A BLOCKED ML TRANSACTIONS REPORT.                       MLKRP217
      *      EACH TRANSACTION ON THE INPUT FILE IS REPORTED.  A PAGE    MLKRP217
      * BREAK OCCURS WHENEVER THE TRANSACTION CODE CHANGES.  THE        MLKRP217
      * TRANSACTION CATEGORIES ARE: PURCHASES, SALES, TRANSFERS,                
      * MARKDOWNS, MARKUPS, AND ADJUSTMENTS.                                    
      *                                                                         
      * CHANGE HISTORY:                                                 MLKRP217
      *    06/10/99 - PAUL KEBER - WR# 10764                                    
      *      ADDED A NEW COSA INDICATOR COLUMN TO THE REPORT.                   
      *                                                                         
      *    09/21/00 - V. LEE YANG                                               
      *      ADDED A NEW POS COLUMN AND A POS SUB-TOTAL LINE TO REPORT          
21015 *                                                                         
  |   *    01/15/2001 - ROD JANSSEN - WR# 21015                                 
  |   *       ADD ML123-BATCH-NBR FIELD TO CL-DEPT-REJECT-FILE SUMMING          
21015 *       BY BATCH NUMBER IN MLKRP217......                                 
W21731*                                                                         
  |   *    04/10/2002 - SCOTT JACKSON WR# 21731 (NEW SKU INITIATIVE)            
  |   *                                                                         
W21731*                                                                         
W29908*    01/31/2006 - V. LEE YANG WR29908 CHANGE STANDARD OUTPUT              
      *                 COPYBOOK AND ADD JOBNAME INFORMATION TO REPORT.         
208057*                                                                         
208057*    09/10/2018 - JIM HUNTER   CHG0208057                                 
208057*                 MAINFRAME REFACTORING - CONVERT FROM CALLING            
208057*                 MLPD502 TO CALLING DPKUT085 TO GET CURRENT              
208057*                 JOB NAME.                                               
      ******************************************************************        
                                                                        MLKRP217
       ENVIRONMENT DIVISION.                                            MLKRP217
       CONFIGURATION SECTION.                                           MLKRP217
       SOURCE-COMPUTER.        IBM-3090.                                MLKRP217
       OBJECT-COMPUTER.        IBM-3090.                                MLKRP217
       INPUT-OUTPUT SECTION.                                            MLKRP217
                                                                        MLKRP217
       FILE-CONTROL.                                                    MLKRP217
           SELECT BLOCKED-TRANS-FILE                                    MLKRP217
               ASSIGN TO INP01.                                         MLKRP217
           SELECT BLOCKED-TRANS-REPORT                                  MLKRP217
               ASSIGN TO RPT01.                                         MLKRP217
                                                                        MLKRP217
       DATA DIVISION.                                                   MLKRP217
       FILE SECTION.                                                    MLKRP217
                                                                        MLKRP217
       FD  BLOCKED-TRANS-FILE                                           MLKRP217
           LABEL RECORDS ARE STANDARD                                   MLKRP217
           BLOCK CONTAINS 0 RECORDS                                     MLKRP217
           RECORDING  MODE IS F.                                        MLKRP217
       01  BLOCKED-TRANS-RECORD        PIC X(80).                       MLKRP217
                                                                        MLKRP217
       FD  BLOCKED-TRANS-REPORT                                         MLKRP217
           LABEL RECORDS ARE OMITTED                                    MLKRP217
           BLOCK CONTAINS 0 RECORDS                                     MLKRP217
           RECORDING  MODE IS F.                                        MLKRP217
       01  BLOCKED-TRANS-REPORT-LINE   PIC X(100).                      MLKRP217
                                                                        MLKRP217
                                                                        MLKRP217
       WORKING-STORAGE SECTION.                                         MLKRP217
                                                                        MLKRP217
       01  PC-PROGRAM-CONSTANTS.                                        MLKRP217
           05  PC-MAX-LINE-COUNT            PIC S9(04)    VALUE +59     MLKRP217
                                                          COMP SYNC.    MLKRP217
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'MLKRP217'.MLKRP800
                                                                        MLKRP217
       01  PV-PROGRAM-VARIABLES.                                        MLKRP217
W29908     05  PV-JOB-INFO.                                             MLKRP217
W29908         10  FILLER                  PIC X(01)       VALUE '('.           
W29908         10  PV-JOBNAME              PIC X(08)       VALUE SPACES.        
W29908         10  FILLER                  PIC X(01)       VALUE ')'.           
W21731     05  PV-MCL-NBR.                                              MLKRP217
W21731         10  PV-MAJ-CLASS-NBR         PIC  9(01)    VALUE ZERO.   MLKRP217
W21731         10  PV-MAJ-CLASS-ZERO        PIC  9(01)    VALUE ZERO.   MLKRP217
                                                                        MLKRP217
           05  PV-ADVANCE                   PIC S9(03)    VALUE ZERO    MLKRP217
                                                          COMP-3.       MLKRP217
           05  PV-LINE-COUNT                PIC S9(04)    VALUE ZERO    MLKRP217
                                                          COMP SYNC.    MLKRP217
           05  PV-PAGE-COUNT                PIC S9(05)    VALUE ZERO.   MLKRP217
           05  PV-PARAGRAPH-NAME            PIC  X(30)    VALUE SPACE.  MLKRP217
           05  PV-PREV-TRANS-CDE            PIC  X(01)    VALUE SPACE.  MLKRP217
21015      05  PV-PREV-BATCH-NBR            PIC  X(03)    VALUE SPACE.  MLKRP217
           05  PV-RETAIL-TOTAL              PIC S9(09)V99 VALUE +0      MLKRP217
                                                          COMP-3.       MLKRP217
           05  PV-COST-TOTAL                PIC S9(09)V99 VALUE +0      MLKRP217
                                                          COMP-3.       MLKRP217
           05  PV-POS-RETAIL-TOTAL          PIC S9(09)V99 VALUE +0      MLKRP217
                                                          COMP-3.       MLKRP217
           05  PV-POS-COST-TOTAL            PIC S9(09)V99 VALUE +0      MLKRP217
                                                          COMP-3.       MLKRP217
           05  PV-COSA-RETAIL-TOTAL         PIC S9(09)V99 VALUE +0      MLKRP217
                                                          COMP-3.       MLKRP217
           05  PV-COSA-COST-TOTAL           PIC S9(09)V99 VALUE +0      MLKRP217
                                                          COMP-3.       MLKRP217
21015      05  PV-RETAIL-SUBTOT             PIC S9(09)V99 VALUE +0      MLKRP217
  |                                                       COMP-3.       MLKRP217
  |        05  PV-COST-SUBTOT               PIC S9(09)V99 VALUE +0      MLKRP217
21015                                                     COMP-3.       MLKRP217
                                                                        MLKRP217
       01  SW-SWITCHES.                                                 MLKRP217
           05  SW-END-OF-INPUT-FILE-SW PIC X       VALUE 'N'.           MLKRP217
               88  SW-END-OF-INPUT-FILE            VALUE 'Y'.           MLKRP217
               88  SW-NOT-END-OF-INPUT-FILE        VALUE 'N'.           MLKRP217
                                                                        MLKRP217
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.MLKRP217
                                                                        MLKRP217
      ***************************************************************** MLKRP217
      * BLOCKED-TRANS-RECORD LAYOUT.                                    MLKRP217
      ***************************************************************** MLKRP217
                                                                        MLKRP217
           COPY MLRD123.                                                   CL**5
                                                                        MLKRP217
208057*****************************************************************         
208057* JOB NAME                                                                
208057*****************************************************************         
208057*01  DP085-GET-CURR-JOB-INFO-PGM.                                         
208057*01  DP085-GET-CURR-JOB-INFO-PARMS.                                       
208057     COPY DPWS085.                                                        
                                                                        MLKRP217
      ******************************************************************MLKRP217
      * STANDARD REPORT HEADING                                         MLKRP217
      ******************************************************************MLKRP217
W29908*01  DP100-STANDRD-HEADER-100-COLS.                               MLKRP217
W29908*01  DP100-CNFDL-HDR-100-COLS.                                    MLKRP217
W29908     COPY DPWS100.                                                MLKRP217
                                                                        MLKRP217
       01  RD-REPORT-TITLE-LINE.                                        MLKRP217
           05  FILLER                  PIC  X(35)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(30)    VALUE              MLKRP217
               'BLOCKED ML TRANSACTIONS REPORT'.                                
           05  FILLER                  PIC  X(35)    VALUE SPACES.      MLKRP217
                                                                        MLKRP217
       01  RD-REPORT-HDR-LINE1.                                         MLKRP217
           05  RD-HDR-CATEGORY         PIC  X(11)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(89)    VALUE SPACES.      MLKRP217
                                                                        MLKRP217
       01  RD-REPORT-HDR-LINE2.                                         MLKRP217
           05  FILLER                  PIC  X(04)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(05)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(09)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(06)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(14)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(04)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(06)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(02)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(09)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(05)    VALUE 'SALES'.     MLKRP217
           05  FILLER                  PIC  X(08)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(16)    VALUE SPACES.      MLKRP217
                                                                        MLKRP217
       01  RD-REPORT-HDR-LINE3.                                         MLKRP217
W21731     05  FILLER                  PIC  X(08)    VALUE '  SKU   '.  MLKRP217
W21731     05  FILLER                  PIC  X(02)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(04)    VALUE 'DEPT'.      MLKRP217
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(05)    VALUE 'SUBCL'.     MLKRP217
           05  FILLER                  PIC  X(02)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(05)    VALUE 'CLASS'.     MLKRP217
           05  FILLER                  PIC  X(08)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(06)    VALUE 'RETAIL'.    MLKRP217
           05  FILLER                  PIC  X(14)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(04)    VALUE 'COST'.      MLKRP217
           05  FILLER                  PIC  X(06)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(02)    VALUE 'PO'.        MLKRP217
           05  FILLER                  PIC  X(09)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(11)    VALUE              MLKRP217
               'CORRECTIONS'.                                           MLKRP217
           05  FILLER                  PIC  X(02)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(03)    VALUE 'POS'.       MLKRP217
W21731*    05  FILLER                  PIC  X(16)    VALUE SPACES.      MLKRP217
W21731     05  FILLER                  PIC  X(06)    VALUE SPACES.      MLKRP217
                                                                        MLKRP217
       01  RD-REPORT-HDR-LINE4.                                         MLKRP217
W21731     05  FILLER                  PIC  X(08)    VALUE '--------'.  MLKRP217
W21731     05  FILLER                  PIC  X(02)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(04)    VALUE '----'.      MLKRP217
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(05)    VALUE '-----'.     MLKRP217
           05  FILLER                  PIC  X(02)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(05)    VALUE '-----'.     MLKRP217
           05  FILLER                  PIC  X(02)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(14)    VALUE              MLKRP217
               '--------------'.                                                
           05  FILLER                  PIC  X(04)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(14)    VALUE              MLKRP217
               '--------------'.                                                
           05  FILLER                  PIC  X(04)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(05)    VALUE '-----'.     MLKRP217
           05  FILLER                  PIC  X(06)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(11)    VALUE              MLKRP217
               '-----------'.                                           MLKRP217
           05  FILLER                  PIC  X(02)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(03)    VALUE '---'.       MLKRP217
           05  FILLER                  PIC  X(16)    VALUE SPACES.      MLKRP217
                                                                        MLKRP217
       01  RT-REPORT-TOTAL-LINE-1.                                      MLKRP217
W21731*    05  FILLER                  PIC  X(21)    VALUE SPACES.      MLKRP217
W21731     05  FILLER                  PIC  X(31)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(14)    VALUE              MLKRP217
               '--------------'.                                                
           05  FILLER                  PIC  X(04)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(14)    VALUE              MLKRP217
               '--------------'.                                                
W21731*    05  FILLER                  PIC  X(47)    VALUE SPACES.      MLKRP217
W21731     05  FILLER                  PIC  X(37)    VALUE SPACES.      MLKRP217
                                                                        MLKRP217
       01  RT-REPORT-TOTAL-LINE-2.                                      MLKRP217
           05  FILLER                  PIC  X(05)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(13)    VALUE              MLKRP217
               ' GRAND TOTALS'.                                         MLKRP217
W21731*    05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
W21731     05  FILLER                  PIC  X(13)    VALUE SPACES.      MLKRP217
           05  RT-RETAIL-AMT-TTL       PIC  ZZZ,ZZZ,ZZ9.99-             MLKRP217
                                                     VALUE ZERO.                
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  RT-COST-AMT-TTL         PIC  ZZZ,ZZZ,ZZ9.99-             MLKRP217
                                                     VALUE ZERO.                
W21731*    05  FILLER                  PIC  X(46)    VALUE SPACES.      MLKRP217
W21731     05  FILLER                  PIC  X(36)    VALUE SPACES.      MLKRP217
                                                                        MLKRP217
       01  RT-REPORT-SUB-TOTAL-LINE-1.                                  MLKRP217
           05  FILLER                  PIC  X(01)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(17)    VALUE                      
               'POINT OF SALE S/T'.                                     MLKRP217
W21731*    05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
W21731     05  FILLER                  PIC  X(13)    VALUE SPACES.      MLKRP217
           05  RT-POS-RETAIL-AMT-TTL   PIC  ZZZ,ZZZ,ZZ9.99-             MLKRP217
                                                     VALUE ZERO.                
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  RT-POS-COST-AMT-TTL     PIC  ZZZ,ZZZ,ZZ9.99-             MLKRP217
                                                     VALUE ZERO.                
           05  FILLER                  PIC  X(28)    VALUE SPACES.      MLKRP217
           05  RT-POS-IND              PIC  X(01)    VALUE SPACES.      MLKRP217
W21731*    05  FILLER                  PIC  X(17)    VALUE SPACES.      MLKRP217
W21731     05  FILLER                  PIC  X(07)    VALUE SPACES.      MLKRP217
                                                                        MLKRP217
       01  RT-REPORT-SUB-TOTAL-LINE-2.                                  MLKRP217
           05  FILLER                  PIC  X(01)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(17)    VALUE                      
               'SALES CORRECT S/T'.                                     MLKRP217
W21731*    05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
W21731     05  FILLER                  PIC  X(13)    VALUE SPACES.      MLKRP217
           05  RT-COSA-RETAIL-AMT-TTL  PIC  ZZZ,ZZZ,ZZ9.99-             MLKRP217
                                                     VALUE ZERO.                
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  RT-COSA-COST-AMT-TTL    PIC  ZZZ,ZZZ,ZZ9.99-             MLKRP217
                                                     VALUE ZERO.                
           05  FILLER                  PIC  X(16)    VALUE SPACES.      MLKRP217
           05  RT-COSA-IND             PIC  X(01)    VALUE SPACES.      MLKRP217
W21731*    05  FILLER                  PIC  X(28)    VALUE SPACES.      MLKRP217
W21731     05  FILLER                  PIC  X(18)    VALUE SPACES.      MLKRP217
                                                                        MLKRP217
21015  01  RT-REPORT-SUB-TOTAL-LINE-3.                                  MLKRP217
  |        05  FILLER                  PIC  X(08)  VALUE ' BATCH  '.            
21015      05  RT-BTCH-BATCH-NBR       PIC  X(03)  VALUE SPACES.        MLKRP217
W21731*    05  FILLER                  PIC  X(10)  VALUE SPACES.                
W21731     05  FILLER                  PIC  X(20)  VALUE SPACES.                
21015      05  RT-BTCH-RETAIL-AMT-TTL  PIC  ZZZ,ZZZ,ZZ9.99- VALUE ZERO. MLKRP217
  |        05  FILLER                  PIC  X(03)  VALUE SPACES.        MLKRP217
21015      05  RT-BTCH-COST-AMT-TTL    PIC  ZZZ,ZZZ,ZZ9.99- VALUE ZERO. MLKRP217
W21731*    05  FILLER                  PIC  X(46)  VALUE SPACES.        MLKRP217
W21731     05  FILLER                  PIC  X(36)  VALUE SPACES.        MLKRP217
                                                                        MLKRP217
       01  RD-REPORT-DETAIL-LINE.                                       MLKRP217
W21731     05  RD-SKU-NBR              PIC  X(08)    VALUE SPACES.      MLKRP217
W21731     05  FILLER                  PIC  X(02)    VALUE SPACES.      MLKRP217
           05  RD-DEPT-NBR             PIC  X(03)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(05)    VALUE SPACES.      MLKRP217
           05  RD-CLASS-NBR            PIC  X(02)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(05)    VALUE SPACES.      MLKRP217
W21731*    05  RD-SEQ-NBR              PIC  X(03)    VALUE SPACES.      MLKRP217
           05  RD-MCL-NBR              PIC  X(02)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(04)    VALUE SPACES.      MLKRP217
           05  RD-RETAIL-AMT           PIC  ZZZ,ZZZ,ZZ9.99-             MLKRP217
                                                     VALUE ZERO.                
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  RD-COST-AMT             PIC  ZZZ,ZZZ,ZZ9.99-             MLKRP217
                                                     VALUE ZERO.                
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  RD-PO-NBR               PIC  X(09)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(04)    VALUE SPACES.      MLKRP217
           05  RD-COSA-IND             PIC  X(01)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(11)    VALUE SPACES.      MLKRP217
           05  RD-POS-IND              PIC  X(01)    VALUE SPACES.      MLKRP217
W21731*    05  FILLER                  PIC  X(17)    VALUE SPACES.      MLKRP217
W21731     05  FILLER                  PIC  X(07)    VALUE SPACES.      MLKRP217
                                                                        MLKRP217
       01  ER-END-OF-REPORT-LINE.                                       MLKRP217
           05  FILLER                       PIC  X(21)    VALUE         MLKRP217
                                                '*** END OF REPORT ***'.MLKRP217
                                                                        MLKRP217
       LINKAGE SECTION.                                                 MLKRP217
                                                                                
                                                                                
       PROCEDURE DIVISION.                                              MLKRP217
                                                                        MLKRP217
       A100-MAIN-MODULE.                                                MLKRP217
                                                                        MLKRP217
           OPEN INPUT  BLOCKED-TRANS-FILE                               MLKRP217
                OUTPUT BLOCKED-TRANS-REPORT.                            MLKRP217
                                                                        MLKRP217
           PERFORM B100-INITIALIZATION.                                 MLKRP217
                                                                        MLKRP217
           PERFORM C100-PROCESS-INPUT-FILE                              MLKRP217
               UNTIL SW-END-OF-INPUT-FILE.                              MLKRP217
                                                                        MLKRP217
           IF PV-PREV-TRANS-CDE NOT = SPACES OR                         MLKRP217
21015         PV-PREV-BATCH-NBR NOT = SPACES                            MLKRP217
21015          PERFORM D110-PRINT-SECTION-TOTAL                         MLKRP217
               PERFORM D100-PRINT-SECTION-TOTAL                         MLKRP217
           END-IF.                                                      MLKRP217
21015                                                                           
           MOVE ER-END-OF-REPORT-LINE TO BLOCKED-TRANS-REPORT-LINE.     MLKRP217
           MOVE 3 TO PV-ADVANCE.                                        MLKRP217
           PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP217
                                                                        MLKRP217
           CLOSE BLOCKED-TRANS-FILE                                     MLKRP217
                 BLOCKED-TRANS-REPORT.                                  MLKRP217
                                                                        MLKRP217
           STOP RUN.                                                    MLKRP217
                                                                        MLKRP217
                                                                        MLKRP217
       B100-INITIALIZATION.                                             MLKRP217
                                                                        MLKRP217
W29908     MOVE FUNCTION CURRENT-DATE (1:4) TO DP100-RUN-YEAR.          MLKRP800
W29908     MOVE FUNCTION CURRENT-DATE (5:2) TO DP100-RUN-MONTH.         MLKRP800
W29908     MOVE FUNCTION CURRENT-DATE (7:2) TO DP100-RUN-DAY.           MLKRP800
W29908     MOVE FUNCTION CURRENT-DATE (9:2) TO DP100-RUN-HOUR.          MLKRP800
W29908     MOVE FUNCTION CURRENT-DATE (11:2) TO DP100-RUN-MINUTE.       MLKRP800
W29908     MOVE PC-PROGRAM-NAME     TO DP100-PROGRAM-NAME.              MLKRP800
W29908     MOVE 1                   TO DP100-REPORT-NUMBER.             MLKRP800
                                                                        MLKRP217
           PERFORM Z100-READ-BLOCKED-TRANS-FILE.                        MLKRP217
           IF SW-NOT-END-OF-INPUT-FILE                                  MLKRP217
               MOVE ML123-TRN-CDE   TO PV-PREV-TRANS-CDE                MLKRP217
21015          MOVE ML123-BATCH-NBR TO PV-PREV-BATCH-NBR                MLKRP217
           END-IF.                                                      MLKRP217
                                                                        MLKRP217
208057*                                                                         
208057*  GET CURRENT JOB NAME                                                   
208057     CALL DP085-GET-CURR-JOB-INFO-PGM                                     
208057         USING DP085-GET-CURR-JOB-INFO-PARMS.                             
208057     MOVE DP085-JOB-NAME     TO PV-JOBNAME.                               
208057                                                                          
208057     DISPLAY 'DP085 JOBNAME - ' PV-JOBNAME.                               
                                                                                
W29908     MOVE PV-JOB-INFO                                                     
W29908         TO DP100-STANDRD-HEADER-100-COLS(20:10).                         
                                                                                
           PERFORM Z300-PRINT-RPT-HEADINGS.                             MLKRP217
                                                                        MLKRP217
                                                                        MLKRP217
      ******************************************************************MLKRP217
      * READ AND PROCESS THE INPUT FILE.                                MLKRP217
      * WRITE A REPORT DETAIL LINE FOR EACH INPUT RECORD.               MLKRP217
      ******************************************************************MLKRP217
       C100-PROCESS-INPUT-FILE.                                         MLKRP217
                                                                        MLKRP217
           IF ML123-TRN-CDE NOT = PV-PREV-TRANS-CDE                     MLKRP217
21015          ADD 3                TO PV-LINE-COUNT                    MLKRP217
21015          PERFORM D110-PRINT-SECTION-TOTAL                         MLKRP217
21015          MOVE ML123-BATCH-NBR TO PV-PREV-BATCH-NBR                MLKRP217
               MOVE 99              TO PV-LINE-COUNT                    MLKRP217
               MOVE ML123-TRN-CDE   TO PV-PREV-TRANS-CDE                MLKRP217
               PERFORM D100-PRINT-SECTION-TOTAL                         MLKRP217
21015      ELSE                                                                 
21015      IF ML123-BATCH-NBR NOT = PV-PREV-BATCH-NBR                   MLKRP217
21015          ADD 3                TO PV-LINE-COUNT                    MLKRP217
21015          PERFORM D110-PRINT-SECTION-TOTAL                         MLKRP217
21015          MOVE ML123-BATCH-NBR TO PV-PREV-BATCH-NBR                MLKRP217
           END-IF.                                                      MLKRP217
                                                                        MLKRP217
           IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                         MLKRP217
               PERFORM Z300-PRINT-RPT-HEADINGS                          MLKRP217
           END-IF.                                                      MLKRP217
                                                                        MLKRP217
           MOVE ML123-DEPT-NBR    TO RD-DEPT-NBR.                       MLKRP217
           MOVE ML123-CL-NBR      TO RD-CLASS-NBR.                      MLKRP217
W21731*    MOVE ML123-SEQ-NBR     TO RD-SEQ-NBR.                        MLKRP217
W21731     MOVE ML123-SKU         TO RD-SKU-NBR.                        MLKRP217
W21731     MOVE ML123-CL-NBR(1:1) TO PV-MAJ-CLASS-NBR                           
W21731     MOVE PV-MCL-NBR        TO RD-MCL-NBR.                                
           MOVE ML123-RTL-AMT     TO RD-RETAIL-AMT.                     MLKRP217
           MOVE ML123-CST-AMT     TO RD-COST-AMT.                       MLKRP217
           MOVE ML123-PO-NBR      TO RD-PO-NBR.                         MLKRP217
           IF ML123-COSA-IND = 'Y'                                              
               MOVE '*'           TO RD-COSA-IND                        MLKRP217
               MOVE '*'           TO RT-COSA-IND                        MLKRP217
               ADD ML123-RTL-AMT  TO PV-COSA-RETAIL-TOTAL                       
               ADD ML123-CST-AMT  TO PV-COSA-COST-TOTAL                         
           ELSE                                                         MLKRP217
               MOVE SPACE         TO RD-COSA-IND                        MLKRP217
           END-IF.                                                      MLKRP217
                                                                        MLKRP217
           IF ML123-POS-IND = 'Y'                                               
               MOVE '*'           TO RD-POS-IND                                 
               MOVE '*'           TO RT-POS-IND                                 
               ADD ML123-RTL-AMT  TO PV-POS-RETAIL-TOTAL                        
               ADD ML123-CST-AMT  TO PV-POS-COST-TOTAL                          
           ELSE                                                                 
               MOVE SPACE         TO RD-POS-IND                                 
           END-IF.                                                              
                                                                                
           MOVE RD-REPORT-DETAIL-LINE TO BLOCKED-TRANS-REPORT-LINE.     MLKRP217
           PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP217
           MOVE 1  TO PV-ADVANCE.                                       MLKRP217
                                                                        MLKRP217
           ADD ML123-RTL-AMT      TO PV-RETAIL-TOTAL.                   MLKRP217
21015      ADD ML123-RTL-AMT      TO PV-RETAIL-SUBTOT.                  MLKRP217
           ADD ML123-CST-AMT      TO PV-COST-TOTAL.                     MLKRP217
21015      ADD ML123-CST-AMT      TO PV-COST-SUBTOT.                    MLKRP217
                                                                        MLKRP217
           PERFORM Z100-READ-BLOCKED-TRANS-FILE.                        MLKRP217
                                                                        MLKRP217
                                                                        MLKRP217
       D100-PRINT-SECTION-TOTAL.                                        MLKRP217
                                                                        MLKRP217
           MOVE RT-REPORT-TOTAL-LINE-1 TO BLOCKED-TRANS-REPORT-LINE.    MLKRP217
           PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP217
           MOVE 1  TO PV-ADVANCE.                                       MLKRP217
                                                                        MLKRP217
21015 *    IF RT-POS-IND = '*'                                                  
  |   *        MOVE PV-POS-RETAIL-TOTAL    TO RT-POS-RETAIL-AMT-TTL         MLKR
  |   *        MOVE PV-POS-COST-TOTAL      TO RT-POS-COST-AMT-TTL           MLKR
  |   *        MOVE RT-REPORT-SUB-TOTAL-LINE-1                                  
  |   *            TO BLOCKED-TRANS-REPORT-LINE                             MLKR
  |   *        PERFORM Z200-WRITE-REPORT-LINE                               MLKR
  |   *        MOVE 1  TO PV-ADVANCE                                        MLKR
  |   *    END-IF.                                                              
  |   *                                                                 MLKRP217
  |   *    IF RT-COSA-IND = '*'                                                 
  |   *        MOVE PV-COSA-RETAIL-TOTAL    TO RT-COSA-RETAIL-AMT-TTL       MLKR
  |   *        MOVE PV-COSA-COST-TOTAL      TO RT-COSA-COST-AMT-TTL         MLKR
  |   *        MOVE RT-REPORT-SUB-TOTAL-LINE-2                                  
  |   *            TO BLOCKED-TRANS-REPORT-LINE                             MLKR
  |   *        PERFORM Z200-WRITE-REPORT-LINE                               MLKR
  |   *        MOVE 1  TO PV-ADVANCE                                        MLKR
21015 *    END-IF.                                                              
                                                                        MLKRP217
           MOVE PV-RETAIL-TOTAL   TO RT-RETAIL-AMT-TTL.                 MLKRP217
           MOVE PV-COST-TOTAL     TO RT-COST-AMT-TTL.                   MLKRP217
           MOVE RT-REPORT-TOTAL-LINE-2 TO BLOCKED-TRANS-REPORT-LINE.    MLKRP217
           PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP217
           MOVE 1  TO PV-ADVANCE.                                           MLKR
                                                                                
21015      IF RT-POS-IND = '*' OR                                               
  |           RT-COSA-IND = '*'                                                 
  |            MOVE 2  TO PV-ADVANCE                                    MLKRP217
  |        END-IF.                                                              
  |                                                                             
  |        IF RT-POS-IND = '*'                                                  
  |            MOVE PV-POS-RETAIL-TOTAL    TO RT-POS-RETAIL-AMT-TTL         MLKR
  |            MOVE PV-POS-COST-TOTAL      TO RT-POS-COST-AMT-TTL           MLKR
  |            MOVE RT-REPORT-SUB-TOTAL-LINE-1                                  
  |                TO BLOCKED-TRANS-REPORT-LINE                             MLKR
  |            PERFORM Z200-WRITE-REPORT-LINE                               MLKR
  |            MOVE 1  TO PV-ADVANCE                                        MLKR
  |        END-IF.                                                              
  |                                                                     MLKRP217
  |        IF RT-COSA-IND = '*'                                                 
  |            MOVE PV-COSA-RETAIL-TOTAL    TO RT-COSA-RETAIL-AMT-TTL       MLKR
  |            MOVE PV-COSA-COST-TOTAL      TO RT-COSA-COST-AMT-TTL         MLKR
  |            MOVE RT-REPORT-SUB-TOTAL-LINE-2                                  
  |                TO BLOCKED-TRANS-REPORT-LINE                             MLKR
  |            PERFORM Z200-WRITE-REPORT-LINE                               MLKR
  |            MOVE 1  TO PV-ADVANCE                                        MLKR
  |        END-IF.                                                              
21015                                                                   MLKRP217
           MOVE ZERO              TO PV-RETAIL-TOTAL.                   MLKRP217
           MOVE ZERO              TO PV-COST-TOTAL.                     MLKRP217
           MOVE ZERO              TO PV-POS-RETAIL-TOTAL                MLKRP217
                                     PV-POS-COST-TOTAL                  MLKRP217
                                     PV-COSA-RETAIL-TOTAL               MLKRP217
                                     PV-COSA-COST-TOTAL.                MLKRP217
           MOVE SPACE             TO RT-POS-IND                                 
                                     RT-COSA-IND.                               
                                                                        MLKRP217
                                                                        MLKRP217
21015  D110-PRINT-SECTION-TOTAL.                                        MLKRP217
  |                                                                     MLKRP217
  |        MOVE RT-REPORT-TOTAL-LINE-1     TO BLOCKED-TRANS-REPORT-LINE.MLKRP217
  |        PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP217
  |        MOVE 1                          TO PV-ADVANCE.               MLKRP217
  |                                                                     MLKRP217
  |        MOVE PV-PREV-BATCH-NBR          TO RT-BTCH-BATCH-NBR.                
  |        MOVE PV-RETAIL-SUBTOT           TO RT-BTCH-RETAIL-AMT-TTL.   MLKRP217
  |        MOVE PV-COST-SUBTOT             TO RT-BTCH-COST-AMT-TTL.     MLKRP217
  |        MOVE RT-REPORT-SUB-TOTAL-LINE-3 TO BLOCKED-TRANS-REPORT-LINE.        
  |        PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP217
  |        MOVE 1                          TO PV-ADVANCE.               MLKRP217
  |                                                                             
  |        MOVE SPACES                     TO BLOCKED-TRANS-REPORT-LINE.        
  |        PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP217
  |        MOVE 1                          TO PV-ADVANCE.               MLKRP217
  |                                                                             
  |        MOVE ZERO                       TO PV-RETAIL-SUBTOT.         MLKRP217
  |        MOVE ZERO                       TO PV-COST-SUBTOT.           MLKRP217
21015                                                                   MLKRP217
       Z100-READ-BLOCKED-TRANS-FILE.                                    MLKRP217
                                                                        MLKRP217
           READ BLOCKED-TRANS-FILE                                      MLKRP217
               INTO ML123-RECORD                                        MLKRP217
                   AT END SET SW-END-OF-INPUT-FILE TO TRUE.             MLKRP217
                                                                        MLKRP217
                                                                        MLKRP217
       Z200-WRITE-REPORT-LINE.                                          MLKRP217
                                                                        MLKRP217
           WRITE BLOCKED-TRANS-REPORT-LINE                              MLKRP217
               AFTER ADVANCING PV-ADVANCE.                              MLKRP217
           ADD PV-ADVANCE TO PV-LINE-COUNT.                             MLKRP217
                                                                        MLKRP217
                                                                        MLKRP217
      ******************************************************************MLKRP217
      * PRINT THE REPORT HEADING LINES.                                 MLKRP217
      ******************************************************************MLKRP217
       Z300-PRINT-RPT-HEADINGS.                                         MLKRP217
                                                                        MLKRP217
           ADD 1 TO PV-PAGE-COUNT.                                      MLKRP217
W29908     MOVE PV-PAGE-COUNT TO DP100-PAGE-NUMBER.                     MLKRP217
           WRITE BLOCKED-TRANS-REPORT-LINE                              MLKRP217
W29908         FROM DP100-STANDRD-HEADER-100-COLS                       MLKRP217
               AFTER ADVANCING PAGE.                                    MLKRP217
           MOVE +1 TO PV-LINE-COUNT.                                    MLKRP217
                                                                        MLKRP217
           WRITE BLOCKED-TRANS-REPORT-LINE FROM RD-REPORT-TITLE-LINE    MLKRP217
               AFTER ADVANCING 1 LINES.                                 MLKRP217
                                                                        MLKRP217
           EVALUATE PV-PREV-TRANS-CDE                                   MLKRP217
               WHEN '1'                                                 MLKRP217
                   MOVE 'PURCHASES'     TO RD-HDR-CATEGORY              MLKRP217
               WHEN '2'                                                 MLKRP217
                   MOVE 'SALES'         TO RD-HDR-CATEGORY              MLKRP217
               WHEN '3'                                                 MLKRP217
                   MOVE 'TRANSFERS'     TO RD-HDR-CATEGORY              MLKRP217
               WHEN '4'                                                 MLKRP217
                   MOVE 'MARKDOWNS'     TO RD-HDR-CATEGORY              MLKRP217
               WHEN '5'                                                 MLKRP217
                   MOVE 'MARKUPS'       TO RD-HDR-CATEGORY              MLKRP217
               WHEN '8'                                                 MLKRP217
                   MOVE 'ADJUSTMENTS'   TO RD-HDR-CATEGORY              MLKRP217
               WHEN OTHER                                                       
                   MOVE SPACES          TO RD-HDR-CATEGORY              MLKRP217
           END-EVALUATE.                                                MLKRP217
                                                                        MLKRP217
           WRITE BLOCKED-TRANS-REPORT-LINE FROM RD-REPORT-HDR-LINE1     MLKRP217
               AFTER ADVANCING 2 LINES.                                 MLKRP217
           WRITE BLOCKED-TRANS-REPORT-LINE FROM RD-REPORT-HDR-LINE2     MLKRP217
               AFTER ADVANCING 1 LINES.                                 MLKRP217
           WRITE BLOCKED-TRANS-REPORT-LINE FROM RD-REPORT-HDR-LINE3     MLKRP217
               AFTER ADVANCING 1 LINES.                                 MLKRP217
           WRITE BLOCKED-TRANS-REPORT-LINE FROM RD-REPORT-HDR-LINE4     MLKRP217
               AFTER ADVANCING 1 LINES.                                 MLKRP217
           ADD +6 TO PV-LINE-COUNT.                                     MLKRP217
           MOVE 2  TO PV-ADVANCE.                                       MLKRP217
                                                                        MLKRP217
