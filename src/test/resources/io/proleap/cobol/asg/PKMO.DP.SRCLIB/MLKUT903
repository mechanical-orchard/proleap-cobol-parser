       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              MLKUT903.                                       
       AUTHOR.                  JILL LIN.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            10/19/2004.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  COMMON EXTRACT OF ML'S WEEKLY SUMMARY TABLE FOR OTHER DOMAINS *        
      *                                                                *        
      *  THIS PROGRAM WILL EXTRACT DATA FROM ML'S WEEKLY SUMMARY       *        
      *  TABLE FOR OTHER DOMAINS TO LOAD INTO THEIR VERSION OF THE     *        
      *  TABLE.  THIS PROGRAM IS SIMILAR TO MLKUT902, BUT IT WILL      *        
      *  SUM UP ALL THE DATA COLUMNS TO THE WEEKLY, DEPT, MAJ-CLASS,   *        
      *  AND VENDOR.                                                   *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * W33539   2007-11-02  JILL LIN - ENHANCE THE PERFORMANCE BY     *        
      *                      REMOVE WKLY AND PARTN TABLES.             *        
238672* 238672   2008-01-27  SCOTT JACKSON - PRODUCE ERROR FILE        *        
      *                      INCIDENT : INC000000238672                         
      *                                                                *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
W33539     SELECT OPTIONAL MLT-WKLY-SUM-VND-FILE                                
               ASSIGN TO INP01.                                                 
                                                                                
W33539     SELECT MLT-WKLY-SUM-EXTRACT                                          
               ASSIGN TO OUT01.                                                 
                                                                                
238672     SELECT MLT-WKLY-SUM-EXTRACT-ERROR                                    
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
W33539 FD  MLT-WKLY-SUM-VND-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
W33539 01  MLT-WKLY-SUM-VND-REC            PIC X(354).                          
                                                                                
       FD  MLT-WKLY-SUM-EXTRACT                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 360 CHARACTERS                                       
           DATA RECORD IS COMMON-EXTRACT-REC.                                   
       01  COMMON-EXTRACT-REC              PIC X(360).                          
                                                                                
238672 FD  MLT-WKLY-SUM-EXTRACT-ERROR                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 354 CHARACTERS                                       
           DATA RECORD IS COMMON-EXTRACT-ERR.                                   
       01  COMMON-EXTRACT-ERR              PIC X(354).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      ******************************************************************        
      *  COMMON EXTRACT DATA LAYOUT                                             
      ******************************************************************        
      *01  ML903-WKLY-SUM-COMMON-EXTRACT.                                       
           COPY MLRD903.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
W33539     '*** PROGRAMMING WORK REC BEGIN ***'.                                
                                                                                
W33539 01  PW-PROGRAM-WORK-REC.                                                 
W33539     05  PW-PARTN-NBR                PIC  S9(4) COMP.                     
W33539     05  PW-DEPT-NBR                 PIC  S9(4)V COMP-3.                  
W33539     05  PW-MAJ-CL-NBR               PIC  S9(4)V COMP-3.                  
W33539     05  PW-SUB-CL-NBR               PIC  S9(4)V COMP-3.                  
W33539     05  PW-LOC-NBR                  PIC  S9(4) COMP.                     
W33539     05  PW-ML-LO-MDSE-LVL-ID        PIC  S9(9) COMP.                     
W33539     05  PW-FSCL-WK-END-DTE          PIC   X(10).                         
W33539     05  PW-SLS-REG-RTL-AMT          PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-SLS-PROMO-RTL-AMT        PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-SLS-PROCLR-RTL-AMT       PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-SLS-CLR-RTL-AMT          PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-SLS-OTH-RTL-AMT          PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-MKDN-PLU-RTL-AMT         PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-MKDN-PROMO-RTL-AMT       PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-MKDN-RGST-RTL-AMT        PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-MKDN-GLDST-RTL-AMT       PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-MKDN-UMTCH-RTL-AMT       PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-MKDN-BYR-RTL-AMT         PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-MKDN-STR-RTL-AMT         PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-MKDN-PRPT-RTL-AMT        PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-MKDN-OTH-RTL-AMT         PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-MKDN-RTL-AMT             PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-MKUP-RTL-AMT             PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-XFER-IN-RTL-AMT          PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-XFER-OUT-RTL-AMT         PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-RCPT-RTL-AMT             PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-RCPT-NET-COST-AMT        PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-RCPT-GRO-COST-AMT        PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-PADJ-RTL-AMT             PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-PADJ-FRGT-COST-AMT       PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-PADJ-DISC-COST-AMT       PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-PADJ-RTV-COST-AMT        PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-PADJ-RTV-RTL-AMT         PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-PADJ-DA-COST-AMT         PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-PADJ-DA-RTL-AMT          PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-PADJ-NET-COST-AMT        PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-PADJ-OTH-COST-AMT        PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-PADJ-OTH-RTL-AMT         PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-DR-ALW-RTL-AMT           PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-RTV-RTL-AMT              PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-MKUP-INIT-AMT            PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-MKUP-NET-AMT             PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-INV-ADJ-RTL-AMT          PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-ONORD-RTL-AMT            PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-ONORD-COST-AMT           PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-SHNK-RTL-AMT             PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-END-INV-RTL-AMT          PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-END-INV-COST-AMT         PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-INV-COST-ADJ1-AMT        PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-NULL-IND-1               PIC  S9(01) COMP-3.                  
W33539     05  PW-INV-COST-ADJ2-AMT        PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-NULL-IND-2               PIC  S9(01) COMP-3.                  
W33539     05  PW-END-INV-COST1-AMT        PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-NULL-IND-3               PIC  S9(01) COMP-3.                  
W33539     05  PW-END-INV-COST2-AMT        PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-NULL-IND-4               PIC  S9(01) COMP-3.                  
W33539     05  PW-ADJ-GRO-MRGN-AMT         PIC  S9(11)V9(2) COMP-3.             
W33539     05  PW-NULL-IND-5               PIC  S9(01) COMP-3.                  
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
W33539     05  PS-END-OF-WKLYVND-FILE      PIC X           VALUE 'N'.           
W33539         88  NOT-END-OF-WKLYVND-FILE                 VALUE 'N'.           
W33539         88  END-OF-WKLYVND-FILE                     VALUE 'Y'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
W33539     05  PV-RECORD-EXTRACTED         PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
238672     05  PV-ERRORS-WRITTEN           PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** SQL WORK AREA BEGIN ***'.                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  ABEND-4003                                  VALUE +4003.         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MLV_BRND_VND - BRAND VENDOR VIEW TABLE                     *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE MLV00000                                                 
           END-EXEC.                                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
W33539     PERFORM Z500-READ-WKLYVND-FILE.                                      
W33539     MOVE ZEROES TO PV-RECORD-EXTRACTED.                                  
238672     MOVE ZEROES TO PV-ERRORS-WRITTEN.                                    
W33539     PERFORM B200-PROCESS-WKLYVND-FILE                                    
W33539         UNTIL END-OF-WKLYVND-FILE.                                       
                                                                                
           PERFORM B400-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
W33539     OPEN INPUT  MLT-WKLY-SUM-VND-FILE                                    
W33539          OUTPUT MLT-WKLY-SUM-EXTRACT                                     
238672                 MLT-WKLY-SUM-EXTRACT-ERROR.                              
                                                                                
      ******************************************************************        
W33539*    PROCESS WKLY SUM FLAT FILE                                           
      ******************************************************************        
W33539 B200-PROCESS-WKLYVND-FILE.                                               
W33539     PERFORM Z200-GET-WKLYVND-INFO.                                       
W33539     PERFORM Z300-GET-BRND-VND-INFO.                                      
           PERFORM Z400-WRITE-EXTRACT.                                          
W33539     PERFORM Z500-READ-WKLYVND-FILE.                                      
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B400-FINALIZE.                                                           
                                                                                
238672     DISPLAY ' ERRORS    ' PV-ERRORS-WRITTEN.                             
W33539     DISPLAY ' EXTRACTED ' PV-RECORD-EXTRACTED.                           
                                                                                
W33539     CLOSE MLT-WKLY-SUM-VND-FILE                                          
W33539           MLT-WKLY-SUM-EXTRACT                                           
238672           MLT-WKLY-SUM-EXTRACT-ERROR.                                    
                                                                                
      ******************************************************************        
W33539*    FILL EXTRACT RECORD WITH FLAT INPUT FILE'S DATA                      
      ******************************************************************        
W33539 Z200-GET-WKLYVND-INFO.                                                   
                                                                                
W33539     MOVE PW-FSCL-WK-END-DTE    TO ML903-FSCL-WK-END-DTE.                 
W33539     MOVE PW-DEPT-NBR           TO ML903-DEPT-NBR.                        
W33539     MOVE PW-MAJ-CL-NBR         TO ML903-MAJ-CL-NBR.                      
W33539     MOVE PW-SLS-REG-RTL-AMT    TO ML903-SLS-REG-RTL-AMT.                 
W33539     MOVE PW-SLS-PROMO-RTL-AMT  TO ML903-SLS-PROMO-RTL-AMT.               
W33539     MOVE PW-SLS-PROCLR-RTL-AMT TO ML903-SLS-PROCLR-RTL-AMT.              
W33539     MOVE PW-SLS-CLR-RTL-AMT    TO ML903-SLS-CLR-RTL-AMT.                 
W33539     MOVE PW-SLS-OTH-RTL-AMT    TO ML903-SLS-OTH-RTL-AMT.                 
W33539     MOVE PW-MKDN-PLU-RTL-AMT   TO ML903-MKDN-PLU-RTL-AMT.                
W33539     MOVE PW-MKDN-PROMO-RTL-AMT TO ML903-MKDN-PROMO-RTL-AMT.              
W33539     MOVE PW-MKDN-RGST-RTL-AMT  TO ML903-MKDN-RGST-RTL-AMT.               
W33539     MOVE PW-MKDN-GLDST-RTL-AMT TO ML903-MKDN-GLDST-RTL-AMT.              
W33539     MOVE PW-MKDN-UMTCH-RTL-AMT TO ML903-MKDN-UMTCH-RTL-AMT.              
W33539     MOVE PW-MKDN-BYR-RTL-AMT   TO ML903-MKDN-BYR-RTL-AMT.                
W33539     MOVE PW-MKDN-STR-RTL-AMT   TO ML903-MKDN-STR-RTL-AMT.                
W33539     MOVE PW-MKDN-PRPT-RTL-AMT  TO ML903-MKDN-PRPT-RTL-AMT.               
W33539     MOVE PW-MKDN-OTH-RTL-AMT   TO ML903-MKDN-OTH-RTL-AMT.                
W33539     MOVE PW-MKDN-RTL-AMT       TO ML903-MKDN-RTL-AMT.                    
W33539     MOVE PW-MKUP-RTL-AMT       TO ML903-MKUP-RTL-AMT.                    
W33539     MOVE PW-XFER-IN-RTL-AMT    TO ML903-XFER-IN-RTL-AMT.                 
W33539     MOVE PW-XFER-OUT-RTL-AMT   TO ML903-XFER-OUT-RTL-AMT.                
W33539     MOVE PW-RCPT-RTL-AMT       TO ML903-RCPT-RTL-AMT.                    
W33539     MOVE PW-RCPT-NET-COST-AMT  TO ML903-RCPT-NET-COST-AMT.               
W33539     MOVE PW-RCPT-GRO-COST-AMT  TO ML903-RCPT-GRO-COST-AMT.               
W33539     MOVE PW-PADJ-RTL-AMT       TO ML903-PADJ-RTL-AMT.                    
W33539     MOVE PW-PADJ-FRGT-COST-AMT TO ML903-PADJ-FRGT-COST-AMT.              
W33539     MOVE PW-PADJ-DISC-COST-AMT TO ML903-PADJ-DISC-COST-AMT.              
W33539     MOVE PW-PADJ-RTV-COST-AMT  TO ML903-PADJ-RTV-COST-AMT.               
W33539     MOVE PW-PADJ-RTV-RTL-AMT   TO ML903-PADJ-RTV-RTL-AMT.                
W33539     MOVE PW-PADJ-DA-COST-AMT   TO ML903-PADJ-DA-COST-AMT.                
W33539     MOVE PW-PADJ-DA-RTL-AMT    TO ML903-PADJ-DA-RTL-AMT.                 
W33539     MOVE PW-PADJ-NET-COST-AMT  TO ML903-PADJ-NET-COST-AMT.               
W33539     MOVE PW-PADJ-OTH-COST-AMT  TO ML903-PADJ-OTH-COST-AMT.               
W33539     MOVE PW-PADJ-OTH-RTL-AMT   TO ML903-PADJ-OTH-RTL-AMT.                
W33539     MOVE PW-DR-ALW-RTL-AMT     TO ML903-DR-ALW-RTL-AMT.                  
W33539     MOVE PW-RTV-RTL-AMT        TO ML903-RTV-RTL-AMT.                     
W33539     MOVE PW-MKUP-INIT-AMT      TO ML903-MKUP-INIT-AMT.                   
W33539     MOVE PW-MKUP-NET-AMT       TO ML903-MKUP-NET-AMT.                    
W33539     MOVE PW-INV-ADJ-RTL-AMT    TO ML903-INV-ADJ-RTL-AMT.                 
W33539     MOVE PW-ONORD-RTL-AMT      TO ML903-ONORD-RTL-AMT.                   
W33539     MOVE PW-ONORD-COST-AMT     TO ML903-ONORD-COST-AMT.                  
W33539     MOVE PW-INV-COST-ADJ1-AMT  TO ML903-INV-COST-ADJ1-AMT.               
W33539     MOVE PW-INV-COST-ADJ2-AMT  TO ML903-INV-COST-ADJ2-AMT.               
W33539     MOVE PW-SHNK-RTL-AMT       TO ML903-SHNK-RTL-AMT.                    
W33539     MOVE PW-END-INV-RTL-AMT    TO ML903-END-INV-RTL-AMT.                 
W33539     MOVE PW-END-INV-COST2-AMT  TO ML903-END-INV-COST-AMT.                
W33539     MOVE PW-ADJ-GRO-MRGN-AMT   TO ML903-ADJ-GRO-MRGN-AMT.                
                                                                                
      ******************************************************************        
      *    GET BRAND NUMBER AND ENT-ID FROM MLV_BRND_VND VIEW TABLE             
      ******************************************************************        
       Z300-GET-BRND-VND-INFO.                                                  
                                                                                
           EXEC SQL                                                             
                SELECT  ENT_ID                                                  
                       ,LBL_SEQ_NBR                                             
                  INTO  :ML903-ENT-ID                                           
                       ,:ML903-LBL-SEQ-NBR                                      
                  FROM  MLV_BRND_VND                                            
W33539            WHERE ML_LO_MDSE_LVL_ID = :PW-ML-LO-MDSE-LVL-ID               
                  WITH UR                                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   DISPLAY ' CANNOT FIND BRAND: '                               
W33539                       PW-ML-LO-MDSE-LVL-ID                               
                           ' FOR FISCAL WEEK: ' ML903-FSCL-WK-END-DTE           
                           ' DEPT: ' ML903-DEPT-NBR                             
                           ' MAJ-CLASS: ' ML903-MAJ-CL-NBR                      
238672*            PERFORM Z900-SQL-ABEND                                       
238672             PERFORM Z600-WRITE-ERROR                                     
               WHEN OTHER                                                       
                   DISPLAY '*** FISCAL WEEK: ' ML903-FSCL-WK-END-DTE            
                           ' DEPT: ' ML903-DEPT-NBR                             
                           ' MAJ-CLASS: ' ML903-MAJ-CL-NBR                      
W33539                     ' BRAND: ' PW-ML-LO-MDSE-LVL-ID                      
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    WRITE OUT COMMON EXTRACT                                             
      ******************************************************************        
       Z400-WRITE-EXTRACT.                                                      
                                                                                
           ADD +1 TO PV-RECORD-EXTRACTED.                                       
           WRITE COMMON-EXTRACT-REC FROM ML903-WKLY-SUM-COMMON-EXTRACT.         
                                                                                
      ******************************************************************        
      *    READ CONTROL CARD                                                    
      ******************************************************************        
W33539 Z500-READ-WKLYVND-FILE.                                                  
                                                                                
W33539     READ MLT-WKLY-SUM-VND-FILE                                           
W33539         INTO PW-PROGRAM-WORK-REC                                         
W33539         AT END SET END-OF-WKLYVND-FILE TO TRUE.                          
                                                                                
      ******************************************************************        
      *    WRITE OUT COMMON EXTRACT ERROR FILE                                  
      ******************************************************************        
       Z600-WRITE-ERROR.                                                        
                                                                                
           ADD +1 TO PV-ERRORS-WRITTEN.                                         
           WRITE COMMON-EXTRACT-ERR FROM MLT-WKLY-SUM-VND-REC.                  
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                                    
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  MLKUT903'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM Z999-ABEND.                                                  
                                                                                
      ******************************************************************        
      *    ABEND CALL                                                           
      ******************************************************************        
       Z999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
