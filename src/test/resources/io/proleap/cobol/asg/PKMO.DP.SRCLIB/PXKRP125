       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      PXKRP125.                                       00020000
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040000
       DATE-WRITTEN.    06/22/2012.                                     00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **CREATES THE NEW STORE YELLOW TICKET PRICE CHANGE REPORT         00080000
      ******************************************************************00090000
      **DATE: 06/15/2012   MADE BY: DAVE WELLER INITIAL WRITE           00100002
      ******************************************************************00110002
      **DATE: 06/23/2014   MADE BY: L. FRIESS   REMOVE PRICE CHANGE ID  00120002
      ******************************************************************00130000
       ENVIRONMENT DIVISION.                                            00140000
       CONFIGURATION SECTION.                                           00150000
       SOURCE-COMPUTER.        IBM-3090.                                00160000
       OBJECT-COMPUTER.        IBM-3090.                                00170000
       INPUT-OUTPUT SECTION.                                            00180000
       FILE-CONTROL.                                                    00190000
                                                                        00200000
           SELECT INPUT-FILE                                            00210000
                  ASSIGN TO INP01.                                      00220000
           SELECT REPORT-FILE                                           00230000
                  ASSIGN TO OUT01.                                      00240000
                                                                        00250000
       DATA DIVISION.                                                   00260000
       FILE SECTION.                                                    00270000
                                                                        00280000
       FD  INPUT-FILE                                                   00290000
           RECORDING MODE F                                             00300000
           BLOCK CONTAINS 0 RECORDS                                     00310000
           LABEL RECORDS ARE OMITTED                                    00320000
           DATA RECORD IS INPUT-RECORD.                                 00330000
       01  INPUT-RECORD                    PIC X(250).                  00340000
                                                                        00350000
       FD  REPORT-FILE                                                  00360000
           RECORDING MODE F                                             00370000
           BLOCK CONTAINS 0 RECORDS                                     00380000
           LABEL RECORDS ARE OMITTED                                    00390000
           DATA RECORD IS REPORT-RECORD.                                00400000
       01  REPORT-RECORD                   PIC X(133).                  00410000
                                                                        00420000
       WORKING-STORAGE SECTION.                                         00430000
      ******************************************************************00440000
      **SWITCHES                                                        00450000
      ******************************************************************00460000
       01  PS-PROGRAM-SWITCHES.                                         00470000
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)   VALUE 'N'.   00480000
               88  PS-END-OF-INPUT-FILE                    VALUE 'Y'.   00490000
               88  PS-NOT-END-OF-INPUT-FILE                VALUE 'N'.   00500000
      ******************************************************************00510000
      **PROGRAM HOLDS                                                   00520000
      ******************************************************************00530000
       01  PH-PROGRAM-HOLDS-1.                                          00540000
           05  PH-STR-NBR                    PIC S9(04) COMP            00550000
                                                        VALUE ZEROS.    00560000
           05  PH-BUS-AREA-ID                PIC S9(02) COMP-3          00570000
                                                        VALUE ZEROS.    00580000
       01  PH-PROGRAM-HOLDS-2.                                          00590000
           05  PH-SUB-CL-NBR                 PIC S9(04) COMP-3          00600000
                                                        VALUE ZEROS.    00610000
           05  PH-DEPT-NBR                   PIC S9(04) COMP-3          00620000
                                                        VALUE ZEROS.    00630000
      ******************************************************************00640000
      **TIMESTAMP VARIABLES                                             00650000
      ******************************************************************00660000
       01 DF-DATE-TIME-FIELDS.                                          00670000
          05 DF-ACCEPT-DATE.                                            00680000
             10  DF-ACCEPT-YR                  PIC 9(02) VALUE ZEROS.   00690000
             10  DF-ACCEPT-MO                  PIC 9(02) VALUE ZEROS.   00700000
             10  DF-ACCEPT-DY                  PIC 9(02) VALUE ZEROS.   00710000
          05 DF-ACCEPT-TIME.                                            00720000
             10  DF-ACCEPT-HR                  PIC 9(02) VALUE ZEROS.   00730000
             10  DF-ACCEPT-MN                  PIC 9(02) VALUE ZEROS.   00740000
             10  DF-ACCEPT-SS                  PIC 9(04) VALUE ZEROS.   00750000
          05 DF-FORMAT-DATE.                                            00760000
             10  DF-FORMAT-DATE-MM             PIC 9(02) VALUE ZEROS.   00770000
             10  FILLER                        PIC X(01) VALUE '/'.     00780000
             10  DF-FORMAT-DATE-DD             PIC 9(02) VALUE ZEROS.   00790000
             10  FILLER                        PIC X(01) VALUE '/'.     00800000
             10  DF-FORMAT-DATE-YY             PIC 9(02) VALUE ZEROS.   00810000
      ******************************************************************00820000
      **PROGRAM CONSTANTS                                               00830000
      ******************************************************************00840000
       01  PC-PROGRAM-CONSTANTS.                                        00850000
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)                00860000
               VALUE 'PXKRP125'.                                        00870000
      ******************************************************************00880000
      **PROGRAM COUNTERS                                                00890000
      ******************************************************************00900000
       01  PC-PROGRAM-COUNTER.                                          00910000
           05  PC-INPUT-RECORDS-READ           PIC 9(09) VALUE ZEROS.   00920000
           05  PC-REPORT-RECORDS-WRITTEN       PIC 9(09) VALUE ZEROS.   00930000
           05  PC-REPORT-LINE-COUNTER          PIC 9(03) VALUE ZEROS.   00940000
           05  PC-PAGE-COUNTER                 PIC 9(03) VALUE ZEROS.   00950000
      ******************************************************************00960000
      **REPORT HEADINGS                                                 00970000
      ******************************************************************00980000
       01  RH-TITLE-1.                                                  00990000
           05  FILLER                          PIC X(45)  VALUE SPACES. 01000000
           05  FILLER                          PIC X(43)                01010000
                   VALUE 'NEW STORE YELLOW TICKET PRICE CHANGE REPORT'. 01020000
           05  FILLER                          PIC X(45)  VALUE SPACES. 01030000
       01  RH-HEADINGS-1.                                               01040000
           05  FILLER                          PIC X(02)  VALUE SPACES. 01050000
           05  FILLER                          PIC X(14)                01060000
                             VALUE ' STORE NUMBER:'.                    01070000
           05  RH-HEADINGS-1-STR-NBR           PIC Z9999  VALUE ZEROS.  01080000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01090000
           05  RH-HEADING-1-STR-NAME           PIC X(20)  VALUE SPACES. 01100000
           05  FILLER                          PIC X(90)  VALUE SPACES. 01110000
       01  RH-HEADINGS-2.                                               01120000
           05  FILLER                          PIC X(17)                01130000
                             VALUE ' EFFECTIVE DATE: '.                 01140000
           05  RH-HEADINGS-2-EFF-DATE          PIC X(10)  VALUE SPACES. 01150000
       01  RH-HEADINGS-3.                                               01160000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01170000
           05  FILLER                          PIC X(16)                01180000
                             VALUE ' BUSINESS AREA: '.                  01190000
           05  RH-HEADINGS-3-BUS-ID            PIC 99     VALUE ZEROS.  01200000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01210000
           05  RH-HEADINGS-3-BUS-DESC          PIC X(25)  VALUE SPACES. 01220000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01230000
           05  FILLER                          PIC X(45)  VALUE SPACES. 01240000
           05  FILLER                          PIC X(35)  VALUE SPACES. 01250000
           05  FILLER                          PIC X(11)  VALUE SPACES. 01260000
       01  RH-HEADINGS-3B.                                              01270000
           05  FILLER                          PIC X(45)  VALUE SPACES. 01280000
           05  FILLER                          PIC X(43)                01290000
                   VALUE '   ENSURE YOUR STORE LOCATES THESE ITEMS   '. 01300000
           05  FILLER                          PIC X(45)  VALUE SPACES. 01310000
       01  RH-HEADINGS-4.                                               01320000
           05  FILLER                          PIC X(06)  VALUE SPACES. 01330000
           05  FILLER                          PIC X(05)                01340000
               VALUE ' SUB '.                                           01350000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01360000
           05  FILLER                          PIC X(09)                01370000
               VALUE 'BOX'.                                             01380000
           05  FILLER                          PIC X(79)  VALUE SPACES. 01390000
           05  FILLER                          PIC X(03)                01400000
               VALUE 'CLR'.                                             01410001
           05  FILLER                          PIC X(09)  VALUE SPACES. 01420000
           05  FILLER                          PIC X(05)                01430001
               VALUE '  %  '.                                           01440001
           05  FILLER                          PIC X(16)  VALUE SPACES. 01450002
       01  RH-HEADINGS-5.                                               01460000
           05  FILLER                          PIC X(05)                01470000
               VALUE 'DEPT '.                                           01480000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01490000
           05  FILLER                          PIC X(05)                01500000
               VALUE 'CLASS'.                                           01510000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01520000
           05  FILLER                          PIC X(09)                01530000
               VALUE ' ID '.                                            01540000
           05  FILLER                          PIC X(04)  VALUE SPACES. 01550000
           05  FILLER                          PIC X(20)                01560000
               VALUE 'STYLE               '.                            01570000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01580000
           05  FILLER                          PIC X(30)                01590000
               VALUE 'STYLE DESCRIPTION            '.                   01600000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01610000
           05  FILLER                          PIC X(20)                01620000
               VALUE 'LABEL NAME          '.                            01630000
           05  FILLER                          PIC X(02)  VALUE SPACES. 01640000
           05  FILLER                          PIC X(11)                01650000
               VALUE ' RETAIL '.                                        01660001
           05  FILLER                          PIC X(02)  VALUE SPACES. 01670000
           05  FILLER                          PIC X(05)                01680001
               VALUE ' OFF '.                                           01690001
           05  FILLER                          PIC X(09)  VALUE SPACES. 01700002
       01  RH-HEADINGS-6.                                               01710000
           05  FILLER                          PIC X(05)                01720000
               VALUE '-----'.                                           01730000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01740000
           05  FILLER                          PIC X(05)                01750000
               VALUE '-----'.                                           01760000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01770000
           05  FILLER                          PIC X(09)                01780000
               VALUE '---------'.                                       01790000
           05  FILLER                          PIC X(04)  VALUE SPACES. 01800000
           05  FILLER                          PIC X(20)                01810000
               VALUE '--------------------'.                            01820000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01830000
           05  FILLER                          PIC X(30)                01840000
               VALUE '------------------------------'.                  01850000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01860000
           05  FILLER                          PIC X(20)                01870000
               VALUE '--------------------'.                            01880000
           05  FILLER                          PIC X(03)  VALUE SPACES. 01890000
           05  FILLER                          PIC X(11)                01900000
               VALUE '-----------'.                                     01910000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01920000
           05  FILLER                          PIC X(05)                01930001
               VALUE ' --- '.                                           01940001
           05  FILLER                          PIC X(10)  VALUE SPACES. 01950002
       01  RH-DETAIL-LINE.                                              01960000
           05  RH-COLUMN-1-DEPT-NBR            PIC ZZZZZ  VALUE ZEROS.  01970000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01980000
           05  RH-COLUMN-1-SUB-CLASS           PIC ZZZZZ  VALUE ZEROS.  01990000
           05  FILLER                          PIC X(01)  VALUE SPACES. 02000000
           05  RH-COLUMN-1-STKR-NBR            PIC ZZZZZZZZZ.           02010000
           05  FILLER                          PIC X(04)  VALUE SPACES. 02020000
           05  RH-COLUMN-1-VS-NBR              PIC X(20)  VALUE ZEROS.  02030000
           05  FILLER                          PIC X(01)  VALUE SPACES. 02040000
           05  RH-COLUMN-1-VS-DESC             PIC X(30)  VALUE SPACES. 02050000
           05  FILLER                          PIC X(01)  VALUE SPACES. 02060000
           05  RH-COLUMN-1-LBL-NM              PIC X(20)  VALUE SPACES. 02070000
           05  FILLER                          PIC X(03)  VALUE SPACES. 02080000
           05  RH-COLUMN-1-RETAIL              PIC $ZZZZZZZ.ZZ.         02090000
           05  FILLER                          PIC X(03)  VALUE SPACES. 02100001
           05  RH-COLUMN-1-PCT-OFF             PIC ZZ.                  02110001
           05  FILLER                          PIC X(17)  VALUE SPACES. 02120002
       01  RH-FOOTERS-1.                                                02130000
           05  FILLER                          PIC X(54)  VALUE SPACES. 02140000
           05  FILLER                          PIC X(19)                02150000
               VALUE '***END OF REPORT***'.                             02160000
      ******************************************************************02170000
      **COPYBOOKS                                                       02180000
      ******************************************************************02190000
      ***INPUT FILE FORMAT                                              02200000
           COPY PXRD142.                                                02210000
                                                                        02220002
      ***STANDARD KOHLS HEADING FOR 132 BYTE REPORT                     02230000
           COPY DPWS132O.                                               02240000
      ******************************************************************02250000
      **ERROR HANDLING                                                  02260000
      ******************************************************************02270000
       01  PV-MISC-ABEND-FIELDS.                                        02280000
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   02290000
           05  PV-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   02300000
           05  PV-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   02310000
                                                                        02320000
       PROCEDURE DIVISION.                                              02330000
      ***************************************************************** 02340000
                                                                        02350000
           PERFORM 1000-INITIALIZATION.                                 02360000
           PERFORM 2000-PROCESS-RECORDS                                 02370000
                   UNTIL PS-END-OF-INPUT-FILE.                          02380000
           PERFORM 9999-WRAPUP.                                         02390000
           GOBACK.                                                      02400000
                                                                        02410000
      ******************************************************************02420000
      **INITIALIZATION AREA                                             02430000
      ******************************************************************02440000
       1000-INITIALIZATION.                                             02450000
                                                                        02460000
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             02470000
                                                                        02480000
           OPEN INPUT  INPUT-FILE                                       02490000
                OUTPUT REPORT-FILE.                                     02500000
                                                                        02510000
      **RETRIEVE DATE FROM SYSTEM                                       02520000
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             02530000
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             02540000
                                                                        02550000
           MOVE DF-ACCEPT-YR          TO DF-FORMAT-DATE-YY.             02560000
           MOVE DF-ACCEPT-MO          TO DF-FORMAT-DATE-MM.             02570000
           MOVE DF-ACCEPT-DY          TO DF-FORMAT-DATE-DD.             02580000
                                                                        02590000
           PERFORM 2999-READ-INPUT-FILE.                                02600000
                                                                        02610000
           IF  PS-NOT-END-OF-INPUT-FILE                                 02620000
               PERFORM 1100-HEADING-PREP                                02630000
               PERFORM 2100-WRITE-HEADINGS                              02640000
               MOVE PXRD142-BUS-AREA-ID                                 02650000
                                      TO PH-BUS-AREA-ID                 02660000
               MOVE PXRD142-STR-NBR   TO PH-STR-NBR                     02670000
           END-IF.                                                      02680000
                                                                        02690000
      ******************************************************************02700000
      **MOVE DATA TO FIELDS IN HEADING FOR REPORT                       02710000
      ******************************************************************02720000
       1100-HEADING-PREP.                                               02730000
                                                                        02740000
           MOVE '1100-HEADING-PREP'  TO PV-PARAGRAPH-NAME.              02750000
                                                                        02760000
      ***PREPS KOHLS STANDARD HEADING                                   02770000
           MOVE PC-CURRENT-PROGRAM-NAME                                 02780000
                                     TO DP132O-PROGRAM-NAME.            02790000
           MOVE 1                                                       02800000
                                     TO DP132O-REPORT-NUMBER.           02810000
           MOVE DF-FORMAT-DATE       TO DP132O-RUN-DATE.                02820000
           MOVE DF-ACCEPT-HR         TO DP132O-RUN-HOUR.                02830000
           MOVE DF-ACCEPT-MN         TO DP132O-RUN-MINUTE.              02840000
                                                                        02850000
      ******************************************************************02860000
      **PROCESSES INPUT FILE                                            02870000
      ******************************************************************02880000
       2000-PROCESS-RECORDS.                                            02890000
                                                                        02900000
           MOVE '2000-PROCESS-RECORDS'  TO PV-PARAGRAPH-NAME.           02910000
                                                                        02920000
           EVALUATE TRUE                                                02930000
              WHEN PXRD142-STR-NBR EQUAL PH-STR-NBR                     02940000
                   IF  PC-REPORT-LINE-COUNTER > 60                      02950000
                    OR PXRD142-BUS-AREA-ID NOT EQUAL PH-BUS-AREA-ID     02960000
                       PERFORM 2100-WRITE-HEADINGS                      02970000
                       INITIALIZE PH-PROGRAM-HOLDS-2                    02980000
                       MOVE PXRD142-BUS-AREA-ID                         02990000
                                        TO PH-BUS-AREA-ID               03000000
                   END-IF                                               03010000
                   PERFORM 2200-WRITE-DETAIL                            03020000
              WHEN PXRD142-STR-NBR NOT EQUAL PH-STR-NBR                 03030000
                   PERFORM 2300-WRITE-FOOTERS                           03040000
                   INITIALIZE PC-PAGE-COUNTER                           03050000
                   PERFORM 2100-WRITE-HEADINGS                          03060000
                   INITIALIZE PH-PROGRAM-HOLDS-2                        03070000
                   PERFORM 2200-WRITE-DETAIL                            03080000
                   MOVE PXRD142-STR-NBR TO PH-STR-NBR                   03090000
                   MOVE PXRD142-BUS-AREA-ID                             03100000
                                        TO PH-BUS-AREA-ID               03110000
           END-EVALUATE.                                                03120000
                                                                        03130000
           PERFORM 2999-READ-INPUT-FILE.                                03140000
                                                                        03150000
      ***************************************************************** 03160000
      **WRITE REPORT HEADERS                                            03170000
      ***************************************************************** 03180000
       2100-WRITE-HEADINGS.                                             03190000
                                                                        03200000
           MOVE '2100-WRITE-HEADINGS'  TO PV-PARAGRAPH-NAME.            03210000
                                                                        03220000
           INITIALIZE PC-REPORT-LINE-COUNTER.                           03230000
                                                                        03240000
           ADD +1                      TO PC-PAGE-COUNTER.              03250000
           MOVE PC-PAGE-COUNTER        TO DP132O-PAGE-NUMBER.           03260000
                                                                        03270000
           MOVE PXRD142-STR-NBR        TO RH-HEADINGS-1-STR-NBR.        03280000
           MOVE PXRD142-LOC-NM         TO RH-HEADING-1-STR-NAME.        03290000
           MOVE PXRD142-BUS-AREA-ID    TO RH-HEADINGS-3-BUS-ID.         03300000
           MOVE PXRD142-BUS-AREA-DESC  TO RH-HEADINGS-3-BUS-DESC.       03310000
           MOVE PXRD142-EFF-DATE       TO RH-HEADINGS-2-EFF-DATE.       03320000
                                                                        03330000
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS      03340000
                               AFTER ADVANCING PAGE.                    03350000
                                                                        03360000
           WRITE REPORT-RECORD FROM RH-TITLE-1 AFTER ADVANCING 1 LINE.  03370000
                                                                        03380000
           WRITE REPORT-RECORD FROM RH-HEADINGS-1                       03390000
                               AFTER ADVANCING 2 LINES.                 03400000
           WRITE REPORT-RECORD FROM RH-HEADINGS-2                       03410000
                               AFTER ADVANCING 1 LINES.                 03420000
           WRITE REPORT-RECORD FROM RH-HEADINGS-3                       03430000
                               AFTER ADVANCING 1 LINES.                 03440000
           WRITE REPORT-RECORD FROM RH-HEADINGS-3B                      03450000
                               AFTER ADVANCING 2 LINES.                 03460000
           WRITE REPORT-RECORD FROM RH-HEADINGS-4                       03470000
                               AFTER ADVANCING 1 LINES.                 03480000
           WRITE REPORT-RECORD FROM RH-HEADINGS-5                       03490000
                               AFTER ADVANCING 1 LINES.                 03500000
           WRITE REPORT-RECORD FROM RH-HEADINGS-6                       03510000
                               AFTER ADVANCING 1 LINES.                 03520000
                                                                        03530000
           ADD +11             TO PC-REPORT-LINE-COUNTER.               03540000
                                                                        03550000
      ***************************************************************** 03560000
      **WRITE DETAIL LINE                                               03570000
      ***************************************************************** 03580000
       2200-WRITE-DETAIL.                                               03590000
                                                                        03600000
           MOVE '2200-WRITE-DETAIL'    TO PV-PARAGRAPH-NAME.            03610000
                                                                        03620000
           INITIALIZE RH-DETAIL-LINE.                                   03630000
                                                                        03640000
           IF  PH-DEPT-NBR NOT EQUAL PXRD142-DEPT-NBR                   03650000
               MOVE PXRD142-DEPT-NBR   TO RH-COLUMN-1-DEPT-NBR          03660000
           END-IF.                                                      03670000
                                                                        03680000
           IF PH-SUB-CL-NBR NOT EQUAL PXRD142-SUB-CL-NBR                03690000
               MOVE PXRD142-SUB-CL-NBR TO RH-COLUMN-1-SUB-CLASS         03700000
           END-IF.                                                      03710000
                                                                        03720000
           MOVE PXRD142-VS-NBR         TO RH-COLUMN-1-VS-NBR.           03730000
           MOVE PXRD142-VS-DESC        TO RH-COLUMN-1-VS-DESC.          03740000
           MOVE PXRD142-LBL-NM         TO RH-COLUMN-1-LBL-NM.           03750000
           MOVE PXRD142-STKR-NBR       TO RH-COLUMN-1-STKR-NBR.         03760000
           MOVE PXRD142-UNT-RTL-AMT    TO RH-COLUMN-1-RETAIL.           03770000
                                                                        03780001
           IF  PXRD142-LAST-NCLR-RTL-AMT > 0                            03790001
      ***NCLR SHOULD NEVER BE ZEROS, BUT JUST IN CASE                   03800001
              COMPUTE RH-COLUMN-1-PCT-OFF =                             03810001
                 100 *( (PXRD142-LAST-NCLR-RTL-AMT                      03820001
                 - PXRD142-UNT-RTL-AMT)                                 03830001
                 /  PXRD142-LAST-NCLR-RTL-AMT)                          03840001
           ELSE                                                         03850001
               MOVE ZEROS              TO  RH-COLUMN-1-PCT-OFF          03860001
           END-IF.                                                      03870001
                                                                        03880000
           ADD +1                      TO PC-REPORT-LINE-COUNTER.       03890000
                                                                        03900000
           WRITE REPORT-RECORD FROM RH-DETAIL-LINE.                     03910000
                                                                        03920000
           MOVE PXRD142-DEPT-NBR       TO PH-DEPT-NBR.                  03930000
           MOVE PXRD142-SUB-CL-NBR     TO PH-SUB-CL-NBR.                03940000
                                                                        03950000
           ADD +1                      TO PC-REPORT-RECORDS-WRITTEN.    03960000
                                                                        03970000
      ***************************************************************** 03980000
      **WRITE FOOTERS                                                   03990000
      ***************************************************************** 04000000
       2300-WRITE-FOOTERS.                                              04010000
                                                                        04020000
           MOVE '2300-WRITE-FOOTERS' TO PV-PARAGRAPH-NAME.              04030000
                                                                        04040000
           WRITE REPORT-RECORD FROM RH-FOOTERS-1.                       04050000
                                                                        04060000
      ***************************************************************** 04070000
      **READ INPUT FILE                                                 04080000
      ***************************************************************** 04090000
       2999-READ-INPUT-FILE.                                            04100000
                                                                        04110000
           READ INPUT-FILE INTO PXRD142-YELLOW-TICKET-REC               04120000
                AT END SET PS-END-OF-INPUT-FILE TO TRUE                 04130000
           END-READ.                                                    04140000
                                                                        04150000
           IF  PS-NOT-END-OF-INPUT-FILE                                 04160000
               ADD +1 TO PC-INPUT-RECORDS-READ                          04170000
           END-IF.                                                      04180000
                                                                        04190000
      ****************************************************************  04200000
      *END OF PROGRAM ROUTINE                                           04210000
      ****************************************************************  04220000
       9999-WRAPUP.                                                     04230000
                                                                        04240000
           IF  PC-REPORT-RECORDS-WRITTEN > 0                            04250000
               PERFORM 2300-WRITE-FOOTERS                               04260000
           END-IF.                                                      04270000
                                                                        04280000
           CLOSE INPUT-FILE                                             04290000
                 REPORT-FILE.                                           04300000
                                                                        04310000
           PERFORM 9999-DISPLAY-STATISTICS.                             04320000
                                                                        04330000
      ****************************************************************  04340000
      *DISPLAY PROGRAM STATISTICS                                       04350000
      ****************************************************************  04360000
       9999-DISPLAY-STATISTICS.                                         04370000
                                                                        04380000
           DISPLAY '*************************************************'. 04390000
           DISPLAY '* PROGRAM...............: ' PC-CURRENT-PROGRAM-NAME.04400000
           DISPLAY '* DATE..................: ' DF-ACCEPT-DATE.         04410000
           DISPLAY '* TIMESTAMP.............: ' DF-ACCEPT-TIME.         04420000
           DISPLAY '*************************************************'. 04430000
           DISPLAY '* INPUT RECORDS READ.........: '                    04440000
                                          PC-INPUT-RECORDS-READ.        04450000
           DISPLAY '* REPORT RECORDS WRITTEN.....: '                    04460000
                                          PC-REPORT-RECORDS-WRITTEN.    04470000
           DISPLAY '*************************************************'. 04480000
                                                                        04490000
