00001 *                                                                 06/22/94
00002 *                                                                 ETKUT006
00003 *                                                                    LV006
00004  IDENTIFICATION DIVISION.                                         ETKUT006
00005  PROGRAM-ID.    ETKUT006.                                         ETKUT006
00006  AUTHOR.        JO ANN SCHMIDT.                                   ETKUT006
00007  INSTALLATION.  KOHLS DEPARTMENT STORES.                          ETKUT006
00008  DATE-WRITTEN.  03/30/93.                                         ETKUT006
00009  DATE-COMPILED.                                                   ETKUT006
00010                                                                   ETKUT006
00011 ******************************************************************ETKUT006
00012 *                                                                 ETKUT006
00013 *    THIS PURPOSE OF THIS PROGRAM IS TO READ THE SORTED SALES AND ETKUT006
00014 *    ONHANDS EXTRACT FILE, AND MERGE SALES AND ONHANDS DATA FOR   ETKUT006
00015 *    THOSE RECORDS HAVING THE SAME ENTITY ID, DEPT, UPC RECORD    ETKUT006
00016 *    TYPE (ALL STORES, INDIVIDUAL STORE) AND STORE NUMBER.        ETKUT006
00017 *                                                                 ETKUT006
00018 ******************************************************************ETKUT006
00019 *    CHANGE ALL REFERENCES OF DUNNS NUMBER TO ENTITY ID.             CL**4
00020 *                                                                    CL**4
00021 *    CHANGE MADE BY: KIM WRIGHT    04/22/94                          CL**4
************************************************************************        
JF0403*  DATE  04/16/03                                                         
JF0403*  CHANGE MADE:                                                           
JF0403*   UPDATED PROGRAM SO IT IS COMPLIANT WITH NI-SKU OBJECTIVES.            
JF0403*  MADE BY: JON FIEDLER             WR# : 22777                           
************************************************************************        
JF0305*  DATE  02/17/2005                                                       
JF0305*  CHANGE MADE:                                                           
JF0305*   UPDATED PROGRAM FOR STORE EXPANSION TO FIVE DIGITS.                   
JF0305*  MADE BY: JON FIEDLER             WR# : WR26913                         
************************************************************************        
00023 *                                                                 ETKUT006
00024  ENVIRONMENT DIVISION.                                            ETKUT006
00025 *                                                                 ETKUT006
00026  CONFIGURATION SECTION.                                           ETKUT006
00027                                                                   ETKUT006
00028  SOURCE-COMPUTER. IBM-3090.                                       ETKUT006
00029  OBJECT-COMPUTER. IBM-3090.                                       ETKUT006
00030 *                                                                 ETKUT006
00031  INPUT-OUTPUT SECTION.                                            ETKUT006
00032                                                                   ETKUT006
00033  FILE-CONTROL.                                                    ETKUT006
00034                                                                   ETKUT006
00035      SELECT EDI-SORTED-EXTRACT-FILE                               ETKUT006
00036          ASSIGN TO INP01.                                         ETKUT006
00037                                                                   ETKUT006
00038      SELECT EDI-NEW-EXTRACT-FILE                                  ETKUT006
00039          ASSIGN TO OUT01.                                         ETKUT006
00040                                                                   ETKUT006
00041 *    SELECT EDI-REPORT-FILE                                       ETKUT006
00042 *        ASSIGN TO RPT01.                                         ETKUT006
00043 *                                                                 ETKUT006
00044  DATA DIVISION.                                                   ETKUT006
00045 *                                                                 ETKUT006
00046  FILE SECTION.                                                    ETKUT006
00047                                                                   ETKUT006
00048  FD  EDI-SORTED-EXTRACT-FILE                                      ETKUT006
00049      RECORDING MODE IS F                                          ETKUT006
00050      LABEL RECORDS ARE STANDARD                                   ETKUT006
00051      BLOCK CONTAINS 0 RECORDS                                     ETKUT006
00052      RECORD CONTAINS 350 CHARACTERS                               ETKUT006
00053      DATA RECORD IS EDI-SORTED-EXTRACT-RECORD.                    ETKUT006
00054                                                                   ETKUT006
00055  01  EDI-SORTED-EXTRACT-RECORD                 PIC X(350).        ETKUT006
00056                                                                   ETKUT006
00057  FD  EDI-NEW-EXTRACT-FILE                                         ETKUT006
00058      RECORDING MODE IS F                                          ETKUT006
00059      LABEL RECORDS ARE STANDARD                                   ETKUT006
00060      BLOCK CONTAINS 0 RECORDS                                     ETKUT006
00061      RECORD CONTAINS 350 CHARACTERS                               ETKUT006
00062      DATA RECORD IS EDI-NEW-EXTRACT-RECORD.                       ETKUT006
00063                                                                   ETKUT006
00064  01  EDI-NEW-EXTRACT-RECORD                    PIC X(350).        ETKUT006
00065                                                                   ETKUT006
00066  WORKING-STORAGE SECTION.                                         ETKUT006
00067                                                                   ETKUT006
00068  01  PC-PROGRAM-CONSTANTS.                                        ETKUT006
00069                                                                   ETKUT006
00070      05  PC-PROGRAM-NAME                  PIC X(08)  VALUE        ETKUT006
00071                                                      'ETKUT006'.  ETKUT006
JF0305*    05  PC-STORE-ZERO                    PIC X(03)  VALUE '000'. ETKUT006
00073                                                                   ETKUT006
00074      05  PC-MAX-LINES                     PIC S9(04) VALUE +63    ETKUT006
00075                                                      COMP SYNC.   ETKUT006
00076                                                                   ETKUT006
00077  01  PC-PROGRAM-COUNTERS.                                         ETKUT006
00078      05  PC-PAGE-CNTR                     PIC S9(03) VALUE +0     ETKUT006
00079                                                      COMP-3.      ETKUT006
00080      05  PC-LINE-CNTR                     PIC S9(04) VALUE +0     ETKUT006
00081                                                      COMP SYNC.   ETKUT006
00082                                                                   ETKUT006
00083  01  PS-PROGRAM-SUBSCRIPTS.                                       ETKUT006
00084      05  PS-SUB                           PIC S9(04) VALUE +0     ETKUT006
00085                                                      COMP SYNC.   ETKUT006
00086                                                                   ETKUT006
00087  01  PS-PROGRAM-SWITCHES.                                         ETKUT006
00088                                                                   ETKUT006
00089      05  PS-EXTRACT-FILE-EOF-SW           PIC X(01)  VALUE 'N'.   ETKUT006
00090          88  PS-EXTRACT-FILE-EOF                     VALUE 'Y'.   ETKUT006
00091                                                                   ETKUT006
00092      05  PS-DEPT-CL-INHSE-SW              PIC X(01)  VALUE 'N'.   ETKUT006
00093          88  PS-NEW-DEPT-CL-INHSE-REC                VALUE 'Y'.   ETKUT006
00094          88  PS-NO-DEPT-CL-INHSE-REC                 VALUE 'N'.   ETKUT006
00095                                                                   ETKUT006
00096      05  PS-PROCESS-ENTRY-SW              PIC X(01)  VALUE 'N'.   ETKUT006
00097          88  PS-PROCESS-ENTRY                        VALUE 'Y'.   ETKUT006
00098          88  PS-DO-NOT-PROCESS-ENTRY                 VALUE 'N'.   ETKUT006
00099                                                                   ETKUT006
00100      05  PS-PROCESSING-COMPLETE-SW        PIC X(01)  VALUE 'N'.   ETKUT006
00101          88  PS-PROCESSING-COMPLETE                  VALUE 'Y'.   ETKUT006
00102          88  PS-PROCESS-NOT-COMPLETE                 VALUE 'N'.   ETKUT006
00103                                                                   ETKUT006
00104      05  PS-FIRST-ITEM-SW                 PIC X(01)  VALUE 'N'.   ETKUT006
00105          88  PS-FIRST-ITEM                           VALUE 'Y'.   ETKUT006
00106          88  PS-NOT-FIRST-ITEM                       VALUE 'N'.   ETKUT006
00107                                                                   ETKUT006
00108      05  PS-SALES-SW                      PIC X(01)  VALUE 'N'.   ETKUT006
00109          88  PS-SALES                                VALUE 'Y'.   ETKUT006
00110          88  PS-NO-SALES                             VALUE 'N'.   ETKUT006
00111                                                                   ETKUT006
00112      05  PS-ONHANDS-SW                    PIC X(01)  VALUE 'N'.   ETKUT006
00113          88  PS-ONHANDS                              VALUE 'Y'.   ETKUT006
00114          88  PS-NO-ONHANDS                           VALUE 'N'.   ETKUT006
00115                                                                   ETKUT006
00116      05  PS-FIRST-STORE-SW                PIC X(01)  VALUE 'N'.   ETKUT006
00117          88  PS-FIRST-STORE                          VALUE 'Y'.   ETKUT006
00118          88  PS-NOT-FIRST-STORE                      VALUE 'N'.   ETKUT006
00119                                                                   ETKUT006
00120                                                                   ETKUT006
00121  01  ABEND-CODE                           PIC S9(04) VALUE +0     ETKUT006
00122                                                      COMP SYNC.   ETKUT006
00123                                                                   ETKUT006
00124  01  PV-PROGRAM-VARIABLES.                                        ETKUT006
00125                                                                   ETKUT006
00126      05  PV-PARAGRAPH-NAME                PIC X(30)  VALUE SPACES.ETKUT006
00127                                                                   ETKUT006
00128      05  PV-SORTED-EXTRACT-KEY-1.                                 ETKUT006
00129          10  PV-KEY1-ENT-ID               PIC S9(9)  VALUE ZEROES    CL**3
00130                                                      COMP.           CL**3
00131          10  PV-KEY1-DEPT                 PIC X(03)  VALUE SPACES.ETKUT006
JF0403         10  FILLER                       PIC X(02)  VALUE SPACES.ETKUT006
JF0403         10  FILLER                       PIC X(03)  VALUE SPACES.ETKUT006
00134          10  PV-KEY1-UPC                  PIC 9(15)  VALUE ZEROES.ETKUT006
JF0305         10  PV-KEY1-STORE                PIC X(05)  VALUE SPACES.ETKUT006
00136          10  PV-KEY1-RECORD-TYPE          PIC X(01)  VALUE SPACE. ETKUT006
00137                                                                   ETKUT006
00138      05  PV-SORTED-EXTRACT-RECORD-1.                              ETKUT006
00139          10  PV-1-ENT-ID                  PIC S9(9)  VALUE ZEROES    CL**3
00140                                                      COMP.           CL**3
00141          10  FILLER                       PIC X(15)  VALUE SPACES.ETKUT006
00142          10  PV-1-RECORD-TYPE             PIC X(01)  VALUE SPACES.ETKUT006
00143          10  PV-1-DEPT                    PIC X(03)  VALUE SPACES.ETKUT006
JF0403         10  FILLER                       PIC X(02)  VALUE SPACES.ETKUT006
JF0403         10  FILLER                       PIC X(03)  VALUE SPACES.ETKUT006
00146          10  PV-1-UPC-CODE                PIC 9(15)  VALUE ZEROES.ETKUT006
JF0305         10  PV-1-STORE-NUMBER            PIC X(05)  VALUE SPACES.ETKUT006
00148          10  FILLER                       PIC X(18)  VALUE SPACES.ETKUT006
00149          10  FILLER                       PIC X(02)  VALUE SPACES.ETKUT006
00150          10  FILLER                       PIC X(09)  VALUE SPACES.ETKUT006
00151          10  PV-1-UNITS-SOLD-ALL          PIC S9(07) VALUE +0     ETKUT006
00152                                                      COMP-3.      ETKUT006
00153          10  PV-1-ONHAND-QUANTITY         PIC S9(07) VALUE +0     ETKUT006
00154                                                      COMP-3.      ETKUT006
00155          10  FILLER                       PIC X(24)  VALUE SPACES.ETKUT006
00156          10  FILLER                       PIC X(02)  VALUE SPACES.ETKUT006
00157          10  FILLER                       PIC X(241) VALUE SPACES.ETKUT006
00158                                                                   ETKUT006
00159  EJECT                                                            ETKUT006
00160                                                                   ETKUT006
00161      05  PV-SORTED-EXTRACT-KEY-2.                                 ETKUT006
00162          10  PV-KEY2-ENT-ID               PIC S9(9)  VALUE ZEROES    CL**3
00163                                                      COMP.           CL**3
00164          10  PV-KEY2-DEPT                 PIC X(03)  VALUE SPACES.ETKUT006
JF0403         10  FILLER                       PIC X(02)  VALUE SPACES.ETKUT006
JF0403         10  FILLER                       PIC X(03)  VALUE SPACES.ETKUT006
00167          10  PV-KEY2-UPC                  PIC 9(15)  VALUE ZEROES.ETKUT006
JF0305         10  PV-KEY2-STORE                PIC X(05)  VALUE SPACES.ETKUT006
00169          10  PV-KEY2-RECORD-TYPE          PIC X(01)  VALUE SPACE. ETKUT006
00170                                                                   ETKUT006
00171      05  PV-SORTED-EXTRACT-RECORD-2.                              ETKUT006
00172          10  PV-2-ENT-ID                  PIC S9(9)  VALUE ZEROES    CL**3
00173                                                      COMP.           CL**3
00174          10  FILLER                       PIC X(15)  VALUE SPACES.ETKUT006
00175          10  PV-2-RECORD-TYPE             PIC X(01)  VALUE SPACES.ETKUT006
00176          10  PV-2-DEPT                    PIC X(03)  VALUE SPACES.ETKUT006
JF0403         10  FILLER                       PIC X(02)  VALUE SPACES.ETKUT006
JF0403         10  FILLER                       PIC X(03)  VALUE SPACES.ETKUT006
00179          10  PV-2-UPC-CODE                PIC 9(15)  VALUE ZEROES.ETKUT006
JF0305         10  PV-2-STORE-NUMBER            PIC X(05)  VALUE SPACES.ETKUT006
00181          10  FILLER                       PIC X(18)  VALUE SPACES.ETKUT006
00182          10  FILLER                       PIC X(02)  VALUE SPACES.ETKUT006
00183          10  FILLER                       PIC X(09)  VALUE SPACES.ETKUT006
00184          10  PV-2-UNITS-SOLD-ALL          PIC S9(07) VALUE +0     ETKUT006
00185                                                      COMP-3.      ETKUT006
00186          10  PV-2-ONHAND-QUANTITY         PIC S9(07) VALUE +0     ETKUT006
00187                                                      COMP-3.      ETKUT006
00188          10  FILLER                       PIC X(24)  VALUE SPACES.ETKUT006
00189          10  FILLER                       PIC X(02)  VALUE SPACES.ETKUT006
00190          10  FILLER                       PIC X(241) VALUE SPACES.ETKUT006
00191                                                                   ETKUT006
00192  EJECT                                                            ETKUT006
00193                                                                   ETKUT006
00194      05  PV-NEW-EXTRACT-RECORD.                                   ETKUT006
00195          10  PV-NEW-ENT-ID                PIC S9(9)  VALUE ZEROES    CL**3
00196                                                      COMP.           CL**3
00197          10  FILLER                       PIC X(15)  VALUE SPACES.ETKUT006
00198          10  PV-NEW-RECORD-TYPE           PIC X(01)  VALUE SPACES.ETKUT006
00199          10  PV-NEW-DEPT                  PIC X(03)  VALUE SPACES.ETKUT006
JF0403         10  FILLER                       PIC X(02)  VALUE SPACES.ETKUT006
JF0403         10  FILLER                       PIC X(03)  VALUE SPACES.ETKUT006
00202          10  PV-NEW-UPC-CODE              PIC 9(15)  VALUE ZEROES.ETKUT006
JF0305         10  PV-NEW-STORE-NUMBER          PIC X(05)  VALUE SPACES.ETKUT006
00204          10  FILLER                       PIC X(18)  VALUE SPACES.ETKUT006
00205          10  FILLER                       PIC X(02)  VALUE SPACES.ETKUT006
00206          10  FILLER                       PIC X(09)  VALUE SPACES.ETKUT006
00207          10  PV-NEW-UNITS-SOLD-ALL        PIC S9(07) VALUE +0     ETKUT006
00208                                                      COMP-3.      ETKUT006
00209          10  PV-NEW-ONHAND-QUANTITY       PIC S9(07) VALUE +0     ETKUT006
00210                                                      COMP-3.      ETKUT006
00211          10  FILLER                       PIC X(24)  VALUE SPACES.ETKUT006
00212          10  FILLER                       PIC X(02)  VALUE SPACES.ETKUT006
00213          10  FILLER                       PIC X(241) VALUE SPACES.ETKUT006
00214                                                                   ETKUT006
00215      05  PV-TEMP-EXTRACT-RECORD                PIC X(350).        ETKUT006
00216                                                                   ETKUT006
00217  EJECT                                                            ETKUT006
00218 ***************************************************************** ETKUT006
00219 *    EDI ON-HAND/SALES HISTORY EXTRACT RECORD LAYOUT.             ETKUT006
00220 ***************************************************************** ETKUT006
00221      COPY ETRD005.                                                   CL**6
00222                                                                   ETKUT006
00223  EJECT                                                            ETKUT006
00224 ***************************************************************** ETKUT006
00225 *    KOHLS STANDARD 132 COLUMN HEADER LAYOUT.                     ETKUT006
00226 ***************************************************************** ETKUT006
00227      COPY DPWS132O.                                               ETKUT006
00228                                                                   ETKUT006
00229  EJECT                                                            ETKUT006
00230 ***************************************************************** ETKUT006
00231 *   REPORT DEFINITION                                             ETKUT006
00232 ***************************************************************** ETKUT006
00233                                                                   ETKUT006
00234  EJECT                                                            ETKUT006
00235 *                                                                 ETKUT006
00236 *                                                                 ETKUT006
00237  PROCEDURE DIVISION.                                              ETKUT006
00238                                                                   ETKUT006
00239  A100-MAIN-MODULE.                                                ETKUT006
00240      MOVE 'A100-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                ETKUT006
00241 *    DISPLAY PV-PARAGRAPH-NAME.                                   ETKUT006
00242 ******************************************************************ETKUT006
00243 *    MAIN LOGIC PARAGRAPH                                         ETKUT006
00244 ******************************************************************ETKUT006
00245                                                                   ETKUT006
00246      OPEN INPUT  EDI-SORTED-EXTRACT-FILE                          ETKUT006
00247           OUTPUT EDI-NEW-EXTRACT-FILE.                            ETKUT006
00248 *                EDI-REPORT-FILE.                                 ETKUT006
00249                                                                   ETKUT006
00250      PERFORM C100-GET-NEXT-EXTRACT-RECS.                          ETKUT006
00251                                                                   ETKUT006
00252      IF PS-EXTRACT-FILE-EOF                                       ETKUT006
00253          CONTINUE                                                 ETKUT006
00254      ELSE                                                         ETKUT006
00255          PERFORM B100-PROCESS-EXTRACT-FILE                        ETKUT006
00256            UNTIL PS-EXTRACT-FILE-EOF                              ETKUT006
00257      END-IF.                                                      ETKUT006
00258                                                                   ETKUT006
00259      CLOSE EDI-SORTED-EXTRACT-FILE                                ETKUT006
00260            EDI-NEW-EXTRACT-FILE.                                  ETKUT006
00261 *          EDI-REPORT-FILE.                                       ETKUT006
00262                                                                   ETKUT006
00263      STOP RUN.                                                    ETKUT006
00264                                                                   ETKUT006
00265  EJECT                                                            ETKUT006
00266  B100-PROCESS-EXTRACT-FILE.                                       ETKUT006
00267      MOVE 'B100-PROCESS-EXTRACT-FILE' TO PV-PARAGRAPH-NAME.       ETKUT006
00268 *    DISPLAY PV-PARAGRAPH-NAME.                                   ETKUT006
00269 ******************************************************************ETKUT006
00270 *                                                                 ETKUT006
00271 ******************************************************************ETKUT006
00272                                                                   ETKUT006
00273 *    ** CREATE A KEY FOR SORTED EXTRACT RECORD 1 **               ETKUT006
00274      MOVE  PV-1-ENT-ID          TO  PV-KEY1-ENT-ID.               ETKUT006
00275      MOVE  PV-1-DEPT            TO  PV-KEY1-DEPT.                 ETKUT006
00278      MOVE  PV-1-UPC-CODE        TO  PV-KEY1-UPC.                  ETKUT006
00279      MOVE  PV-1-STORE-NUMBER    TO  PV-KEY1-STORE.                ETKUT006
00280      MOVE  PV-1-RECORD-TYPE     TO  PV-KEY1-RECORD-TYPE.          ETKUT006
00281                                                                   ETKUT006
00282 *    ** CREATE A KEY FOR SORTED EXTRACT RECORD 2 **               ETKUT006
00283      MOVE  PV-2-ENT-ID          TO  PV-KEY2-ENT-ID.               ETKUT006
00284      MOVE  PV-2-DEPT            TO  PV-KEY2-DEPT.                 ETKUT006
00287      MOVE  PV-2-UPC-CODE        TO  PV-KEY2-UPC.                  ETKUT006
00288      MOVE  PV-2-STORE-NUMBER    TO  PV-KEY2-STORE.                ETKUT006
00289      MOVE  PV-2-RECORD-TYPE     TO  PV-KEY2-RECORD-TYPE.          ETKUT006
00290                                                                   ETKUT006
00291                                                                   ETKUT006
00292      IF ( (PV-SORTED-EXTRACT-KEY-1 < PV-SORTED-EXTRACT-KEY-2)     ETKUT006
00293           AND ( NOT PS-EXTRACT-FILE-EOF ) )                       ETKUT006
00294                                                                   ETKUT006
00295          IF PV-KEY1-RECORD-TYPE = '2'                             ETKUT006
00296              IF PV-KEY2-RECORD-TYPE = '2'                         ETKUT006
00297 *                ** DROP THE 1ST SORT FILE RECORD **              ETKUT006
00298 *                ** BECAUSE IT HAS NO DETAILS     **              ETKUT006
JF0305                 DISPLAY ' NO DETAILS ==> STORE 00000 RECORD'     ETKUT006
00300                          ' DROPPED FOR UPC : ' PV-KEY1-UPC        ETKUT006
00301 *                CONTINUE                                         ETKUT006
00302              ELSE                                                 ETKUT006
00303 *                ** WRITE THE 1ST SORT FILE RECORD **             ETKUT006
00304                  MOVE PV-SORTED-EXTRACT-RECORD-1                  ETKUT006
00305                    TO PV-NEW-EXTRACT-RECORD                       ETKUT006
00306                  PERFORM Z200-WRITE-NEW-EXTRACT-FILE              ETKUT006
00307              END-IF                                               ETKUT006
00308          ELSE                                                     ETKUT006
00309 *            ** WRITE THE 1ST SORT FILE RECORD **                 ETKUT006
00310              MOVE PV-SORTED-EXTRACT-RECORD-1                      ETKUT006
00311                TO PV-NEW-EXTRACT-RECORD                           ETKUT006
00312              PERFORM Z200-WRITE-NEW-EXTRACT-FILE                  ETKUT006
00313          END-IF                                                   ETKUT006
00314                                                                   ETKUT006
00315 *        ** ADVANCE THE SECOND RECORD TO 1ST RECORD POSITION **   ETKUT006
00316          MOVE PV-SORTED-EXTRACT-RECORD-2                          ETKUT006
00317            TO PV-SORTED-EXTRACT-RECORD-1                          ETKUT006
00318                                                                   ETKUT006
00319 *        ** READ A SECOND SORT FILE RECORD **                     ETKUT006
00320          PERFORM Z100-READ-SORTED-EXTRACT-FILE                    ETKUT006
00321                                                                   ETKUT006
00322          IF PS-EXTRACT-FILE-EOF                                   ETKUT006
00323              IF PV-1-RECORD-TYPE = '2'                            ETKUT006
00324 *               ** DO NOT WRITE OUT '2' REC AT EOF **             ETKUT006
00325                  CONTINUE                                         ETKUT006
00326              ELSE                                                 ETKUT006
00327 *               ** WRITE THE 1ST SORT FILE RECORD **              ETKUT006
00328                  MOVE PV-SORTED-EXTRACT-RECORD-1                  ETKUT006
00329                    TO PV-NEW-EXTRACT-RECORD                       ETKUT006
00330                  PERFORM Z200-WRITE-NEW-EXTRACT-FILE              ETKUT006
00331              END-IF                                               ETKUT006
00332          ELSE                                                     ETKUT006
00333              MOVE EDI-SORTED-EXTRACT-RECORD                       ETKUT006
00334                TO PV-SORTED-EXTRACT-RECORD-2                      ETKUT006
00335          END-IF                                                   ETKUT006
00336      END-IF.                                                      ETKUT006
00337                                                                   ETKUT006
00338      IF ( (PV-SORTED-EXTRACT-KEY-1 = PV-SORTED-EXTRACT-KEY-2)     ETKUT006
00339           AND ( NOT PS-EXTRACT-FILE-EOF ) )                       ETKUT006
00340 *       **                                              **        ETKUT006
JF0403*       ** FOR A GIVEN ENTITY ID, DEPT, UPC,            **        ETKUT006
00342 *       **              RECORD TYPE, AND STORE NUMBER;  **        ETKUT006
00343 *       **    ONE SALES RECORD AND ONE ON-HANDS RECORD  **        ETKUT006
00344 *       **    HAS BEEN IDENTIFIED.                      **        ETKUT006
00345 *       **                                              **        ETKUT006
00346                                                                   ETKUT006
00347          IF PV-1-ONHAND-QUANTITY = 0                              ETKUT006
00348                                                                   ETKUT006
00349              IF PV-2-ONHAND-QUANTITY = 0                          ETKUT006
00350 *                ** THIS CONDITION WILL EXIST WHEN AN       **    ETKUT006
00351 *                ** ONHAND 000 RECORD IS GENERATED, WITH    **    ETKUT006
00352 *                ** ONHANDS OF 0, AND A SALES RECORD EXISTS **    ETKUT006
00353                                                                   ETKUT006
00354                  IF PV-1-UNITS-SOLD-ALL = 0                       ETKUT006
00355                                                                   ETKUT006
00356                      IF PV-2-UNITS-SOLD-ALL = 0                   ETKUT006
00357 *                        ** BOTH 000'S RECORDS INDICATE ZERO **   ETKUT006
00358 *                        ** SALES AND ONHANDS, THERE MAYBE   **   ETKUT006
00359 *                        ** STORE LEVEL DETAIL THAT TOTALS 0 **   ETKUT006
00360                                                                   ETKUT006
00361                          DISPLAY '==> BOTH ON-HANDS AND SALES'    ETKUT006
JF0305                             ' ARE ZERO FOR STORE 00000 RECORDS'  ETKUT006
00363                                                                   ETKUT006
00364 *                        ** WRITE THE 1ST RECORD (SALES) **       ETKUT006
00365                          MOVE PV-SORTED-EXTRACT-RECORD-1          ETKUT006
00366                            TO PV-NEW-EXTRACT-RECORD               ETKUT006
00367                          PERFORM Z200-WRITE-NEW-EXTRACT-FILE      ETKUT006
00368                          PERFORM C100-GET-NEXT-EXTRACT-RECS       ETKUT006
00369                      ELSE                                         ETKUT006
00370 *                        ** WRITE THE 2ND RECORD (SALES) **       ETKUT006
00371                          MOVE PV-SORTED-EXTRACT-RECORD-2          ETKUT006
00372                            TO PV-NEW-EXTRACT-RECORD               ETKUT006
00373                          PERFORM Z200-WRITE-NEW-EXTRACT-FILE      ETKUT006
00374                          PERFORM C100-GET-NEXT-EXTRACT-RECS       ETKUT006
00375                      END-IF                                       ETKUT006
00376                                                                   ETKUT006
00377                  ELSE                                             ETKUT006
00378 *                    ** RECORD 1 IS A SALES ONLY RECORD **        ETKUT006
00379                      MOVE PV-2-ONHAND-QUANTITY                    ETKUT006
00380                        TO PV-1-ONHAND-QUANTITY                    ETKUT006
00381 *                    ** WRITE THE 1ST RECORD (SALES) **           ETKUT006
00382                      MOVE PV-SORTED-EXTRACT-RECORD-1              ETKUT006
00383                        TO PV-NEW-EXTRACT-RECORD                   ETKUT006
00384                      PERFORM Z200-WRITE-NEW-EXTRACT-FILE          ETKUT006
00385                      PERFORM C100-GET-NEXT-EXTRACT-RECS           ETKUT006
00386                  END-IF                                           ETKUT006
00387                                                                   ETKUT006
00388              ELSE                                                 ETKUT006
00389 *                ** RECORD 1 IS A SALES ONLY RECORD, AND   **     ETKUT006
00390 *                ** RECORD 2 IS AN ON-HANDS ONLY RECORD    **     ETKUT006
00391                                                                   ETKUT006
00392                  MOVE PV-2-ONHAND-QUANTITY                        ETKUT006
00393                    TO PV-1-ONHAND-QUANTITY                        ETKUT006
00394                                                                   ETKUT006
00395 *                ** WRITE THE MODIFIED 1ST RECORD **              ETKUT006
00396                  MOVE PV-SORTED-EXTRACT-RECORD-1                  ETKUT006
00397                    TO PV-NEW-EXTRACT-RECORD                       ETKUT006
00398                  PERFORM Z200-WRITE-NEW-EXTRACT-FILE              ETKUT006
00399                                                                   ETKUT006
00400                  PERFORM C100-GET-NEXT-EXTRACT-RECS               ETKUT006
00401                                                                   ETKUT006
00402              END-IF                                               ETKUT006
00403                                                                   ETKUT006
00404          ELSE                                                     ETKUT006
00405 *            ** RECORD 1 IS AN ON-HANDS ONLY RECORD    **         ETKUT006
00406 *            ** THUS, RECORD 2 IS A SALES ONLY RECORD  **         ETKUT006
00407                                                                   ETKUT006
00408              MOVE PV-1-ONHAND-QUANTITY                            ETKUT006
00409                TO PV-2-ONHAND-QUANTITY                            ETKUT006
00410                                                                   ETKUT006
00411 *            ** WRITE THE MODIFIED 2ND RECORD **                  ETKUT006
00412              MOVE PV-SORTED-EXTRACT-RECORD-2                      ETKUT006
00413                TO PV-NEW-EXTRACT-RECORD                           ETKUT006
00416              PERFORM C100-GET-NEXT-EXTRACT-RECS                   ETKUT006
031397                                                                          
031397             IF PV-KEY1-RECORD-TYPE = '2' AND                             
031397                PV-KEY2-RECORD-TYPE = '2' AND                             
031397                PV-1-RECORD-TYPE = '2'                                    
JF0305                   DISPLAY ' NO DETAILS ==> STORE 00000 RECORD'           
031397                           ' DROPPED FOR UPC : ' PV-KEY1-UPC              
031397             ELSE                                                         
00414                 PERFORM Z200-WRITE-NEW-EXTRACT-FILE               ETKUT006
031397             END-IF                                                       
00417                                                                   ETKUT006
00418          END-IF                                                   ETKUT006
00419                                                                   ETKUT006
00420      END-IF.                                                      ETKUT006
00421                                                                   ETKUT006
00422      IF ( (PV-SORTED-EXTRACT-KEY-1 > PV-SORTED-EXTRACT-KEY-2)     ETKUT006
00423           AND ( NOT PS-EXTRACT-FILE-EOF ) )                       ETKUT006
00424 *                                                                 ETKUT006
00425 *         ** THIS CONDITION CAN EXIST ONLY WHEN A '2'             ETKUT006
00426 *         ** RECORD (STORE '000') AND A '1' RECORD                ETKUT006
00427 *         ** FOR STORE '000' EXIST.                               ETKUT006
00428 *         ** (NOTE: SORT ON RECORD-TYPE WAS IN DECENDING ORDER)   ETKUT006
00429 *                                                                 ETKUT006
00430          IF ( (PV-KEY1-ENT-ID = PV-KEY2-ENT-ID) AND               ETKUT006
00431               (PV-KEY1-DEPT = PV-KEY2-DEPT) AND                   ETKUT006
00434               (PV-KEY1-UPC =  PV-KEY2-UPC) AND                    ETKUT006
JF0305              (PV-KEY1-STORE = '00000' ) AND                      ETKUT006
JF0305              (PV-KEY2-STORE = '00000' ) AND                      ETKUT006
00437               (PV-KEY1-RECORD-TYPE = '2' ) AND                    ETKUT006
00438               (PV-KEY2-RECORD-TYPE = '1' ) )                      ETKUT006
00439                                                                   ETKUT006
00440                  MOVE PV-SORTED-EXTRACT-RECORD-1                  ETKUT006
00441                    TO PV-NEW-EXTRACT-RECORD                       ETKUT006
00442                  PERFORM Z200-WRITE-NEW-EXTRACT-FILE              ETKUT006
00443                                                                   ETKUT006
00444                  MOVE PV-SORTED-EXTRACT-RECORD-2                  ETKUT006
00445                    TO PV-NEW-EXTRACT-RECORD                       ETKUT006
00446                  PERFORM Z200-WRITE-NEW-EXTRACT-FILE              ETKUT006
00447                                                                   ETKUT006
00448                  PERFORM C100-GET-NEXT-EXTRACT-RECS               ETKUT006
00449                                                                   ETKUT006
00450          ELSE                                                     ETKUT006
00451               DISPLAY '** ERROR **'                               ETKUT006
00452                       '  KEY-1 IS GREATER THAN KEY-2 '            ETKUT006
00453          END-IF                                                   ETKUT006
00454                                                                   ETKUT006
00455      END-IF.                                                      ETKUT006
00456                                                                   ETKUT006
00457  EJECT                                                            ETKUT006
00458  C100-GET-NEXT-EXTRACT-RECS.                                      ETKUT006
00459      MOVE 'C100-GET-NEXT-EXTRACT-RECS' TO PV-PARAGRAPH-NAME.      ETKUT006
00460 *    DISPLAY PV-PARAGRAPH-NAME.                                   ETKUT006
00461 ******************************************************************ETKUT006
00462 *                                                                 ETKUT006
00463 ******************************************************************ETKUT006
00464                                                                   ETKUT006
00465 *    ** READ FIRST SORT FILE RECORD **                            ETKUT006
00466      PERFORM Z100-READ-SORTED-EXTRACT-FILE.                       ETKUT006
00467                                                                   ETKUT006
00468      IF PS-EXTRACT-FILE-EOF                                       ETKUT006
00469          CONTINUE                                                 ETKUT006
00470      ELSE                                                         ETKUT006
00471          MOVE EDI-SORTED-EXTRACT-RECORD                           ETKUT006
00472            TO PV-SORTED-EXTRACT-RECORD-1                          ETKUT006
00473                                                                   ETKUT006
00474 *        ** READ SECOND SORT FILE RECORD **                       ETKUT006
00475          PERFORM Z100-READ-SORTED-EXTRACT-FILE                    ETKUT006
00476                                                                   ETKUT006
00477          IF PS-EXTRACT-FILE-EOF                                   ETKUT006
00478              IF PV-1-RECORD-TYPE = '2'                            ETKUT006
00479 *               ** DO NOT WRITE OUT '2' REC AT EOF **             ETKUT006
00480                  CONTINUE                                         ETKUT006
00481              ELSE                                                 ETKUT006
00482 *               ** WRITE THE 1ST SORT FILE RECORD **              ETKUT006
00483                  MOVE PV-SORTED-EXTRACT-RECORD-1                  ETKUT006
00484                    TO PV-NEW-EXTRACT-RECORD                       ETKUT006
00485                  PERFORM Z200-WRITE-NEW-EXTRACT-FILE              ETKUT006
00486              END-IF                                               ETKUT006
00487          ELSE                                                     ETKUT006
00488              MOVE EDI-SORTED-EXTRACT-RECORD                       ETKUT006
00489                TO PV-SORTED-EXTRACT-RECORD-2                      ETKUT006
00490          END-IF                                                   ETKUT006
00491                                                                   ETKUT006
00492      END-IF.                                                      ETKUT006
00493                                                                   ETKUT006
00494  EJECT                                                            ETKUT006
00495  Z100-READ-SORTED-EXTRACT-FILE.                                   ETKUT006
00496      MOVE 'Z100-READ-SORTED-EXTRACT-FILE' TO PV-PARAGRAPH-NAME.   ETKUT006
00497 *    DISPLAY PV-PARAGRAPH-NAME.                                   ETKUT006
00498 ******************************************************************ETKUT006
00499 *                                                                 ETKUT006
00500 ******************************************************************ETKUT006
00501                                                                   ETKUT006
00502      READ EDI-SORTED-EXTRACT-FILE                                 ETKUT006
00503      INTO ET005-EDI-SALES-EXTRACT-REC                                CL**5
00504        AT END                                                     ETKUT006
00505              SET PS-EXTRACT-FILE-EOF TO TRUE.                     ETKUT006
00506                                                                   ETKUT006
00507  EJECT                                                            ETKUT006
00508  Z200-WRITE-NEW-EXTRACT-FILE.                                     ETKUT006
00509      MOVE 'Z200-WRITE-NEW-EXTRACT-FILE' TO PV-PARAGRAPH-NAME.     ETKUT006
00510 *    DISPLAY PV-PARAGRAPH-NAME.                                   ETKUT006
00511 ******************************************************************ETKUT006
00512 *                                                                 ETKUT006
00513 ******************************************************************ETKUT006
00514                                                                   ETKUT006
           IF PV-NEW-ENT-ID = ZERO AND                                          
              PV-NEW-DEPT = SPACES AND                                          
              PV-NEW-UPC-CODE = ZERO                                            
                 NEXT SENTENCE                                                  
           ELSE                                                                 
00515      WRITE EDI-NEW-EXTRACT-RECORD                                 ETKUT006
00516       FROM PV-NEW-EXTRACT-RECORD.                                 ETKUT006
00517                                                                   ETKUT006
00518      INITIALIZE PV-NEW-EXTRACT-RECORD.                            ETKUT006
00519                                                                   ETKUT006
00520  EJECT                                                            ETKUT006
