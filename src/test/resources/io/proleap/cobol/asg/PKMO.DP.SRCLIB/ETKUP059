       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    ETKUP059.                                                 
       AUTHOR.        WADE GROVE.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  04/17/07.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM WILL CREATE THE FLAT FILE USED IN ETK5005.                 
      * THE PROGRAM WILL REPLACE THE MANUAL PROCESS PERFORMED BY THE            
      * EDI TEAM TO PROVIDE A FULL 816 FILE. THE DATA WILL BE SELECTED          
      * FROM DB2 TABLE ETTW_TRD_PRTNR_DAT WHICH IS NOW UPDATED BY               
      * MIO THROUGH ONLINE SCREENS.                                             
      ******************************************************************        
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT FULL-816-FILE                                                 
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  FULL-816-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 18 CHARACTERS                                        
           DATA RECORD IS FULL-816-RECORD.                                      
                                                                                
       01  FULL-816-RECORD                      PIC X(18).                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
                                                                                
           05  PC-PROGRAM-NAME                  PIC X(08)  VALUE                
                                                           'ETKUP059'.          
       01  PV-PROGRAM-VARIABLES.                                                
                                                                                
           05  PV-PARAGRAPH-NAME                PIC X(30)  VALUE SPACES.        
           05  PV-DB2-OPERATION-ATTEMPTED       PIC X(30)  VALUE SPACES.        
                                                                                
       01  ABEND-CODE                           PIC S9(04) VALUE +0             
                                                           COMP SYNC.           
       01  WS-FULL-816-RECORD.                                                  
           05  WS-FULL-816-DESC                 PIC X(02).                      
           05  FILLER                           PIC X(01).                      
           05  WS-FULL-816-ID                   PIC X(15).                      
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      *    SQL COMMUNICATIONS AREA                                              
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      *    ETTW_TRD_PRTNR_DAT TABLE LAYOUT                                      
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE ETT00000                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *     SELECT ETTW_TRD_PRTNR_DAT DATA                                      
      *                                                                         
      *****************************************************************         
            EXEC SQL                                                            
               DECLARE FULL_816 CURSOR FOR                                      
               SELECT VEND_INCHG_ID_DESC,                                       
                      VEND_INCHG_ID                                             
               FROM   ETTW_TRD_PRTNR_DAT                                        
            END-EXEC.                                                           
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      ******************************************************************        
      *    OPEN ALL FILES,                                                      
      *    PERFORM INITIALIZATIONS                                              
      *    PROCESS ALL RECORDS IN THE TABLE                                     
      *    CLOSE ALL FILES                                                      
      ******************************************************************        
                                                                                
           MOVE 'A100-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                        
                                                                                
           OPEN OUTPUT FULL-816-FILE.                                           
                                                                                
           PERFORM D100-OPEN-FULL-816-CURSOR.                                   
                                                                                
           PERFORM D200-FETCH-FULL-816-RECS                                     
                   UNTIL SQLCODE = +100.                                        
                                                                                
           PERFORM D300-CLOSE-FULL-816-CURSOR.                                  
                                                                                
           PERFORM D400-CLEAR-ETTW-TRD-PRTNR-DAT.                               
                                                                                
           CLOSE FULL-816-FILE.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
       D100-OPEN-FULL-816-CURSOR.                                               
      ******************************************************************        
                                                                                
           MOVE 'D100-OPEN-FULL-816-CURSOR' TO                                  
                PV-PARAGRAPH-NAME.                                              
                                                                                
           EXEC SQL                                                             
                OPEN FULL_816                                                   
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0 = 'W')                                                  
                                                                                
               MOVE 'OPEN FULL_816 CURSOR'           TO                         
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-ABEND                                               
                                                                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
       D200-FETCH-FULL-816-RECS.                                                
      ******************************************************************        
                                                                                
           MOVE 'D200-FETCH-FULL-816-RECS' TO PV-PARAGRAPH-NAME.                
                                                                                
           EXEC SQL                                                             
               FETCH FULL_816                                                   
               INTO  :ETT00000-VEND-INCHG-ID-DESC,                              
                     :ETT00000-VEND-INCHG-ID                                    
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
                                                                                
              PERFORM F100-WRITE-FULL-816-RECORD                                
                                                                                
           ELSE                                                                 
                                                                                
               IF (SQLCODE NOT = +100) OR                                       
                  (SQLWARN0    = 'W')                                           
                                                                                
                   MOVE 'FETCH-FULL-816-RECS'  TO                               
                        PV-DB2-OPERATION-ATTEMPTED                              
                                                                                
                   PERFORM Z999-ABEND                                           
                                                                                
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
       D300-CLOSE-FULL-816-CURSOR.                                              
      ******************************************************************        
                                                                                
           MOVE 'D300-CLOSE-FULL-816-CURSOR' TO PV-PARAGRAPH-NAME.              
                                                                                
           EXEC SQL                                                             
                CLOSE FULL_816                                                  
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0 = 'W')                                                  
                                                                                
               MOVE 'CLOSE FULL_816'    TO                                      
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-ABEND                                               
                                                                                
           END-IF.                                                              
                                                                                
      *****************************************************************         
       D400-CLEAR-ETTW-TRD-PRTNR-DAT.                                           
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
              DELETE FROM ETTW_TRD_PRTNR_DAT                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +000                                              
                   DISPLAY 'TABLE CLEARED'                                      
               WHEN SQLCODE = +100                                              
                   DISPLAY 'TABLE IS EMPTY'                                     
               WHEN OTHER                                                       
                   MOVE 'D400-CLEAR-ETTWTRD-PRTNR-DAT' TO                       
                   PV-PARAGRAPH-NAME                                            
                   MOVE 'DELETE ETTW_TRD_PRTNR_DAT PROBLEM' TO                  
                   PV-DB2-OPERATION-ATTEMPTED                                   
                   PERFORM Z999-ABEND                                           
           END-EVALUATE.                                                        
      *****************************************************************         
       F100-WRITE-FULL-816-RECORD.                                              
      *****************************************************************         
                                                                                
           INITIALIZE WS-FULL-816-RECORD.                                       
                                                                                
           MOVE ETT00000-VEND-INCHG-ID-DESC TO WS-FULL-816-DESC.                
           MOVE ETT00000-VEND-INCHG-ID      TO WS-FULL-816-ID.                  
                                                                                
           WRITE FULL-816-RECORD FROM WS-FULL-816-RECORD.                       
                                                                                
      *****************************************************************         
       Z999-ABEND.                                                              
      *****************************************************************         
                                                                                
           MOVE 'Z999-ABEND' TO PV-PARAGRAPH-NAME.                              
           DISPLAY '  '.                                                        
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'ETKUP059 - ABEND'.                                          
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
                                                                                
