      *****************************************************************         
      *  PROCEDURES FOR MAINFRAME FAXING USING CommercePath.                    
      *****************************************************************         
       DP028-0000-FAX-START.                                                    
                                                                                
           IF DP028-SENDING-PROGRAM <= SPACES                                   
               DISPLAY '*** FAXING ERROR ***'                                   
               DISPLAY 'REQUIRED FIELD MISSING: '                               
                       'DP028-SENDING-PROGRAM'                                  
               CALL 'ILBOABN0' USING DP028-ABEND-CODE.                  05790000
                                                                                
           IF NOT DP028-TEST-ONE-FILE-OPEN                                      
               PERFORM DP028-0000-OPEN-FILE.                                    
                                                                                
           MOVE '{{Begin}}' TO FAX-LINE.                                        
           WRITE FAX-LINE.                                                      
                                                                                
                                                                                
           IF DP028-COVER-PAGE > SPACES                                         
               MOVE SPACES TO FAX-LINE                                          
               STRING '{{Cover KOHLS'                                           
                      DP028-COVER-PAGE '.COV}}'                                 
                   DELIMITED BY SIZE INTO FAX-LINE                              
               WRITE FAX-LINE.                                                  
                                                                                
           PERFORM  VARYING DP028-TBL FROM 1 BY 1                               
                      UNTIL DP028-TBL > 10                                      
               IF DP028-COVERTEXT-TABLE (DP028-TBL) > SPACES                    
                   PERFORM DP028-0001-COVERTEXT                                 
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
                                                                                
           IF DP028-NO-BANNER-LINE                                              
               MOVE '{{Banner none}}'  TO FAX-LINE                              
               WRITE FAX-LINE.                                                  
                                                                                
                                                                                
           IF DP028-SENDING-PROGRAM > SPACES                                    
               MOVE SPACES TO FAX-LINE                                          
               STRING '{{User1 '                                                
                      DP028-SENDING-PROGRAM '}}'                                
                   DELIMITED BY SIZE INTO FAX-LINE                              
           ELSE                                                                 
               MOVE '{{User1 Program Not Specified}}'  TO FAX-LINE              
           END-IF.                                                              
           WRITE FAX-LINE.                                                      
                                                                                
                                                                                
           IF DP028-FROM-VOICE-NUMBER > ZERO                                    
               MOVE DP028-FROM-VOICE-NUMBER TO DP028-PHONE                      
               PERFORM DP028-0002-FORMAT-PHONE                                  
               STRING '{{Voice ' DP028-PHONE (DP028-START:DP028-LEN)            
                      '}}'                                                      
                   DELIMITED BY SIZE INTO FAX-LINE                              
               WRITE FAX-LINE                                                   
           END-IF.                                                              
                                                                                
           IF DP028-FROM-FAX-NUMBER > ZERO                                      
               MOVE DP028-FROM-FAX-NUMBER TO DP028-PHONE                        
               PERFORM DP028-0002-FORMAT-PHONE                                  
               STRING '{{CSI ' DP028-PHONE (DP028-START:DP028-LEN) '}}'         
                   DELIMITED BY SIZE INTO FAX-LINE                              
               WRITE FAX-LINE                                                   
           END-IF.                                                              
                                                                                
           IF DP028-FROM-PERSON > SPACES                                        
               MOVE SPACES TO FAX-LINE                                          
               STRING '{{Owner '                                                
                      DP028-FROM-PERSON '}}'                                    
                   DELIMITED BY SIZE INTO FAX-LINE                              
               WRITE FAX-LINE                                                   
           END-IF.                                                              
                                                                                
           IF DP028-FROM-COMPANY > SPACES                                       
               MOVE SPACES TO FAX-LINE                                          
               STRING '{{RTI '                                                  
                      DP028-FROM-COMPANY '}}'                                   
                   DELIMITED BY SIZE INTO FAX-LINE                              
               WRITE FAX-LINE                                                   
           END-IF.                                                              
                                                                                
                                                                                
           IF DP028-SUCCESS-FAX-NUMBER > ZERO                                   
               MOVE DP028-SUCCESS-FAX-NUMBER TO DP028-PHONE                     
               PERFORM DP028-0002-FORMAT-PHONE                                  
               STRING '{{OnSuccess Fax '                                        
                       DP028-PHONE (DP028-START:DP028-LEN) '}}'                 
                   DELIMITED BY SIZE INTO FAX-LINE                              
               WRITE FAX-LINE                                                   
           END-IF.                                                              
                                                                                
                                                                                
           IF DP028-ERROR-FAX-NUMBER > ZERO                                     
               MOVE DP028-ERROR-FAX-NUMBER TO DP028-PHONE                       
               PERFORM DP028-0002-FORMAT-PHONE                                  
               STRING '{{OnError Fax '                                          
                       DP028-PHONE (DP028-START:DP028-LEN) '}}'                 
                   DELIMITED BY SIZE INTO FAX-LINE                              
               WRITE FAX-LINE                                                   
           END-IF.                                                              
                                                                                
                                                                                
           IF DP028-STANDARD-QUALITY                                            
               MOVE '{{Quality Standard}}'     TO FAX-LINE                      
               WRITE FAX-LINE.                                                  
                                                                                
           IF DP028-FINE-QUALITY                                                
               MOVE '{{Quality Fine}}'         TO FAX-LINE                      
               WRITE FAX-LINE.                                                  
                                                                                
                                                                                
           PERFORM DP028-0004-SET-UNITS.                                        
                                                                                
                                                                                
           IF DP028-TOP-MARGIN > ZERO                                           
               MOVE SPACES                     TO FAX-LINE                      
               STRING '{{TM ' DP028-TOP-MARGIN '}}'                             
                           DELIMITED BY SIZE INTO FAX-LINE                      
               WRITE FAX-LINE.                                                  
                                                                                
           IF DP028-BOTTOM-MARGIN > ZERO                                        
               MOVE SPACES                     TO FAX-LINE                      
               STRING '{{BM ' DP028-BOTTOM-MARGIN '}}'                          
                           DELIMITED BY SIZE INTO FAX-LINE                      
               WRITE FAX-LINE.                                                  
                                                                                
           IF DP028-LEFT-MARGIN > ZERO                                          
               MOVE SPACES                     TO FAX-LINE                      
               STRING '{{LM ' DP028-LEFT-MARGIN '}}'                            
                           DELIMITED BY SIZE INTO FAX-LINE                      
               WRITE FAX-LINE.                                                  
                                                                                
                                                                                
           IF DP028-FAX-WITHOUT-AFP-FORM                                        
               PERFORM DP028-0003-NO-AFP-FORM.                                  
                                                                                
                                                                                
                                                                                
       DP028-0000-OPEN-FILE.                                                    
                                                                                
           MOVE 'FAX'          TO DP023-DDNAME.                                 
           MOVE SPACES         TO DP023-DSNAME.                                 
           MOVE 'YES'          TO DP023-FREE-CLOSE.                             
           MOVE 'A'            TO DP023-PRINT-CLASS.                            
           IF DP028-TEST-MODE                                                   
           OR DP028-TEST-ONE-FILE                                               
               MOVE 'X'        TO DP023-PRINT-CLASS                             
           END-IF.                                                              
           IF DP028-FAX-WITHOUT-AFP-FORM                                        
               MOVE 'U4901'    TO DP023-PRINT-DEST                              
               MOVE SPACES     TO DP023-MEMBER-NAME                             
           ELSE                                                                 
               MOVE 'U4900'    TO DP023-PRINT-DEST                              
               MOVE 'FAXOUT'   TO DP023-MEMBER-NAME                             
           END-IF.                                                              
           IF DP028-TEST-ONE-FILE                                               
               MOVE 'LOCAL'    TO DP023-PRINT-DEST                              
           END-IF.                                                              
           MOVE 'FB'           TO DP023-RECFM.                                  
      * USE MAX RECORD LENGTH                                                   
           MOVE 9999           TO DP023-RECSZ.                                  
           PERFORM DP023-0000-DYNALC.                                           
           OPEN OUTPUT FAX-FILE.                                                
                                                                                
           IF DP028-TEST-ONE-FILE                                               
               SET DP028-TEST-ONE-FILE-OPEN TO TRUE.                            
                                                                                
           MOVE SPACES         TO FAX-LINE.                                     
           MOVE ZERO           TO DP028-FAX-SIZE.                               
           INSPECT FAX-LINE TALLYING DP028-FAX-SIZE FOR ALL SPACES.             
                                                                                
                                                                                
                                                                                
       DP028-0001-COVERTEXT.                                                    
                                                                                
           COMPUTE DP028-FCL-TBL = DP028-TBL - 1.                               
                                                                                
           MOVE SPACES TO FAX-LINE.                                             
           STRING '{{CoverText ' DP028-FCL-TBL '}}'                             
               DELIMITED BY SIZE INTO FAX-LINE.                                 
           WRITE FAX-LINE.                                                      
                                                                                
           PERFORM  VARYING DP028-LINE FROM 1 BY 1                              
                      UNTIL DP028-LINE > 10                                     
               MOVE DP028-COVERTEXT (DP028-TBL, DP028-LINE) TO FAX-LINE         
               WRITE FAX-LINE                                                   
           END-PERFORM.                                                         
                                                                                
           MOVE '{{EndCoverText}}' TO FAX-LINE.                                 
           WRITE FAX-LINE.                                                      
                                                                                
                                                                                
       DP028-0002-FORMAT-PHONE.                                                 
                                                                                
           MOVE SPACE TO FAX-LINE.                                              
           MOVE ZERO  TO DP028-START.                                           
           INSPECT DP028-PHONE                                                  
               TALLYING DP028-START FOR LEADING SPACE.                          
           PERFORM  VARYING DP028-LEN FROM 24 BY -1                             
                      UNTIL DP028-LEN <= ZERO                                   
                         OR DP028-PHONE (DP028-LEN:1) NOT = ' '                 
               CONTINUE                                                         
           END-PERFORM.                                                         
           COMPUTE DP028-LEN   = DP028-LEN - DP028-START.                       
           COMPUTE DP028-START = DP028-START + 1.                               
                                                                                
                                                                                
       DP028-0003-NO-AFP-FORM.                                                  
                                                                                
           PERFORM DP028-2000-CHANGE-FONT.                                      
                                                                                
           IF DP028-PORTRAIT                                                    
               MOVE '{{Orient Portrait}}'  TO FAX-LINE                          
               WRITE FAX-LINE.                                                  
                                                                                
           IF DP028-LANDSCAPE                                                   
               MOVE '{{Orient Landscape}}' TO FAX-LINE                          
               WRITE FAX-LINE.                                                  
                                                                                
           IF DP028-DEFAULT-ORIENTATION                                         
               IF DP028-FAX-SIZE > +80                                          
                   MOVE '{{Orient Landscape}}' TO FAX-LINE                      
                   WRITE FAX-LINE                                               
               ELSE                                                             
                   MOVE '{{Orient Portrait}}'  TO FAX-LINE                      
                   WRITE FAX-LINE.                                              
                                                                                
                                                                                
                                                                                
       DP028-0004-SET-UNITS.                                                    
                                                                                
           IF DP028-UNITS NOT = DP028-PREV-UNITS                                
               MOVE DP028-UNITS TO DP028-PREV-UNITS                             
               EVALUATE TRUE                                                    
                 WHEN DP028-INCHES                                              
                     MOVE '{{Units Inches}}'      TO FAX-LINE                   
                     WRITE FAX-LINE                                             
                 WHEN DP028-CENTIMETERS                                         
                     MOVE '{{Units Centimeters}}' TO FAX-LINE                   
                     WRITE FAX-LINE                                             
                 WHEN DP028-POINTS                                              
                     MOVE '{{Units Points}}'      TO FAX-LINE                   
                     WRITE FAX-LINE                                             
                 WHEN DP028-PIXELS                                              
                     MOVE '{{Units Pixels}}'      TO FAX-LINE                   
                     WRITE FAX-LINE                                             
               END-EVALUATE.                                                    
                                                                                
                                                                                
                                                                                
       DP028-0005-SET-LINE-WIDTH.                                               
                                                                                
           IF DP028-LINE-WIDTH NOT = DP028-PREV-WIDTH                           
               MOVE DP028-LINE-WIDTH TO DP028-PREV-WIDTH                        
               IF DP028-DEFAULT-WIDTH                                           
                   MOVE '{{LineWidth 1}}' TO FAX-LINE                           
               ELSE                                                             
                   MOVE SPACES TO FAX-LINE                                      
                   STRING '{{LineWidth ' DP028-LINE-WIDTH '}}'                  
                      DELIMITED BY SIZE INTO FAX-LINE                           
               END-IF                                                           
               WRITE FAX-LINE                                                   
           END-IF.                                                              
                                                                                
                                                                                
                                                                                
       DP028-1000-BOX.                                                          
                                                                                
           PERFORM DP028-0004-SET-UNITS.                                        
           PERFORM DP028-0005-SET-LINE-WIDTH.                                   
                                                                                
           MOVE DP028-COORD1-X TO DP028-COORD1-X-FMT.                           
           MOVE DP028-COORD1-Y TO DP028-COORD1-Y-FMT.                           
           MOVE DP028-COORD2-X TO DP028-COORD2-X-FMT.                           
           MOVE DP028-COORD2-Y TO DP028-COORD2-Y-FMT.                           
                                                                                
           MOVE SPACES TO FAX-LINE.                                             
           STRING '{{Box ' DP028-COORD1-X-FMT                                   
                       ',' DP028-COORD1-Y-FMT                                   
                       ' ' DP028-COORD2-X-FMT                                   
                       ',' DP028-COORD2-Y-FMT '}}'                              
                  DELIMITED BY SIZE INTO FAX-LINE.                              
           WRITE FAX-LINE.                                                      
                                                                                
                                                                                
                                                                                
       DP028-1010-RELATIVE-BOX.                                                 
                                                                                
           PERFORM DP028-0004-SET-UNITS.                                        
           PERFORM DP028-0005-SET-LINE-WIDTH.                                   
                                                                                
           MOVE DP028-COORD1-X TO DP028-COORD1-X-FMT.                           
           MOVE DP028-COORD1-Y TO DP028-COORD1-Y-FMT.                           
           MOVE DP028-COORD2-X TO DP028-COORD2-X-FMT.                           
           MOVE DP028-COORD2-Y TO DP028-COORD2-Y-FMT.                           
                                                                                
           MOVE SPACES TO FAX-LINE.                                             
           STRING '{{RBox ' DP028-COORD1-X-FMT                                  
                        ',' DP028-COORD1-Y-FMT                                  
                        ' ' DP028-COORD2-X-FMT                                  
                        ',' DP028-COORD2-Y-FMT '}}'                             
                  DELIMITED BY SIZE INTO FAX-LINE.                              
           WRITE FAX-LINE.                                                      
                                                                                
                                                                                
                                                                                
       DP028-1100-LINE.                                                         
                                                                                
           PERFORM DP028-0004-SET-UNITS.                                        
           PERFORM DP028-0005-SET-LINE-WIDTH.                                   
                                                                                
           MOVE DP028-COORD1-X TO DP028-COORD1-X-FMT.                           
           MOVE DP028-COORD1-Y TO DP028-COORD1-Y-FMT.                           
           MOVE DP028-COORD2-X TO DP028-COORD2-X-FMT.                           
           MOVE DP028-COORD2-Y TO DP028-COORD2-Y-FMT.                           
                                                                                
           MOVE SPACES TO FAX-LINE.                                             
           STRING '{{Line ' DP028-COORD1-X-FMT                                  
                        ',' DP028-COORD1-Y-FMT                                  
                        ' ' DP028-COORD2-X-FMT                                  
                        ',' DP028-COORD2-Y-FMT '}}'                             
                  DELIMITED BY SIZE INTO FAX-LINE.                              
           WRITE FAX-LINE.                                                      
                                                                                
                                                                                
                                                                                
       DP028-1110-RELATIVE-LINE.                                                
                                                                                
           PERFORM DP028-0004-SET-UNITS.                                        
           PERFORM DP028-0005-SET-LINE-WIDTH.                                   
                                                                                
           MOVE DP028-COORD1-X TO DP028-COORD1-X-FMT.                           
           MOVE DP028-COORD1-Y TO DP028-COORD1-Y-FMT.                           
           MOVE DP028-COORD2-X TO DP028-COORD2-X-FMT.                           
           MOVE DP028-COORD2-Y TO DP028-COORD2-Y-FMT.                           
                                                                                
           MOVE SPACES TO FAX-LINE.                                             
           STRING '{{RLine ' DP028-COORD1-X-FMT                                 
                         ',' DP028-COORD1-Y-FMT                                 
                         ' ' DP028-COORD2-X-FMT                                 
                         ',' DP028-COORD2-Y-FMT '}}'                            
                  DELIMITED BY SIZE INTO FAX-LINE.                              
           WRITE FAX-LINE.                                                      
                                                                                
                                                                                
                                                                                
       DP028-2000-CHANGE-FONT.                                                  
                                                                                
           COMPUTE DP028-FAX-CPI-PORT = (DP028-FAX-SIZE +  7) /  8.             
           COMPUTE DP028-FAX-CPI-LAND = (DP028-FAX-SIZE + 10) / 11.             
           IF DP028-FAX-CPI-PORT > 15                                           
               MOVE 07 TO DP028-FAX-PNT-PORT                                    
           ELSE                                                                 
           IF DP028-FAX-CPI-PORT > 10                                           
               MOVE 09 TO DP028-FAX-PNT-PORT                                    
           ELSE                                                                 
               MOVE 12 TO DP028-FAX-PNT-PORT.                                   
                                                                                
           IF DP028-FAX-CPI-LAND > 15                                           
               MOVE 07 TO DP028-FAX-PNT-LAND                                    
           ELSE                                                                 
           IF DP028-FAX-CPI-LAND > 10                                           
               MOVE 09 TO DP028-FAX-PNT-LAND                                    
           ELSE                                                                 
               MOVE 12 TO DP028-FAX-PNT-LAND.                                   
                                                                                
                                                                                
           MOVE SPACES      TO FAX-LINE.                                        
           IF NOT DP028-DEFAULT-FONT                                            
               STRING '{{Font ' DP028-FONT '}}'                                 
                           DELIMITED BY SIZE INTO FAX-LINE                      
               WRITE FAX-LINE                                                   
           ELSE                                                                 
           IF DP028-PORTRAIT                                                    
               STRING '{{Font CourB' DP028-FAX-PNT-PORT                         
                      ' ' DP028-FAX-PNT-PORT                                    
                      ' ' DP028-FAX-CPI-PORT '}}'                               
                       DELIMITED BY SIZE INTO FAX-LINE                          
               WRITE FAX-LINE                                                   
           ELSE                                                                 
           IF DP028-LANDSCAPE                                                   
               STRING '{{Font CourB' DP028-FAX-PNT-LAND                         
                      ' ' DP028-FAX-PNT-LAND                                    
                      ' ' DP028-FAX-CPI-LAND '}}'                               
                       DELIMITED BY SIZE INTO FAX-LINE                          
               WRITE FAX-LINE                                                   
           ELSE                                                                 
           IF DP028-FAX-SIZE > +80                                              
               STRING '{{Font CourB' DP028-FAX-PNT-LAND                         
                      ' ' DP028-FAX-PNT-LAND                                    
                      ' ' DP028-FAX-CPI-LAND '}}'                               
                       DELIMITED BY SIZE INTO FAX-LINE                          
               WRITE FAX-LINE                                                   
           ELSE                                                                 
               STRING '{{Font CourB' DP028-FAX-PNT-PORT                         
                      ' ' DP028-FAX-PNT-PORT                                    
                      ' ' DP028-FAX-CPI-PORT '}}'                               
                       DELIMITED BY SIZE INTO FAX-LINE                          
               WRITE FAX-LINE.                                                  
                                                                                
                                                                                
                                                                                
       DP028-9000-FAX-END.                                                      
                                                                                
      **** IF DP028-TO-FAX-NUMBER (1) <= SPACES                                 
      ****     DISPLAY '*** FAXING ERROR ***'                                   
      ****     DISPLAY 'REQUIRED FIELD MISSING: '                               
      ****             'DP028-TO-FAX-NUMBER (1)'                                
      ****     CALL 'ILBOABN0' USING DP028-ABEND-CODE.                  05790000
                                                                                
           IF DP028-TO-COMPANY (1) <= SPACES                                    
               DISPLAY '*** FAXING ERROR ***'                                   
               DISPLAY 'REQUIRED FIELD MISSING: '                               
                       'DP028-TO-COMPANY (1)'                                   
               CALL 'ILBOABN0' USING DP028-ABEND-CODE.                  05790000
                                                                                
           IF DP028-FAX-WITH-AFP-FORM                                           
               MOVE '{{BeginPCL}}' TO FAX-LINE                                  
               WRITE FAX-LINE                                                   
               MOVE '{{EndPCL}}' TO FAX-LINE                                    
               WRITE FAX-LINE.                                                  
                                                                                
      * ALWAYS SEND FIRST OCCURANCE.                                            
           SET DP028-IDX TO 1.                                                  
           PERFORM DP028-9001-DESTINATION.                                      
                                                                                
           PERFORM  VARYING DP028-IDX FROM 2 BY 1                               
                      UNTIL DP028-IDX > 10                                      
               IF DP028-TO-FAX-NUMBER    (DP028-IDX) > SPACES                   
                   PERFORM DP028-9001-DESTINATION                               
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           MOVE '{{End}}' TO FAX-LINE.                                          
           WRITE FAX-LINE.                                                      
                                                                                
           IF NOT DP028-TEST-ONE-FILE-OPEN                                      
               CLOSE FAX-FILE.                                                  
                                                                                
           INITIALIZE DP028-SEND-TO-INFO                                        
                      DP028-CT-TABLES.                                          
                                                                                
                                                                                
                                                                                
       DP028-9001-DESTINATION.                                                  
                                                                                
           IF DP028-TO-FAX-NUMBER (DP028-IDX) > SPACES                          
               MOVE DP028-TO-FAX-NUMBER (DP028-IDX) TO DP028-PHONE              
               PERFORM DP028-0002-FORMAT-PHONE                                  
               STRING '{{Fax '                                                  
                       DP028-PHONE (DP028-START:DP028-LEN) '}}'                 
                   DELIMITED BY SIZE INTO FAX-LINE                              
               WRITE FAX-LINE                                                   
           END-IF.                                                              
                                                                                
           IF DP028-ALT-FAX-NUMBER (DP028-IDX) > ZERO                           
               MOVE DP028-ALT-FAX-NUMBER (DP028-IDX) TO DP028-PHONE             
               PERFORM DP028-0002-FORMAT-PHONE                                  
               STRING '{{AltFax '                                               
                       DP028-PHONE (DP028-START:DP028-LEN) '}}'                 
                   DELIMITED BY SIZE INTO FAX-LINE                              
               WRITE FAX-LINE                                                   
           END-IF.                                                              
                                                                                
           IF DP028-TO-PERSON (DP028-IDX) > SPACES                              
               MOVE SPACES TO FAX-LINE                                          
               STRING '{{Contact '                                              
                      DP028-TO-PERSON (DP028-IDX) '}}'                          
                   DELIMITED BY SIZE INTO FAX-LINE                              
               WRITE FAX-LINE                                                   
           END-IF.                                                              
                                                                                
           IF DP028-TO-COMPANY (DP028-IDX) > SPACES                             
               MOVE SPACES TO FAX-LINE                                          
               STRING '{{Company '                                              
                      DP028-TO-COMPANY (DP028-IDX) '}}'                         
                   DELIMITED BY SIZE INTO FAX-LINE                              
               WRITE FAX-LINE                                                   
           END-IF.                                                              
