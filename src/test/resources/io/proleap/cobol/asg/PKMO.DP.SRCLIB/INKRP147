       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    INKRP147.                                                 
       AUTHOR.        JIM SHIFF.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  04/15/00.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *     INKRP147 - TOTAL PIECE COUNT BY AREA                                
      *                                                                         
      * THIS PROGRAM READS AN EXTRACT FROM THE STORE LEVEL AND PRODUCES         
      * THE THE TOTAL COUNT BY AREA FOR EACH STORE REPORT.                      
      *                                                                         
      * TOTALS ARE REPORTED FOR THE FOLLOWING LEVELS:                           
      *       STORE                                                             
      *       AREA                                                              
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
W21732* W21732   11-01-2002  NON-INTELLIGENT SKU. - IGSI                        
25248 * 25248    10-06-2003  UPC CAPABILITIES -ROD JANSSEN                      
W26600*                                                                         
W26600* W26600   06-  -2004  PHYSICAL INVENTORY STORE EXPANSION                 
W26600*                      M JOHNSON                                          
W26600*                                                                         
003400******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT STORE-EXTRACT-FILE                                            
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT STORE-REPORT-FILE                                             
               ASSIGN RPT01.                                                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  STORE-EXTRACT-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-EXTRACT-RECORD            PIC  X(30).                          
                                                                                
       FD  STORE-REPORT-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-REPORT-RECORD             PIC  X(132).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-LINE-CNT               PIC 9(02) VALUE 57.                
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-LINE-CNT                   PIC 99 VALUE 60.                   
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
W26600     05  PV-PREV-STORE                 PIC X(04)  VALUE SPACE.            
           05  PV-PREV-AREA                  PIC S9(09) COMP VALUE 0.           
           05  PV-ADD-AREA-QTY               PIC S9(09) VALUE ZERO.             
           05  PV-ADD-STORE-QTY              PIC S9(09) VALUE ZERO.             
           05  PV-FIRST-PASS                 PIC 9      VALUE ZERO.             
           05  PV-DEPT-NBR                   PIC  X(03) VALUE SPACE.            
           05  PV-PAGE-CNT                   PIC S9(05)  COMP-3                 
                                                           VALUE ZERO.          
25248      05  PV-EDIT-UPC-SKU      PIC 9(15)  VALUE ZEROS.                     
25248      05  PV-EDIT-UPC-SKU-ALPH REDEFINES                                   
25248          PV-EDIT-UPC-SKU.                                                 
25248          10  PV-EDIT-UPC      PIC X(07).                                  
25248          10  PV-EDIT-SKU      PIC X(08).                                  
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-STORE-EXTRACT-SW    PIC X     VALUE 'N'.               
               88 PS-END-OF-STORE-EXTRACT             VALUE 'Y'.                
                                                                                
       01  TD-TODAYS-DATE.                                                      
           05  TD-YEAR                      PIC  9(02)     VALUE ZERO.          
           05  TD-MONTH                     PIC  9(02)     VALUE ZERO.          
           05  TD-DAY                       PIC  9(02)     VALUE ZERO.          
                                                                                
       01  TT-TODAYS-TIME.                                                      
           05  TT-HOUR                      PIC  9(02)     VALUE ZERO.          
           05  TT-MINUTE                    PIC  9(02)     VALUE ZERO.          
           05  FILLER                       PIC  9(04)     VALUE ZERO.          
                                                                                
      ******************************************************************        
      *  KOHL'S STANDARD FIRST HEADING LINE                                     
      ******************************************************************        
           COPY DPWS132O.                                                       
                                                                                
       01  HL2-HEADING-LINE-2.                                                  
25248      05  FILLER                       PIC X(48) VALUE SPACE.              
25248      05  FILLER                       PIC X(37) VALUE                     
25248          'TOTAL PIECE COUNT BY AREA, BY UPC/SKU'.                         
25248      05  FILLER                       PIC X(47) VALUE SPACE.              
                                                                                
       01  HL3-HEADING-LINE-3.                                                  
           05  FILLER                       PIC X(02)  VALUE SPACE.             
           05  FILLER                       PIC X(06)  VALUE 'STORE:'.          
           05  FILLER                       PIC X(02)  VALUE SPACE.             
W26600     05  HL3-STORE-NAME               PIC X(04)  VALUE SPACE.             
W26600     05  FILLER                       PIC X(118) VALUE SPACE.             
                                                                                
                                                                                
       01  HL4-HEADING-LINE-4.                                                  
25248      05  FILLER                       PIC X(20) VALUE SPACE.              
25248      05  FILLER                       PIC X(11) VALUE                     
25248                                             'UPC/SKU    '.                
           05  FILLER                       PIC X(05) VALUE SPACE.              
           05  FILLER                       PIC X(11) VALUE                     
                                            'AREA NUMBER'.                      
           05  FILLER                       PIC X(10) VALUE SPACES.             
           05  FILLER                       PIC X(10) VALUE                     
                                            'QUANTITY  '.                       
W21732     05  FILLER                       PIC X(72) VALUE SPACE.              
                                                                                
       01  DL1-DETAIL-LINE.                                                     
W21732     05  FILLER                       PIC X(15) VALUE SPACE.              
25248      05  DL1-UPC-SKU.                                                     
25248          10  DL1-UPC                  PIC X(07) VALUE SPACES.             
25248          10  DL1-SKU                  PIC X(08) VALUE SPACES.             
           05  FILLER                       PIC X(04) VALUE SPACE.              
           05  DL1-AREA                     PIC ZZZZZZZZ9.                      
           05  FILLER                       PIC X(08) VALUE SPACE.              
           05  DL1-QTY                      PIC ZZZ,ZZZ,ZZ9.                    
W21732     05  FILLER                       PIC X(74) VALUE SPACE.              
       01  ST-SUB-TOTAL-LINE-1.                                                 
           05  FILLER                       PIC X(01) VALUE SPACE.              
           05  FILLER                       PIC X(11) VALUE                     
                                            'AREA TOTAL '.                      
           05  FILLER                       PIC X(44) VALUE SPACE.              
           05  ST1-TOTAL-QTY                PIC ZZZ,ZZZ,ZZ9.                    
           05  FILLER                       PIC X(65) VALUE SPACE.              
                                                                                
       01  ST-SUB-TOTAL-LINE-2.                                                 
           05  FILLER                       PIC X(01) VALUE SPACE.              
           05  FILLER                       PIC X(11) VALUE                     
                                            'STORE TOTAL'.                      
           05  FILLER                       PIC X(44) VALUE SPACE.              
           05  ST2-TOTAL-QTY                PIC ZZZ,ZZZ,ZZ9.                    
           05  FILLER                       PIC X(65) VALUE SPACE.              
                                                                                
                                                                                
      *****************************************************************         
      * RECORD LAYOUT FOR STORE EXTRACT FILE                                    
      *****************************************************************         
                                                                                
           COPY INRD135.                                                        
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
       0000-INKRP147.                                                           
                                                                                
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-EXTRACT                                         
               UNTIL PS-END-OF-STORE-EXTRACT.                                   
                                                                                
           PERFORM 8000-EOJ-PROCESS.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  STORE-EXTRACT-FILE                                       
                OUTPUT STORE-REPORT-FILE.                                       
           PERFORM 4000-READ-STORE-EXTRACT-FILE.                                
                                                                                
           MOVE IN135-STORE                TO PV-PREV-STORE                     
                                              HL3-STORE-NAME.                   
           MOVE IN135-AREA                 TO PV-PREV-AREA.                     
                                                                                
      *    BUILD STANDARD REPORT HEADING.                                       
                                                                                
           ACCEPT TD-TODAYS-DATE FROM DATE.                                     
           MOVE TD-YEAR    TO DP132O-RUN-YEAR.                                  
           MOVE TD-MONTH   TO DP132O-RUN-MONTH.                                 
           MOVE TD-DAY     TO DP132O-RUN-DAY.                                   
                                                                                
           ACCEPT TT-TODAYS-TIME FROM TIME.                                     
           MOVE TT-HOUR    TO DP132O-RUN-HOUR.                                  
           MOVE TT-MINUTE  TO DP132O-RUN-MINUTE.                                
                                                                                
           MOVE 'INKRP147' TO DP132O-PROGRAM-NAME.                              
           MOVE 1          TO DP132O-REPORT-NUMBER.                             
                                                                                
                                                                                
       2000-PROCESS-EXTRACT.                                                    
                                                                                
           IF IN135-AREA = PV-PREV-AREA                                         
               IF PV-PREV-STORE  = IN135-STORE                                  
                   CONTINUE                                                     
               ELSE                                                             
                   MOVE PV-ADD-STORE-QTY   TO ST2-TOTAL-QTY                     
                   WRITE STORE-REPORT-RECORD                                    
                       FROM ST-SUB-TOTAL-LINE-2                                 
                       AFTER ADVANCING 1 LINE                                   
                   PERFORM 7200-PRINT-PAGE-HEADINGS                             
                   MOVE 0                  TO PV-ADD-STORE-QTY                  
                   MOVE IN135-STORE        TO PV-PREV-STORE                     
                                              HL3-STORE-NAME                    
               END-IF                                                           
           ELSE                                                                 
               MOVE PV-ADD-AREA-QTY    TO ST1-TOTAL-QTY                         
               WRITE STORE-REPORT-RECORD                                        
                   FROM ST-SUB-TOTAL-LINE-1                                     
                   AFTER ADVANCING 1 LINE                                       
               ADD 1                   TO PA-LINE-CNT                           
               MOVE IN135-AREA         TO PV-PREV-AREA                          
               MOVE 0                  TO PV-ADD-AREA-QTY                       
               IF PV-PREV-STORE  = IN135-STORE                                  
                   CONTINUE                                                     
               ELSE                                                             
                   MOVE PV-ADD-STORE-QTY   TO ST2-TOTAL-QTY                     
                   WRITE STORE-REPORT-RECORD                                    
                       FROM ST-SUB-TOTAL-LINE-2                                 
                       AFTER ADVANCING 1 LINE                                   
                   ADD 1                   TO PA-LINE-CNT                       
                   MOVE IN135-STORE        TO PV-PREV-STORE                     
                                              HL3-STORE-NAME                    
                   MOVE 0                  TO PV-ADD-STORE-QTY                  
               END-IF                                                           
               PERFORM 7200-PRINT-PAGE-HEADINGS                                 
           END-IF.                                                              
                                                                                
           PERFORM 7500-PRINT-DETAIL-LINE.                                      
                                                                                
           PERFORM 4000-READ-STORE-EXTRACT-FILE.                                
                                                                                
      ******************************************************************        
      * READ STORE FILE.                                                        
      ******************************************************************        
       4000-READ-STORE-EXTRACT-FILE.                                            
                                                                                
           READ STORE-EXTRACT-FILE INTO INRD135-PIECE-EXTR-REC                  
               AT END SET PS-END-OF-STORE-EXTRACT TO TRUE.                      
                                                                                
      ******************************************************************        
      * PRINT THE PAGE HEADINGS.                                                
      ******************************************************************        
       7200-PRINT-PAGE-HEADINGS.                                                
                                                                                
           ADD  1           TO PV-PAGE-CNT.                                     
           MOVE PV-PAGE-CNT TO DP132O-PAGE-NUMBER.                              
           MOVE ZERO        TO PA-LINE-CNT.                                     
                                                                                
           WRITE STORE-REPORT-RECORD                                            
               FROM DP132O-STANDRD-HEADER-132-COLS                              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE STORE-REPORT-RECORD                                            
               FROM HL2-HEADING-LINE-2                                          
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE STORE-REPORT-RECORD                                            
               FROM HL3-HEADING-LINE-3                                          
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE STORE-REPORT-RECORD                                            
               FROM HL4-HEADING-LINE-4                                          
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE 3           TO PA-LINE-CNT.                                     
                                                                                
       7500-PRINT-DETAIL-LINE.                                                  
                                                                                
           IF PA-LINE-CNT > PC-MAX-LINE-CNT                                     
               PERFORM 7200-PRINT-PAGE-HEADINGS                                 
           END-IF.                                                              
                                                                                
           MOVE IN135-AREA     TO DL1-AREA.                                     
25248                                                                           
25248      IF IN135-UPC-CODE NOT EQUAL ZEROS                                    
25248          MOVE IN135-UPC-CODE  TO PV-EDIT-UPC-SKU                          
25248          MOVE PV-EDIT-UPC-SKU TO DL1-UPC-SKU                              
25248      ELSE                                                                 
25248          MOVE SPACES          TO DL1-UPC-SKU                              
25248          MOVE IN135-SKU       TO PV-EDIT-UPC-SKU                          
25248          MOVE PV-EDIT-SKU     TO DL1-SKU                                  
25248      END-IF.                                                              
25248                                                                           
           MOVE IN135-QTY      TO DL1-QTY.                                      
           WRITE STORE-REPORT-RECORD                                            
               FROM DL1-DETAIL-LINE                                             
               AFTER ADVANCING 1 LINE.                                          
           ADD  IN135-QTY    TO  PV-ADD-AREA-QTY.                               
           ADD  IN135-QTY    TO  PV-ADD-STORE-QTY.                              
                                                                                
           ADD  1          TO PA-LINE-CNT.                                      
                                                                                
                                                                                
       7800-PRINT-REPORT-TOTALS.                                                
                                                                                
           MOVE PV-ADD-AREA-QTY          TO ST1-TOTAL-QTY.                      
           WRITE STORE-REPORT-RECORD                                            
               FROM ST-SUB-TOTAL-LINE-1                                         
               AFTER ADVANCING 2 LINE.                                          
           MOVE PV-ADD-STORE-QTY         TO ST2-TOTAL-QTY.                      
           WRITE STORE-REPORT-RECORD                                            
               FROM ST-SUB-TOTAL-LINE-2                                         
               AFTER ADVANCING 2 LINE.                                          
                                                                                
                                                                                
                                                                                
       8000-EOJ-PROCESS.                                                        
           PERFORM 7800-PRINT-REPORT-TOTALS                                     
           CLOSE STORE-EXTRACT-FILE                                             
                 STORE-REPORT-FILE.                                             
                                                                                
