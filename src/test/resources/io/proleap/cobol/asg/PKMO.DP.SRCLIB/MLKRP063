       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              MLKRP063.                                       
       AUTHOR.                  LEE YANG.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            10/07/2004.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  PARTITION BALANCE AND CONTROL REPORT                          *        
      *                                                                *        
      *  THIS PROGRAM TAKES A DATE RANGE CONTROL CARD AND REPORT ON    *        
      *  THE PARTITION BALANCE AND CONTROL                             *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
W26006* 26006    2004-12-15  ADD 'MONTHLY' OR 'WEEKLY' TO RPT TITLE    *        
W26006*                      AND REMOVE FROM DETAIL LINE               *        
      *                                                                *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT OPTIONAL REQUEST-CNTL-FILE                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT PARTN-CNTL-RPT                                                
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  REQUEST-CNTL-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  REQUEST-CNTL-REC                PIC X(80).                           
                                                                                
       FD  PARTN-CNTL-RPT                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS REPORT-LINE.                                          
       01  REPORT-LINE                     PIC X(132).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
       01  RQ-CNTL-AREA.                                                        
           05  RQ-COMMENT-IND              PIC X(01)       VALUE SPACE.         
               88  RQ-COMMENTS                             VALUE '*'.           
           05  RQ-BEG-DATE                 PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES RQ-BEG-DATE.                                    
               10  RQ-BEG-DATE-CCYY        PIC X(04).                           
               10  RQ-BEG-DATE-DASH-1      PIC X(01).                           
                   88  REQ-BEG-DATE-VALID-DASH-1           VALUE '-'.           
               10  RQ-BEG-DATE-MM          PIC X(02).                           
               10  RQ-BEG-DATE-DASH-2      PIC X(01).                           
                   88  REQ-BEG-DATE-VALID-DASH-2           VALUE '-'.           
               10  RQ-BEG-DATE-DD          PIC X(02).                           
           05  FILLER                      PIC X(01)       VALUE SPACE.         
           05  RQ-END-DATE                 PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES RQ-END-DATE.                                    
               10  RQ-END-DATE-CCYY        PIC X(04).                           
               10  RQ-END-DATE-DASH-1      PIC X(01).                           
                   88  REQ-END-DATE-VALID-DASH-1           VALUE '-'.           
               10  RQ-END-DATE-MM          PIC X(02).                           
               10  RQ-END-DATE-DASH-2      PIC X(01).                           
                   88  REQ-END-DATE-VALID-DASH-2           VALUE '-'.           
               10  RQ-END-DATE-DD          PIC X(02).                           
           05  FILLER                      PIC X(58)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** REPORT RECORD/LAYOUT BEGIN ***'.                                
                                                                                
      ******************************************************************        
      *  HEADER LINE                                                            
      ******************************************************************        
      *01  DP132O-STANDRD-HEADER-132-COLS.                                      
      *01  DP132O-CNFDL-HDR-132-COLS.                                           
           COPY DPWS132O.                                                       
                                                                                
       01  RP-REPORT-TITLE-1.                                                   
W26006     05  FILLER                      PIC X(43)       VALUE SPACES.        
W26006     05  RP-REPORT-SECTION           PIC X(08)       VALUE SPACES.        
W26006         88  RP-MONTHLY-SECTION                      VALUE                
W26006             ' MONTHLY'.                                                  
W26006         88  RP-WEEKLY-SECTION                       VALUE                
W26006             '  WEEKLY'.                                                  
W26006     05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-REPORT-TITLE             PIC X(37)       VALUE                
               'PARTITION CONTROL AND BALANCE REPORT'.                          
W26006     05  FILLER                      PIC X(43)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-1.                                                  
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(04)       VALUE                
               'DEPT'.                                                          
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE                
               'BEGIN DATE'.                                                    
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(10)       VALUE                
               ' END DATE '.                                                    
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(05)       VALUE                
               'GROUP'.                                                         
           05  FILLER                      PIC X(01)       VALUE SPACES.        
W26006     05  FILLER                      PIC X(20)       VALUE SPACES.        
W26006     05  FILLER                      PIC X(65)       VALUE                
W26006         '         PARTITION NUMBER(FISCAL DATE)'.                        
W26006     05  FILLER                      PIC X(05)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-2.                                                  
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(04)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(10)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(10)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(05)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
W26006     05  RP-HEADER-1-PARTN-INFO OCCURS 5 TIMES.                           
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  FILLER                  PIC X(16)      VALUE ALL '-'.        
W26006     05  FILLER                      PIC X(05)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-DETAIL.                                                    
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-DEPT-NBR                 PIC Z9(03)      VALUE ZEROES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-EFF-BEG-DATE             PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-EFF-END-DATE             PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(06)       VALUE SPACES.        
           05  RP-DEPT-GROUP-CDE           PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
W26006     05  RP-PARTN-INFO-AREA OCCURS 5 TIMES.                               
               10  FILLER                  PIC X(01)       VALUE SPACES.        
               10  RP-PARTN-NBR            PIC ZZZ9        VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE SPACES.        
                   88  RP-PARTN-INFO-MARK-1                VALUE '('.           
               10  RP-FSCL-DATE            PIC X(10)       VALUE SPACES.        
               10  FILLER                  PIC X(01)       VALUE SPACES.        
                   88  RP-PARTN-INFO-MARK-2                VALUE ')'.           
W26006     05  FILLER                      PIC X(05)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-END.                                                       
           05  FILLER                      PIC X(46)       VALUE SPACES.        
           05  FILLER                      PIC X(39)       VALUE                
               '*****  E N D   O F   R E P O R T  *****'.                       
           05  FILLER                      PIC X(47)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-PRINT-LINE-MAX           PIC S9(04)      VALUE +57            
                               COMP.                                            
W26006     05  PC-PARTN-INFO-INDX-MAX      PIC S9(04)      VALUE +5             
                               COMP.                                            
           05  PC-NULL-DTE                 PIC X(10)       VALUE                
                                                           '9999-12-31'.        
           05  PC-Y                        PIC X           VALUE 'Y'.           
           05  PC-N                        PIC X           VALUE 'N'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-PARTN-INFO        PIC X           VALUE 'N'.           
               88  END-OF-PARTN-INFO                       VALUE 'Y'.           
               88  NOT-END-OF-PARTN-INFO                   VALUE 'N'.           
           05  PS-END-OF-REQUEST-CNTL      PIC X           VALUE 'N'.           
               88  END-OF-REQUEST-CNTL                     VALUE 'Y'.           
               88  NOT-END-OF-REQUEST-CNTL                 VALUE 'N'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-PARTN-INFO-INDX          PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PAGE-COUNT               PIC S9(05)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PRINT-LINE-COUNT         PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-ADVANCE-LINE             PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-CONTROL-READ             PIC S9(07)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-RECORD-RETRIEVED         PIC S9(07)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-PREV-TBL-TM-LVL-CDE      PIC X(01)       VALUE SPACES.        
           05  PV-PREV-DEPT-NBR            PIC S9(04)V     VALUE ZEROES         
                               COMP-3.                                          
           05  PV-RQ-FSCL-BEG-DATE         PIC X(10)       VALUE SPACES.        
           05  PV-RQ-FSCL-END-DATE         PIC X(10)       VALUE SPACES.        
           05  PV-PREV-EFF-BEG-DTE         PIC X(10)       VALUE SPACES.        
           05  PV-PREV-DEPT-GP-CDE         PIC X(01)       VALUE SPACES.        
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-CC        PIC X(02).                           
               10  PV-PROCESSING-YY        PIC X(02).                           
               10  PV-PROC-DSH1            PIC X(01).                           
               10  PV-PROCESSING-MM        PIC X(02).                           
               10  PV-PROC-DSH2            PIC X(01).                           
               10  PV-PROCESSING-DD        PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
           05  PV-TODAYS-TIME.                                                  
               10  PV-TODAYS-TIME-HR       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-MIN      PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-SEC      PIC 9(02)       VALUE ZEROES.        
               10  FILLER                  PIC 9(02)       VALUE ZEROES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** SQL WORK AREA BEGIN ***'.                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  CONTROL-CARD-ABEND                          VALUE +4001.         
           88  TABLE-AREA-ABEND                            VALUE +4002.         
           88  NON-SQL-ABEND-CODE                          VALUE +4004.         
           88  LOGICAL-ISSUE-ABEND                         VALUE +4012.         
           88  SQL-ABEND-CODE                              VALUE +4016.         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TMLCNTL - ML'S DATE CONTROL TABLE                          *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TDTATTR - ML'S DATE ATTRIBUTE TABLE                        *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MLT_PARTN_BAL - ML'S PARTITION BALANCE TABLE               *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE MLT00002                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MLT_PARTN_CNTL - ML'S PARTITION CONTROL TABLE              *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE MLT00003                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    PARTITION INFO CURSOR                                      *         
      *---------------------------------------------------------------*         
           EXEC SQL   DECLARE PARTN-INFO-CSR CURSOR FOR                         
                SELECT                                                          
                        A.TBL_TM_LVL_CDE                                        
                       ,A.DEPT_NBR                                              
                       ,A.EFF_BEG_DTE                                           
                       ,COALESCE(A.EFF_END_DTE, :PC-NULL-DTE)                   
                       ,A.DEPT_GP_CDE                                           
                       ,B.FSCL_DTE                                              
                       ,B.PARTN_NBR                                             
                FROM                                                            
                        MLT_PARTN_BAL   A                                       
                       ,MLT_PARTN_CNTL  B                                       
                WHERE                                                           
      *  GET ROWS FROM PARTN_BAL THAT ARE EFFECTIVE FROM CURRENT                
      *  REQUESTED MONTH BEGINNING DATE ALL THE WAY THROUGH                     
      *  CURRENT REQUESTED ENDING DATE    ----BEG>>>>>>>>END>>>                 
                      A.EFF_BEG_DTE <= :PV-RQ-FSCL-END-DATE                     
                  AND COALESCE(A.EFF_END_DTE, :PC-NULL-DTE)                     
                          >= :PV-RQ-FSCL-BEG-DATE                               
                  AND B.FSCL_DTE BETWEEN :PV-RQ-FSCL-BEG-DATE                   
                          AND :PV-RQ-FSCL-END-DATE                              
                  AND B.FSCL_DTE BETWEEN A.EFF_BEG_DTE                          
                          AND COALESCE(A.EFF_END_DTE, :PC-NULL-DTE)             
                  AND A.TBL_TM_LVL_CDE = B.TBL_TM_LVL_CDE                       
                  AND A.DEPT_GP_CDE    = B.DEPT_GP_CDE                          
                ORDER BY                                                        
                        A.TBL_TM_LVL_CDE                                        
                       ,A.DEPT_NBR                                              
                       ,A.EFF_BEG_DTE                                           
                       ,A.DEPT_GP_CDE                                           
                       ,B.FSCL_DTE                                              
           END-EXEC.                                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM Z200-OPEN-PARTN-INFO.                                        
           PERFORM Z225-FETCH-PARTN-INFO.                                       
           IF NOT-END-OF-PARTN-INFO                                             
               PERFORM C200-LOAD-DEPT-GROUP                                     
               PERFORM B200-PROCESS-PARTN-INFO                                  
                   UNTIL END-OF-PARTN-INFO                                      
           END-IF.                                                              
           PERFORM Z250-CLOSE-PARTN-INFO.                                       
                                                                                
           IF PV-CONTROL-READ > ZEROES                                          
               AND PV-RECORD-RETRIEVED > ZEROES                                 
      *  PRINT LAST REPORT-LINE                                                 
               PERFORM Z700-PRINT-DETAILS                                       
           ELSE                                                                 
               SET CONTROL-CARD-ABEND     TO TRUE                               
               DISPLAY ' *** NOTHING TO PROCESS, CHECK INPUT FILE'              
               PERFORM Z999-ABEND                                               
           END-IF.                                                              
                                                                                
           PERFORM B900-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
                                                                                
           MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
           MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
           MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
                                                                                
           MOVE '-' TO PV-PROC-DSH1.                                            
           MOVE '-' TO PV-PROC-DSH2.                                            
                                                                                
           IF PV-TODAYS-DATE-YY > '80'                                          
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC                                    
           END-IF.                                                              
                                                                                
           MOVE PV-TODAYS-DATE-MM  TO DP132O-RUN-MONTH.                         
           MOVE PV-TODAYS-DATE-DD  TO DP132O-RUN-DAY.                           
           MOVE PV-TODAYS-DATE-YY  TO DP132O-RUN-YEAR.                          
           MOVE PV-TODAYS-TIME-HR  TO DP132O-RUN-HOUR.                          
           MOVE PV-TODAYS-TIME-MIN TO DP132O-RUN-MINUTE.                        
           MOVE 'MLKRP063'         TO DP132O-PROGRAM-NAME.                      
                                                                                
           OPEN INPUT  REQUEST-CNTL-FILE                                        
                OUTPUT PARTN-CNTL-RPT.                                          
                                                                                
           PERFORM C100-PROCESS-RQ-CNTL.                                        
                                                                                
           MOVE SPACES             TO RP-REPORT-DETAIL.                         
                                                                                
      ******************************************************************        
      *    PROCESS PARTITION INFO                                               
      ******************************************************************        
       B200-PROCESS-PARTN-INFO.                                                 
                                                                                
           ADD +1                        TO PV-PARTN-INFO-INDX.                 
                                                                                
           IF MLT00002-TBL-TM-LVL-CDE     = PV-PREV-TBL-TM-LVL-CDE              
               AND MLT00002-DEPT-NBR      = PV-PREV-DEPT-NBR                    
               AND MLT00002-EFF-BEG-DTE   = PV-PREV-EFF-BEG-DTE                 
               AND MLT00002-DEPT-GP-CDE   = PV-PREV-DEPT-GP-CDE                 
               AND PV-PARTN-INFO-INDX    <= PC-PARTN-INFO-INDX-MAX              
               PERFORM C300-LOAD-PARTN-INFO                                     
           ELSE                                                                 
               PERFORM Z700-PRINT-DETAILS                                       
                                                                                
               PERFORM C200-LOAD-DEPT-GROUP                                     
                                                                                
               MOVE +1                       TO PV-PARTN-INFO-INDX              
               PERFORM C300-LOAD-PARTN-INFO                                     
           END-IF.                                                              
                                                                                
           PERFORM Z225-FETCH-PARTN-INFO.                                       
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B900-FINALIZE.                                                           
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' **** CONTROL READ:      '                                  
                   PV-CONTROL-READ.                                             
           DISPLAY ' **** ROWS RETRIEVED:    '                                  
                   PV-RECORD-RETRIEVED.                                         
                                                                                
           WRITE REPORT-LINE FROM RP-REPORT-END                                 
               AFTER ADVANCING 3 LINES.                                         
                                                                                
           CLOSE   REQUEST-CNTL-FILE                                            
                   PARTN-CNTL-RPT.                                              
                                                                                
      ******************************************************************        
      *    PROCESS REQUEST CONTROL CARD AND/OR GET REQUEST DATES                
      ******************************************************************        
       C100-PROCESS-RQ-CNTL.                                                    
                                                                                
           DISPLAY 'EXTRACT REQUESTED:'.                                        
           DISPLAY '  ----+----1----+----2----+----3----+----4'.                
           PERFORM Z100-READ-REQUEST-CNTL.                                      
                                                                                
           PERFORM                                                              
               UNTIL END-OF-REQUEST-CNTL                                        
                   OR NOT RQ-COMMENTS                                           
                                                                                
               DISPLAY '  ' RQ-CNTL-AREA                                        
               PERFORM Z100-READ-REQUEST-CNTL                                   
                                                                                
           END-PERFORM.                                                         
                                                                                
           DISPLAY ' '.                                                         
           IF NOT RQ-COMMENTS                                                   
               AND NOT END-OF-REQUEST-CNTL                                      
               DISPLAY '  ' RQ-CNTL-AREA                                        
               IF RQ-BEG-DATE-CCYY IS NUMERIC                                   
                   AND RQ-BEG-DATE-MM IS NUMERIC                                
                   AND RQ-BEG-DATE-DD IS NUMERIC                                
                   AND REQ-BEG-DATE-VALID-DASH-1                                
                   AND REQ-BEG-DATE-VALID-DASH-2                                
                   IF RQ-END-DATE-CCYY IS NUMERIC                               
                       AND RQ-END-DATE-MM IS NUMERIC                            
                       AND RQ-END-DATE-DD IS NUMERIC                            
                       AND REQ-END-DATE-VALID-DASH-1                            
                       AND REQ-END-DATE-VALID-DASH-2                            
                       IF RQ-BEG-DATE > RQ-END-DATE                             
                           DISPLAY ' *** SWAP REQUEST BEG AND END DATE'         
                           MOVE RQ-BEG-DATE   TO DTATTR-KY-DTE                  
                           MOVE RQ-END-DATE   TO RQ-BEG-DATE                    
                           MOVE DTATTR-KY-DTE TO RQ-END-DATE                    
                       END-IF                                                   
                   ELSE                                                         
                       DISPLAY ' *** USE REQUESTED BEG AS END DATE'             
                       MOVE RQ-BEG-DATE TO RQ-END-DATE                          
                   END-IF                                                       
               ELSE                                                             
                   MOVE SPACES TO RQ-BEG-DATE                                   
                                  RQ-END-DATE                                   
               END-IF                                                           
           ELSE                                                                 
               MOVE SPACES TO RQ-BEG-DATE                                       
                              RQ-END-DATE                                       
           END-IF.                                                              
           IF RQ-BEG-DATE = SPACES                                              
               PERFORM C150-GET-ML-CONTROL                                      
               DISPLAY ' *** DEFAULT TO ML CONTROL DATES: '                     
                       RQ-BEG-DATE ' - '                                        
                       RQ-END-DATE                                              
           END-IF.                                                              
                                                                                
      *  GET THE FISCAL MONTH BEGINNING DATE FOR REQUESTED BEGIN DATE           
           EXEC SQL                                                             
                SELECT  X.FSCL_MN_BEG_DTE                                       
                  INTO :PV-RQ-FSCL-BEG-DATE                                     
                  FROM  TDTATTR    X                                            
                  WHERE X.KY_DTE = :RQ-BEG-DATE                                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'GET REQ. FSCL BEG DATE' TO PV-OPER-ATTEMPTED               
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *  GET THE FISCAL MONTH ENDING DATE FOR REQUESTED END DATE                
           EXEC SQL                                                             
                SELECT  Y.FSCL_MN_END_DTE                                       
                  INTO :PV-RQ-FSCL-END-DATE                                     
                  FROM  TDTATTR    Y                                            
                  WHERE Y.KY_DTE = :RQ-END-DATE                                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'GET REQ. FSCL END DATE' TO PV-OPER-ATTEMPTED               
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    GET DEFAULT DATES FROM ML CONTROL                                    
      ******************************************************************        
       C150-GET-ML-CONTROL.                                                     
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                       B.FSCL_MN_BEG_DTE                                        
                      ,B.FSCL_MN_END_DTE                                        
                  INTO                                                          
                       :DTATTR-FSCL-MN-BEG-DTE                                  
                      ,:DTATTR-FSCL-MN-END-DTE                                  
                  FROM                                                          
                       TMLCNTL    A                                             
                      ,TDTATTR    B                                             
                  WHERE                                                         
                       B.KY_DTE    = A.OPN_FSCL_MN_DTE + 1 DAY                  
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               MOVE DTATTR-FSCL-MN-BEG-DTE TO RQ-BEG-DATE                       
               MOVE DTATTR-FSCL-MN-END-DTE TO RQ-END-DATE                       
           ELSE                                                                 
               MOVE 'GET BEG/END REQ DATE' TO PV-OPER-ATTEMPTED                 
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    LOAD DEPARTMENT GROUP INFORMATION                                    
      ******************************************************************        
       C200-LOAD-DEPT-GROUP.                                                    
                                                                                
           IF MLT00002-TBL-TM-LVL-CDE NOT = PV-PREV-TBL-TM-LVL-CDE              
               MOVE ZEROES                   TO PV-PRINT-LINE-COUNT             
           END-IF.                                                              
                                                                                
           IF MLT00002-TBL-TM-LVL-CDE NOT = PV-PREV-TBL-TM-LVL-CDE              
               OR MLT00002-DEPT-NBR   NOT = PV-PREV-DEPT-NBR                    
               OR PV-PRINT-LINE-COUNT     > PC-PRINT-LINE-MAX                   
               OR PV-PRINT-LINE-COUNT     = ZEROES                              
               IF MLT00002-TBL-TM-LVL-CDE = 'M'                                 
W26006             SET RP-MONTHLY-SECTION        TO TRUE                        
               ELSE                                                             
W26006             SET RP-WEEKLY-SECTION         TO TRUE                        
               END-IF                                                           
               MOVE MLT00002-DEPT-NBR        TO RP-DEPT-NBR                     
           END-IF.                                                              
                                                                                
           IF MLT00002-EFF-BEG-DTE    NOT = PV-PREV-EFF-BEG-DTE                 
               OR MLT00002-DEPT-NBR   NOT = PV-PREV-DEPT-NBR                    
               OR PV-PRINT-LINE-COUNT     > PC-PRINT-LINE-MAX                   
               OR PV-PRINT-LINE-COUNT     = ZEROES                              
               MOVE MLT00002-EFF-BEG-DTE     TO RP-EFF-BEG-DATE                 
               IF MLT00002-EFF-END-DTE = PC-NULL-DTE                            
                   MOVE SPACES                   TO RP-EFF-END-DATE             
               ELSE                                                             
                   MOVE MLT00002-EFF-END-DTE     TO RP-EFF-END-DATE             
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF MLT00002-DEPT-GP-CDE    NOT = PV-PREV-DEPT-GP-CDE                 
               OR MLT00002-DEPT-NBR   NOT = PV-PREV-DEPT-NBR                    
               OR PV-PRINT-LINE-COUNT     > PC-PRINT-LINE-MAX                   
               OR PV-PRINT-LINE-COUNT     = ZEROES                              
               MOVE MLT00002-DEPT-GP-CDE     TO RP-DEPT-GROUP-CDE               
           END-IF.                                                              
                                                                                
           MOVE MLT00002-TBL-TM-LVL-CDE  TO PV-PREV-TBL-TM-LVL-CDE.             
           MOVE MLT00002-DEPT-NBR        TO PV-PREV-DEPT-NBR.                   
           MOVE MLT00002-EFF-BEG-DTE     TO PV-PREV-EFF-BEG-DTE.                
           MOVE MLT00002-DEPT-GP-CDE     TO PV-PREV-DEPT-GP-CDE.                
                                                                                
      ******************************************************************        
      *    LOAD PARTITION NUMBER AND FISCAL DATE                                
      ******************************************************************        
       C300-LOAD-PARTN-INFO.                                                    
                                                                                
           MOVE MLT00003-PARTN-NBR                                              
               TO RP-PARTN-NBR(PV-PARTN-INFO-INDX).                             
           SET RP-PARTN-INFO-MARK-1(PV-PARTN-INFO-INDX)                         
               RP-PARTN-INFO-MARK-2(PV-PARTN-INFO-INDX) TO TRUE.                
           MOVE MLT00003-FSCL-DTE                                               
               TO RP-FSCL-DATE(PV-PARTN-INFO-INDX).                             
                                                                                
      ******************************************************************        
      *    READ CONTROL CARD                                                    
      ******************************************************************        
       Z100-READ-REQUEST-CNTL.                                                  
                                                                                
           ADD +1                      TO PV-CONTROL-READ.                      
           READ REQUEST-CNTL-FILE INTO RQ-CNTL-AREA                             
               AT END SET END-OF-REQUEST-CNTL TO TRUE.                          
                                                                                
      ******************************************************************        
      *    OPEN PARTN-INFO CURSOR                                               
      ******************************************************************        
       Z200-OPEN-PARTN-INFO.                                                    
                                                                                
           DISPLAY ' REQUEST DATE: ' RQ-BEG-DATE                                
                   ' THROUGH ' RQ-END-DATE.                                     
           DISPLAY ' REQUESTED FISCAL DATE: ' PV-RQ-FSCL-BEG-DATE               
                   ' THROUGH ' PV-RQ-FSCL-END-DATE.                             
           EXEC SQL                                                             
               OPEN PARTN-INFO-CSR                                              
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               MOVE ZEROES                TO PV-PARTN-INFO-INDX                 
               SET NOT-END-OF-PARTN-INFO  TO TRUE                               
           ELSE                                                                 
               MOVE 'OPEN PARTN-INFO-CSR' TO PV-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    FETCH THE NEXT RECORD FROM PARTN-INFO CURSOR                         
      ******************************************************************        
       Z225-FETCH-PARTN-INFO.                                                   
                                                                                
           EXEC SQL                                                             
                FETCH PARTN-INFO-CSR                                            
                  INTO                                                          
                       :MLT00002-TBL-TM-LVL-CDE                                 
                      ,:MLT00002-DEPT-NBR                                       
                      ,:MLT00002-EFF-BEG-DTE                                    
                      ,:MLT00002-EFF-END-DTE                                    
                      ,:MLT00002-DEPT-GP-CDE                                    
                      ,:MLT00003-FSCL-DTE                                       
                      ,:MLT00003-PARTN-NBR                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   ADD +1                      TO PV-RECORD-RETRIEVED           
               WHEN +100                                                        
                   SET END-OF-PARTN-INFO TO TRUE                                
               WHEN OTHER                                                       
                   MOVE 'FETCH PARTN-INFO-CSR' TO PV-OPER-ATTEMPTED             
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    CLOSE PARTN-INFO CURSOR                                              
      ******************************************************************        
       Z250-CLOSE-PARTN-INFO.                                                   
                                                                                
           EXEC SQL                                                             
                CLOSE PARTN-INFO-CSR                                            
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'CLOSE PARTN-INFO-CSR' TO PV-OPER-ATTEMPTED                 
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    PRINT THE REPORT DETAILS                                             
      ******************************************************************        
       Z700-PRINT-DETAILS.                                                      
                                                                                
           IF PV-PRINT-LINE-COUNT > PC-PRINT-LINE-MAX                           
               OR PV-PRINT-LINE-COUNT = ZEROES                                  
               PERFORM Z750-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           WRITE REPORT-LINE FROM RP-REPORT-DETAIL                              
               AFTER ADVANCING PV-ADVANCE-LINE LINES.                           
                                                                                
           ADD  PV-ADVANCE-LINE TO PV-PRINT-LINE-COUNT.                         
           MOVE 1               TO PV-ADVANCE-LINE.                             
           MOVE SPACES TO RP-REPORT-DETAIL.                                     
                                                                                
      ******************************************************************        
      *    PRINT THE REPORT HEADINGS                                            
      ******************************************************************        
       Z750-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1 TO PV-PAGE-COUNT.                                              
           MOVE PV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                            
           MOVE 1             TO DP132O-REPORT-NUMBER.                          
           WRITE REPORT-LINE FROM DP132O-STANDRD-HEADER-132-COLS                
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE REPORT-LINE FROM RP-REPORT-TITLE-1.                            
                                                                                
           WRITE REPORT-LINE FROM RP-REPORT-HEADER-1                            
               AFTER ADVANCING 2 LINES.                                         
           WRITE REPORT-LINE FROM RP-REPORT-HEADER-2.                           
                                                                                
           MOVE 6 TO PV-PRINT-LINE-COUNT.                                       
           MOVE 2 TO PV-ADVANCE-LINE.                                           
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                                    
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           SET SQL-ABEND-CODE         TO TRUE.                                  
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  MLKRP063'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM Z999-ABEND.                                                  
                                                                                
      ******************************************************************        
      *    ABEND CALL                                                           
      ******************************************************************        
       Z999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
