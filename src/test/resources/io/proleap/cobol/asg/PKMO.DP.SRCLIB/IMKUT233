      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    IMKUT233.                                                 
       AUTHOR.        DAVID FELLENZ.                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  02/14/12.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *    THIS PROGRAM WILL CREATE A EXTRACT FILE THAT WILL FOLLOW             
      *    THE FOLLOWING CRITERIA FOR EITHER A WRITE OR NO WRITE.               
      ******************************************************************        
      *********    WRITE/NO WRITE CRITERIA  ****************************        
      ******************************************************************        
      *    FOUND ON EXTRACT | FOUND ON STYLE  | FOUND ON | WRITE                
      *          INPUT      | ID DOMAIN INPUT |HIST INPUT|NO WRITE              
      *    --------------------------------------------------------             
      *           NO        |     NO          |   NO     | NO WRITE             
      *          YES        |     NO          |   NO     | NO WRITE             
      *           NO        |     NO          |  YES     | NO WRITE             
      *          YES        |    YES          |  YES     | WRITE                
      *          YES        |    YES          |   NO     | WRITE                
      *          YES        |     NO          |  YES     | WRITE                
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT IM-CC-EXT-FILE                                                
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT IM-DOMAIN-STYL-IDS-FILE                                       
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT IM-CC-EXT-HIST-FILE                                           
               ASSIGN TO INP03.                                                 
                                                                                
           SELECT IM-CC-NEW-EXT-FILE                                            
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT IM-CC-TOTAL-VALID-FILE                                        
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT IM-CC-NEW-INVALID-FILE                                        
               ASSIGN TO OUT03.                                                 
                                                                                
           SELECT IM-CC-TOTAL-INVALID-FILE                                      
               ASSIGN TO OUT04.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  IM-CC-EXT-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-CC-EXT-REC                    PIC X(160).                         
                                                                                
       FD  IM-DOMAIN-STYL-IDS-FILE                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-DOMAIN-STYL-IDS-REC           PIC X(8).                           
                                                                                
       FD  IM-CC-EXT-HIST-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-CC-EXT-HIST-REC              PIC X(08).                           
                                                                                
       FD  IM-CC-NEW-EXT-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-CC-NEW-EXT-REC                PIC X(160).                         
                                                                                
       FD  IM-CC-TOTAL-VALID-FILE                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-CC-TOTAL-VALID-REC            PIC X(80).                          
                                                                                
       FD  IM-CC-NEW-INVALID-FILE                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-CC-NEW-INVALID-REC            PIC X(160).                         
                                                                                
       FD  IM-CC-TOTAL-INVALID-FILE                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IM-CC-TOTAL-INVALID-REC          PIC X(80).                          
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(8)   VALUE 'IMKUT233'.        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW            PIC X      VALUE 'N'.               
               88  PS-END-OF-FILE                      VALUE 'Y'.               
               88  PS-NOT-END-OF-FILE                  VALUE 'N'.               
           05  PS-END-OF-STYLID-SW          PIC X      VALUE 'N'.               
               88  PS-END-OF-STYLID-FILE               VALUE 'Y'.               
               88  PS-NOT-END-OF-STYLID-FILE           VALUE 'N'.               
           05  PS-END-OF-HIST-SW            PIC X      VALUE 'N'.               
               88  PS-END-OF-HIST-FILE                 VALUE 'Y'.               
               88  PS-NOT-END-OF-HIST-FILE             VALUE 'N'.               
           05  PS-WRITE-EXT-SW              PIC X      VALUE 'N'.               
               88  PS-WRITE-EXT-REC                    VALUE 'Y'.               
               88  PS-DONT-WRITE-EXT-REC               VALUE 'N'.               
           05  PS-HIST-REC-FOUND-SW         PIC X      VALUE 'N'.               
               88  PS-HIST-REC-NOT-FOUND               VALUE 'Y'.               
               88  PS-HIST-REC-FOUND                   VALUE 'N'.               
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORD-COUNT        PIC 9(09)  VALUE 0.                 
           05  PA-INPUT-DOMAIN-COUNT        PIC 9(09)  VALUE 0.                 
           05  PA-INV-REC-WRITTEN-CNT       PIC 9(09)  VALUE 0.                 
           05  PA-NEW-EXT-RECORD-COUNT      PIC 9(12)  VALUE 0.                 
           05  PA-OUTPUT-RECORD-COUNT1      PIC 9(09)  VALUE 0.                 
           05  PA-INP-MINUS-INVLD-COUNT     PIC 9(12)  VALUE 0.                 
           05  PA-INPUT-HIST-REC-COUNT      PIC 9(12)  VALUE 0.                 
                                                                                
       01  PROGRAM-VARIABLES.                                                   
                                                                                
           05  PV-IN-COUNTER1               PIC 9(9)    VALUE 0.                
           05  PV-OUT-COUNTER1              PIC 9(9)    VALUE 0.                
                                                                                
           05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACES.           
                                                                                
       01  WS-DOMAIN-STYL-ID-FILE.                                              
           05  WS-DOM-STYL-ID-REC           PIC 9(8)    VALUE 0.                
                                                                                
       01  WS-CC-EXT-HIST-FILE.                                                 
           05  WS-CC-EXT-HIST-REC           PIC 9(8)    VALUE 0.                
      ******************************************************************        
      * INPUT FILE LAYOUT FOR IM CC DAILY DELIM FILE                            
      ******************************************************************        
           COPY IMRD123.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-FILE                                            
               UNTIL PS-END-OF-FILE                                             
                                                                                
           PERFORM 8400-WRITE-CC-TOTAL-CNT.                                     
                                                                                
           PERFORM 9000-END-OF-JOB.                                             
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  OPEN ALL FILES.                                                        
      *  READ THE FIRST RECORD FROM THE INPUT FILE.                             
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  IM-CC-EXT-FILE                                           
                       IM-DOMAIN-STYL-IDS-FILE                                  
                       IM-CC-EXT-HIST-FILE                                      
                OUTPUT IM-CC-NEW-EXT-FILE                                       
                       IM-CC-TOTAL-VALID-FILE                                   
                       IM-CC-NEW-INVALID-FILE                                   
                       IM-CC-TOTAL-INVALID-FILE.                                
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 8000-READ-CC-EXT-FILE.                                       
           PERFORM 8300-READ-DOM-STYL-ID.                                       
           PERFORM 8500-READ-CC-EXT-HIST-FILE.                                  
                                                                                
      ******************************************************************        
      *  ONLY CHOOSE RECORDS THAT DO NOT MATCH THE INVALID FILE                 
      *  RECORDS.                                                               
      ******************************************************************        
       2000-PROCESS-FILE.                                                       
                                                                                
           IF IM123-STYL-ID = WS-DOM-STYL-ID-REC                                
DAFDAF*           (Y Y Y - Y Y N - YES WRITE)                                   
                  PERFORM 8100-WRITE-CC-NEW-EXT-REC                             
                  PERFORM 8000-READ-CC-EXT-FILE                                 
           ELSE                                                                 
             IF IM123-STYL-ID < WS-DOM-STYL-ID-REC                              
                PERFORM 3000-CHECK-HISTORY-FILE                                 
                                   UNTIL PS-WRITE-EXT-REC                       
                   OR PS-HIST-REC-NOT-FOUND                                     
                IF PS-HIST-REC-NOT-FOUND                                        
DAFDAF*            (Y N N - DONT WRITE)                                         
                   PERFORM 8200-WRITE-CC-INVALID-REC                            
                   PERFORM 8000-READ-CC-EXT-FILE                                
                   SET PS-HIST-REC-FOUND TO TRUE                                
                ELSE                                                            
DAFDAF*            (Y N Y - YES WRITE)                                          
                   PERFORM 8100-WRITE-CC-NEW-EXT-REC                            
                   PERFORM 8000-READ-CC-EXT-FILE                                
                   SET PS-DONT-WRITE-EXT-REC TO TRUE                            
                END-IF                                                          
             ELSE                                                               
                PERFORM 8300-READ-DOM-STYL-ID                                   
             END-IF                                                             
           END-IF.                                                              
                                                                                
      ************************************************************              
      *  CHECK FOR STYLE ID ON HISTORY FILE                                     
      ************************************************************              
       3000-CHECK-HISTORY-FILE.                                                 
                                                                                
           IF IM123-STYL-ID = WS-CC-EXT-HIST-REC                                
              SET PS-WRITE-EXT-REC TO TRUE                                      
           ELSE                                                                 
               IF IM123-STYL-ID > WS-CC-EXT-HIST-REC                            
                  PERFORM 8500-READ-CC-EXT-HIST-FILE                            
               ELSE                                                             
                  SET PS-HIST-REC-NOT-FOUND TO TRUE                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ************************************************************              
      *  READ THE CC EXTRACT FILE                                               
      ************************************************************              
       8000-READ-CC-EXT-FILE.                                                   
                                                                                
           READ IM-CC-EXT-FILE INTO IM123-CC-DELIM-RECORD                       
               AT END                                                           
                   SET PS-END-OF-FILE TO TRUE                                   
               NOT AT END                                                       
                   ADD 1          TO   PA-INPUT-RECORD-COUNT.                   
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE A CC VALID RECORD.                                               
      ******************************************************************        
       8100-WRITE-CC-NEW-EXT-REC.                                               
                                                                                
           WRITE IM-CC-NEW-EXT-REC                                              
               FROM IM123-CC-DELIM-RECORD.                                      
                                                                                
           ADD 1 TO PA-NEW-EXT-RECORD-COUNT.                                    
                                                                                
      ******************************************************************        
      *  WRITE A CC TOTAL COUNT RECORD                                          
      ******************************************************************        
       8200-WRITE-CC-INVALID-REC.                                               
                                                                                
           WRITE IM-CC-NEW-INVALID-REC                                          
               FROM IM123-CC-DELIM-RECORD.                                      
                                                                                
           ADD 1 TO PA-INV-REC-WRITTEN-CNT.                                     
                                                                                
      ************************************************************              
      *  READ THE INVALID STYLE ID FILE                                         
      ************************************************************              
       8300-READ-DOM-STYL-ID.                                                   
                                                                                
           READ IM-DOMAIN-STYL-IDS-FILE INTO WS-DOM-STYL-ID-REC                 
               AT END                                                           
                   MOVE '99999999'         TO WS-DOM-STYL-ID-REC                
                   SET PS-END-OF-STYLID-FILE TO TRUE                            
               NOT AT END                                                       
                   ADD 1          TO   PA-INPUT-DOMAIN-COUNT.                   
                                                                                
      ******************************************************************        
      *  WRITE TOTAL COUNTS.                                                    
      ******************************************************************        
       8400-WRITE-CC-TOTAL-CNT.                                                 
                                                                                
           WRITE IM-CC-TOTAL-VALID-REC                                          
               FROM PA-NEW-EXT-RECORD-COUNT.                                    
                                                                                
           WRITE IM-CC-TOTAL-INVALID-REC                                        
               FROM PA-INV-REC-WRITTEN-CNT.                                     
                                                                                
      ************************************************************              
      *  READ THE CC EXTRACT HISTORY FILE                                       
      ************************************************************              
       8500-READ-CC-EXT-HIST-FILE.                                              
                                                                                
           READ IM-CC-EXT-HIST-FILE INTO WS-CC-EXT-HIST-REC                     
               AT END                                                           
                   MOVE '99999999'         TO WS-CC-EXT-HIST-REC                
                   SET PS-END-OF-HIST-FILE TO TRUE                              
               NOT AT END                                                       
                   ADD 1          TO   PA-INPUT-HIST-REC-COUNT.                 
                                                                                
      ************************************************************              
      *  DISPLAY RECORD COUNTS AT END OF JOB                                    
      *  CLOSE THE FILES.                                                       
      ************************************************************              
       9000-END-OF-JOB.                                                         
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
                                                                                
           DISPLAY '   IM CC RECORDS INPUT         = '                          
                    PA-INPUT-RECORD-COUNT.                                      
                                                                                
           DISPLAY '   STYLE ID DOMAIN INPUT      = '                           
                    PA-INPUT-DOMAIN-COUNT.                                      
                                                                                
           DISPLAY '   IM CC NEW REC WRITTEN OUTPUT      = '                    
                    PA-NEW-EXT-RECORD-COUNT.                                    
                                                                                
           DISPLAY '   IM CC HISTORY RECS READ      = '                         
                    PA-INPUT-HIST-REC-COUNT.                                    
                                                                                
           DISPLAY '   STYLE ID INVALID WRITTEN OUTPUT   = '                    
                    PA-INV-REC-WRITTEN-CNT.                                     
                                                                                
           SUBTRACT PA-INV-REC-WRITTEN-CNT FROM PA-INPUT-RECORD-COUNT           
                    GIVING PA-INP-MINUS-INVLD-COUNT.                            
                                                                                
           DISPLAY ' IM INP MINUS INVLD TOTAL    = '                            
                    PA-INP-MINUS-INVLD-COUNT.                                   
                                                                                
           CLOSE IM-CC-EXT-FILE                                                 
                 IM-DOMAIN-STYL-IDS-FILE                                        
                 IM-CC-EXT-HIST-FILE                                            
                 IM-CC-NEW-EXT-FILE                                             
                 IM-CC-TOTAL-VALID-FILE                                         
                 IM-CC-NEW-INVALID-FILE                                         
                 IM-CC-TOTAL-INVALID-FILE.                                      
