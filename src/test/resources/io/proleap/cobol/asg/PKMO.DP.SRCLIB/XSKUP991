       IDENTIFICATION DIVISION.                                         00010001
       PROGRAM-ID.    XSKUP991.                                         00020001
       AUTHOR.        COGNIZANT.                                        00030001
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040001
       DATE-WRITTEN.  04/21/2015.                                       00050001
       DATE-COMPILED.                                                   00060001
                                                                        00070001
      ***************************************************************** 00080001
      *  XSKUP991 - THIS IS AN ADHOC PROGRAM TO END THE INDIVIDUAL STR  00090001
      *             SPECIFIC PRICE CHANGE DETAILS IN XST_SKU_LOC AND    00100001
      *             DELETE THE RELAVANT DATA FROM XST_SKU_LOC_ATTR      00110001
      *             TABLE. TUPCPLS TABLE IS CONSIDERED AS THE SOURCE    00120001
      *             OF TRUTH.                                           00130001
      ***************************************************************** 00140001
      *  TABLES ACCESSED ARE:                                           00150001
      *      TSKXREF - SKU CROSS REFERENCE                              00160001
      *      TUPCPLS - UPC PHYSICAL LOCATION STATUS                     00170001
      *  TABLES WRITTEN ARE:                                            00180001
      *      XST_SKU_LOC                                                00190001
      *      XST_SKU_LOC_ATTR                                           00200001
      *  FILES READ ARE:                                                00210001
      *      INP01  - UPC STORE FILE - BASE  INPUT FILE CONTAINING      00220001
      *                                STORE AND UPC DETAILS FOR WHICH  00230001
      *                                CLEAN UP HAS TO BE DONE.         00240001
      *      INP02  - END TMSTP FILE - THIS FILE CONTAINS THE PRICE     00250001
      *                                CHANGE END TIMESTAMP DETAILS     00260001
      *                                FOR WHICH THE ABOVE UPC AND      00270001
      *                                STORE COMBINATION ROW TO BE      00280001
      *                                ENDED IN XST_SKU_LOC TABLE.      00290001
      ***************************************************************** 00300001
      * 04/21/15 - COGNIZANT                                            00310001
      *    - NEW PROGRAM                                                00320001
      ***************************************************************** 00330001
                                                                        00340001
       ENVIRONMENT DIVISION.                                            00350001
       CONFIGURATION SECTION.                                           00360001
       SOURCE-COMPUTER. IBM-3090.                                       00370001
       OBJECT-COMPUTER. IBM-3090.                                       00380001
       INPUT-OUTPUT SECTION.                                            00390001
                                                                        00400001
       FILE-CONTROL.                                                    00410001
           SELECT SKU-UPDATE-FILE              ASSIGN TO INP01.         00420001
           SELECT CONTROL-TMST                 ASSIGN TO INP02.         00430001
                                                                        00440001
      ***************************************************************** 00450001
       DATA DIVISION.                                                   00460001
      ***************************************************************** 00470001
       FILE SECTION.                                                    00480001
                                                                        00490001
       FD  SKU-UPDATE-FILE                                              00500001
           LABEL RECORDS ARE STANDARD                                   00510001
           RECORDING MODE IS F                                          00520001
           BLOCK CONTAINS 0 RECORDS                                     00530001
           RECORD CONTAINS 80 CHARACTERS                                00540001
           DATA RECORD IS PRICE-UPDATE-RECORD.                          00550001
       01  SKU-UPDATE-RECORD           PIC  X(80).                      00560001
      *                                                                 00570001
       FD  CONTROL-TMST                                                 00580001
           LABEL RECORDS ARE STANDARD                                   00590001
           RECORDING MODE IS F                                          00600001
           BLOCK CONTAINS 0 RECORDS                                     00610001
           RECORD CONTAINS 80 CHARACTERS.                               00620001
       01  CONTROL-TMST-RECORD         PIC  X(80).                      00630001
      *                                                                 00640001
       WORKING-STORAGE SECTION.                                         00650001
      ******************************************************************00660001
      *              P R O G R A M     C O N S T A N T S               *00670001
      ******************************************************************00680001
       01  PC-PROGRAM-CONSTANTS.                                        00690001
           05  PC-PROGRAM-NAME                PIC  X(08)   VALUE        00700001
                                                           'XSKUP991'.  00710001
           05  PC-MAXIMUM-RETRY-COUNT         PIC S9(04)   VALUE +2     00720001
                                                           COMP SYNC.   00730001
                                                                        00740001
      ******************************************************************00750001
      *               P R O G R A M     C O U N T E R S                *00760001
      ******************************************************************00770001
       01  PC-PROGRAM-COUNTERS.                                         00780001
           05  PC-SKU-XREF-FND-CNTR           PIC  9(05) VALUE ZEROES.  00790001
           05  PC-SKU-XREF-NTFND-CNTR         PIC  9(05) VALUE ZEROES.  00800001
           05  PC-TOT-STR-PRCD-CNTR           PIC  9(08) VALUE ZEROES.  00810001
           05  PC-TOT-STR-NOT-PRCD-CNTR       PIC  9(08) VALUE ZEROES.  00820001
           05  PC-RECS-READ-CNTR              PIC  9(05) VALUE ZEROES.  00830001
           05  PC-TOT-STR-IN-TUPC-CNTR        PIC  9(05) VALUE ZEROES.  00840001
           05  PC-RETRY-COUNT                 PIC S9(01) VALUE +0       00850001
                                                           COMP.        00860001
           05  PC-COMMIT-CNTR                 PIC  9(08) VALUE ZEROES.  00870001
                                                                        00880001
      ******************************************************************00890001
      *          P R O G R A M     E R R O R     M E S S A G E         *00900001
      ******************************************************************00910001
                                                                        00920001
      ******************************************************************00930001
      *              P R O G R A M     L A Y O U T S                   *00940001
      ******************************************************************00950001
       01  PL-SKU-RECORD.                                               00960001
           05  PL-INPUT-SKU-NBR               PIC S9(08)   VALUE +0.    00970001
           05  FILLER                         PIC  X(72)   VALUE SPACES.00980001
                                                                        00990001
       01  CC-CONTROL-CARD.                                             01000001
           05  CC-END-TMST                    PIC  X(26)   VALUE SPACES.01010001
                                                                        01020001
      ******************************************************************01030001
      *              P R O G R A M     V A R I A B L E S               *01040001
      ******************************************************************01050001
       01  PV-WORK-VARIABLES.                                           01060001
           05  PV-UNT-COST-NULL-IND           PIC S9(04)   VALUE +0     01070001
                                                           COMP.        01080001
           05  PV-ORIG-CLR-NULL-IND           PIC S9(04)   VALUE +0     01090001
                                                           COMP.        01100001
           05  PV-MKDN-CDE-NULL-IND           PIC S9(04)   VALUE +0     01110001
                                                           COMP.        01120001
           05  PV-EFF-END-NULL-IND            PIC S9(04)   VALUE +0     01130001
                                                           COMP.        01140001
           05  PV-GP-PRICE-IND                PIC S9(04)   VALUE +0     01150001
                                                           COMP.        01160001
           05  PV-GP-QTY-IND                  PIC S9(04)   VALUE +0     01170001
                                                           COMP.        01180001
           05  PV-GP-AMT-IND                  PIC S9(04)   VALUE +0     01190001
                                                           COMP.        01200001
           05  PV-PARAGRAPH-NAME              PIC  X(30)   VALUE SPACES.01210001
           05  PV-OPERATION-ATTEMPTED         PIC  X(30)   VALUE SPACES.01220001
                                                                        01230001
      ******************************************************************01240001
      *              P R O G R A M     S W I T C H E S                 *01250001
      ******************************************************************01260001
       01  PS-PROGRAM-SWITCHES.                                         01270001
           05  PS-END-OF-FILE-SW              PIC  X(01)   VALUE 'N'.   01280001
               88  PS-END-OF-FILE                          VALUE 'Y'.   01290001
           05  PS-XSSTORE-ROW-FOUND-SW        PIC  X(01)   VALUE 'N'.   01300001
               88  PS-XSSTORE-ROW-FOUND                    VALUE 'Y'.   01310001
               88  PS-XSSTORE-ROW-NOT-FOUND                VALUE 'N'.   01320001
           05  PS-END-OF-TUPCPLS-CSR-SW       PIC  X(01)   VALUE 'N'.   01330001
               88  PS-END-OF-TUPCPLS-CSR                   VALUE 'Y'.   01340001
               88  PS-NOT-END-OF-TUPCPLS-CSR               VALUE 'N'.   01350001
           05  PS-TSKXREF-SW                  PIC  X(01)   VALUE 'N'.   01360001
               88  PS-TSKXREF-FND                          VALUE 'Y'.   01370001
               88  PS-TSKXREF-NOT-FND                      VALUE 'N'.   01380001
                                                                        01390001
      ******************************************************************01400001
      *          P R O G R A M     A B E N D     C O D E               *01410001
      ******************************************************************01420001
                                                                        01430001
ABD    01  ABEND-CODE                         PIC S9(04)   VALUE +0     01440001
                                                           COMP SYNC.   01450001
           88  PV-ABEND-4013                   VALUE +4013.             01460001
      *        DB2 SQL ABEND                                            01470001
           88  PV-ABEND-4020                   VALUE +4020.             01480001
      *        CONTROL CARD FILE EMPTY                                  01490001
                                                                        01500001
      ******************************************************************01510001
      *               P R O G R A M     D I S P L A Y S                *01520001
      ******************************************************************01530001
       01  PD-PROGRAM-DISPLAYS.                                         01540001
           05  PD-SKU-XREF-FND-CNTR           PIC  ZZZ9(01).            01550001
           05  PD-SKU-XREF-NTFND-CNTR         PIC  ZZZ9(01).            01560001
           05  PD-TOT-STR-PRCD-CNTR           PIC  ZZZZZZZ9(01).        01570001
           05  PD-TOT-STR-NOT-PRCD-CNTR       PIC  ZZZZZZZ9(01).        01580001
           05  PD-RECS-READ-CNTR              PIC  ZZZ9(01).            01590001
           05  PD-TOT-STR-IN-TUPC-CNTR        PIC  ZZZ9(01).            01600001
           05  PD-COMMIT-CNTR                 PIC  ZZZZZZZ9(01).        01610001
           05  PD-SKU-NBR-8-DISP              PIC  ZZZZZZZ9(01).        01620001
           05  PD-STR-NBR-4-DISP              PIC  ZZZ9(01).            01630001
           05  PD-ITM-NBR-15-DISP             PIC  ZZZZZZZZZZZZZZ9(01). 01640001
           05  PD-SQLCODE-DISPLAY             PIC +(8)9 VALUE ZEROES.   01650001
      ***************************************************************** 01660001
      *    DB2 COMMUNICATION AREA                                       01670001
      ***************************************************************** 01680001
           COPY DPWS004.                                                01690001
                                                                        01700001
      ***************************************************************** 01710001
      *  TSKXREF - SKU CROSS REFERENCE TABLE                            01720001
      ***************************************************************** 01730001
           EXEC SQL                                                     01740001
                INCLUDE TSKXREF                                         01750001
           END-EXEC.                                                    01760001
                                                                        01770001
      ***************************************************************** 01780001
      *  TUPCPLS - UPC PHYSICAL LOCATION STATUS TABLE                   01790001
      ***************************************************************** 01800001
           EXEC SQL                                                     01810001
                INCLUDE TUPCPLS                                         01820001
           END-EXEC.                                                    01830001
                                                                        01840001
      ***************************************************************** 01850001
      *  XST_SKU_LOC TABLE                                              01860001
      ***************************************************************** 01870001
           EXEC SQL                                                     01880001
                INCLUDE XST00008                                        01890001
           END-EXEC.                                                    01900001
                                                                        01910001
      ***************************************************************** 01920001
      *  XST_SKU_LOC_ATTR TABLE                                         01930001
      ***************************************************************** 01940001
           EXEC SQL                                                     01950001
                INCLUDE XST00041                                        01960001
           END-EXEC.                                                    01970001
                                                                        01980001
      ***************************************************************** 01990001
      *    COBOL DECL DB2 COMMUNICATION AREA                            02000001
      ***************************************************************** 02010001
           EXEC SQL                                                     02020001
               INCLUDE SQLCA                                            02030001
           END-EXEC.                                                    02040001
                                                                        02050001
           COPY SQLCA2.                                                 02060001
      ******************************************************************02070001
      *  TUPCPLS CURSOR DECLARATION                                     02080001
      ******************************************************************02090001
                                                                        02100001
           EXEC SQL                                                     02110001
             DECLARE TUPCPLS_CSR CURSOR FOR                             02120001
               SELECT UPC_ID,                                           02130001
                      STR_NBR,                                          02140001
                      EFF_BEG_TMST,                                     02150001
                      EFF_END_TMST,                                     02160001
                      UPC_STAT_CDE,                                     02170001
                      UNT_COST_AMT,                                     02180001
                      UNT_RTL_AMT,                                      02190001
                      ITM_NBR                                           02200001
               FROM TUPCPLS                                             02210001
                    WHERE ITM_NBR = :UPCPLS-ITM-NBR                     02220001
                      AND EFF_END_TMST = :CC-END-TMST                   02230001
                      AND STR_NBR > 0                                   02240001
               ORDER BY ITM_NBR                                         02250001
               FOR FETCH ONLY                                           02260001
               WITH UR                                                  02270001
           END-EXEC.                                                    02280001
                                                                        02290001
       PROCEDURE DIVISION.                                              02300001
      ******************************************************************02310001
      * 0000-MAIN-MODULE.                                              *02320001
      *  ==> PROCESSES:                                                *02330001
      *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *02340001
      ******************************************************************02350001
       0000-MAIN-MODULE.                                                02360001
           MOVE '0000-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                02370001
                                                                        02380001
           PERFORM 1000-INITIALIZATION.                                 02390001
                                                                        02400001
           PERFORM 2000-MAIN-PROCESS                                    02410001
             UNTIL PS-END-OF-FILE.                                      02420001
                                                                        02430001
           PERFORM 3000-EOJ-PROCESSING.                                 02440001
                                                                        02450001
           STOP RUN.                                                    02460001
                                                                        02470001
      ******************************************************************02480001
      * 1000-INITIALIZATION.                                           *02490001
      *  ==> PROCESSES:                                                *02500001
      *      - INITIALIZING THE SWITCHES AND PROGRAM VARIABLES         *02510001
      *      - OPEN AND READS THE INPUT FILES.                         *02520001
      ******************************************************************02530001
       1000-INITIALIZATION.                                             02540001
           MOVE '1000-INITIALIZATION ' TO PV-PARAGRAPH-NAME.            02550001
                                                                        02560001
           OPEN INPUT SKU-UPDATE-FILE                                   02570001
                      CONTROL-TMST.                                     02580001
                                                                        02590001
           PERFORM 7000-READ-RECORD.                                    02600001
           PERFORM 7100-READ-CONTROL.                                   02610001
                                                                        02620001
      ******************************************************************02630001
      * 2000-MAIN-PROCESS.                                             *02640001
      *  ==> PROCESSES:                                                *02650001
      *    - PROCESSES A SKU UPDATE RECORD                             *02660001
      *    - UPDATE XST_SKU_LOC ROWS                                   *02670001
      ******************************************************************02680001
       2000-MAIN-PROCESS.                                               02690001
           MOVE '2000-MAIN-PROCESS'   TO PV-PARAGRAPH-NAME.             02700001
                                                                        02710001
           INITIALIZE PD-TOT-STR-PRCD-CNTR                              02720001
                      PD-TOT-STR-NOT-PRCD-CNTR                          02730001
                      PD-TOT-STR-IN-TUPC-CNTR.                          02740001
                                                                        02750001
           MOVE PL-INPUT-SKU-NBR      TO SKXREF-SKU.                    02760001
           MOVE SKXREF-SKU            TO PD-SKU-NBR-8-DISP.             02770001
           SET  PS-TSKXREF-NOT-FND    TO TRUE.                          02780001
                                                                        02790001
           PERFORM 2050-GET-ITMNBR.                                     02800001
           IF PS-TSKXREF-FND                                            02810001
               DISPLAY 'PROCESSING FOR INPUT SKU NBR **'                02820001
                      PD-SKU-NBR-8-DISP '** STARTED'                    02830001
               MOVE SKXREF-ITM-NBR    TO UPCPLS-ITM-NBR                 02840001
                                         PD-ITM-NBR-15-DISP             02850001
               DISPLAY 'SKXREF ITM NBR  ' UPCPLS-ITM-NBR                02860003
               SET PS-NOT-END-OF-TUPCPLS-CSR                            02880001
                                      TO TRUE                           02890001
               PERFORM 2100-OPEN-TUPCPLS-CURSOR                         02900001
               PERFORM 2200-FETCH-TUPCPLS-CURSOR                        02910001
               PERFORM 2300-PROCESS-SKU-LOC-UPD                         02920001
                               UNTIL PS-END-OF-TUPCPLS-CSR              02930001
               PERFORM 2500-CLOSE-TUPCPLS-CURSOR                        02940001
               PERFORM 2600-COMMIT                                      02950001
               DISPLAY 'PROCESSING FOR INPUT SKU NBR **'                02960001
                      PD-SKU-NBR-8-DISP '**  COMPLETED'                 02970001
                                                                        02980001
               PERFORM 2700-DISP-SKU-REL-STAT                           02990001
           ELSE                                                         03000001
               DISPLAY 'INPUT SKU NBR    '  PD-SKU-NBR-8-DISP           03010001
               DISPLAY 'ITEM NOT FOUND IN TSKXREF'                      03020001
               DISPLAY 'ITM NBR          '  PD-ITM-NBR-15-DISP          03030001
           END-IF.                                                      03040001
                                                                        03050001
           PERFORM 7000-READ-RECORD.                                    03060001
                                                                        03070001
      ******************************************************************03080001
      * 2050-GET-ITMNBR.                                               *03090001
      *  ==> PROCESSES: GETS ITEM NUMBER AND INTER UPC ID FROM TSKXREF *03100001
      *                 TABLE.                                         *03110001
      *    - READS TSKXREF                                             *03120001
      ******************************************************************03130001
       2050-GET-ITMNBR.                                                 03140001
                                                                        03150001
           INITIALIZE PC-RETRY-COUNT.                                   03160001
           PERFORM  WITH TEST AFTER                                     03170001
             UNTIL SQLCODE = ZERO   OR                                  03180001
                   PC-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT              03190001
                                                                        03200001
               EXEC SQL                                                 03210001
                   SELECT  ITM_NBR                                      03220001
                          ,INTR_UPC_ID                                  03230001
                     INTO :SKXREF-ITM-NBR                               03240001
                         ,:SKXREF-INTR-UPC-ID                           03250001
                     FROM  TSKXREF                                      03260001
                    WHERE  SKU  = :SKXREF-SKU                           03270001
                    WITH UR                                             03280001
               END-EXEC                                                 03290001
                                                                        03300001
               EVALUATE TRUE                                            03310001
                   WHEN SQLCODE = ZERO                                  03320001
                       ADD  1             TO PC-SKU-XREF-FND-CNTR       03330001
                       SET PS-TSKXREF-FND TO TRUE                       03340001
                   WHEN SQLCODE = +100                                  03370001
                       ADD  1             TO PC-SKU-XREF-NTFND-CNTR     03380001
                       MOVE 'READ TSKXREF TABLE'                        03390001
                                          TO PV-OPERATION-ATTEMPTED     03400001
                   WHEN SQLCODE = -913                                  03410001
                       ADD  1             TO PC-RETRY-COUNT             03420001
                   WHEN OTHER                                           03430001
                       MOVE 'READ TSKXREF TABLE'                        03440001
                                          TO PV-OPERATION-ATTEMPTED     03450001
                       PERFORM 9990-SQL-ABEND                           03460001
               END-EVALUATE                                             03470001
           END-PERFORM.                                                 03480001
                                                                        03490001
           IF PC-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                   03500001
               DISPLAY ' MAXIMUM NUMBER OF RETRY REACHED'               03510001
               MOVE 'READ TSKXREF TABLE'  TO                            03520001
                                      PV-OPERATION-ATTEMPTED            03530001
               PERFORM 9990-SQL-ABEND                                   03540001
           END-IF.                                                      03550001
                                                                        03560001
      ******************************************************************03570001
      * 2100-SELECT-TUPCPLS.                                           *03580001
      *  ==> PROCESSES: OPENS THE MAIN CURSOR OF THE PROGRAM           *03590001
      ******************************************************************03600001
       2100-OPEN-TUPCPLS-CURSOR.                                        03610001
           MOVE '2100-OPEN-TUPCPLS-CURSOR' TO PV-PARAGRAPH-NAME.        03620001
                                                                        03630001
           EXEC SQL                                                     03640001
               OPEN TUPCPLS_CSR                                         03650001
           END-EXEC.                                                    03660001
                                                                        03670001
           EVALUATE SQLCODE                                             03680001
               WHEN 0                                                   03690001
                   CONTINUE                                             03700001
               WHEN OTHER                                               03710001
                   MOVE 'ERROR ON OPEN TUPCPLS CURSOR'                  03720001
                                           TO PV-OPERATION-ATTEMPTED    03730001
                   PERFORM 9990-SQL-ABEND                               03740001
           END-EVALUATE.                                                03750001
      ******************************************************************03760001
      * 2200-FETCH-TUPCPLS-CURSOR.                                     *03770001
      *  ==> PROCESSES: RETRIVES THE KEY INFORMATION FROM TUPCPLS      *03780001
      *    - TABLE FOR NON-CORPORATE STORE (STR_NBR<> 0). INPUT SKU NBR*03790001
      *    - AND END TIMESTAMP ARE USED IN THIS CURSOR.                *03800001
      ******************************************************************03810001
       2200-FETCH-TUPCPLS-CURSOR.                                       03820001
           MOVE '2200-FETCH-TUPCPLS-CURSOR' TO PV-PARAGRAPH-NAME.       03830001
                                                                        03840001
           EXEC SQL                                                     03850001
               FETCH TUPCPLS_CSR                                        03860001
               INTO  :UPCPLS-UPC-ID                                     03870001
                    ,:UPCPLS-STR-NBR                                    03880001
                    ,:UPCPLS-EFF-BEG-TMST                               03890001
                    ,:UPCPLS-EFF-END-TMST :PV-EFF-END-NULL-IND          03900001
                    ,:UPCPLS-UPC-STAT-CDE                               03910001
                    ,:UPCPLS-UNT-COST-AMT :PV-UNT-COST-NULL-IND         03920001
                    ,:UPCPLS-UNT-RTL-AMT                                03930001
                    ,:UPCPLS-ITM-NBR                                    03940001
           END-EXEC.                                                    03950001
                                                                        03960001
           EVALUATE TRUE                                                03970001
               WHEN SQLCODE = ZERO                                      03980001
                   SET PS-NOT-END-OF-TUPCPLS-CSR                        03990001
                                           TO TRUE                      04000001
                   ADD +1                  TO PC-TOT-STR-IN-TUPC-CNTR   04010001
               WHEN SQLCODE = +100                                      04050001
                   SET PS-END-OF-TUPCPLS-CSR                            04060001
                                           TO TRUE                      04070001
               WHEN OTHER                                               04080001
                   MOVE 'FETCH TUPCPLS TABLE'                           04090001
                                           TO PV-OPERATION-ATTEMPTED    04100001
                   PERFORM 9990-SQL-ABEND                               04110001
           END-EVALUATE.                                                04120001
                                                                        04130001
      ******************************************************************04140001
      * 2300-PROCESS-SKU-LOC-UPD                                       *04150001
      *  ==> PROCESSES:                                                *04160001
      *    - SELECT THE CURRENT UNIT PRICE FROM XST_SKU_LOC TABLE.     *04170001
      ******************************************************************04180001
       2300-PROCESS-SKU-LOC-UPD.                                        04190001
                                                                        04200001
            PERFORM 2400-SELECT-SKU-LOC.                                04210001
                                                                        04220001
            IF PS-XSSTORE-ROW-FOUND                                     04230001
               PERFORM 2450-UPDATE-SKU-LOC                              04240001
               PERFORM 2475-DELETE-SKU-LOC-ATTRIB                       04250001
               ADD +1                 TO PC-TOT-STR-PRCD-CNTR           04260001
            ELSE                                                        04270001
               MOVE UPCPLS-STR-NBR    TO PD-STR-NBR-4-DISP              04271003
               DISPLAY 'XS SKU LOC DESNOT HAVE ACTIVE RECORD FOR SKU '  04280005
                       'NBR **' PD-SKU-NBR-8-DISP '** AND LOC NBR **'   04300005
                       PD-STR-NBR-4-DISP '**.'                          04301005
               ADD +1                 TO PC-TOT-STR-NOT-PRCD-CNTR       04310001
            END-IF.                                                     04320001
                                                                        04330001
            PERFORM 2200-FETCH-TUPCPLS-CURSOR.                          04340001
                                                                        04350001
                                                                        04360001
      ******************************************************************04370001
      * 2400-SELECT-SKU-LOC.                                           *04380001
      *  ==> PROCESSES:                                                *04390001
      *    - SELECT THE CURRENT UNIT PRICE FROM XST_SKU_LOC TABLE.     *04400001
      ******************************************************************04410001
       2400-SELECT-SKU-LOC.                                             04420001
           MOVE '2400-SELECT-SKU-LOC'            TO PV-PARAGRAPH-NAME.  04430001
                                                                        04440001
           INITIALIZE PC-RETRY-COUNT.                                   04450001
           PERFORM  WITH TEST AFTER                                     04460001
             UNTIL SQLCODE = ZERO OR                                    04470001
                   SQLCODE = +100 OR                                    04480001
                   PC-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT              04490001
               EXEC SQL                                                 04500001
                   SELECT INTR_UPC_ID,                                  04510001
                          LOC_NBR                                       04520001
                    INTO :XST00008-INTR-UPC-ID,                         04530001
                         :XST00008-LOC-NBR                              04540001
                     FROM XST_SKU_LOC                                   04550001
                    WHERE INTR_UPC_ID  = :UPCPLS-UPC-ID                 04560001
                      AND LOC_NBR      = :UPCPLS-STR-NBR                04570001
                      AND END_TMST     IS NULL                          04580001
                    WITH UR                                             04590001
               END-EXEC                                                 04600001
                                                                        04610001
               EVALUATE TRUE                                            04620001
                   WHEN SQLCODE = ZERO                                  04630001
                       SET PS-XSSTORE-ROW-FOUND  TO TRUE                04640001
                   WHEN SQLCODE = +100                                  04680001
                       SET PS-XSSTORE-ROW-NOT-FOUND                     04690001
                                                 TO TRUE                04700001
                   WHEN SQLCODE = -913                                  04710001
                       ADD +1                    TO PC-RETRY-COUNT      04720001
                   WHEN OTHER                                           04730001
                       MOVE 'READ SKU LOC TABLE' TO                     04740001
                             PV-OPERATION-ATTEMPTED                     04750001
                       PERFORM 9990-SQL-ABEND                           04760001
               END-EVALUATE                                             04770001
           END-PERFORM.                                                 04780001
                                                                        04790001
           IF PC-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                   04800001
               DISPLAY ' MAXIMUM NUMBER OF RETRY REACHED'               04810001
               MOVE 'READ SKU LOC TABLE'         TO                     04820001
                     PV-OPERATION-ATTEMPTED                             04830001
               PERFORM 9990-SQL-ABEND                                   04840001
           END-IF.                                                      04850001
                                                                        04860001
      ******************************************************************04870001
      * 2450-UPDATE-SKU-LOC.                                           *04880001
      *  ==> PROCESSES:                                                *04890001
      *    - UPDATE THE CURRENT LOC ROW FROM THE SKU LOC TABLE.        *04900001
      ******************************************************************04910001
       2450-UPDATE-SKU-LOC.                                             04920001
           MOVE '2450-UPDATE-SKU-LOC'   TO PV-PARAGRAPH-NAME.           04930001
                                                                        04940001
           INITIALIZE PC-RETRY-COUNT.                                   04950001
           PERFORM  WITH TEST AFTER                                     04960001
             UNTIL SQLCODE = ZERO  OR                                   04970001
                   SQLCODE = +100  OR                                   04980001
                   PC-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT              04990001
                                                                        05000001
                 EXEC SQL                                               05010001
                    UPDATE XST_SKU_LOC                                  05020001
                        SET END_TMST       = :CC-END-TMST,              05030001
                            LAST_UPD_TMST  = CURRENT TIMESTAMP          05040001
                        WHERE INTR_UPC_ID  = :XST00008-INTR-UPC-ID      05050001
                          AND LOC_NBR      = :XST00008-LOC-NBR          05060001
                          AND END_TMST     IS NULL                      05070001
                 END-EXEC                                               05080001
                                                                        05090001
                 EVALUATE TRUE                                          05100001
                     WHEN SQLCODE = ZERO                                05110001
                         CONTINUE                                       05120001
                     WHEN SQLCODE = -913                                05130001
                         ADD +1        TO PC-RETRY-COUNT                05140001
                     WHEN OTHER                                         05150001
                         MOVE 'UPDATE SKU LOC TABLE'                    05160001
                                       TO PV-OPERATION-ATTEMPTED        05170001
                         PERFORM 9990-SQL-ABEND                         05180001
                 END-EVALUATE                                           05190001
           END-PERFORM.                                                 05200001
                                                                        05210001
           IF PC-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                   05220001
               DISPLAY ' MAXIMUM NUMBER OF RETRY REACHED'               05230001
               MOVE 'UPDATE SKU LOC TABLE'                              05240001
                                       TO PV-OPERATION-ATTEMPTED        05250001
               PERFORM 9990-SQL-ABEND                                   05260001
           END-IF.                                                      05270001
                                                                        05280001
      ******************************************************************05290001
      * 2475-DELETE-SKU-LOC-ATTRIB                                     *05300001
      *  ==> PROCESSES:                                                *05310001
      *    - DELETE XST_SKU_ATTRIB TABLE FOR STORE SPECIFIC ROW.       *05320001
      ******************************************************************05330001
       2475-DELETE-SKU-LOC-ATTRIB.                                      05340001
           MOVE '2475-DELETE-SKU-LOC-ATTRIB' TO PV-PARAGRAPH-NAME.      05350001
           INITIALIZE PC-RETRY-COUNT.                                   05360001
                                                                        05370001
           PERFORM  WITH TEST AFTER                                     05380001
             UNTIL SQLCODE = ZERO OR                                    05390001
                   SQLCODE = -803 OR                                    05400001
                   PC-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT              05410001
               EXEC SQL                                                 05420001
                  DELETE                                                05430001
                    FROM XST_SKU_LOC_ATTR                               05440001
                   WHERE INTR_UPC_ID = :XST00008-INTR-UPC-ID            05450001
                     AND LOC_NBR     = :XST00008-LOC-NBR                05460001
               END-EXEC                                                 05470001
                                                                        05480001
               EVALUATE TRUE                                            05490001
                   WHEN SQLCODE = ZERO                                  05500001
                       CONTINUE                                         05510001
                   WHEN SQLCODE = -913                                  05520001
                       ADD +1         TO PC-RETRY-COUNT                 05530001
                   WHEN OTHER                                           05540001
                       MOVE 'DELETE XST_SKU_LOC_ATTR TABLE'             05560001
                                      TO PV-OPERATION-ATTEMPTED         05570001
                       PERFORM 9990-SQL-ABEND                           05580001
               END-EVALUATE                                             05590001
           END-PERFORM.                                                 05600001
                                                                        05610001
           IF PC-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                   05620001
               DISPLAY ' MAXIMUM NUMBER OF RETRY REACHED'               05630001
               MOVE 'DELETE XST_SKU_LOC_ATTR TABLE'                     05640001
                                      TO PV-OPERATION-ATTEMPTED         05650001
               PERFORM 9990-SQL-ABEND                                   05660001
           END-IF.                                                      05670001
                                                                        05680001
      ******************************************************************05690001
      *  CLOSE RESERVATION CURSOR                                       05700001
      ******************************************************************05710001
       2500-CLOSE-TUPCPLS-CURSOR.                                       05720001
                                                                        05730001
           EXEC SQL                                                     05740001
              CLOSE TUPCPLS_CSR                                         05750001
           END-EXEC.                                                    05760001
                                                                        05770001
           EVALUATE SQLCODE                                             05780001
               WHEN 0                                                   05790001
                   CONTINUE                                             05800001
               WHEN OTHER                                               05810001
                   MOVE '2500-CLOSE-TUPCPLS-CURSOR'                     05820001
                                  TO PV-PARAGRAPH-NAME                  05830001
                   MOVE 'ERROR ON CLOSE TUPCPLS CURSOR'                 05840001
                                  TO PV-OPERATION-ATTEMPTED             05850001
                   PERFORM 9990-SQL-ABEND                               05860001
           END-EVALUATE.                                                05870001
                                                                        05880001
      ******************************************************************05890001
      * 2600-COMMIT.                                                   *05900001
      *  ==> PROCESSES:                                                *05910001
      *    - PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.           *05920001
      *    - TAKE A COMMIT.                                            *05930001
      ******************************************************************05940001
       2600-COMMIT.                                                     05950001
           MOVE '2600-COMMIT'      TO PV-PARAGRAPH-NAME.                05960001
                                                                        05970001
           EXEC SQL                                                     05980001
               COMMIT                                                   05990001
           END-EXEC.                                                    06000001
                                                                        06010001
           IF SQLCODE = 0                                               06020001
               ADD +1              TO PC-COMMIT-CNTR                    06030001
           ELSE                                                         06040001
               MOVE 'ERROR ON COMMIT'                                   06050001
                                   TO PV-OPERATION-ATTEMPTED            06060001
               PERFORM 9990-SQL-ABEND                                   06070001
           END-IF.                                                      06080001
                                                                        06090001
      ******************************************************************06100001
      * 2700-DISP-SKU-REL-STAT.                                        *06110001
      *  ==> PROCESSES:                                                *06120001
      *    - PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.           *06130001
      *    - TAKE A COMMIT.                                            *06140001
      ******************************************************************06150001
       2700-DISP-SKU-REL-STAT.                                          06160001
           MOVE '2700-DISP-SKU-REL-STAT' TO PV-PARAGRAPH-NAME.          06170002
                                                                        06180001
           DISPLAY '***** STORE STATS FOR INPUT SKU ' PD-SKU-NBR-8-DISP 06190002
                   ' *****'.                                            06200001
           MOVE PC-TOT-STR-PRCD-CNTR      TO PD-TOT-STR-PRCD-CNTR.      06210001
           MOVE PC-TOT-STR-NOT-PRCD-CNTR  TO PD-TOT-STR-NOT-PRCD-CNTR.  06220001
           MOVE PC-TOT-STR-IN-TUPC-CNTR   TO PD-TOT-STR-IN-TUPC-CNTR.   06230001
           DISPLAY 'NO.OF STORES EXTRACTED IN TUPCPLS        '          06240001
                   PD-TOT-STR-IN-TUPC-CNTR                              06250001
           DISPLAY 'NO.OF STORES UPDATED & DELETED IN XS     '          06260001
                   PD-TOT-STR-PRCD-CNTR.                                06270001
           DISPLAY 'NO.OF STORES NOT UPDATED & DELETED IN XS '          06280001
                   PD-TOT-STR-NOT-PRCD-CNTR.                            06290001
           DISPLAY '    *********************    '.                     06300001
           DISPLAY '  '.                                                06310001
                                                                        06310103
           INITIALIZE PC-TOT-STR-IN-TUPC-CNTR                           06311003
                      PD-TOT-STR-IN-TUPC-CNTR                           06312003
                      PC-TOT-STR-PRCD-CNTR                              06312103
                      PD-TOT-STR-PRCD-CNTR                              06312203
                      PC-TOT-STR-NOT-PRCD-CNTR                          06313003
                      PD-TOT-STR-NOT-PRCD-CNTR.                         06314003
                                                                        06320001
      ******************************************************************06330001
      * 3000-EOJ-PROCESSING.                                           *06340001
      *  ==> PROCESSES:                                                *06350001
      *    - CHECK IF LAST SKU PROCESSED SHOULD BE COMPRESSED          *06360001
      *    - DELETE STYLE RESERVATION FOR LAST SKU PROCESSED           *06370001
      *    - CLOSE FILES                                               *06380001
      *    - DISPLAYS PROCESSING TOTALS                                *06390001
      *    - TAKES A FINAL COMMIT AND UPDATES THE CHECKPOINT STATUS    *06400001
      *      TO INDICATE SUCESSFULL COMPLETION                         *06410001
      ******************************************************************06420001
       3000-EOJ-PROCESSING.                                             06430001
           MOVE '3000-EOJ-PROCESSING' TO PV-PARAGRAPH-NAME.             06440001
                                                                        06450001
           CLOSE SKU-UPDATE-FILE                                        06460001
                 CONTROL-TMST.                                          06470001
                                                                        06480001
           PERFORM 9990-DISPLAY-CONTROL-INFO.                           06490001
                                                                        06500001
      ******************************************************************06510001
      * 7000-READ-RECORD.                                              *06520001
      *  ==> PROCESSES:                                                *06530001
      *    - READS THE NEXT RECORD IN THE INPUT FILE.                  *06540001
      ******************************************************************06550001
       7000-READ-RECORD.                                                06560001
           MOVE '7000-READ-RECORD'           TO PV-PARAGRAPH-NAME.      06570001
                                                                        06580001
           READ SKU-UPDATE-FILE                                         06590001
               INTO PL-SKU-RECORD                                       06600001
                   AT END                                               06610001
                       SET PS-END-OF-FILE    TO TRUE                    06620001
                   NOT AT END                                           06630001
                       ADD +1                TO PC-RECS-READ-CNTR       06640001
                       MOVE PL-INPUT-SKU-NBR TO PD-SKU-NBR-8-DISP       06650001
           END-READ.                                                    06660001
                                                                        06670001
      ******************************************************************06680001
      * 7100-READ-CONTROL.                                             *06690001
      *  READ END TIMESTAMP FROM INPUT FILE                             06700001
      ******************************************************************06710001
       7100-READ-CONTROL.                                               06720001
           MOVE '7100-READ-CONTROL'       TO PV-PARAGRAPH-NAME.         06730001
                                                                        06740001
           READ CONTROL-TMST              INTO CC-CONTROL-CARD          06750001
                                                                        06760001
                   AT END                                               06770001
                      MOVE +4020          TO ABEND-CODE                 06780001
                      MOVE 'NO TIMESTAMP PASSED IN CONTROL CARD'        06790001
                                          TO PV-OPERATION-ATTEMPTED     06800001
                      PERFORM 9999-ABEND                                06810001
           END-READ.                                                    06820001
                                                                        06830001
           DISPLAY "        XSKUP991 PGM PROCESSING STARTED  ".         06840004
           DISPLAY "ADHOC RUN IS FOR **" CC-CONTROL-CARD "** END TIME"  06860001
                   "STAMP".                                             06870001
           DISPLAY "                                         ".         06871004
                                                                        06880001
      ******************************************************************06890001
      * 9990-DISPLAY-CONTROL-INFO.                                     *06900001
      *  ==> PROCESSES:                                                *06910001
      *    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *06920001
      ******************************************************************06930001
       9990-DISPLAY-CONTROL-INFO.                                       06940001
                                                                        06950001
           MOVE PC-RECS-READ-CNTR       TO PD-RECS-READ-CNTR            06960001
           MOVE PC-SKU-XREF-FND-CNTR    TO PD-SKU-XREF-FND-CNTR         06970001
           MOVE PC-SKU-XREF-NTFND-CNTR  TO PD-SKU-XREF-NTFND-CNTR       06980001
           MOVE PC-COMMIT-CNTR          TO PD-COMMIT-CNTR               06990001
                                                                        07000001
           DISPLAY "        XSKUP991 PGM PROCESSING COMPLETED"          07010003
           DISPLAY "                                         "          07020001
           DISPLAY '********************************************'.      07030001
           DISPLAY '**** RUN STATISTICS FOR PROGRAM XSKUP991 ***'.      07040001
           DISPLAY '****'.                                              07050001
           DISPLAY 'INPUT RECORDS READ:           ' PD-RECS-READ-CNTR   07060001
           DISPLAY 'NO. OF SKUS FOUND IN TSKXREF  '                     07070001
                   PD-SKU-XREF-FND-CNTR.                                07080001
           DISPLAY 'NO. OF SKUS NT FND IN TSKXREF '                     07090001
                   PD-SKU-XREF-NTFND-CNTR.                              07100001
           DISPLAY 'COMMITS TAKEN:         ' PD-COMMIT-CNTR.            07110001
           DISPLAY '****'.                                              07120001
           DISPLAY '**** RUN STATS END FOR PROGRAM XSKUP991  ***'.      07130001
           DISPLAY '********************************************'.      07140001
                                                                        07150001
      ******************************************************************07160001
      * 9990-SQL-ABEND.                                                *07170001
      *  ==> PROCESSES:                                                *07180001
      *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *07190001
      ******************************************************************07200001
       9990-SQL-ABEND.                                                  07210001
                                                                        07220001
           MOVE SQLCODE TO PD-SQLCODE-DISPLAY.                          07230001
           DISPLAY 'SQL CODE  = ' PD-SQLCODE-DISPLAY.                   07240001
           MOVE +4013                     TO ABEND-CODE                 07250001
                                                                        07260001
           PERFORM 9990-DISPLAY-CONTROL-INFO.                           07270001
           PERFORM 9999-ABEND.                                          07280001
                                                                        07290001
      ******************************************************************07300001
      * 9999-ABEND.                                                    *07310001
      *  ==> PROCESSES:                                                *07320001
      *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *07330001
      ******************************************************************07340001
       9999-ABEND.                                                      07350001
           COPY DPPD004.                                                07360001
                                                                        07370001
           DISPLAY '** ABEND **'.                                       07380001
           DISPLAY 'XSKUP991 - ABEND'.                                  07390001
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                    07400001
           DISPLAY 'OPERATION = ' PV-OPERATION-ATTEMPTED.               07410001
           DISPLAY 'INPUT SKU = ' PD-SKU-NBR-8-DISP.                    07420001
           DISPLAY 'STORE NBR = ' PD-STR-NBR-4-DISP.                    07430001
           CALL 'ILBOABN0' USING ABEND-CODE.                            07440001
                                                                        07450001
