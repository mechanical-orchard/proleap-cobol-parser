000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400 PROGRAM-ID.             WSKRP005.                                        
000500 AUTHOR.                 VIRGENE LENNEMAN                                 
000600 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000700 DATE-WRITTEN            JUNE 2001.                                       
000800 DATE-COMPILED.                                                           
000900*****************************************************************         
001000*  THIS PROGRAM WILL TAKE THE INVENTORY ADJUSTMENT UPLOAD       *         
001100*  FILE (WSRD043) FROM WSKUT031 AND CREATE THE IVNENTORY        *         
001200*  ADJUSTMENT REPORT.                                           *         
001300* JAK 06/2007 - ADD CONTROL-CARD INPUT FILE. *                  *         
001400* CRAIG KEHOE 02/2008 - FILTER OUT ERRONEOUS FASHION-ADJUSTMENTS*         
001410* GERMAN BANDA 09/2020 -PROGRAM RENAMED FROM WFKRP004           *         
001500*****************************************************************         
001600 ENVIRONMENT DIVISION.                                                    
001700 CONFIGURATION SECTION.                                                   
001800 SOURCE-COMPUTER.        IBM-3090.                                        
001900 OBJECT-COMPUTER.        IBM-3090.                                        
002000*****************************************************************         
002100 INPUT-OUTPUT SECTION.                                                    
002200 FILE-CONTROL.                                                            
002300     SELECT INPUT-EXTRACT-FILE  ASSIGN TO INP01.                          
002400     SELECT CONTROL-CARD        ASSIGN TO INP02.                          
002500     SELECT OUTPUT-REPORT       ASSIGN TO RPT01.                          
002600*****************************************************************         
002700 DATA DIVISION.                                                           
002800 FILE SECTION.                                                            
002900 FD  INPUT-EXTRACT-FILE                                                   
003000     LABEL RECORDS ARE STANDARD                                           
003100     RECORDING MODE IS F                                                  
003200     BLOCK CONTAINS 0 RECORDS                                             
003300     RECORD CONTAINS 150 CHARACTERS.                                      
003400 01  EXTRACT-REC                     PIC X(150).                          
003500                                                                          
003600 FD  CONTROL-CARD                                                         
003700     LABEL RECORDS ARE OMITTED                                            
003800     RECORDING MODE IS F                                                  
003900     BLOCK CONTAINS 0 RECORDS                                             
004000     RECORD CONTAINS 80 CHARACTERS.                                       
004100 01  CONTROL-CARD-REC                PIC X(80).                           
004200                                                                          
004300 FD  OUTPUT-REPORT                                                        
004400     RECORDING MODE IS F                                                  
004500     BLOCK CONTAINS 0 RECORDS.                                            
004600 01  OUTPUT-REC                      PIC X(132).                          
004700*****************************************************************         
004800 WORKING-STORAGE SECTION.                                                 
004900                                                                          
005000 01  PROGRAM-SWITCHES.                                                    
005100     05  PS-FIRST-TIME-SW            PIC X(01) VALUE 'N'.                 
005200         88  PS-FIRST-TIME-YES                 VALUE 'Y'.                 
005300         88  PS-FIRST-TIME-NO                  VALUE 'N'.                 
005400     05  PS-END-OF-INPUT-FILE-SW     PIC X(01) VALUE 'N'.                 
005500         88  PS-END-OF-INPUT                   VALUE 'Y'.                 
005600         88  PS-NOT-END-OF-INPUT               VALUE 'N'.                 
005700     05  PS-END-OF-CONTROL-SW        PIC X(01) VALUE 'N'.                 
005800         88  PS-END-OF-CONTROL                 VALUE 'Y'.                 
005900         88  PS-NOT-END-OF-CONTROL             VALUE 'N'.                 
006000     05  PS-PAGE-SW                  PIC X(01) VALUE 'N'.                 
006100         88  PS-NEW-PAGE                       VALUE 'Y'.                 
006200         88  PS-SAME-PAGE                      VALUE 'N'.                 
006300     05  PS-END-OF-CURSOR-SW         PIC X(01) VALUE 'N'.                 
006400         88  PS-END-OF-CURSOR                  VALUE 'Y'.                 
006500         88  PS-NOT-END-OF-CURSOR              VALUE 'N'.                 
006600     05  PS-END-OF-TABLE-SW          PIC X(01) VALUE 'N'.                 
006700         88  PS-END-OF-TABLE                   VALUE 'Y'.                 
006800         88  PS-NOT-END-OF-TABLE               VALUE 'N'.                 
006900     05  PS-END-VALID-LOC-CSR-SW     PIC X(01) VALUE 'N'.                 
007000         88  PS-END-LOC-CSR                    VALUE 'Y'.                 
007100         88  PS-NOT-END-LOC-CSR                VALUE 'N'.                 
007200     05  PS-FIRST-LOC-SW             PIC X(01) VALUE 'Y'.                 
007300         88  PS-FIRST-LOC-YES                  VALUE 'Y'.                 
007400         88  PS-FIRST-LOC-NO                   VALUE 'N'.                 
007500     05  PS-EMPTY-FILE-SW            PIC X(01) VALUE 'N'.                 
007600         88  PS-EMPTY-FILE                     VALUE 'Y'.                 
007700         88  PS-NOT-EMPTY-FILE                 VALUE 'N'.                 
007800     05  PS-REPORT-SW                PIC X(01) VALUE '0'.                 
007900         88  PS-REPORT-1                       VALUE '1'.                 
008000         88  PS-REPORT-2                       VALUE '2'.                 
008010     05  PS-LOCATION-CHANGE-SW       PIC X(01) VALUE 'N'.                 
008020         88  PS-LOCATION-CHANGE-YES            VALUE 'Y'.                 
008030         88  PS-LOCATION-CHANGE-NO             VALUE 'N'.                 
008100* CRAIG KEHOE 02/2008 (W=WHERE IT GETS WRITTEN                            
008200*                      R=WHERE IT GETS READ)                              
008300     05  PS-FASHION-ADJ-SW           PIC X(01) VALUE ' '.                 
008400*        ( W) 2600-GET-RSN-CDE.                                           
008500         88  PS-FASHION-ADJ                    VALUE 'Y'.                 
008600*            (R ) 2060-PRINT-DETAIL.                                      
008700         88  PS-NOT-FASHION-ADJ                VALUE 'N'.                 
008800*            (R ) 2060-PRINT-DETAIL.                                      
008900*            ( W) 2610-GET-PIX-DESC.                                      
009000* END CRAIG KEHOE 02/2008                                                 
009100                                                                          
009200 01  PC-PROGRAM-CONSTANTS.                                                
009300     05  PC-WSKRP005                 PIC  X(08) VALUE 'WSKRP005'.         
009400     05  PC-REPORT-NBR-1             PIC  X(02) VALUE '-1'.               
009500     05  PC-REPORT-NBR-2             PIC  X(02) VALUE '-2'.               
009600     05  PC-MAX-LINE-COUNT           PIC S9(03) VALUE 60 COMP-3.          
009700     05  PC-DS                       PIC  X(02) VALUE 'DS'.               
009800     05  PC-Y                        PIC  X(01) VALUE 'Y'.                
009900                                                                          
010000 01  DK-DATABASE-KEYS.                                                    
010100     05  DK-LOC-NBR                  PIC S9(04) COMP                      
010200                                                VALUE ZERO.               
010300                                                                          
010400 01  PV-PROGRAM-VARIABLES.                                                
010500     05  L                         PIC S9(04) VALUE ZERO                  
010600                                                    COMP SYNC.            
010700     05  PV-ABEND-CODE             PIC S9(04) VALUE ZERO COMP.            
010800     05  PV-DB2-TABLE-NAME         PIC  X(08) VALUE SPACES.               
010900     05  PV-ABEND-PARAGRAPH        PIC  X(50) VALUE SPACES.               
011000     05  PV-ABEND-OPERATION        PIC  X(50) VALUE SPACES.               
011100     05  PV-PAGE-COUNT             PIC S9(04) VALUE ZERO COMP-3.          
011200     05  PV-LINE-COUNT             PIC S9(03) VALUE ZERO COMP-3.          
011300     05  PV-REC-OUT-QTY            PIC  9(07) VALUE ZERO.                 
011400     05  PV-FIELDS.                                                       
011500         10  PV-CURR-LOCN            PIC  X(04) VALUE SPACES.             
011600         10  PV-PREV-LOCN            PIC  X(04) VALUE SPACES.             
011700         10  PV-CURR-DATE            PIC  X(10) VALUE ZERO.               
011800         10  PV-CURRENT-DATE.                                             
011900             15  PV-CURRENT-DATE-MM  PIC  X(02) VALUE SPACES.             
012000             15  FILLER              PIC  X(01) VALUE '/'.                
012100             15  PV-CURRENT-DATE-DD  PIC  X(02) VALUE SPACES.             
012200             15  FILLER              PIC  X(01) VALUE '/'.                
012300             15  PV-CURRENT-DATE-CC  PIC  X(02) VALUE SPACES.             
012400             15  PV-CURRENT-DATE-YY  PIC  X(02) VALUE SPACES.             
012500         10  PV-CURR-DEPT            PIC  9(04) VALUE ZERO.               
012600         10  PV-STORE-NMBR           PIC  9(04) VALUE ZERO.               
012700         10  PV-DC-STORE-NAME        PIC  X(25) VALUE SPACES.             
012710         10  PV-STORE-NMBR-PREV      PIC  9(04) VALUE ZERO.               
012720         10  PV-DC-STORE-NAME-PREV   PIC  X(25) VALUE SPACES.             
012800                                                                          
012900         10  PV-BATCH-NMBR           PIC  X(04).                          
013000         10  FILLER                  REDEFINES                            
013100             PV-BATCH-NMBR.                                               
013200             15  PV-BATCH-NMBR-1     PIC  X(01).                          
013300             15  PV-BATCH-NMBR-3     PIC  X(03).                          
013400                                                                          
013500     05  PV-CURRENT-DB2-DATE         PIC  X(10).                          
013600                                                                          
013700     05  PV-ACCUMULATORS.                                                 
013800         10  PV-ACCUM-TOT-ADJ-DEPT       PIC S9(09) VALUE ZERO.           
013900         10  PV-ACCUM-TOT-ADJ-DEPT-FSHN  PIC S9(09) VALUE ZERO.           
014000         10  PV-ACCUM-TOT-ADJ-LOCN       PIC S9(09) VALUE ZERO.           
014100         10  PV-ACCUM-TOT-ADJ-LOCN-FSHN  PIC S9(09) VALUE ZERO.           
014200         10  PV-ACCUM-TOT-ADJ-DATE       PIC S9(09) VALUE ZERO.           
014300         10  PV-ACCUM-TOT-ADJ-DATE-FSHN  PIC S9(09) VALUE ZERO.           
014400                                                                          
014500         10  PV-ACCUM-QTY-DEPT           PIC S9(09) VALUE ZERO.           
014600         10  PV-ACCUM-RET-EXTENS-DEPT    PIC S9(09)V99                    
014700                                                    VALUE ZERO.           
014800         10  PV-ACCUM-QTY-DEPT-FSHN      PIC S9(09) VALUE ZERO.           
014900         10  PV-ACCUM-RET-EXT-DEPT-FSHN  PIC S9(09)V99                    
015000                                                    VALUE ZERO.           
015100                                                                          
015200         10  PV-ACCUM-QTY-LOCN           PIC S9(09) VALUE ZERO.           
015300         10  PV-ACCUM-RET-EXTENS-LOCN    PIC S9(09)V99                    
015400                                                    VALUE ZERO.           
015500         10  PV-ACCUM-QTY-LOCN-FSHN      PIC S9(09) VALUE ZERO.           
015600         10  PV-ACCUM-RET-EXT-LOCN-FSHN  PIC S9(09)V99                    
015700                                                    VALUE ZERO.           
015800                                                                          
015900         10  PV-ACCUM-QTY-DATE           PIC S9(09) VALUE ZERO.           
016000         10  PV-ACCUM-RET-EXTENS-DATE    PIC S9(09)V99                    
016100                                                    VALUE ZERO.           
016200         10  PV-ACCUM-QTY-DATE-FSHN      PIC S9(09) VALUE ZERO.           
016300         10  PV-ACCUM-RET-EXT-DATE-FSHN  PIC S9(09)V99                    
016400                                                    VALUE ZERO.           
016500                                                                          
016600     05  PV-ADJUST-QTY               PIC  ZZZ,ZZZ,ZZ9-                    
016700                                                    VALUE ZERO.           
016800                                                                          
016900     05  PV-FORMAT-DATE.                                                  
017000         10  PV-FORMAT-DATE-MM       PIC  X(02) VALUE SPACES.             
017100         10  FILLER                  PIC  X(01) VALUE '/'.                
017200         10  PV-FORMAT-DATE-DD       PIC  X(02) VALUE SPACES.             
017300         10  FILLER                  PIC  X(01) VALUE '/'.                
017400         10  PV-FORMAT-DATE-CC       PIC  X(02) VALUE SPACES.             
017500         10  PV-FORMAT-DATE-YY       PIC  X(02) VALUE SPACES.             
017600                                                                          
017700     05  PV-FORMAT-DEPT-CLASS.                                            
017800         10  PV-FORMAT-SKU-DEPT      PIC  9(04) VALUE ZERO.               
017900         10  FILLER                  PIC  X(01) VALUE '-'.                
018000         10  PV-FORMAT-SKU-CLASS     PIC  9(04) VALUE ZERO.               
018100         10  FILLER                  PIC  X(01) VALUE '-'.                
018200         10  PV-FORMAT-SKU-SUB-CL    PIC  9(04) VALUE ZERO.               
018300                                                                          
018400     05  PV-TODAYS-DATE.                                                  
018500         10  PV-TODAYS-DATE-YY       PIC  9(02) VALUE ZERO.               
018600         10  PV-TODAYS-DATE-MM       PIC  9(02) VALUE ZERO.               
018700         10  PV-TODAYS-DATE-DD       PIC  9(02) VALUE ZERO.               
018800                                                                          
018900     05  PV-TODAYS-DATE-YYYY.                                             
019000         10  PV-TODAYS-DATE-CC       PIC  9(02) VALUE 20.                 
019100         10  PV-TODAYS-DATE-Y2       PIC  9(02) VALUE ZERO.               
019200                                                                          
019300     05  PV-TODAYS-TIME.                                                  
019400         10  PV-TODAYS-TIME-HH       PIC  9(02) VALUE ZERO.               
019500         10  PV-TODAYS-TIME-MM       PIC  9(02) VALUE ZERO.               
019600         10  PV-TODAYS-TIME-SS       PIC  9(02) VALUE ZERO.               
019700         10  FILLER                  PIC  9(02) VALUE ZERO.               
019800                                                                          
019900     05 PV-HOLD-BTCH-PRC-DTE         PIC  X(10) VALUE SPACES.             
020000     05 PV-TRAN-CDE                  PIC  X(02) VALUE SPACES.             
020100                                                                          
020200******************************************************************        
020300**CONTROL CARD RECORD LAYOUT                                              
020400******************************************************************        
020500 01  CC-CONTROL-CARD-LAYOUT.                                              
020600     05  CC-REPORT-CODE             PIC X(01)   VALUE SPACES.             
020700     05  FILLER                     PIC X(79)   VALUE SPACES.             
020800                                                                          
020900******************************************************************        
021000*  RECORD LAYOUT FOR INPUT FILE                                           
021100******************************************************************        
021200     COPY WSRD043.                                                        
021300                                                                          
021400******************************************************************        
021500*  DB2 FORMATTED MESSAGE AREA                                             
021600******************************************************************        
021700     COPY DPWS004.                                                        
021800                                                                          
021900******************************************************************        
022000*  WORK FIELDS FOR PRINTING A 132 CHARACTER REPORT.                       
022100******************************************************************        
022200     COPY DPWS132.                                                        
022300                                                                          
022400 01  WS-BATCH-PROC-DATE.                                                  
022500     05  WS-BATCH-PROC-CC            PIC X(02)     VALUE SPACES.          
022600     05  WS-BATCH-PROC-YY            PIC X(02)     VALUE SPACES.          
022700     05  FILLER                      PIC X(01)     VALUE SPACES.          
022800     05  WS-BATCH-PROC-MM            PIC X(02)     VALUE SPACES.          
022900     05  FILLER                      PIC X(01)     VALUE SPACES.          
023000     05  WS-BATCH-PROC-DD            PIC X(02)     VALUE SPACES.          
023100                                                                          
023200 01  HEADING-LINES.                                                       
023300     05  HEADING-LINE-1A.                                                 
023400         10  FILLER                  PIC  X(33)    VALUE SPACES.          
023500         10  FILLER                  PIC  X(46)    VALUE                  
023600           'DAILY LISTING OF E-COMM INVENTORY ADJUSTMENTS '.              
023700         10  FILLER                  PIC  X(13)    VALUE                  
023800           'FROM WMOS FOR'.                                               
023900         10  FILLER                  PIC  X(01)    VALUE SPACES.          
024000         10  HL1A-DATE-MMDDYYYY      PIC  X(10)    VALUE SPACES.          
024100         10  FILLER                  PIC  X(30)    VALUE SPACES.          
024200                                                                          
024300     05  HEADING-LINE-1B.                                                 
024400         10  FILLER                  PIC  X(30)    VALUE SPACES.          
024500         10  FILLER                  PIC  X(49)    VALUE                  
024600           'DAILY LISTING OF E-COMM FASHION SALES ADJUSTMENTS'.           
024700         10  FILLER                  PIC  X(14)    VALUE                  
024800           ' FROM WMOS FOR'.                                              
024900         10  FILLER                  PIC  X(01)    VALUE SPACES.          
025000         10  HL1B-DATE-MMDDYYYY      PIC  X(10)    VALUE SPACES.          
025100         10  FILLER                  PIC  X(26)    VALUE SPACES.          
025200                                                                          
025300     05  HEADING-LINE-2.                                                  
025400         10  FILLER                  PIC  X(01)    VALUE SPACES.          
025500         10  FILLER                  PIC  X(14)    VALUE                  
025600             'DC LOCATION - '.                                            
025700         10  FILLER                  PIC  X(01)    VALUE SPACES.          
025800         10  HL2-DC-STORE-NBR        PIC  9999     VALUE ZERO.            
025900         10  FILLER                  PIC  X(01)    VALUE SPACES.          
026000         10  HL2-DC-STORE-NAME       PIC  X(25)    VALUE SPACES.          
026100         10  FILLER                  PIC  X(86)    VALUE SPACES.          
026200                                                                          
026300     05  HEADING-LINE-3.                                                  
026400         10  FILLER                  PIC  X(20)    VALUE SPACES.          
026500         10  FILLER                  PIC  X(03)    VALUE                  
026600             'SKU'.                                                       
026700         10  FILLER                  PIC  X(06)    VALUE SPACES.          
026800         10  FILLER                  PIC  X(06)    VALUE                  
026900             'ADJUST'.                                                    
027000         10  FILLER                  PIC  X(06)    VALUE SPACES.          
027100         10  FILLER                  PIC  X(04)    VALUE                  
027200             'UNIT'.                                                      
027300         10  FILLER                  PIC  X(12)    VALUE SPACES.          
027400         10  FILLER                  PIC  X(06)    VALUE                  
027500             'RETAIL'.                                                    
027600         10  FILLER                  PIC  X(05)    VALUE SPACES.          
027700         10  FILLER                  PIC  X(05)    VALUE                  
027800             'BATCH'.                                                     
027900         10  FILLER                  PIC  X(03)    VALUE SPACES.          
028000         10  FILLER                  PIC  X(05)    VALUE                  
028100             'FASHN'.                                                     
028200         10  FILLER                  PIC  X(51)    VALUE SPACES.          
028300                                                                          
028400     05  HEADING-LINE-4.                                                  
028500         10  FILLER                  PIC  X(03)    VALUE SPACES.          
028600         10  FILLER                  PIC  X(10)    VALUE                  
028700             'DEPT/CLASS'.                                                
028800         10  FILLER                  PIC  X(06)    VALUE SPACES.          
028900         10  FILLER                  PIC  X(06)    VALUE                  
029000             'NUMBER'.                                                    
029100         10  FILLER                  PIC  X(05)    VALUE SPACES.          
029200         10  FILLER                  PIC  X(03)    VALUE                  
029300             'QTY'.                                                       
029400         10  FILLER                  PIC  X(07)    VALUE SPACES.          
029500         10  FILLER                  PIC  X(06)    VALUE                  
029600             'RETAIL'.                                                    
029700         10  FILLER                  PIC  X(09)    VALUE SPACES.          
029800         10  FILLER                  PIC  X(09)    VALUE                  
029900             'EXTENSION'.                                                 
030000         10  FILLER                  PIC  X(05)    VALUE SPACES.          
030100         10  FILLER                  PIC  X(03)    VALUE                  
030200             'NBR'.                                                       
030300         10  FILLER                  PIC  X(04)    VALUE SPACES.          
030400         10  FILLER                  PIC  X(05)    VALUE                  
030500             'SALES'.                                                     
030600         10  FILLER                  PIC  X(03)    VALUE SPACES.          
030700         10  FILLER                  PIC  X(25)    VALUE                  
030800             'REASON/ERROR DESCRIPTION '.                                 
030900         10  FILLER                  PIC  X(20)    VALUE SPACES.          
031000                                                                          
031100 01  DETAIL-LINES.                                                        
031200     05  DETAIL-LINE-1.                                                   
031300         10  FILLER                  PIC  X(01)    VALUE SPACES.          
031400         10  DL1-DEPT-CLASS          PIC  X(14)    VALUE SPACES.          
031500         10  FILLER                  PIC  X(03)    VALUE SPACES.          
031600         10  DL1-SKU-NUMBER          PIC  X(08)    VALUE SPACES.          
031700         10  FILLER                  PIC  X(03)    VALUE SPACES.          
031800         10  DL1-ADJ-QTY             PIC  ZZ,ZZ9-  VALUE ZERO.            
031900         10  FILLER                  PIC  X(03)    VALUE SPACES.          
032000         10  DL1-UNIT-RETAIL         PIC  ZZZ,ZZZ.99                      
032100                                                   VALUE ZERO.            
032200         10  FILLER                  PIC  X(03)    VALUE SPACES.          
032300         10  DL1-RETAIL-EXT          PIC  ZZ,ZZZ,ZZZ.99-                  
032400                                                   VALUE ZERO.            
032500         10  FILLER                  PIC  X(03)    VALUE SPACES.          
032600         10  DL1-BATCH-NBR           PIC  X(04)    VALUE SPACES.          
032700         10  FILLER                  PIC  X(03)    VALUE SPACES.          
032800         10  DL1-SYSTEM-CDE          PIC  X(05)    VALUE SPACES.          
032900         10  FILLER                  PIC  X(03)    VALUE SPACES.          
033000         10  DL1-REASON-CDE          PIC  X(02)    VALUE SPACES.          
033100         10  FILLER                  PIC  X(01)    VALUE SPACES.          
033200         10  DL1-DESCRIPTION         PIC  X(47)    VALUE SPACES.          
033300                                                                          
033400 01  TOTAL-LINES.                                                         
033500     05  TOTAL-LINE-DEPT-1A.                                              
033600         10  FILLER                  PIC  X(01)    VALUE SPACES.          
033700         10  TLD1-DEPARTMENT         PIC  X(10)    VALUE                  
033800             'DEPARTMENT'.                                                
033900         10  FILLER                  PIC  X(01)    VALUE SPACES.          
034000         10  TLD1-DEPT-NBR           PIC  X(04)    VALUE SPACES.          
034100         10  FILLER                  PIC  X(01)    VALUE SPACES.          
034200         10  TLD1-TOTAL              PIC  X(06)    VALUE                  
034300             'TOTAL:'.                                                    
034400         10  FILLER                  PIC  X(06)    VALUE SPACES.          
034500         10  TLD1-TOTAL-ADJUSTMENTS  PIC  X(28)    VALUE                  
034600             'ADJUSTMENTS SENT TO WEB....:'.                              
034700         10  FILLER                  PIC  X(02)    VALUE SPACES.          
034800         10  TLD1-TOT-NUM-OF-ADJ     PIC  ZZ,ZZ9   VALUE ZERO.            
034900         10  FILLER                  PIC  X(02)    VALUE SPACES.          
035000         10  TLD1-ADJUST-QTY         PIC  X(08)    VALUE                  
035100             'ADJ QTY:'.                                                  
035200         10  FILLER                  PIC  X(02)    VALUE SPACES.          
035300         10  TLD1-TOT-ADJ-QTY        PIC  ZZZ,ZZ9-                        
035400                                                   VALUE ZERO.            
035500         10  FILLER                  PIC  X(03)    VALUE SPACES.          
035600         10  TLD1-RETAIL-EXTENSION   PIC  X(17)    VALUE                  
035700             'RETAIL EXTENSION:'.                                         
035800         10  TLD1-RETAIL-EXT-TOT     PIC  ZZ,ZZZ,ZZ9.99-                  
035900                                                   VALUE ZERO.            
036000     05  TOTAL-LINE-DEPT-2A.                                              
036100         10  FILLER                  PIC  X(29)    VALUE SPACES.          
036200         10  TLD2-TOTAL-ADJUSTMENTS  PIC  X(28)    VALUE                  
036300             'ADJUSTMENTS SENT TO FASHION:'.                              
036400         10  FILLER                  PIC  X(02)    VALUE SPACES.          
036500         10  TLD2-TOT-NUM-OF-ADJ     PIC  ZZ,ZZ9   VALUE ZERO.            
036600         10  FILLER                  PIC  X(02)    VALUE SPACES.          
036700         10  TLD2-ADJUST-QTY         PIC  X(08)    VALUE                  
036800             'ADJ QTY:'.                                                  
036900         10  FILLER                  PIC  X(02)    VALUE SPACES.          
037000         10  TLD2-TOT-ADJ-QTY        PIC  ZZZ,ZZ9-                        
037100                                                   VALUE ZERO.            
037200         10  FILLER                  PIC  X(03)    VALUE SPACES.          
037300         10  TLD2-RETAIL-EXTENSION   PIC  X(17)    VALUE                  
037400             'RETAIL EXTENSION:'.                                         
037500         10  TLD2-RETAIL-EXT-TOT     PIC  ZZ,ZZZ,ZZ9.99-                  
037600                                                   VALUE ZERO.            
037700     05  TOTAL-LINE-DEPT-3A.                                              
037800         10  FILLER                  PIC  X(29)    VALUE SPACES.          
037900         10  TLD3-TOTAL-ADJUSTMENTS  PIC  X(28)    VALUE                  
038000             'ADJUSTMENTS DIFFERENCE.....:'.                              
038100         10  FILLER                  PIC  X(02)    VALUE SPACES.          
038200         10  TLD3-TOT-NUM-OF-ADJ     PIC  ZZ,ZZ9   VALUE ZERO.            
038300         10  FILLER                  PIC  X(02)    VALUE SPACES.          
038400         10  TLD3-ADJUST-QTY         PIC  X(08)    VALUE                  
038500             'ADJ QTY:'.                                                  
038600         10  FILLER                  PIC  X(02)    VALUE SPACES.          
038700         10  TLD3-TOT-ADJ-QTY        PIC  ZZZ,ZZ9-                        
038800                                                   VALUE ZERO.            
038900         10  FILLER                  PIC  X(03)    VALUE SPACES.          
039000         10  TLD3-RETAIL-EXTENSION   PIC  X(17)    VALUE                  
039100             'RETAIL EXTENSION:'.                                         
039200         10  TLD3-RETAIL-EXT-TOT     PIC  ZZ,ZZZ,ZZ9.99-                  
039300                                                   VALUE ZERO.            
039400     05  TOTAL-LINE-DEPT-1B.                                              
039500         10  FILLER                  PIC  X(01)    VALUE SPACES.          
039600         10  TLD1B-DEPARTMENT        PIC  X(10)    VALUE                  
039700             'DEPARTMENT'.                                                
039800         10  FILLER                  PIC  X(01)    VALUE SPACES.          
039900         10  TLD1B-DEPT-NBR          PIC  X(04)    VALUE SPACES.          
040000         10  FILLER                  PIC  X(01)    VALUE SPACES.          
040100         10  TLD1B-TOTAL             PIC  X(06)    VALUE                  
040200             'TOTAL:'.                                                    
040300         10  FILLER                  PIC  X(06)    VALUE SPACES.          
040400         10  TLD1B-TOTAL-ADJUSTMENTS PIC  X(28)    VALUE                  
040500             'ADJUSTMENTS SENT TO FASHION:'.                              
040600         10  FILLER                  PIC  X(02)    VALUE SPACES.          
040700         10  TLD1B-TOT-NUM-OF-ADJ    PIC  ZZ,ZZ9   VALUE ZERO.            
040800         10  FILLER                  PIC  X(02)    VALUE SPACES.          
040900         10  TLD1B-ADJUST-QTY        PIC  X(08)    VALUE                  
041000             'ADJ QTY:'.                                                  
041100         10  FILLER                  PIC  X(02)    VALUE SPACES.          
041200         10  TLD1B-TOT-ADJ-QTY       PIC  ZZZ,ZZ9-                        
041300                                                   VALUE ZERO.            
041400         10  FILLER                  PIC  X(03)    VALUE SPACES.          
041500         10  TLD1B-RETAIL-EXTENSION  PIC  X(17)    VALUE                  
041600             'RETAIL EXTENSION:'.                                         
041700         10  TLD1B-RETAIL-EXT-TOT    PIC  ZZ,ZZZ,ZZ9.99-                  
041800                                                   VALUE ZERO.            
041900     05  TOTAL-LINE-DC-1A.                                                
042000         10  FILLER                  PIC  X(05)    VALUE SPACES.          
042100         10  TLDC1-DC-LOCATION-TOT   PIC  X(18)    VALUE                  
042200             'DC LOCATION TOTAL:'.                                        
042300         10  FILLER                  PIC  X(06)    VALUE SPACES.          
042400         10  TLDC1-TOTAL-ADJUSTMENTS PIC  X(28)    VALUE                  
042500             'ADJUSTMENTS SENT TO WEB....:'.                              
042600         10  FILLER                  PIC  X(02)    VALUE SPACES.          
042700         10  TLDC1-TOT-NUM-OF-ADJ    PIC  ZZ,ZZ9   VALUE ZERO.            
042800         10  FILLER                  PIC  X(02)    VALUE SPACES.          
042900         10  TLDC1-ADJUST-QTY        PIC  X(08)    VALUE                  
043000             'ADJ QTY:'.                                                  
043100         10  FILLER                  PIC  X(02)    VALUE SPACES.          
043200         10  TLDC1-TOT-ADJ-QTY       PIC  ZZZ,ZZ9-                        
043300                                                   VALUE ZERO.            
043400         10  FILLER                  PIC  X(03)    VALUE SPACES.          
043500         10  TLDC1-RETAIL-EXTENSION  PIC  X(17)    VALUE                  
043600             'RETAIL EXTENSION:'.                                         
043700         10  TLDC1-RETAIL-EXT-TOT    PIC  ZZ,ZZZ,ZZ9.99-                  
043800                                                   VALUE ZERO.            
043900     05  TOTAL-LINE-DC-2A.                                                
044000         10  FILLER                  PIC  X(29)    VALUE SPACES.          
044100         10  TLDC2-TOTAL-ADJUSTMENTS PIC  X(28)    VALUE                  
044200             'ADJUSTMENTS SENT TO FASHION:'.                              
044300         10  FILLER                  PIC  X(02)    VALUE SPACES.          
044400         10  TLDC2-TOT-NUM-OF-ADJ    PIC  ZZ,ZZ9   VALUE ZERO.            
044500         10  FILLER                  PIC  X(02)    VALUE SPACES.          
044600         10  TLDC2-ADJUST-QTY        PIC  X(08)    VALUE                  
044700             'ADJ QTY:'.                                                  
044800         10  FILLER                  PIC  X(02)    VALUE SPACES.          
044900         10  TLDC2-TOT-ADJ-QTY       PIC  ZZZ,ZZ9-                        
045000                                                   VALUE ZERO.            
045100         10  FILLER                  PIC  X(03)    VALUE SPACES.          
045200         10  TLDC2-RETAIL-EXTENSION  PIC  X(17)    VALUE                  
045300             'RETAIL EXTENSION:'.                                         
045400         10  TLDC2-RETAIL-EXT-TOT    PIC  ZZ,ZZZ,ZZ9.99-                  
045500                                                   VALUE ZERO.            
045600     05  TOTAL-LINE-DC-3A.                                                
045700         10  FILLER                  PIC  X(29)    VALUE SPACES.          
045800         10  TLDC3-TOTAL-ADJUSTMENTS PIC  X(28)    VALUE                  
045900             'ADJUSTMENTS DIFFERENCE.....:'.                              
046000         10  FILLER                  PIC  X(02)    VALUE SPACES.          
046100         10  TLDC3-TOT-NUM-OF-ADJ    PIC  ZZ,ZZ9   VALUE ZERO.            
046200         10  FILLER                  PIC  X(02)    VALUE SPACES.          
046300         10  TLDC3-ADJUST-QTY        PIC  X(08)    VALUE                  
046400             'ADJ QTY:'.                                                  
046500         10  FILLER                  PIC  X(02)    VALUE SPACES.          
046600         10  TLDC3-TOT-ADJ-QTY       PIC  ZZZ,ZZ9-                        
046700                                                   VALUE ZERO.            
046800         10  FILLER                  PIC  X(03)    VALUE SPACES.          
046900         10  TLDC3-RETAIL-EXTENSION  PIC  X(17)    VALUE                  
047000             'RETAIL EXTENSION:'.                                         
047100         10  TLDC3-RETAIL-EXT-TOT    PIC  ZZ,ZZZ,ZZ9.99-                  
047200                                                   VALUE ZERO.            
047300                                                                          
047400     05  TOTAL-LINE-DC-1B.                                                
047500         10  FILLER                  PIC  X(05)    VALUE SPACES.          
047600         10  TLDC1B-DC-LOCATION-TOT  PIC  X(18)    VALUE                  
047700             'DC LOCATION TOTAL:'.                                        
047800         10  FILLER                  PIC  X(06)    VALUE SPACES.          
047900         10  TLDC1B-TTL-ADJUSTMENTS  PIC  X(28)    VALUE                  
048000             'ADJUSTMENTS SENT TO FASHION:'.                              
048100         10  FILLER                  PIC  X(02)    VALUE SPACES.          
048200         10  TLDC1B-TOT-NUM-OF-ADJ   PIC  ZZ,ZZ9   VALUE ZERO.            
048300         10  FILLER                  PIC  X(02)    VALUE SPACES.          
048400         10  TLDC1B-ADJUST-QTY       PIC  X(08)    VALUE                  
048500             'ADJ QTY:'.                                                  
048600         10  FILLER                  PIC  X(02)    VALUE SPACES.          
048700         10  TLDC1B-TOT-ADJ-QTY      PIC  ZZZ,ZZ9- VALUE ZERO.            
048800         10  FILLER                  PIC  X(03)    VALUE SPACES.          
048900         10  TLDC1B-RETAIL-EXTENSION PIC  X(17)    VALUE                  
049000             'RETAIL EXTENSION:'.                                         
049100         10  TLDC1B-RETAIL-EXT-TOT   PIC  ZZ,ZZZ,ZZ9.99-                  
049200                                                   VALUE ZERO.            
049300     05  TOTAL-LINE-SUM-1A.                                               
049400         10  FILLER                  PIC  X(06)    VALUE SPACES.          
049500         10  TLSUM1-DATE             PIC  X(10)    VALUE SPACES.          
049600         10  FILLER                  PIC  X(01)    VALUE SPACES.          
049700         10  TLSUM1-TOTAL-HDG        PIC  X(06)    VALUE                  
049800             'TOTAL:'.                                                    
049900         10  FILLER                  PIC  X(06)    VALUE SPACES.          
050000         10  TLSUM1-TOTAL-ADJUSTMENTS PIC X(28)    VALUE                  
050100             'ADJUSTMENTS SENT TO WEB....:'.                              
050200         10  FILLER                  PIC  X(02)    VALUE SPACES.          
050300         10  TLSUM1-TOT-NUM-OF-ADJ   PIC  ZZ,ZZ9   VALUE ZERO.            
050400         10  FILLER                  PIC  X(02)    VALUE SPACES.          
050500         10  TLSUM1-ADJUST-QTY       PIC  X(08)    VALUE                  
050600             'ADJ QTY:'.                                                  
050700         10  FILLER                  PIC  X(02)    VALUE SPACES.          
050800         10  TLSUM1-TOT-ADJ-QTY      PIC  ZZZ,ZZ9-                        
050900                                                   VALUE ZERO.            
051000         10  FILLER                  PIC  X(03)    VALUE SPACES.          
051100         10  TLSUM1-RETAIL-EXTENSION PIC  X(17)    VALUE                  
051200             'RETAIL EXTENSION:'.                                         
051300         10  TLSUM1-RETAIL-EXT-TOT   PIC  ZZ,ZZZ,ZZ9.99-                  
051400                                                   VALUE ZERO.            
051500     05  TOTAL-LINE-SUM-2A.                                               
051600         10  FILLER                  PIC  X(29)    VALUE SPACES.          
051700         10  TLSUM2-TOTAL-ADJUSTMENTS PIC X(28)    VALUE                  
051800             'ADJUSTMENTS SENT TO FASHION:'.                              
051900         10  FILLER                  PIC  X(02)    VALUE SPACES.          
052000         10  TLSUM2-TOT-NUM-OF-ADJ   PIC  ZZ,ZZ9   VALUE ZERO.            
052100         10  FILLER                  PIC  X(02)    VALUE SPACES.          
052200         10  TLSUM2-ADJUST-QTY       PIC  X(08)    VALUE                  
052300             'ADJ QTY:'.                                                  
052400         10  FILLER                  PIC  X(02)    VALUE SPACES.          
052500         10  TLSUM2-TOT-ADJ-QTY      PIC  ZZZ,ZZ9-                        
052600                                                   VALUE ZERO.            
052700         10  FILLER                  PIC  X(03)    VALUE SPACES.          
052800         10  TLSUM2-RETAIL-EXTENSION PIC  X(17)    VALUE                  
052900             'RETAIL EXTENSION:'.                                         
053000         10  TLSUM2-RETAIL-EXT-TOT   PIC  ZZ,ZZZ,ZZ9.99-                  
053100                                                   VALUE ZERO.            
053200     05  TOTAL-LINE-SUM-3A.                                               
053300         10  FILLER                  PIC  X(29)    VALUE SPACES.          
053400         10  TLSUM3-TOTAL-ADJUSTMENTS PIC X(28)    VALUE                  
053500             'ADJUSTMENTS DIFFERENCE.....:'.                              
053600         10  FILLER                  PIC  X(02)    VALUE SPACES.          
053700         10  TLSUM3-TOT-NUM-OF-ADJ   PIC  ZZ,ZZ9   VALUE ZERO.            
053800         10  FILLER                  PIC  X(02)    VALUE SPACES.          
053900         10  TLSUM3-ADJUST-QTY       PIC  X(08)    VALUE                  
054000             'ADJ QTY:'.                                                  
054100         10  FILLER                  PIC  X(02)    VALUE SPACES.          
054200         10  TLSUM3-TOT-ADJ-QTY      PIC  ZZZ,ZZ9-                        
054300                                                   VALUE ZERO.            
054400         10  FILLER                  PIC  X(03)    VALUE SPACES.          
054500         10  TLSUM3-RETAIL-EXTENSION PIC  X(17)    VALUE                  
054600             'RETAIL EXTENSION:'.                                         
054700         10  TLSUM3-RETAIL-EXT-TOT   PIC  ZZ,ZZZ,ZZ9.99-                  
054800                                                   VALUE ZERO.            
054900     05  TOTAL-LINE-SUM-1B.                                               
055000         10  FILLER                  PIC  X(06)    VALUE SPACES.          
055100         10  TLSUM1B-DATE            PIC  X(10)    VALUE SPACES.          
055200         10  FILLER                  PIC  X(01)    VALUE SPACES.          
055300         10  TLSUM1B-TOTAL-HDG       PIC  X(06)    VALUE                  
055400             'TOTAL:'.                                                    
055500         10  FILLER                  PIC  X(06)    VALUE SPACES.          
055600         10  TLSUM1B-TTL-ADJUSTMENTS PIC X(28)    VALUE                   
055700             'ADJUSTMENTS SENT TO FASHION:'.                              
055800         10  FILLER                  PIC  X(02)    VALUE SPACES.          
055900         10  TLSUM1B-TOT-NUM-OF-ADJ  PIC  ZZ,ZZ9   VALUE ZERO.            
056000         10  FILLER                  PIC  X(02)    VALUE SPACES.          
056100         10  TLSUM1B-ADJUST-QTY      PIC  X(08)    VALUE                  
056200             'ADJ QTY:'.                                                  
056300         10  FILLER                  PIC  X(02)    VALUE SPACES.          
056400         10  TLSUM1B-TOT-ADJ-QTY     PIC  ZZZ,ZZ9-                        
056500                                                   VALUE ZERO.            
056600         10  FILLER                  PIC  X(03)    VALUE SPACES.          
056700         10  TLSUM1B-RTL-EXTENSION   PIC  X(17)    VALUE                  
056800             'RETAIL EXTENSION:'.                                         
056900         10  TLSUM1B-RETAIL-EXT-TOT  PIC  ZZ,ZZZ,ZZ9.99-                  
057000                                                   VALUE ZERO.            
057100 01  MISCELLANEOUS-LINES.                                                 
057200     05  BLANK-LINE.                                                      
057300         10  FILLER                  PIC  X(132)   VALUE SPACES.          
057400                                                                          
057500     05  END-OF-REPORT-LINE.                                              
057600         10  FILLER                  PIC  X(44)    VALUE SPACES.          
057700         10  FILLER                  PIC  X(45)    VALUE                  
057800             '*******   E N D   O F   R E P O R T   *******'.             
057900         10  FILLER                  PIC  X(43)    VALUE SPACES.          
058000                                                                          
058100     05  NO-INV-ITEMS-TO-PROCESS-LINE.                                    
058200         10  FILLER                  PIC  X(40)    VALUE SPACES.          
058300         10  FILLER                  PIC  X(49)    VALUE                  
058400         '*******  NO INVENTORY ITEMS TO PROCESS    *******'.             
058500         10  FILLER                  PIC  X(43)    VALUE SPACES.          
058600                                                                          
058700******************************************************************        
058800*  DB2 AREA FOR XLT_LOC                                                   
058900******************************************************************        
059000     EXEC SQL                                                             
059100         INCLUDE XLT00010                                                 
059200     END-EXEC.                                                            
059300                                                                          
059400******************************************************************        
059500*  DB2 AREA FOR TCOCNTL (COMPANY CONTROL TABLE)                           
059600******************************************************************        
059700     EXEC SQL                                                             
059800         INCLUDE TCOCNTL                                                  
059900     END-EXEC.                                                            
060000                                                                          
060100******************************************************************        
060200*  DB2 VIEW DECLARATION - WSV_PIX_RSN_CDE                                 
060300******************************************************************        
060400     EXEC SQL                                                             
060500         INCLUDE WSV00001                                                 
060600     END-EXEC.                                                            
060700                                                                          
060800******************************************************************        
060900*  DB2 VIEW DECLARATION - WSV_PIX_RTE                                     
061000******************************************************************        
061100     EXEC SQL                                                             
061200         INCLUDE WSV00002                                                 
061300     END-EXEC.                                                            
061400                                                                          
061500******************************************************************        
061600*    DB2 AREA FOR COMMUNICATIONS                                          
061700******************************************************************        
061800     EXEC SQL                                                             
061900         INCLUDE SQLCA                                                    
062000     END-EXEC.                                                            
062100                                                                          
062200******************************************************************        
062300*    DB2 AREA FOR COMMUNICATIONS AREA2                                    
062400******************************************************************        
062500     COPY  SQLCA2.                                                        
062600                                                                          
062700******************************************************************        
062800*    CURSOR FOR VALID LOCATION                                            
062900******************************************************************        
063000     EXEC SQL                                                             
063100         DECLARE VALID-LOC-CSR CURSOR FOR                                 
063200          SELECT LOC_NBR                                                  
063300               , LOC_TYP_CDE                                              
063400               , LOC_NM                                                   
063500          FROM   XLT_LOC                                                  
063600          WHERE  LOC_TYP_CDE   = :PC-DS                                   
063700            AND  E_BUS_IND     = :PC-Y                                    
063800          ORDER BY LOC_NBR                                                
063900     END-EXEC.                                                            
064000                                                                          
064100/*****************************************************************        
064200 PROCEDURE DIVISION.                                                      
064300     DISPLAY '**** START OF WSKRP005 ****'.                               
064400                                                                          
064500*OPEN FILES.                                                              
064600     OPEN INPUT  INPUT-EXTRACT-FILE                                       
064700          INPUT  CONTROL-CARD                                             
064800          OUTPUT OUTPUT-REPORT.                                           
064900                                                                          
065000*READ CONTROL CARD RECORD.                                                
065100     PERFORM 2200-READ-CONTROL-CARD.                                      
065200     IF PS-END-OF-CONTROL                                                 
065300         MOVE '0000 PROC DIVISION' TO PV-ABEND-PARAGRAPH                  
065400         MOVE 'CONTROL CARD'       TO PV-DB2-TABLE-NAME                   
065500         MOVE 'SELECT'             TO PV-ABEND-OPERATION                  
065600         PERFORM Z998-ERROR                                               
065700     END-IF.                                                              
065800                                                                          
065900     IF CC-REPORT-CODE = '1'                                              
066000         SET PS-REPORT-1           TO TRUE                                
066100     ELSE                                                                 
066200         SET PS-REPORT-2           TO TRUE                                
066300     END-IF.                                                              
066400                                                                          
066500                                                                          
066600*READ FIRST INPUT RECORD.                                                 
066700     SET PS-NOT-END-OF-INPUT TO TRUE.                                     
066800     PERFORM 2100-READ-EXTRACT.                                           
066900     IF PS-END-OF-INPUT                                                   
067000         SET PS-EMPTY-FILE TO TRUE                                        
067100     END-IF.                                                              
067200                                                                          
067300*SET/PRINT REPORT HEADINGS.                                               
067400     IF NOT PS-END-OF-INPUT                                               
067500         MOVE WS043-CNSTK-LOC-ID TO PV-PREV-LOCN                          
067600         MOVE WS043-BTCH-PRC-DTE TO PV-HOLD-BTCH-PRC-DTE                  
067700     ELSE                                                                 
067800         PERFORM 1290-SELECT-BTCH-PRCSS-DTE                               
067900     END-IF.                                                              
068000                                                                          
068100     PERFORM 1000-INITIALIZATION.                                         
068200                                                                          
068300     IF PS-EMPTY-FILE                                                     
068400         PERFORM 2500-OPEN-VALID-LOC-CSR                                  
068500         PERFORM 2520-GET-VALID-LOC                                       
068600           UNTIL PS-END-LOC-CSR                                           
068700         PERFORM 2550-CLOSE-VALID-LOC-CSR                                 
068800     ELSE                                                                 
068900*PROCESS INPUT FILE.                                                      
069000         IF PS-END-OF-INPUT                                               
069100             PERFORM 1300-NO-DETAIL-LINE                                  
069200         ELSE                                                             
069300             MOVE WS043-CNSTK-LOC-ID     TO PV-CURR-LOCN                  
069400             MOVE WS043-BTCH-PRC-DTE     TO PV-HOLD-BTCH-PRC-DTE          
069500             MOVE WS043-DEPT-NBR         TO PV-CURR-DEPT                  
069600             PERFORM 2000-PROCESS-EXTRACT                                 
069700                UNTIL PS-END-OF-INPUT                                     
069800         END-IF                                                           
069900*WRITE LAST TOTALS.                                                       
070000         PERFORM 2020-DEPT-BREAK                                          
070100         PERFORM 2040-LOCN-BREAK                                          
070200         PERFORM 2050-DATE-BREAK                                          
070300                                                                          
070400         PERFORM 1400-END-OF-RPT-LINE                                     
070500     END-IF.                                                              
070600                                                                          
070700     DISPLAY '**** RECORDS WRITTEN ==> ' PV-REC-OUT-QTY                   
070800                                                                          
070900*CLOSE FILES.                                                             
071000     CLOSE INPUT-EXTRACT-FILE                                             
071100           CONTROL-CARD                                                   
071200           OUTPUT-REPORT.                                                 
071300                                                                          
071400     DISPLAY '****  END OF WSKRP005  ****'.                               
071500     GOBACK.                                                              
071600                                                                          
071700******************************************************************        
071800*  PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.               *        
071900******************************************************************        
072000 1000-INITIALIZATION.                                                     
072100                                                                          
072200     ACCEPT PV-TODAYS-DATE FROM DATE.                                     
072300     ACCEPT PV-TODAYS-TIME FROM TIME.                                     
072400                                                                          
072500     MOVE PV-TODAYS-DATE-MM    TO DP132-RUN-MONTH                         
072600                                  PV-CURRENT-DATE-MM.                     
072700     MOVE PV-TODAYS-DATE-DD    TO DP132-RUN-DAY                           
072800                                  PV-CURRENT-DATE-DD.                     
072900     MOVE PV-TODAYS-DATE-CC    TO PV-CURRENT-DATE-CC.                     
073000     MOVE PV-TODAYS-DATE-YY    TO PV-TODAYS-DATE-Y2                       
073100                                  PV-CURRENT-DATE-YY.                     
073200     MOVE PV-TODAYS-DATE-YYYY  TO DP132-RUN-YEAR.                         
073300                                                                          
073400     MOVE PV-TODAYS-TIME-HH    TO DP132-RUN-HOUR.                         
073500     MOVE PV-TODAYS-TIME-MM    TO DP132-RUN-MINUTE.                       
073600                                                                          
073700     MOVE PC-WSKRP005          TO DP132-PROGRAM-NAME.                     
073800                                                                          
073900     IF PS-REPORT-1                                                       
074000         MOVE PC-REPORT-NBR-1  TO DP132-REPORT-NUMBER                     
074100     ELSE                                                                 
074200         MOVE PC-REPORT-NBR-2  TO DP132-REPORT-NUMBER                     
074300     END-IF.                                                              
074400                                                                          
074500     SET PS-FIRST-TIME-YES     TO TRUE.                                   
074600     SET PS-FIRST-LOC-YES      TO TRUE.                                   
074700                                                                          
074800     MOVE ZERO                 TO PV-REC-OUT-QTY.                         
074900                                                                          
075000     MOVE ZERO                 TO PV-PAGE-COUNT                           
075100                                  PV-ACCUM-TOT-ADJ-DEPT                   
075200                                  PV-ACCUM-TOT-ADJ-DEPT-FSHN              
075300                                  PV-ACCUM-TOT-ADJ-LOCN                   
075400                                  PV-ACCUM-TOT-ADJ-LOCN-FSHN              
075500                                  PV-ACCUM-TOT-ADJ-DATE                   
075600                                  PV-ACCUM-TOT-ADJ-DATE-FSHN              
075700                                  PV-ACCUM-QTY-DEPT                       
075800                                  PV-ACCUM-RET-EXTENS-DEPT                
075900                                  PV-ACCUM-QTY-DEPT-FSHN                  
076000                                  PV-ACCUM-RET-EXT-DEPT-FSHN              
076100                                  PV-ACCUM-QTY-LOCN                       
076200                                  PV-ACCUM-RET-EXTENS-LOCN                
076300                                  PV-ACCUM-QTY-LOCN-FSHN                  
076400                                  PV-ACCUM-RET-EXT-LOCN-FSHN              
076500                                  PV-ACCUM-QTY-DATE                       
076600                                  PV-ACCUM-RET-EXTENS-DATE                
076700                                  PV-ACCUM-QTY-DATE-FSHN                  
076800                                  PV-ACCUM-RET-EXT-DATE-FSHN.             
076900                                                                          
077000     IF PS-END-OF-INPUT                                                   
077100         CONTINUE                                                         
077200     ELSE                                                                 
077300         PERFORM 1200-PRINT-HEADINGS                                      
077400     END-IF.                                                              
077500                                                                          
077600******************************************************************        
077700*  RETRIEVE STORE NAME FROM INPUT RECORD.                        *        
077800******************************************************************        
077900 1100-GET-LOC-NAME.                                                       
078000                                                                          
078210     MOVE WS043-CNSTK-LOC-ID  TO DK-LOC-NBR                               
078220                                 PV-STORE-NMBR.                           
078310                                                                          
078400     EXEC SQL                                                             
078500         SELECT  LOC_NBR                                                  
078600              ,  LOC_NM                                                   
078700           INTO  :XLT00010-LOC-NBR                                        
078800              ,  :XLT00010-LOC-NM                                         
078900           FROM  XLT_LOC                                                  
079000          WHERE  LOC_NBR = :DK-LOC-NBR                                    
079100     END-EXEC.                                                            
079200                                                                          
079300     EVALUATE TRUE                                                        
079400       WHEN SQLCODE = ZERO                                                
079500           MOVE XLT00010-LOC-NBR     TO PV-STORE-NMBR                     
079600           MOVE XLT00010-LOC-NM      TO PV-DC-STORE-NAME                  
079700*CRAIG ADDED THESE LINES FOR THE SAKE OF SANITY 02/2008                   
079800           MOVE ZERO   TO XLT00010-LOC-NBR                                
079900           MOVE SPACES TO XLT00010-LOC-NM                                 
080000*END SANITY 02/2008 (OR "BEGIN INSANITY"--MAKE YOUR CHOICE)               
080100                                                                          
080200       WHEN SQLCODE = 100                                                 
080300           MOVE 'LOCATION NOT FOUND' TO PV-DC-STORE-NAME                  
080400                                                                          
080500       WHEN OTHER                                                         
080600           MOVE '1100-GET-LOC-NAME'  TO PV-ABEND-PARAGRAPH                
080700           MOVE 'XLT_LOC'            TO PV-DB2-TABLE-NAME                 
080800           MOVE 'SELECT'             TO PV-ABEND-OPERATION                
080900           PERFORM Z999-SQL-ERROR                                         
081000     END-EVALUATE.                                                        
081100                                                                          
081200******************************************************************        
081300*  PRINT THE REPORT HEADINGS.                                    *        
081400******************************************************************        
081500 1200-PRINT-HEADINGS.                                                     
081600                                                                          
081700     PERFORM 1100-GET-LOC-NAME                                            
081800     PERFORM 1205-PRINT-HEADING-LINES.                                    
081900                                                                          
082000******************************************************************        
082100*  PRINT HEADINGS                                                *        
082200*  THIS WAS MADE A SEPARATE PARAGRAPH SO THIS PART COULD BE      *        
082300*   CALLED FROM ANOTHER PART OF PROGRAM WITHOUT DOING 1100-.     *        
082400******************************************************************        
082500 1205-PRINT-HEADING-LINES.                                                
082600                                                                          
082700     ADD 1  TO PV-PAGE-COUNT.                                             
082800     MOVE PV-PAGE-COUNT TO DP132-PAGE-NUMBER.                             
082900                                                                          
083000     WRITE OUTPUT-REC                                                     
083100       FROM DP132-STANDRD-HEADER-132-COLS                                 
083200         AFTER ADVANCING PAGE.                                            
083300                                                                          
083400     MOVE 1 TO PV-LINE-COUNT.                                             
083500                                                                          
083600     IF PS-REPORT-1                                                       
083700         MOVE PV-CURRENT-DATE     TO HL1A-DATE-MMDDYYYY                   
083800         WRITE OUTPUT-REC                                                 
083900            FROM HEADING-LINE-1A                                          
084000              AFTER ADVANCING 2 LINES                                     
084100     ELSE                                                                 
084200         MOVE PV-CURRENT-DATE     TO HL1B-DATE-MMDDYYYY                   
084300         WRITE OUTPUT-REC                                                 
084400            FROM HEADING-LINE-1B                                          
084500              AFTER ADVANCING 2 LINES                                     
084600     END-IF.                                                              
084700     IF PS-LOCATION-CHANGE-NO                                             
084800        MOVE PV-DC-STORE-NAME TO HL2-DC-STORE-NAME                        
084900        MOVE PV-STORE-NMBR    TO HL2-DC-STORE-NBR                         
084910     ELSE                                                                 
084911**GB 4/12/13 BEG                                                          
084912        MOVE PV-DC-STORE-NAME-PREV TO HL2-DC-STORE-NAME                   
084913        MOVE PV-STORE-NMBR-PREV    TO HL2-DC-STORE-NBR                    
084914**GB 4/12/13 END                                                          
084920     END-IF                                                               
085000                                                                          
085100     WRITE OUTPUT-REC                                                     
085200       FROM HEADING-LINE-2                                                
085300         AFTER ADVANCING 2 LINES.                                         
085400                                                                          
085500     WRITE OUTPUT-REC                                                     
085600       FROM HEADING-LINE-3                                                
085700         AFTER ADVANCING 2 LINES.                                         
085800                                                                          
085900                                                                          
086000     WRITE OUTPUT-REC                                                     
086100       FROM HEADING-LINE-4                                                
086200         AFTER ADVANCING 1 LINES.                                         
086300                                                                          
086400     WRITE OUTPUT-REC                                                     
086500       FROM BLANK-LINE                                                    
086600         AFTER ADVANCING 1 LINES.                                         
086700                                                                          
086800     MOVE 10 TO PV-LINE-COUNT.                                            
086900                                                                          
087000******************************************************************        
087100*  PRINT A BLANK REPORT LINE.                                    *        
087200******************************************************************        
087300 1210-PRINT-BLANK-LINE.                                                   
087400                                                                          
087500*CHECK FOR OVERFLOW.                                                      
087600     ADD 1 TO PV-LINE-COUNT.                                              
087700     IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                                 
087800         SET PS-NEW-PAGE TO TRUE                                          
087900         PERFORM 1200-PRINT-HEADINGS                                      
088000     ELSE                                                                 
088100*PRINT LINE.                                                              
088200         WRITE OUTPUT-REC                                                 
088300           FROM BLANK-LINE                                                
088400             AFTER ADVANCING 1 LINES                                      
088500     END-IF.                                                              
088600                                                                          
088700******************************************************************        
088800*  SELECT  BATCH PROCESS DATE.                                   *        
088900******************************************************************        
089000 1290-SELECT-BTCH-PRCSS-DTE.                                              
089100*GET EXTRACT DATE.                                                        
089200     EXEC SQL                                                             
089300         SELECT  BTCH_PRC_DTE                                             
089400           INTO  :PV-CURRENT-DB2-DATE                                     
089500           FROM  TCOCNTL                                                  
089600          WHERE  OPER_UNT_ID = 90                                         
089700     END-EXEC.                                                            
089800                                                                          
089900     EVALUATE TRUE                                                        
090000       WHEN SQLCODE = ZERO                                                
090100           MOVE PV-CURRENT-DB2-DATE   TO PV-CURR-DATE                     
090200                                                                          
090300       WHEN OTHER                                                         
090400           MOVE 'INITIALIZATION' TO PV-ABEND-PARAGRAPH                    
090500           MOVE 'TCOCNTL'      TO PV-DB2-TABLE-NAME                       
090600           MOVE 'EXTRACT DATE' TO PV-ABEND-OPERATION                      
090700           PERFORM Z999-SQL-ERROR                                         
090800     END-EVALUATE.                                                        
090900                                                                          
091000******************************************************************        
091100*  PRINT A 'NO INPUT' REPORT LINE.                               *        
091200******************************************************************        
091300 1300-NO-DETAIL-LINE.                                                     
091400                                                                          
091500     WRITE OUTPUT-REC                                                     
091600       FROM NO-INV-ITEMS-TO-PROCESS-LINE                                  
091700         AFTER ADVANCING 3 LINES.                                         
091800                                                                          
091900******************************************************************        
092000*  PRINT A BLANK REPORT LINE                                     *        
092100******************************************************************        
092200 1350-WRITE-BLANK-LINE.                                                   
092300                                                                          
092400     WRITE OUTPUT-REC                                                     
092500       FROM BLANK-LINE                                                    
092600         AFTER ADVANCING 3 LINES.                                         
092700                                                                          
092800******************************************************************        
092900*  PRINT A 'END OF REPORT' REPORT LINE.                          *        
093000******************************************************************        
093100 1400-END-OF-RPT-LINE.                                                    
093200                                                                          
093300*PRINT LINE.                                                              
093400     WRITE OUTPUT-REC                                                     
093500       FROM END-OF-REPORT-LINE                                            
093600         AFTER ADVANCING 3 LINES.                                         
093700                                                                          
093800******************************************************************        
093900*  THIS IS THE MAIN CONTROLLING PARAGRAPH FOR READING THE INPUT  *        
094000*  EXTRACT FILE (FORMAT CERD014) AND CREATING THE 'E-COMMERCE    *        
094100*  INVENTORY ADJUSTMENT' REPORT                                  *        
094200******************************************************************        
094300 2000-PROCESS-EXTRACT.                                                    
094400                                                                          
094500*CHECK FOR BREAKS.                                                        
094600     IF WS043-BTCH-PRC-DTE   NOT = PV-HOLD-BTCH-PRC-DTE                   
094700         PERFORM 2020-DEPT-BREAK                                          
094800         PERFORM 2040-LOCN-BREAK                                          
094900         PERFORM 2050-DATE-BREAK                                          
095000         MOVE WS043-BTCH-PRC-DTE TO PV-HOLD-BTCH-PRC-DTE                  
095100         MOVE ZERO               TO PV-PAGE-COUNT                         
095200         PERFORM 1200-PRINT-HEADINGS                                      
095300     ELSE                                                                 
095400         IF WS043-CNSTK-LOC-ID   NOT = PV-CURR-LOCN                       
095410             SET PS-LOCATION-CHANGE-YES TO TRUE                           
095420             MOVE PV-DC-STORE-NAME TO PV-DC-STORE-NAME-PREV               
095430             MOVE PV-STORE-NMBR    TO PV-STORE-NMBR-PREV                  
095500             PERFORM 2020-DEPT-BREAK                                      
095510             SET PS-LOCATION-CHANGE-NO TO TRUE                            
095600             PERFORM 2040-LOCN-BREAK                                      
095700             MOVE WS043-CNSTK-LOC-ID TO PV-CURR-LOCN                      
095800             MOVE ZERO               TO PV-PAGE-COUNT                     
095900             PERFORM 1200-PRINT-HEADINGS                                  
096000         ELSE                                                             
096100             IF WS043-DEPT-NBR   NOT = PV-CURR-DEPT                       
096200                 PERFORM 2020-DEPT-BREAK                                  
096300                 PERFORM 1210-PRINT-BLANK-LINE                            
096400                 MOVE WS043-DEPT-NBR TO PV-CURR-DEPT                      
096500             END-IF                                                       
096600         END-IF                                                           
096700     END-IF.                                                              
096800                                                                          
096900*PRINT DETAIL LINE.                                                       
097000     PERFORM 2060-PRINT-DETAIL.                                           
097100     SET PS-FIRST-TIME-NO TO TRUE.                                        
097200                                                                          
097300*READ NEXT INPUT RECORD.                                                  
097400     ADD 1 TO PV-REC-OUT-QTY.                                             
097500     PERFORM 2100-READ-EXTRACT.                                           
097600                                                                          
097700******************************************************************        
097800*  PRINT A REPORT SUB-TOTAL LINE (BY DEPARTMENT).                *        
097900******************************************************************        
098000 2020-DEPT-BREAK.                                                         
098100                                                                          
098200      IF PS-REPORT-1                                                      
098300          MOVE PV-CURR-DEPT          TO TLD1-DEPT-NBR                     
098400      ELSE                                                                
098500          MOVE PV-CURR-DEPT          TO TLD1B-DEPT-NBR                    
098600      END-IF.                                                             
098700                                                                          
098800      ADD PV-ACCUM-TOT-ADJ-DEPT      TO PV-ACCUM-TOT-ADJ-LOCN             
098900      ADD PV-ACCUM-TOT-ADJ-DEPT-FSHN TO PV-ACCUM-TOT-ADJ-LOCN-FSHN        
099000      ADD PV-ACCUM-QTY-DEPT          TO PV-ACCUM-QTY-LOCN                 
099100      ADD PV-ACCUM-RET-EXTENS-DEPT   TO PV-ACCUM-RET-EXTENS-LOCN          
099200      ADD PV-ACCUM-QTY-DEPT-FSHN     TO PV-ACCUM-QTY-LOCN-FSHN            
099300      ADD PV-ACCUM-RET-EXT-DEPT-FSHN TO PV-ACCUM-RET-EXT-LOCN-FSHN        
099400                                                                          
099500      IF PS-REPORT-1                                                      
099600*FILL TOTAL-NUMBER-OF-ADJUSTMENTS.                                        
099700          MOVE PV-ACCUM-TOT-ADJ-DEPT    TO TLD1-TOT-NUM-OF-ADJ            
099800                                                                          
099900*FILL TOTAL-ADJ-QTY.                                                      
100000          MOVE PV-ACCUM-QTY-DEPT        TO TLD1-TOT-ADJ-QTY               
100100                                                                          
100200*FILL TOTAL-RETAIL-EXT.                                                   
100300          MOVE PV-ACCUM-RET-EXTENS-DEPT TO TLD1-RETAIL-EXT-TOT            
100400***************************************************************           
100500*FILL TOTAL-ADJUSTMENT SENT TO FASHION.                                   
100600          MOVE PV-ACCUM-TOT-ADJ-DEPT-FSHN  TO TLD2-TOT-NUM-OF-ADJ         
100700                                                                          
100800*FILL TOTAL-ADJ-QTY  FASHION                                              
100900          MOVE PV-ACCUM-QTY-DEPT-FSHN      TO TLD2-TOT-ADJ-QTY            
101000                                                                          
101100*FILL RETAIL-EXTENSION  FASHION.                                          
101200          MOVE PV-ACCUM-RET-EXT-DEPT-FSHN  TO TLD2-RETAIL-EXT-TOT         
101300                                                                          
101400***************************************************************           
101500*FILL TOTAL-ADJUSTMENT BYPASSED.                                          
101600          SUBTRACT                                                        
101700              PV-ACCUM-TOT-ADJ-DEPT-FSHN                                  
101800              FROM                                                        
101900              PV-ACCUM-TOT-ADJ-DEPT                                       
102000              GIVING                                                      
102100              TLD3-TOT-NUM-OF-ADJ                                         
102200*FILL TOTAL-ADJ-QTY  BYPASSED                                             
102300          SUBTRACT                                                        
102400              PV-ACCUM-QTY-DEPT-FSHN                                      
102500              FROM                                                        
102600              PV-ACCUM-QTY-DEPT                                           
102700              GIVING                                                      
102800              TLD3-TOT-ADJ-QTY                                            
102900*FILL TOTAL-QTY RETAIL EXT                                                
103000          SUBTRACT                                                        
103100              PV-ACCUM-RET-EXT-DEPT-FSHN                                  
103200              FROM                                                        
103300              PV-ACCUM-RET-EXTENS-DEPT                                    
103400              GIVING                                                      
103500              TLD3-RETAIL-EXT-TOT                                         
103600      ELSE                                                                
103700*FILL TOTAL-ADJUSTMENT SENT TO FASHION.                                   
103800          MOVE PV-ACCUM-TOT-ADJ-DEPT-FSHN TO TLD1B-TOT-NUM-OF-ADJ         
103900                                                                          
104000*FILL TOTAL-ADJ-QTY FASHION                                               
104100          MOVE PV-ACCUM-QTY-DEPT-FSHN   TO TLD1B-TOT-ADJ-QTY              
104200                                                                          
104300*FILL TOTAL-RETAIL-EXT FASHION                                            
104400          MOVE PV-ACCUM-RET-EXT-DEPT-FSHN TO TLD1B-RETAIL-EXT-TOT         
104500      END-IF.                                                             
104600                                                                          
104700                                                                          
104800*CHECK FOR OVERFLOW.                                                      
104900     ADD 4 TO PV-LINE-COUNT.                                              
105000     IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                                 
105100         SET PS-NEW-PAGE TO TRUE                                          
105200         PERFORM 1200-PRINT-HEADINGS                                      
105300     END-IF.                                                              
105400                                                                          
105500     IF PS-REPORT-1                                                       
105600*WRITE TOTAL LINE.                                                        
105700         WRITE OUTPUT-REC                                                 
105800           FROM TOTAL-LINE-DEPT-1A                                        
105900             AFTER ADVANCING 2 LINES                                      
106000                                                                          
106100*WRITE TOTAL LINE.                                                        
106200         WRITE OUTPUT-REC                                                 
106300           FROM TOTAL-LINE-DEPT-2A                                        
106400             AFTER ADVANCING 1 LINES                                      
106500                                                                          
106600*WRITE TOTAL LINE.                                                        
106700         WRITE OUTPUT-REC                                                 
106800           FROM TOTAL-LINE-DEPT-3A                                        
106900             AFTER ADVANCING 1 LINES                                      
107000                                                                          
107100     ELSE                                                                 
107200*WRITE TOTAL LINE.                                                        
107300         WRITE OUTPUT-REC                                                 
107400           FROM TOTAL-LINE-DEPT-1B                                        
107500             AFTER ADVANCING 2 LINES                                      
107600     END-IF.                                                              
107700                                                                          
107800*RE-INITIALIZE ACCUMULATORS.                                              
107900     MOVE ZERO   TO PV-ACCUM-TOT-ADJ-DEPT                                 
108000                    PV-ACCUM-TOT-ADJ-DEPT-FSHN                            
108100                    PV-ACCUM-QTY-DEPT                                     
108200                    PV-ACCUM-RET-EXTENS-DEPT                              
108300                    PV-ACCUM-QTY-DEPT-FSHN                                
108400                    PV-ACCUM-RET-EXT-DEPT-FSHN.                           
108500                                                                          
108600******************************************************************        
108700*  PRINT A REPORT SUB-TOTAL LINE (BY DC LOCATION).               *        
108800******************************************************************        
108900 2040-LOCN-BREAK.                                                         
109000                                                                          
109100     ADD PV-ACCUM-TOT-ADJ-LOCN      TO PV-ACCUM-TOT-ADJ-DATE.             
109200     ADD PV-ACCUM-TOT-ADJ-LOCN-FSHN TO PV-ACCUM-TOT-ADJ-DATE-FSHN.        
109300     ADD PV-ACCUM-QTY-LOCN          TO PV-ACCUM-QTY-DATE.                 
109400     ADD PV-ACCUM-RET-EXTENS-LOCN   TO PV-ACCUM-RET-EXTENS-DATE.          
109500     ADD PV-ACCUM-QTY-LOCN-FSHN     TO PV-ACCUM-QTY-DATE-FSHN.            
109600     ADD PV-ACCUM-RET-EXT-LOCN-FSHN TO PV-ACCUM-RET-EXT-DATE-FSHN.        
109700                                                                          
109800     IF PS-REPORT-1                                                       
109900*FILL TOTAL-NUMBER-OF-ADJUSTMENTS.                                        
110000         MOVE PV-ACCUM-TOT-ADJ-LOCN     TO TLDC1-TOT-NUM-OF-ADJ           
110100                                                                          
110200*FILL TOTAL-ADJ-QTY.                                                      
110300         MOVE PV-ACCUM-QTY-LOCN         TO TLDC1-TOT-ADJ-QTY              
110400                                                                          
110500*FILL TOTAL-RETAIL-EXT.                                                   
110600         MOVE PV-ACCUM-RET-EXTENS-LOCN  TO TLDC1-RETAIL-EXT-TOT           
110700                                                                          
110800*FILL TOTAL-ADJUSTMENT SENT TO FASHION.                                   
110900         MOVE PV-ACCUM-TOT-ADJ-LOCN-FSHN TO TLDC2-TOT-NUM-OF-ADJ          
111000                                                                          
111100*FILL TOTAL-ADJ-QTY  FASHION                                              
111200         MOVE PV-ACCUM-QTY-LOCN-FSHN     TO TLDC2-TOT-ADJ-QTY             
111300                                                                          
111400*FILL RETAIL-EXTENSION  FASHION.                                          
111500         MOVE PV-ACCUM-RET-EXT-LOCN-FSHN TO  TLDC2-RETAIL-EXT-TOT         
111600                                                                          
111700***************************************************************           
111800*FILL TOTAL-ADJUSTMENT BYPASSED.                                          
111900         SUBTRACT                                                         
112000              PV-ACCUM-TOT-ADJ-LOCN-FSHN                                  
112100              FROM                                                        
112200              PV-ACCUM-TOT-ADJ-LOCN                                       
112300              GIVING                                                      
112400              TLDC3-TOT-NUM-OF-ADJ                                        
112500*FILL TOTAL-ADJ-QTY  BYPASSED                                             
112600         SUBTRACT                                                         
112700              PV-ACCUM-QTY-LOCN-FSHN                                      
112800              FROM                                                        
112900              PV-ACCUM-QTY-LOCN                                           
113000              GIVING                                                      
113100              TLDC3-TOT-ADJ-QTY                                           
113200                                                                          
113300*FILL RETAIL-EXTENSION  BYPASSED.                                         
113400         SUBTRACT                                                         
113500              PV-ACCUM-RET-EXT-LOCN-FSHN                                  
113600              FROM                                                        
113700              PV-ACCUM-RET-EXTENS-LOCN                                    
113800              GIVING                                                      
113900              TLDC3-RETAIL-EXT-TOT                                        
114000                                                                          
114100     ELSE                                                                 
114200*FILL TOTAL-ADJUSTMENT SENT TO FASHION.                                   
114300         MOVE PV-ACCUM-TOT-ADJ-LOCN-FSHN TO TLDC1B-TOT-NUM-OF-ADJ         
114400                                                                          
114500*FILL TOTAL-ADJ-QTY  FASHION                                              
114600         MOVE PV-ACCUM-QTY-LOCN-FSHN     TO TLDC1B-TOT-ADJ-QTY            
114700                                                                          
114800*FILL RETAIL-EXTENSION  FASHION.                                          
114900         MOVE PV-ACCUM-RET-EXT-LOCN-FSHN TO  TLDC1B-RETAIL-EXT-TOT        
115000     END-IF.                                                              
115100                                                                          
115200*CHECK FOR OVERFLOW.                                                      
115300     ADD 4 TO PV-LINE-COUNT.                                              
115400     IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                                 
115500         SET PS-NEW-PAGE TO TRUE                                          
115600         PERFORM 1200-PRINT-HEADINGS                                      
115700     END-IF.                                                              
115800                                                                          
115900     IF PS-REPORT-1                                                       
116000*WRITE TOTAL LINE.                                                        
116100         WRITE OUTPUT-REC                                                 
116200           FROM TOTAL-LINE-DC-1A                                          
116300             AFTER ADVANCING 2 LINES                                      
116400                                                                          
116500*WRITE TOTAL LINE.                                                        
116600         WRITE OUTPUT-REC                                                 
116700           FROM TOTAL-LINE-DC-2A                                          
116800             AFTER ADVANCING 1 LINES                                      
116900                                                                          
117000*WRITE TOTAL LINE.                                                        
117100         WRITE OUTPUT-REC                                                 
117200           FROM TOTAL-LINE-DC-3A                                          
117300             AFTER ADVANCING 1 LINES                                      
117400                                                                          
117500     ELSE                                                                 
117600*WRITE TOTAL LINE.                                                        
117700         WRITE OUTPUT-REC                                                 
117800           FROM TOTAL-LINE-DC-1B                                          
117900             AFTER ADVANCING 2 LINES                                      
118000     END-IF.                                                              
118100                                                                          
118200*RE-INITIALIZE ACCUMULATORS.                                              
118300     MOVE ZERO   TO PV-ACCUM-TOT-ADJ-LOCN                                 
118400                    PV-ACCUM-TOT-ADJ-LOCN-FSHN                            
118500                    PV-ACCUM-QTY-LOCN                                     
118600                    PV-ACCUM-RET-EXTENS-LOCN                              
118700                    PV-ACCUM-QTY-LOCN-FSHN                                
118800                    PV-ACCUM-RET-EXT-LOCN-FSHN.                           
118900                                                                          
119000******************************************************************        
119100*  PRINT A REPORT SUB-TOTAL LINE (BY CREATE DATE).               *        
119200******************************************************************        
119300 2050-DATE-BREAK.                                                         
119400                                                                          
119500     IF PS-REPORT-1                                                       
119600**** PV-CURR-DATE CONTAINS BATCH PROCESSING DATE                          
119700**** PV-CURRENT-DATE CONTAINS TODAY'S  DATE WHICH WE WANT                 
119800         MOVE PV-CURRENT-DATE          TO TLSUM1-DATE                     
119900                                                                          
120000*FILL TOTAL-NUMBER-OF-ADJUSTMENTS.                                        
120100         MOVE PV-ACCUM-TOT-ADJ-DATE    TO TLSUM1-TOT-NUM-OF-ADJ           
120200*FILL TOTAL-ADJ-QTY.                                                      
120300         MOVE PV-ACCUM-QTY-DATE        TO TLSUM1-TOT-ADJ-QTY              
120400                                                                          
120500*FILL TOTAL-RETAIL-EXT.                                                   
120600         MOVE PV-ACCUM-RET-EXTENS-DATE TO TLSUM1-RETAIL-EXT-TOT           
120700                                                                          
120800*FILL TOTAL-ADJUSTMENT SENT TO FASHION.                                   
120900         MOVE PV-ACCUM-TOT-ADJ-DATE-FSHN TO TLSUM2-TOT-NUM-OF-ADJ         
121000*FILL TOTAL-ADJ-QTY  FASHION                                              
121100         MOVE PV-ACCUM-QTY-DATE-FSHN      TO TLSUM2-TOT-ADJ-QTY           
121200                                                                          
121300*FILL RETAIL-EXTENSION  FASHION.                                          
121400         MOVE PV-ACCUM-RET-EXT-DATE-FSHN TO TLSUM2-RETAIL-EXT-TOT         
121500                                                                          
121600*FILL TOTAL-ADJUSTMENT BYPASSED.                                          
121700         SUBTRACT                                                         
121800              PV-ACCUM-TOT-ADJ-DATE-FSHN                                  
121900              FROM                                                        
122000              PV-ACCUM-TOT-ADJ-DATE                                       
122100              GIVING                                                      
122200              TLSUM3-TOT-NUM-OF-ADJ                                       
122300*FILL TOTAL-ADJ-QTY  BYPASSED                                             
122400         SUBTRACT                                                         
122500              PV-ACCUM-QTY-DATE-FSHN                                      
122600              FROM                                                        
122700              PV-ACCUM-QTY-DATE                                           
122800              GIVING                                                      
122900              TLSUM3-TOT-ADJ-QTY                                          
123000                                                                          
123100*FILL RETAIL-EXTENSION  BYPASSED.                                         
123200         SUBTRACT                                                         
123300              PV-ACCUM-RET-EXT-DATE-FSHN                                  
123400              FROM                                                        
123500              PV-ACCUM-RET-EXTENS-DATE                                    
123600              GIVING                                                      
123700              TLSUM3-RETAIL-EXT-TOT                                       
123800     ELSE                                                                 
123900**** PV-CURR-DATE CONTAINS BATCH PROCESSING DATE                          
124000**** PV-CURRENT-DATE CONTAINS TODAY'S  DATE WHICH WE WANT                 
124100         MOVE PV-CURRENT-DATE          TO TLSUM1B-DATE                    
124200                                                                          
124300*FILL TOTAL-ADJUSTMENT SENT TO FASHION.                                   
124400         MOVE PV-ACCUM-TOT-ADJ-DATE-FSHN TO TLSUM1B-TOT-NUM-OF-ADJ        
124500*FILL TOTAL-ADJ-QTY  FASHION                                              
124600         MOVE PV-ACCUM-QTY-DATE-FSHN      TO TLSUM1B-TOT-ADJ-QTY          
124700                                                                          
124800*FILL RETAIL-EXTENSION  FASHION.                                          
124900         MOVE PV-ACCUM-RET-EXT-DATE-FSHN TO TLSUM1B-RETAIL-EXT-TOT        
125000     END-IF.                                                              
125100                                                                          
125200                                                                          
125300*CHECK FOR OVERFLOW.                                                      
125400     ADD 4 TO PV-LINE-COUNT.                                              
125500     IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                                 
125600         SET PS-NEW-PAGE TO TRUE                                          
125700         PERFORM 1200-PRINT-HEADINGS                                      
125800     END-IF.                                                              
125900                                                                          
126000     IF PS-REPORT-1                                                       
126100*WRITE TOTAL LINE.                                                        
126200         WRITE OUTPUT-REC                                                 
126300           FROM TOTAL-LINE-SUM-1A                                         
126400             AFTER ADVANCING 2 LINES                                      
126500                                                                          
126600*WRITE TOTAL LINE.                                                        
126700         WRITE OUTPUT-REC                                                 
126800           FROM TOTAL-LINE-SUM-2A                                         
126900             AFTER ADVANCING 1 LINES                                      
127000                                                                          
127100*WRITE TOTAL LINE.                                                        
127200         WRITE OUTPUT-REC                                                 
127300           FROM TOTAL-LINE-SUM-3A                                         
127400             AFTER ADVANCING 1 LINES                                      
127500                                                                          
127600     ELSE                                                                 
127700*WRITE TOTAL LINE.                                                        
127800         WRITE OUTPUT-REC                                                 
127900           FROM TOTAL-LINE-SUM-1B                                         
128000             AFTER ADVANCING 2 LINES                                      
128100     END-IF.                                                              
128200                                                                          
128300*RE-INITIALIZE ACCUMULATORS.                                              
128400     MOVE ZERO   TO PV-ACCUM-TOT-ADJ-DATE                                 
128500                    PV-ACCUM-TOT-ADJ-DATE-FSHN                            
128600                    PV-ACCUM-QTY-DATE                                     
128700                    PV-ACCUM-RET-EXTENS-DATE                              
128800                    PV-ACCUM-QTY-DATE-FSHN                                
128900                    PV-ACCUM-RET-EXT-DATE-FSHN.                           
129000                                                                          
129100******************************************************************        
129200*  FILL AND DETAIL REPORT LINE.                                  *        
129300******************************************************************        
129400 2060-PRINT-DETAIL.                                                       
129500                                                                          
129600* CRAIG KEHOE 02/2008                                                     
129700*    NOTE THAT I INSERTED 'THEN'S INTO THE IF/ELSE STATEMENTS             
129800*    BELOW FOR READABILITY.                                               
129900     MOVE SPACES TO DL1-DESCRIPTION.                                      
130000                                                                          
130100     IF      PS-REPORT-1                                                  
130200         AND WS043-ADJ-REASON-CDE = SPACES                                
130300     THEN                                                                 
130400         PERFORM 2610-GET-PIX-DESC                                        
130500     ELSE                                                                 
130600         PERFORM 2600-GET-RSN-DESC                                        
130700     END-IF.                                                              
130800*    FILL DL1-SYSTEM-CDE AND/OR SET THE ACCUMULATORS.                     
130900     IF     PS-FASHION-ADJ                                                
131000        AND WS043-FSHN-ADAPTER-CDE > ZERO                                 
131100     THEN                                                                 
131200         MOVE ' YES '          TO DL1-SYSTEM-CDE                          
131300         ADD 1                 TO PV-ACCUM-TOT-ADJ-DEPT-FSHN              
131400         ADD WS043-ADJ-UNIT-QTY                                           
131500                               TO PV-ACCUM-QTY-DEPT-FSHN                  
131600         ADD WS043-UNIT-PRICE-EXT                                         
131700                               TO PV-ACCUM-RET-EXT-DEPT-FSHN              
131800     ELSE                                                                 
131900         MOVE SPACES           TO DL1-SYSTEM-CDE                          
132000     END-IF.                                                              
132100                                                                          
132200     IF    PS-REPORT-1                                                    
132300        OR (    PS-REPORT-2                                               
132400            AND PS-FASHION-ADJ                                            
132500            AND WS043-FSHN-ADAPTER-CDE > ZERO)                            
132600     THEN                                                                 
132700         MOVE WS043-DEPT-NBR        TO PV-FORMAT-SKU-DEPT                 
132800         MOVE WS043-CLASS-NBR       TO PV-FORMAT-SKU-CLASS                
132900         MOVE WS043-SUB-CLASS-NBR   TO PV-FORMAT-SKU-SUB-CL               
133000         MOVE PV-FORMAT-DEPT-CLASS  TO DL1-DEPT-CLASS                     
133100         MOVE WS043-ITEM-NBR        TO DL1-SKU-NUMBER                     
133200         MOVE WS043-ADJ-UNIT-QTY    TO DL1-ADJ-QTY                        
133300         MOVE WS043-UNIT-PRICE      TO DL1-UNIT-RETAIL                    
133400         MOVE WS043-UNIT-PRICE-EXT  TO DL1-RETAIL-EXT                     
133500         MOVE WS043-BTCH-NBR        TO DL1-BATCH-NBR                      
133600         MOVE WS043-ADJ-REASON-CDE  TO DL1-REASON-CDE                     
133700         ADD 1                      TO PV-ACCUM-TOT-ADJ-DEPT              
133800         ADD WS043-ADJ-UNIT-QTY     TO PV-ACCUM-QTY-DEPT                  
133900         ADD WS043-UNIT-PRICE-EXT   TO PV-ACCUM-RET-EXTENS-DEPT           
134000*        CHECK FOR OVERFLOW.                                              
134100         ADD 1 TO PV-LINE-COUNT                                           
134200         IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                             
134300             PERFORM 1200-PRINT-HEADINGS                                  
134400         END-IF                                                           
134500         WRITE OUTPUT-REC                                                 
134600           FROM DETAIL-LINE-1                                             
134700             AFTER ADVANCING 1 LINES                                      
134800     END-IF.                                                              
134900* END CRAIG KEHOE 02/2008                                                 
135000                                                                          
135100******************************************************************        
135200*  READ NEXT INPUT FILE RECORD.                                  *        
135300******************************************************************        
135400 2100-READ-EXTRACT.                                                       
135500     READ INPUT-EXTRACT-FILE INTO WS043-INV-ADJ-UPLOAD-RECORD             
135600        AT END                                                            
135700           SET PS-END-OF-INPUT TO TRUE.                                   
135800                                                                          
135900******************************************************************        
136000*  READ CONTROL CARD TO DETERMINE WHICH REPORT TO CREATE         *        
136100******************************************************************        
136200 2200-READ-CONTROL-CARD.                                                  
136300     READ CONTROL-CARD INTO CC-CONTROL-CARD-LAYOUT                        
136400        AT END                                                            
136500           SET PS-END-OF-CONTROL TO TRUE.                                 
136600                                                                          
136700******************************************************************        
136800*  OPEN THE VALID LOCATION CSR                                   *        
136900******************************************************************        
137000 2500-OPEN-VALID-LOC-CSR.                                                 
137100     EXEC SQL                                                             
137200        OPEN VALID-LOC-CSR                                                
137300     END-EXEC.                                                            
137400                                                                          
137500     EVALUATE TRUE                                                        
137600         WHEN SQLCODE = ZERO                                              
137700             CONTINUE                                                     
137800         WHEN OTHER                                                       
137900             MOVE '2500-OPEN-VALID-LOC-CSR'                               
138000                                      TO PV-ABEND-PARAGRAPH               
138100             MOVE 'OPEN'              TO PV-ABEND-OPERATION               
138200             PERFORM Z999-SQL-ERROR                                       
138300     END-EVALUATE.                                                        
138400                                                                          
138500******************************************************************        
138600* FETCH THE VALID LOCATION CSR                                   *        
138700******************************************************************        
138800 2520-GET-VALID-LOC.                                                      
138900                                                                          
139000     INITIALIZE DCLXLT00010.                                              
139100                                                                          
139200     EXEC SQL                                                             
139300         FETCH VALID-LOC-CSR                                              
139400          INTO :XLT00010-LOC-NBR                                          
139500             , :XLT00010-LOC-NM                                           
139600     END-EXEC.                                                            
139700                                                                          
139800     EVALUATE TRUE                                                        
139900       WHEN SQLCODE = ZERO                                                
140000           MOVE XLT00010-LOC-NBR    TO PV-STORE-NMBR                      
140100           MOVE XLT00010-LOC-NM     TO PV-DC-STORE-NAME                   
140200*CRAIG ADDED THESE LINES FOR THE SAKE OF SANITY 02/2008                   
140300           MOVE ZERO TO XLT00010-LOC-NBR                                  
140400           MOVE SPACES TO XLT00010-LOC-NM                                 
140500*END SANITY 02/2008 (OR "BEGIN INSANITY"--MAKE YOUR CHOICE)               
140600           PERFORM 2530-EMPTY-FILE-PROCESS                                
140700                                                                          
140800       WHEN SQLCODE = 100                                                 
140900           SET PS-END-LOC-CSR TO TRUE                                     
141000                                                                          
141100       WHEN SQLWARN0 NOT = SPACES                                         
141200       WHEN SQLCODE < ZERO                                                
141300       WHEN SQLCODE > ZERO                                                
141400           MOVE '2520-GET-VALID-LOC' TO PV-ABEND-PARAGRAPH                
141500           MOVE 'SELECT LOCATION FROM XLT_LOC'                            
141600                                     TO PV-ABEND-OPERATION                
141700           PERFORM Z999-SQL-ERROR                                         
141800     END-EVALUATE.                                                        
141900                                                                          
142000******************************************************************        
142100* PROCESSING WHEN THERE IS AN EMPTY INPUT FILE TO CREATE         *        
142200*  THE CORRECT PAGES FOR RDS SO USER RECEIVES REPORT WITH        *        
142300*  COMMENT NO INVENTORY ITEMS TO PROCESS.                        *        
142400******************************************************************        
142500 2530-EMPTY-FILE-PROCESS.                                                 
142600                                                                          
142700     PERFORM 1205-PRINT-HEADING-LINES                                     
142800     PERFORM 1300-NO-DETAIL-LINE                                          
142900     PERFORM 1350-WRITE-BLANK-LINE.                                       
143000                                                                          
143100     PERFORM 2020-DEPT-BREAK                                              
143200     PERFORM 2040-LOCN-BREAK                                              
143300     PERFORM 2050-DATE-BREAK                                              
143400     PERFORM 1400-END-OF-RPT-LINE.                                        
143500                                                                          
143600******************************************************************        
143700* CLOSE THE VALID LOCATION CSR                                   *        
143800******************************************************************        
143900 2550-CLOSE-VALID-LOC-CSR.                                                
144000                                                                          
144100     EXEC SQL                                                             
144200         CLOSE VALID-LOC-CSR                                              
144300     END-EXEC.                                                            
144400                                                                          
144500     EVALUATE TRUE                                                        
144600       WHEN SQLCODE = ZERO                                                
144700           CONTINUE                                                       
144800                                                                          
144900       WHEN OTHER                                                         
145000           MOVE '2550-CLOSE-VALID-LOC-CSR' TO PV-ABEND-PARAGRAPH          
145100           MOVE 'CLOSE'                    TO PV-ABEND-OPERATION          
145200           PERFORM Z999-SQL-ERROR                                         
145300     END-EVALUATE.                                                        
145400                                                                          
145500******************************************************************        
145600* GET THE PIX REASON CODE & FSHN IND, LOAD DL1-DESCRIPTION                
145700******************************************************************        
145800* CRAIG KEHOE 02/2008 CHANGED THIS PARAGRAPH'S NAME TO                    
145900 2600-GET-RSN-DESC.                                                       
146000* END CRAIG KEHOE 02/2008                                                 
146100                                                                          
146200     EXEC SQL                                                             
146300         SELECT RSN_DESC                                                  
146400              , FSHN_SYS_IND                                              
146500          INTO :WSV00001-RSN-DESC                                         
146600             , :WSV00001-FSHN-SYS-IND                                     
146700          FROM  WSV_PIX_RSN_CDE                                           
146800         WHERE  RSN_CDE    = :WS043-ADJ-REASON-CDE                        
146900     END-EXEC.                                                            
147000                                                                          
147100     EVALUATE TRUE                                                        
147200       WHEN SQLCODE = ZERO                                                
147300       WHEN SQLCODE = -811                                                
147400           MOVE WSV00001-RSN-DESC-TEXT     TO DL1-DESCRIPTION             
147500* CRAIG KEHOE 02/2008                                                     
147600           MOVE WSV00001-FSHN-SYS-IND-TEXT(1:1)                           
147700                                           TO PS-FASHION-ADJ-SW           
147800           MOVE SPACES TO WSV00001-RSN-DESC-TEXT                          
147900                          WSV00001-FSHN-SYS-IND-TEXT                      
148000* END CRAIG KEHOE 02/2008                                                 
148100                                                                          
148200       WHEN SQLCODE = 100                                                 
148300           PERFORM 2610-GET-PIX-DESC                                      
148400                                                                          
148500       WHEN SQLWARN0 NOT = SPACES                                         
148600       WHEN SQLCODE < ZERO                                                
148700       WHEN SQLCODE > ZERO                                                
148800           MOVE '2600-GET-RSN-CDE'   TO PV-ABEND-PARAGRAPH                
148900           MOVE 'SELECT DESCRIPTION FROM WSV_PIX_RSN_CDE'                 
149000                                     TO PV-ABEND-OPERATION                
149100           PERFORM Z999-SQL-ERROR                                         
149200     END-EVALUATE.                                                        
149300                                                                          
149400******************************************************************        
149500* GET THE PIX REASON CODE & FSHN IND, LOAD DL1-DESCRIPTION                
149600******************************************************************        
149700 2610-GET-PIX-DESC.                                                       
149800                                                                          
149900     MOVE WS043-TRAN-CDE(1:2)   TO PV-TRAN-CDE.                           
150000* CRAIG KEHOE 02/2008                                                     
150100     SET PS-NOT-FASHION-ADJ TO TRUE.                                      
150200     EXEC SQL                                                             
150300         SELECT DISTINCT PIX_DESC                                         
150400          INTO :WSV00002-PIX-DESC                                         
150500          FROM  WSV_PIX_RTE                                               
150600         WHERE  PIX_TYP         = :WS043-TRAN-TYP                         
150700           AND  PIX_CDE         = :PV-TRAN-CDE                            
150800           AND  (PIX_ACTN_CDE   = :WS043-ACTN-CDE                         
150900                 OR (PIX_ACTN_CDE IS NULL AND                             
151000                       :WS043-ACTN-CDE = '  '))                           
151100     END-EXEC.                                                            
151200* END CRAIG KEHOE 02/2008                                                 
151300                                                                          
151400     EVALUATE TRUE                                                        
151500       WHEN SQLCODE = ZERO                                                
151600       WHEN SQLCODE = -811                                                
151700* CRAIG KEHOE 02/2008                                                     
151800           MOVE WSV00002-PIX-DESC-TEXT    TO DL1-DESCRIPTION              
151900           MOVE SPACES TO WSV00002-PIX-DESC-TEXT                          
152000* END CRAIG KEHOE 02/2008                                                 
152100       WHEN SQLCODE = 100                                                 
152200           MOVE 'UNKNOWN ADJUSTMENT CODE' TO DL1-DESCRIPTION              
152300                                                                          
152400       WHEN SQLWARN0 NOT = SPACES                                         
152500       WHEN SQLCODE < ZERO                                                
152600       WHEN SQLCODE > ZERO                                                
152700           MOVE '2610-GET-PIX-DESC'  TO PV-ABEND-PARAGRAPH                
152800           MOVE 'SELECT DESCRIPTION FROM WSV_PIX_RTE'                     
152900                                     TO PV-ABEND-OPERATION                
153000           PERFORM Z999-SQL-ERROR                                         
153100     END-EVALUATE.                                                        
153200                                                                          
153300******************************************************************        
153400*     ERROR PROCESSING                                      **            
153500******************************************************************        
153600 Z998-ERROR.                                                              
153700                                                                          
153800     DISPLAY '** ABEND **'.                                               
153900     DISPLAY '** PROGRAM        ** = WSKRP005'.                           
154000     DISPLAY '*** PARAGRAPH = ' PV-ABEND-PARAGRAPH.                       
154100     DISPLAY '*** OPERATION = ' PV-ABEND-OPERATION.                       
154200                                                                          
154300     MOVE 4003 TO PV-ABEND-CODE.                                          
154400     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
154500                                                                          
154600******************************************************************        
154700******************************************************************        
154800*     SQL ERROR PROCESSING                                      **        
154900******************************************************************        
155000 Z999-SQL-ERROR.                                                          
155100                                                                          
155200     MOVE SQLCODE    TO SQLCODE2.                                         
155300     MOVE SQLERRD(3) TO SQLERRD3.                                         
155400     DISPLAY '** ABEND **       ** = SQLERROR'.                           
155500     DISPLAY '** PROGRAM        ** = WSKRP005'.                           
155600     DISPLAY '*** PARAGRAPH = ' PV-ABEND-PARAGRAPH.                       
155700     DISPLAY '*** OPERATION = ' PV-ABEND-OPERATION.                       
155800     DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
155900     DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
156000     DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
156100                                                                          
156200     COPY DPPD004.                                                        
156300                                                                          
156400     MOVE 4003 TO PV-ABEND-CODE.                                          
156500     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
156600                                                                          
156700******************************************************************        
