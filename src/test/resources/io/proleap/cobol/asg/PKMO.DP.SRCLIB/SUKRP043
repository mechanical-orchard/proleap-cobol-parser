000100 IDENTIFICATION DIVISION.                                                 
000200 TITLE 'INVENTORY STORES TRAILER SUSPENSE REPORT BY DC'.                  
000300 PROGRAM-ID.             SUKRP043.                                        
000400 AUTHOR.                 DENNIS STEFFEN.                                  
000500 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000600 DATE-WRITTEN            SEPTEMBER 2005.                                  
000700 DATE-COMPILED.                                                           
000800*****************************************************************         
000900*  THIS PROGRAM WILL CREATE THE DAILY TRAILER SUSPENSE REPORT - *         
001000*  SHIPPED BUT NOT OPENED AT STORE REPORT.                      *         
001100*****************************************************************         
001200 ENVIRONMENT DIVISION.                                                    
001300 CONFIGURATION SECTION.                                                   
001400 SOURCE-COMPUTER.        IBM-3090.                                        
001500 OBJECT-COMPUTER.        IBM-3090.                                        
001600                                                                          
001700 INPUT-OUTPUT SECTION.                                                    
001800 FILE-CONTROL.                                                            
001900                                                                          
002000     SELECT INPUT-FILE                                                    
002100         ASSIGN TO INP01.                                                 
002200                                                                          
002300     SELECT REPORT-FILE                                                   
002400         ASSIGN TO RPT01.                                                 
002500                                                                          
002600 DATA DIVISION.                                                           
002700 FILE SECTION.                                                            
002800                                                                          
002900 FD  INPUT-FILE                                                           
003000     RECORDING MODE IS F                                                  
003100     LABEL RECORDS ARE STANDARD                                           
003200     BLOCK CONTAINS 0 RECORDS                                             
003300     RECORD CONTAINS 106 CHARACTERS.                                      
003400 01  INPUT-RECORD                    PIC  X(106).                         
003500                                                                          
003600 FD  REPORT-FILE                                                          
003700     RECORDING MODE IS F                                                  
003800     BLOCK CONTAINS 0  RECORDS.                                           
003900 01  REPORT-RECORD                   PIC  X(132).                         
004000                                                                          
004100 WORKING-STORAGE SECTION.                                                 
004200                                                                          
004300 01  PROGRAM-SWITCHES.                                                    
004400     05  PS-END-OF-INPUT-FILE-SW     PIC X(01) VALUE 'N'.                 
004500         88  PS-END-OF-INPUT                   VALUE 'Y'.                 
004600         88  PS-NOT-END-OF-INPUT               VALUE 'N'.                 
004700                                                                          
004800 01  PV-CURR-KEY.                                                         
004900     05  PV-CURR-OPER                 PIC 9(04).                          
005000                                                                          
005100 01  PV-PREV-KEY.                                                         
005200     05  PV-PREV-OPER                 PIC 9(04).                          
005300                                                                          
005400 01  PV-CURR-KEY-2.                                                       
005500     05  PV-CURR-PLND-INV-DTE         PIC X(10).                          
005600 01  PV-PREV-KEY-2.                                                       
005700     05  PV-PREV-PLND-INV-DTE         PIC X(10).                          
005800                                                                          
005900 01  PV-PROGRAM-CONSTANTS.                                                
006000     05  PC-SUKRP043                 PIC X(08) VALUE  'SUKRP043'.         
006100     05  PC-REPORT-NBR               PIC X(02) VALUE '-1'.                
006200                                                                          
006300 01  PV-PROGRAM-VARIABLES.                                                
006400     05  PV-PARAGRAPH-NAME           PIC X(30) VALUE SPACE.               
006500     05  PV-PAGE-COUNT               PIC S9(4) VALUE ZERO                 
006600                                                   COMP-3.                
006700     05  PV-LINE-COUNT               PIC S9(3) VALUE ZERO                 
006800                                                   COMP-3.                
006900     05  PV-MAX-LINE-COUNT           PIC S9(3) VALUE +60                  
007000                                                   COMP-3.                
007100     05  PV-READ-COUNT               PIC S9(9) VALUE ZERO                 
007200                                                   COMP-3.                
007300     05  PV-DETAIL-COUNT             PIC S9(9) VALUE ZERO                 
007400                                                   COMP-3.                
007500     05  PV-DISP-NBR                 PIC ZZZ,ZZZ,ZZ9.                     
007600     05  PV-OPER-UNT-ID              PIC S9(04) VALUE ZERO                
007700                                                   COMP.                  
007800     05  PV-STR-NBR                  PIC S9(04) VALUE ZERO                
007900                                                   COMP.                  
008000     05  PV-TODAYS-DATE.                                                  
008100         10  PV-TODAYS-DATE-YY       PIC 9(02) VALUE ZERO.                
008200         10  PV-TODAYS-DATE-MM       PIC 9(02) VALUE ZERO.                
008300         10  PV-TODAYS-DATE-DD       PIC 9(02) VALUE ZERO.                
008400     05  PV-TODAYS-TIME.                                                  
008500         10  PV-TODAYS-TIME-HH       PIC 9(02) VALUE ZERO.                
008600         10  PV-TODAYS-TIME-MM       PIC 9(02) VALUE ZERO.                
008700         10  PV-TODAYS-TIME-SS       PIC 9(02) VALUE ZERO.                
008800         10  FILLER                  PIC 9(02) VALUE ZERO.                
008900     05  PV-COMPARE-DATE.                                                 
009000         10  PV-COMPARE-DATE-CC      PIC 9(02) VALUE ZERO.                
009100         10  PV-COMPARE-DATE-YY      PIC 9(02) VALUE ZERO.                
009200         10  FILLER                  PIC X(01) VALUE '-'.                 
009300         10  PV-COMPARE-DATE-MM      PIC 9(02) VALUE ZERO.                
009400         10  FILLER                  PIC X(01) VALUE '-'.                 
009500         10  PV-COMPARE-DATE-DD      PIC 9(02) VALUE ZERO.                
009600     05  PV-PLAN-INV-DTE.                                                 
009700         10  PV-PLAN-INV-DTE-CCYY    PIC 9(04) VALUE ZERO.                
009800         10  FILLER                  PIC X(01) VALUE '-'.                 
009900         10  PV-PLAN-INV-DTE-MM      PIC 9(02) VALUE ZERO.                
010000         10  FILLER                  PIC X(01) VALUE '-'.                 
010100         10  PV-PLAN-INV-DTE-DD      PIC 9(02) VALUE ZERO.                
010200                                                                          
010300 01  PV-DATE-TIME.                                                        
010400     05  PV-DATE-TIME-CCYY            PIC X(04) VALUE SPACE.              
010500     05  FILLER                       PIC X(01) VALUE SPACE.              
010600     05  PV-DATE-TIME-MM              PIC X(02) VALUE SPACE.              
010700     05  FILLER                       PIC X(01) VALUE SPACE.              
010800     05  PV-DATE-TIME-DD              PIC X(02) VALUE SPACE.              
010900     05  FILLER                       PIC X(01) VALUE SPACE.              
011000     05  PV-DATE-TIME-HH              PIC X(02) VALUE SPACE.              
011100     05  FILLER                       PIC X(01) VALUE SPACE.              
011200     05  PV-DATE-TIME-MN              PIC X(02) VALUE SPACE.              
011300     05  FILLER                       PIC X(01) VALUE SPACE.              
011400     05  PV-DATE-TIME-SS              PIC X(02) VALUE SPACE.              
011500     05  FILLER                       PIC X(07) VALUE SPACE.              
011600                                                                          
011700 01  PV-DISPLAY-DATE.                                                     
011800     05  PV-DISPLAY-MM                PIC X(02) VALUE SPACE.              
011900     05  FILLER                       PIC X(01) VALUE '/'.                
012000     05  PV-DISPLAY-DD                PIC X(02) VALUE SPACE.              
012100     05  FILLER                       PIC X(01) VALUE '/'.                
012200     05  PV-DISPLAY-CCYY              PIC X(04) VALUE SPACE.              
012300                                                                          
012400 01  PV-DISPLAY-TIME.                                                     
012500     05  PV-DISPLAY-HH                PIC X(02) VALUE SPACE.              
012600     05  FILLER                       PIC X(01) VALUE ':'.                
012700     05  PV-DISPLAY-MN                PIC X(02) VALUE SPACE.              
012800                                                                          
012900 01  DK-DB2-KEY-FIELDS.                                                   
013000     05  DK-OPER-UNT-ID              PIC S9(04) COMP VALUE ZEROES.        
013100                                                                          
013200 01  ABEND-CODE                      PIC S9(4)  VALUE +0                  
013300                                                 COMP SYNC.               
013400                                                                          
013500******************************************************************        
013600* COPY BOOK FOR TRAILER SUSPENSE - SHIPPED BUT NOT OPENED AT STORE        
013700******************************************************************        
013800     COPY SURD055.                                                        
013900                                                                          
014000******************************************************************        
014100*  DB2 FORMATTED MESSAGE AREA                                             
014200******************************************************************        
014300     COPY DPWS004.                                                        
014400*                                                                         
014500******************************************************************        
014600*  WORK FIELDS FOR PRINTING A 132 CHARACTER REPORT.                       
014700******************************************************************        
014800     COPY DPWS132O.                                                       
014900/                                                                         
015000 01  HEADING-LINES.                                                       
015100     05  HEADING-LINE-1.                                                  
015200         10  FILLER                  PIC  X(54)    VALUE SPACES.          
015300         10  FILLER                  PIC  X(23)    VALUE                  
015400             'TRAILER SUSPENSE REPORT'.                                   
015500         10  FILLER                  PIC  X(55)    VALUE SPACES.          
015600                                                                          
015700     05  HEADING-LINE-2.                                                  
015800         10  FILLER                  PIC  X(50)    VALUE SPACES.          
015900         10  FILLER                  PIC  X(37)    VALUE                  
016000             'INVENTORY STORES WITH TRAILERS'.                            
016100         10  FILLER                  PIC  X(48)    VALUE SPACES.          
016200                                                                          
016300     05  HEADING-LINE-3.                                                  
016400         10  FILLER                  PIC  X(48)    VALUE SPACES.          
016500         10  FILLER                  PIC  X(37)    VALUE                  
016600             'SHIPPED OR OPENED BUT NOT UNLOADED'.                        
016700         10  FILLER                  PIC  X(48)    VALUE SPACES.          
016800                                                                          
016900     05  HEADING-LINE-4.                                                  
017000         10  FILLER                  PIC  X(71)    VALUE SPACES.          
017100         10  FILLER                  PIC  X(07)    VALUE                  
017200             'TRAILER'.                                                   
017300         10  FILLER                  PIC  X(05)    VALUE SPACES.          
017400         10  FILLER                  PIC  X(07)    VALUE                  
017500             'TRAILER'.                                                   
017600         10  FILLER                  PIC  X(08)    VALUE SPACES.          
017700         10  FILLER                  PIC  X(14)    VALUE                  
017800             'TRAILER STATUS'.                                            
017900         10  FILLER                  PIC  X(06)    VALUE SPACES.          
018000         10  FILLER                  PIC  X(07)    VALUE                  
018100             'PLANNED'.                                                   
018200         10  FILLER                  PIC  X(09)    VALUE SPACES.          
018300                                                                          
018400     05  HEADING-LINE-5.                                                  
018500         10  FILLER                  PIC  X(02)    VALUE SPACES.          
018600         10  FILLER                  PIC  X(05)    VALUE                  
018700             'STORE'.                                                     
018800         10  FILLER                  PIC  X(04)    VALUE SPACES.          
018900         10  FILLER                  PIC  X(02)    VALUE                  
019000             'DC'.                                                        
019100         10  FILLER                  PIC  X(13)    VALUE SPACES.          
019200         10  FILLER                  PIC  X(10)    VALUE                  
019300             'STORE NAME'.                                                
019400         10  FILLER                  PIC  X(12)    VALUE SPACES.          
019500         10  FILLER                  PIC  X(04)    VALUE                  
019600             'SCAC'.                                                      
019700         10  FILLER                  PIC  X(05)    VALUE SPACES.          
019800         10  FILLER                  PIC  X(10)    VALUE                  
019900             'TRAILER ID'.                                                
020000         10  FILLER                  PIC  X(04)    VALUE SPACES.          
020100         10  FILLER                  PIC  X(06)    VALUE                  
020200             'SERIAL'.                                                    
020300         10  FILLER                  PIC  X(06)    VALUE SPACES.          
020400         10  FILLER                  PIC  X(08)    VALUE                  
020500             '# STATUS'.                                                  
020600         10  FILLER                  PIC  X(09)    VALUE SPACES.          
020700         10  FILLER                  PIC  X(09)    VALUE                  
020800             'DATE/TIME'.                                                 
020900         10  FILLER                  PIC  X(09)    VALUE SPACES.          
021000         10  FILLER                  PIC  X(08)    VALUE                  
021100             'INV DATE'.                                                  
021200         10  FILLER                  PIC  X(08)    VALUE SPACES.          
021300                                                                          
021400     05  HEADING-LINE-6.                                                  
021500         10  FILLER                  PIC  X(02)    VALUE SPACES.          
021600         10  FILLER                  PIC  X(05)    VALUE                  
021700             '-----'.                                                     
021800         10  FILLER                  PIC  X(03)    VALUE SPACES.          
021900         10  FILLER                  PIC  X(04)    VALUE                  
022000             '----'.                                                      
022100         10  FILLER                  PIC  X(02)    VALUE SPACES.          
022200         10  FILLER                  PIC  X(30)    VALUE                  
022300             '------------------------------'.                            
022400         10  FILLER                  PIC  X(02)    VALUE SPACES.          
022500         10  FILLER                  PIC  X(04)    VALUE                  
022600             '----'.                                                      
022700         10  FILLER                  PIC  X(05)    VALUE SPACES.          
022800         10  FILLER                  PIC  X(10)    VALUE                  
022900             '----------'.                                                
023000         10  FILLER                  PIC  X(04)    VALUE SPACES.          
023100         10  FILLER                  PIC  X(09)    VALUE                  
023200             '---------'.                                                 
023300         10  FILLER                  PIC  X(03)    VALUE SPACES.          
023400         10  FILLER                  PIC  X(10)    VALUE                  
023500             '----------'.                                                
023600         10  FILLER                  PIC  X(04)    VALUE SPACES.          
023700         10  FILLER                  PIC  X(17)    VALUE                  
023800             '-----------------'.                                         
023900         10  FILLER                  PIC  X(03)    VALUE SPACES.          
024000         10  FILLER                  PIC  X(10)    VALUE                  
024100             '----------'.                                                
024200         10  FILLER                  PIC  X(07)    VALUE SPACES.          
024300                                                                          
024400 01  DETAIL-LINES.                                                        
024500     05  DETAIL-LINE1.                                                    
024600         10  FILLER                 PIC  X(02)     VALUE SPACES.          
024700         10  DL1-STR-NBR            PIC  Z(04)     VALUE SPACES.          
024800         10  FILLER                 PIC  X(04)     VALUE SPACES.          
024900         10  DL1-OPER-UNT           PIC  Z(04)     VALUE SPACES.          
025000         10  FILLER                 PIC  X(02)     VALUE SPACES.          
025100         10  DL1-STORE-NM           PIC  X(30)     VALUE SPACES.          
025200         10  FILLER                 PIC  X(02)     VALUE SPACES.          
025300         10  DL1-SCAC               PIC  X(04)     VALUE SPACES.          
025400         10  FILLER                 PIC  X(06)     VALUE SPACES.          
025500         10  DL1-TRLR-ID            PIC  X(12)     VALUE SPACES.          
025600         10  FILLER                 PIC  X(01)     VALUE SPACES.          
025700         10  DL1-TRLR-SRL-NBR       PIC  X(09)     VALUE SPACES.          
025800         10  FILLER                 PIC  X(03)     VALUE SPACES.          
025900         10  DL1-TRLR-STATUS        PIC  X(10)     VALUE SPACES.          
026000         10  FILLER                 PIC  X(04)     VALUE SPACES.          
026100         10  DL1-DATE               PIC  X(10)     VALUE SPACES.          
026200         10  FILLER                 PIC  X(02)     VALUE SPACES.          
026300         10  DL1-TIME               PIC  X(05)     VALUE SPACES.          
026400         10  FILLER                 PIC  X(03)     VALUE SPACES.          
026500         10  DL1-PLAN-INV-DT        PIC  X(10)     VALUE SPACES.          
026600                                                                          
026700 01  MISCELLANEOUS-LINES.                                                 
026800     05  PV-BLANK-LINE.                                                   
026900         10  FILLER                  PIC  X(132)   VALUE SPACES.          
027000     05  NO-DATA-LINE.                                                    
027100         10  FILLER                  PIC  X(42)    VALUE SPACES.          
027200         10  FILLER                  PIC  X(47)    VALUE                  
027300             '**** NO INFORMATION EXISTS FOR THIS REPORT ****'.           
027400         10  FILLER                  PIC  X(43)    VALUE SPACES.          
027500     05  END-OF-REPORT-LINE.                                              
027600         10  FILLER                  PIC  X(54)    VALUE SPACES.          
027700         10  FILLER                  PIC  X(44)    VALUE                  
027800             '  ** END OF REPORT **                       '.              
027900         10  FILLER                  PIC  X(44)    VALUE SPACES.          
028000                                                                          
028100******************************************************************        
028200*  COPY BOOK FOR THE TABLE TSUOBSC                                        
028300******************************************************************        
028400     EXEC SQL                                                             
028500          INCLUDE TSUOBSC                                                 
028600     END-EXEC.                                                            
028700                                                                          
028800     EXEC SQL                                                             
028900         INCLUDE SQLCA                                                    
029000     END-EXEC.                                                            
029100                                                                          
029200     COPY SQLCA2.                                                         
029300/                                                                         
029400 PROCEDURE DIVISION.                                                      
029500                                                                          
029600 0000-MAINLINE-PROCESSING.                                                
029700     DISPLAY '****** STARTING SUKRP043 ******'.                           
029800                                                                          
029900     PERFORM 1000-INITIALIZATION.                                         
030000                                                                          
030100     PERFORM 2000-PROCESS-INPUT UNTIL PS-END-OF-INPUT.                    
030200                                                                          
030300     PERFORM 9000-END.                                                    
030400                                                                          
030500     GOBACK.                                                              
030600                                                                          
030700 1000-INITIALIZATION.                                                     
030800                                                                          
030900     OPEN INPUT INPUT-FILE                                                
031000          OUTPUT REPORT-FILE.                                             
031100                                                                          
031200     ACCEPT PV-TODAYS-DATE                                                
031300       FROM DATE.                                                         
031400                                                                          
031500     ACCEPT PV-TODAYS-TIME                                                
031600       FROM TIME.                                                         
031700                                                                          
031800     MOVE PV-TODAYS-DATE-MM  TO DP132O-RUN-MONTH                          
031900                                PV-COMPARE-DATE-MM.                       
032000     MOVE PV-TODAYS-DATE-DD  TO DP132O-RUN-DAY                            
032100                                PV-COMPARE-DATE-DD.                       
032200     MOVE PV-TODAYS-DATE-YY  TO DP132O-RUN-YEAR                           
032300                                PV-COMPARE-DATE-YY.                       
032400     IF PV-COMPARE-DATE-YY  >  80                                         
032500         MOVE  19            TO PV-COMPARE-DATE-CC                        
032600     ELSE                                                                 
032700         MOVE  20            TO PV-COMPARE-DATE-CC                        
032800     END-IF.                                                              
032900                                                                          
033000     MOVE PV-TODAYS-TIME-HH  TO DP132O-RUN-HOUR.                          
033100     MOVE PV-TODAYS-TIME-MM  TO DP132O-RUN-MINUTE.                        
033200                                                                          
033300     MOVE PC-SUKRP043        TO DP132O-PROGRAM-NAME.                      
033400     MOVE PC-REPORT-NBR      TO DP132O-REPORT-NUMBER.                     
033500                                                                          
033600     MOVE +0                 TO PV-PAGE-COUNT.                            
033700                                                                          
033800     SET PS-NOT-END-OF-INPUT TO TRUE.                                     
033900                                                                          
034000     PERFORM 8000-READ-EXTRACT-FILE.                                      
034100                                                                          
034200     IF PS-END-OF-INPUT                                                   
034300         ADD +1                  TO PV-PAGE-COUNT                         
034400         MOVE PV-PAGE-COUNT      TO DP132O-PAGE-NUMBER                    
034500         WRITE REPORT-RECORD                                              
034600           FROM DP132O-STANDRD-HEADER-132-COLS                            
034700           AFTER ADVANCING PAGE                                           
034800         WRITE REPORT-RECORD                                              
034900           FROM HEADING-LINE-1                                            
035000           AFTER ADVANCING 2 LINES                                        
035100         WRITE REPORT-RECORD                                              
035200           FROM HEADING-LINE-2                                            
035300           AFTER ADVANCING 1 LINE                                         
035400         WRITE REPORT-RECORD                                              
035500           FROM HEADING-LINE-3                                            
035600           AFTER ADVANCING 1 LINE                                         
035700         WRITE REPORT-RECORD                                              
035800           FROM NO-DATA-LINE                                              
035900         AFTER ADVANCING 2 LINES                                          
036000     ELSE                                                                 
036100         PERFORM 2200-PAGE-BREAK                                          
036200     END-IF.                                                              
036300                                                                          
036400 2000-PROCESS-INPUT.                                                      
036500     IF PV-CURR-KEY NOT = PV-PREV-KEY                                     
036600         MOVE PV-CURR-KEY        TO PV-PREV-KEY                           
036700         PERFORM 2200-PAGE-BREAK                                          
036800         PERFORM 2100-PRINT-DETAIL-LINE                                   
036900     ELSE                                                                 
037000         IF PV-CURR-KEY-2 = PV-PREV-KEY-2                                 
037100             PERFORM 2100-PRINT-DETAIL-LINE                               
037200         ELSE                                                             
037300             WRITE REPORT-RECORD  FROM PV-BLANK-LINE                      
037400             ADD +1                   TO PV-LINE-COUNT                    
037500             PERFORM 2100-PRINT-DETAIL-LINE                               
037600             MOVE PV-CURR-KEY-2       TO PV-PREV-KEY-2                    
037700         END-IF                                                           
037800     END-IF.                                                              
037900                                                                          
038000     PERFORM 8000-READ-EXTRACT-FILE.                                      
038100                                                                          
038200 2100-PRINT-DETAIL-LINE.                                                  
038300                                                                          
038400     MOVE SU055-LOC-NBR            TO DL1-STR-NBR.                        
038500     MOVE SU055-ORGN-OPER-UNT-ID   TO DL1-OPER-UNT.                       
038600     MOVE SU055-LOC-NM             TO DL1-STORE-NM.                       
038700     MOVE SU055-SHIP-SCAC-CDE      TO DL1-SCAC.                           
038800     MOVE SU055-TRLR-ID            TO DL1-TRLR-ID.                        
038900     MOVE SU055-TRLR-SRL-NBR       TO DL1-TRLR-SRL-NBR.                   
039000*    MOVE SU055-TRIP-STAT-CDE      TO DL1-TRLR-STATUS.                    
039100     MOVE SU055-OTBND-STAT-TMST    TO PV-DATE-TIME.                       
039200     MOVE PV-DATE-TIME-CCYY        TO PV-DISPLAY-CCYY.                    
039300     MOVE PV-DATE-TIME-MM          TO PV-DISPLAY-MM.                      
039400     MOVE PV-DATE-TIME-DD          TO PV-DISPLAY-DD.                      
039500     MOVE PV-DATE-TIME-HH          TO PV-DISPLAY-HH.                      
039600     MOVE PV-DATE-TIME-MN          TO PV-DISPLAY-MN.                      
039700     MOVE PV-DISPLAY-DATE          TO DL1-DATE.                           
039800     MOVE PV-DISPLAY-TIME          TO DL1-TIME.                           
039900     MOVE PV-DISPLAY-DATE          TO DL1-DATE.                           
040000     MOVE PV-DISPLAY-TIME          TO DL1-TIME.                           
040100     MOVE SU055-PLND-INV-DTE       TO DL1-PLAN-INV-DT.                    
040200                                                                          
040300     PERFORM 7000-GET-TRLR-STATUS.                                        
040400                                                                          
040500     ADD +1 TO PV-LINE-COUNT.                                             
040600     IF PV-LINE-COUNT > PV-MAX-LINE-COUNT                                 
040700         PERFORM 2200-PAGE-BREAK                                          
040800     END-IF.                                                              
040900                                                                          
041000     WRITE REPORT-RECORD                                                  
041100       FROM DETAIL-LINE1                                                  
041200         AFTER ADVANCING 1 LINE.                                          
041300     ADD 1                            TO PV-DETAIL-COUNT.                 
041400                                                                          
041500 2200-PAGE-BREAK.                                                         
041600                                                                          
041700     ADD +1                  TO PV-PAGE-COUNT.                            
041800     MOVE PV-PAGE-COUNT      TO DP132O-PAGE-NUMBER.                       
041900                                                                          
042000     WRITE REPORT-RECORD                                                  
042100       FROM DP132O-STANDRD-HEADER-132-COLS                                
042200         AFTER ADVANCING PAGE.                                            
042300                                                                          
042400     WRITE REPORT-RECORD                                                  
042500       FROM HEADING-LINE-1                                                
042600         AFTER ADVANCING 2 LINES.                                         
042700                                                                          
042800     WRITE REPORT-RECORD                                                  
042900       FROM HEADING-LINE-2                                                
043000         AFTER ADVANCING 1 LINE.                                          
043100                                                                          
043200     WRITE REPORT-RECORD                                                  
043300       FROM HEADING-LINE-3                                                
043400         AFTER ADVANCING 1 LINE.                                          
043500                                                                          
043600     WRITE REPORT-RECORD                                                  
043700       FROM HEADING-LINE-4                                                
043800         AFTER ADVANCING 2 LINE.                                          
043900                                                                          
044000     WRITE REPORT-RECORD                                                  
044100       FROM HEADING-LINE-5                                                
044200         AFTER ADVANCING 1 LINE.                                          
044300                                                                          
044400     WRITE REPORT-RECORD                                                  
044500       FROM HEADING-LINE-6                                                
044600         AFTER ADVANCING 1 LINE.                                          
044700                                                                          
044800     MOVE +7                 TO PV-LINE-COUNT.                            
044900     MOVE PV-CURR-KEY        TO PV-PREV-KEY.                              
045000     MOVE PV-CURR-KEY-2      TO PV-PREV-KEY-2.                            
045100/                                                                         
045200 7000-GET-TRLR-STATUS.                                                    
045300                                                                          
045400     EXEC SQL                                                             
045500          SELECT STAT_SHRT_DESC                                           
045600            INTO :SUOBSC-STAT-SHRT-DESC                                   
045700            FROM TSUOBSC                                                  
045800           WHERE TRIP_STAT_CDE  =  :SU055-TRIP-STAT-CDE                   
045900     END-EXEC.                                                            
046000                                                                          
046100     EVALUATE TRUE                                                        
046200         WHEN SQLCODE EQUAL ZERO                                          
046300              MOVE  SUOBSC-STAT-SHRT-DESC TO  DL1-TRLR-STATUS             
046400                                                                          
046500         WHEN OTHER                                                       
046600              MOVE  'UNKNOWN'             TO  DL1-TRLR-STATUS             
046700     END-EVALUATE.                                                        
046800/                                                                         
046900 8000-READ-EXTRACT-FILE.                                                  
047000     READ INPUT-FILE                                                      
047100       INTO SU055-EXTRACT-RECORD                                          
047200         AT END                                                           
047300           SET PS-END-OF-INPUT        TO TRUE                             
047400         NOT AT END                                                       
047500             ADD 1                    TO PV-READ-COUNT                    
047600             MOVE SU055-ORGN-OPER-UNT-ID                                  
047700                                      TO PV-CURR-OPER                     
047800             MOVE SU055-PLND-INV-DTE  TO PV-CURR-PLND-INV-DTE             
047900*            MOVE SU055-LOC-NBR       TO PV-CURR-LOC                      
048000                                                                          
048100     END-READ.                                                            
048200                                                                          
048300 9000-END.                                                                
048400*FINISH REPORT.                                                           
048500     WRITE REPORT-RECORD                                                  
048600       FROM END-OF-REPORT-LINE                                            
048700         AFTER ADVANCING 2 LINES.                                         
048800                                                                          
048900*CLOSE FILES.                                                             
049000     CLOSE INPUT-FILE                                                     
049100           REPORT-FILE.                                                   
049200                                                                          
049300     MOVE PV-READ-COUNT               TO PV-DISP-NBR                      
049400     DISPLAY 'RECORDS READ         = ' PV-DISP-NBR.                       
049500                                                                          
049600     MOVE PV-DETAIL-COUNT             TO PV-DISP-NBR                      
049700     DISPLAY 'DETAIL LINES PRINTED = ' PV-DISP-NBR.                       
049800/                                                                         
049900******************************************************************        
050000*     SQL WARNING OR ERROR PROCESSING                            *        
050100******************************************************************        
050200 Z999-SQL-ERROR.                                                          
050300     MOVE SQLCODE    TO SQLCODE2.                                         
050400     MOVE SQLERRD(3) TO SQLERRD3.                                         
050500     DISPLAY '** ABEND **       ** = SQLERROR'.                           
050600     DISPLAY '** PROGRAM        ** = SUKRP043'.                           
050700     DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
050800     DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
050900     DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
051000     DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
051100                                                                          
051200******************************************************************        
051300* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
051400* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
051500* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
051600* FORMAT.                                                                 
051700******************************************************************        
051800     COPY DPPD004.                                                        
051900                                                                          
052000     MOVE +4003 TO  ABEND-CODE.                                           
052100     CALL 'ILBOABN0' USING ABEND-CODE.                                    
052200                                                                          
