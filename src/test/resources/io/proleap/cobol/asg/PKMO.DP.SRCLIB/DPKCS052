000100*-----------------------*                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*-----------------------*                                                 
000400 PROGRAM-ID.    DPKCS052.                                                 
000500 AUTHOR.        CHERI MASTEL - COMPUWARE.                                 
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000700 DATE-WRITTEN.  05-08-96.                                                 
000800 DATE-COMPILED.                                                           
000900*----------------------------------------------------------------*        
001000*    Z052 - ADD PHYSICAL UNIT.                                   *        
001100*                                                                *        
001200*    THIS PROGRAM ALLOWS THE USER TO ADD A NEW PHYSICAL UNIT,    *        
001300*    TO THE DB2 TABLE TPHYUNT.                                   *        
001400*                                                                *        
001500*    * LOCATION (STORE AND FACILITY) MUST BE ENTERED.            *        
001600*      LOCATION EDITS ------------------------------------------ *        
001700*      - STORE NUMBER MUST BE ON TABLE 'XIT_LOC'.                *        
001800*      - FACILITY MUST BE NUMERIC.                               *        
001900*    * IP ADDRESS RANGE (BEGIN AND END) AND/OR                   *        
002000*      VTAM (LINE AND UNIT) MUST ALSO BE ENTERED.                *        
002100*      IP ADDRESS RANGE EDITS (IF ENTERED) --------------------- *        
002200*      - IP ADDRESS OCTETS MUST BE NUMERIC AND EACH OCTET'S      *        
002300*        VALUE MUST BE 000-255 INCLUSIVE.                        *        
002400*      - THE ENDING IP ADDRESS MUST BE GREATER THAN OR           *        
002500*        EQUAL TO THE BEGINNING IP ADDRESS.                      *        
002600*      VTAM EDITS (IF ENTERED) --------------------------------- *        
002700*      - LINE MUST BE 2 POSITIONS.                               *        
002800*      - UNIT MUST BE 2 POSITIONS AND A VALID HEX VALUE.         *        
002900*----------------------------------------------------------------*        
      *    12/22/03  TKMA464   MADE CHANGES FOR EXPANDING LOCATION     *        
      *                        FROM 3 TO 5 DIGITS.                     *        
002900*----------------------------------------------------------------*        
262023*  CHANGED 09/28/2021    BY JIM HUNTER      WR: CHG0262023       *        
262023*  CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE   *        
262023*  MAKING THE CALL DYNAMIC VS STATIC                             *        
262023*----------------------------------------------------------------*        
003000                                                                          
003100                                                                          
003200 ENVIRONMENT DIVISION.                                                    
003300                                                                          
003400                                                                          
003500 DATA DIVISION.                                                           
003600                                                                          
003700                                                                          
003800 WORKING-STORAGE SECTION.                                                 
003900                                                                          
004000 01  DK-DB2-KEYS.                                                         
004100     15  DK-OPERATING-COMPANY           PIC  X(02)  VALUE SPACE.          
004500     15  DK-LOC-NBR                     PIC S9(04)  VALUE ZERO            
004600                                                    COMP.                 
004700     15  DK-VTAM.                                                         
004800         20  DK-LNE-ADDR-NBR            PIC  X(02)  VALUE SPACE.          
004900         20  DK-PHYUNT-ADDR-NBR         PIC  X(02)  VALUE SPACE.          
005000     15  DK-IP-ADDR-BEG                 PIC  X(15)  VALUE SPACE.          
005100     15  DK-IP-ADDR-END                 PIC  X(15)  VALUE SPACE.          
005200                                                                          
005300 01  PC-PROGRAM-CONSTANTS.                                                
005400     05  PC-CURRENT-MAP-NAME      PIC  X(08)  VALUE  'DP052A  '.          
005500     05  PC-CURRENT-MAPSET-NAME   PIC  X(08)  VALUE  'DPKM052 '.          
005600     05  PC-CURRENT-PROGRAM-NAME  PIC  X(08)  VALUE  'DPKCS052'.          
005700     05  PC-MAX-RETRIES           PIC S9(04)  VALUE  +3                   
005800                                              COMP.                       
005900     05  PC-DOT                   PIC  X(01)  VALUE  '.'.                 
006000     05  PC-OPERATING-COMPANY     PIC  X(02)  VALUE  '03'.                
006100     05  PC-TSYMSG-NUMBERS.                                               
006200         10  PC-TSYMSG-00005      PIC  9(05)  VALUE  00005.               
006300         10  PC-TSYMSG-00007      PIC  9(05)  VALUE  00007.               
006400         10  PC-TSYMSG-00008      PIC  9(05)  VALUE  00008.               
006500         10  PC-TSYMSG-00017      PIC  9(05)  VALUE  00017.               
006600         10  PC-TSYMSG-00052      PIC  9(05)  VALUE  00052.               
006700         10  PC-TSYMSG-00068      PIC  9(05)  VALUE  00068.               
006800         10  PC-TSYMSG-00223      PIC  9(05)  VALUE  00223.               
006900         10  PC-TSYMSG-00224      PIC  9(05)  VALUE  00224.               
007000         10  PC-TSYMSG-00388      PIC  9(05)  VALUE  00388.               
007100         10  PC-TSYMSG-01176      PIC  9(05)  VALUE  01176.               
007200         10  PC-TSYMSG-01177      PIC  9(05)  VALUE  01177.               
007300         10  PC-TSYMSG-01282      PIC  9(05)  VALUE  01282.               
007400         10  PC-TSYMSG-01283      PIC  9(05)  VALUE  01283.               
007500         10  PC-TSYMSG-01285      PIC  9(05)  VALUE  01285.               
007600     05  PC-VALID-OCTET-MIN       PIC  X(03)  VALUE  '000'.               
007700     05  PC-VALID-OCTET-MAX       PIC  X(03)  VALUE  '255'.               
007800     05  PC-VALIDATE-PHYU1        PIC  X(01)  VALUE  SPACE.               
007900         88  PC-VALID-HEX-CHAR1               VALUE  '0', '1',            
008000                                                     '2', '3',            
008100                                                     '4', '5',            
008200                                                     '6', '7',            
008300                                                     '8', '9',            
008400                                                     'A', 'B',            
008500                                                     'C', 'D',            
008600                                                     'E', 'F'.            
008700     05  PC-VALIDATE-PHYU2        PIC  X(01)  VALUE  SPACE.               
008800         88  PC-VALID-HEX-CHAR2               VALUE  '0', '1',            
008900                                                     '2', '3',            
009000                                                     '4', '5',            
009100                                                     '6', '7',            
009200                                                     '8', '9',            
009300                                                     'A', 'B',            
009400                                                     'C', 'D',            
009500                                                     'E', 'F'.            
009600     05  PC-IP-ADDR-NOT-ENTERED.                                          
009700         10  PC-ZEROS-IP-ADDR           PIC  X(15)  VALUE                 
009800                                             '000.000.000.000'.           
009900         10  PC-ZERO-IP-ADDR            PIC  X(15)  VALUE                 
010000                                             '  0.  0.  0.  0'.           
010100         10  PC-SPACES-IP-ADDR          PIC  X(15)  VALUE SPACES.         
010200         10  PC-SPACE-IP-ADDR           PIC  X(15)  VALUE                 
010300                                             '   .   .   .   '.           
010400                                                                          
010500 01  PM-PROGRAM-MESSAGES.                                                 
010600     05  PM-ABEND-INFO-A.                                                 
010700         10  FILLER                PIC X(15)   VALUE                      
010800                                   'DP020-SRC-AID: '.                     
010900         10  PM-A-SRC-AID          PIC X(02)   VALUE  SPACE.              
011000         10  FILLER                PIC X(19)   VALUE                      
011100                                   '. DP020-FK-ACTION: '.                 
011200         10  PM-A-FK-ACTION        PIC X(01)   VALUE  SPACE.              
011300         10  FILLER                PIC X(22)   VALUE                      
011400                                   '. DP020-FK-DISPN-IND: '.              
011500         10  PM-A-FK-DISPN         PIC X(01)   VALUE  SPACE.              
011600         10  FILLER                PIC X(01)   VALUE '.'.                 
011700         10  FILLER                PIC X(19)   VALUE SPACE.               
011800                                                                          
011900     05  PM-ABEND-KEYFIELDS.                                              
012000         10  FILLER                PIC X(14)   VALUE                      
012100                                   'LNE-ADDR-NBR: '.                      
012200         10  PM-ABEND-KEYFIELD1    PIC X(02)   VALUE  SPACE.              
012300         10  FILLER                PIC X(02)   VALUE  ', '.               
012400         10  FILLER                PIC X(17)   VALUE                      
012500                                   'PHYUNT-ADDR-NBR: '.                   
012600         10  PM-ABEND-KEYFIELD2    PIC X(02)   VALUE  SPACE.              
012700         10  FILLER                PIC X(01)   VALUE  '.'.                
012800                                                                          
012900     05  PM-ABEND-RANGES.                                                 
013000         10  FILLER                PIC X(17)   VALUE                      
013100                                   'BEG-RNG-IP-ADDR: '.                   
013200         10  PM-ABEND-RANGE-B      PIC X(15)   VALUE  SPACE.              
013300         10  FILLER                PIC X(02)   VALUE  ', '.               
013400         10  FILLER                PIC X(17)   VALUE                      
013500                                   'END-RNG-IP-ADDR: '.                   
013600         10  PM-ABEND-RANGE-E      PIC X(15)   VALUE  SPACE.              
013700         10  FILLER                PIC X(01)   VALUE  '.'.                
013800                                                                          
013900 01  PS-PROGRAM-SWITCHES.                                                 
014000     05  PS-ERROR-SW              PIC  X(01)  VALUE  'N'.                 
014100         88  PS-ERROR                         VALUE  'Y'.                 
014200         88  PS-NO-ERROR                      VALUE  'N'.                 
014300                                                                          
014400 01  PV-PROGRAM-VARIABLES.                                                
014500     05  PV-CICS-RESPONSE         PIC S9(04)  VALUE +0                    
014600                                              COMP SYNC.                  
014700     05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0                    
014800                                              COMP SYNC.                  
014900     05  PV-MSG-VAR-CNT           PIC S9(04)  VALUE +0                    
015000                                              COMP SYNC.                  
015100     05  PV-COUNT-OVERLAP-IP-ADDR PIC S9(09)  VALUE +0                    
015200                                              COMP SYNC.                  
015300     05  PV-COUNT-DUPLICATE-VTAM  PIC S9(09)  VALUE +0                    
015400                                              COMP SYNC.                  
015500                                                                          
015600**--------------------------------------------------------------**        
015700** THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT    **        
015800** SUB-ROUTINE (DPKUT100).                                      **        
015900**--------------------------------------------------------------**        
016000     COPY DPWS010I.                                                       
016100                                                                          
016200                                                                          
016300*----------------------------------------------------------------*        
016400*    MAP LAYOUT                                                  *        
016500*----------------------------------------------------------------*        
016600     COPY DPKM052.                                                        
016700                                                                          
016800*----------------------------------------------------------------*        
016900*    ATTRIBUTE SETTINGS COPYBOOK.                                *        
017000*----------------------------------------------------------------*        
017100     COPY DPWS015.                                                        
017200                                                                          
017300*----------------------------------------------------------------*        
017400*    FUNCTION KEYS COPYBOOK                                      *        
017500*----------------------------------------------------------------*        
017600     COPY DPWS016.                                                        
017700                                                                          
      *----------------------------------------------------------------*        
      *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.           *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS030.                                                        
           COPY DPWS930.                                                        
018200                                                                          
018300*****************************************************************         
018400*    ARCHITECTURE COMMAREAS: 4072 MAX LENGTH TOTAL                        
018500*    * GLOBAL COMMAREA:      1265 BYTES, COPY BOOK DPWS020                
018600*    * INTER-APPLICATION:     200 BYTES, TEMPORARY!                       
018700*    * APPLICATION SPECIFIC: 2865 BYTES MAX                               
018800*****************************************************************         
018900                                                                          
019000*****************************************************************         
019100*    STANDARD COMMAREA.                                                   
019200*****************************************************************         
019300     COPY DPWS020.                                                        
019400     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
019500                                                                          
019600*****************************************************************         
019700*    INTER-APPLICATION COMMAREA.                                          
019800*****************************************************************         
019900         10  INTER-APPL-COMM-AREA PIC X(200).                             
020000     COPY DPWS250.                                                        
020100                                                                          
020200*****************************************************************         
020300*    SPECIFIC COMMMAREA FOR DPKCS052.                                     
020400*****************************************************************         
020500         10  ASC-SPECIFIC-COMMAREA.                                       
020600             15  ASC-ADD-COMPLETE-SW         PIC  X(01).                  
020700                 88  ASC-ADD-COMPLETE                   VALUE 'Y'.        
020800                 88  ASC-ADD-PENDING                    VALUE 'N'.        
020900             15  ASC-LOCATION.                                            
021000                 20  ASC-STORE.                                           
021100                     25  ASC-STORE-N         PIC  9(05).                  
021200                 20  ASC-FCLTY.                                           
021300                     25  ASC-FCLTY-N         PIC  9(04).                  
021400             15  ASC-VTAM.                                                
021500                 20  ASC-LINE.                                            
021600                     25  ASC-LINE1           PIC  X(01).                  
021700                     25  ASC-LINE2           PIC  X(01).                  
021800                 20  ASC-PHYSICAL-UNIT.                                   
021900                     25  ASC-PHYSICAL-UNIT1  PIC  X(01).                  
022000                     25  ASC-PHYSICAL-UNIT2  PIC  X(01).                  
022100             15  ASC-IP-ADDR-RANGE.                                       
022200                 20  ASC-IP-ADDR-BEGIN.                                   
022300                     25  ASC-IP-ADDR-B-OCTET1.                            
022400                         30  ASC-IP-ADDR-B-OCTET1-N                       
022500                                             PIC  9(03).                  
022600                     25  ASC-IP-ADDR-B-DOT1  PIC  X(01).                  
022700                     25  ASC-IP-ADDR-B-OCTET2.                            
022800                         30  ASC-IP-ADDR-B-OCTET2-N                       
022900                                             PIC  9(03).                  
023000                     25  ASC-IP-ADDR-B-DOT2  PIC  X(01).                  
023100                     25  ASC-IP-ADDR-B-OCTET3.                            
023200                         30  ASC-IP-ADDR-B-OCTET3-N                       
023300                                             PIC  9(03).                  
023400                     25  ASC-IP-ADDR-B-DOT3  PIC  X(01).                  
023500                     25  ASC-IP-ADDR-B-OCTET4.                            
023600                         30  ASC-IP-ADDR-B-OCTET4-N                       
023700                                             PIC  9(03).                  
023800                 20  ASC-IP-ADDR-END.                                     
023900                     25  ASC-IP-ADDR-E-OCTET1.                            
024000                         30  ASC-IP-ADDR-E-OCTET1-N                       
024100                                             PIC  9(03).                  
024200                     25  ASC-IP-ADDR-E-DOT1  PIC  X(01).                  
024300                     25  ASC-IP-ADDR-E-OCTET2.                            
024400                         30  ASC-IP-ADDR-E-OCTET2-N                       
024500                                             PIC  9(03).                  
024600                     25  ASC-IP-ADDR-E-DOT2  PIC  X(01).                  
024700                     25  ASC-IP-ADDR-E-OCTET3.                            
024800                         30  ASC-IP-ADDR-E-OCTET3-N                       
024900                                             PIC  9(03).                  
025000                     25  ASC-IP-ADDR-E-DOT3  PIC  X(01).                  
025100                     25  ASC-IP-ADDR-E-OCTET4.                            
025200                         30  ASC-IP-ADDR-E-OCTET4-N                       
025300                                             PIC  9(03).                  
025500                                                                          
025600*----------------------------------------------------------------*        
025700*    ABEND PROCESSING COPYBOOK.                                  *        
025800*----------------------------------------------------------------*        
025900     COPY DPWS013.                                                        
026000                                                                          
026100******************************************************************        
026200*    DB2 AREA AND COBOL DECLARATIONS FOR TABLE:                  *        
026300*    XIT_LOC (LOCATION TABLE)                                    *        
026400******************************************************************        
026500     EXEC SQL                                                             
026600          INCLUDE XIT00000                                                
026700     END-EXEC.                                                            
026800                                                                          
026900******************************************************************        
027000*    DB2 AREA AND COBOL DECLARATIONS FOR TABLE:                  *        
027100*    TPHYUNT (PHYSICAL UNIT TABLE)                               *        
027200******************************************************************        
027300     EXEC SQL                                                             
027400          INCLUDE TPHYUNT                                                 
027500     END-EXEC.                                                            
027600                                                                          
027700******************************************************************        
027800*    DB2 AREA FOR COMMUNICATIONS:                                *        
027900******************************************************************        
028000     EXEC SQL                                                             
028100          INCLUDE SQLCA                                                   
028200     END-EXEC.                                                            
028300                                                                          
028400                                                                          
028500 LINKAGE SECTION.                                                         
028600                                                                          
028700 01  DFHCOMMAREA.                                                         
028800     05  FILLER                         OCCURS  1 TO 4072 TIMES           
028900                                        DEPENDING ON EIBCALEN.            
029000         10  FILLER                     PIC  X(01).                       
029100                                                                          
029200                                                                          
029300 PROCEDURE DIVISION.                                                      
029400*****************************************************************         
029500** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
029600** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
029700** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
029800**                                                             **         
029900** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
030000** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
030100*****************************************************************         
030200 0000-MAIN-MODULE.                                                        
030300     INITIALIZE DP030-CICS-API-FIELDS.                                    
030400     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
030500     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
030600     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
030700     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
030800     MOVE LENGTH OF DP052AI        TO DP030-MAP-LENGTH (1).               
030900     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
031000     PERFORM 0001-CALL-CICS-ARCH-API.                                     
031100                                                                          
031200     PERFORM 1000-CONTROL-PROCESSING                                      
031300                                                                          
031400     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
031500     PERFORM 0001-CALL-CICS-ARCH-API.                                     
031600                                                                          
031700 0001-CALL-CICS-ARCH-API.                                                 
031800     CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
031900                                    DFHCOMMAREA                           
032000                                    DP030-CICS-API-FIELDS                 
032100                                    DP020-STANDARD-COMMAREA               
032200                                    DP052AI.                              
032300                                                                          
032400     IF  DP030-RC-CALL-SUCCESSFUL                                         
032500         CONTINUE                                                         
032600     ELSE                                                                 
032700         SET DP013-NO-ROLLBACK                                            
032800             DP013-XCTL-DISPLAY-RESTART                                   
032900             DP013-CICS-ABEND      TO TRUE                                
033000         MOVE 'BEFORE 0000-MAIN-MODULE'                                   
033100                                   TO DP013-PARAGRAPH                     
033200         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
033300-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
033400         MOVE DP030-RETURN-CODE                                           
033500                                   TO DP013-MESSAGE-TEXT (3)              
033600         PERFORM DP013-0000-PROCESS-ABEND                                 
033700     END-IF.                                                              
033800                                                                          
033900*----------------------------------------------------------------*        
034000*    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
034100*    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
034200*----------------------------------------------------------------*        
034300 1000-CONTROL-PROCESSING.                                                 
034400                                                                          
034500     EVALUATE TRUE                                                        
034600         WHEN DP020-NEXT-ACT-INITIAL                                      
034700*            *****************************************************        
034800*            * NEXT ACT: INITIAL                                 *        
034900*            *****************************************************        
035000             INITIALIZE ASC-SPECIFIC-COMMAREA                             
035100             SET ASC-ADD-PENDING   TO TRUE                                
035200             PERFORM 4000-BUILD-INITIAL-PANEL                             
035300                                                                          
035400         WHEN DP020-NEXT-ACT-READ-MAP                                     
035500*            *****************************************************        
035600*            * NEXT ACT: READ MAP                                *        
035700*            *****************************************************        
035800             PERFORM 2000-PROCESS-PANEL                                   
035900                                                                          
036000         WHEN DP020-NEXT-ACT-RETURN                                       
036100*            *****************************************************        
036200*            * NEXT ACT: RETURN                                  *        
036300*            *****************************************************        
036400             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
036500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
036600                                                                          
036700         WHEN OTHER                                                       
036800*            *****************************************************        
036900*            * NEXT ACT: OTHER (UNDEFINED)                       *        
037000*            *****************************************************        
037100             SET DP013-LOGIC-ABEND                                        
037200                 DP013-NO-ROLLBACK TO TRUE                                
037300             MOVE '1000-CONTROL-PROCESSING'                               
037400                                   TO DP013-PARAGRAPH                     
037500             MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
037600                                   TO DP013-MESSAGE-TEXT(1)               
037700             MOVE DP020-NEXT-APPL-ACTIVITY                                
037800                                   TO DP013-MESSAGE-TEXT(2)               
037900             PERFORM DP013-0000-PROCESS-ABEND                             
038000     END-EVALUATE.                                                        
038100                                                                          
038200*----------------------------------------------------------------*        
038300* FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*        
038400* SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*        
038500* EDITS.                                                         *        
038600*----------------------------------------------------------------*        
038700 2000-PROCESS-PANEL.                                                      
038800                                                                          
038900     EVALUATE TRUE                                                        
039000                                                                          
039100         WHEN DP020-SRC-AID = DP016-CLEAR                                 
039200*            *****************************************************        
039300*            * PFKEY ACTION: CLEAR                         <ESC> *        
039400*            *   <CLEAR> ("ESC" KEY)                             *        
039500*            *                                                   *        
039600*            * PRESENT SCREEN AS IT WAS AT THE TIME OF THE LAST  *        
039700*            * <ENTER>, INCLUDING ANY ERROR MESSAGES.            *        
039800*            *****************************************************        
039900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
040000             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
040100                                                                          
040200         WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
040300*            *****************************************************        
040400*            * PFKEY ACTION: <REFRESH>                           *        
040500*            *                                                   *        
040600*            * PRESENT SCREEN AS FOLLOWS:                        *        
040700*            * - BRING BACK WHAT WAS LAST SAVED.                 *        
040800*            * - SHOW SCREEN AS ORIGINALLY PRESENTED (BLANK FOR  *        
040900*            *   "ADD" SCREEN).                                  *        
041000*            *****************************************************        
041100             INITIALIZE ASC-SPECIFIC-COMMAREA                             
041200             PERFORM 4000-BUILD-INITIAL-PANEL                             
041300             PERFORM 3200-RESET-ATTRIBUTES                                
041400                                                                          
041500         WHEN OTHER                                                       
041600*            *****************************************************        
041700*            * PFKEY ACTION: OTHER (APPLICATION SPECIFIC)        *        
041800*            *****************************************************        
041900             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
042000             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
042100             IF  PS-NO-ERROR                                              
042200                 PERFORM 2100-CHECK-FUNCTION-KEY                          
042300                 SET DP030-CONTINUE-GO TO TRUE                            
042400             ELSE                                                         
042500                 SET DP030-OVERRIDE-GO TO TRUE                            
042600             END-IF                                                       
042700     END-EVALUATE.                                                        
042800                                                                          
042900                                                                          
043000******************************************************************        
043100** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **        
043200** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **        
043300** FROM THE CICS ARCHITECTURE API.                              **        
043400******************************************************************        
043500 2100-CHECK-FUNCTION-KEY.                                                 
043600     EVALUATE TRUE                                                        
043700                                                                          
043800         WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                      
043900*            *****************************************************        
044000*            * APPLICATION-SPECIFIC PFKEY: LOCAL 01:  ADD        *        
044100*            *****************************************************        
044200             PERFORM 5000-ADD-PHYSICAL-UNIT                               
044300             IF PS-ERROR                                                  
044400                 CONTINUE                                                 
044500             ELSE                                                         
044600                 SET ASC-ADD-COMPLETE  TO TRUE                            
044700*                -- OBJECT WAS ADDED --                                   
044800                 MOVE PC-TSYMSG-00223  TO DP020-MSG-NUMBER                
044900*                MOVE PC-TSYMSG-00224  TO DP020-MSG-NUMBER                
045000                 SET DP020-MSG-INFORMATIONAL                              
045100                                       TO TRUE                            
045200             END-IF                                                       
045300*            *---------------------------------------------------*        
045400*            *   PRESENT SCREEN IN ANTICIPATION OF CLIENT/USER   *        
045500*            *   CHOOSING TO DO ANOTHER "ADD".                   *        
045600*            *   (SAME LOGIC AS NEXT-ACT-INITIAL, FOR "ADD".)    *        
045700*            *---------------------------------------------------*        
045800             INITIALIZE ASC-SPECIFIC-COMMAREA                             
045900             MOVE SPACES TO ASC-STORE                                     
046000                            ASC-FCLTY                                     
046100                            ASC-IP-ADDR-RANGE                             
046200             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
046300             PERFORM 4000-BUILD-INITIAL-PANEL                             
046400             SET DP030-ERASE-UNPROT TO TRUE                               
046500                                                                          
046600         WHEN DP020-SRC-AID = DP016-ENTER                                 
046700*            *****************************************************        
046800*            * APPLICATION-SPECIFIC PFKEY: <ENTER>               *        
046900*            *   EDIT DATA AND PRESENT IT ON THE SCREEN          *        
047000*            *****************************************************        
047100             CONTINUE                                                     
047200                                                                          
047300         WHEN OTHER                                                       
047400*            *****************************************************        
047500*            * APPLICATION-SPECIFIC PFKEY: "OTHER" (UNDEFINED)   *        
047600*            *****************************************************        
047700             SET DP013-NO-ROLLBACK                                        
047800                 DP013-XCTL-DISPLAY-RESTART                               
047900                 DP013-CICS-ABEND  TO TRUE                                
048000             MOVE '2100-CHECK-FUNCTION-KEY'                               
048100                                   TO DP013-PARAGRAPH                     
048200             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
048300                                   TO DP013-MESSAGE-TEXT (1)              
048400             MOVE 'OR CODE MISSING FROM THIS PROGRAM TO PROCESS:'         
048500                                   TO DP013-MESSAGE-TEXT (2)              
048600             MOVE DP020-SRC-AID    TO PM-A-SRC-AID                        
048700             MOVE DP020-FK-ACTION (DP020-SRC-AID)                         
048800                                   TO PM-A-FK-ACTION                      
048900             MOVE DP020-FK-DISPN-IND (DP020-SRC-AID)                      
049000                                   TO PM-A-FK-DISPN                       
049100             MOVE PM-ABEND-INFO-A  TO DP013-MESSAGE-TEXT (3)              
049200             MOVE 'REVIEW PFKEYS SETUP FOR THIS MNEMONIC.'                
049300                                   TO DP013-MESSAGE-TEXT (4)              
049400             PERFORM DP013-0000-PROCESS-ABEND                             
049500     END-EVALUATE.                                                        
049600                                                                          
049700******************************************************************        
049800** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **        
049900** INTER-APPLICATION COMMAREA.                                  **        
050000******************************************************************        
050100 2110-PREPARE-INTER-APPL-COMM.                                            
050200     MOVE ASC-STORE-N              TO DP250-LOCATION-NBR.                 
050300     MOVE ASC-FCLTY-N              TO DP250-FCLTY-N.                      
050400     MOVE ASC-LINE                 TO DP250-LINE.                         
050500     MOVE ASC-PHYSICAL-UNIT        TO DP250-PHYSICAL-UNIT.                
050600     MOVE ASC-IP-ADDR-BEGIN        TO DP250-IP-ADDR.                      
050700     MOVE ASC-IP-ADDR-END          TO DP250-IP-ADDR-END.                  
050800                                                                          
050900                                                                          
051000*----------------------------------------------------------------*        
051100* MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*        
051200* THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *        
051300* APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *        
051400*----------------------------------------------------------------*        
051500 2200-MOVE-SCREEN-TO-COMMAREA.                                            
051600*    *************************************************************        
051700*    * LOCATION: STORE                                           *        
051800*    *************************************************************        
051900     IF  ASTORL > ZERO                                                    
052000         MOVE ASTORI               TO ASC-STORE                           
052100     ELSE                                                                 
052200         IF  ASTORF = DP015-ERASE-EOF                                     
052300             MOVE SPACE            TO ASC-STORE                           
052400         END-IF                                                           
052500     END-IF.                                                              
052600                                                                          
052700*    *************************************************************        
052800*    * LOCATION: FACILITY                                        *        
052900*    *************************************************************        
053000     IF  AFCLTL > ZERO                                                    
053100         MOVE AFCLTI               TO ASC-FCLTY                           
053200     ELSE                                                                 
053300         IF  AFCLTF = DP015-ERASE-EOF                                     
053400             MOVE SPACE            TO ASC-FCLTY                           
053500         END-IF                                                           
053600     END-IF.                                                              
053700                                                                          
053800*    *************************************************************        
053900*    * VTAM:     LINE                                            *        
054000*    *************************************************************        
054100     IF  ALINEL > ZERO                                                    
054200         MOVE ALINEI               TO ASC-LINE                            
054300     ELSE                                                                 
054400         IF  ALINEF = DP015-ERASE-EOF                                     
054500             MOVE SPACE            TO ASC-LINE                            
054600         END-IF                                                           
054700     END-IF.                                                              
054800                                                                          
054900*    *************************************************************        
055000*    * VTAM:     UNIT                                            *        
055100*    *************************************************************        
055200     IF  APHYUL > ZERO                                                    
055300         MOVE APHYUI               TO ASC-PHYSICAL-UNIT                   
055400     ELSE                                                                 
055500         IF  APHYUF = DP015-ERASE-EOF                                     
055600             MOVE SPACE            TO ASC-PHYSICAL-UNIT                   
055700         END-IF                                                           
055800     END-IF.                                                              
055900                                                                          
056000*    *************************************************************        
056100*    * IP ADDRESS RANGE: BEGINNING OF RANGE                      *        
056200*    *************************************************************        
056300     IF  AIPB1L > ZERO                                                    
056400         MOVE AIPB1I               TO ASC-IP-ADDR-B-OCTET1                
056500     ELSE                                                                 
056600         IF  AIPB1F = DP015-ERASE-EOF                                     
056700             MOVE SPACE            TO ASC-IP-ADDR-B-OCTET1                
056800         END-IF                                                           
056900     END-IF.                                                              
057000                                                                          
057100     IF  AIPB2L > ZERO                                                    
057200         MOVE AIPB2I               TO ASC-IP-ADDR-B-OCTET2                
057300     ELSE                                                                 
057400         IF  AIPB2F = DP015-ERASE-EOF                                     
057500             MOVE SPACE            TO ASC-IP-ADDR-B-OCTET2                
057600         END-IF                                                           
057700     END-IF.                                                              
057800                                                                          
057900     IF  AIPB3L > ZERO                                                    
058000         MOVE AIPB3I               TO ASC-IP-ADDR-B-OCTET3                
058100     ELSE                                                                 
058200         IF  AIPB3F = DP015-ERASE-EOF                                     
058300             MOVE SPACE            TO ASC-IP-ADDR-B-OCTET3                
058400         END-IF                                                           
058500     END-IF.                                                              
058600                                                                          
058700     IF  AIPB4L > ZERO                                                    
058800         MOVE AIPB4I               TO ASC-IP-ADDR-B-OCTET4                
058900     ELSE                                                                 
059000         IF  AIPB4F = DP015-ERASE-EOF                                     
059100             MOVE SPACE            TO ASC-IP-ADDR-B-OCTET4                
059200         END-IF                                                           
059300     END-IF.                                                              
059400                                                                          
059500*    *************************************************************        
059600*    * IP ADDRESS RANGE: END OF RANGE                            *        
059700*    *************************************************************        
059800     IF  AIPE1L > ZERO                                                    
059900         MOVE AIPE1I               TO ASC-IP-ADDR-E-OCTET1                
060000     ELSE                                                                 
060100         IF  AIPE1F = DP015-ERASE-EOF                                     
060200             MOVE SPACE            TO ASC-IP-ADDR-E-OCTET1                
060300         END-IF                                                           
060400     END-IF.                                                              
060500                                                                          
060600     IF  AIPE2L > ZERO                                                    
060700         MOVE AIPE2I               TO ASC-IP-ADDR-E-OCTET2                
060800     ELSE                                                                 
060900         IF  AIPE2F = DP015-ERASE-EOF                                     
061000             MOVE SPACE            TO ASC-IP-ADDR-E-OCTET2                
061100         END-IF                                                           
061200     END-IF.                                                              
061300                                                                          
061400     IF  AIPE3L > ZERO                                                    
061500         MOVE AIPE3I               TO ASC-IP-ADDR-E-OCTET3                
061600     ELSE                                                                 
061700         IF  AIPE3F = DP015-ERASE-EOF                                     
061800             MOVE SPACE            TO ASC-IP-ADDR-E-OCTET3                
061900         END-IF                                                           
062000     END-IF.                                                              
062100                                                                          
062200     IF  AIPE4L > ZERO                                                    
062300         MOVE AIPE4I               TO ASC-IP-ADDR-E-OCTET4                
062400     ELSE                                                                 
062500         IF  AIPE4F = DP015-ERASE-EOF                                     
062600             MOVE SPACE            TO ASC-IP-ADDR-E-OCTET4                
062700         END-IF                                                           
062800     END-IF.                                                              
062900                                                                          
063000*----------------------------------------------------------------*        
063100* PERFORM EDITS ON EACH FIELD ENTERED BY THE USER.  IF AN ERROR  *        
063200* IS FOUND THE CURSOR IS POSITIONED AT THAT FIELD AND AN         *        
063300* APPROPRIATE ERROR MESSAGE IS DISPLAYED.                        *        
063400*----------------------------------------------------------------*        
063500 3000-EDIT-DATA-IN-COMMAREA.                                              
063600     PERFORM 3200-RESET-ATTRIBUTES.                                       
063700     PERFORM 3220-INDIVIDUAL-FIELD-EDITS.                                 
063800                                                                          
063900     IF  PS-NO-ERROR                                                      
064000         PERFORM 3300-CROSSCHECK-FIELD-EDITS                              
064100     END-IF.                                                              
064200                                                                          
064300     IF  PS-NO-ERROR                                                      
064400         PERFORM 3400-CROSSCHECK-TABLE-EDITS                              
064500     END-IF.                                                              
064600                                                                          
064700     IF  PS-NO-ERROR                                                      
064800         MOVE -1 TO ASTORL                                                
064900         SET DP030-SET-CURSOR-APPL-1 TO TRUE                              
065000     END-IF.                                                              
065100                                                                          
065200                                                                          
065300*----------------------------------------------------------------*        
065400*    RESET ATTRIBUTE BYTES FOR EACH FIELD ON SCREEN.             *        
065500*    - PROTECTION                                                *        
065600*    - COLOR                                                     *        
065700*    - HIGHLIGHT                                                 *        
065800*----------------------------------------------------------------*        
065900 3200-RESET-ATTRIBUTES.                                                   
066000     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTORA                              
066100                                      AFCLTA                              
066200                                      ALINEA                              
066300                                      APHYUA                              
066400                                      AIPB1A                              
066500                                      AIPB2A                              
066600                                      AIPB3A                              
066700                                      AIPB4A                              
066800                                      AIPE1A                              
066900                                      AIPE2A                              
067000                                      AIPE3A                              
067100                                      AIPE4A.                             
067200     MOVE DP015-GREEN              TO ASTORC                              
067300                                      AFCLTC                              
067400                                      ALINEC                              
067500                                      APHYUC                              
067600                                      AIPB1C                              
067700                                      AIPB2C                              
067800                                      AIPB3C                              
067900                                      AIPB4C                              
068000                                      AIPE1C                              
068100                                      AIPE2C                              
068200                                      AIPE3C                              
068300                                      AIPE4C.                             
068400     MOVE DP015-UNDERLINE          TO ASTORH                              
068500                                      AFCLTH                              
068600                                      ALINEH                              
068700                                      APHYUH                              
068800                                      AIPB1H                              
068900                                      AIPB2H                              
069000                                      AIPB3H                              
069100                                      AIPB4H                              
069200                                      AIPE1H                              
069300                                      AIPE2H                              
069400                                      AIPE3H                              
069500                                      AIPE4H.                             
069600                                                                          
069700                                                                          
069800*----------------------------------------------------------------*        
069900*    BASIC FIELD EDITS                                           *        
070000*    *****************                                           *        
070100*                                                                *        
070200*    FIELDS ARE EDITED IN REVERSE ORDER OF HOW THEY APPEAR ON    *        
070300*    THE SCREEN.  IN THIS WAY, FOR EACH ERROR ENCOUNTERED, THE   *        
070400*    ERROR MESSAGE AND CURSOR POSITIONING WILL OVERLAY ANY       *        
070500*    PREVIOUS ERROR MESSAGE AND CURSOR POSITIONING, UNTIL THE    *        
070600*    ERROR MESSAGE AND CURSOR POSITIONING ARE SET FOR THE FIRST  *        
070700*    ERROR ENCOUNTERED ON THE SCREEN.                            *        
070800*                                                                *        
070900*    WHEN ALL FIELDS HAVE PASSED THEIR EDITS, THE CROSSCHECK     *        
071000*    EDITS ARE PERFORMED ON THE VALUES STORED IN THE "TABLE"     *        
071100*    FORMAT PORTION OF THE COMMAREA.                             *        
071200*                                                                *        
071300*    EDITS:                                                      *        
071400*    * LOCATION (STORE AND FACILITY) MUST BE ENTERED.            *        
071500*      LOCATION EDITS ------------------------------------------ *        
071600*      - STORE NUMBER MUST BE ON TABLE 'XIT_LOC'.                *        
071700*      - FACILITY MUST BE NUMERIC.                               *        
071800*    * IP ADDRESS RANGE (BEGIN AND END) AND/OR                   *        
071900*      VTAM (LINE AND UNIT) MUST ALSO BE ENTERED.                *        
072000*      IP ADDRESS RANGE EDITS (IF ENTERED) --------------------- *        
072100*      - IP ADDRESS OCTETS MUST BE NUMERIC AND EACH OCTET'S      *        
072200*        VALUE MUST BE 000-255 INCLUSIVE.                        *        
072300*      - THE ENDING IP ADDRESS MUST BE GREATER THAN OR           *        
072400*        EQUAL TO THE BEGINNING IP ADDRESS.                      *        
072500*      VTAM EDITS (IF ENTERED) --------------------------------- *        
072600*      - LINE MUST BE 2 POSITIONS.                               *        
072700*      - UNIT MUST BE 2 POSITIONS AND A VALID HEX VALUE.         *        
072800*----------------------------------------------------------------*        
072900 3220-INDIVIDUAL-FIELD-EDITS.                                             
073000*    *************************************************************        
073100*    * IP ADDRESS RANGE: END OF RANGE, OCTET4                    *        
073200*    *************************************************************        
073300     IF  ASC-IP-ADDR-E-OCTET4 = SPACE                                     
073400         CONTINUE                                                         
073500     ELSE                                                                 
073600         MOVE ASC-IP-ADDR-E-OCTET4 TO DP010I-UNEDITED-FIELD               
073700         MOVE LENGTH OF ASC-IP-ADDR-E-OCTET4                              
073800                                  TO DP010I-MAXIMUM-DIGITS                
073900         MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS              
074000         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
074100                                  TO TRUE                                 
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
074300         IF  DP010I-ERROR-DETECTED                                        
074400             SET PS-ERROR         TO TRUE                                 
074500             SET DP020-MSG-FATAL  TO TRUE                                 
074600*            ---- MUST BE NUMERIC                                         
074700             MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                     
074800             MOVE DP015-UNP-ALP-BRT-OFF                                   
074900                                  TO AIPE4A                               
075000             MOVE DP015-RED       TO AIPE4C                               
075100             MOVE DP015-REVERSE   TO AIPE4H                               
075200             MOVE -1              TO AIPE4L                               
075300             SET DP030-SET-CURSOR-APPL-1                                  
075400                                  TO TRUE                                 
075500         ELSE                                                             
075600             MOVE DP010I-NUMERIC-FIELD                                    
075700                                  TO ASC-IP-ADDR-E-OCTET4-N               
075800             IF ASC-IP-ADDR-E-OCTET4 < PC-VALID-OCTET-MIN                 
075900             OR ASC-IP-ADDR-E-OCTET4 > PC-VALID-OCTET-MAX                 
076000*                ---- MUST BE VALID VALUE, 000 - 255 INCLUSIVE            
076100                 SET PS-ERROR         TO TRUE                             
076200                 MOVE PC-TSYMSG-01285 TO DP020-MSG-NUMBER                 
076300                 MOVE DP015-UNP-ALP-BRT-OFF                               
076400                                      TO AIPE4A                           
076500                 MOVE DP015-RED       TO AIPE4C                           
076600                 MOVE DP015-REVERSE   TO AIPE4H                           
076700                 MOVE -1              TO AIPE4L                           
076800                 SET DP030-SET-CURSOR-APPL-1                              
076900                                      TO TRUE                             
077000             ELSE                                                         
077100                 CONTINUE                                                 
077200         END-IF                                                           
077300     END-IF.                                                              
077400                                                                          
077500                                                                          
077600*    *************************************************************        
077700*    * IP ADDRESS RANGE: END OF RANGE, OCTET3                    *        
077800*    *************************************************************        
077900     IF  ASC-IP-ADDR-E-OCTET3 = SPACE                                     
078000         CONTINUE                                                         
078100     ELSE                                                                 
078200         MOVE ASC-IP-ADDR-E-OCTET3 TO DP010I-UNEDITED-FIELD               
078300         MOVE LENGTH OF ASC-IP-ADDR-E-OCTET3                              
078400                                  TO DP010I-MAXIMUM-DIGITS                
078500         MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS              
078600         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
078700                                  TO TRUE                                 
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
078900         IF  DP010I-ERROR-DETECTED                                        
079000             SET PS-ERROR         TO TRUE                                 
079100             SET DP020-MSG-FATAL  TO TRUE                                 
079200*            ---- MUST BE NUMERIC                                         
079300             MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                     
079400             MOVE DP015-UNP-ALP-BRT-OFF                                   
079500                                  TO AIPE3A                               
079600             MOVE DP015-RED       TO AIPE3C                               
079700             MOVE DP015-REVERSE   TO AIPE3H                               
079800             MOVE -1              TO AIPE3L                               
079900             SET DP030-SET-CURSOR-APPL-1                                  
080000                                  TO TRUE                                 
080100         ELSE                                                             
080200             MOVE DP010I-NUMERIC-FIELD                                    
080300                                  TO ASC-IP-ADDR-E-OCTET3-N               
080400             IF ASC-IP-ADDR-E-OCTET3 < PC-VALID-OCTET-MIN                 
080500             OR ASC-IP-ADDR-E-OCTET3 > PC-VALID-OCTET-MAX                 
080600                 SET PS-ERROR         TO TRUE                             
080700                 SET DP020-MSG-FATAL  TO TRUE                             
080800*                ---- MUST BE VALID VALUE, 000 - 255 INCLUSIVE            
080900                 MOVE PC-TSYMSG-01285 TO DP020-MSG-NUMBER                 
081000                 MOVE DP015-UNP-ALP-BRT-OFF                               
081100                                      TO AIPE3A                           
081200                 MOVE DP015-RED       TO AIPE3C                           
081300                 MOVE DP015-REVERSE   TO AIPE3H                           
081400                 MOVE -1              TO AIPE3L                           
081500                 SET DP030-SET-CURSOR-APPL-1                              
081600                                      TO TRUE                             
081700             ELSE                                                         
081800                 CONTINUE                                                 
081900         END-IF                                                           
082000     END-IF.                                                              
082100                                                                          
082200                                                                          
082300*    *************************************************************        
082400*    * IP ADDRESS RANGE: END OF RANGE, OCTET2                    *        
082500*    *************************************************************        
082600     IF  ASC-IP-ADDR-E-OCTET2 = SPACE                                     
082700         CONTINUE                                                         
082800     ELSE                                                                 
082900         MOVE ASC-IP-ADDR-E-OCTET2 TO DP010I-UNEDITED-FIELD               
083000         MOVE LENGTH OF ASC-IP-ADDR-E-OCTET2                              
083100                                  TO DP010I-MAXIMUM-DIGITS                
083200         MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS              
083300         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
083400                                  TO TRUE                                 
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
083600         IF  DP010I-ERROR-DETECTED                                        
083700             SET PS-ERROR         TO TRUE                                 
083800             SET DP020-MSG-FATAL  TO TRUE                                 
083900*            ---- MUST BE NUMERIC                                         
084000             MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                     
084100             MOVE DP015-UNP-ALP-BRT-OFF                                   
084200                                  TO AIPE2A                               
084300             MOVE DP015-RED       TO AIPE2C                               
084400             MOVE DP015-REVERSE   TO AIPE2H                               
084500             MOVE -1              TO AIPE2L                               
084600             SET DP030-SET-CURSOR-APPL-1                                  
084700                                  TO TRUE                                 
084800         ELSE                                                             
084900             MOVE DP010I-NUMERIC-FIELD                                    
085000                                  TO ASC-IP-ADDR-E-OCTET2-N               
085100             IF ASC-IP-ADDR-E-OCTET2 < PC-VALID-OCTET-MIN                 
085200             OR ASC-IP-ADDR-E-OCTET2 > PC-VALID-OCTET-MAX                 
085300                 SET PS-ERROR         TO TRUE                             
085400                 SET DP020-MSG-FATAL  TO TRUE                             
085500*                ---- MUST BE VALID VALUE, 000 - 255 INCLUSIVE            
085600                 MOVE PC-TSYMSG-01285 TO DP020-MSG-NUMBER                 
085700                 MOVE DP015-UNP-ALP-BRT-OFF                               
085800                                      TO AIPE2A                           
085900                 MOVE DP015-RED       TO AIPE2C                           
086000                 MOVE DP015-REVERSE   TO AIPE2H                           
086100                 MOVE -1              TO AIPE2L                           
086200                 SET DP030-SET-CURSOR-APPL-1                              
086300                                      TO TRUE                             
086400             ELSE                                                         
086500                 CONTINUE                                                 
086600         END-IF                                                           
086700     END-IF.                                                              
086800                                                                          
086900                                                                          
087000                                                                          
087100*    *************************************************************        
087200*    * IP ADDRESS RANGE: END OF RANGE, OCTET1                    *        
087300*    *************************************************************        
087400     IF  ASC-IP-ADDR-E-OCTET1 = SPACE                                     
087500         CONTINUE                                                         
087600     ELSE                                                                 
087700         MOVE ASC-IP-ADDR-E-OCTET1 TO DP010I-UNEDITED-FIELD               
087800         MOVE LENGTH OF ASC-IP-ADDR-E-OCTET1                              
087900                                  TO DP010I-MAXIMUM-DIGITS                
088000         MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS              
088100         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
088200                                  TO TRUE                                 
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
088400         IF  DP010I-ERROR-DETECTED                                        
088500             SET PS-ERROR         TO TRUE                                 
088600             SET DP020-MSG-FATAL  TO TRUE                                 
088700*            ---- MUST BE NUMERIC                                         
088800             MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                     
088900             MOVE DP015-UNP-ALP-BRT-OFF                                   
089000                                  TO AIPE1A                               
089100             MOVE DP015-RED       TO AIPE1C                               
089200             MOVE DP015-REVERSE   TO AIPE1H                               
089300             MOVE -1              TO AIPE1L                               
089400             SET DP030-SET-CURSOR-APPL-1                                  
089500                                  TO TRUE                                 
089600         ELSE                                                             
089700             MOVE DP010I-NUMERIC-FIELD                                    
089800                                  TO ASC-IP-ADDR-E-OCTET1-N               
089900             IF ASC-IP-ADDR-E-OCTET1 < PC-VALID-OCTET-MIN                 
090000             OR ASC-IP-ADDR-E-OCTET1 > PC-VALID-OCTET-MAX                 
090100                 SET PS-ERROR         TO TRUE                             
090200                 SET DP020-MSG-FATAL  TO TRUE                             
090300*                ---- MUST BE VALID VALUE, 000 - 255 INCLUSIVE            
090400                 MOVE PC-TSYMSG-01285 TO DP020-MSG-NUMBER                 
090500                 MOVE DP015-UNP-ALP-BRT-OFF                               
090600                                      TO AIPE1A                           
090700                 MOVE DP015-RED       TO AIPE1C                           
090800                 MOVE DP015-REVERSE   TO AIPE1H                           
090900                 MOVE -1              TO AIPE1L                           
091000                 SET DP030-SET-CURSOR-APPL-1                              
091100                                      TO TRUE                             
091200             ELSE                                                         
091300                 CONTINUE                                                 
091400         END-IF                                                           
091500     END-IF.                                                              
091600                                                                          
091700                                                                          
091800*----------------------------------------------------------------*        
091900* THIS IS A CROSS-CHECK EDIT, BUT TO THE USER OF THIS SCREEN,    *        
092000* THIS IS PROBABLY PERCEIVED AS ONE FIELD...                     *        
092100* IF ONE OCTET OF AN IP ADDRESS IS ENTERED, ALL OCTETS MUST      *        
092200*    BE ENTERED (IE: THE FULL IP ADDRESS MUST BE ENTERED).       *        
092300*----------------------------------------------------------------*        
092400     IF  ASC-IP-ADDR-E-OCTET1 = SPACE                                     
092500     AND ASC-IP-ADDR-E-OCTET2 = SPACE                                     
092600     AND ASC-IP-ADDR-E-OCTET3 = SPACE                                     
092700     AND ASC-IP-ADDR-E-OCTET4 = SPACE                                     
092800         CONTINUE                                                         
092900     ELSE                                                                 
093000         IF  ASC-IP-ADDR-E-OCTET1 = SPACE                                 
093100         OR  ASC-IP-ADDR-E-OCTET2 = SPACE                                 
093200         OR  ASC-IP-ADDR-E-OCTET3 = SPACE                                 
093300         OR  ASC-IP-ADDR-E-OCTET4 = SPACE                                 
093400             SET PS-ERROR         TO TRUE                                 
093500             SET DP020-MSG-FATAL  TO TRUE                                 
093600*            ---- INVALID VALUE                                           
093700             MOVE PC-TSYMSG-00005 TO DP020-MSG-NUMBER                     
093800             MOVE DP015-UNP-ALP-BRT-OFF                                   
093900                                  TO AIPE1A                               
094000                                     AIPE2A                               
094100                                     AIPE3A                               
094200                                     AIPE4A                               
094300             MOVE DP015-RED       TO AIPE1C                               
094400                                     AIPE2C                               
094500                                     AIPE3C                               
094600                                     AIPE4C                               
094700             MOVE DP015-REVERSE   TO AIPE1H                               
094800                                     AIPE2H                               
094900                                     AIPE3H                               
095000                                     AIPE4H                               
095100             SET DP030-SET-CURSOR-APPL-1                                  
095200                                  TO TRUE                                 
095300             IF  ASC-IP-ADDR-E-OCTET4 = SPACE                             
095400                 MOVE -1          TO AIPE4L                               
095500             END-IF                                                       
095600             IF  ASC-IP-ADDR-E-OCTET3 = SPACE                             
095700                 MOVE -1          TO AIPE3L                               
095800             END-IF                                                       
095900             IF  ASC-IP-ADDR-E-OCTET2 = SPACE                             
096000                 MOVE -1          TO AIPE2L                               
096100             END-IF                                                       
096200             IF  ASC-IP-ADDR-E-OCTET1 = SPACE                             
096300                 MOVE -1          TO AIPE1L                               
096400             END-IF                                                       
096500         ELSE                                                             
096600*            *****************************************************        
096700*            * ALL OCTETS POPULATED, SO ADD THE DOTS IN-BETWEEN  *        
096800*            *****************************************************        
096900             MOVE PC-DOT          TO  ASC-IP-ADDR-E-DOT1                  
097000                                      ASC-IP-ADDR-E-DOT2                  
097100                                      ASC-IP-ADDR-E-DOT3                  
097200         END-IF                                                           
097300     END-IF.                                                              
097400                                                                          
097500     IF ASC-IP-ADDR-END    = PC-ZEROS-IP-ADDR                             
097600     OR ASC-IP-ADDR-END    = PC-ZERO-IP-ADDR                              
097700     OR ASC-IP-ADDR-END    = PC-SPACE-IP-ADDR                             
097800         MOVE PC-SPACES-IP-ADDR TO ASC-IP-ADDR-END.                       
097900                                                                          
098000                                                                          
098100*    *************************************************************        
098200*    * IP ADDRESS RANGE: BEGINNING OF RANGE, OCTET4              *        
098300*    *************************************************************        
098400     IF  ASC-IP-ADDR-B-OCTET4 = SPACE                                     
098500         CONTINUE                                                         
098600     ELSE                                                                 
098700         MOVE ASC-IP-ADDR-B-OCTET4 TO DP010I-UNEDITED-FIELD               
098800         MOVE LENGTH OF ASC-IP-ADDR-B-OCTET4                              
098900                                  TO DP010I-MAXIMUM-DIGITS                
099000         MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS              
099100         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
099200                                  TO TRUE                                 
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
099400         IF  DP010I-ERROR-DETECTED                                        
099500             SET PS-ERROR         TO TRUE                                 
099600             SET DP020-MSG-FATAL  TO TRUE                                 
099700*            ---- MUST BE NUMERIC                                         
099800             MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                     
099900             MOVE DP015-UNP-ALP-BRT-OFF                                   
100000                                  TO AIPB4A                               
100100             MOVE DP015-RED       TO AIPB4C                               
100200             MOVE DP015-REVERSE   TO AIPB4H                               
100300             MOVE -1              TO AIPB4L                               
100400             SET DP030-SET-CURSOR-APPL-1                                  
100500                                  TO TRUE                                 
100600         ELSE                                                             
100700             MOVE DP010I-NUMERIC-FIELD                                    
100800                                  TO ASC-IP-ADDR-B-OCTET4-N               
100900             IF ASC-IP-ADDR-B-OCTET4 < PC-VALID-OCTET-MIN                 
101000             OR ASC-IP-ADDR-B-OCTET4 > PC-VALID-OCTET-MAX                 
101100                 SET PS-ERROR         TO TRUE                             
101200                 SET DP020-MSG-FATAL  TO TRUE                             
101300*                ---- MUST BE VALID VALUE, 000 - 255 INCLUSIVE            
101400                 MOVE PC-TSYMSG-01285 TO DP020-MSG-NUMBER                 
101500                 MOVE DP015-UNP-ALP-BRT-OFF                               
101600                                      TO AIPB4A                           
101700                 MOVE DP015-RED       TO AIPB4C                           
101800                 MOVE DP015-REVERSE   TO AIPB4H                           
101900                 MOVE -1              TO AIPB4L                           
102000                 SET DP030-SET-CURSOR-APPL-1                              
102100                                      TO TRUE                             
102200             ELSE                                                         
102300                 CONTINUE                                                 
102400         END-IF                                                           
102500     END-IF.                                                              
102600                                                                          
102700                                                                          
102800*    *************************************************************        
102900*    * IP ADDRESS RANGE: BEGINNING OF RANGE, OCTET3              *        
103000*    *************************************************************        
103100     IF  ASC-IP-ADDR-B-OCTET3 = SPACE                                     
103200         CONTINUE                                                         
103300     ELSE                                                                 
103400         MOVE ASC-IP-ADDR-B-OCTET3 TO DP010I-UNEDITED-FIELD               
103500         MOVE LENGTH OF ASC-IP-ADDR-B-OCTET3                              
103600                                  TO DP010I-MAXIMUM-DIGITS                
103700         MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS              
103800         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
103900                                  TO TRUE                                 
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
104100         IF  DP010I-ERROR-DETECTED                                        
104200             SET PS-ERROR         TO TRUE                                 
104300             SET DP020-MSG-FATAL  TO TRUE                                 
104400*            ---- MUST BE NUMERIC                                         
104500             MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                     
104600             MOVE DP015-UNP-ALP-BRT-OFF                                   
104700                                  TO AIPB3A                               
104800             MOVE DP015-RED       TO AIPB3C                               
104900             MOVE DP015-REVERSE   TO AIPB3H                               
105000             MOVE -1              TO AIPB3L                               
105100             SET DP030-SET-CURSOR-APPL-1                                  
105200                                  TO TRUE                                 
105300         ELSE                                                             
105400             MOVE DP010I-NUMERIC-FIELD                                    
105500                                  TO ASC-IP-ADDR-B-OCTET3-N               
105600             IF ASC-IP-ADDR-B-OCTET3 < PC-VALID-OCTET-MIN                 
105700             OR ASC-IP-ADDR-B-OCTET3 > PC-VALID-OCTET-MAX                 
105800                 SET PS-ERROR         TO TRUE                             
105900                 SET DP020-MSG-FATAL  TO TRUE                             
106000*                ---- MUST BE VALID VALUE, 000 - 255 INCLUSIVE            
106100                 MOVE PC-TSYMSG-01285 TO DP020-MSG-NUMBER                 
106200                 MOVE DP015-UNP-ALP-BRT-OFF                               
106300                                      TO AIPB3A                           
106400                 MOVE DP015-RED       TO AIPB3C                           
106500                 MOVE DP015-REVERSE   TO AIPB3H                           
106600                 MOVE -1              TO AIPB3L                           
106700                 SET DP030-SET-CURSOR-APPL-1                              
106800                                      TO TRUE                             
106900             ELSE                                                         
107000                 CONTINUE                                                 
107100         END-IF                                                           
107200     END-IF.                                                              
107300                                                                          
107400                                                                          
107500*    *************************************************************        
107600*    * IP ADDRESS RANGE: BEGINNING OF RANGE, OCTET2              *        
107700*    *************************************************************        
107800     IF  ASC-IP-ADDR-B-OCTET2 = SPACE                                     
107900         CONTINUE                                                         
108000     ELSE                                                                 
108100         MOVE ASC-IP-ADDR-B-OCTET2 TO DP010I-UNEDITED-FIELD               
108200         MOVE LENGTH OF ASC-IP-ADDR-B-OCTET2                              
108300                                  TO DP010I-MAXIMUM-DIGITS                
108400         MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS              
108500         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
108600                                  TO TRUE                                 
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
108800         IF  DP010I-ERROR-DETECTED                                        
108900             SET PS-ERROR         TO TRUE                                 
109000             SET DP020-MSG-FATAL  TO TRUE                                 
109100*            ---- MUST BE NUMERIC                                         
109200             MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                     
109300             MOVE DP015-UNP-ALP-BRT-OFF                                   
109400                                  TO AIPB2A                               
109500             MOVE DP015-RED       TO AIPB2C                               
109600             MOVE DP015-REVERSE   TO AIPB2H                               
109700             MOVE -1              TO AIPB2L                               
109800             SET DP030-SET-CURSOR-APPL-1                                  
109900                                  TO TRUE                                 
110000         ELSE                                                             
110100             MOVE DP010I-NUMERIC-FIELD                                    
110200                                  TO ASC-IP-ADDR-B-OCTET2-N               
110300             IF ASC-IP-ADDR-B-OCTET2 < PC-VALID-OCTET-MIN                 
110400             OR ASC-IP-ADDR-B-OCTET2 > PC-VALID-OCTET-MAX                 
110500                 SET PS-ERROR         TO TRUE                             
110600                 SET DP020-MSG-FATAL  TO TRUE                             
110700*                ---- MUST BE VALID VALUE, 000 - 255 INCLUSIVE            
110800                 MOVE PC-TSYMSG-01285 TO DP020-MSG-NUMBER                 
110900                 MOVE DP015-UNP-ALP-BRT-OFF                               
111000                                      TO AIPB2A                           
111100                 MOVE DP015-RED       TO AIPB2C                           
111200                 MOVE DP015-REVERSE   TO AIPB2H                           
111300                 MOVE -1              TO AIPB2L                           
111400                 SET DP030-SET-CURSOR-APPL-1                              
111500                                      TO TRUE                             
111600             ELSE                                                         
111700                 CONTINUE                                                 
111800         END-IF                                                           
111900     END-IF.                                                              
112000                                                                          
112100                                                                          
112200*    *************************************************************        
112300*    * IP ADDRESS RANGE: BEGINNING OF RANGE, OCTET1              *        
112400*    *************************************************************        
112500     IF  ASC-IP-ADDR-B-OCTET1 = SPACE                                     
112600         CONTINUE                                                         
112700     ELSE                                                                 
112800         MOVE ASC-IP-ADDR-B-OCTET1 TO DP010I-UNEDITED-FIELD               
112900         MOVE LENGTH OF ASC-IP-ADDR-B-OCTET1                              
113000                                  TO DP010I-MAXIMUM-DIGITS                
113100         MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS              
113200         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
113300                                  TO TRUE                                 
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
113500         IF  DP010I-ERROR-DETECTED                                        
113600             SET PS-ERROR         TO TRUE                                 
113700             SET DP020-MSG-FATAL  TO TRUE                                 
113800*            ---- MUST BE NUMERIC                                         
113900             MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                     
114000             MOVE DP015-UNP-ALP-BRT-OFF                                   
114100                                  TO AIPB1A                               
114200             MOVE DP015-RED       TO AIPB1C                               
114300             MOVE DP015-REVERSE   TO AIPB1H                               
114400             MOVE -1              TO AIPB1L                               
114500             SET DP030-SET-CURSOR-APPL-1                                  
114600                                  TO TRUE                                 
114700         ELSE                                                             
114800             MOVE DP010I-NUMERIC-FIELD                                    
114900                                  TO ASC-IP-ADDR-B-OCTET1-N               
115000             IF ASC-IP-ADDR-B-OCTET1 < PC-VALID-OCTET-MIN                 
115100             OR ASC-IP-ADDR-B-OCTET1 > PC-VALID-OCTET-MAX                 
115200                 SET PS-ERROR         TO TRUE                             
115300                 SET DP020-MSG-FATAL  TO TRUE                             
115400*                ---- MUST BE VALID VALUE, 000 - 255 INCLUSIVE            
115500                 MOVE PC-TSYMSG-01285 TO DP020-MSG-NUMBER                 
115600                 MOVE DP015-UNP-ALP-BRT-OFF                               
115700                                      TO AIPB1A                           
115800                 MOVE DP015-RED       TO AIPB1C                           
115900                 MOVE DP015-REVERSE   TO AIPB1H                           
116000                 MOVE -1              TO AIPB1L                           
116100                 SET DP030-SET-CURSOR-APPL-1                              
116200                                      TO TRUE                             
116300             ELSE                                                         
116400                 CONTINUE                                                 
116500         END-IF                                                           
116600     END-IF.                                                              
116700                                                                          
116800                                                                          
116900*----------------------------------------------------------------*        
117000* THIS IS A CROSS-CHECK EDIT, BUT TO THE USER OF THIS SCREEN,    *        
117100* THIS IS PROBABLY PERCEIVED AS ONE FIELD...                     *        
117200* IF ONE OCTET OF AN IP ADDRESS IS ENTERED, ALL OCTETS MUST      *        
117300*    BE ENTERED (IE: THE FULL IP ADDRESS MUST BE ENTERED).       *        
117400*----------------------------------------------------------------*        
117500     IF  ASC-IP-ADDR-B-OCTET1 = SPACE                                     
117600     AND ASC-IP-ADDR-B-OCTET2 = SPACE                                     
117700     AND ASC-IP-ADDR-B-OCTET3 = SPACE                                     
117800     AND ASC-IP-ADDR-B-OCTET4 = SPACE                                     
117900         CONTINUE                                                         
118000     ELSE                                                                 
118100         IF  ASC-IP-ADDR-B-OCTET1 = SPACE                                 
118200         OR  ASC-IP-ADDR-B-OCTET2 = SPACE                                 
118300         OR  ASC-IP-ADDR-B-OCTET3 = SPACE                                 
118400         OR  ASC-IP-ADDR-B-OCTET4 = SPACE                                 
118500             SET PS-ERROR         TO TRUE                                 
118600             SET DP020-MSG-FATAL  TO TRUE                                 
118700*            ---- INVALID VALUE                                           
118800             MOVE PC-TSYMSG-00005 TO DP020-MSG-NUMBER                     
118900             MOVE DP015-UNP-ALP-BRT-OFF                                   
119000                                  TO AIPB1A                               
119100                                     AIPB2A                               
119200                                     AIPB3A                               
119300                                     AIPB4A                               
119400             MOVE DP015-RED       TO AIPB1C                               
119500                                     AIPB2C                               
119600                                     AIPB3C                               
119700                                     AIPB4C                               
119800             MOVE DP015-REVERSE   TO AIPB1H                               
119900                                     AIPB2H                               
120000                                     AIPB3H                               
120100                                     AIPB4H                               
120200             SET DP030-SET-CURSOR-APPL-1                                  
120300                                  TO TRUE                                 
120400             IF  ASC-IP-ADDR-B-OCTET4 = SPACE                             
120500                 MOVE -1          TO AIPB4L                               
120600             END-IF                                                       
120700             IF  ASC-IP-ADDR-B-OCTET3 = SPACE                             
120800                 MOVE -1          TO AIPB3L                               
120900             END-IF                                                       
121000             IF  ASC-IP-ADDR-B-OCTET2 = SPACE                             
121100                 MOVE -1          TO AIPB2L                               
121200             END-IF                                                       
121300             IF  ASC-IP-ADDR-B-OCTET1 = SPACE                             
121400                 MOVE -1          TO AIPB1L                               
121500             END-IF                                                       
121600         ELSE                                                             
121700*            *****************************************************        
121800*            * ALL OCTETS POPULATED, SO ADD THE DOTS IN-BETWEEN  *        
121900*            *****************************************************        
122000             MOVE PC-DOT          TO  ASC-IP-ADDR-B-DOT1                  
122100                                      ASC-IP-ADDR-B-DOT2                  
122200                                      ASC-IP-ADDR-B-DOT3                  
122300         END-IF                                                           
122400     END-IF.                                                              
122500                                                                          
122600     IF ASC-IP-ADDR-BEGIN  = PC-ZEROS-IP-ADDR                             
122700     OR ASC-IP-ADDR-BEGIN  = PC-ZERO-IP-ADDR                              
122800     OR ASC-IP-ADDR-BEGIN  = PC-SPACE-IP-ADDR                             
122900         MOVE PC-SPACES-IP-ADDR TO ASC-IP-ADDR-BEGIN.                     
123000                                                                          
123100                                                                          
123200*    *************************************************************        
123300*    * VTAM:     UNIT                                            *        
123400*    *************************************************************        
123500     IF ASC-PHYSICAL-UNIT  =  SPACE                                       
123600         CONTINUE                                                         
123700     ELSE                                                                 
123800         MOVE ASC-PHYSICAL-UNIT1  TO  PC-VALIDATE-PHYU1                   
123900         MOVE ASC-PHYSICAL-UNIT2  TO  PC-VALIDATE-PHYU2                   
124000         IF  PC-VALID-HEX-CHAR1                                           
124100         AND PC-VALID-HEX-CHAR2                                           
124200             CONTINUE                                                     
124300         ELSE                                                             
124400             SET PS-ERROR         TO TRUE                                 
124500             SET DP020-MSG-FATAL  TO TRUE                                 
124600*            ---- EACH CHARACTER MUST BE 0-9 OR A-F                       
124700             MOVE PC-TSYMSG-01282 TO DP020-MSG-NUMBER                     
124800             MOVE DP015-UNP-ALP-BRT-OFF                                   
124900                                  TO APHYUA                               
125000             MOVE DP015-RED       TO APHYUC                               
125100             MOVE DP015-REVERSE   TO APHYUH                               
125200             MOVE -1              TO APHYUL                               
125300             SET DP030-SET-CURSOR-APPL-1                                  
125400                                  TO TRUE                                 
125500         END-IF                                                           
125600     END-IF.                                                              
125700                                                                          
125800*    *************************************************************        
125900*    * VTAM:     LINE                                            *        
126000*    *************************************************************        
126100     IF ASC-LINE  =  SPACE                                                
126200         CONTINUE                                                         
126300     ELSE                                                                 
126400         IF  ASC-LINE1  =  SPACE                                          
126500         OR  ASC-LINE2  =  SPACE                                          
126600             SET PS-ERROR         TO TRUE                                 
126700             SET DP020-MSG-FATAL  TO TRUE                                 
126800*            ---- ENTRY MUST BE 2 POSITIONS                               
126900             MOVE PC-TSYMSG-01283 TO DP020-MSG-NUMBER                     
127000             MOVE DP015-UNP-ALP-BRT-OFF                                   
127100                                  TO ALINEA                               
127200             MOVE DP015-RED       TO ALINEC                               
127300             MOVE DP015-REVERSE   TO ALINEH                               
127400             MOVE -1              TO ALINEL                               
127500             SET DP030-SET-CURSOR-APPL-1                                  
127600                                  TO TRUE                                 
127700         END-IF                                                           
127800     END-IF.                                                              
127900                                                                          
128000*    *************************************************************        
128100*    * LOCATION: FACILITY                                        *        
128200*    *************************************************************        
128300     IF  ASC-FCLTY  =  SPACE                                              
128400     OR  ASC-FCLTY  =  ZEROES                                             
128500         SET PS-ERROR         TO TRUE                                     
128600         SET DP020-MSG-FATAL  TO TRUE                                     
128700*        ---- REQUIRED ENTRY                                              
128800         MOVE PC-TSYMSG-00007 TO DP020-MSG-NUMBER                         
128900         MOVE DP015-UNP-ALP-BRT-OFF                                       
129000                              TO AFCLTA                                   
129100         MOVE DP015-RED       TO AFCLTC                                   
129200         MOVE DP015-REVERSE   TO AFCLTH                                   
129300         MOVE -1              TO AFCLTL                                   
129400         SET DP030-SET-CURSOR-APPL-1                                      
129500                              TO TRUE                                     
129600     ELSE                                                                 
129700         MOVE ASC-FCLTY           TO DP010I-UNEDITED-FIELD                
129800         MOVE LENGTH OF ASC-FCLTY TO DP010I-MAXIMUM-DIGITS                
129900         MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS              
130000         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
130100                                  TO TRUE                                 
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
130300         IF  DP010I-ERROR-DETECTED                                        
130400             SET PS-ERROR         TO TRUE                                 
130500             SET DP020-MSG-FATAL  TO TRUE                                 
130600*            ---- MUST BE NUMERIC                                         
130700             MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                     
130800             MOVE DP015-UNP-ALP-BRT-OFF                                   
130900                                  TO AFCLTA                               
131000             MOVE DP015-RED       TO AFCLTC                               
131100             MOVE DP015-REVERSE   TO AFCLTH                               
131200             MOVE -1              TO AFCLTL                               
131300             SET DP030-SET-CURSOR-APPL-1                                  
131400                                  TO TRUE                                 
131500         ELSE                                                             
131600             MOVE DP010I-NUMERIC-FIELD                                    
131700                                  TO ASC-FCLTY-N                          
131800         END-IF                                                           
131900     END-IF.                                                              
132000                                                                          
132100                                                                          
132200*    *************************************************************        
132300*    * LOCATION: STORE                                           *        
132400*    *************************************************************        
132500     IF ASC-STORE = SPACE                                                 
132600     OR ASC-STORE = ZEROES                                                
132700         SET PS-ERROR              TO TRUE                                
132800         SET DP020-MSG-FATAL       TO TRUE                                
132900*        ---- REQUIRED FIELD -----                                        
133000         MOVE PC-TSYMSG-00007      TO DP020-MSG-NUMBER                    
133100         MOVE DP015-UNP-ALP-BRT-OFF                                       
133200                                   TO ASTORA                              
133300         MOVE DP015-RED            TO ASTORC                              
133400         MOVE DP015-REVERSE        TO ASTORH                              
133500         MOVE -1                   TO ASTORL                              
133600         SET DP030-SET-CURSOR-APPL-1                                      
133700                                   TO TRUE                                
133800     ELSE                                                                 
133900         PERFORM 3221-VERIFY-STORE-NUMERIC                                
134000         IF  PS-ERROR                                                     
134100             CONTINUE                                                     
134200         ELSE                                                             
134300             MOVE ZERO TO SQLCODE                                         
134500             MOVE ASC-STORE-N          TO DK-LOC-NBR                      
134600             PERFORM 3222-VERIFY-STORE-EXISTS                             
134700             IF SQLCODE = +100                                            
134800                 SET PS-ERROR                                             
134900                     DP020-MSG-FATAL   TO TRUE                            
135000*                ---- STORE  DOESN'T EXIST ----                           
135100                 MOVE PC-TSYMSG-00017  TO DP020-MSG-NUMBER                
135200                 MOVE DP015-UNP-ALP-BRT-OFF                               
135300                                       TO ASTORA                          
135400                 MOVE DP015-RED        TO ASTORC                          
135500                 MOVE DP015-REVERSE    TO ASTORH                          
135600                 MOVE -1               TO ASTORL                          
135700                 SET DP030-SET-CURSOR-APPL-1                              
135800                                       TO TRUE                            
135900             END-IF                                                       
136000         END-IF                                                           
136100     END-IF.                                                              
136200                                                                          
136300*----------------------------------------------------------------*        
136400* CALL THE NUMERIC EDIT ROUTINE TO VERIFY STORE ENTRY IS NUMERIC.*        
136500*----------------------------------------------------------------*        
136600 3221-VERIFY-STORE-NUMERIC.                                               
136700     MOVE ASC-STORE           TO DP010I-UNEDITED-FIELD.                   
136800     MOVE LENGTH OF ASC-STORE TO DP010I-MAXIMUM-DIGITS.                   
136900     MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS.                 
137000     SET DP010I-NEGATIVE-NOT-ALLOWED                                      
137100                              TO TRUE.                                    
262023     CALL DP010I-NUMERIC-EDIT-ROUTINE                                     
262023          USING DP010I-NUMERIC-EDIT-AREA.                                 
137300     IF  DP010I-ERROR-DETECTED                                            
137400         SET PS-ERROR         TO TRUE                                     
137500         SET DP020-MSG-FATAL  TO TRUE                                     
137600*        ---- MUST BE NUMERIC                                             
137700         MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                         
137800         MOVE DP015-UNP-ALP-BRT-OFF                                       
137900                              TO ASTORA                                   
138000         MOVE DP015-RED       TO ASTORC                                   
138100         MOVE DP015-REVERSE   TO ASTORH                                   
138200         MOVE -1              TO ASTORL                                   
138300         SET DP030-SET-CURSOR-APPL-1                                      
138400                              TO TRUE                                     
138500     ELSE                                                                 
138600         MOVE DP010I-NUMERIC-FIELD                                        
138700                              TO ASC-STORE-N                              
138800                                 DK-LOC-NBR                               
138900     END-IF.                                                              
139000                                                                          
139100                                                                          
139200*----------------------------------------------------------------*        
139300* SELECT STORE FROM TABLE TO VERIFY STORE EXISTS.                *        
139400*----------------------------------------------------------------*        
139500 3222-VERIFY-STORE-EXISTS.                                                
139600     EXEC SQL                                                             
139700          SELECT LOC_NBR                                                  
139800           INTO  :XIT00000-LOC-NBR                                        
139900           FROM  XIT_LOC                                                  
140100           WHERE LOC_NBR = :DK-LOC-NBR                                    
140200     END-EXEC.                                                            
140300                                                                          
140400                                                                          
140500*----------------------------------------------------------------*        
140600*    CROSS-CHECK EDITS TO OTHER FIELDS ENTERED ON THE SCREEN     *        
140700*    *****************                                           *        
140800*                                                                *        
140900* IP ADDRESS RANGE CROSS-CHECK EDITS:                            *        
141000* 1) IF ONE IP ADDRESS IS ENTERED, BOTH MUST BE ENTERED.         *        
141100* 2) THE ENDING IP ADDRESS MUST BE GREATER THAN OR               *        
141200*      EQUAL TO THE BEGINNING IP ADDRESS.                        *        
141300*                                                                *        
141400* VTAM CROSS-CHECK EDITS:                                        *        
141500* 1) IF LINE IS ENTERED, UNIT MUST ALSO BE ENTERED;              *        
141600* 2) IF UNIT IS ENTERED, LINE MUST ALSO BE ENTERED.              *        
141700*                                                                *        
141800* VTAM / ADDRESS CROSS-CHECK EDITS:                              *        
141900* 1) EITHER VTAM -OR- ADDRESS RANGE MUST BE ENTERED.             *        
142000* 2) BOTH VTAM -AND- ADDRESS RANGE MAY BE ENTERED.               *        
142100*                                                                *        
142200*----------------------------------------------------------------*        
142300 3300-CROSSCHECK-FIELD-EDITS.                                             
142400*    *************************************************************        
142500*    * CROSSCHECK: IP ADDRESS RANGE (ALL OCTETS)                 *        
142600*    *************************************************************        
142700     IF  (ASC-IP-ADDR-BEGIN = SPACES)                                     
142800     AND (ASC-IP-ADDR-END   = SPACES)                                     
142900         CONTINUE                                                         
143000     ELSE                                                                 
143100         IF (ASC-IP-ADDR-BEGIN = SPACES)                                  
143200         OR (ASC-IP-ADDR-END   = SPACES)                                  
143300             SET PS-ERROR                                                 
143400                 DP020-MSG-FATAL  TO TRUE                                 
143500*            ---- REQUIRED, DUE TO ANOTHER FIELD ENTERED                  
143600             MOVE PC-TSYMSG-01177 TO DP020-MSG-NUMBER                     
143700             MOVE DP015-UNP-ALP-BRT-OFF                                   
143800                                  TO AIPB1A                               
143900                                     AIPB2A                               
144000                                     AIPB3A                               
144100                                     AIPB4A                               
144200                                     AIPE1A                               
144300                                     AIPE2A                               
144400                                     AIPE3A                               
144500                                     AIPE4A                               
144600             MOVE DP015-RED       TO AIPB1C                               
144700                                     AIPB2C                               
144800                                     AIPB3C                               
144900                                     AIPB4C                               
145000                                     AIPE1C                               
145100                                     AIPE2C                               
145200                                     AIPE3C                               
145300                                     AIPE4C                               
145400             MOVE DP015-REVERSE   TO AIPB1H                               
145500                                     AIPB2H                               
145600                                     AIPB3H                               
145700                                     AIPB4H                               
145800                                     AIPE1H                               
145900                                     AIPE2H                               
146000                                     AIPE3H                               
146100                                     AIPE4H                               
146200             MOVE -1              TO AIPB1L                               
146300             SET DP030-SET-CURSOR-APPL-1                                  
146400                                  TO TRUE                                 
146500         ELSE                                                             
146600             IF  ASC-IP-ADDR-BEGIN > ASC-IP-ADDR-END                      
146700                 SET PS-ERROR              TO TRUE                        
146800                 SET DP020-MSG-FATAL       TO TRUE                        
146900*                ---- END OF RANGE PRECEDED START OF RANGE                
147000                 MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                 
147100                 MOVE DP015-UNP-ALP-BRT-OFF                               
147200                                      TO AIPB1A                           
147300                                         AIPB2A                           
147400                                         AIPB3A                           
147500                                         AIPB4A                           
147600                                         AIPE1A                           
147700                                         AIPE2A                           
147800                                         AIPE3A                           
147900                                         AIPE4A                           
148000                 MOVE DP015-RED       TO AIPB1C                           
148100                                         AIPB2C                           
148200                                         AIPB3C                           
148300                                         AIPB4C                           
148400                                         AIPE1C                           
148500                                         AIPE2C                           
148600                                         AIPE3C                           
148700                                         AIPE4C                           
148800                 MOVE DP015-REVERSE   TO AIPB1H                           
148900                                         AIPB2H                           
149000                                         AIPB3H                           
149100                                         AIPB4H                           
149200                                         AIPE1H                           
149300                                         AIPE2H                           
149400                                         AIPE3H                           
149500                                         AIPE4H                           
149600                 MOVE -1              TO AIPB1L                           
149700                 SET DP030-SET-CURSOR-APPL-1                              
149800                                      TO TRUE                             
149900             END-IF                                                       
150000         END-IF                                                           
150100     END-IF.                                                              
150200                                                                          
150300                                                                          
150400*    *************************************************************        
150500*    * CROSSCHECK: VTAM (LINE AND UNIT)                          *        
150600*    *************************************************************        
150700     IF  (ASC-PHYSICAL-UNIT = SPACE   AND                                 
150800          ASC-LINE  =  SPACE)                                             
150900         CONTINUE                                                         
151000     ELSE                                                                 
151100         IF  ASC-PHYSICAL-UNIT = SPACE                                    
151200             SET PS-ERROR         TO TRUE                                 
151300             SET DP020-MSG-FATAL  TO TRUE                                 
151400*            ---- REQUIRED, DUE TO ANOTHER FIELD ENTERED                  
151500             MOVE PC-TSYMSG-01177 TO DP020-MSG-NUMBER                     
151600             MOVE DP015-UNP-ALP-BRT-OFF                                   
151700                                  TO ALINEA                               
151800                                     APHYUA                               
151900             MOVE DP015-RED       TO ALINEC                               
152000                                     APHYUC                               
152100             MOVE DP015-REVERSE   TO ALINEH                               
152200                                     APHYUH                               
152300             MOVE -1              TO APHYUL                               
152400             SET DP030-SET-CURSOR-APPL-1                                  
152500                                  TO TRUE                                 
152600         ELSE                                                             
152700             IF  ASC-LINE  = SPACE                                        
152800                 SET PS-ERROR         TO TRUE                             
152900                 SET DP020-MSG-FATAL  TO TRUE                             
153000*                ---- REQUIRED, DUE TO ANOTHER FIELD ENTERED              
153100                 MOVE PC-TSYMSG-01177 TO DP020-MSG-NUMBER                 
153200                 MOVE DP015-UNP-ALP-BRT-OFF                               
153300                                      TO ALINEA                           
153400                                         APHYUA                           
153500                 MOVE DP015-RED       TO ALINEC                           
153600                                         APHYUC                           
153700                 MOVE DP015-REVERSE   TO ALINEH                           
153800                                         APHYUH                           
153900                 MOVE -1              TO ALINEL                           
154000                 SET DP030-SET-CURSOR-APPL-1                              
154100                                      TO TRUE                             
154200             END-IF                                                       
154300         END-IF                                                           
154400     END-IF.                                                              
154500                                                                          
154600                                                                          
154700*    *************************************************************        
154800*    * CROSSCHECK: VTAM -AND- IP ADDRESS RANGE                   *        
154900*    *************************************************************        
155000     IF  (ASC-PHYSICAL-UNIT     = SPACE   AND                             
155100          ASC-LINE              = SPACE   AND                             
155200          ASC-IP-ADDR-B-OCTET1  = SPACE   AND                             
155300          ASC-IP-ADDR-B-OCTET2  = SPACE   AND                             
155400          ASC-IP-ADDR-B-OCTET3  = SPACE   AND                             
155500          ASC-IP-ADDR-B-OCTET4  = SPACE   AND                             
155600          ASC-IP-ADDR-E-OCTET1  = SPACE   AND                             
155700          ASC-IP-ADDR-E-OCTET2  = SPACE   AND                             
155800          ASC-IP-ADDR-E-OCTET3  = SPACE   AND                             
155900          ASC-IP-ADDR-E-OCTET4  = SPACE)                                  
156000         SET PS-ERROR         TO TRUE                                     
156100         SET DP020-MSG-FATAL  TO TRUE                                     
156200*        ---- REQUIRED, DUE TO ANOTHER FIELD ENTERED                      
156300         MOVE PC-TSYMSG-01177 TO DP020-MSG-NUMBER                         
156400         MOVE DP015-UNP-ALP-BRT-OFF                                       
156500                              TO ALINEA                                   
156600                                 APHYUA                                   
156700                                 AIPB1A                                   
156800                                 AIPB2A                                   
156900                                 AIPB3A                                   
157000                                 AIPB4A                                   
157100                                 AIPE1A                                   
157200                                 AIPE2A                                   
157300                                 AIPE3A                                   
157400                                 AIPE4A                                   
157500         MOVE DP015-RED       TO ALINEC                                   
157600                                 APHYUC                                   
157700                                 AIPB1C                                   
157800                                 AIPB2C                                   
157900                                 AIPB3C                                   
158000                                 AIPB4C                                   
158100                                 AIPE1C                                   
158200                                 AIPE2C                                   
158300                                 AIPE3C                                   
158400                                 AIPE4C                                   
158500         MOVE DP015-REVERSE   TO ALINEH                                   
158600                                 APHYUH                                   
158700                                 AIPB1H                                   
158800                                 AIPB2H                                   
158900                                 AIPB3H                                   
159000                                 AIPB4H                                   
159100                                 AIPE1H                                   
159200                                 AIPE2H                                   
159300                                 AIPE3H                                   
159400                                 AIPE4H                                   
159500         MOVE -1              TO ALINEL                                   
159600         SET DP030-SET-CURSOR-APPL-1                                      
159700                              TO TRUE                                     
159800     END-IF.                                                              
159900                                                                          
160000                                                                          
160100*----------------------------------------------------------------*        
160200*    CROSS-CHECK EDITS TO THE DB2 TABLE                          *        
160300*    *****************                                           *        
160400*                                                                *        
160500* IP ADDRESS RANGE CROSS-CHECK EDITS:                            *        
160600*    COMPARE WITH RANGE(S) ALREADY ON THE TABLE;                 *        
160700*    OVERLAP NOT ALLOWED.                                        *        
160800*                                                                *        
160900* VTAM CROSS-CHECK EDITS:                                        *        
161000*    COMPARE WITH LINE/UNIT COMBINATIONS ALREADY ON THE TABLE;   *        
161100*    DUPLICATE NOT ALLOWED -EXCEPT- FOR SPACES.                  *        
161200*                                                                *        
161300*----------------------------------------------------------------*        
161400 3400-CROSSCHECK-TABLE-EDITS.                                             
161500                                                                          
161600     MOVE ASC-IP-ADDR-BEGIN  TO  DK-IP-ADDR-BEG.                          
161700     MOVE ASC-IP-ADDR-END    TO  DK-IP-ADDR-END.                          
161800     MOVE +0                 TO  PV-COUNT-OVERLAP-IP-ADDR.                
161900     IF  (ASC-IP-ADDR-BEGIN = SPACES)                                     
162000     AND (ASC-IP-ADDR-END   = SPACES)                                     
162100*        *********************************************************        
162200*        * NO ENTRY IN THESE OPTIONAL FIELDS                     *        
162300*        *********************************************************        
162400         CONTINUE                                                         
162500     ELSE                                                                 
162600         PERFORM 3410-CHECK-FOR-OVERLAP                                   
162700         IF PV-COUNT-OVERLAP-IP-ADDR > ZERO                               
162800             SET PS-ERROR                                                 
162900                 DP020-MSG-FATAL  TO TRUE                                 
163000*            ---- ADD WOULD RESULT IN AN OVERLAPPING IP ADDR RANGE        
163100             MOVE PC-TSYMSG-01176 TO DP020-MSG-NUMBER                     
163200             MOVE DP015-UNP-ALP-BRT-OFF                                   
163300                                  TO AIPB1A                               
163400                                     AIPB2A                               
163500                                     AIPB3A                               
163600                                     AIPB4A                               
163700                                     AIPE1A                               
163800                                     AIPE2A                               
163900                                     AIPE3A                               
164000                                     AIPE4A                               
164100             MOVE DP015-RED       TO AIPB1C                               
164200                                     AIPB2C                               
164300                                     AIPB3C                               
164400                                     AIPB4C                               
164500                                     AIPE1C                               
164600                                     AIPE2C                               
164700                                     AIPE3C                               
164800                                     AIPE4C                               
164900             MOVE DP015-REVERSE   TO AIPB1H                               
165000                                     AIPB2H                               
165100                                     AIPB3H                               
165200                                     AIPB4H                               
165300                                     AIPE1H                               
165400                                     AIPE2H                               
165500                                     AIPE3H                               
165600                                     AIPE4H                               
165700             MOVE -1              TO AIPB1L                               
165800             SET DP030-SET-CURSOR-APPL-1                                  
165900                                  TO TRUE                                 
166000         END-IF                                                           
166100     END-IF.                                                              
166200                                                                          
166300                                                                          
166400     MOVE ASC-LINE           TO  DK-LNE-ADDR-NBR.                         
166500     MOVE ASC-PHYSICAL-UNIT  TO  DK-PHYUNT-ADDR-NBR.                      
166600     MOVE +0                 TO  PV-COUNT-DUPLICATE-VTAM.                 
166700     IF  ASC-LINE          = SPACES                                       
166800     AND ASC-PHYSICAL-UNIT = SPACES                                       
166900*        *********************************************************        
167000*        * NO ENTRY IN THESE OPTIONAL FIELDS                     *        
167100*        *********************************************************        
167200         CONTINUE                                                         
167300     ELSE                                                                 
167400         PERFORM 3420-CHECK-FOR-DUPLICATE                                 
167500         IF PV-COUNT-DUPLICATE-VTAM  > ZERO                               
167600             SET PS-ERROR                                                 
167700                 DP020-MSG-FATAL  TO TRUE                                 
167800*            ---- ADD WOULD RESULT IN A DUPLICATE VTAM                    
167900             MOVE PC-TSYMSG-01176 TO DP020-MSG-NUMBER                     
168000             MOVE DP015-UNP-ALP-BRT-OFF                                   
168100                                  TO ALINEA                               
168200                                     APHYUA                               
168300             MOVE DP015-RED       TO ALINEC                               
168400                                     APHYUC                               
168500             MOVE DP015-REVERSE   TO ALINEH                               
168600                                     APHYUH                               
168700             MOVE -1              TO ALINEL                               
168800             SET DP030-SET-CURSOR-APPL-1                                  
168900                                  TO TRUE                                 
169000         END-IF                                                           
169100     END-IF.                                                              
169200                                                                          
169300                                                                          
169400*----------------------------------------------------------------*        
169500* CHECK ENTERED DATA AGAINST EXISTING ROWS FOR POSSIBLE "BEWTEEN"*        
169600* OVERLAPPING IP ADDRESS RANGE(S) -OR- DUPLICATE VTAM (LINE/UNIT).        
169700*----------------------------------------------------------------*        
169800 3410-CHECK-FOR-OVERLAP.                                                  
169900                                                                          
170000     PERFORM                                                              
170100         WITH TEST AFTER                                                  
170200         VARYING PV-RETRY-COUNTER                                         
170300         FROM 1 BY 1                                                      
170400         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
170500                   OR (SQLCODE = ZERO))                                   
170600         EXEC SQL                                                         
170700             SELECT                                                       
170800                 COUNT(*)                                                 
170900             INTO                                                         
171000                 :PV-COUNT-OVERLAP-IP-ADDR                                
171100             FROM   TPHYUNT                                               
171200             WHERE  (END_RNG_IP_ADDR  >=  :DK-IP-ADDR-BEG)                
171300               AND  (BEG_RNG_IP_ADDR  <=  :DK-IP-ADDR-END)                
171400         END-EXEC                                                         
171500                                                                          
171600                                                                          
171700         EVALUATE TRUE                                                    
171800             WHEN SQLCODE = ZERO                                          
171900             WHEN SQLCODE = +100                                          
172000             WHEN SQLCODE = -904                                          
172100             WHEN SQLCODE = -913                                          
172200                 CONTINUE                                                 
172300             WHEN SQLWARN0 NOT = SPACES                                   
172400             WHEN SQLCODE < ZERO                                          
172500             WHEN SQLCODE > ZERO                                          
172600                 MOVE '3410-CHECK-FOR-OVERLAP'                            
172700                                   TO DP013-PARAGRAPH                     
172800                 MOVE 'SELECT / COUNT FAILED FOR:'                        
172900                                   TO DP013-MESSAGE-TEXT(1)               
173000                 MOVE DK-IP-ADDR-BEG                                      
173100                                   TO PM-ABEND-RANGE-B                    
173200                 MOVE DK-IP-ADDR-END                                      
173300                                   TO PM-ABEND-RANGE-E                    
173400                 MOVE PM-ABEND-RANGES                                     
173500                                   TO DP013-MESSAGE-TEXT(2)               
173600                 MOVE SQLCA        TO DP013-SQLCA                         
173700                 MOVE 'TPHYUNT'    TO DP013-DB2-TABLE-NAME (1)            
173800                 SET DP013-DB2-ABEND                                      
173900                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
174000                 PERFORM DP013-0000-PROCESS-ABEND                         
174100         END-EVALUATE                                                     
174200     END-PERFORM.                                                         
174300                                                                          
174400     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
174500         MOVE '3410-CHECK-FOR-OVERLAP'                                    
174600                                   TO DP013-PARAGRAPH                     
174700         MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT A PHY        
174800-             'SICAL UNIT'         TO DP013-MESSAGE-TEXT (1)              
174900         MOVE SQLCA                TO DP013-SQLCA                         
175000         MOVE 'TPHYUNT '           TO DP013-DB2-TABLE-NAME (1)            
175100         SET DP013-DB2-ABEND                                              
175200             DP013-XCTL-DISPLAY-RESTART                                   
175300                                   TO TRUE                                
175400         PERFORM DP013-0000-PROCESS-ABEND                                 
175500     END-IF.                                                              
175600                                                                          
175700                                                                          
175800*----------------------------------------------------------------*        
175900* CHECK ENTERED DATA AGAINST EXISTING ROWS FOR POSSIBLE          *        
176000* DUPLICATE VTAM (LINE/UNIT).                                    *        
176100*----------------------------------------------------------------*        
176200 3420-CHECK-FOR-DUPLICATE.                                                
176300                                                                          
176400     PERFORM                                                              
176500         WITH TEST AFTER                                                  
176600         VARYING PV-RETRY-COUNTER                                         
176700         FROM 1 BY 1                                                      
176800         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
176900                   OR (SQLCODE = ZERO))                                   
177000         EXEC SQL                                                         
177100             SELECT COUNT(*)                                              
177200             INTO   :PV-COUNT-DUPLICATE-VTAM                              
177300             FROM   TPHYUNT                                               
177400             WHERE  ((LNE_ADDR_NBR   = :DK-LNE-ADDR-NBR)                  
177500               AND  (PHYUNT_ADDR_NBR = :DK-PHYUNT-ADDR-NBR))              
177600         END-EXEC                                                         
177700                                                                          
177800         EVALUATE TRUE                                                    
177900             WHEN SQLCODE = ZERO                                          
178000             WHEN SQLCODE = +100                                          
178100             WHEN SQLCODE = -904                                          
178200             WHEN SQLCODE = -913                                          
178300             WHEN SQLCODE = -913                                          
178400                 CONTINUE                                                 
178500             WHEN SQLWARN0 NOT = SPACES                                   
178600             WHEN SQLCODE < ZERO                                          
178700             WHEN SQLCODE > ZERO                                          
178800                 MOVE '3420-CHECK-FOR-DUPLICATE'                          
178900                                   TO DP013-PARAGRAPH                     
179000                 MOVE 'SELECT / COUNT FAILED FOR:'                        
179100                                   TO DP013-MESSAGE-TEXT(1)               
179200                 MOVE DK-LNE-ADDR-NBR                                     
179300                                   TO PM-ABEND-KEYFIELD1                  
179400                 MOVE DK-PHYUNT-ADDR-NBR                                  
179500                                   TO PM-ABEND-KEYFIELD2                  
179600                 MOVE PM-ABEND-KEYFIELDS                                  
179700                                   TO DP013-MESSAGE-TEXT(2)               
179800                 MOVE SQLCA        TO DP013-SQLCA                         
179900                 MOVE 'TPHYUNT'    TO DP013-DB2-TABLE-NAME (1)            
180000                 SET DP013-DB2-ABEND                                      
180100                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
180200                 PERFORM DP013-0000-PROCESS-ABEND                         
180300         END-EVALUATE                                                     
180400     END-PERFORM.                                                         
180500                                                                          
180600     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
180700         MOVE '3420-CHECK-FOR-DUPLICATE'                                  
180800                                   TO DP013-PARAGRAPH                     
180900         MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT A PHY        
181000-             'SICAL UNIT'         TO DP013-MESSAGE-TEXT (1)              
181100         MOVE SQLCA                TO DP013-SQLCA                         
181200         MOVE 'TPHYUNT '           TO DP013-DB2-TABLE-NAME (1)            
181300         SET DP013-DB2-ABEND                                              
181400             DP013-XCTL-DISPLAY-RESTART                                   
181500                                   TO TRUE                                
181600         PERFORM DP013-0000-PROCESS-ABEND                                 
181700     END-IF.                                                              
181800                                                                          
181900                                                                          
182000                                                                          
182100*----------------------------------------------------------------*        
182200* GET THE NEXT AVAILABLE MESSAGE NUMBER, MOVE IT TO THE SCREEN   *        
182300* AND POSITION THE CURSOR FOR THE INITIAL PANEL.                 *        
182400*----------------------------------------------------------------*        
182500 4000-BUILD-INITIAL-PANEL.                                                
182600                                                                          
182700     SET DP030-SET-CURSOR-APPL-1   TO TRUE.                               
182800     MOVE -1                       TO ASTORL.                             
182900                                                                          
183000                                                                          
183100*----------------------------------------------------------------*        
183200*    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
183300*----------------------------------------------------------------*        
183400 4400-MOVE-COMMAREA-TO-SCREEN.                                            
183500     MOVE ASC-STORE                TO ASTORO.                             
183600     MOVE ASC-FCLTY                TO AFCLTO.                             
183700     MOVE ASC-LINE                 TO ALINEO.                             
183800     MOVE ASC-PHYSICAL-UNIT        TO APHYUO.                             
183900     MOVE ASC-IP-ADDR-B-OCTET1-N   TO AIPB1O.                             
184000     MOVE ASC-IP-ADDR-B-OCTET2-N   TO AIPB2O.                             
184100     MOVE ASC-IP-ADDR-B-OCTET3-N   TO AIPB3O.                             
184200     MOVE ASC-IP-ADDR-B-OCTET4-N   TO AIPB4O.                             
184300     MOVE ASC-IP-ADDR-E-OCTET1-N   TO AIPE1O.                             
184400     MOVE ASC-IP-ADDR-E-OCTET2-N   TO AIPE2O.                             
184500     MOVE ASC-IP-ADDR-E-OCTET3-N   TO AIPE3O.                             
184600     MOVE ASC-IP-ADDR-E-OCTET4-N   TO AIPE4O.                             
184700                                                                          
184800                                                                          
184900*----------------------------------------------------------------*        
185000* CREATE THE PHYSICAL UNIT OBJECT ON THE TPHYUNT TABLE.          *        
185100*----------------------------------------------------------------*        
185200 5000-ADD-PHYSICAL-UNIT.                                                  
185300     MOVE PC-OPERATING-COMPANY     TO PHYUNT-OPERCO-NBR.                  
185400     MOVE ASC-LINE                 TO PHYUNT-LNE-ADDR-NBR.                
185500     MOVE ASC-PHYSICAL-UNIT        TO PHYUNT-PHYUNT-ADDR-NBR.             
185600     MOVE ASC-STORE(3:3)           TO PHYUNT-STR-NBR.                     
185700     MOVE ASC-STORE-N              TO PHYUNT-OPER-UNT-NBR.                
185800     MOVE ASC-FCLTY-N              TO PHYUNT-FCLTY-ID.                    
185900     MOVE ASC-IP-ADDR-BEGIN        TO PHYUNT-BEG-RNG-IP-ADDR.             
186000     MOVE ASC-IP-ADDR-END          TO PHYUNT-END-RNG-IP-ADDR.             
185700     MOVE ASC-STORE-N              TO PHYUNT-LOC-NBR.                     
186100                                                                          
186200     PERFORM 5100-INSERT-PHYS-UNIT.                                       
186300                                                                          
186400                                                                          
186500*----------------------------------------------------------------*        
186600* INSERT A ROW INTO THE TPHYUNT TABLE.                           *        
186700*----------------------------------------------------------------*        
186800 5100-INSERT-PHYS-UNIT.                                                   
186900     PERFORM                                                              
187000         WITH TEST AFTER                                                  
187100         VARYING PV-RETRY-COUNTER                                         
187200         FROM 1 BY 1                                                      
187300         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
187400                   OR (SQLCODE = ZERO))                                   
187500         EXEC SQL                                                         
187600             INSERT INTO TPHYUNT                                          
187700                (OPERCO_NBR,                                              
187800                 LNE_ADDR_NBR,                                            
187900                 PHYUNT_ADDR_NBR,                                         
188000                 STR_NBR,                                                 
188100                 OPER_UNT_NBR,                                            
188200                 FCLTY_ID,                                                
188300                 BEG_RNG_IP_ADDR,                                         
188400                 END_RNG_IP_ADDR,                                         
                       LOC_NBR)                                                 
188500             VALUES                                                       
188600                (:PHYUNT-OPERCO-NBR,                                      
188700                 :PHYUNT-LNE-ADDR-NBR,                                    
188800                 :PHYUNT-PHYUNT-ADDR-NBR,                                 
188900                 :PHYUNT-STR-NBR,                                         
189000                 :PHYUNT-OPER-UNT-NBR,                                    
189100                 :PHYUNT-FCLTY-ID,                                        
189200                 :PHYUNT-BEG-RNG-IP-ADDR,                                 
189300                 :PHYUNT-END-RNG-IP-ADDR,                                 
189300                 :PHYUNT-LOC-NBR)                                         
189400         END-EXEC                                                         
189500                                                                          
189600                                                                          
189700         EVALUATE TRUE                                                    
189800             WHEN SQLCODE = ZERO                                          
189900             WHEN SQLCODE = -904                                          
190000             WHEN SQLCODE = -913                                          
190100                 CONTINUE                                                 
190200             WHEN SQLWARN0 NOT = SPACES                                   
190300             WHEN SQLCODE < ZERO                                          
190400             WHEN SQLCODE > ZERO                                          
190500                 MOVE '5100-INSERT-PHYS-UNIT'                             
190600                                   TO DP013-PARAGRAPH                     
190700                 MOVE 'INSERT A ROW INTO THE PHYSICAL UNIT TABLE'         
190800                                   TO DP013-MESSAGE-TEXT(1)               
190900                 MOVE PHYUNT-LNE-ADDR-NBR                                 
191000                                   TO PM-ABEND-KEYFIELD1                  
191100                 MOVE PHYUNT-PHYUNT-ADDR-NBR                              
191200                                   TO PM-ABEND-KEYFIELD2                  
191300                 MOVE PM-ABEND-KEYFIELDS                                  
191400                                   TO DP013-MESSAGE-TEXT(2)               
191500                 MOVE SQLCA        TO DP013-SQLCA                         
191600                 MOVE 'TPHYUNT'    TO DP013-DB2-TABLE-NAME (1)            
191700                 SET DP013-DB2-ABEND                                      
191800                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
191900                 PERFORM DP013-0000-PROCESS-ABEND                         
192000         END-EVALUATE                                                     
192100     END-PERFORM.                                                         
192200                                                                          
192300     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
192400         MOVE '5100-INSERT-PHYS-UNIT'                                     
192500                                   TO DP013-PARAGRAPH                     
192600         MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO INSERT A NEW        
192700-             ' PHYSICAL UNIT'     TO DP013-MESSAGE-TEXT (1)              
192800         MOVE SQLCA                TO DP013-SQLCA                         
192900         MOVE 'TPHYUNT '           TO DP013-DB2-TABLE-NAME (1)            
193000         SET DP013-DB2-ABEND                                              
193100             DP013-XCTL-DISPLAY-RESTART                                   
193200                                   TO TRUE                                
193300         PERFORM DP013-0000-PROCESS-ABEND                                 
193400     END-IF.                                                              
193500                                                                          
193600                                                                          
193700*----------------------------------------------------------------*        
193800*    ABEND PROCESSOR MODULE                                      *        
193900*----------------------------------------------------------------*        
194000     COPY DPPD013.                                                        
