       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    RCKUT071.                                                 
       AUTHOR.        DAN FRANKLIN.                                             
       DATE-WRITTEN.  03/16/88.                                                 
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *    SALES RECEIPTS SKU RENUMBER.                                         
      *                                                                         
      *    WRITE ALL THE SALES RECEIPTS RECORDS TO AN UPDATED SALES             
      *    RECEIPTS FILE UPDATING THE SALES RECEIPTS SKU IF IT IS FOUND         
      *    IN THE SKU RENUMBER LOG.  THE SKU RENUMBER LOG WILL CONTAIN          
      *    A RECORD FOR EACH SKU CHANGE TRANSACTION.  THE RECORD                
      *    CONTAINS THE OLD AND NEW SKU VALUE.  IF THE SALES RECEIPTS           
      *    SKU HAS A MATCH ON THE RENUMBER LOG BY OLD SKU, IT IS                
      *    UPDATED TO THE NEW SKU.  THE LOG IS THEN SEARCHED FOR A              
      *    MATCH ON THE SALES RECEIPTS SKU JUST UPDATED.  IF A MATCH            
      *    IS FOUND, THE SALES RECEIPTS SKU IS UPDATED AGAIN AND THE            
      *    SEARCH CONTINUES.  THE SEARCH WILL END WHEN THE END OF LOG           
      *    IS REACHED.  THIS CONTINUAL SEARCHING AND UPDATING ARE DONE,         
      *    BECAUSE AN SKU MAY HAVE BEEN INVOLVED IN MULTIPLE RENUMBERS.         
      *****************************************************************         
      *****************************************************************         
      *  THIS PROGRAM WAS ORIGINALLY NAMED MIKUT005 AND WAS RENAMED             
      *  ON 04/11/2002                                                          
      *****************************************************************         
                                                                                
      *                                                                         
      *                                                                         
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
      *                                                                         
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
      *                                                                         
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AS-SKU-RENUM-LOG                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT SALES-RECEIPTS-FILE                                           
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT UPDATED-SALES-RECEIPTS-FILE                                   
               ASSIGN TO OUT01.                                                 
      *                                                                         
      *                                                                         
      *                                                                         
       DATA DIVISION.                                                           
      *                                                                         
      *                                                                         
       FILE SECTION.                                                            
                                                                                
       FD  AS-SKU-RENUM-LOG                                                     
           RECORD CONTAINS 37 CHARACTERS.                                       
                                                                                
           COPY IMRD027.                                                        
                                                                                
       FD  SALES-RECEIPTS-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 300 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
      *  INCLUDE STOREEXT                                                       
       01  SALES-RECEIPTS-RECORD             PIC X(300).                        
                                                                                
       FD  UPDATED-SALES-RECEIPTS-FILE                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 300 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS UR-UPDATED-RECEIPTS-RECORD.                           
                                                                                
       01  UR-UPDATED-RECEIPTS-RECORD        PIC X(300).                        
       EJECT                                                                    
      *                                                                         
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
                                                                                
           COPY FSWS001U.                                                       
                                                                                
           COPY STOREEXT.                                                       
                                                                                
       01  CS-CHECK-SKU.                                                        
           05  CS-DEPT                       PIC X(3)     VALUE SPACES.         
           05  CS-CLASS                      PIC X(2)     VALUE SPACES.         
           05  CS-SEQ                        PIC X(3)     VALUE SPACES.         
                                                                                
       01  PV-CLASS.                                                            
           05  PV-CLASS-1-2                  PIC X(2)     VALUE SPACES.         
           05  PV-CLASS-3                    PIC X(1)     VALUE ZERO.           
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-RENUM-EOF-SW               PIC X(1)     VALUE 'N'.            
               88  PS-RENUM-EOF                           VALUE 'Y'.            
           05  PS-SALES-RCPTS-EOF-SE         PIC X(1)     VALUE 'N'.            
               88  PS-SALES-RCPTS-EOF                     VALUE 'Y'.            
                                                                                
       01  ABEND-CODE                        PIC S9(4)    VALUE ZEROES          
                                                          COMP SYNC.            
       EJECT                                                                    
      *                                                                         
      *                                                                         
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
                                                                                
      *****************************************************************         
      *    OPEN ALL FILES.                                                      
      *    INITIALIZE THE RENUMBER TABLE AND READ THE FIRST RECORD              
      *    FROM THE RENUMBER LOG.                                               
      *    IF THERE IS DATA ON THE FILE, LOAD THE TABLE FROM THE FILE           
      *    UNTIL END OF FILE CONDITION.                                         
      *    READ THE FIRST SALES RECEIPTS RECORD                                 
      *    CREATE THE OUTPUT FILE USING THE COPY MEMBER.                        
      *    CLOSE ALL FILES.                                                     
      *****************************************************************         
                                                                                
           OPEN INPUT  AS-SKU-RENUM-LOG                                         
                       SALES-RECEIPTS-FILE                                      
                OUTPUT UPDATED-SALES-RECEIPTS-FILE.                             
                                                                                
           PERFORM B100-INITIALIZE-AND-READ.                                    
                                                                                
           PERFORM B200-LOAD-RENUM-TABLE                                        
               VARYING MC-INDEX1 FROM 1 BY 1                                    
                   UNTIL PS-RENUM-EOF                                           
                                                                                
           PERFORM Z500-READ-SALES-RCPTS                                        
           IF NOT PS-SALES-RCPTS-EOF                                            
               PERFORM C100-PROCESS-SALES-RCPTS                                 
                   UNTIL PS-SALES-RCPTS-EOF                                     
           ELSE                                                                 
               PERFORM Z400-END-OF-SALES-RCPTS                                  
           END-IF.                                                              
                                                                                
           CLOSE AS-SKU-RENUM-LOG                                               
                 SALES-RECEIPTS-FILE                                            
                 UPDATED-SALES-RECEIPTS-FILE.                                   
                                                                                
           STOP RUN.                                                            
       EJECT                                                                    
       B100-INITIALIZE-AND-READ.                                                
                                                                                
      *****************************************************************         
      *    INITIALIZE RENUMBER TABLE.                                           
      *    READ THE FIRST RECORD FROM THE RENUMBER LOG (DUMMY                   
      *    HEADER RECORD).                                                      
      *    IF FIRST READ WAS O.K, READ THE NEXT RECORD TO BYPASS THE            
      *    DUMMY RECORD.                                                        
      *                                                                         
      *    PERFORMED BY A100-MAIN-MODULE.                                       
      *****************************************************************         
                                                                                
           INITIALIZE MC-RENUM-SKU-TABLE.                                       
           PERFORM Z100-READ-SKU-RENUM.                                         
           IF NOT PS-RENUM-EOF                                                  
               PERFORM Z100-READ-SKU-RENUM                                      
           ELSE                                                                 
               PERFORM Z300-END-OF-RENUM                                        
           END-IF.                                                              
                                                                                
       B200-LOAD-RENUM-TABLE.                                                   
                                                                                
      *****************************************************************         
      *    LOAD THE TABLE FROM THE FILE.                                        
      *    IF THE TABLE IS TOO SMALL, ABEND.                                    
      *                                                                         
      *    PERFORMED BY A100-MAIN-MODULE.                                       
      *****************************************************************         
                                                                                
           IF MC-INDEX1 IS GREATER THAN MC-INDEX-MAX                            
               DISPLAY '** ABEND ** IN PROGRAM RCKUT071'                        
               DISPLAY 'OVERFLOW IN MC-RENUM-SKU-TABLE'                         
               MOVE 4004 TO ABEND-CODE                                          
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
           MOVE IM027-SKU           TO MC-SKU(MC-INDEX1).                       
           MOVE IM027-HIST-SKU      TO MC-HIST-SKU(MC-INDEX1).                  
           MOVE IM027-INHOUSE-NMBR  TO MC-INHOUSE-NMBR(MC-INDEX1).              
                                                                                
           PERFORM Z100-READ-SKU-RENUM.                                         
                                                                                
       EJECT                                                                    
       C100-PROCESS-SALES-RCPTS.                                                
                                                                                
      *****************************************************************         
      *    LET THE COPY MEMBER DO ITS STUFF.                                    
      *    WRITE THE RECORD (UPDATED OR NOT) TO THE UPDATED SALES               
      *    RECEIPTS FILE.  READ THE NEXT SALES RECEIPTS RECORD.                 
      *                                                                         
      *    PERFORMED BY A100-MAIN-MODULE.                                       
      *****************************************************************         
                                                                                
           MOVE SE-DEPT      TO CS-DEPT.                                        
           MOVE SE-CLASS     TO CS-CLASS.                                       
           MOVE SE-SKU-SEQ   TO CS-SEQ.                                         
           MOVE CS-CHECK-SKU TO MC-CHECK-SKU.                                   
           INITIALIZE           MC-CHECK-INHOUSE.                               
                                                                                
           PERFORM MATCH-HISTORY.                                               
                                                                                
           IF MC-SKU-MATCH                                                      
               MOVE MC-CHECK-SKU TO CS-CHECK-SKU                                
               MOVE CS-DEPT      TO SE-DEPT                                     
               MOVE CS-CLASS     TO PV-CLASS-1-2                                
               MOVE PV-CLASS     TO SE-CLASS                                    
               MOVE CS-SEQ       TO SE-SKU-SEQ                                  
           END-IF.                                                              
                                                                                
           PERFORM Z600-WRITE-SALES-RCPTS.                                      
                                                                                
           PERFORM Z500-READ-SALES-RCPTS.                                       
                                                                                
       EJECT                                                                    
           COPY FSPD001U.                                                       
       EJECT                                                                    
       Z100-READ-SKU-RENUM.                                                     
                                                                                
      *****************************************************************         
      *    READ THE SKU RENUMBER LOG                                            
      *                                                                         
      *    PERFORMED BY B100-INITIALIZE-AND-READ.                               
      *    PERFORMED BY B200-LOAD-RENUM-TABLE.                                  
      *****************************************************************         
                                                                                
           READ AS-SKU-RENUM-LOG                                                
                AT END                                                          
                   SET PS-RENUM-EOF TO TRUE.                                    
                                                                                
       Z300-END-OF-RENUM.                                                       
                                                                                
      *****************************************************************         
      *    SET RETURN CODE TO 4051 IF THERE ARE NO RECORDS ON THE               
      *    SKU RENUMBER LOG.                                                    
      *                                                                         
      *    PERFORMED BY A100-MAIN-MODULE.                                       
      *****************************************************************         
                                                                                
           DISPLAY 'NO RECORDS ON FILE - INP01 FS RENUM LOG'.                   
           DISPLAY 'IN PROGRAM RCKUT071'.                                       
           MOVE 4051 TO RETURN-CODE.                                            
       EJECT                                                                    
       Z400-END-OF-SALES-RCPTS.                                                 
                                                                                
      *****************************************************************         
      *    DISPLAY THAT THERE WERE NO RECORD ON THE SALES RECEIPTS              
      *    INPUT FILE.                                                          
      *                                                                         
      *    PERFORMED BY A100-MAIN-MODULE.                                       
      *****************************************************************         
                                                                                
           DISPLAY 'NO RECORDS ON FILE - INP02 MTD STORE EXTRACT'.              
           DISPLAY 'IN PROGRAM RCKUT071'.                                       
                                                                                
       Z500-READ-SALES-RCPTS.                                                   
                                                                                
      *****************************************************************         
      *    READ THE SALES RECEIPTS INPUT FILE.                                  
      *                                                                         
      *    PERFORMED BY A100-MAIN-MODULE.                                       
      *    PERFORMED BY C100-PROCESS-SALES-RCPTS.                               
      *****************************************************************         
                                                                                
           READ SALES-RECEIPTS-FILE                                             
               INTO STORE-EXTRACT-LINE                                          
               AT END                                                           
                   SET PS-SALES-RCPTS-EOF TO TRUE.                              
                                                                                
       Z600-WRITE-SALES-RCPTS.                                                  
                                                                                
      *****************************************************************         
      *    WRITE THE SALES RECEIPT RECORD.                                      
      *                                                                         
      *    PERFORMED BY C100-PROCESS-SALES-RCPTS.                               
      *****************************************************************         
                                                                                
           WRITE UR-UPDATED-RECEIPTS-RECORD                                     
               FROM STORE-EXTRACT-LINE.                                         
       EJECT                                                                    
                                                                                
