000100*-----------------------*                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*-----------------------*                                                 
000400 PROGRAM-ID.    DPKCS051.                                                 
000500 AUTHOR.        CHERI MASTEL - COMPUWARE.                                 
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000700 DATE-WRITTEN.  05-01-96.                                                 
000800 DATE-COMPILED.                                                           
000900                                                                          
001000*---------------------------------------------------------------*         
001100*    Z051 - PHYSICAL UNIT LIST PROGRAM.                         *         
001200*                                                               *         
001300*    THIS PROGRAM GENERATES A LIST OF PHYSICAL UNITS.           *         
001400*    THE VTAM ADDRESS (LINE / UNIT),                            *         
001500*    THE ASSOCIATED LOCATION (STORE / FACILITY) AND             *         
001600*    TCP/IP ADDRESS RANGE ARE DISPLAYED.                        *         
001700*    THE APPROPRIATE PROGRAM IS INVOKED WHEN THE OPERATOR SELECTS         
001800*    ONE OR MORE ITEMS AND PRESSES A FUNCTION KEY.  NO SELECTION*         
001900*    IS NECESSARY TO ADD A NEW PHYSICAL UNIT.                   *         
002000*                                                               *         
002100*    DATE      CHG'D BY  DESCRIPTION                            *         
002200*    ********  ********  *************************************  *         
002300*    10-15-96  TKMA235   CHANGE "ORDER BY" CLAUSES ON CURSORS,  *         
002400*                        TO PRESENT DATA BY BEGINNING.IP.ADDR,  *         
002500*                        WITHIN LOCATION. (IE: ORDER BY STORE,  *         
002600*                        FACILITY, BEG_IP_ADDR.)                *         
002700*                        OLD CODE: ORDER BY                     *         
002800*                                        OPER_UNT_NBR           *         
002900*                                      , LNE_ADDR_NBR           *         
003000*                                      , PHYUNT_ADDR_NBR        *         
003100*                        NEW CODE: ORDER BY                     *         
003200*                                        OPER_UNT_NBR           *         
003300*                                      , FCLTY_ID               *         
003400*                                      , LNE_ADDR_NBR           *         
003500*                                      , PHYUNT_ADDR_NBR        *         
003600*                                      , BEG_RNG_IP_ADDR        *         
003700*    12/22/03  TKMA464   MADE CHANGES FOR EXPANDING LOCATION    *         
003701*                        FROM 3 TO 5 DIGITS.                    *         
003702*                                                               *         
262023*  CHANGED 09/28/2021    BY JIM HUNTER      WR: CHG0262023      *         
262023*  CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE  *         
262023*  MAKING THE CALL DYNAMIC VS STATIC                            *         
003800*---------------------------------------------------------------*         
003900                                                                          
004000 ENVIRONMENT DIVISION.                                                    
004100                                                                          
004200 DATA DIVISION.                                                           
004300                                                                          
004400 WORKING-STORAGE SECTION.                                                 
004500                                                                          
004600 01  DK-DB2-KEYS.                                                         
004900     15  DK-LOCATION-NBR                PIC S9(04)  VALUE ZERO            
005000                                                    COMP.                 
005100     15  DK-VTAM.                                                         
005200         20  DK-LNE-ADDR-NBR            PIC  X(02)  VALUE SPACE.          
005300         20  DK-PHYUNT-ADDR-NBR         PIC  X(02)  VALUE SPACE.          
005400     15  DK-IP-ADDR                     PIC  X(15)  VALUE SPACE.          
005500     15  DK-IP-ADDR-BYTE                REDEFINES DK-IP-ADDR              
005600                                        OCCURS 15 TIMES                   
005700                                        PIC  X(01).                       
005800                                                                          
005900 01  PC-PROGRAM-CONSTANTS.                                                
006000     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
006100                                                    'DP051A  '.           
006200     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
006300                                                    'DPKM051 '.           
006400     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
006500                                                    'DPKCS051'.           
006600     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
006700     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
006800     05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '2'.            
006900     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +14             
007000                                                    COMP SYNC.            
007100     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
007200                                                    COMP SYNC.            
007300     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
007400                                                    +1000000              
007500                                                    COMP-3.               
007600     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
007700                                                    COMP SYNC.            
007800     05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
007900                                                    COMP-3.               
008000     05  PC-PERCENT                     PIC  X(01)  VALUE '%'.            
008100     05  PC-LAST-ACTION.                                                  
008200         10  PC-INQ                     PIC  X(03)  VALUE 'INQ'.          
008300         10  PC-UPD                     PIC  X(03)  VALUE 'UPD'.          
008400         10  PC-DEL                     PIC  X(03)  VALUE 'DEL'.          
008500         10  PC-ERR                     PIC  X(03)  VALUE 'ERR'.          
008600     05  PC-OPERCO-NBR                  PIC  X(02)  VALUE '03'.           
008700     05  PC-IP-ADDR-NOT-ENTERED.                                          
008800         10  PC-ZEROS-IP-ADDR           PIC  X(15)  VALUE                 
008900                                             '000.000.000.000'.           
009000         10  PC-ZERO-IP-ADDR            PIC  X(15)  VALUE                 
009100                                             '  0.  0.  0.  0'.           
009200         10  PC-SPACES-IP-ADDR          PIC  X(15)  VALUE SPACES.         
009300         10  PC-SPACE-IP-ADDR           PIC  X(15)  VALUE                 
009400                                             '   .   .   .   '.           
009500     05  PC-OPERATING-COMPANY           PIC  X(02)  VALUE '03'.           
009600     05  PC-PM-LQW-DESC.                                                  
009700         10  PC-PM-LQW-DESC-ITEM        PIC  X(08)  VALUE                 
009800                                                    ', ITEM: '.           
009900         10  PC-PM-LQW-DESC-BLOCK       PIC  X(08)  VALUE                 
010000                                                    ', BLCK: '.           
010100                                                                          
010200     05  PC-TSYMSG-NUMBERS.                                               
010300         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
010400         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
010500         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
010600         10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
010700         10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
010800         10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
010900         10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
011000         10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
011100         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
011200         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
011300         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
011400         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
011500         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
011600         10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.          
011700                                                                          
011800 01  PS-PROGRAM-SWITCHES.                                                 
011900     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
012000         88  PS-NO-ERROR                            VALUE 'Y'.            
012100         88  PS-ERROR                               VALUE 'N'.            
012200     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
012300         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
012400         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
012500     05  PS-DONE-SW                     PIC  X(01)  VALUE 'N'.            
012600         88  PS-DONE                                VALUE 'Y'.            
012700         88  PS-NOT-DONE                            VALUE 'N'.            
012800                                                                          
012900 01  PV-PROGRAM-VARIABLES.                                                
013000     05  PV-SUB                         PIC S9(04)  VALUE +0              
013100                                                    COMP SYNC.            
013200     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
013300                                                    COMP SYNC.            
013400     05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
013500                                                    COMP-3.               
013600     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
013700                                                    COMP-3.               
013800     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
013900                                                    COMP-3.               
014000     05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
014100                                                    COMP-3.               
014200     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
014300                                                    COMP-3.               
014400     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
014500                                                    COMP-3.               
014600     05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0              
014700                                                    COMP-3.               
014800     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
014900                                                    COMP SYNC.            
015000     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
015100                                                    COMP SYNC.            
015200     05  PV-ITEM                        PIC S9(04)  VALUE +0              
015300                                                    COMP SYNC.            
015400     05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
015500                                                    COMP SYNC.            
015600     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
015700                                                    COMP SYNC.            
015800     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
015900                                                    COMP SYNC.            
016000     05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
016100                                                    COMP SYNC.            
016200     05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
016300                                                    COMP SYNC.            
016400     05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
016500                                                    COMP SYNC.            
016600     05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
016700                                                    COMP SYNC.            
016800     05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
016900                                                    COMP SYNC.            
017000     05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
017100                                                    COMP SYNC.            
017200     05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
017300                                                    COMP SYNC.            
017400     05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
017500                                                    COMP SYNC.            
017600     05  PV-SQL-SUB                     PIC S9(04)  VALUE +0              
017700                                                    COMP SYNC.            
017800     05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
017900                                                    COMP SYNC.            
018000     05  PV-LQW-LENGTH                  PIC S9(4)   VALUE +0              
018100                                                    COMP SYNC.            
018200                                                                          
018300 01  PM-PROGRAM-MESSAGES.                                                 
018400     05  PM-ABEND-INFO-A.                                                 
018500         10  FILLER                PIC X(15)   VALUE                      
018600                                   'DP020-SRC-AID: '.                     
018700         10  PM-A-SRC-AID          PIC X(02)   VALUE  SPACE.              
018800         10  FILLER                PIC X(19)   VALUE                      
018900                                   '. DP020-FK-ACTION: '.                 
019000         10  PM-A-FK-ACTION        PIC X(01)   VALUE  SPACE.              
019100         10  FILLER                PIC X(22)   VALUE                      
019200                                   '. DP020-FK-DISPN-IND: '.              
019300         10  PM-A-FK-DISPN         PIC X(01)   VALUE  SPACE.              
019400         10  FILLER                PIC X(01)   VALUE '.'.                 
019500         10  FILLER                PIC X(19)   VALUE SPACE.               
019600                                                                          
019700     05  PM-LQW-ABEND-MSG.                                                
019800         10  FILLER                     PIC X(06)   VALUE                 
019900                                                    'TS Q: '.             
020000         10  PM-LQW-NAME.                                                 
020100             15  PM-LQW-TERM-ID         PIC X(04).                        
020200             15  PM-LQW-TASK            PIC 9(05).                        
020300             15  PM-LQW-SEQ             PIC 9(01).                        
020400         10  PM-LQW-DESC                PIC X(08)   VALUE                 
020500                                                    ', ITEM: '.           
020600         10  PM-LQW-ITEM-OR-BLOCK       PIC 9(04)   VALUE ZERO.           
020700         10  FILLER                     PIC X(10)   VALUE                 
020800                                                    ', LENGTH: '.         
020900         10  PM-LQW-LENGTH              PIC 9(04)   VALUE ZERO.           
021000         10  FILLER                     PIC X(01)   VALUE '.'.            
021100                                                                          
021200******************************************************************        
021300**  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
021400**  THIS MUST BE THE EXACT SAME FORMAT AS A BLOCK IN THE        **        
021500**  APPLICATION-SPECIFIC COMMAREA.                              **        
021600**  (SEE:    15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.)             **        
021700******************************************************************        
021800 01  LQW-LIST-QUEUE-WORK-AREA.                                            
021900     05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
022000     05  LQW-ITEM-ENTRIES               OCCURS 14 TIMES                   
022100                                        INDEXED BY LQW-IDX.               
022200         10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
022300         10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
022400         10  LQW-SELECTED-SW            PIC  X(01).                       
022500             88  LQW-SELECT                         VALUE 'S'.            
022600             88  LQW-DESELECT                       VALUE ' '.            
022700         10  LQW-ACTION                 PIC  X(03).                       
022800         10  LQW-SCREEN-LIST-FIELDS.                                      
022900*                *************************************************        
023000*                * FIELDS SPECIFIC TO THIS APPLICATION:          *        
023100*                *************************************************        
023200             15  LQW-LOCATION.                                            
023500                 20  LQW-FCLTY.                                           
023600                     25  LQW-FCLTY-N         PIC  9(04).                  
023700                 20  LQW-LOCATION-NBR        PIC S9(04) COMP.             
023800                 20  LQW-FCLTY-N4-COMP       PIC S9(04) COMP.             
023900             15  LQW-VTAM.                                                
024000                 20  LQW-LINE                PIC  X(02).                  
024100                 20  LQW-PHYSICAL-UNIT       PIC  X(02).                  
024200             15  LQW-IP-ADDR-BEG.                                         
024300                 20  LQW-IP-ADDR-BEG-OCTET1.                              
024400                     25  LQW-IP-ADDR-BEG-OCTET1-N                         
024500                                             PIC  9(03).                  
024600                 20  LQW-IP-ADDR-BEG-DOT1    PIC  X(01).                  
024700                 20  LQW-IP-ADDR-BEG-OCTET2.                              
024800                     25  LQW-IP-ADDR-BEG-OCTET2-N                         
024900                                             PIC  9(03).                  
025000                 20  LQW-IP-ADDR-BEG-DOT2    PIC  X(01).                  
025100                 20  LQW-IP-ADDR-BEG-OCTET3.                              
025200                     25  LQW-IP-ADDR-BEG-OCTET3-N                         
025300                                             PIC  9(03).                  
025400                 20  LQW-IP-ADDR-BEG-DOT3    PIC  X(01).                  
025500                 20  LQW-IP-ADDR-BEG-OCTET4.                              
025600                     25  LQW-IP-ADDR-BEG-OCTET4-N                         
025700                                             PIC  9(03).                  
025800             15  LQW-IP-ADDR-END.                                         
025900                 20  LQW-IP-ADDR-END-OCTET1.                              
026000                     25  LQW-IP-ADDR-END-OCTET1-N                         
026100                                             PIC  9(03).                  
026200                 20  LQW-IP-ADDR-END-DOT1    PIC  X(01).                  
026300                 20  LQW-IP-ADDR-END-OCTET2.                              
026400                     25  LQW-IP-ADDR-END-OCTET2-N                         
026500                                             PIC  9(03).                  
026600                 20  LQW-IP-ADDR-END-DOT2    PIC  X(01).                  
026700                 20  LQW-IP-ADDR-END-OCTET3.                              
026800                     25  LQW-IP-ADDR-END-OCTET3-N                         
026900                                             PIC  9(03).                  
027000                 20  LQW-IP-ADDR-END-DOT3    PIC  X(01).                  
027100                 20  LQW-IP-ADDR-END-OCTET4.                              
027200                     25  LQW-IP-ADDR-END-OCTET4-N                         
027300                                             PIC  9(03).                  
027400*    05  LQW-FILLER                          PIC  X(3301)                 
027500*                                            VALUE SPACES.                
027600                                                                          
027700*---------------------------------------------------------------*         
027800*  RECORD LAYOUT FOR THE PHYS UNIT SELECTION TEMP STORAGE QUEUE.*         
027900*---------------------------------------------------------------*         
028000     COPY DPWS252.                                                        
028100                                                                          
028200                                                                          
028300*---------------------------------------------------------------*         
028400*    MAP LAYOUT                                                 *         
028500*---------------------------------------------------------------*         
028600     COPY DPKM051.                                                        
028700                                                                          
028800 01  MR-OCCURS-LAYOUT                   REDEFINES  DP051AO.               
028900     05  FILLER                         PIC X(42).                        
029000     05  MR-SCROLL-AREA                 OCCURS 14 TIMES.                  
029100         10  MR-SELECTL                 PIC S9(04) COMP.                  
029200         10  MR-SELECTA                 PIC  X(01).                       
029300         10  MR-SELECTC                 PIC  X(01).                       
029400         10  MR-SELECTH                 PIC  X(01).                       
029500         10  MR-SELECTI                 PIC  X(01).                       
029600                                                                          
029700         10  MR-ACTIONL                 PIC S9(04) COMP.                  
029800         10  MR-ACTIONA                 PIC  X(01).                       
029900         10  MR-ACTIONC                 PIC  X(01).                       
030000         10  MR-ACTIONH                 PIC  X(01).                       
030100         10  MR-ACTION                  PIC  X(03).                       
030200                                                                          
030300         10  MR-STOREL                  PIC S9(04) COMP.                  
030400         10  MR-STOREA                  PIC  X(01).                       
030500         10  MR-STOREC                  PIC  X(01).                       
030600         10  MR-STOREH                  PIC  X(01).                       
030700         10  MR-STORE-X.                                                  
030800             15  MR-STORE-N             PIC 99999.                        
030900                                                                          
031000         10  MR-FCLTYL                  PIC S9(04) COMP.                  
031100         10  MR-FCLTYA                  PIC  X(01).                       
031200         10  MR-FCLTYC                  PIC  X(01).                       
031300         10  MR-FCLTYH                  PIC  X(01).                       
031400         10  MR-FCLTY-X.                                                  
031500             15  MR-FCLTY-N             PIC  ZZZ9.                        
031600                                                                          
031700         10  MR-LINEL                   PIC S9(04) COMP.                  
031800         10  MR-LINEA                   PIC  X(01).                       
031900         10  MR-LINEC                   PIC  X(01).                       
032000         10  MR-LINEH                   PIC  X(01).                       
032100         10  MR-LINE                    PIC  X(02).                       
032200                                                                          
032300         10  MR-UNITL                   PIC S9(04) COMP.                  
032400         10  MR-UNITA                   PIC  X(01).                       
032500         10  MR-UNITC                   PIC  X(01).                       
032600         10  MR-UNITH                   PIC  X(01).                       
032700         10  MR-UNIT                    PIC  X(02).                       
032800                                                                          
032900         10  MR-AIPBRL                  PIC S9(04) COMP.                  
033000         10  MR-AIPBRA                  PIC  X(01).                       
033100         10  MR-AIPBRC                  PIC  X(01).                       
033200         10  MR-AIPBRH                  PIC  X(01).                       
033300         10  MR-AIPBR                   PIC  X(15).                       
033400                                                                          
033500         10  MR-AIPERL                  PIC S9(04) COMP.                  
033600         10  MR-AIPERA                  PIC  X(01).                       
033700         10  MR-AIPERC                  PIC  X(01).                       
033800         10  MR-AIPERH                  PIC  X(01).                       
033900         10  MR-AIPER                   PIC  X(15).                       
034000                                                                          
034100                                                                          
034200*---------------------------------------------------------------*         
034300*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
034400*---------------------------------------------------------------*         
034500     COPY DPWS015.                                                        
034600                                                                          
034700                                                                          
034800*---------------------------------------------------------------*         
034900*    FUNCTION KEYS COPYBOOK.                                    *         
035000*---------------------------------------------------------------*         
035100     COPY DPWS016.                                                        
035200                                                                          
035300                                                                          
035400*---------------------------------------------------------------*         
035500*    NUMERIC EDIT LAYOUT.                                       *         
035600*---------------------------------------------------------------*         
035700     COPY DPWS010I.                                                       
035800                                                                          
      *---------------------------------------------------------------*         
      *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS030.                                                        
           COPY DPWS930.                                                        
036500                                                                          
036600*---------------------------------------------------------------*         
036700*    STANDARD COMMAREA.                                         *         
036800*---------------------------------------------------------------*         
036900     COPY DPWS020.                                                        
037000     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
037100                                                                          
037200*---------------------------------------------------------------*         
037300* INTER-APPLICATION COMMAREA.                                   *         
037400*   DPWS250 - IS THE INPUT LAYOUT WHEN COMING FROM THE CRIT.    *         
037500*             SPEC SCREEN.                                      *         
037600*   DPWS251 - IS THE OUTPUT LAYOUT THAT WILL BE PASSED FROM     *         
037700*             THIS LIST PROGRAMMING FOR A LOCAL-GO FUNCTION.    *         
037800*---------------------------------------------------------------*         
037900         10  INTER-APPL-COMM-AREA       PIC X(200).                       
038000                                                                          
038100     COPY DPWS250.                                                        
038200     COPY DPWS251.                                                        
038300*---------------------------------------------------------------*         
038400*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
038500*---------------------------------------------------------------*         
038600         10  ASC-SPECIFIC-COMMAREA.                                       
038700             15  ASC-LIST-KEYS.                                           
038800*                *************************************************        
038900*                * KEYS SPECIFIC TO THIS APPLICATION:            *        
039000*                *************************************************        
039100                 20  ASC-KEY-LOCATION                PIC S9(04)           
039110                                                         COMP.            
039200                 20  ASC-KEY-VTAM.                                        
039300                     25  ASC-KEY-LINE                PIC  X(02).          
039400                     25  ASC-KEY-UNIT                PIC  X(02).          
039500                 20  ASC-KEY-IPAD                    PIC  X(15).          
039600             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
039700*                *************************************************        
039800*                * ONE BLOCK MUST MATCH EXACTLY TO THE LISTQ.    *        
039900*                * (SEE: 01  LQW-LIST-QUEUE-WORK-AREA.)          *        
040000*                *************************************************        
040100                 20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
040200                     88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
040300                     88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
040400                                                                          
040500                 20  ASC-ITEM-ARRAY.                                      
040600                     25  ASC-ITEM-ENTRIES OCCURS 14 TIMES.                
040700*                        *****************************************        
040800*                        * 14 LINES OF DATA PRESENTED IN LIST:   *        
040900*                        *****************************************        
041000                         30  ASC-LIST-ITEM-NUMBER    PIC S9(09)           
041100                                                     COMP-3.              
041200                         30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
041300                             88  ASC-VALID-SELECT    VALUE ' '            
041400                                                           'S'            
041500                                                           'B'            
041600                                                           'E'.           
041700                             88  ASC-TEMP-NO-SELECT  VALUE ' '.           
041800                             88  ASC-TEMP-SELECT     VALUE 'S'.           
041900                             88  ASC-TEMP-BEGIN      VALUE 'B'.           
042000                             88  ASC-TEMP-END        VALUE 'E'.           
042100                         30  ASC-SELECTED-SW         PIC  X(01).          
042200                             88  ASC-NO-SELECT       VALUE ' '.           
042300                             88  ASC-SELECT          VALUE 'S'.           
042400                             88  ASC-BEGIN           VALUE 'B'.           
042500                             88  ASC-END             VALUE 'E'.           
042600                         30  ASC-ACTION              PIC  X(03).          
042700                         30  ASC-SCREEN-LIST-FIELDS.                      
042800*                        *****************************************        
042900*                        * FIELDS SPECIFIC TO THIS APPLICATION:  *        
043000*                        *****************************************        
043100                             35  ASC-LOCATION.                            
043400                                 40  ASC-FCLTY.                           
043500                                     45  ASC-FCLTY-N PIC  9(04).          
043600                                 40  ASC-LOC-NBR                          
043700                                                     PIC S9(04)           
043800                                                         COMP.            
043900                                 40  ASC-FCLTY-N4-COMP                    
044000                                                     PIC S9(04)           
044100                                                         COMP.            
044200                             35  ASC-VTAM.                                
044300                                 40  ASC-LINE        PIC  X(02).          
044400                                 40  ASC-PHYSICAL-UNIT                    
044500                                                     PIC  X(02).          
044600                             35  ASC-IP-ADDR-BEG.                         
044700                                 40  ASC-IP-ADDR-BEG-OCTET1.              
044800                                     45  ASC-IP-ADDR-BEG-OCTET1-N         
044900                                                     PIC  9(03).          
045000                                 40  ASC-IP-ADDR-BEG-DOT1                 
045100                                                     PIC  X(01).          
045200                                 40  ASC-IP-ADDR-BEG-OCTET2.              
045300                                     45  ASC-IP-ADDR-BEG-OCTET2-N         
045400                                                     PIC  9(03).          
045500                                 40  ASC-IP-ADDR-BEG-DOT2                 
045600                                                     PIC  X(01).          
045700                                 40  ASC-IP-ADDR-BEG-OCTET3.              
045800                                     45  ASC-IP-ADDR-BEG-OCTET3-N         
045900                                                     PIC  9(03).          
046000                                 40  ASC-IP-ADDR-BEG-DOT3                 
046100                                                     PIC  X(01).          
046200                                 40  ASC-IP-ADDR-BEG-OCTET4.              
046300                                     45  ASC-IP-ADDR-BEG-OCTET4-N         
046400                                                     PIC  9(03).          
046500                             35  ASC-IP-ADDR-END.                         
046600                                 40  ASC-IP-ADDR-END-OCTET1.              
046700                                     45  ASC-IP-ADDR-END-OCTET1-N         
046800                                                     PIC  9(03).          
046900                                 40  ASC-IP-ADDR-END-DOT1                 
047000                                                     PIC  X(01).          
047100                                 40  ASC-IP-ADDR-END-OCTET2.              
047200                                     45  ASC-IP-ADDR-END-OCTET2-N         
047300                                                     PIC  9(03).          
047400                                 40  ASC-IP-ADDR-END-DOT2                 
047500                                                     PIC  X(01).          
047600                                 40  ASC-IP-ADDR-END-OCTET3.              
047700                                     45  ASC-IP-ADDR-END-OCTET3-N         
047800                                                     PIC  9(03).          
047900                                 40  ASC-IP-ADDR-END-DOT3                 
048000                                                     PIC  X(01).          
048100                                 40  ASC-IP-ADDR-END-OCTET4.              
048200                                     45  ASC-IP-ADDR-END-OCTET4-N         
048300                                                     PIC  9(03).          
048400*                *************************************************        
048500*                * FIELDS REQUIRED FOR CICS ARCHITECTURE:        *        
048600*                *************************************************        
048700             15  ASC-BLK-SUB                         PIC S9(04)           
048800                                                          COMP            
048900                                                          SYNC.           
049000             15  ASC-ITEM-SUB                        PIC S9(04)           
049100                                                          COMP            
049200                                                          SYNC.           
049300             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
049400                                                          COMP-3.         
049500             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
049600                                                          COMP-3.         
049700             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
049800                                                          COMP-3.         
049900             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
050000                                                          COMP-3.         
050100             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
050200                                                          COMP-3.         
050300             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
050400                                                          COMP            
050500                                                          SYNC.           
050600             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
050700                                                          COMP-3.         
050800             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
050900                                                          COMP-3.         
051000             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
051100                 88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
051200                 88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
051300             15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
051400                 88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
051500                 88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
051600             15  ASC-LIST-QUEUE-NAME.                                     
051700                 20  ASC-LIST-TERM-ID                PIC  X(04).          
051800                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
051900                                                          COMP-3.         
052000                 20  ASC-LIST-SEQ                    PIC  9(01).          
052100             15  ASC-SEL-QUEUE-NAME.                                      
052200                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
052300                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
052400                                                          COMP-3.         
052500                 20  ASC-SEL-SEQ                     PIC  9(01).          
052600*            *****************************************************        
052700*            * CURSOR SPECIFIC TO THIS APPLICATION:              *        
052800*            *****************************************************        
052900             15  ASC-CURSOR-SW                       PIC  X(01).          
053000                 88  ASC-NO-CURSOR                   VALUE 'N'.           
053100                 88  ASC-STOR-CURSOR                 VALUE 'S'.           
053200                 88  ASC-LINE-CURSOR                 VALUE 'L'.           
053300                 88  ASC-UNIT-CURSOR                 VALUE 'U'.           
053400                 88  ASC-VTAM-CURSOR                 VALUE 'V'.           
053500                 88  ASC-IPAD-CURSOR                 VALUE 'I'.           
053600             15  ASC-START-ITEM-REQUEST.                                  
053700                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
053900                                                                          
054000*---------------------------------------------------------------*         
054100*    ABEND PROCESSING COPYBOOK.                                 *         
054200*---------------------------------------------------------------*         
054300     COPY DPWS013.                                                        
054400                                                                          
054500                                                                          
054600*---------------------------------------------------------------*         
054700*    DB2 AREA FOR PHYSICAL UNIT TABLE: TPHYUNT                  *         
054800*---------------------------------------------------------------*         
054900     EXEC SQL                                                             
055000          INCLUDE TPHYUNT                                                 
055100     END-EXEC.                                                            
055200                                                                          
055300                                                                          
055400*---------------------------------------------------------------*         
055500*    DB2 AREA FOR COMMUNICATIONS                                *         
055600*---------------------------------------------------------------*         
055700     EXEC SQL                                                             
055800          INCLUDE SQLCA                                                   
055900     END-EXEC.                                                            
056000                                                                          
056100                                                                          
056200*---------------------------------------------------------------*         
056300*    "NO" CURSOR WHEN NO SELECTION CRITERIA                     *         
056400*    ENTERED ON THE CRITERIA SPEC SCREEN.                       *         
056500*---------------------------------------------------------------*         
056600     EXEC SQL                                                             
056700          DECLARE NO-CSR CURSOR                                           
056800          FOR SELECT                                                      
056900                LNE_ADDR_NBR                                              
057000              , PHYUNT_ADDR_NBR                                           
057100              , LOC_NBR                                                   
057200              , FCLTY_ID                                                  
057300              , BEG_RNG_IP_ADDR                                           
057400              , END_RNG_IP_ADDR                                           
057500          FROM                                                            
057600                TPHYUNT                                                   
057700          ORDER BY                                                        
057800                LOC_NBR                                                   
057900              , FCLTY_ID                                                  
058000              , LNE_ADDR_NBR                                              
058100              , PHYUNT_ADDR_NBR                                           
058200              , BEG_RNG_IP_ADDR                                           
058300     END-EXEC.                                                            
058400                                                                          
058500                                                                          
058600*---------------------------------------------------------------*         
058700*    STOR CURSOR WHEN "STORE" IS                                *         
058800*    ENTERED ON THE CRITERIA SPEC SCREEN.                       *         
058900*---------------------------------------------------------------*         
059000     EXEC SQL                                                             
059100          DECLARE STOR-CSR CURSOR                                         
059200          FOR SELECT                                                      
059300                LNE_ADDR_NBR                                              
059400              , PHYUNT_ADDR_NBR                                           
059500              , LOC_NBR                                                   
059600              , FCLTY_ID                                                  
059700              , BEG_RNG_IP_ADDR                                           
059800              , END_RNG_IP_ADDR                                           
059900          FROM                                                            
060000                TPHYUNT                                                   
060100          WHERE                                                           
060200                LOC_NBR = :DK-LOCATION-NBR                                
060300          ORDER BY                                                        
060400                LOC_NBR                                                   
060500              , FCLTY_ID                                                  
060600              , LNE_ADDR_NBR                                              
060700              , PHYUNT_ADDR_NBR                                           
060800              , BEG_RNG_IP_ADDR                                           
060900     END-EXEC.                                                            
061000                                                                          
061100                                                                          
061200*---------------------------------------------------------------*         
061300*    VTAM CURSOR WHEN BOTH "LINE" AND "UNIT" OF THE VTAM ADDRESS*         
061400*    ARE ENTERED ON THE CRITERIA SPEC SCREEN.                   *         
061500*---------------------------------------------------------------*         
061600     EXEC SQL                                                             
061700          DECLARE VTAM-CSR CURSOR                                         
061800          FOR SELECT                                                      
061900                LNE_ADDR_NBR                                              
062000              , PHYUNT_ADDR_NBR                                           
062100              , LOC_NBR                                                   
062200              , FCLTY_ID                                                  
062300              , BEG_RNG_IP_ADDR                                           
062400              , END_RNG_IP_ADDR                                           
062500          FROM                                                            
062600                TPHYUNT                                                   
062700          WHERE                                                           
062800              ((LNE_ADDR_NBR    = :DK-LNE-ADDR-NBR)                       
062900          AND  (PHYUNT_ADDR_NBR = :DK-PHYUNT-ADDR-NBR))                   
063000          ORDER BY                                                        
063100                LOC_NBR                                                   
063200              , FCLTY_ID                                                  
063300              , LNE_ADDR_NBR                                              
063400              , PHYUNT_ADDR_NBR                                           
063500              , BEG_RNG_IP_ADDR                                           
063600     END-EXEC.                                                            
063700                                                                          
063800                                                                          
063900*---------------------------------------------------------------*         
064000*    LINE CURSOR WHEN ONLY "LINE" OF THE VTAM ADDRESS IS        *         
064100*    ENTERED ON THE CRITERIA SPEC SCREEN.                       *         
064200*---------------------------------------------------------------*         
064300     EXEC SQL                                                             
064400          DECLARE LINE-CSR CURSOR                                         
064500          FOR SELECT                                                      
064600                LNE_ADDR_NBR                                              
064700              , PHYUNT_ADDR_NBR                                           
064800              , LOC_NBR                                                   
064900              , FCLTY_ID                                                  
065000              , BEG_RNG_IP_ADDR                                           
065100              , END_RNG_IP_ADDR                                           
065200          FROM                                                            
065300                TPHYUNT                                                   
065400          WHERE                                                           
065500                LNE_ADDR_NBR   = :DK-LNE-ADDR-NBR                         
065600          ORDER BY                                                        
065700                LOC_NBR                                                   
065800              , FCLTY_ID                                                  
065900              , LNE_ADDR_NBR                                              
066000              , PHYUNT_ADDR_NBR                                           
066100              , BEG_RNG_IP_ADDR                                           
066200     END-EXEC.                                                            
066300                                                                          
066400                                                                          
066500*---------------------------------------------------------------*         
066600*    UNIT CURSOR WHEN ONLY "UNIT" OF THE VTAM ADDRESS IS        *         
066700*    ENTERED ON THE CRITERIA SPEC SCREEN.                       *         
066800*---------------------------------------------------------------*         
066900     EXEC SQL                                                             
067000          DECLARE UNIT-CSR CURSOR                                         
067100          FOR SELECT                                                      
067200                LNE_ADDR_NBR                                              
067300              , PHYUNT_ADDR_NBR                                           
067400              , LOC_NBR                                                   
067500              , FCLTY_ID                                                  
067600              , BEG_RNG_IP_ADDR                                           
067700              , END_RNG_IP_ADDR                                           
067800          FROM                                                            
067900                TPHYUNT                                                   
068000          WHERE                                                           
068100                PHYUNT_ADDR_NBR = :DK-PHYUNT-ADDR-NBR                     
068200          ORDER BY                                                        
068300                LOC_NBR                                                   
068400              , FCLTY_ID                                                  
068500              , LNE_ADDR_NBR                                              
068600              , PHYUNT_ADDR_NBR                                           
068700              , BEG_RNG_IP_ADDR                                           
068800     END-EXEC.                                                            
068900                                                                          
069000                                                                          
069100*---------------------------------------------------------------*         
069200*    IPAD CURSOR WHEN IP ADDRESS (OR PARTIAL IP ADDRESS) IS     *         
069300*    ENTERED ON THE CRITERIA SPEC SCREEN.                       *         
069400*---------------------------------------------------------------*         
069500     EXEC SQL                                                             
069600          DECLARE IPAD-CSR CURSOR                                         
069700          FOR SELECT                                                      
069800                LNE_ADDR_NBR                                              
069900              , PHYUNT_ADDR_NBR                                           
070000              , LOC_NBR                                                   
070100              , FCLTY_ID                                                  
070200              , BEG_RNG_IP_ADDR                                           
070300              , END_RNG_IP_ADDR                                           
070400          FROM                                                            
070500                TPHYUNT                                                   
070600          WHERE                                                           
070700              (:DK-IP-ADDR    BETWEEN  BEG_RNG_IP_ADDR                    
070800                              AND      END_RNG_IP_ADDR)                   
070900          ORDER BY                                                        
071000                LOC_NBR                                                   
071100              , FCLTY_ID                                                  
071200              , LNE_ADDR_NBR                                              
071300              , PHYUNT_ADDR_NBR                                           
071400              , BEG_RNG_IP_ADDR                                           
071500     END-EXEC.                                                            
071600                                                                          
071700                                                                          
071800                                                                          
071900 LINKAGE SECTION.                                                         
072000                                                                          
072100 01  DFHCOMMAREA.                                                         
072200     05  FILLER                         OCCURS  1 TO 4072 TIMES           
072300                                        DEPENDING ON EIBCALEN.            
072400         10  FILLER                     PIC X.                            
072500                                                                          
072600                                                                          
072700 PROCEDURE DIVISION.                                                      
072800******************************************************************        
072900**  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
073000**  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
073100**  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
073200******************************************************************        
073300 0000-MAIN-MODULE.                                                        
073400     INITIALIZE DP030-CICS-API-FIELDS.                                    
073500     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
073600     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
073700     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
073800     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
073900     MOVE LENGTH OF DP051AI        TO DP030-MAP-LENGTH (1).               
074000     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
074100     PERFORM 0001-CALL-CICS-ARCH-API.                                     
074200                                                                          
074300     PERFORM 1000-CONTROL-PROCESSING                                      
074400                                                                          
074500     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
074600     PERFORM 0001-CALL-CICS-ARCH-API.                                     
074700                                                                          
074800                                                                          
074900******************************************************************        
075000**  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
075100**  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
075200******************************************************************        
075300 0001-CALL-CICS-ARCH-API.                                                 
075400     CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
075500                                    DFHCOMMAREA                           
075600                                    DP030-CICS-API-FIELDS                 
075700                                    DP020-STANDARD-COMMAREA               
075800                                    DP051AI.                              
075900                                                                          
076000     IF  DP030-RC-CALL-SUCCESSFUL                                         
076100         CONTINUE                                                         
076200     ELSE                                                                 
076300         SET DP013-NO-ROLLBACK                                            
076400             DP013-XCTL-DISPLAY-RESTART                                   
076500             DP013-CICS-ABEND      TO TRUE                                
076600         MOVE '0001-CALL-CICS-ARCH-API'                                   
076700                                   TO DP013-PARAGRAPH                     
076800         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
076900-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
077000         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
077100         PERFORM DP013-0000-PROCESS-ABEND                                 
077200     END-IF.                                                              
077300                                                                          
077400                                                                          
077500*----------------------------------------------------------------*        
077600* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
077700* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
077800*                                                                *        
077900* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
078000*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
078100*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
078200*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
078300*----------------------------------------------------------------*        
078400 1000-CONTROL-PROCESSING.                                                 
078500                                                                          
078600     EVALUATE TRUE                                                        
078700         WHEN DP020-NEXT-ACT-INITIAL                                      
078800*            *****************************************************        
078900*            * NEXT ACT: INITIAL                                 *        
079000*            *****************************************************        
079100             INITIALIZE ASC-SPECIFIC-COMMAREA                             
079200             MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
079300                                      ASC-SEL-TERM-ID                     
079400             MOVE DP020-SRC-TASK-NUMBER                                   
079500                                   TO ASC-LIST-TASK-NUMBER                
079600                                      ASC-SEL-TASK-NUMBER                 
079700             MOVE PC-LIST-QUEUE-SEQ                                       
079800                                   TO ASC-LIST-SEQ                        
079900             MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
080000             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
080100***-----$TEMPLATE NOTE$-------------------------------------------        
080200*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
080300*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
080400*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
080500***---------------------------------------------------------------        
080600             IF  PS-COMMAREA-NOT-VALID                                    
080700***              MOVE PC-TSYMSG-99901                                     
080800***                                TO DP020-MSG-NUMBER                    
080900***              SET DP020-MSG-FATAL                                      
081000***                                TO TRUE                                
081100                 SET DP020-NEXT-ACT-APPL-ERROR                            
081200                                   TO TRUE                                
081300             ELSE                                                         
081400                 PERFORM 4000-BUILD-INITIAL-PANEL                         
081500             END-IF                                                       
081600                                                                          
081700         WHEN DP020-NEXT-ACT-READ-MAP                                     
081800*            *****************************************************        
081900*            * NEXT ACT: READ MAP                                *        
082000*            *****************************************************        
082100             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
082200                                   TO PV-TOP-ITEM                         
082300             PERFORM 2000-PROCESS-PANEL                                   
082400                                                                          
082500         WHEN DP020-NEXT-ACT-RETURN                                       
082600*            *****************************************************        
082700*            * NEXT ACT: RETURN FROM MAINTENANCE PROGRAM         *        
082800*            *****************************************************        
082900             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
083000                                   TO PV-TOP-ITEM                         
083100             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
083200             PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
083300             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
083400             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
083500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
083600                                                                          
083700         WHEN OTHER                                                       
083800*            *****************************************************        
083900*            * NEXT ACT: OTHER (UNDEFINED)                       *        
084000*            *****************************************************        
084100             SET DP013-LOGIC-ABEND TO TRUE                                
084200             SET DP013-NO-ROLLBACK TO TRUE                                
084300             MOVE '1000-CONTROL-PROCESSING'                               
084400                                   TO DP013-PARAGRAPH                     
084500             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
084600                                   TO DP013-MESSAGE-TEXT(1)               
084700             PERFORM DP013-0000-PROCESS-ABEND                             
084800     END-EVALUATE.                                                        
084900                                                                          
085000                                                                          
085100*----------------------------------------------------------------*        
085200* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
085300* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
085400*----------------------------------------------------------------*        
085500 1100-PROCESS-INTER-APPL-COMM.                                            
085600                                                                          
085700     SET PS-COMMAREA-VALID         TO TRUE.                               
085800     MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
085900     IF  DP020-INT-APPL-FORMAT-IND = PC-CRIT-SPEC-FORMAT                  
086000         MOVE DP250-LOCATION-NBR   TO ASC-KEY-LOCATION                    
086100         MOVE DP250-LOCATION-NBR   TO DK-LOCATION-NBR                     
086700         MOVE DP250-LINE           TO ASC-KEY-LINE                        
086800                                      DK-LNE-ADDR-NBR                     
086900         MOVE DP250-PHYSICAL-UNIT  TO ASC-KEY-UNIT                        
087000                                      DK-PHYUNT-ADDR-NBR                  
087100         MOVE DP250-IP-ADDR        TO ASC-KEY-IPAD                        
087200                                      DK-IP-ADDR                          
087300     ELSE                                                                 
087400         IF  DP020-NAV-KEY-DATA > SPACE                                   
087500             CONTINUE                                                     
087600         END-IF                                                           
087700     END-IF.                                                              
087800                                                                          
087900                                                                          
088000*----------------------------------------------------------------*        
088100* DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
088200* ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
088300* USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
088400* OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
088500* FUNCTION KEYS CAN GET THIS FAR.                                *        
088600*----------------------------------------------------------------*        
088700 2000-PROCESS-PANEL.                                                      
088800     EVALUATE TRUE                                                        
088900         WHEN DP020-SRC-AID = DP016-CLEAR                                 
089000*            *****************************************************        
089100*            * PFKEY ACTION: CLEAR                         <ESC> *        
089200*            *   <CLEAR> ("ESC" KEY)                             *        
089300*            *                                                   *        
089400*            * PRESENT SCREEN AS IT WAS AT THE TIME OF THE LAST  *        
089500*            * <ENTER>, INCLUDING ANY ERROR MESSAGES.            *        
089600*            *****************************************************        
089700             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
089800             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
089900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
090000                                                                          
090100         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
090200*            *****************************************************        
090300*            * PFKEY ACTION: REFRESH                             *        
090400*            *                                                   *        
090500*            * PRESENT SCREEN AS FOLLOWS:                        *        
090600*            * - BRING BACK WHAT WAS LAST SAVED.                 *        
090700*            * - SHOW SCREEN AS ORIGINALLY PRESENTED (BLANK OR   *        
090800*            *   WITH DATA FROM DATABASE).                       *        
090900*            *****************************************************        
091000             MOVE ASC-KEY-LOCATION  TO DK-LOCATION-NBR                    
091100             MOVE ASC-KEY-LINE      TO DK-LNE-ADDR-NBR                    
091200             MOVE ASC-KEY-UNIT      TO DK-PHYUNT-ADDR-NBR                 
091300             MOVE ASC-KEY-IPAD      TO DK-IP-ADDR                         
091400             PERFORM 2050-DELETE-LIST-QUEUE                               
091500             PERFORM 4000-BUILD-INITIAL-PANEL                             
091600                                                                          
091700         WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
091800*            *****************************************************        
091900*            * PFKEY ACTION: DESELECT ALL                        *        
092000*            *****************************************************        
092100             PERFORM 2140-DESELECT-ALL-ITEMS                              
092200             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
092300                                                                          
092400         WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
092500*            *****************************************************        
092600*            * PFKEY ACTION: SELECT ALL                          *        
092700*            *****************************************************        
092800             PERFORM 2150-SELECT-ALL-ITEMS                                
092900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
093000                                                                          
093100         WHEN OTHER                                                       
093200*            *****************************************************        
093300*            * PFKEY ACTION: OTHER (APPLICATION SPECIFIC)        *        
093400*            *****************************************************        
093500             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
093600             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
093700             IF  PS-NO-ERROR                                              
093800                 SET DP030-CONTINUE-GO                                    
093900                                 TO TRUE                                  
094000                 PERFORM 2100-CHECK-FUNCTION-KEY                          
094100             ELSE                                                         
094200                 SET DP030-OVERRIDE-GO                                    
094300                                 TO TRUE                                  
094400             END-IF                                                       
094500     END-EVALUATE.                                                        
094600                                                                          
094700                                                                          
094800*----------------------------------------------------------------*        
094900*  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
095000*----------------------------------------------------------------*        
095100 2050-DELETE-LIST-QUEUE.                                                  
095200                                                                          
095300     EXEC CICS                                                            
095400         DELETEQ TS                                                       
095500             QUEUE (ASC-LIST-QUEUE-NAME)                                  
095600             RESP  (PV-CICS-RESP)                                         
095700     END-EXEC.                                                            
095800                                                                          
095900     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
096000        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
096100         CONTINUE                                                         
096200     ELSE                                                                 
096300         SET DP013-CICS-ABEND      TO TRUE                                
096400         SET DP013-NO-ROLLBACK     TO TRUE                                
096500                                                                          
096600         MOVE '2050-DELETE-LIST-QUEUE'                                    
096700                                   TO DP013-PARAGRAPH                     
096800         MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
096900                                   TO DP013-MESSAGE-TEXT (1)              
097000         PERFORM DP013-0000-PROCESS-ABEND                                 
097100     END-IF.                                                              
097200                                                                          
097300     MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
097400                                                                          
097500                                                                          
097600*----------------------------------------------------------------*        
097700* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
097800* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
097900* CICS ARCHITECTURE API.                                         *        
098000*----------------------------------------------------------------*        
098100 2100-CHECK-FUNCTION-KEY.                                                 
098200     EVALUATE TRUE                                                        
098300                                                                          
098400         WHEN DP020-FK-LOCAL-FUNC-02 (DP020-SRC-AID)                      
098500         WHEN DP020-FK-LOCAL-FUNC-03 (DP020-SRC-AID)                      
098600         WHEN DP020-FK-LOCAL-FUNC-04 (DP020-SRC-AID)                      
098700*            *****************************************************        
098800*            * APPLICATION-SPECIFIC PFKEY: LOCAL 02:  INQUIRE    *        
098900*            *                             LOCAL 03:  UPDATE     *        
099000*            *                             LOCAL 04:  DELETE     *        
099100*            *****************************************************        
099200             IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
099300                 INITIALIZE DP251-INTER-APPL-COMMAREA                     
099400                 MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
099500                                TO DP251-NUMBER-OF-SELECTED-ITEMS         
099600                 PERFORM 5000-PREPARE-SEL-QUEUE                           
099700             ELSE                                                         
099800                 SET DP030-OVERRIDE-GO                                    
099900                                   TO TRUE                                
100000                 MOVE PC-TSYMSG-00057                                     
100100                                   TO DP020-MSG-NUMBER                    
100200                 SET DP020-MSG-WARNING                                    
100300                                   TO TRUE                                
100400             END-IF                                                       
100500                                                                          
100600         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
100700*            *****************************************************        
100800*            * APPLICATION-SPECIFIC PFKEY: FIRST PAGE            *        
100900*            *****************************************************        
100901             MOVE +1               TO PV-TOP-ITEM                         
100902             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
100903                                                                          
100904         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
100905*            *****************************************************        
100906*            * APPLICATION-SPECIFIC PFKEY: PREV PAGE             *        
100907*            *****************************************************        
100908             PERFORM 2110-BROWSE-BACKWARD                                 
100909             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
100910                                                                          
100920         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
100930*            *****************************************************        
100940*            * APPLICATION-SPECIFIC PFKEY: NEXT PAGE             *        
100950*            *****************************************************        
100960             PERFORM 2120-BROWSE-FORWARD                                  
100970             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
100980                                                                          
100990         WHEN DP020-SRC-AID = DP016-ENTER                                 
101000*            *****************************************************        
101100*            * APPLICATION-SPECIFIC PFKEY: <ENTER>               *        
101200*            *   EDIT DATA AND PRESENT IT ON THE SCREEN          *        
101300*            *****************************************************        
101400             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
101500             IF  PS-ERROR                                                 
101600                 CONTINUE                                                 
101700             ELSE                                                         
101800                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
101900             END-IF                                                       
102000                                                                          
102100         WHEN OTHER                                                       
102200*            *****************************************************        
102300*            * APPLICATION-SPECIFIC PFKEY: "OTHER" (UNDEFINED)   *        
102400*            *****************************************************        
102500             SET DP013-NO-ROLLBACK                                        
102600                 DP013-XCTL-DISPLAY-RESTART                               
102700                 DP013-CICS-ABEND  TO TRUE                                
102800             MOVE '2100-CHECK-FUNCTION-KEY'                               
102900                                   TO DP013-PARAGRAPH                     
103000             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
103100                                   TO DP013-MESSAGE-TEXT (1)              
103200             MOVE 'OR CODE MISSING FROM THIS PROGRAM TO PROCESS:'         
103300                                   TO DP013-MESSAGE-TEXT (2)              
103400             MOVE DP020-SRC-AID    TO PM-A-SRC-AID                        
103500             MOVE DP020-FK-ACTION (DP020-SRC-AID)                         
103600                                   TO PM-A-FK-ACTION                      
103700             MOVE DP020-FK-DISPN-IND (DP020-SRC-AID)                      
103800                                   TO PM-A-FK-DISPN                       
103900             MOVE PM-ABEND-INFO-A  TO DP013-MESSAGE-TEXT (3)              
104000             MOVE 'REVIEW PFKEYS SETUP FOR THIS MNEMONIC.'                
104100                                   TO DP013-MESSAGE-TEXT (4)              
104200             PERFORM DP013-0000-PROCESS-ABEND                             
104300     END-EVALUATE.                                                        
104400                                                                          
104500                                                                          
104600*----------------------------------------------------------------*        
104700*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
104800*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
104900*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
105000*    MESSAGE.                                                    *        
105100*----------------------------------------------------------------*        
105200 2110-BROWSE-BACKWARD.                                                    
105300     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
105400         GIVING PV-TOP-ITEM.                                              
105500                                                                          
105600     IF  ((PV-TOP-ITEM < +1)                                              
105700       OR (PV-TOP-ITEM = +1))                                             
105800         MOVE +1                   TO PV-TOP-ITEM                         
105900*        -- TOP OF LIST --                                                
106000         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
106100         SET DP020-MSG-INFORMATIONAL TO TRUE                              
106200     END-IF.                                                              
106300                                                                          
106400                                                                          
106500*----------------------------------------------------------------*        
106600*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
106700*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
106800*    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
106900*----------------------------------------------------------------*        
107000 2120-BROWSE-FORWARD.                                                     
107100                                                                          
107200     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
107300         GIVING PV-TOP-ITEM.                                              
107400     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
107500         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
107600                                   TO PV-TOP-ITEM                         
107700*        -- BOTTOM OF LIST --                                             
107800         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
107900         SET DP020-MSG-INFORMATIONAL                                      
108000                                   TO TRUE                                
108100     ELSE                                                                 
108200         COMPUTE PV-BOTTOM-ITEM =                                         
108300             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
108400         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
108500*            -- BOTTOM OF LIST --                                         
108600             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
108700             SET DP020-MSG-INFORMATIONAL                                  
108800                                   TO TRUE                                
108900         END-IF                                                           
109000     END-IF.                                                              
109100                                                                          
109200                                                                          
109300*----------------------------------------------------------------*        
109400*    "JUMP BROWSE" FEATURE OF THE ARCHITECTURE:                  *        
109500*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
109600*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
109700*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
109800*    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
109900*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
110000*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
110100*----------------------------------------------------------------*        
110200 2130-CHECK-FOR-JUMP-BROWSE.                                              
110300     SET PS-NO-ERROR               TO TRUE.                               
110400     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
110500         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
110600                                   TO PV-TOP-ITEM                         
110700     ELSE                                                                 
110800         MOVE ASC-START-ITEM-REQUEST                                      
110900                                   TO DP010I-UNEDITED-FIELD               
111000         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
111100         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
111200         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
111300                                   TO TRUE                                
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
111500                                                                          
111600         IF  NOT DP010I-ERROR-DETECTED                                    
111700             MOVE DP010I-NUMERIC-FIELD                                    
111800                                   TO  ASC-START-ITEM-REQUEST-N           
111900             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
112000                 IF  ASC-START-ITEM-REQUEST-N >                           
112100                                          ASC-NUMBER-OF-ITEMS-READ        
112200                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
112300                     COMPUTE PV-TOP-ITEM =                                
112400                             ASC-NUMBER-OF-ITEMS-READ                     
112500                             - PC-ITEMS-PER-PANEL + 1                     
112600                     IF  PV-TOP-ITEM < +1                                 
112700                         MOVE +1   TO PV-TOP-ITEM                         
112800                         MOVE PC-TSYMSG-00070                             
112900                                   TO DP020-MSG-NUMBER                    
113000                         SET DP020-MSG-INFORMATIONAL                      
113100                                   TO TRUE                                
113200                     END-IF                                               
113300                 ELSE                                                     
113400                     MOVE ASC-START-ITEM-REQUEST-N                        
113500                                   TO PV-TOP-ITEM                         
113600                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
113700                     COMPUTE PV-BOTTOM-ITEM =                             
113800                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
113900                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
114000*                        -- BOTTOM OF LIST --                             
114100                         MOVE PC-TSYMSG-00070                             
114200                                   TO DP020-MSG-NUMBER                    
114300                         SET DP020-MSG-INFORMATIONAL                      
114400                                   TO TRUE                                
114500                     END-IF                                               
114600                 END-IF                                                   
114700             ELSE                                                         
114800*                --- VALUE OUT OF RANGE ----                              
114900                 MOVE PC-TSYMSG-00101                                     
115000                                   TO DP020-MSG-NUMBER                    
115100                 MOVE -1           TO ASTRTITL                            
115200                 SET DP030-SET-CURSOR-APPL-1                              
115300                                   TO TRUE                                
115400                 MOVE DP015-REVERSE                                       
115500                                   TO ASTRTITH                            
115600                 MOVE DP015-RED    TO ASTRTITC                            
115700                 MOVE DP015-UNP-NUM-BRT-MDT                               
115800                                   TO ASTRTITA                            
115900                 SET DP020-MSG-FATAL                                      
116000                                   TO TRUE                                
116100                 SET PS-ERROR      TO TRUE                                
116200             END-IF                                                       
116300                                                                          
116400         ELSE                                                             
116500             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
116600             MOVE -1               TO ASTRTITL                            
116700             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
116800             MOVE DP015-REVERSE    TO ASTRTITH                            
116900             MOVE DP015-RED        TO ASTRTITC                            
117000             MOVE DP015-UNP-NUM-BRT-MDT                                   
117100                                   TO ASTRTITA                            
117200             SET DP020-MSG-FATAL   TO TRUE                                
117300             SET PS-ERROR          TO TRUE                                
117400         END-IF                                                           
117500     END-IF.                                                              
117600                                                                          
117700                                                                          
117800*----------------------------------------------------------------*        
117900*    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
118000*    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
118100*----------------------------------------------------------------*        
118200 2140-DESELECT-ALL-ITEMS.                                                 
118300     MOVE +1                       TO ASC-BLK-SUB.                        
118400                                                                          
118500     PERFORM 2141-CLEAR-SELECTED-INDS                                     
118600         VARYING ASC-ITEM-SUB                                             
118700         FROM 1 BY 1                                                      
118800         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
118900                                                                          
119000     ADD +1                        TO ASC-BLK-SUB.                        
119100                                                                          
119200     PERFORM 2141-CLEAR-SELECTED-INDS                                     
119300         VARYING ASC-ITEM-SUB                                             
119400         FROM 1 BY 1                                                      
119500         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
119600                                                                          
119700     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
119800                                   TO PV-LIST-ITEM-NUMBER.                
119900                                                                          
120000     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
120100         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
120200                                                                          
120300     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
120400        (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
120500         MOVE +1                   TO ASC-BLK-SUB                         
120600                                                                          
120700         PERFORM 5100-WRITE-TEMP-STORAGE                                  
120800                                                                          
120900         MOVE +1                   TO PV-BLOCK-NUMBER                     
121000                                                                          
121100         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
121200             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
121300                                                                          
121400         PERFORM                                                          
121500             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
121600                                                                          
121700             PERFORM 4410-READ-TEMP-STORAGE                               
121800                                                                          
121900             PERFORM 2141-CLEAR-SELECTED-INDS                             
122000                 VARYING ASC-ITEM-SUB FROM 1 BY 1                         
122100                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
122200                                                                          
122300             PERFORM 5100-WRITE-TEMP-STORAGE                              
122400                                                                          
122500             ADD +1                TO PV-BLOCK-NUMBER                     
122600                                                                          
122700         END-PERFORM                                                      
122800                                                                          
122900         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
123000             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
123100                                                                          
123200         PERFORM                                                          
123300             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
123400                   ASC-HIGHEST-BLOCK-WRITTEN                              
123500                                                                          
123600             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
123700                                   TO PV-BLOCK-NUMBER                     
123800                                                                          
123900             PERFORM 4410-READ-TEMP-STORAGE                               
124000                                                                          
124100             PERFORM 2141-CLEAR-SELECTED-INDS                             
124200                 VARYING ASC-ITEM-SUB                                     
124300                 FROM 1 BY 1                                              
124400                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
124500                                                                          
124600             PERFORM 5100-WRITE-TEMP-STORAGE                              
124700                                                                          
124800             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
124900                                                                          
125000         END-PERFORM                                                      
125100                                                                          
125200         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
125300                                   TO PV-BLOCK-NUMBER                     
125400                                                                          
125500         PERFORM 4410-READ-TEMP-STORAGE                                   
125600     END-IF.                                                              
125700                                                                          
125800                                                                          
125900     MOVE +0                      TO ASC-NUMBER-OF-SELECTED-ITEMS.        
126000     MOVE +0                      TO ASC-START-OF-SELECTED-RANGE.         
126100     MOVE +0                      TO ASC-END-OF-SELECTED-RANGE.           
126200                                                                          
126300     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
126400                                                                          
126500                                                                          
126600*----------------------------------------------------------------*        
126700*    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
126800*----------------------------------------------------------------*        
126900 2141-CLEAR-SELECTED-INDS.                                                
127000                                                                          
127100     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
127200             NOT EQUAL ZERO                                               
127300         IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
127400             SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
127500                 ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
127600                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
127700                                   TO TRUE                                
127800         END-IF                                                           
127900     END-IF.                                                              
128000                                                                          
128100                                                                          
128200*----------------------------------------------------------------*        
128300*  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *        
128400*  FOR ALL ITEMS.                                                *        
128500*----------------------------------------------------------------*        
128600 2150-SELECT-ALL-ITEMS.                                                   
128700                                                                          
128800     MOVE +1                       TO ASC-BLK-SUB.                        
128900                                                                          
129000     PERFORM 2151-SET-SELECTED-INDS                                       
129100         VARYING ASC-ITEM-SUB                                             
129200         FROM 1 BY 1                                                      
129300         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
129400                                                                          
129500     ADD +1                        TO ASC-BLK-SUB.                        
129600                                                                          
129700     PERFORM 2151-SET-SELECTED-INDS                                       
129800         VARYING ASC-ITEM-SUB                                             
129900         FROM 1 BY 1                                                      
130000         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
130100                                                                          
130200     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
130300                                   TO PV-LIST-ITEM-NUMBER.                
130400                                                                          
130500     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
130600         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
130700                                                                          
130800     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
130900        (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
131000         MOVE +1                   TO ASC-BLK-SUB                         
131100                                                                          
131200         PERFORM 5100-WRITE-TEMP-STORAGE                                  
131300                                                                          
131400         MOVE +1                   TO PV-BLOCK-NUMBER                     
131500                                                                          
131600         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
131700             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
131800                                                                          
131900         PERFORM                                                          
132000             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
132100                                                                          
132200             PERFORM 4410-READ-TEMP-STORAGE                               
132300                                                                          
132400             PERFORM 2151-SET-SELECTED-INDS                               
132500                 VARYING ASC-ITEM-SUB                                     
132600                 FROM 1 BY 1                                              
132700                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
132800                                                                          
132900             PERFORM 5100-WRITE-TEMP-STORAGE                              
133000                                                                          
133100             ADD +1                TO PV-BLOCK-NUMBER                     
133200                                                                          
133300         END-PERFORM                                                      
133400                                                                          
133500         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
133600             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
133700                                                                          
133800         PERFORM                                                          
133900             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
134000                   ASC-HIGHEST-BLOCK-WRITTEN                              
134100                                                                          
134200             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
134300                                   TO PV-BLOCK-NUMBER                     
134400                                                                          
134500             PERFORM 4410-READ-TEMP-STORAGE                               
134600                                                                          
134700             PERFORM 2151-SET-SELECTED-INDS                               
134800                 VARYING ASC-ITEM-SUB                                     
134900                 FROM 1 BY 1                                              
135000                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
135100                                                                          
135200             PERFORM 5100-WRITE-TEMP-STORAGE                              
135300                                                                          
135400             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
135500                                                                          
135600         END-PERFORM                                                      
135700                                                                          
135800         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
135900                                   TO PV-BLOCK-NUMBER                     
136000                                                                          
136100         PERFORM 4410-READ-TEMP-STORAGE                                   
136200     END-IF.                                                              
136300                                                                          
136400                                                                          
136500     MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
136600                       ASC-NUMBER-OF-SELECTED-ITEMS.                      
136700                                                                          
136800     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
136900                                                                          
137000                                                                          
137100*----------------------------------------------------------------*        
137200*    MARK EACH ITEM AS BEING SELECTED.                           *        
137300*----------------------------------------------------------------*        
137400 2151-SET-SELECTED-INDS.                                                  
137500                                                                          
137600     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
137700             NOT EQUAL ZERO                                               
137800         IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
137900             SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
138000                 ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
138100                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
138200                               TO  TRUE                                   
138300         END-IF                                                           
138400     END-IF.                                                              
138500                                                                          
138600                                                                          
138700******************************************************************        
138800** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
138900** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
139000******************************************************************        
139100 2200-MOVE-SCREEN-TO-COMMAREA.                                            
139200                                                                          
139300     MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
139400     MOVE +1                       TO PV-SUB.                             
139500                                                                          
139600     PERFORM 3100-SET-SUBSCRIPTS.                                         
139700     IF  ASTRTITL > ZERO                                                  
139800         MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
139900     ELSE                                                                 
140000         IF  ASTRTITF = DP015-ERASE-EOF                                   
140100             INITIALIZE ASC-START-ITEM-REQUEST                            
140200         END-IF                                                           
140300     END-IF.                                                              
140400     PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
140500         VARYING PV-SUB                                                   
140600         FROM 1 BY 1                                                      
140700         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
140800                                                                          
140900                                                                          
141000******************************************************************        
141100** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
141200******************************************************************        
141300 2210-MOVE-LINES-TO-COMMAREA.                                             
141400     IF  MR-SELECTL (PV-SUB) > ZERO                                       
141500         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
141600         MOVE MR-SELECTI (PV-SUB)                                         
141700                                   TO ASC-TEMP-SELECTED-SW                
141800                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
141900     ELSE                                                                 
142000         IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
142100                 SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
142200                 MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
142300                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
142400         END-IF                                                           
142500     END-IF.                                                              
142600     ADD +1                        TO ASC-ITEM-SUB.                       
142700     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
142800         ADD +1                    TO ASC-BLK-SUB                         
142900         MOVE +1                   TO ASC-ITEM-SUB                        
143000     END-IF.                                                              
143100                                                                          
143200                                                                          
143300******************************************************************        
143400** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
143500** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
143600******************************************************************        
143700 3000-EDIT-DATA-IN-COMMAREA.                                              
143800     SET PS-NO-ERROR               TO TRUE.                               
143900     PERFORM 3200-RESET-ATTRIBUTES.                                       
144000     MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
144100**                                                                        
144200* CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
144300* EDITTING IS DONE FROM THE BOTTOM UP.                                    
144400**                                                                        
144500     COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
144600         ASC-ITEMS-DISPLAYED - 1.                                         
144700     PERFORM 3100-SET-SUBSCRIPTS.                                         
144800     PERFORM 3300-EDIT-SELECTIONS                                         
144900         VARYING PV-SUB                                                   
145000         FROM PV-SUB BY -1                                                
145100         UNTIL PV-SUB < +1                                                
145200                                                                          
145300     IF  PS-NO-ERROR                                                      
145400         PERFORM 3400-CHECK-RANGE-SEQUENCE                                
145500     END-IF.                                                              
145600                                                                          
145700     IF  ((PS-NO-ERROR)                                                   
145800       AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
145900       AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
146000             PERFORM 3500-MARK-RANGE-AS-SELECTED                          
146100     END-IF.                                                              
146200                                                                          
146300                                                                          
146400*----------------------------------------------------------------*        
146500*    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
146600*    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
146700*    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
146800*                                                                *        
146900*    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
147000*    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
147100*    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
147200*    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
147300*----------------------------------------------------------------*        
147400 3100-SET-SUBSCRIPTS.                                                     
147500                                                                          
147600     COMPUTE ASC-ITEM-SUB =                                               
147700         (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
147800                                                                          
147900     IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
148000         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
148100                                                                          
148200         MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
148300     ELSE                                                                 
148400         MOVE +1                     TO ASC-BLK-SUB                       
148500     END-IF.                                                              
148600                                                                          
148700                                                                          
148800******************************************************************        
148900** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
149000******************************************************************        
149100 3200-RESET-ATTRIBUTES.                                                   
149200     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
149300     MOVE DP015-GREEN              TO ASTRTITC.                           
149400     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
149500                                                                          
149600     PERFORM                                                              
149700         VARYING PV-SUB                                                   
149800         FROM 1 BY 1                                                      
149900         UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
150000         MOVE DP015-UNP-ALP-NOR-OFF                                       
150100                                   TO MR-SELECTA (PV-SUB)                 
150200         MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
150300         MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
150400     END-PERFORM.                                                         
150500                                                                          
150600                                                                          
150700******************************************************************        
150800** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **        
150900** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **        
151000** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **        
151100** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **        
151200******************************************************************        
151300 3300-EDIT-SELECTIONS.                                                    
151400     EVALUATE TRUE                                                        
151500         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
151600             = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
151700                 CONTINUE                                                 
151800         WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
151900             IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
152000                 ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
152100                 MOVE ASC-TEMP-SELECTED-SW                                
152200                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
152300                                TO ASC-SELECTED-SW                        
152400                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
152500             END-IF                                                       
152600         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
152700                 = SPACE                                                  
152800             EVALUATE TRUE                                                
152900                 WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
153000                     SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
153100                     MOVE SPACE TO ASC-SELECTED-SW                        
153200                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
153300                 WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
153400                     MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
153500                     MOVE SPACE TO ASC-SELECTED-SW                        
153600                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
153700                 WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
153800                     MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
153900                     MOVE SPACE TO ASC-SELECTED-SW                        
154000                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
154100             END-EVALUATE                                                 
154200         WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
154300             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
154400                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
154500             END-IF                                                       
154600             PERFORM 3310-EDIT-RANGE                                      
154700         WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
154800             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
154900                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
155000             END-IF                                                       
155100             PERFORM 3310-EDIT-RANGE                                      
155200         WHEN OTHER                                                       
155300             MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
155400             SET DP020-MSG-FATAL    TO TRUE                               
155500             MOVE -1                TO MR-SELECTL (PV-SUB)                
155600             SET DP030-SET-CURSOR-APPL-1                                  
155700                                    TO TRUE                               
155800             MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
155900             MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
156000             SET PS-ERROR TO TRUE                                         
156100             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
156200                 SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
156300                 MOVE SPACE         TO ASC-SELECTED-SW                    
156400                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
156500             END-IF                                                       
156600     END-EVALUATE.                                                        
156700                                                                          
156800     SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
156900     SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
157000     IF  ASC-ITEM-SUB < 1                                                 
157100         SUBTRACT +1 FROM ASC-BLK-SUB                                     
157200         MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
157300     END-IF.                                                              
157400                                                                          
157500                                                                          
157600******************************************************************        
157700** IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A      **        
157800** RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1    **        
157900** ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN)**        
158000** THE ENDING.                                                  **        
158100******************************************************************        
158200 3310-EDIT-RANGE.                                                         
158300     IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
158400         IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
158500             MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
158600                                   TO ASC-SELECTED-SW                     
158700                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
158800             MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
158900         ELSE                                                             
159000             IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
159100                 CONTINUE                                                 
159200             ELSE                                                         
159300*                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
159400                 MOVE PC-TSYMSG-00054                                     
159500                                   TO DP020-MSG-NUMBER                    
159600                 SET DP020-MSG-FATAL                                      
159700                                   TO TRUE                                
159800                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
159900                 SET DP030-SET-CURSOR-APPL-1                              
160000                                   TO TRUE                                
160100                 MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
160200                 MOVE DP015-UNDERLINE                                     
160300                                   TO MR-SELECTH (PV-SUB)                 
160400                 SET PS-ERROR      TO TRUE                                
160500             END-IF                                                       
160600         END-IF                                                           
160700     ELSE                                                                 
160800         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
160900             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
161000                                   TO ASC-SELECTED-SW                     
161100                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
161200             MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
161300         ELSE                                                             
161400             IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
161500                 CONTINUE                                                 
161600             ELSE                                                         
161700*                -- ONLY ONE END-OF-RANGE ALLOWED --                      
161800                 MOVE PC-TSYMSG-00055                                     
161900                                   TO DP020-MSG-NUMBER                    
162000                 SET DP020-MSG-FATAL                                      
162100                                   TO TRUE                                
162200                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
162300                 SET DP030-SET-CURSOR-APPL-1                              
162400                                   TO TRUE                                
162500                 SET PS-ERROR      TO TRUE                                
162600             END-IF                                                       
162700         END-IF                                                           
162800     END-IF.                                                              
162900                                                                          
163000                                                                          
163100******************************************************************        
163200** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **        
163300** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **        
163400******************************************************************        
163500 3400-CHECK-RANGE-SEQUENCE.                                               
163600     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
163700       AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
163800       AND  ASC-START-OF-SELECTED-RANGE >                                 
163900                                         ASC-END-OF-SELECTED-RANGE        
164000*        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
164100         MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
164200         SET DP020-MSG-FATAL TO TRUE                                      
164300         SET PS-ERROR TO TRUE                                             
164400         PERFORM 3410-POSITION-AT-ERROR                                   
164500     END-IF.                                                              
164600                                                                          
164700                                                                          
164800******************************************************************        
164900** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **        
165000** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **        
165100** SHOULD BE POSITIONED ON A RANGE ERROR.                       **        
165200******************************************************************        
165300 3410-POSITION-AT-ERROR.                                                  
165400     SET DP030-SET-CURSOR-APPL-1                                          
165500                                   TO TRUE.                               
165600     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
165700                    - PV-TOP-ITEM + 1.                                    
165800     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
165900         CONTINUE                                                         
166000     ELSE                                                                 
166100         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
166200         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
166300         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
166400     END-IF.                                                              
166500                                                                          
166600     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
166700                    - PV-TOP-ITEM + 1.                                    
166800     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
166900         CONTINUE                                                         
167000     ELSE                                                                 
167100         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
167200         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
167300         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
167400     END-IF.                                                              
167500                                                                          
167600                                                                          
167700*----------------------------------------------------------------*        
167800*    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
167900*    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
168000*    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
168100*----------------------------------------------------------------*        
168200 3500-MARK-RANGE-AS-SELECTED.                                             
168300     MOVE +1                       TO  ASC-BLK-SUB.                       
168400     PERFORM 3510-MARK-SELECTED-INDS                                      
168500         VARYING ASC-ITEM-SUB                                             
168600         FROM 1 BY 1                                                      
168700         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
168800                                                                          
168900     ADD +1                        TO ASC-BLK-SUB.                        
169000     PERFORM 3510-MARK-SELECTED-INDS                                      
169100         VARYING ASC-ITEM-SUB                                             
169200         FROM 1 BY 1                                                      
169300         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
169400                                                                          
169500     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
169600                                   TO  PV-LIST-ITEM-NUMBER.               
169700     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
169800         1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
169900               PC-ITEMS-PER-PANEL).                                       
170000                                                                          
170100     MOVE ASC-START-OF-SELECTED-RANGE                                     
170200                                   TO PV-START-OF-SELECTED-RANGE.         
170300     COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
170400         1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
170500               PC-ITEMS-PER-PANEL).                                       
170600                                                                          
170700     MOVE ASC-END-OF-SELECTED-RANGE                                       
170800                                   TO  PV-END-OF-SELECTED-RANGE.          
170900     COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
171000         1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
171100               PC-ITEMS-PER-PANEL).                                       
171200     COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
171300         PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
171400                                                                          
171500     IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
171600        (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
171700         MOVE +1                   TO ASC-BLK-SUB                         
171800         PERFORM 5100-WRITE-TEMP-STORAGE                                  
171900         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
172000             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
172100                                                                          
172200         PERFORM                                                          
172300             UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
172400                   PV-PREV-BLOCK-IN-ARRAY                                 
172500             MOVE PV-FIRST-BLOCK-IN-RANGE                                 
172600                                   TO PV-BLOCK-NUMBER                     
172700             PERFORM 4410-READ-TEMP-STORAGE                               
172800             PERFORM 3510-MARK-SELECTED-INDS                              
172900                 VARYING ASC-ITEM-SUB                                     
173000                 FROM 1 BY 1                                              
173100                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
173200             PERFORM 5100-WRITE-TEMP-STORAGE                              
173300             ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
173400         END-PERFORM                                                      
173500                                                                          
173600         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
173700             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
173800         PERFORM                                                          
173900             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
174000                   PV-LAST-BLOCK-IN-RANGE                                 
174100             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
174200                                   TO PV-BLOCK-NUMBER                     
174300             PERFORM 4410-READ-TEMP-STORAGE                               
174400             PERFORM 3510-MARK-SELECTED-INDS                              
174500                 VARYING ASC-ITEM-SUB                                     
174600                 FROM 1 BY 1                                              
174700                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
174800             PERFORM 5100-WRITE-TEMP-STORAGE                              
174900             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
175000         END-PERFORM                                                      
175100                                                                          
175200         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
175300                                   TO PV-BLOCK-NUMBER                     
175400         PERFORM 4410-READ-TEMP-STORAGE                                   
175500     END-IF.                                                              
175600                                                                          
175700     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
175800                                      ASC-END-OF-SELECTED-RANGE.          
175900                                                                          
176000                                                                          
176100*----------------------------------------------------------------*        
176200* SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
176300*----------------------------------------------------------------*        
176400 3510-MARK-SELECTED-INDS.                                                 
176500                                                                          
176600     IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
176700                                ASC-ITEM-SUB) >                           
176800         ASC-START-OF-SELECTED-RANGE)                                     
176900     OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
177000                                ASC-ITEM-SUB) =                           
177100         ASC-START-OF-SELECTED-RANGE)                                     
177200                                                                          
177300         IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
177400                                    ASC-ITEM-SUB) <                       
177500             ASC-END-OF-SELECTED-RANGE)                                   
177600         OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
177700                                    ASC-ITEM-SUB) =                       
177800             ASC-END-OF-SELECTED-RANGE)                                   
177900                                                                          
178000             IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
178100                 SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
178200                     ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
178300                               TO TRUE                                    
178400                 SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      
178500                               TO TRUE                                    
178600                 ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS            
178700             END-IF                                                       
178800         END-IF                                                           
178900     END-IF.                                                              
179000                                                                          
179100                                                                          
179200*----------------------------------------------------------------*        
179300*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
179400*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
179500*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
179600*    DISPLAY THE PANEL TO USER.                                  *        
179700*----------------------------------------------------------------*        
179800 4000-BUILD-INITIAL-PANEL.                                                
179900                                                                          
180000     INITIALIZE                 ASC-BLOCK-ENTRIES (1)                     
180100                                ASC-BLOCK-ENTRIES (2).                    
180200     MOVE +1                 TO ASC-BLK-SUB.                              
180300     SET ASC-ITEMS-LEFT-ON-DB                                             
180400                             TO TRUE.                                     
180500                                                                          
180600     MOVE +0                 TO ASC-ITEM-SUB                              
180700                                ASC-NUMBER-OF-ITEMS-READ                  
180800                                ASC-NUMBER-OF-SELECTED-ITEMS              
180900                                ASC-HIGHEST-BLOCK-WRITTEN                 
181000                                ASC-START-OF-SELECTED-RANGE               
181100                                ASC-END-OF-SELECTED-RANGE                 
181200                                ASC-ITEM-AT-TOP-OF-PANEL.                 
181300     PERFORM 4100-PREPARE-CURSOR.                                         
181400                                                                          
181500     PERFORM 4010-OPEN-CURSOR.                                            
181600                                                                          
181700     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
181800                                                                          
181900     PERFORM 4020-CLOSE-CURSOR.                                           
182000                                                                          
182100     MOVE +1                   TO  PV-TOP-ITEM.                           
182200     IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
182300         SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
182400         SET DP020-MSG-FATAL   TO  TRUE                                   
182500         MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
182600     ELSE                                                                 
182700         MOVE -1               TO MR-SELECTL (1)                          
182800         SET DP030-SET-CURSOR-APPL-1                                      
182900                               TO TRUE                                    
183000         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
183100     END-IF.                                                              
183200                                                                          
183300                                                                          
183400*----------------------------------------------------------------*        
183500* OPEN THE CURSOR FOR THE PHYSICAL UNIT TABLE: TPHYUNT.          *        
183600*   IN THIS PARTICULAR APPLICATION ONE OF 6 DIFFERENT CURSORS    *        
183700*   WILL BE OPENED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *        
183800*   USER.                                                        *        
183900*----------------------------------------------------------------*        
184000 4010-OPEN-CURSOR.                                                        
184100     PERFORM                                                              
184200         WITH TEST AFTER                                                  
184300         VARYING PV-RETRY-COUNTER                                         
184400         FROM 1 BY 1                                                      
184500         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
184600                   OR (SQLCODE = ZERO))                                   
184700         EVALUATE TRUE                                                    
184800             WHEN ASC-NO-CURSOR                                           
184900                 MOVE 'CURSOR:  NO-CSR'                                   
185000                                   TO DP013-MESSAGE-TEXT (2)              
185100                 EXEC SQL                                                 
185200                      OPEN NO-CSR                                         
185300                 END-EXEC                                                 
185400                                                                          
185500             WHEN ASC-STOR-CURSOR                                         
185600                 MOVE 'CURSOR:  STOR-CSR'                                 
185700                                   TO DP013-MESSAGE-TEXT (2)              
185800                 EXEC SQL                                                 
185900                      OPEN STOR-CSR                                       
186000                 END-EXEC                                                 
186100                                                                          
186200             WHEN ASC-LINE-CURSOR                                         
186300                 MOVE 'CURSOR:  LINE-CSR'                                 
186400                                   TO DP013-MESSAGE-TEXT (2)              
186500                 EXEC SQL                                                 
186600                      OPEN LINE-CSR                                       
186700                 END-EXEC                                                 
186800                                                                          
186900             WHEN ASC-UNIT-CURSOR                                         
187000                 MOVE 'CURSOR:  UNIT-CSR'                                 
187100                                   TO DP013-MESSAGE-TEXT (2)              
187200                 EXEC SQL                                                 
187300                      OPEN UNIT-CSR                                       
187400                 END-EXEC                                                 
187500                                                                          
187600             WHEN ASC-VTAM-CURSOR                                         
187700                 MOVE 'CURSOR:  VTAM-CSR'                                 
187800                                   TO DP013-MESSAGE-TEXT (2)              
187900                 EXEC SQL                                                 
188000                      OPEN VTAM-CSR                                       
188100                 END-EXEC                                                 
188200                                                                          
188300             WHEN ASC-IPAD-CURSOR                                         
188400                 MOVE 'CURSOR:  IPAD-CSR'                                 
188500                                   TO DP013-MESSAGE-TEXT (2)              
188600                 EXEC SQL                                                 
188700                      OPEN IPAD-CSR                                       
188800                 END-EXEC                                                 
188900         END-EVALUATE                                                     
189000                                                                          
189100         EVALUATE TRUE                                                    
189200             WHEN SQLCODE = ZERO                                          
189300             WHEN SQLCODE = -904                                          
189400             WHEN SQLCODE = -913                                          
189500                 CONTINUE                                                 
189600             WHEN SQLWARN0 NOT = SPACES                                   
189700             WHEN SQLCODE < ZERO                                          
189800             WHEN SQLCODE > ZERO                                          
189900                 MOVE '4010-OPEN-CURSOR'                                  
190000                                   TO DP013-PARAGRAPH                     
190100                 MOVE 'OPEN PHYSICAL UNIT CURSOR - '                      
190200                                   TO DP013-MESSAGE-TEXT (1)              
190300                 MOVE SQLCA        TO DP013-SQLCA                         
190400                 MOVE 'TPHYUNT '   TO DP013-DB2-TABLE-NAME (1)            
190500                 SET DP013-DB2-ABEND                                      
190600                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
190700                 PERFORM DP013-0000-PROCESS-ABEND                         
190800         END-EVALUATE                                                     
190900     END-PERFORM.                                                         
191000                                                                          
191100     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
191200         MOVE '4010-OPEN-CURSOR'   TO DP013-PARAGRAPH                     
191300         MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'           
191400                                   TO DP013-MESSAGE-TEXT (1)              
191500         MOVE SQLCA                TO DP013-SQLCA                         
191600         MOVE 'TPHYUNT '           TO DP013-DB2-TABLE-NAME (1)            
191700         SET DP013-DB2-ABEND                                              
191800             DP013-XCTL-DISPLAY-RESTART                                   
191900                                   TO TRUE                                
192000         PERFORM DP013-0000-PROCESS-ABEND                                 
192100     END-IF.                                                              
192200                                                                          
192300                                                                          
192400*----------------------------------------------------------------*        
192500* OPEN THE CURSOR FOR THE PHYSICAL UNIT TABLE: TPHYUNT.          *        
192600*   IN THIS PARTICULAR APPLICATION ONE OF 6 DIFFERENT CURSORS    *        
192700*   WILL BE CLOSED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *        
192800*   USER.                                                        *        
192900*----------------------------------------------------------------*        
193000 4020-CLOSE-CURSOR.                                                       
193100     EVALUATE TRUE                                                        
193200         WHEN ASC-NO-CURSOR                                               
193300             EXEC SQL                                                     
193400                 CLOSE NO-CSR                                             
193500             END-EXEC                                                     
193600                                                                          
193700         WHEN ASC-STOR-CURSOR                                             
193800             EXEC SQL                                                     
193900                 CLOSE STOR-CSR                                           
194000             END-EXEC                                                     
194100                                                                          
194200         WHEN ASC-VTAM-CURSOR                                             
194300             EXEC SQL                                                     
194400                 CLOSE VTAM-CSR                                           
194500             END-EXEC                                                     
194600                                                                          
194700         WHEN ASC-LINE-CURSOR                                             
194800             EXEC SQL                                                     
194900                 CLOSE LINE-CSR                                           
195000             END-EXEC                                                     
195100                                                                          
195200         WHEN ASC-UNIT-CURSOR                                             
195300             EXEC SQL                                                     
195400                 CLOSE UNIT-CSR                                           
195500             END-EXEC                                                     
195600                                                                          
195700         WHEN ASC-IPAD-CURSOR                                             
195800             EXEC SQL                                                     
195900                 CLOSE IPAD-CSR                                           
196000             END-EXEC                                                     
196100     END-EVALUATE.                                                        
196200                                                                          
196300     EVALUATE TRUE                                                        
196400         WHEN SQLCODE = ZERO                                              
196500             CONTINUE                                                     
196600         WHEN SQLWARN0 NOT = SPACES                                       
196700         WHEN SQLCODE < ZERO                                              
196800         WHEN SQLCODE > ZERO                                              
196900             MOVE '4020-CLOSE-CURSOR'                                     
197000                               TO DP013-PARAGRAPH                         
197100             MOVE 'CLOSE PHYSICAL UNIT CURSOR '                           
197200                               TO DP013-MESSAGE-TEXT (1)                  
197300             MOVE SQLCA        TO DP013-SQLCA                             
197400             MOVE 'TPHYUNT '   TO DP013-DB2-TABLE-NAME (1)                
197500             SET DP013-DB2-ABEND                                          
197600                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
197700             PERFORM DP013-0000-PROCESS-ABEND                             
197800     END-EVALUATE.                                                        
197900                                                                          
198000                                                                          
198100*----------------------------------------------------------------*        
198200*    SET UP THE HOST VARIABLES.                                  *        
198300*----------------------------------------------------------------*        
198400 4100-PREPARE-CURSOR.                                                     
198500                                                                          
198600     SET ASC-NO-CURSOR         TO TRUE.                                   
198700                                                                          
198800     IF  DK-LOCATION-NBR GREATER THAN ZERO                                
199000*        *********************************************************        
199100*        * STORE CURSOR                                          *        
199200*        *********************************************************        
199300         SET ASC-STOR-CURSOR    TO TRUE                                   
199400     END-IF.                                                              
199500                                                                          
199600     IF  DK-LNE-ADDR-NBR    GREATER THAN SPACES                           
199700     AND DK-PHYUNT-ADDR-NBR GREATER THAN SPACES                           
199800*        *********************************************************        
199900*        * VTAM CURSOR                                           *        
200000*        *********************************************************        
200100         SET ASC-VTAM-CURSOR   TO TRUE                                    
200200     ELSE                                                                 
200300         IF  DK-LNE-ADDR-NBR GREATER THAN SPACES                          
200400*            *****************************************************        
200500*            * LINE CURSOR                                       *        
200600*            *****************************************************        
200700             SET ASC-LINE-CURSOR                                          
200800                               TO TRUE                                    
200900         ELSE                                                             
201000             IF  DK-PHYUNT-ADDR-NBR GREATER THAN SPACES                   
201100*                *************************************************        
201200*                * UNIT CURSOR                          *                 
201300*                *************************************************        
201400                 SET ASC-UNIT-CURSOR                                      
201500                               TO TRUE                                    
201600             END-IF                                                       
201700         END-IF                                                           
201800     END-IF.                                                              
201900                                                                          
202000     IF  DK-IP-ADDR      GREATER THAN SPACES                              
202100     AND DK-IP-ADDR      NOT EQUAL    PC-SPACE-IP-ADDR                    
202200     AND DK-IP-ADDR      NOT EQUAL    PC-ZEROS-IP-ADDR                    
202300     AND DK-IP-ADDR      NOT EQUAL    PC-ZERO-IP-ADDR                     
202400*        *********************************************************        
202500*        * IP ADDRESS CURSOR                                     *        
202600*        *********************************************************        
202700         SET ASC-IPAD-CURSOR   TO TRUE                                    
202800     END-IF.                                                              
202900                                                                          
203000                                                                          
203100*----------------------------------------------------------------*        
203200*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
203300*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
203400*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
203500*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
203600*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
203700*                                                                *        
203800*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
203900*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
204000*    UPON INITIALIZATION.                                        *        
204100*----------------------------------------------------------------*        
204200 4300-READ-ITEMS-FROM-DB.                                                 
204300                                                                          
204400     PERFORM                                                              
204500         VARYING PV-READ-COUNT                                            
204600         FROM 1 BY 1                                                      
204700         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
204800            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
204900                                                                          
205000         PERFORM 4310-FETCH                                               
205100                                                                          
205200         IF  ASC-ITEMS-LEFT-ON-DB                                         
205300             IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
205400                 ADD +1            TO ASC-ITEM-SUB                        
205500             ELSE                                                         
205600                 ADD +1            TO ASC-BLK-SUB                         
205700                 MOVE +1           TO ASC-ITEM-SUB                        
205800                                                                          
205900                 IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
206000                     MOVE +1       TO ASC-BLK-SUB                         
206100                     SET ASC-BLOCK-MODIFIED (1)                           
206200                                   TO TRUE                                
206300                     PERFORM 5100-WRITE-TEMP-STORAGE                      
206400                     MOVE ASC-BLOCK-ENTRIES                               
206500                               (PC-MAX-BLOCKS-IN-ARRAY)                   
206600                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
206700                     ADD +1        TO ASC-BLK-SUB                         
206800                     INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
206900                     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
207000                                   TO TRUE                                
207100                 END-IF                                                   
207200             END-IF                                                       
207300             ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
207400             MOVE ASC-NUMBER-OF-ITEMS-READ                                
207500                                   TO ASC-LIST-ITEM-NUMBER                
207600                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
207700             MOVE PHYUNT-LNE-ADDR-NBR                                     
207800                                   TO ASC-LINE                            
207900                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
208000             MOVE PHYUNT-PHYUNT-ADDR-NBR                                  
208100                                   TO ASC-PHYSICAL-UNIT                   
208200                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
208300             MOVE PHYUNT-LOC-NBR                                          
208400                                   TO ASC-LOC-NBR                         
208500                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
208600             MOVE PHYUNT-FCLTY-ID                                         
208700                                   TO ASC-FCLTY-N                         
208800                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
208900             MOVE PHYUNT-BEG-RNG-IP-ADDR                                  
209000                                   TO ASC-IP-ADDR-BEG                     
209100                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
209200             MOVE PHYUNT-END-RNG-IP-ADDR                                  
209300                                   TO ASC-IP-ADDR-END                     
209400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
209500         END-IF                                                           
209600     END-PERFORM.                                                         
209700                                                                          
209800     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
209900         MOVE +1               TO ASC-BLK-SUB                             
210000         PERFORM 5100-WRITE-TEMP-STORAGE                                  
210100         ADD +1                TO ASC-BLK-SUB                             
210200         PERFORM 5100-WRITE-TEMP-STORAGE                                  
210300                                                                          
210400         MOVE 1                TO ASC-BLK-SUB                             
210500                                  PV-BLOCK-NUMBER                         
210600         PERFORM 4410-READ-TEMP-STORAGE                                   
210700         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
210800             MOVE 2            TO ASC-BLK-SUB                             
210900                                  PV-BLOCK-NUMBER                         
211000             PERFORM 4410-READ-TEMP-STORAGE                               
211100         END-IF                                                           
211200     END-IF.                                                              
211300                                                                          
211400                                                                          
211500*----------------------------------------------------------------*        
211600* FETCH NEXT ROW FROM THE OPEN CURSOR FOR THE PHYSICAL UNIT      *        
211700* TABLE: TPHYUNT.                                                *        
211800*----------------------------------------------------------------*        
211900 4310-FETCH.                                                              
212000                                                                          
212100     EVALUATE TRUE                                                        
212200         WHEN ASC-NO-CURSOR                                               
212300              EXEC SQL                                                    
212400                   FETCH NO-CSR                                           
212500                   INTO  :PHYUNT-LNE-ADDR-NBR                             
212600                       , :PHYUNT-PHYUNT-ADDR-NBR                          
212700                       , :PHYUNT-LOC-NBR                                  
212800                       , :PHYUNT-FCLTY-ID                                 
212900                       , :PHYUNT-BEG-RNG-IP-ADDR                          
213000                       , :PHYUNT-END-RNG-IP-ADDR                          
213100              END-EXEC                                                    
213200                                                                          
213300         WHEN ASC-STOR-CURSOR                                             
213400              EXEC SQL                                                    
213500                   FETCH STOR-CSR                                         
213600                   INTO  :PHYUNT-LNE-ADDR-NBR                             
213700                       , :PHYUNT-PHYUNT-ADDR-NBR                          
213800                       , :PHYUNT-LOC-NBR                                  
213900                       , :PHYUNT-FCLTY-ID                                 
214000                       , :PHYUNT-BEG-RNG-IP-ADDR                          
214100                       , :PHYUNT-END-RNG-IP-ADDR                          
214200              END-EXEC                                                    
214300                                                                          
214400         WHEN ASC-VTAM-CURSOR                                             
214500              EXEC SQL                                                    
214600                   FETCH VTAM-CSR                                         
214700                   INTO  :PHYUNT-LNE-ADDR-NBR                             
214800                       , :PHYUNT-PHYUNT-ADDR-NBR                          
214900                       , :PHYUNT-LOC-NBR                                  
215000                       , :PHYUNT-FCLTY-ID                                 
215100                       , :PHYUNT-BEG-RNG-IP-ADDR                          
215200                       , :PHYUNT-END-RNG-IP-ADDR                          
215300              END-EXEC                                                    
215400                                                                          
215500         WHEN ASC-LINE-CURSOR                                             
215600              EXEC SQL                                                    
215700                   FETCH LINE-CSR                                         
215800                   INTO  :PHYUNT-LNE-ADDR-NBR                             
215900                       , :PHYUNT-PHYUNT-ADDR-NBR                          
216000                       , :PHYUNT-LOC-NBR                                  
216100                       , :PHYUNT-FCLTY-ID                                 
216200                       , :PHYUNT-BEG-RNG-IP-ADDR                          
216300                       , :PHYUNT-END-RNG-IP-ADDR                          
216400              END-EXEC                                                    
216500                                                                          
216600         WHEN ASC-UNIT-CURSOR                                             
216700              EXEC SQL                                                    
216800                   FETCH UNIT-CSR                                         
216900                   INTO  :PHYUNT-LNE-ADDR-NBR                             
217000                       , :PHYUNT-PHYUNT-ADDR-NBR                          
217100                       , :PHYUNT-LOC-NBR                                  
217200                       , :PHYUNT-FCLTY-ID                                 
217300                       , :PHYUNT-BEG-RNG-IP-ADDR                          
217400                       , :PHYUNT-END-RNG-IP-ADDR                          
217500              END-EXEC                                                    
217600                                                                          
217700         WHEN ASC-IPAD-CURSOR                                             
217800              EXEC SQL                                                    
217900                   FETCH IPAD-CSR                                         
218000                   INTO  :PHYUNT-LNE-ADDR-NBR                             
218100                       , :PHYUNT-PHYUNT-ADDR-NBR                          
218200                       , :PHYUNT-LOC-NBR                                  
218300                       , :PHYUNT-FCLTY-ID                                 
218400                       , :PHYUNT-BEG-RNG-IP-ADDR                          
218500                       , :PHYUNT-END-RNG-IP-ADDR                          
218600              END-EXEC                                                    
218700                                                                          
218800     END-EVALUATE.                                                        
218900                                                                          
219000     EVALUATE TRUE                                                        
219100         WHEN SQLCODE = ZERO                                              
219200         WHEN SQLCODE = +100                                              
219300              CONTINUE                                                    
219400         WHEN SQLWARN0 NOT = SPACE                                        
219500         WHEN SQLCODE NOT = ZERO                                          
219600              MOVE '4310-FETCH-CURSOR'                                    
219700                               TO DP013-PARAGRAPH                         
219800              MOVE 'FETCH MESSAGE CURSOR'                                 
219900                               TO DP013-MESSAGE-TEXT (1)                  
220000              MOVE SQLCA       TO DP013-SQLCA                             
220100              SET DP013-DB2-ABEND                                         
220200                               TO TRUE                                    
220300              PERFORM DP013-0000-PROCESS-ABEND                            
220400     END-EVALUATE.                                                        
220500                                                                          
220600     IF  SQLCODE EQUAL +100                                               
220700         SET ASC-NO-ITEMS-LEFT-ON-DB                                      
220800                                   TO TRUE                                
220900     END-IF.                                                              
221000                                                                          
221100                                                                          
221200*----------------------------------------------------------------*        
221300* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
221400*----------------------------------------------------------------*        
221500 4400-MOVE-COMMAREA-TO-SCREEN.                                            
221600     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
221700         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
221800         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
221900             MOVE +1    TO ASC-BLK-SUB                                    
222000             PERFORM 5100-WRITE-TEMP-STORAGE                              
222100             ADD +1     TO ASC-BLK-SUB                                    
222200             PERFORM 5100-WRITE-TEMP-STORAGE                              
222300             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
222400                 GIVING PV-BLOCK-NUMBER                                   
222500                 REMAINDER PV-REMAINDER                                   
222600             IF  PV-REMAINDER > ZERO                                      
222700                 ADD +1 TO PV-BLOCK-NUMBER                                
222800             END-IF                                                       
222900             MOVE +1    TO ASC-BLK-SUB                                    
223000             PERFORM 4410-READ-TEMP-STORAGE                               
223100             ADD +1     TO ASC-BLK-SUB                                    
223200                           PV-BLOCK-NUMBER                                
223300             PERFORM 4410-READ-TEMP-STORAGE                               
223400     END-IF.                                                              
223500     MOVE +1            TO ASC-BLK-SUB.                                   
223600     COMPUTE ASC-ITEM-SUB =                                               
223700         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
223800         + PC-ITEMS-PER-PANEL).                                           
223900                                                                          
224000     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
224100         ADD +1         TO ASC-BLK-SUB                                    
224200         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
224300     END-IF.                                                              
224400                                                                          
224500     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
224600         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
224700     PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
224800     PERFORM 4420-FORMAT-LIST-LINES                                       
224900         VARYING PV-SUB                                                   
225000         FROM PC-ITEMS-PER-PANEL BY -1                                    
225100         UNTIL PV-SUB < +1.                                               
225200                                                                          
225300     COMPUTE ASC-ITEMS-DISPLAYED =                                        
225400         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
225500     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
225600                                      ASTRTITO.                           
225700     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
225800     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
225900     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
226000                                   TO ASELITO.                            
226100                                                                          
226200     IF  DP020-MSG-NUMBER-X = SPACE                                       
226300         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
226400*            --- MORE ITEMS TO DISPLAY ----                               
226500             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
226600             SET DP020-MSG-INFORMATIONAL TO TRUE                          
226700         END-IF                                                           
226800     END-IF.                                                              
226900                                                                          
227000*----------------------------------------------------------------*        
227100*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
227200*----------------------------------------------------------------*        
227300 4410-READ-TEMP-STORAGE.                                                  
227400                                                                          
227500     MOVE LENGTH OF ASC-BLOCK-ENTRIES(ASC-BLK-SUB)                        
227600                                             TO PV-LQW-LENGTH.            
227700                                                                          
227800     EXEC CICS                                                            
227900         READQ TS                                                         
228000             QUEUE (ASC-LIST-QUEUE-NAME)                                  
228100             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
228200             ITEM  (PV-BLOCK-NUMBER)                                      
228300             LENGTH(PV-LQW-LENGTH)                                        
228400             RESP  (PV-CICS-RESP)                                         
228500     END-EXEC.                                                            
228600                                                                          
228700     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
228800       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
228900         CONTINUE                                                         
229000     ELSE                                                                 
229100         SET DP013-CICS-ABEND     TO TRUE                                 
229200         SET DP013-NO-ROLLBACK    TO TRUE                                 
229300         MOVE '4410-READ-TEMP-STORAGE'                                    
229400                                  TO DP013-PARAGRAPH                      
229500         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
229600                                  TO DP013-MESSAGE-TEXT(1)                
229700         MOVE ASC-LIST-TERM-ID    TO PM-LQW-TERM-ID                       
229800         MOVE ASC-LIST-TASK-NUMBER                                        
229900                                  TO PM-LQW-TASK                          
230000         MOVE ASC-LIST-SEQ        TO PM-LQW-SEQ                           
230100         MOVE PC-PM-LQW-DESC-BLOCK                                        
230200                                  TO PM-LQW-DESC                          
230300         MOVE PV-BLOCK-NUMBER     TO PM-LQW-ITEM-OR-BLOCK                 
230400         MOVE PV-LQW-LENGTH       TO PM-LQW-LENGTH                        
230500         MOVE PM-LQW-ABEND-MSG    TO DP013-MESSAGE-TEXT(2)                
230600         PERFORM DP013-0000-PROCESS-ABEND                                 
230700     END-IF.                                                              
230800                                                                          
230900     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
231000                                                                          
231100     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
231200         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
231300     END-IF.                                                              
231400                                                                          
231500                                                                          
231600*----------------------------------------------------------------*        
231700*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
231800*----------------------------------------------------------------*        
231900 4420-FORMAT-LIST-LINES.                                                  
232000     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
232100         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
232200         MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
232300         MOVE DP015-HL-OFF       TO MR-SELECTH (PV-SUB)                   
232400         MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
232500                                    MR-ACTION  (PV-SUB)                   
232600                                    MR-LINE    (PV-SUB)                   
232700                                    MR-UNIT    (PV-SUB)                   
232800                                    MR-STORE-X (PV-SUB)                   
232900                                    MR-FCLTY-X (PV-SUB)                   
233000                                    MR-AIPBR   (PV-SUB)                   
233100                                    MR-AIPER   (PV-SUB)                   
233200     ELSE                                                                 
233300         IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
233400                                                   > SPACE                
233500             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
233600                                 TO MR-SELECTI (PV-SUB)                   
233700         ELSE                                                             
233800             MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
233900                                 TO MR-SELECTI (PV-SUB)                   
234000         END-IF                                                           
234100                                                                          
234200         IF  DP030-SET-CURSOR-TRAILER                                     
234300             IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
234400                 > SPACE)                                                 
234500               OR (PV-SUB = 1))                                           
234600                SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
234700                MOVE -1          TO MR-SELECTL (PV-SUB)                   
234800             END-IF                                                       
234900         END-IF                                                           
235000                                                                          
235100         IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
235200             MOVE ASC-ACTION        (ASC-BLK-SUB ASC-ITEM-SUB)            
235300                                 TO MR-ACTION  (PV-SUB)                   
235400             MOVE ASC-LINE          (ASC-BLK-SUB ASC-ITEM-SUB)            
235500                                 TO MR-LINE    (PV-SUB)                   
235600             MOVE ASC-PHYSICAL-UNIT (ASC-BLK-SUB ASC-ITEM-SUB)            
235700                                 TO MR-UNIT    (PV-SUB)                   
235800             MOVE ASC-LOC-NBR  (ASC-BLK-SUB ASC-ITEM-SUB)                 
235900                                 TO MR-STORE-N (PV-SUB)                   
236000             MOVE ASC-FCLTY-N       (ASC-BLK-SUB ASC-ITEM-SUB)            
236100                                 TO MR-FCLTY-N (PV-SUB)                   
236200             MOVE ASC-IP-ADDR-BEG   (ASC-BLK-SUB ASC-ITEM-SUB)            
236300                                 TO MR-AIPBR   (PV-SUB)                   
236400             MOVE ASC-IP-ADDR-END   (ASC-BLK-SUB ASC-ITEM-SUB)            
236500                                 TO MR-AIPER   (PV-SUB)                   
236600         END-IF                                                           
236700     END-IF.                                                              
236800     SUBTRACT +1 FROM ASC-ITEM-SUB                                        
236900     IF  ASC-ITEM-SUB < + 1                                               
237000         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
237100         MOVE +1                 TO ASC-BLK-SUB                           
237200     END-IF.                                                              
237300                                                                          
237400******************************************************************        
237500** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
237600** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
237700** ISSUE A WARNING.                                             **        
237800******************************************************************        
237900 4430-CHECK-FOR-BEGIN-AND-END.                                            
238000     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
238100         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
238200*            -- NO END-OF-RANGE SPECFIED ---                              
238300             MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
238400             SET DP020-MSG-WARNING TO TRUE                                
238500             PERFORM 4431-POSITION-AT-WARNING                             
238600         END-IF                                                           
238700     ELSE                                                                 
238800         IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
238900*            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
239000             MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
239100             SET DP020-MSG-WARNING TO TRUE                                
239200             PERFORM 4431-POSITION-AT-WARNING                             
239300         END-IF                                                           
239400     END-IF.                                                              
239500                                                                          
239600******************************************************************        
239700** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
239800** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
239900** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
240000** POSITION THE CURSOR THERE.                                   **        
240100******************************************************************        
240200 4431-POSITION-AT-WARNING.                                                
240300     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
240400                    - PV-TOP-ITEM + 1.                                    
240500     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
240600         CONTINUE                                                         
240700     ELSE                                                                 
240800         SET DP030-SET-CURSOR-APPL-1                                      
240900                                   TO TRUE                                
241000         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
241100         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
241200         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
241300     END-IF.                                                              
241400                                                                          
241500     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
241600                    - PV-TOP-ITEM + 1.                                    
241700     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
241800         CONTINUE                                                         
241900     ELSE                                                                 
242000         SET DP030-SET-CURSOR-APPL-1                                      
242100                                   TO TRUE                                
242200         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
242300         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
242400         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
242500     END-IF.                                                              
242600                                                                          
242700                                                                          
242800*----------------------------------------------------------------*        
242900* PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
243000* ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
243100* FOR THE SELECTED ITEMS QUEUE.                                  *        
243200*                                                                *        
243300* FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
243400* QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
243500* THE ARRAY IS FULL.                                             *        
243600*----------------------------------------------------------------*        
243700 5000-PREPARE-SEL-QUEUE.                                                  
243800                                                                          
243900     MOVE +1                   TO  ASC-BLK-SUB.                           
244000     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
244100     ADD +1                    TO  ASC-BLK-SUB.                           
244200     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
244300                                                                          
244400     MOVE ASC-SEL-QUEUE-NAME   TO  DP251-SEL-QUEUE-NAME.                  
244500     PERFORM 6010-DELETE-SEL-QUEUE.                                       
244600                                                                          
244700     SET ASC-UPDATE-TSQ        TO  TRUE.                                  
244800                                                                          
244900     SET DP252-IDX             TO  1.                                     
245000     MOVE ZERO                 TO DP251-NUMBER-OF-TS-ITEMS.               
245100     INITIALIZE DP252-SELECTED-ITEM-ARRAY.                                
245200                                                                          
245300     PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
245400             VARYING PV-ITEM                                              
245500             FROM 1 BY 1                                                  
245600             UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
245700                                                                          
245800     IF  DP252-IDX > 1                                                    
245900         PERFORM 5205-WRITE-SEL-QUEUE                                     
246000         ADD +1                TO DP251-NUMBER-OF-TS-ITEMS                
246100     END-IF.                                                              
246200                                                                          
246300                                                                          
246400*----------------------------------------------------------------*        
246500*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
246600*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
246700*----------------------------------------------------------------*        
246800 5100-WRITE-TEMP-STORAGE.                                                 
246900     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
247000                                   TO PV-LIST-ITEM-NUMBER.                
247100     COMPUTE PV-BLOCK-NUMBER =                                            
247200         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
247300                                                                          
247400     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
247500     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
247600         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
247700             PERFORM 5105-REWRITE-ASC-LISTQ                               
247800         END-IF                                                           
247900     ELSE                                                                 
248000         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
248100         ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
248200         PERFORM 5110-WRITE-ASC-LISTQ                                     
248300         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
248400     END-IF.                                                              
248500                                                                          
248600     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
248700                                                                          
248800                                                                          
248900******************************************************************        
249000** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
249100** SPECIFIC COMMAREA.                                           **        
249200******************************************************************        
249300 5105-REWRITE-ASC-LISTQ.                                                  
249400     EXEC CICS                                                            
249500         WRITEQ TS                                                        
249600             QUEUE (ASC-LIST-QUEUE-NAME)                                  
249700             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
249800             ITEM  (PV-BLOCK-NUMBER)                                      
249900             REWRITE                                                      
250000             RESP  (PV-CICS-RESP)                                         
250100     END-EXEC.                                                            
250200                                                                          
250300     IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
250400         CONTINUE                                                         
250500     ELSE                                                                 
250600         SET DP013-CICS-ABEND            TO TRUE                          
250700         SET DP013-NO-ROLLBACK           TO TRUE                          
250800         MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
250900         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
251000                                         TO DP013-MESSAGE-TEXT(1)         
251100         PERFORM DP013-0000-PROCESS-ABEND                                 
251200     END-IF.                                                              
251300                                                                          
251400                                                                          
251500******************************************************************        
251600** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
251700** SPECIFIC COMMAREA.                                           **        
251800******************************************************************        
251900 5110-WRITE-ASC-LISTQ.                                                    
252000     EXEC CICS                                                            
252100         WRITEQ TS                                                        
252200             QUEUE (ASC-LIST-QUEUE-NAME)                                  
252300             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
252400             ITEM  (PV-BLOCK-NUMBER)                                      
252500             RESP  (PV-CICS-RESP)                                         
252600     END-EXEC.                                                            
252700     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
252800         SET DP013-CICS-ABEND      TO TRUE                                
252900         SET DP013-NO-ROLLBACK     TO TRUE                                
253000         MOVE '5110-WRITE-ASC-LISTQ'                                      
253100                                   TO DP013-PARAGRAPH                     
253200         MOVE 'ATTEMPTING TO WRITE TEMP STORAGE QUEUE'                    
253300                                   TO DP013-MESSAGE-TEXT(1)               
253400         PERFORM DP013-0000-PROCESS-ABEND                                 
253500     END-IF.                                                              
253600                                                                          
253700                                                                          
253800*----------------------------------------------------------------*        
253900* READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
254000* TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
254100*----------------------------------------------------------------*        
254200 5200-FILL-SEL-QUEUE-RECORD.                                              
254300                                                                          
254400     PERFORM 6112-READ-LIST-QUEUE.                                        
254500                                                                          
254600     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
254700         PERFORM                                                          
254800         VARYING LQW-IDX                                                  
254900         FROM 1 BY 1                                                      
255000         UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
255100           OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
255200                                                                          
255300             IF  LQW-SELECT (LQW-IDX)                                     
255400                 MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
255500                                   TO DP252-ITEM (DP252-IDX)              
255900                 MOVE LQW-LOCATION-NBR (LQW-IDX)                          
256000                                   TO DP252-LOCATION-NBR                  
256100                                        (DP252-IDX)                       
256200                 MOVE LQW-FCLTY (LQW-IDX)                                 
256300                                   TO DP252-FCLTY                         
256400                                        (DP252-IDX)                       
256500                 MOVE LQW-FCLTY-N4-COMP (LQW-IDX)                         
256600                                   TO DP252-FCLTY-N4-COMP                 
256700                                        (DP252-IDX)                       
256800                 MOVE LQW-LINE (LQW-IDX)                                  
256900                                   TO DP252-LNE-ADDR-NBR                  
257000                                        (DP252-IDX)                       
257100                 MOVE LQW-PHYSICAL-UNIT (LQW-IDX)                         
257200                                   TO DP252-PHYSICAL-UNIT                 
257300                                        (DP252-IDX)                       
257400                 MOVE LQW-IP-ADDR-BEG (LQW-IDX)                           
257500                                   TO DP252-IP-ADDR-BEG                   
257600                                        (DP252-IDX)                       
257700                 MOVE LQW-IP-ADDR-END (LQW-IDX)                           
257800                                   TO DP252-IP-ADDR-END                   
257900                                        (DP252-IDX)                       
258000                 MOVE SPACE        TO DP252-ACTION-SW (DP252-IDX)         
258100                 SET DP252-IDX UP BY 1                                    
258200                 IF  DP252-IDX > DP252-MAX-ITEMS                          
258300                     PERFORM 5205-WRITE-SEL-QUEUE                         
258400                     ADD +1        TO DP251-NUMBER-OF-TS-ITEMS            
258500                     INITIALIZE DP252-SELECTED-ITEM-ARRAY                 
258600                     SET DP252-IDX TO  1                                  
258700                 END-IF                                                   
258800             END-IF                                                       
258900         END-PERFORM                                                      
259000     END-IF.                                                              
259100                                                                          
259200                                                                          
259300*----------------------------------------------------------------*        
259400*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
259500*----------------------------------------------------------------*        
259600 5205-WRITE-SEL-QUEUE.                                                    
259700                                                                          
259800     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
259900                                   TO DP252-SELECTED-ITEMS.               
260000                                                                          
260100     EXEC CICS                                                            
260200          WRITEQ TS                                                       
260300              QUEUE (ASC-SEL-QUEUE-NAME)                                  
260400              FROM  (DP252-PHYS-UNIT-SEL-REC)                             
260500              ITEM  (PV-SEL-QUEUE-ITEM)                                   
260600              RESP  (PV-CICS-RESP)                                        
260700     END-EXEC.                                                            
260800                                                                          
260900     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
261000         CONTINUE                                                         
261100     ELSE                                                                 
261200         SET DP013-CICS-ABEND      TO TRUE                                
261300         SET DP013-NO-ROLLBACK     TO TRUE                                
261400         MOVE '5205-WRITE-SEL-QUEUE'                                      
261500                                   TO DP013-PARAGRAPH                     
261600         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
261700                                   TO DP013-MESSAGE-TEXT(1)               
261800         PERFORM DP013-0000-PROCESS-ABEND                                 
261900     END-IF.                                                              
262000                                                                          
262100                                                                          
262200******************************************************************        
262300** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **        
262400** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **        
262500** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **        
262600** THE USER.                                                    **        
262700******************************************************************        
262800 6000-REFRESH-PANEL-FROM-TEMP.                                            
262900     IF  ASC-UPDATE-TSQ                                                   
263000         SET ASC-NO-TSQ-UPDATE TO TRUE                                    
263100         MOVE ZERO                 TO PV-SAVE-ITEM                        
263200                                      PV-SEL-QUEUE-ITEM                   
263300         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
263400                                   TO PV-HOLD-SELECTED-ITEMS              
263500         MOVE +1                   TO PV-ITEMS-PROCESSED                  
263600         PERFORM 6100-UPDATE-TSQ                                          
263700             UNTIL PV-ITEMS-PROCESSED >                                   
263800                                     PV-HOLD-SELECTED-ITEMS               
263900         PERFORM 6010-DELETE-SEL-QUEUE                                    
264000         INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
264100                    ASC-BLOCK-ENTRIES (2)                                 
264200     END-IF.                                                              
264300                                                                          
264400                                                                          
264500*----------------------------------------------------------------*        
264600*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
264700*----------------------------------------------------------------*        
264800 6010-DELETE-SEL-QUEUE.                                                   
264900                                                                          
265000     EXEC CICS                                                            
265100         DELETEQ TS                                                       
265200             QUEUE (ASC-SEL-QUEUE-NAME)                                   
265300             RESP  (PV-CICS-RESP)                                         
265400     END-EXEC.                                                            
265500                                                                          
265600     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
265700        (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
265800         CONTINUE                                                         
265900     ELSE                                                                 
266000         SET DP013-CICS-ABEND TO TRUE                                     
266100         SET DP013-NO-ROLLBACK TO TRUE                                    
266200         MOVE '6010-DELETE-SEL-QUEUE'                                     
266300                        TO DP013-PARAGRAPH                                
266400         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
266500                        TO DP013-MESSAGE-TEXT (1)                         
266600         PERFORM DP013-0000-PROCESS-ABEND                                 
266700     END-IF.                                                              
266800                                                                          
266900*----------------------------------------------------------------*        
267000*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
267100*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
267200*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
267300*    THE TEMP QUEUE.                                             *        
267400*----------------------------------------------------------------*        
267500 6100-UPDATE-TSQ.                                                         
267600                                                                          
267700     ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
267800     PERFORM 6105-READ-SEL-QUEUE.                                         
267900     PERFORM                                                              
268000         VARYING DP252-IDX                                                
268100         FROM 1 BY 1                                                      
268200         UNTIL ((PV-ITEMS-PROCESSED >                                     
268300                              PV-HOLD-SELECTED-ITEMS)                     
268400         OR (DP252-IDX GREATER THAN DP252-MAX-ITEMS))                     
268500         IF  DP252-ACTION-SW (DP252-IDX) > SPACE                          
268600             PERFORM 6110-PROCESS-ITEMS                                   
268700         END-IF                                                           
268800         ADD +1                TO PV-ITEMS-PROCESSED                      
268900     END-PERFORM.                                                         
269000     PERFORM 6111-WRITE-LIST-QUEUE.                                       
269100                                                                          
269200*----------------------------------------------------------------*        
269300*    READ THE LIST QUEUE RECORD.                                 *        
269400*----------------------------------------------------------------*        
269500 6105-READ-SEL-QUEUE.                                                     
269600                                                                          
269700     EXEC CICS                                                            
269800          READQ TS                                                        
269900              QUEUE (ASC-SEL-QUEUE-NAME)                                  
270000              INTO  (DP252-PHYS-UNIT-SEL-REC)                             
270100              ITEM  (PV-SEL-QUEUE-ITEM)                                   
270200              RESP  (PV-CICS-RESP)                                        
270300     END-EXEC.                                                            
270400                                                                          
270500     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
270600         CONTINUE                                                         
270700     ELSE                                                                 
270800         SET DP013-CICS-ABEND      TO  TRUE                               
270900         SET DP013-NO-ROLLBACK     TO  TRUE                               
271000         MOVE '6105-READ-SEL-QUEUE'                                       
271100                                   TO  DP013-PARAGRAPH                    
271200         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
271300                                   TO  DP013-MESSAGE-TEXT(1)              
271400         PERFORM DP013-0000-PROCESS-ABEND                                 
271500     END-IF.                                                              
271600                                                                          
271700                                                                          
271800*----------------------------------------------------------------*        
271900*    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
272000*    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
272100*    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
272200*----------------------------------------------------------------*        
272300 6110-PROCESS-ITEMS.                                                      
272400                                                                          
272500     COMPUTE PV-ITEM = 1 + ((DP252-ITEM (DP252-IDX) - 1)                  
272600                     / PC-ITEMS-PER-PANEL).                               
272700                                                                          
272800     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
272900         IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
273000             PERFORM 6111-WRITE-LIST-QUEUE                                
273100         END-IF                                                           
273200         MOVE PV-ITEM          TO PV-SAVE-ITEM                            
273300                                                                          
273400         PERFORM 6112-READ-LIST-QUEUE                                     
273500         IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
273600             CONTINUE                                                     
273700         END-IF                                                           
273800     END-IF.                                                              
273900                                                                          
274000     COMPUTE PV-SUB = (DP252-ITEM (DP252-IDX)                             
274100                    - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
274200     SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
274300     MOVE LQW-SELECTED-SW (PV-SUB)                                        
274400                               TO LQW-TEMP-SELECTED-SW (PV-SUB).          
274500     EVALUATE TRUE                                                        
274600         WHEN DP252-INQ (DP252-IDX)                                       
274700              MOVE PC-INQ      TO LQW-ACTION (PV-SUB)                     
274800                                                                          
274900         WHEN DP252-UPD (DP252-IDX)                                       
275000              MOVE PC-UPD      TO LQW-ACTION (PV-SUB)                     
275100                                                                          
275200         WHEN DP252-DEL (DP252-IDX)                                       
275300              MOVE PC-DEL      TO LQW-ACTION (PV-SUB)                     
275400                                                                          
275500         WHEN DP252-ERR (DP252-IDX)                                       
275600              MOVE PC-ERR      TO LQW-ACTION (PV-SUB)                     
275700     END-EVALUATE.                                                        
275800                                                                          
275900     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
276000                                                                          
276100                                                                          
276200*----------------------------------------------------------------*        
276300*    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *        
276400*----------------------------------------------------------------*        
276500 6111-WRITE-LIST-QUEUE.                                                   
276600                                                                          
276700     EXEC CICS                                                            
276800         WRITEQ TS                                                        
276900             QUEUE (ASC-LIST-QUEUE-NAME)                                  
277000             FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
277100             ITEM  (PV-SAVE-ITEM)                                         
277200             REWRITE                                                      
277300             RESP  (PV-CICS-RESP)                                         
277400     END-EXEC.                                                            
277500                                                                          
277600     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
277700         CONTINUE                                                         
277800     ELSE                                                                 
277900         SET DP013-CICS-ABEND  TO  TRUE                                   
278000         SET DP013-NO-ROLLBACK TO  TRUE                                   
278100         MOVE '6111-WRITE-LIST-QUEUE'                                     
278200                               TO  DP013-PARAGRAPH                        
278300         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
278400                               TO  DP013-MESSAGE-TEXT(1)                  
278500         PERFORM DP013-0000-PROCESS-ABEND                                 
278600     END-IF.                                                              
278700                                                                          
278800                                                                          
278900*----------------------------------------------------------------*        
279000*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
279100*----------------------------------------------------------------*        
279200 6112-READ-LIST-QUEUE.                                                    
279300                                                                          
279400     MOVE LENGTH OF LQW-LIST-QUEUE-WORK-AREA TO PV-LQW-LENGTH.            
279500                                                                          
279600     EXEC CICS                                                            
279700         READQ TS                                                         
279800             QUEUE (ASC-LIST-QUEUE-NAME)                                  
279900             INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
280000             ITEM  (PV-ITEM)                                              
280100             LENGTH(PV-LQW-LENGTH)                                        
280200             RESP  (PV-CICS-RESP)                                         
280300     END-EXEC.                                                            
280400                                                                          
280500     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
280600         CONTINUE                                                         
280700     ELSE                                                                 
280800         SET DP013-CICS-ABEND        TO TRUE                              
280900         SET DP013-NO-ROLLBACK       TO TRUE                              
281000         MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
281100         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
281200                                     TO DP013-MESSAGE-TEXT(1)             
281300         MOVE ASC-LIST-TERM-ID       TO PM-LQW-TERM-ID                    
281400         MOVE ASC-LIST-TASK-NUMBER   TO PM-LQW-TASK                       
281500         MOVE ASC-LIST-SEQ           TO PM-LQW-SEQ                        
281600         MOVE PC-PM-LQW-DESC-ITEM    TO PM-LQW-DESC                       
281700         MOVE PV-ITEM                TO PM-LQW-ITEM-OR-BLOCK              
281800         MOVE PV-LQW-LENGTH          TO PM-LQW-LENGTH                     
281900         MOVE PM-LQW-ABEND-MSG       TO DP013-MESSAGE-TEXT(2)             
282000         PERFORM DP013-0000-PROCESS-ABEND                                 
282100     END-IF.                                                              
282200                                                                          
282300                                                                          
282400*----------------------------------------------------------------*        
282500*    ABEND PROCESSOR MODULE                                      *        
282600*----------------------------------------------------------------*        
282700     COPY DPPD013.                                                        
