       IDENTIFICATION DIVISION.                                         00010004
       PROGRAM-ID.      XSKUT021.                                       00020048
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030005
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040004
       DATE-WRITTEN.    10/02/2002.                                     00050035
       DATE-COMPILED.                                                   00060004
      ******************************************************************00070020
      **XS DOMAIN TABLE INTEGRITY PROCESS - FOR TABLE XST_UPC           00080048
      ******************************************************************00090007
      **THIS PROGRAM VALIDATES THE DATA IN THE XS DOMAIN IS THE SAME    00100027
      **AS THE DATA IN THE SOR - VSM.                                   00110027
      ******************************************************************00120007
      *DATE:10/02/2002   MADE BY:JULIE SMITH INITIAL PROGRAM WRITE      00130035
      *     01/04/2004   MADE BY:RICH KELLEN. REMOVE DISPLAY OF WT-UM-CD00131056
      *                  IN THE REPORT AND CHANGE REPORT TO USE 2 LINES 00132056
      *                  PER REPORT ENTRY INSTEAD OF 3 AND TO BE 200    00133056
      *                  BYTES WIDE INSTEAD OF 132.                     00134056
      *     05/14/2009   MADE BY:DAVE WELLER ALLOW 10 DIGIT INTR-UPC-ID 00134157
      *                  AND 10 DIGIT UPC-ID                            00134264
      ******************************************************************00135057
       ENVIRONMENT DIVISION.                                            00150004
       CONFIGURATION SECTION.                                           00160004
       SOURCE-COMPUTER.        IBM-3090.                                00170004
       OBJECT-COMPUTER.        IBM-3090.                                00180004
       INPUT-OUTPUT SECTION.                                            00190004
       FILE-CONTROL.                                                    00200004
                                                                        00210004
           SELECT CONTROL-CARD                                          00220027
                  ASSIGN TO INP00.                                      00230027
           SELECT VSM-EXTRACT                                           00240027
                  ASSIGN TO INP01.                                      00250027
           SELECT XS-EXTRACT                                            00260027
                  ASSIGN TO INP02.                                      00270027
           SELECT DIFFERENCES-FILE                                      00280028
                  ASSIGN TO OUT01.                                      00290028
                                                                        00300004
       DATA DIVISION.                                                   00310004
       FILE SECTION.                                                    00320004
                                                                        00330004
       FD  CONTROL-CARD                                                 00340027
           RECORDING MODE F                                             00350007
           BLOCK CONTAINS 0 RECORDS                                     00360007
           LABEL RECORDS ARE OMITTED                                    00370007
           DATA RECORD IS CONTROL-CARD-RECORD.                          00380027
       01  CONTROL-CARD-RECORD             PIC X(080).                  00390027
                                                                        00400004
       FD  VSM-EXTRACT                                                  00410027
           RECORDING MODE F                                             00420027
           BLOCK CONTAINS 0 RECORDS                                     00430027
           LABEL RECORDS ARE OMITTED                                    00440027
           DATA RECORD IS VSM-RECORD.                                   00450027
       01  VSM-RECORD                      PIC X(250).                  00460048
                                                                        00470027
       FD  XS-EXTRACT                                                   00480027
           RECORDING MODE F                                             00490027
           BLOCK CONTAINS 0 RECORDS                                     00500027
           LABEL RECORDS ARE OMITTED                                    00510027
           DATA RECORD IS XS-RECORD.                                    00520027
       01  XS-RECORD                       PIC X(017).                  00530048
                                                                        00540027
       FD  DIFFERENCES-FILE                                             00550028
           RECORDING MODE F                                             00560028
           BLOCK CONTAINS 0 RECORDS                                     00570028
           LABEL RECORDS ARE OMITTED                                    00580028
           DATA RECORD IS DIFFERENCES-RECORD.                           00590028
       01  DIFFERENCES-RECORD              PIC X(080).                  00600048
                                                                        00610028
       WORKING-STORAGE SECTION.                                         00620004
      ******************************************************************00630004
      **SWITCHES                                                        00640007
      ******************************************************************00650004
       01  PS-PROGRAM-SWITCHES.                                         00660004
           05  PS-END-OF-VSM-EXTRACT-SW        PIC X(01) VALUE 'N'.     00670027
               88  PS-END-OF-VSM-EXTRACT                 VALUE 'Y'.     00680028
               88  PS-NOT-END-OF-VSM-EXTRACT             VALUE 'N'.     00690028
           05  PS-END-OF-XS-EXTRACT-SW         PIC X(01) VALUE 'N'.     00700027
               88  PS-END-OF-XS-EXTRACT                  VALUE 'Y'.     00710027
               88  PS-NOT-END-OF-XS-EXTRACT              VALUE 'N'.     00720028
           05  PS-ERROR-FOUND-SW               PIC X(01) VALUE 'N'.     00730028
               88  PS-ERROR-FOUND                        VALUE 'Y'.     00740028
               88  PS-ERROR-NOT-FOUND                    VALUE 'N'.     00750028
      ******************************************************************00760027
      **CONTROL CARD LAYOUT                                             00770027
      ******************************************************************00780027
       01  CONTROL-CARD-LAYOUT.                                         00790027
           05  CC-START-POINT                  PIC 9(05) VALUE ZEROS.   00800031
           05  FILLER                          PIC X(01) VALUE SPACES.  00810031
           05  CC-READ-FREQUENCY               PIC 9(05) VALUE ZEROS.   00820031
           05  FILLER                          PIC X(69) VALUE SPACES.  00830031
      ******************************************************************00840007
      **TIMESTAMP VARIABLES                                             00850007
      ******************************************************************00860007
       01  SD-TIMESTAMP                        PIC X(26) VALUE SPACES.  00870028
       01  FILLER REDEFINES SD-TIMESTAMP.                               00880007
           05  SD-CURR-DTE                     PIC X(10).               00890028
           05  FILLER                          PIC X(01).               00900028
           05  SD-HOUR                         PIC X(02).               00910028
           05  FILLER                          PIC X(01).               00920028
           05  SD-MINUTE                       PIC X(02).               00930028
           05  FILLER                          PIC X(01).               00940028
           05  SD-SECOND                       PIC X(02).               00950028
           05  FILLER                          PIC X(01).               00960028
           05  SD-MICROSEC                     PIC X(06).               00970028
      ******************************************************************00980041
      **PROGRAM CONSTANTS                                               00990041
      ******************************************************************01000041
       01  PC-PROGRAM-CONSTANTS.                                        01010041
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)                01020041
                                                       VALUE 'XSKUT021'.01030048
      ******************************************************************01040007
      **PROGRAM COUNTERS                                                01050013
      ******************************************************************01060007
       01  PC-PROGRAM-COUNTER.                                          01070007
           05  PC-RECORD-COUNT                 PIC S9(09) VALUE ZEROS   01080029
                                                          COMP-3.       01090029
           05  PC-VSM-EXTRACT-RECS-READ        PIC 9(09) VALUE ZEROS.   01100028
           05  PC-XS-EXTRACT-RECS-READ         PIC 9(09) VALUE ZEROS.   01110028
           05  PC-ERROR-RECORD-WRITTEN         PIC 9(09) VALUE ZEROS.   01120028
      ******************************************************************01130046
      **PROGRAM VARIABLES                                               01140046
      ******************************************************************01150046
       01  PV-PROGRAM-VARIABLES.                                        01160046
           05  PV-VSM-INTRNL-UPC-ID            PIC 9(10).               01170047
           05  PV-XS-INTRNL-UPC-ID             PIC 9(10).               01180047
           05  PV-VSM-UPC-ID                   PIC 9(10).               01190051
           05  PV-XS-UPC-ID                    PIC 9(10).               01200051
      ******************************************************************01210050
      **PROGRAM CALCULATIONS                                            01220050
      ******************************************************************01230050
      **ERROR HANDLING                                                  01290030
      ******************************************************************01300030
       01  PV-MISC-ABEND-FIELDS.                                        01310030
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.      01320030
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.      01330030
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.      01340030
           05  PV-DB2-OPERATION             PIC X(30) VALUE SPACE.      01350030
       01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 01360030
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           01370030
      ******************************************************************01380004
      **COPYBOOKS                                                       01390007
      ******************************************************************01400004
      ***VSM EXTRACT FILE LAYOUT                                        01410045
           COPY IMRD055.                                                01420052
      ***XS EXTRACT FILE LAYOUT                                         01430027
           COPY XSRD013.                                                01440049
      ***XS ERROR FILE LAYOUT                                           01450028
           COPY XSRD014.                                                01460049
      ***VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        01470007
           COPY DPWS004.                                                01480004
      ***DB2 ABEND COPYBOOK                                             01490007
           COPY DPWS900.                                                01500007
      ***DB2 MESSAGE AREA                                               01510007
           COPY SQLCA2.                                                 01520018
      ******************************************************************01530004
      **TABLE INCLUSIONS                                                01540007
      ******************************************************************01550004
      ***DB2 COMMUNICATIONS AREA                                        01560007
           EXEC SQL                                                     01570004
                INCLUDE SQLCA                                           01580007
           END-EXEC.                                                    01590004
      ***TABLE XST_UPC                                                  01600049
           EXEC SQL                                                     01610007
                INCLUDE XST00012                                        01620049
           END-EXEC.                                                    01630007
                                                                        01640004
       PROCEDURE DIVISION.                                              01650004
      ***************************************************************** 01660004
       000-MAINLINE.                                                    01670027
                                                                        01680027
           PERFORM 1000-INITIALIZATION.                                 01690007
           PERFORM 2000-PROCESS-RECORDS                                 01700024
                   UNTIL PS-END-OF-VSM-EXTRACT                          01710029
                      OR PS-END-OF-XS-EXTRACT.                          01720029
           PERFORM 3000-PROCESS-EXTRA-RECORDS                           01730030
                   UNTIL PS-END-OF-VSM-EXTRACT                          01740030
                     AND PS-END-OF-XS-EXTRACT.                          01750030
           PERFORM 9999-WRAPUP.                                         01760007
           GOBACK.                                                      01770004
                                                                        01780004
       EJECT.                                                           01790007
      ******************************************************************01800004
      **INITIALIZATION AREA                                             01810007
      ******************************************************************01820004
       1000-INITIALIZATION.                                             01830007
                                                                        01840004
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01850007
                                                                        01860007
           INITIALIZE PC-PROGRAM-COUNTER.                               01870033
                                                                        01880033
           OPEN INPUT  CONTROL-CARD                                     01890028
                       VSM-EXTRACT                                      01900028
                       XS-EXTRACT                                       01910028
                OUTPUT DIFFERENCES-FILE.                                01920028
                                                                        01930007
           PERFORM 1100-SELECT-DB2-TIME.                                01940007
           PERFORM 1200-SELECT-RECORD-COUNT.                            01950028
                                                                        01960004
           PERFORM 9999-READ-CONTROL-CARD.                              01970031
           PERFORM 9999-READ-VSM-EXTRACT CC-START-POINT TIMES.          01980031
           PERFORM 9999-READ-XS-EXTRACT  CC-START-POINT TIMES.          01990031
                                                                        02000007
       EJECT.                                                           02010007
      ******************************************************************02020007
      **RETRIEVE TIME FROM DB2                                          02030007
      ******************************************************************02040007
       1100-SELECT-DB2-TIME.                                            02050007
                                                                        02060007
           MOVE '1100-SELECT-DB2-TIME' TO PV-PARAGRAPH-NAME.            02070007
                                                                        02080007
           EXEC SQL                                                     02090007
                SELECT CURRENT TIMESTAMP                                02100007
                INTO  :SD-TIMESTAMP                                     02110007
                FROM   SYSIBM.SYSDUMMY1                                 02120007
           END-EXEC.                                                    02130007
                                                                        02140007
           EVALUATE TRUE                                                02150034
              WHEN SQLCODE = ZERO                                       02160034
                   CONTINUE                                             02170034
              WHEN OTHER                                                02180034
               PERFORM 9999-ABEND-ROUTINE                               02190007
           END-EVALUATE.                                                02200034
                                                                        02210007
       EJECT.                                                           02220007
      ******************************************************************02230028
      **RETRIEVE TIME FROM DB2                                          02240028
      ******************************************************************02250028
       1200-SELECT-RECORD-COUNT.                                        02260028
                                                                        02270028
           MOVE '1200-SELECT-RECORD-COUNT' TO PV-PARAGRAPH-NAME.        02280028
                                                                        02290028
           EXEC SQL                                                     02300028
                SELECT COUNT(*)                                         02310028
                INTO  :PC-RECORD-COUNT                                  02320028
                FROM   XST_UPC                                          02330049
           END-EXEC.                                                    02340028
                                                                        02350028
           EVALUATE TRUE                                                02360030
              WHEN SQLCODE = ZERO                                       02370028
                   CONTINUE                                             02380028
              WHEN OTHER                                                02390028
                   PERFORM 9999-ABEND-ROUTINE                           02400028
           END-EVALUATE.                                                02410028
                                                                        02420028
       EJECT.                                                           02430028
      ******************************************************************02440007
      **PROCESSES INPUT FILE                                            02450007
      ******************************************************************02460007
       2000-PROCESS-RECORDS.                                            02470007
                                                                        02480007
           MOVE '2000-PROCESS-RECORDS' TO PV-PARAGRAPH-NAME.            02490007
                                                                        02500007
           INITIALIZE XSRD014-UPC-ERR-RECORD.                           02510053
                                                                        02520029
           MOVE IM055-UPC-ID   TO PV-VSM-UPC-ID.                        02530052
           MOVE XSRD013-UPC-ID TO PV-XS-UPC-ID.                         02540051
                                                                        02550046
           EVALUATE TRUE                                                02560028
              WHEN PV-VSM-UPC-ID = PV-XS-UPC-ID                         02570051
                   PERFORM 2100-COMPARE-FIELDS                          02590028
                   PERFORM 9999-READ-XS-EXTRACT  CC-READ-FREQUENCY TIMES02600032
                   PERFORM 9999-READ-VSM-EXTRACT CC-READ-FREQUENCY TIMES02610032
              WHEN PV-VSM-UPC-ID > PV-XS-UPC-ID                         02620051
                   PERFORM 9999-MOVE-XS-VALUES                          02640029
                   PERFORM 9999-WRITE-ERROR-RECORD                      02650029
                   PERFORM 9999-READ-XS-EXTRACT                         02660030
              WHEN PV-VSM-UPC-ID < PV-XS-UPC-ID                         02670051
                   PERFORM 9999-MOVE-VSM-VALUES                         02690030
                   PERFORM 9999-WRITE-ERROR-RECORD                      02700030
                   PERFORM 9999-READ-VSM-EXTRACT                        02710028
           END-EVALUATE.                                                02720028
                                                                        02730007
       EJECT.                                                           02740007
      ******************************************************************02750028
      **COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   02760028
      ******************************************************************02770028
       2100-COMPARE-FIELDS.                                             02780028
                                                                        02790028
           MOVE '2100-COMPARE-FIELDS' TO PV-PARAGRAPH-NAME.             02800028
                                                                        02810028
           INITIALIZE PS-ERROR-FOUND-SW.                                02820029
                                                                        02830029
           MOVE IM055-INTR-UPC-ID   TO PV-VSM-INTRNL-UPC-ID.            02840053
           MOVE XSRD013-INTR-UPC-ID TO PV-XS-INTRNL-UPC-ID.             02850051
           IF  PV-XS-INTRNL-UPC-ID  NOT EQUAL PV-VSM-INTRNL-UPC-ID      02860053
               SET PS-ERROR-FOUND     TO TRUE                           02880041
           END-IF.                                                      02890041
                                                                        02900037
           IF  XSRD013-UPC-TYP-CDE  NOT EQUAL IM055-UPC-TYP-CDE         02910053
               SET PS-ERROR-FOUND     TO TRUE                           02930037
           END-IF.                                                      02940037
                                                                        02950028
           IF  XSRD013-UPC-NBR      NOT EQUAL IM055-UPC-NBR             02960053
               SET PS-ERROR-FOUND     TO TRUE                           02980029
           END-IF.                                                      02990028
                                                                        03000045
           IF  PS-ERROR-FOUND                                           03010045
               PERFORM 9999-MOVE-XS-VALUES                              03020029
               PERFORM 9999-MOVE-VSM-VALUES                             03030029
               PERFORM 9999-WRITE-ERROR-RECORD                          03040028
           END-IF.                                                      03050028
                                                                        03060028
       EJECT.                                                           03070028
      ******************************************************************03080030
      **COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   03090030
      ******************************************************************03100030
       3000-PROCESS-EXTRA-RECORDS.                                      03110030
                                                                        03120030
           MOVE '3000-PROCESS-EXTRA-RECORDS' TO PV-PARAGRAPH-NAME.      03130030
                                                                        03140030
           INITIALIZE DIFFERENCES-RECORD.                               03150030
                                                                        03160030
           IF  PS-NOT-END-OF-VSM-EXTRACT                                03170030
               PERFORM 9999-MOVE-VSM-VALUES                             03180030
               PERFORM 9999-WRITE-ERROR-RECORD                          03190030
               PERFORM 9999-READ-VSM-EXTRACT                            03200030
           END-IF.                                                      03210030
                                                                        03220030
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03230030
               PERFORM 9999-MOVE-XS-VALUES                              03240030
               PERFORM 9999-WRITE-ERROR-RECORD                          03250030
               PERFORM 9999-READ-XS-EXTRACT                             03260030
           END-IF.                                                      03270030
                                                                        03280030
       EJECT.                                                           03290030
      ***************************************************************** 03300007
      **READ CONTROL CARD FOR FREQUENCY OF COMPARE                      03310027
      ***************************************************************** 03320007
       9999-READ-CONTROL-CARD.                                          03330027
                                                                        03340007
           READ CONTROL-CARD INTO CONTROL-CARD-LAYOUT.                  03350027
                                                                        03360007
       EJECT.                                                           03370007
      ***************************************************************** 03380027
      **READ VSM EXTRACT FILE                                           03390027
      ***************************************************************** 03400027
       9999-READ-VSM-EXTRACT.                                           03410027
                                                                        03420027
           IF  PS-NOT-END-OF-VSM-EXTRACT                                03430032
               READ VSM-EXTRACT INTO IM055-ITEM-UPC-DOMAIN-REC          03440053
                    AT END                                              03450032
                       SET PS-END-OF-VSM-EXTRACT TO TRUE                03460032
               END-READ                                                 03470032
           END-IF.                                                      03480032
                                                                        03490027
           IF  PS-NOT-END-OF-VSM-EXTRACT                                03500027
               ADD +1                       TO PC-VSM-EXTRACT-RECS-READ 03510027
           END-IF.                                                      03520027
                                                                        03530027
       EJECT.                                                           03540027
      ***************************************************************** 03550027
      **READ XS EXTRACT FILE                                            03560027
      ***************************************************************** 03570027
       9999-READ-XS-EXTRACT.                                            03580027
                                                                        03590027
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03600032
               READ XS-EXTRACT INTO XSRD013-UPC-UNLOAD-FILE             03610049
                    AT END                                              03620032
                       SET PS-END-OF-XS-EXTRACT TO TRUE                 03630032
               END-READ                                                 03640032
           END-IF.                                                      03650032
                                                                        03660027
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03670028
               ADD +1                       TO PC-XS-EXTRACT-RECS-READ  03680027
           END-IF.                                                      03690027
                                                                        03700027
       EJECT.                                                           03710027
      ***************************************************************** 03720028
      **MOVES XS EXTRACT VALUES TO ERROR RECORD                         03730029
      ***************************************************************** 03740028
       9999-MOVE-XS-VALUES.                                             03750029
                                                                        03760028
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     03770051
      *     COMP FIELDS.                                                03780051
           COMPUTE XSRD014-XS-UPC-ID = XSRD013-UPC-ID * 1.              03822067
                                                                        03830051
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     03840051
      *     COMP FIELDS.                                                03850051
           COMPUTE XSRD014-XS-INTRL-UPC-ID = XSRD013-INTR-UPC-ID * 1.   03890267
                                                                        03900058
           MOVE XSRD013-UPC-TYP-CDE        TO XSRD014-XS-UPC-TYP-CDE.   03910053
           MOVE XSRD013-UPC-NBR            TO XSRD014-XS-UPC-NBR.       03920053
                                                                        03930043
       EJECT.                                                           03940043
      ***************************************************************** 03950043
      **MOVES VSM EXTRACT VALUES TO ERROR RECORD                        03960029
      ***************************************************************** 03970029
       9999-MOVE-VSM-VALUES.                                            03980029
                                                                        03990029
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     04000051
      *     COMP FIELDS.                                                04010051
           COMPUTE XSRD014-VSM-UPC-ID = IM055-UPC-ID * 1.               04052067
                                                                        04060051
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     04070051
      *     COMP FIELDS.                                                04080051
           COMPUTE XSRD014-VSM-INTRL-UPC-ID = IM055-INTR-UPC-ID * 1.    04131067
           MOVE IM055-UPC-TYP-CDE     TO XSRD014-VSM-UPC-TYP-CDE.       04140053
           MOVE IM055-UPC-NBR         TO XSRD014-VSM-UPC-NBR.           04150053
                                                                        04160029
       EJECT.                                                           04170029
      ***************************************************************** 04180029
      **ERROR FILE                                                      04190029
      ***************************************************************** 04200029
       9999-WRITE-ERROR-RECORD.                                         04210029
                                                                        04220029
           WRITE DIFFERENCES-RECORD FROM XSRD014-UPC-ERR-RECORD.        04230053
                                                                        04240029
           ADD +1                       TO PC-ERROR-RECORD-WRITTEN.     04250029
                                                                        04260029
       EJECT.                                                           04270029
      ****************************************************************  04280027
      *END OF PROGRAM ROUTINE                                           04290007
      ****************************************************************  04300007
       9999-WRAPUP.                                                     04310007
                                                                        04320007
           CLOSE CONTROL-CARD                                           04330027
                 VSM-EXTRACT                                            04340027
                 XS-EXTRACT                                             04350028
                 DIFFERENCES-FILE.                                      04360028
                                                                        04370007
           PERFORM 9999-DISPLAY-STATISTICS.                             04380007
                                                                        04390033
           IF  PC-ERROR-RECORD-WRITTEN > 0                              04400033
               MOVE 04               TO RETURN-CODE                     04410033
           END-IF.                                                      04420033
                                                                        04430007
       EJECT.                                                           04440007
      ****************************************************************  04450007
      *DISPLAY PROGRAM STATISTICS                                       04460007
      ****************************************************************  04470007
       9999-DISPLAY-STATISTICS.                                         04480007
                                                                        04490007
           DISPLAY '*************************************************'. 04500007
           DISPLAY '* PROGRAM..............: ' PC-CURRENT-PROGRAM-NAME. 04510029
           DISPLAY '* DATE/TIMESTAMP.......: ' SD-TIMESTAMP.            04520029
           DISPLAY '* RECORDS IN TABLE.....: ' PC-RECORD-COUNT.         04530030
           DISPLAY '* VSM EXTRACT RECORDS..: ' PC-VSM-EXTRACT-RECS-READ.04540029
           DISPLAY '* XS EXTRACT RECORDS...: ' PC-XS-EXTRACT-RECS-READ. 04550029
           DISPLAY '* ERROR RECORDS WRITTEN: ' PC-ERROR-RECORD-WRITTEN. 04560029
           DISPLAY '*************************************************'. 04570032
                                                                        04580007
       EJECT.                                                           04590007
      ******************************************************************04600007
      **SQL ABEND ROUTINE                                               04610007
      ******************************************************************04620007
       9999-ABEND-ROUTINE.                                              04630007
                                                                        04640007
           DISPLAY '** ABEND     **'.                                   04650007
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        04660007
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              04670007
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               04680007
      ******************************************************************04690007
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     04700007
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      04710007
      ******************************************************************04720007
           COPY DPPD004.                                                04730055
           SET ABEND-4013-DB2-ERROR TO TRUE.                            04740007
           CALL 'ILBOABN0' USING ABEND-CODE.                            04750007
                                                                        04760007
       EJECT                                                            04770007
