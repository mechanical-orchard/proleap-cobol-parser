       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    IMKUP991.                                                 
       AUTHOR.        JOHN SELWITSCHKA.                                         
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  11/11/2014.                                               
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *  IMKUP991 - TUPCPLS UPDATE AND INSERT                                   
      *****************************************************************         
      *                                                                         
      *  THIS PROGRAM WILL UPDATE TUPCPLS                                       
      *                                                                         
      *  TABLES ACCESSED ARE:                                                   
      *      TSKXREF - SKU CROSS REFERENCE                                      
      *      TUPCPLS - UPC PHYSICAL LOCATION STATUS                             
      *      TSK     - ITEM                                                     
      *      TUPC    - UPC                                                      
      *  TABLES UPDATED ARE:                                                    
      *      TSKXREF - SKU CROSS REFERENCE                                      
      *      TUPCPLS - UPC PHYSICAL LOCATION STATUS                             
      *      TSK     - ITEM                                                     
      *      TUPC    - UPC                                                      
      *  FILES READ ARE:                                                        
      *      IMRDXXX SKU UPDATE FILE                                            
      *****************************************************************         
      * 11/11/14 - JOHN SELWITSCHKA                                             
      *    - NEW PROGRAM                                                        
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT SKU-UPDATE-FILE              ASSIGN TO INP01.                 
           SELECT CONTROL-CARD-FILE            ASSIGN TO INP02.                 
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
       FILE SECTION.                                                            
                                                                                
       FD  SKU-UPDATE-FILE                                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS PRICE-UPDATE-RECORD.                                  
       01  SKU-UPDATE-RECORD           PIC  X(80).                              
      *                                                                         
       FD  CONTROL-CARD-FILE                                            00590094
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                          00600094
           BLOCK CONTAINS 0 RECORDS.                                    00610094
       01  CONTROL-RECORD              PIC X(80).                       00620094
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME                PIC  X(08)   VALUE                
                                                           'IMKUP991'.          
           05  PC-OUT-MSGLENGTH               PIC S9(09)   VALUE +450           
                                                           COMP.                
           05  PC-MAXIMUM-RETRY-COUNT         PIC S9(04)   VALUE +2             
                                                           COMP SYNC.           
           05  PC-MAX-STORES                  PIC  9(02)   VALUE 40.            
           05  PC-CLEARANCE                   PIC  X(02)   VALUE '30'.          
           05  PC-MIXED                       PIC  X(02)   VALUE '25'.          
           05  PC-ORIGIN-CNTRY                PIC  X(03)   VALUE 'US '.         
           05  PC-DEFAULT-TIMESTAMP           PIC  X(26)   VALUE                
               '9999-09-09-23.59.00.000000'.                                    
           05  PC-TIMESTAMP-INACTIVE-TIME     PIC  X(16)   VALUE                
               '-23.59.59.999999'.                                              
           05  PC-TIMESTAMP-ACTIVE-TIME       PIC  X(16)   VALUE                
               '-00.00.00.000000'.                                              
                                                                                
      ******************************************************************        
      * PV PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01  PV-SKU-RECORD.                                                       
           05  PV-SKU                         PIC S9(08)   VALUE +0.            
           05  FILLER                         PIC  X(72)   VALUE SPACES.        
                                                                                
       01  PV-WORK-VARIABLES.                                                   
           05  PV-STAT-CHNGD-DATE             PIC  9(06)   VALUE ZEROS.         
           05  PV-STAT-CHNGD-DATE-REFORMAT REDEFINES PV-STAT-CHNGD-DATE.        
               10  PV-STAT-CHNGD-DATE-YY      PIC  9(02).                       
               10  PV-STAT-CHNGD-DATE-MM      PIC  9(02).                       
               10  PV-STAT-CHNGD-DATE-DD      PIC  9(02).                       
                                                                                
           05  PV-UPC-COMP-10                 PIC S9(10)   VALUE +0             
                                                           COMP.                
           05  PV-COMPLETION-CODE             PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-REASON                      PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-REASON-TEXT                 PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-UNT-COST-NULL-IND           PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-ORIG-CLR-NULL-IND           PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-MKDN-CDE-NULL-IND           PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-ORIG-EFF-END-NULL-IND       PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-EFF-END-NULL-IND            PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-EFF-END-NULL-IND-UNP        PIC S9(04)   VALUE +0.            
           05  PV-EFF-END-NULL-IND-DIS        PIC ----9.                        
           05  PV-RETRY-COUNT                 PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-PREV-STR-NBR                PIC S9(04)   VALUE +0             
                                                           COMP.                
                                                                                
           05  PV-PARAGRAPH-NAME              PIC  X(30)   VALUE SPACES.        
           05  PV-OPERATION-MSG-1             PIC  X(40)   VALUE SPACES.        
           05  PV-DB2-OPERATION-ATTEMPTED     PIC  X(30)   VALUE SPACES.        
           05  PV-NEW-SKU-STATUS              PIC  X(02)   VALUE SPACES.        
               88  PV-NEW-SKU-STATUS-ACTIVE                VALUE '20'.          
               88  PV-NEW-SKU-STATUS-CLEARANCE             VALUE '30'.          
               88  PV-NEW-SKU-STATUS-MIXED                 VALUE '25'.          
           05  PV-HOLD-UIR-EFF-DATE.                                            
               10  PV-HOLD-UIR-EFF-CC         PIC  X(02)   VALUE SPACES.        
               10  PV-HOLD-UIR-EFF-YY         PIC  X(02)   VALUE SPACES.        
               10  PV-HOLD-UIR-EFF-DASH1      PIC  X(01)   VALUE '-'.           
               10  PV-HOLD-UIR-EFF-MM         PIC  X(02)   VALUE SPACES.        
               10  PV-HOLD-UIR-EFF-DASH2      PIC  X(01)   VALUE '-'.           
               10  PV-HOLD-UIR-EFF-DD         PIC  X(02)   VALUE SPACES.        
           05  PV-COMMIT-TIMESTAMP            PIC  X(26)   VALUE SPACES.        
           05  PV-CURRENT-TIMESTAMP           PIC  X(26)   VALUE SPACES.        
           05  PV-RUN-DATE                    PIC  X(10)   VALUE SPACES.        
           05  PV-SAVE-SKU                    PIC S9(08)   VALUE +0             
                                                           COMP-3.              
           05  PV-PREV-SKU                    PIC S9(08)   VALUE +0             
                                                           COMP-3.              
           05  PV-PREV-PRCHG-TYP-CDE          PIC  X(03)   VALUE SPACES.        
           05  PV-ORIG-UNT-RTL-AMT            PIC S9(7)V99 VALUE +0             
                                                           COMP-3.              
           05  PV-ORIG-UNT-COST-AMT           PIC S9(7)V999 VALUE +0            
                                                           COMP-3.              
           05  PV-CURR-UNT-RTL-AMT            PIC S9(7)V99 VALUE +0             
                                                           COMP-3.              
           05  PV-CURR-UNT-COST-AMT           PIC S9(7)V999 VALUE +0            
                                                           COMP-3.              
           05  PV-OLD-MEITGP-NBR              PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-RTL-EQ-TO-CORP              PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-SAVE-REGN-PRIC-IND          PIC  X(01)   VALUE SPACES.        
           05  PV-STORE0-UPC-STAT-CDE         PIC  X(02)   VALUE SPACES.        
           05  PV-STORE0-ORIG-CLR-DTE         PIC  X(10)   VALUE SPACES.        
           05  PV-RECS-READ-CNTR              PIC  9(09)   VALUE ZEROES.00830000
           05  PV-SQLCODE-DISPLAY             PIC +(8)9    VALUE ZEROES.00830000
           05  PV-UPCPLS-UPC-ID-NUM           PIC  9(10)   VALUE ZERO.          
           05  PV-DETAIL-KEY.                                                   
               10  PV-DK-SKU                  PIC  X(08)   VALUE SPACES.        
               10  PV-DK-STORE-NBR            PIC  9(04)   VALUE ZERO.          
           05  PV-DELAY-INTERVAL.                                               
               10  FILLER                   PIC X(02)   VALUE SPACES.           
               10  PV-DELAY-TIME            PIC 9(08)   VALUE 6000.             
           05  WS-SYSWAIT                   PIC X(08)   VALUE 'SYSWAIT'.        
           05  PV-UPCPLS-EFF-BEG-TMST       PIC X(26)   VALUE SPACES.           
           05  PV-UPCPLS-EFF-END-TMST       PIC X(26)   VALUE SPACES.           
           05  PV-DISPLAY-10                PIC 9(10)   VALUE ZERO.             
           05  PV-DISPLAY-4                 PIC 9(04)   VALUE ZERO.             
           05  PV-DISPLAY-8                 PIC 9(08)   VALUE ZERO.             
           05  PV-DISPLAY-15                PIC 9(15)   VALUE ZERO.             
                                                                                
      ******************************************************************        
      * CR CHECKPOINT RESTART VARIABLES                                *        
      ******************************************************************        
       01  CR-CHECKPOINT-RESTART-AREA.                                          
           05  CR-AREA-LEN                    PIC S9(04) VALUE +138             
                                                         COMP.                  
           05  CR-CHECKPOINT-RESTART-VAR.                                       
               10  CR-DETAIL-KEY              PIC  X(12) VALUE SPACES.          
               10  CR-RECS-PROCESSED-CNTR     PIC  9(09) VALUE ZERO.            
               10  CR-RECS-READ-CNTR          PIC  9(09) VALUE ZERO.            
               10  CR-SKUS-READ-CNTR          PIC  9(09) VALUE ZERO.            
               10  CR-SKUS-NOT-FOUND-CNTR     PIC  9(09) VALUE ZERO.            
               10  CR-TUPCPLS-ROW-UPD-CNTR    PIC  9(09) VALUE ZERO.            
               10  CR-TUPCPLS-ROW-INS-CNTR    PIC  9(09) VALUE ZERO.            
               10  CR-TUPCPLS-ROW-STR-CNTR    PIC  9(09) VALUE ZERO.            
               10  CR-TSKXREF-ROW-UPD-CNTR    PIC  9(09) VALUE ZERO.            
               10  CR-TSK-ROW-UPD-CNTR        PIC  9(09) VALUE ZERO.            
               10  CR-TUPC-ROW-UPD-CNTR       PIC  9(09) VALUE ZERO.            
               10  CR-TSKXREF-ROW-NOT-FOUND   PIC  9(09) VALUE ZERO.            
               10  CR-TSK-ROW-NOT-FOUND       PIC  9(09) VALUE ZERO.            
               10  CR-TUPC-ROW-NOT-FOUND      PIC  9(09) VALUE ZERO.            
               10  CR-MSGS-TO-XSITEM-QUEUE    PIC  9(09) VALUE ZERO.            
               10  CR-TYPE-75-MSGS-CREATED    PIC  9(09) VALUE ZERO.            
               10  CR-TYPE-80-MSGS-CREATED    PIC  9(09) VALUE ZERO.            
               10  CR-IMT-EVNT-MSG-ROW-INS-CNTR                                 
                                              PIC  9(09) VALUE ZERO.            
               10  CR-COMMIT-CNTR             PIC  9(09) VALUE ZERO.            
               10  CR-SKU-STRS-BYPASSED-CNTR  PIC  9(09) VALUE ZERO.            
                                                                                
      ******************************************************************        
      * PS PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW              PIC  X(01)   VALUE 'N'.           
               88  PS-END-OF-FILE                          VALUE 'Y'.           
           05  PS-LOC-CSR-SW                  PIC X(01)    VALUE 'N'.           
               88  PS-END-OF-LOC-CSR                       VALUE 'Y'.           
               88  PS-MORE-LOC-ROWS                        VALUE 'N'.           
           05  PS-FIRST-COMMIT-SW             PIC  X(01)   VALUE 'N'.           
               88  PS-FIRST-COMMIT                         VALUE 'Y'.           
           05  PS-FIRST-SKU-STORE-SW          PIC  X(01)   VALUE 'N'.           
               88  PS-FIRST-SKU-STORE                      VALUE 'Y'.           
               88  PS-NOT-FIRST-SKU-STORE                  VALUE 'N'.           
           05  PS-NO-DOCUMENTS-SW             PIC  X(01)   VALUE 'N'.           
               88  PS-NO-DOCUMENTS                         VALUE 'Y'.           
           05  PS-CURRENT-ROW-FOUND-SW        PIC  X(01)   VALUE 'N'.           
               88  PS-CURRENT-ROW-FOUND                    VALUE 'Y'.           
               88  PS-CURRENT-ROW-NOT-FOUND                VALUE 'N'.           
           05  PS-CURRENT-ROW-FOUND-SW        PIC  X(01)   VALUE 'N'.           
               88  PS-CURRENT-LOC-ROW-FOUND                VALUE 'Y'.           
               88  PS-CURRENT-LOC-ROW-NOT-FOUND            VALUE 'N'.           
           05  PS-HAVE-UPCPLS-COST-INFO-SW    PIC  X(01)   VALUE 'N'.           
               88  PS-HAVE-UPCPLS-COST-INFO                VALUE 'Y'.           
               88  PS-NOT-HAVE-UPCPLS-COST-INFO            VALUE 'N'.           
           05  PS-HAVE-STORE0-TUPCPLS-SW      PIC  X(01)   VALUE 'N'.           
               88  PS-HAVE-STORE0-TUPCPLS                  VALUE 'Y'.           
               88  PS-NOT-HAVE-STORE0-TUPCPLS              VALUE 'N'.           
           05  PS-IMT-EVNT-MSG-STARTED-SW     PIC  X(01)   VALUE 'N'.           
               88  PS-IMT-EVNT-MSG-STARTED                 VALUE 'Y'.           
               88  PS-IMT-EVNT-MSG-NOT-STARTED             VALUE 'N'.           
           05  PS-SKU-INFO-FOUND-SW           PIC  X(01)   VALUE 'N'.           
               88  PS-SKU-INFO-FOUND                       VALUE 'Y'.           
               88  PS-SKU-INFO-NOT-FOUND                   VALUE 'N'.           
           05  PS-NEW-CLR-RCP-SW              PIC  X(01)   VALUE 'N'.           
               88  PS-NEW-CLR-RCP                          VALUE 'Y'.           
               88  PS-NOT-NEW-CLR-RCP                      VALUE 'N'.           
           05  PS-CHECKPOINT-RESTART-SW       PIC  X(01)   VALUE 'N'.           
               88  PS-CHECKPOINT-RESTART                   VALUE 'Y'.           
               88  PS-NOT-CHECKPOINT-RESTART               VALUE 'N'.           
           05  PS-STRS-ALL-SAME-PRICE-SW      PIC  X(01)   VALUE 'N'.           
               88  PS-STRS-ALL-SAME-PRICE                  VALUE 'Y'.           
               88  PS-NOT-STRS-ALL-SAME-PRICE              VALUE 'N'.           
                                                                                
       01  ABEND-CODE                         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           88  PV-ABEND-4001                   VALUE +4001.                     
      *        NO CONTROL CARD 1                                                
           88  PV-ABEND-4002                   VALUE +4002.                     
      *        INCORRECT CONTROL CARD DATA                                      
           88  PV-ABEND-4003                   VALUE +4003.                     
      *        EFFECTIVE DATE LESS THAN CURRENT DATE                            
           88  PV-ABEND-4004                   VALUE +4004.                     
      *        INVALID CONTROL CARD JOB NAME                                    
           88  PV-ABEND-4005                   VALUE +4005.                     
      *        INVALID CONTROL CARD XS ITEM QUEUE NAME                          
           88  PV-ABEND-4006                   VALUE +4006.                     
      *        NO QUEUE MANAGER CONTROL CARD                                    
           88  PV-ABEND-4007                   VALUE +4007.                     
      *        INVALID CONTROL CARD QUEUE MANAGER NAME                          
           88  PV-ABEND-4008                   VALUE +4008.                     
      *        NO TUPCPLS ROW FOR FINAL OR MARK2                                
           88  PV-ABEND-4013                   VALUE +4013.                     
      *        DB2 SQL ABEND                                                    
           88  PV-ABEND-4020                   VALUE +4020.                     
      *        MQSERIES ABEND                                                   
                                                                                
       01  DISPLAY-EDITS.                                                       
           05  DI-RECS-PROCESSED-CNTR       PIC  Z(08)9  VALUE ZEROS.           
           05  DI-RECS-READ-CNTR            PIC  Z(08)9  VALUE ZEROS.           
           05  DI-SKUS-READ-CNTR            PIC  Z(08)9  VALUE ZEROS.           
           05  DI-SKUS-NOT-FOUND-CNTR       PIC  Z(08)9  VALUE ZEROS.           
           05  DI-SKU-STRS-BYPASSED-CNTR    PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPCPLS-ROW-UPDATE-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPCPLS-ROW-INSERT-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPCPLS-ROW-INACTV-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPCPLS-ROW-DELETE-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPCPLS-ROW-UPDSTR-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TSKXREF-ROW-UPDATE-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TSK-ROW-UPDATE-CNTR       PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPC-ROW-UPDATE-CNTR      PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TSKXREF-ROW-NOT-FOUND     PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TSK-ROW-NOT-FOUND         PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPC-ROW-NOT-FOUND        PIC  Z(08)9  VALUE ZEROS.           
           05  DI-MSGS-TO-XSITEM-QUEUE      PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TYPE-75-MSGS-CREATED      PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TYPE-80-MSGS-CREATED      PIC  Z(08)9  VALUE ZEROS.           
           05  DI-IMT-EVNT-ROW-INSERT-CNTR  PIC  Z(08)9  VALUE ZEROS.           
           05  DI-COMMIT-CNTR               PIC  Z(08)9  VALUE ZEROS.           
           05  DI-INPUT-RECS-DISPLAY        PIC  9(06)   VALUE ZEROS.           
                                                                                
       01  PT-PROGRAM-TABLES.                                                   
           05  LOC-TBL-MAX               PIC 9(5)    VALUE 10000.               
           05  LOCATION-TABLE.                                                  
               10  LOC-TBL-SIZE          PIC 9(5)    VALUE ZEROES.              
               10  LOC-ENTRY  OCCURS 10000 TIMES INDEXED BY LOC-INDEX.          
                   15  LOC-NBR           PIC 9(05).                             
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR CONTROL CARD FILE.                                    
      * CC PROCESS OLD DATE WILL DEFAULT TO N.  IT WILL BE Y ONLY IF    00840094
      * WE NEED TO PROCESS A PRICE CHANGE THAT IS IN THE PAST.          00840094
      ****************************************************************          
       01  CC-CONTROL-CARD.                                                     
           05  CC-END-DATE             PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  CC-BEG-DATE             PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  CC-UPDATE-IND           PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(57) VALUE SPACES.                  
                                                                                
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      *  TSKXREF - SKU CROSS REFERENCE TABLE                                    
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TSKXREF                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TUPCPLS - UPC PHYSICAL LOCATION STATUS TABLE                           
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TUPCPLS                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TSK     - TSK TABLE                                                    
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TSK                                                     
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TUPC    - TUPC TABLE                                                   
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TUPC                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    COBOL DECL DB2 COMMUNICATION AREA                                    
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      * 0000-MAIN-MODULE.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
           MOVE '0000-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-SKU-UPDATES                                     
             UNTIL PS-END-OF-FILE.                                              
                                                                                
           PERFORM 3000-EOJ-PROCESSING.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZATION.                                           *        
      *  ==> PROCESSES:                                                *        
      *      - CHECKS FOR RESTART PROCESSING.                          *        
      *      - INITIALIZES VARIABLES.                                  *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
           MOVE '1000-INITIALIZATION ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           OPEN INPUT SKU-UPDATE-FILE                                           
                      CONTROL-CARD-FILE.                                        
                                                                                
           PERFORM 4100-READ-CONTROL-CARD-1.                                    
                                                                                
           MOVE CC-END-DATE TO PV-UPCPLS-EFF-END-TMST (1:10).                   
           MOVE PC-TIMESTAMP-INACTIVE-TIME TO                                   
                     PV-UPCPLS-EFF-END-TMST (11:16).                            
           MOVE CC-BEG-DATE TO PV-UPCPLS-EFF-BEG-TMST (1:10).                   
           MOVE PC-TIMESTAMP-ACTIVE-TIME TO                                     
                     PV-UPCPLS-EFF-BEG-TMST (11:16).                            
                                                                                
           PERFORM 4000-READ-RECORD.                                            
                                                                                
      ******************************************************************        
      * 2000-PROCESS-SKU-UPDATES.                                      *        
      *  ==> PROCESSES:                                                *        
      *    - PROCESSES A SKU UPDATE RECORD                             *        
      *    - UPDATE TUPCPLS ROWS                                       *        
      ******************************************************************        
       2000-PROCESS-SKU-UPDATES.                                                
           MOVE '2000-PROCESS-SKU-UPDATES' TO PV-PARAGRAPH-NAME.                
                                                                                
           DISPLAY 'PV-SKU ' PV-SKU                                             
           MOVE PV-SKU                TO SKXREF-SKU.                            
           PERFORM 2010-SELECT-TSKXREF.                                         
           IF SQLCODE = ZERO                                                    
               MOVE SKXREF-ITM-NBR     TO UPCPLS-ITM-NBR                        
                                          PV-DISPLAY-15                         
               MOVE ZERO               TO UPCPLS-STR-NBR                        
                                          PV-DISPLAY-4                          
               PERFORM 2015-SELECT-TUPCPLS                                      
           END-IF.                                                              
           IF SQLCODE = +100                                                    
               DISPLAY 'TUPCPLS LOC ZERO NOT READ, TSKXREF NOT FOUND'           
               DISPLAY 'ITM NBR          '  PV-DISPLAY-15                       
               DISPLAY 'LOC NBR          '  PV-DISPLAY-4                        
               ADD +1 TO CR-SKU-STRS-BYPASSED-CNTR                              
           ELSE                                                                 
               MOVE PV-UPCPLS-EFF-END-TMST TO UPCPLS-EFF-END-TMST               
               IF CC-UPDATE-IND = 'U'                                           
                   PERFORM 2020-UPDATE-TUPCPLS                                  
                   IF SQLCODE = +100                                            
                       DISPLAY 'TUPCPLS LOC ZERO NOT FOUND'                     
                       DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
                       DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                   ELSE                                                         
                       DISPLAY 'TUPCPLS LOC ZERO FOUND'                         
                       DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
                       DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                       PERFORM 2030-SELECT-TUPCPLS                              
                       PERFORM 2035-SELECT-TUPCPLS                              
                       PERFORM 2040-SELECT-TUPCPLS                              
                       PERFORM 2045-INSERT-TUPCPLS                              
                       IF SQLCODE = ZERO                                        
                           PERFORM 2055-UPDATE-TSKXREF                          
                           PERFORM 2060-SELECT-TUPCPLS                          
                           IF PV-RTL-EQ-TO-CORP > 1161                          
                               MOVE 'N' TO TSK-REGN-PRIC-IND                    
                                           UPC-REGN-PRIC-IND                    
                           ELSE                                                 
                               MOVE 'Y' TO TSK-REGN-PRIC-IND                    
                                           UPC-REGN-PRIC-IND                    
                           END-IF                                               
                           PERFORM 2085-UPDATE-TSK                              
                           PERFORM 2090-UPDATE-TUPC                             
                           PERFORM 2095-UPDATE-TUPCPLS                          
                       END-IF                                                   
                   END-IF                                                       
                   PERFORM 7800-COMMIT                                          
               ELSE                                                             
                   PERFORM 2025-SELECT-TUPCPLS                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE SKXREF-SKU         TO PV-DISPLAY-8.                             
           IF CC-UPDATE-IND = 'U'                                               
              DISPLAY 'SKU PROCESSED/COMMITTED ' PV-DISPLAY-8                   
           ELSE                                                                 
              DISPLAY 'SKU PROCESSED           ' PV-DISPLAY-8                   
           END-IF.                                                              
                                                                                
           ADD +1 TO CR-RECS-PROCESSED-CNTR.                                    
                                                                                
           PERFORM 4000-READ-RECORD.                                            
                                                                                
      ******************************************************************        
      * 2010-SELECT-TSKXREF.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - READS TSKXREF                                             *        
      ******************************************************************        
       2010-SELECT-TSKXREF.                                                     
           MOVE '2010-SELECT-TSKXREF' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                      
               EXEC SQL                                                         
                   SELECT  ITM_NBR                                              
                          ,INTR_UPC_ID                                          
                     INTO :SKXREF-ITM-NBR                                       
                         ,:SKXREF-INTR-UPC-ID                                   
                     FROM  TSKXREF                                              
                    WHERE  SKU        = :SKXREF-SKU                             
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD +1      TO CR-SKUS-READ-CNTR                         
                   WHEN SQLCODE = +100                                          
                       ADD +1      TO CR-SKUS-NOT-FOUND-CNTR                    
                       MOVE 'READ TSKXREF TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'READ TSKXREF TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'READ TSKXREF TABLE'                                        
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2015-SELECT-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - SELECT THE CURRENT LOC ZERO ROW FROM THE TUPCPLS TABLE.   *        
      ******************************************************************        
       2015-SELECT-TUPCPLS.                                                     
           MOVE '2015-SELECT-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
               EXEC SQL                                                         
                   SELECT UPC_ID                                                
                        , OPERCO_NBR                                            
                        , STR_NBR                                               
                        , EFF_BEG_TMST                                          
                        , EFF_END_TMST                                          
                        , UPC_STAT_CDE                                          
                        , UNT_COST_AMT                                          
                        , UNT_RTL_AMT                                           
                        , MEITGP_NBR                                            
                        , CRE_BY_USR_ID                                         
                        , CRE_TMST                                              
                        , ITM_NBR                                               
                        , PLU_IND                                               
                        , UPC_STAT_CHG_DTE                                      
                        , UNT_RTL_CHG_DTE                                       
                        , ORIG_CLR_DTE                                          
                        , MKDN_CDE                                              
                    INTO :UPCPLS-UPC-ID                                         
                        ,:UPCPLS-OPERCO-NBR                                     
                        ,:UPCPLS-STR-NBR                                        
                        ,:UPCPLS-EFF-BEG-TMST                                   
                        ,:UPCPLS-EFF-END-TMST :PV-EFF-END-NULL-IND              
                        ,:UPCPLS-UPC-STAT-CDE                                   
                        ,:UPCPLS-UNT-COST-AMT :PV-UNT-COST-NULL-IND             
                        ,:UPCPLS-UNT-RTL-AMT                                    
                        ,:UPCPLS-MEITGP-NBR                                     
                        ,:UPCPLS-CRE-BY-USR-ID                                  
                        ,:UPCPLS-CRE-TMST                                       
                        ,:UPCPLS-ITM-NBR                                        
                        ,:UPCPLS-PLU-IND                                        
                        ,:UPCPLS-UPC-STAT-CHG-DTE                               
                        ,:UPCPLS-UNT-RTL-CHG-DTE                                
                        ,:UPCPLS-ORIG-CLR-DTE :PV-ORIG-CLR-NULL-IND             
                        ,:UPCPLS-MKDN-CDE     :PV-MKDN-CDE-NULL-IND             
                     FROM TUPCPLS                                               
                    WHERE ITM_NBR      = :UPCPLS-ITM-NBR                        
                      AND STR_NBR      = :UPCPLS-STR-NBR                        
      *               AND EFF_BEG_TMST < :PV-UPCPLS-EFF-BEG-TMST                
      *               AND (EFF_END_TMST IS NULL OR                              
      *                    EFF_END_TMST >= :PV-UPCPLS-EFF-BEG-TMST)             
      *               AND EFF_END_TMST > :PV-UPCPLS-EFF-BEG-TMST                
                      AND EFF_END_TMST IS NULL                                  
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       SET PS-CURRENT-ROW-FOUND       TO TRUE                   
      *                                                                         
      *  A 'NOT FOUND' IS VALID FOR A STORE SPECIFIC ROW, BUT SHOULD            
      *  NEVER HAPPEN FOR A '000' STORE ROW.                                    
      *                                                                         
                   WHEN SQLCODE = +100                                          
                       IF UPCPLS-STR-NBR  = ZEROES                              
                           MOVE 'READ TUPCPLS TABLE '                           
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                           PERFORM 9999-SQL-ABEND                               
                       ELSE                                                     
                           SET PS-CURRENT-ROW-NOT-FOUND TO TRUE                 
                       END-IF                                                   
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'READ TUPCPLS TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'READ TUPCPLS TABLE'                                        
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2020-UPDATE-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - UPDATE THE CURRENT LOC ZERO ROW FROM THE TUPCPLS TABLE.   *        
      ******************************************************************        
       2020-UPDATE-TUPCPLS.                                                     
           MOVE '2020-UPDATE-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
                                                                                
                 EXEC SQL                                                       
                    UPDATE TUPCPLS                                              
                        SET EFF_END_TMST   = :PV-UPCPLS-EFF-END-TMST            
                           ,CRE_BY_USR_ID  = 'IMKUP991'                         
                        WHERE ITM_NBR      = :UPCPLS-ITM-NBR                    
                          AND STR_NBR      = 0                                  
                          AND EFF_END_TMST IS NULL                              
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = ZERO                                        
                         ADD SQLERRD(3) TO CR-TUPCPLS-ROW-UPD-CNTR              
                     WHEN SQLCODE = -913                                        
                         ADD +1      TO PV-RETRY-COUNT                          
                     WHEN OTHER                                                 
                         MOVE 'UPDATE TUPCPLS TABLE - STORE ZERO'               
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'UPDATE TUPCPLS TABLE - STORE ZERO'                         
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2025-SELECT-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - SELECT THE CURRENT LOC ROW FROM THE TUPCPLS TABLE.        *        
      ******************************************************************        
       2025-SELECT-TUPCPLS.                                                     
           MOVE '2025-SELECT-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
                                                                                
                 EXEC SQL                                                       
                    SELECT  UPC_STAT_CDE                                        
                      INTO :UPCPLS-UPC-STAT-CDE                                 
                      FROM TUPCPLS                                              
                    WHERE ITM_NBR      = :UPCPLS-ITM-NBR                        
                      AND STR_NBR      = :UPCPLS-STR-NBR                        
                      AND EFF_END_TMST IS NULL                                  
                    WITH UR                                                     
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = ZERO                                        
                       DISPLAY 'TUPCPLS LOC ZERO FOUND'                         
                       DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
                       DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                    DISPLAY 'LOC ZERO UPC STAT CDE ' UPCPLS-UPC-STAT-CDE        
                     WHEN SQLCODE = +100                                        
                       DISPLAY 'TUPCPLS LOC ZERO NOT FOUND'                     
                       DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
                       DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                     WHEN SQLCODE = -913                                        
                         ADD +1      TO PV-RETRY-COUNT                          
                     WHEN OTHER                                                 
                         MOVE 'SELECT TUPCPLS TABLE'                            
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'SELECT TUPCPLS TABLE'                                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2030-SELECT-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - SELECT MAXIMUM STORE SPECIFIC RETAIL FROM TUPCPLS TABLE.  *        
      ******************************************************************        
       2030-SELECT-TUPCPLS.                                                     
           MOVE '2030-SELECT-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
                                                                                
                 EXEC SQL                                                       
                    SELECT  MAX(UNT_RTL_AMT)                                    
                      INTO :UPCPLS-UNT-RTL-AMT                                  
                      FROM TUPCPLS                                              
                    WHERE ITM_NBR      = :UPCPLS-ITM-NBR                        
                      AND UPC_STAT_CDE = '30'                                   
                      AND STR_NBR      <> 0                                     
                      AND EFF_END_TMST IS NULL                                  
                    WITH UR                                                     
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = ZERO                                        
      *                DISPLAY 'TUPCPLS MAX RETAIL FOUND'                       
      *                DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
      *                DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                       CONTINUE                                                 
                     WHEN SQLCODE = +100                                        
                       MOVE ZERO TO UPCPLS-UNT-RTL-AMT                          
                       DISPLAY 'TUPCPLS MAX RETAIL NOT FOUND'                   
                       DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
                       DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                     WHEN SQLCODE = -913                                        
                         ADD +1      TO PV-RETRY-COUNT                          
                     WHEN OTHER                                                 
                         MOVE 'SELECT TUPCPLS RETAIL'                           
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'SELECT TUPCPLS RETAIL'                                     
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2035-SELECT-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - SELECT MAXIMUM STORE SPECIFIC ORIG CLR DTE FROM TUPCPLS.  *        
      ******************************************************************        
       2035-SELECT-TUPCPLS.                                                     
           MOVE '2035-SELECT-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
                                                                                
                 EXEC SQL                                                       
                    SELECT  MAX(ORIG_CLR_DTE)                                   
                      INTO :UPCPLS-ORIG-CLR-DTE                                 
                      FROM TUPCPLS                                              
                    WHERE ITM_NBR      = :UPCPLS-ITM-NBR                        
                      AND UPC_STAT_CDE = '30'                                   
                      AND STR_NBR      <> 0                                     
                      AND EFF_END_TMST IS NULL                                  
                    WITH UR                                                     
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = ZERO                                        
                       MOVE +0 TO PV-ORIG-CLR-NULL-IND                          
      *                DISPLAY 'TUPCPLS MAX ORIG CLR DTE FOUND'                 
      *                DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
      *                DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                     WHEN SQLCODE = +100                                        
                       MOVE -1 TO PV-ORIG-CLR-NULL-IND                          
                       DISPLAY 'TUPCPLS MAX ORIG CLR DTE NOT FOUND'             
                       DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
                       DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                     WHEN SQLCODE = -913                                        
                         ADD +1      TO PV-RETRY-COUNT                          
                     WHEN OTHER                                                 
                         MOVE 'SELECT TUPCPLS ORIG CLR DTE'                     
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'SELECT TUPCPLS ORIG CLR DTE'                               
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2040-SELECT-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - SELECT MAXIMUM STORE SPECIFIC MAX MKDN CDE FROM TUPCPLS.  *        
      ******************************************************************        
       2040-SELECT-TUPCPLS.                                                     
           MOVE '2040-SELECT-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
                                                                                
                 EXEC SQL                                                       
                    SELECT  MAX(MKDN_CDE)                                       
                      INTO :UPCPLS-MKDN-CDE                                     
                      FROM TUPCPLS                                              
                    WHERE ITM_NBR      = :UPCPLS-ITM-NBR                        
                      AND UPC_STAT_CDE = '30'                                   
                      AND STR_NBR      <> 0                                     
                      AND EFF_END_TMST IS NULL                                  
                    WITH UR                                                     
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = ZERO                                        
                       MOVE +0 TO PV-MKDN-CDE-NULL-IND                          
      *                DISPLAY 'TUPCPLS MAX MKDN CDE FOUND'                     
      *                DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
      *                DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                     WHEN SQLCODE = +100                                        
                       MOVE -1 TO PV-MKDN-CDE-NULL-IND                          
                       DISPLAY 'TUPCPLS MAX MKDN CDE NOT FOUND'                 
                       DISPLAY 'ITM NBR          '  PV-DISPLAY-15               
                       DISPLAY 'LOC NBR          '  PV-DISPLAY-4                
                     WHEN SQLCODE = -913                                        
                         ADD +1      TO PV-RETRY-COUNT                          
                     WHEN OTHER                                                 
                         MOVE 'SELECT TUPCPLS MKDN CDE'                         
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'SELECT TUPCPLS MKDN CDE'                                   
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2045-INSERT-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - INSERT A NEW STORE ROW FOR A UPC ID ON THE TUPCPLS TABLE. *        
      ******************************************************************        
       2045-INSERT-TUPCPLS.                                                     
           MOVE '2045-INSERT-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           MOVE PV-UPCPLS-EFF-BEG-TMST TO UPCPLS-EFF-BEG-TMST                   
           MOVE SPACES TO UPCPLS-EFF-END-TMST                                   
           IF UPCPLS-EFF-END-TMST = SPACES                                      
               MOVE -1 TO PV-EFF-END-NULL-IND                                   
           ELSE                                                                 
               MOVE 0  TO PV-EFF-END-NULL-IND                                   
           END-IF.                                                              
                                                                                
           DISPLAY ':PV-UPCPLS-EFF-BEG-TMST ' PV-UPCPLS-EFF-BEG-TMST            
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR SQLCODE = -803                                                
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                   INSERT INTO TUPCPLS                                          
                           (UPC_ID                                              
                           ,OPERCO_NBR                                          
                           ,STR_NBR                                             
                           ,EFF_BEG_TMST                                        
                           ,EFF_END_TMST                                        
                           ,UPC_STAT_CDE                                        
                           ,UNT_COST_AMT                                        
                           ,UNT_RTL_AMT                                         
                           ,MEITGP_NBR                                          
                           ,CRE_BY_USR_ID                                       
                           ,CRE_TMST                                            
                           ,ITM_NBR                                             
                           ,PLU_IND                                             
                           ,UPC_STAT_CHG_DTE                                    
                           ,UNT_RTL_CHG_DTE                                     
                           ,ORIG_CLR_DTE                                        
                           ,MKDN_CDE)                                           
                   VALUES (:UPCPLS-UPC-ID                                       
                          ,:UPCPLS-OPERCO-NBR                                   
                          ,:UPCPLS-STR-NBR                                      
                          ,:UPCPLS-EFF-BEG-TMST                                 
                          ,:UPCPLS-EFF-END-TMST :PV-EFF-END-NULL-IND            
                          ,'30'                                                 
                          ,:UPCPLS-UNT-COST-AMT :PV-UNT-COST-NULL-IND           
                          ,:UPCPLS-UNT-RTL-AMT                                  
                          ,:UPCPLS-MEITGP-NBR                                   
                          ,:PC-PROGRAM-NAME                                     
                          ,CURRENT TIMESTAMP                                    
                          ,:UPCPLS-ITM-NBR                                      
                          ,:UPCPLS-PLU-IND                                      
                          ,:UPCPLS-UPC-STAT-CHG-DTE                             
                          ,:UPCPLS-UNT-RTL-CHG-DTE                              
                          ,:UPCPLS-ORIG-CLR-DTE :PV-ORIG-CLR-NULL-IND           
                          ,:UPCPLS-MKDN-CDE     :PV-MKDN-CDE-NULL-IND)          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD SQLERRD(3) TO CR-TUPCPLS-ROW-INS-CNTR                
                   WHEN SQLCODE = -803                                          
                       DISPLAY 'DUPLICATE SKU NUMBER: ' PV-DISPLAY-8    0672    
                               ' STORE: ' PV-DISPLAY-4                  0672    
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'INSERT TUPCPLS TABLE'                              
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'INSERT TUPCPLS TABLE'                                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2055-UPDATE-TSKXREF.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - UPDATE THE STATUS ON TSKXREF.                             *        
      ******************************************************************        
       2055-UPDATE-TSKXREF.                                                     
           MOVE '2055-UPDATE-TSKXREF' TO PV-PARAGRAPH-NAME.                     
                                                                                
           MOVE PV-SKU TO SKXREF-SKU.                                           
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
                                                                                
                 EXEC SQL                                                       
                    UPDATE TSKXREF                                              
                        SET SKU_STAT_CDE   = '30'                               
                        WHERE SKU          = :SKXREF-SKU                        
                          AND SKU_STAT_CDE = '25'                               
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = ZERO                                        
                         ADD SQLERRD(3) TO CR-TSKXREF-ROW-UPD-CNTR              
                     WHEN SQLCODE = +100                                        
                         ADD +1         TO CR-TSKXREF-ROW-NOT-FOUND             
                         DISPLAY 'TSKXREF NOT FOUND'                            
                     WHEN SQLCODE = -913                                        
                         ADD +1      TO PV-RETRY-COUNT                          
                     WHEN OTHER                                                 
                         MOVE 'UPDATE TSKXREF TABLE'                            
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'UPDATE TSKXREF TABLE'                                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2060-SELECT-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - SELECT THE COUNT OF STORE SPECIFIC ROWS WITH RETAIL       *        
      *      EQUAL TO THE STORE ZERO ROW.                              *        
      ******************************************************************        
       2060-SELECT-TUPCPLS.                                                     
           MOVE '2060-SELECT-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
               EXEC SQL                                                         
                   SELECT COUNT(*)                                              
                    INTO :PV-RTL-EQ-TO-CORP                                     
                     FROM TUPCPLS                                               
                    WHERE ITM_NBR      = :UPCPLS-ITM-NBR                        
                      AND STR_NBR      <> 0                                     
                      AND UNT_RTL_AMT  = :UPCPLS-UNT-RTL-AMT                    
                      AND UPC_STAT_CDE = '30'                                   
                      AND EFF_END_TMST IS NULL                                  
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                  WHEN SQLCODE = ZERO                                           
                      CONTINUE                                                  
                  WHEN SQLCODE = +100                                           
                      MOVE ZERO TO PV-RTL-EQ-TO-CORP                            
                  WHEN SQLCODE = -913                                           
                      ADD +1      TO PV-RETRY-COUNT                             
                  WHEN OTHER                                                    
                      MOVE 'COUNT TUPCPLS TABLE'                                
                                  TO PV-DB2-OPERATION-ATTEMPTED                 
                      PERFORM 9999-SQL-ABEND                                    
              END-EVALUATE                                                      
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
              MOVE 'COUNT TUPCPLS TABLE'                                        
                                  TO PV-DB2-OPERATION-ATTEMPTED                 
              PERFORM 9999-SQL-ABEND                                            
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2085-UPDATE-TSK.                                               *        
      *  ==> PROCESSES:                                                *        
      *    - UPDATE THE STATUS ON TSK.                                 *        
      ******************************************************************        
       2085-UPDATE-TSK.                                                         
           MOVE '2085-UPDATE-TSK'  TO PV-PARAGRAPH-NAME.                        
                                                                                
           MOVE UPCPLS-ITM-NBR     TO  TSK-ITEM-NMBR.                           
           MOVE CC-BEG-DATE (3:2)  TO  PV-STAT-CHNGD-DATE-YY.                   
           MOVE CC-BEG-DATE (6:2)  TO  PV-STAT-CHNGD-DATE-MM.                   
           MOVE CC-BEG-DATE (9:2)  TO  PV-STAT-CHNGD-DATE-DD.                   
           MOVE PV-STAT-CHNGD-DATE TO  TSK-STAT-CHNGD-DATE.                     
           MOVE UPCPLS-UNT-RTL-AMT TO  TSK-CUR-UNIT-RTL-AMT.                    
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
                                                                                
                 EXEC SQL                                                       
                    UPDATE TSK                                                  
                        SET STATUS_CODE      = 30                               
                           ,STAT_CHNGD_DATE  = :TSK-STAT-CHNGD-DATE             
                           ,REGN_PRIC_IND    = :TSK-REGN-PRIC-IND               
                           ,CUR_UNIT_RTL_AMT = :TSK-CUR-UNIT-RTL-AMT            
                        WHERE ITEM_NMBR      = :TSK-ITEM-NMBR                   
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = ZERO                                        
                         ADD SQLERRD(3) TO CR-TSK-ROW-UPD-CNTR                  
                     WHEN SQLCODE = +100                                        
                         ADD +1         TO CR-TSK-ROW-NOT-FOUND                 
                         DISPLAY 'TSK NOT FOUND'                                
                     WHEN SQLCODE = -913                                        
                         ADD +1      TO PV-RETRY-COUNT                          
                     WHEN OTHER                                                 
                         MOVE 'UPDATE TSK TABLE'                                
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'UPDATE TSK TABLE'                                          
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2090-UPDATE-TUPC.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - UPDATE THE STATUS ON TUPC.                                *        
      ******************************************************************        
       2090-UPDATE-TUPC.                                                        
           MOVE '2090-UPDATE-TUPC' TO PV-PARAGRAPH-NAME.                        
                                                                                
           MOVE SKXREF-INTR-UPC-ID TO UPC-INTR-UPC-ID.                          
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
                                                                                
                 EXEC SQL                                                       
                    UPDATE TUPC                                                 
                        SET REGN_PRIC_IND      = :UPC-REGN-PRIC-IND             
                           ,LAST_UPD_TMST      = CURRENT TIMESTAMP              
                           ,LAST_UPD_BY_USR_ID = 'IMKUP991'                     
                        WHERE UPC_TYP_CDE      = 'I'                            
                          AND EFF_END_DTE IS NULL                               
                          AND INTR_UPC_ID      = :UPC-INTR-UPC-ID               
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = ZERO                                        
                         ADD SQLERRD(3) TO CR-TUPC-ROW-UPD-CNTR                 
                     WHEN SQLCODE = +100                                        
                         ADD +1         TO CR-TUPC-ROW-NOT-FOUND                
                         DISPLAY 'TUPC NOT FOUND'                               
                     WHEN SQLCODE = -913                                        
                         ADD +1      TO PV-RETRY-COUNT                          
                     WHEN OTHER                                                 
                         MOVE 'UPDATE TUPC TABLE'                               
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'UPDATE TUPC TABLE'                                         
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2095-UPDATE-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - UPDATE THE LOC SPECIFIC ROWS. SET EFF_END_TMST ON ROWS    *        
      *      WHERE RETAIL EQUALS RETAIL OF LOC ZERO ROW.               *        
      ******************************************************************        
       2095-UPDATE-TUPCPLS.                                                     
           MOVE '2095-UPDATE-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
                                                                                
                 EXEC SQL                                                       
                    UPDATE TUPCPLS                                              
                        SET EFF_END_TMST   = :PV-UPCPLS-EFF-END-TMST            
                           ,CRE_BY_USR_ID  = 'IMKUP991'                         
                        WHERE ITM_NBR      = :UPCPLS-ITM-NBR                    
                          AND STR_NBR      <> 0                                 
                          AND UNT_RTL_AMT  = :UPCPLS-UNT-RTL-AMT                
                          AND UPC_STAT_CDE = '30'                               
                          AND EFF_END_TMST IS NULL                              
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = ZERO                                        
                         ADD SQLERRD(3) TO CR-TUPCPLS-ROW-STR-CNTR              
                     WHEN SQLCODE = -913                                        
                         ADD +1      TO PV-RETRY-COUNT                          
                     WHEN OTHER                                                 
                         MOVE 'UPDATE TUPCPLS TABLE - STORE SPECIFIC'           
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'UPDATE TUPCPLS TABLE - STORE SPECIFIC'                     
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 3000-EOJ-PROCESSING.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - CHECK IF LAST SKU PROCESSED SHOULD BE COMPRESSED          *        
      *    - DELETE STYLE RESERVATION FOR LAST SKU PROCESSED           *        
      *    - CLOSE FILES                                               *        
      *    - DISPLAYS PROCESSING TOTALS                                *        
      *    - TAKES A FINAL COMMIT AND UPDATES THE CHECKPOINT STATUS    *        
      *      TO INDICATE SUCESSFULL COMPLETION                         *        
      ******************************************************************        
       3000-EOJ-PROCESSING.                                                     
           MOVE '3000-EOJ-PROCESSING' TO PV-PARAGRAPH-NAME.                     
                                                                                
           CLOSE SKU-UPDATE-FILE                                                
                 CONTROL-CARD-FILE.                                             
                                                                                
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
      ******************************************************************        
      * 4000-READ-RECORD.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - READS THE NEXT RECORD IN THE INPUT FILE.                  *        
      ******************************************************************        
       4000-READ-RECORD.                                                        
           MOVE '4000-READ-RECORD' TO PV-PARAGRAPH-NAME.                        
                                                                                
           READ SKU-UPDATE-FILE                                                 
               INTO PV-SKU-RECORD                                               
                   AT END                                                       
                       MOVE 'Y'     TO PS-END-OF-FILE-SW                        
                   NOT AT END                                                   
                       ADD +1       TO PV-RECS-READ-CNTR                        
                       ADD 1        TO DI-INPUT-RECS-DISPLAY                    
                       MOVE PV-SKU  TO PV-DISPLAY-8                             
           END-READ.                                                            
                                                                                
      ******************************************************************07160094
      * READ CONTROL FILE FOR JOB NAME, XS ITEM QUEUE NAME, AND        *07170094
      * PROCESS OLD DATE INDICATOR.                                    *07170094
      ******************************************************************07180094
       4100-READ-CONTROL-CARD-1.                                        07140094
           MOVE '4100-READ-CONTROL-CARD-1'  TO PV-PARAGRAPH-NAME.       07150094
                                                                        07210094
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                          
               AT END                                                           
                   DISPLAY '*** NO CONTROL CARD 1 ***'                          
                   SET PV-ABEND-4001 TO TRUE                                    
                   PERFORM 9999-ABEND.                                          
                                                                                
           DISPLAY CC-CONTROL-CARD.                                             
                                                                                
           IF CC-BEG-DATE = SPACES                                              
               DISPLAY '*** INVALID BEGIN DATE ***'                             
               SET PV-ABEND-4004 TO TRUE                                        
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
           IF CC-END-DATE = SPACES                                              
               DISPLAY '*** INVALID END DATE NAME ***'                          
               SET PV-ABEND-4005 TO TRUE                                        
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7200-SET-CURRENT-TIMESTAMP.                                    *        
      *  ==> PROCESSES:                                                *        
      *    - GET THE CURRENT TIMESTAMP FROM DB2.                       *        
      ******************************************************************        
       7200-SET-CURRENT-TIMESTAMP.                                              
           MOVE '7200-SET-CURRENT-TIMESTAMP' TO PV-PARAGRAPH-NAME.              
                                                                                
           EXEC SQL                                                             
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   MOVE PV-CURRENT-TIMESTAMP (1:10) TO                          
                          PV-RUN-DATE                                           
               WHEN OTHER                                                       
                   MOVE 'ERROR ON SET OF TIMESTAMP '                            
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 7800-COMMIT.                                                   *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.           *        
      *    - TAKE A COMMIT.                                            *        
      ******************************************************************        
       7800-COMMIT.                                                             
           MOVE '7800-COMMIT'      TO PV-PARAGRAPH-NAME.                        
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               ADD +1              TO CR-COMMIT-CNTR                            
           ELSE                                                                 
               MOVE 'ERROR ON COMMIT'                                           
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 9990-DISPLAY-CONTROL-INFO.                                     *        
      *  ==> PROCESSES:                                                *        
      *    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *        
      ******************************************************************        
       9990-DISPLAY-CONTROL-INFO.                                               
                                                                                
           MOVE CR-RECS-PROCESSED-CNTR                                          
                                   TO DI-RECS-PROCESSED-CNTR.                   
           MOVE PV-RECS-READ-CNTR  TO CR-RECS-READ-CNTR.                        
           MOVE CR-RECS-READ-CNTR  TO DI-RECS-READ-CNTR.                        
           MOVE CR-SKUS-READ-CNTR  TO DI-SKUS-READ-CNTR.                        
           MOVE CR-SKUS-NOT-FOUND-CNTR                                          
                                   TO DI-SKUS-NOT-FOUND-CNTR.                   
           MOVE CR-SKU-STRS-BYPASSED-CNTR                                       
                                   TO DI-SKU-STRS-BYPASSED-CNTR.                
           MOVE CR-TUPCPLS-ROW-UPD-CNTR                                         
                                   TO DI-TUPCPLS-ROW-UPDATE-CNTR.               
           MOVE CR-TUPCPLS-ROW-INS-CNTR                                         
                                   TO DI-TUPCPLS-ROW-INSERT-CNTR.               
           MOVE CR-TUPCPLS-ROW-STR-CNTR                                         
                                   TO DI-TUPCPLS-ROW-UPDSTR-CNTR.               
           MOVE CR-TSKXREF-ROW-UPD-CNTR                                         
                                   TO DI-TSKXREF-ROW-UPDATE-CNTR.               
           MOVE CR-TSK-ROW-UPD-CNTR                                             
                                   TO DI-TSK-ROW-UPDATE-CNTR.                   
           MOVE CR-TUPC-ROW-UPD-CNTR                                            
                                   TO DI-TUPC-ROW-UPDATE-CNTR.                  
           MOVE CR-TSKXREF-ROW-NOT-FOUND                                        
                                   TO DI-TSKXREF-ROW-NOT-FOUND.                 
           MOVE CR-TSK-ROW-NOT-FOUND                                            
                                   TO DI-TSK-ROW-NOT-FOUND.                     
           MOVE CR-TUPC-ROW-NOT-FOUND                                           
                                   TO DI-TUPC-ROW-NOT-FOUND.                    
           MOVE CR-COMMIT-CNTR     TO DI-COMMIT-CNTR.                           
                                                                                
           DISPLAY '********************************************'.              
           DISPLAY '**** RUN STATISTICS FOR PROGRAM IMKUP991 ***'.              
           DISPLAY '****'.                                                      
           DISPLAY 'INPUT RECS PROCESSED:  ' DI-RECS-PROCESSED-CNTR.            
           DISPLAY 'INPUT RECORDS READ:    ' DI-RECS-READ-CNTR.                 
           DISPLAY 'SKUS READ:             ' DI-SKUS-READ-CNTR.                 
           DISPLAY 'SKUS NOT FOUND:        ' DI-SKUS-NOT-FOUND-CNTR.            
           DISPLAY 'SKU/STORES BYPASSED:   ' DI-SKU-STRS-BYPASSED-CNTR.         
           DISPLAY 'TUPCPLS ROWS UPDATED:  ' DI-TUPCPLS-ROW-UPDATE-CNTR.        
           DISPLAY 'TUPCPLS ROWS INSERTED: ' DI-TUPCPLS-ROW-INSERT-CNTR.        
           DISPLAY 'TUPCPLS STR ROWS UPD:  ' DI-TUPCPLS-ROW-UPDSTR-CNTR.        
           DISPLAY 'TSKXREF ROWS UPDATED:  ' DI-TSKXREF-ROW-UPDATE-CNTR.        
           DISPLAY 'TSK ROWS UPDATED:      ' DI-TSK-ROW-UPDATE-CNTR.            
           DISPLAY 'TUPC ROWS UPDATED:     ' DI-TUPC-ROW-UPDATE-CNTR.           
           DISPLAY 'TSKXREF ROWS NOT FND:  ' DI-TSKXREF-ROW-NOT-FOUND.          
           DISPLAY 'TSK ROWS NOT FND:      ' DI-TSK-ROW-NOT-FOUND.              
           DISPLAY 'TUPC ROWS NOT FND:     ' DI-TUPC-ROW-NOT-FOUND.             
           DISPLAY 'COMMITS TAKEN:         ' DI-COMMIT-CNTR.                    
           DISPLAY '****'.                                                      
           DISPLAY '**** RUN STATS END FOR PROGRAM IMKUP991  ***'.              
           DISPLAY '********************************************'.              
                                                                                
      ******************************************************************        
      * 9999-SQL-ABEND.                                                *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *        
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
                                                                                
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'IMKUP991 - ABEND'.                                          
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
           DISPLAY 'SQL CODE  = ' PV-SQLCODE-DISPLAY.                           
           DISPLAY 'INPUT SKU = ' PV-DISPLAY-8.                                 
           DISPLAY 'STORE NBR = ' PV-DISPLAY-4.                                 
      *    DISPLAY 'EFFECTIVE DATE = ' IM048-UIR-EFF-DTE.                       
                                                                                
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       9999-ABEND.                                                      07730094
      ******************************************************************07740094
      * ABEND ROUTINE MISC ERRORS                                      *07750094
      ******************************************************************07760094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07770094
           DISPLAY '** PARAGRAPH **      = ' PV-PARAGRAPH-NAME.         07780094
           CALL 'ILBOABN0' USING ABEND-CODE.                            07800094
