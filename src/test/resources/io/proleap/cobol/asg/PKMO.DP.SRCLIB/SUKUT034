000100 IDENTIFICATION DIVISION.                                                 
000200******************************************************************        
000300*  RE-CREATE CARTON EXTRACT FILE UPDATING STORE NUMBER.                   
000400******************************************************************        
000500                                                                          
000600 PROGRAM-ID.        SUKUT034.                                             
000700 AUTHOR.            PATRICK BURKE.                                        
000800 INSTALLATION.      KOHLS.                                                
000900 DATE-WRITTEN       JUNE 2000.                                            
001000 DATE-COMPILED.                                                           
001100                                                                          
001200 ENVIRONMENT DIVISION.                                                    
001300 CONFIGURATION SECTION.                                                   
001400 SOURCE-COMPUTER.   IBM-3090.                                             
001500 OBJECT-COMPUTER.   IBM-3090.                                             
001600 INPUT-OUTPUT SECTION.                                                    
001700                                                                          
001800 FILE-CONTROL.                                                            
001900     SELECT TRAILER-EXTRACT-FILE                                          
002000         ASSIGN TO INP01.                                                 
002100     SELECT CARTON-EXTRACT-FILE1                                          
002200         ASSIGN TO OUT01.                                                 
002210     SELECT CARTON-EXTRACT-FILE2                                          
002220         ASSIGN TO OUT02.                                                 
002300                                                                          
002400 DATA DIVISION.                                                           
002500 FILE SECTION.                                                            
002600 FD  TRAILER-EXTRACT-FILE                                                 
002700     RECORDING MODE IS F                                                  
002800     DATA RECORD IS TRAILER-EXTRACT-RECORD.                               
002900                                                                          
003000 01  TRAILER-EXTRACT-RECORD            PIC X(200).                        
003100                                                                          
003200 FD  CARTON-EXTRACT-FILE1                                                 
003300     RECORDING MODE IS F                                                  
003400     DATA RECORD IS CARTON-EXTRACT-RECORD1.                               
003500                                                                          
003600 01  CARTON-EXTRACT-RECORD1            PIC X(200).                        
003601                                                                          
003610 FD  CARTON-EXTRACT-FILE2                                                 
003620     RECORDING MODE IS F                                                  
003630     DATA RECORD IS CARTON-EXTRACT-RECORD2.                               
003640                                                                          
003650 01  CARTON-EXTRACT-RECORD2            PIC X(200).                        
003700/                                                                         
003800 WORKING-STORAGE SECTION.                                                 
003900                                                                          
003901**************************************************************            
003902* FORMAT OF THE CONTROL CARD INFORMATION PASSED INTO THE PGM *            
003903**************************************************************            
003904 01  CC-CONTROL-CARD.                                                     
003905     05  CC-PRINTER-ID                PIC  X(08).                         
003906     05  CC-PRTR-CNTL-IND             PIC  X(01).                         
003907     05  CC-TRACK-NBR                 PIC  9(01).                         
003908     05  FILLER                       PIC  X(01).                         
003909     05  CC-CUT-REPORT                PIC  X(01).                         
003910         88 CC-CUT-REPORT-AT-END                   VALUE 'C'.             
003911     05  CC-REQUESTOR-ID              PIC  X(08).                         
003912     05  CC-PRINT-SUKRP002-IND        PIC  X(01).                         
003913         88 CC-PRINT-SUKRP002                      VALUE 'Y'.             
003914     05  CC-PRINT-SUKRP003-IND        PIC  X(01).                         
003915         88 CC-PRINT-SUKRP003                      VALUE 'Y'.             
003916     05  CC-PRINT-SUKRP004-IND        PIC  X(01).                         
003917         88 CC-PRINT-SUKRP004                      VALUE 'Y'.             
003918     05  CC-PRINT-SUKRP005-IND        PIC  X(01).                         
003919         88 CC-PRINT-SUKRP005                      VALUE 'Y'.             
003920     05  CC-PRINT-SUKRP006-IND        PIC  X(01).                         
003921         88 CC-PRINT-SUKRP006                      VALUE 'Y'.             
003922     05  CC-PRINT-SUKRP007-IND        PIC  X(01).                         
003923         88 CC-PRINT-SUKRP007                      VALUE 'Y'.             
003924     05  CC-PRINT-SUKRP008-IND        PIC  X(01).                         
003925         88 CC-PRINT-SUKRP008                      VALUE 'Y'.             
003926     05  CC-PRINT-SUKRP023-IND        PIC  X(01).                         
003927         88 CC-PRINT-SUKRP023                      VALUE 'Y'.             
003930*                                                                         
004000 01  ABEND-CODE                       PIC S9(04)  COMP SYNC               
004100                                                    VALUE ZERO.           
004200 01  PS-PROGRAM-SWITCHES.                                                 
004300     05  PS-EOF-EXTRACT-SW             PIC X(01) VALUE 'N'.               
004400         88  PS-EOF-EXTRACT                        VALUE 'Y'.             
004500     05  PS-SHPUNT-FOUND-SW            PIC X(01) VALUE 'N'.               
004600         88  PS-SHPUNT-FOUND                       VALUE 'Y'.             
004700         88  PS-SHPUNT-NOT-FOUND                   VALUE 'N'.             
004710     05  PS-SULBXF-FOUND-SW            PIC X(01) VALUE 'N'.               
004720         88  PS-SULBXF-FOUND                       VALUE 'Y'.             
004730         88  PS-SULBXF-NOT-FOUND                   VALUE 'N'.             
004740     05  PS-GOOD-RECORD-SW             PIC X(01) VALUE 'N'.               
004750         88  PS-GOOD-RECORD                        VALUE 'Y'.             
004760         88  PS-NOT-GOOD-RECORD                    VALUE 'N'.             
004770     05  PS-WRITE-RECORD-SW            PIC X(01) VALUE 'N'.               
004780         88  PS-WRITE-RECORD1                      VALUE 'Y'.             
004790         88  PS-WRITE-RECORD2                      VALUE 'N'.             
004791     05  PS-SULBXF-CSR-SW              PIC X(01) VALUE 'N'.               
004792         88  PS-END-SULBXF-CSR                     VALUE 'Y'.             
004793         88  PS-NOT-END-SULBXF-CSR                 VALUE 'N'.             
004800                                                                          
004900 01  PC-PROGRAM-CONSTANTS.                                                
005000     05  PC-DEFAULT-TMST              PIC  X(26)  VALUE                   
005100         '9999-09-09-09.09.09.999999'.                                    
005400                                                                          
005600                                                                          
005900 01  PV-PROGRAM-VARIABLES.                                                
006401     05  PV-PO-NBR                   PIC S9(9) COMP VALUE ZERO.           
006402     05  PV-REC-SEQ-NBR              PIC S9(9) COMP VALUE ZERO.           
006404     05  PV-SHIPT-UNT-ID             PIC S9(18) COMP-3 VALUE ZERO.        
006405     05  PV-STR-NBR                  PIC S9(04) COMP VALUE ZERO.          
006406     05  PV-CARTON-NBR               PIC S9(04) COMP VALUE ZERO.          
006407     05  PV-ABEND-PARAGRAPH          PIC X(30)  VALUE SPACES.             
006410                                                                          
006601     05  PV-FORMAT-TMST.                                                  
006602         10  PV-FORMAT-TMST-DTE.                                          
006603             15  PV-FORMAT-TMST-DTE-CY  PIC X(04)  VALUE SPACES.          
006604             15  PV-FORMAT-TMST-DTE-D1  PIC X(01)  VALUE '-'.             
006605             15  PV-FORMAT-TMST-DTE-MM  PIC X(02)  VALUE SPACES.          
006606             15  PV-FORMAT-TMST-DTE-D2  PIC X(01)  VALUE '-'.             
006607             15  PV-FORMAT-TMST-DTE-DD  PIC X(02)  VALUE SPACES.          
006608             15  PV-FORMAT-TMST-DTE-D3  PIC X(01)  VALUE '-'.             
006609         10  PV-FORMAT-TMST-TM.                                           
006610             15  PV-FORMAT-TMST-TM-HH   PIC X(02)  VALUE SPACES.          
006611             15  PV-FORMAT-TMST-TM-D1   PIC X(01)  VALUE '.'.             
006612             15  PV-FORMAT-TMST-TM-MM   PIC X(02)  VALUE SPACES.          
006613             15  PV-FORMAT-TMST-TM-D2   PIC X(10)  VALUE                  
006620                                                     '.00.000000'.        
006621     05  PV-REFORMAT-TMST.                                                
006622         10  PV-REFORMAT-TMST-DTE.                                        
006623             15  PV-REFORMAT-TMST-DTE-CY  PIC 9(04).                      
006624             15  PV-REFORMAT-TMST-DTE-MM  PIC 9(02).                      
006625             15  PV-REFORMAT-TMST-DTE-DD  PIC 9(02).                      
006626         10  PV-REFORMAT-TMST-TM.                                         
006627             15  PV-REFORMAT-TMST-TM-HH   PIC 9(02).                      
006628             15  PV-REFORMAT-TMST-TM-MM   PIC 9(02).                      
006630                                                                          
006700 01  PV-COUNTERS.                                                         
007000     05  PV-TOTAL-INPUT-READ          PIC S9(17) COMP-3                   
007100                                                VALUE ZERO.               
007200     05  PV-RECORD1-WRITTEN           PIC S9(17) COMP-3                   
007300                                                VALUE ZERO.               
007310     05  PV-RECORD2-WRITTEN           PIC S9(17) COMP-3                   
007320                                                VALUE ZERO.               
007400     05  PV-RECORDS-NOT-UPDATED       PIC S9(17) COMP-3                   
007500                                                VALUE ZERO.               
007800/                                                                         
007900*  CARTON-UPDATE-RECORD.                                                  
007910                                                                          
008000     COPY SURD001.                                                        
008100/                                                                         
009400*  SHIPPING UNIT TABLE.                                                   
009410                                                                          
009500     EXEC SQL                                                             
009600         INCLUDE TSHPUNT                                                  
009700     END-EXEC.                                                            
009800/                                                                         
009900*    LOCATION MASTER XLT_LOC                                              
010330     EXEC SQL                                                             
010340         INCLUDE XLT00010                                                 
010350     END-EXEC.                                                            
010360/                                                                         
010370                                                                          
010380     EXEC SQL                                                             
010390         INCLUDE TSULBXF                                                  
010391     END-EXEC.                                                            
010392/                                                                         
010400     EXEC SQL                                                             
010500         INCLUDE SQLCA                                                    
010600     END-EXEC.                                                            
010610                                                                          
010620     COPY SQLCA2.                                                         
010700/                                                                         
010701******************************************************************        
010702*  CURSOR DECLARATION FOR DEST-OPER-UNT-ID.                               
010703******************************************************************        
010705                                                                          
010706     EXEC SQL                                                             
010707         DECLARE SULBXF-CSR CURSOR FOR                                    
010708         SELECT                                                           
010709                A.SHIPT_UNT_ID                                            
010710              , A.DEST_OPER_UNT_ID                                        
010711              , A.BRCDE_NBR                                               
010714         FROM   TSULBXF A                                                 
010715         WHERE                                                            
010716                A.SHIPT_UNT_ID       = :PV-SHIPT-UNT-ID                   
010717          AND   A.STAT_CDE           = 'A'                                
010730     END-EXEC.                                                            
010900/                                                                         
011000 PROCEDURE DIVISION.                                                      
011100                                                                          
011200 0000-PROGRAM-MAINLINE.                                                   
011300                                                                          
011301     DISPLAY '**** START OF SUKUT034 ****'.                               
011302     DISPLAY ' '.                                                         
011303                                                                          
011304     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
011305     DISPLAY 'CONTROL CARD = ' CC-CONTROL-CARD.                           
011306                                                                          
011307     IF CC-PRINT-SUKRP007                                                 
011430        PERFORM 1000-INITIALIZE                                           
011500                                                                          
011600        PERFORM 2000-PROCESS-EXTRACT-FILE                                 
011700           UNTIL PS-EOF-EXTRACT                                           
011800                                                                          
011900        PERFORM 9000-EOJ-ROUTINE                                          
011901     ELSE                                                                 
011910        CONTINUE.                                                         
012000                                                                          
012100     GOBACK.                                                              
012200/                                                                         
012300 1000-INITIALIZE.                                                         
012400                                                                          
012500     OPEN INPUT  TRAILER-EXTRACT-FILE                                     
012600          OUTPUT CARTON-EXTRACT-FILE1                                     
012700          OUTPUT CARTON-EXTRACT-FILE2.                                    
012730                                                                          
012800     PERFORM 8000-READ-EXT-FILE                                           
012900     IF PS-EOF-EXTRACT                                                    
013300         DISPLAY 'EXTRACT FILE INP01 IS EMPTY'                            
013310     ELSE                                                                 
013320        IF SU001-ERROR-RECORD                                             
013330          PERFORM 8100-WRITE-UPDATE-RECORD1                               
013340          PERFORM 8200-WRITE-UPDATE-RECORD2                               
013350          SET PS-EOF-EXTRACT TO TRUE                                      
013351        ELSE                                                              
013352          PERFORM 8000-READ-EXT-FILE                                      
013360        END-IF                                                            
013400     END-IF.                                                              
013500/                                                                         
013600 2000-PROCESS-EXTRACT-FILE.                                               
013700                                                                          
013710     IF  SU001-SHIPT-UNT-ID = ZERO                                        
013711         CONTINUE                                                         
013730     ELSE                                                                 
013800         PERFORM 7900-SELECT-TSHPUNT                                      
013900         EVALUATE TRUE                                                    
014000           WHEN PS-SHPUNT-FOUND                                           
014010               PERFORM 3000-CHECK-TYPE-CODE                               
015713           WHEN OTHER                                                     
015720               ADD 1 TO PV-RECORDS-NOT-UPDATED                            
015800         END-EVALUATE                                                     
015810     END-IF.                                                              
015900                                                                          
016000     PERFORM 8000-READ-EXT-FILE.                                          
016110                                                                          
016120/                                                                         
016121 3000-CHECK-TYPE-CODE.                                                    
016122*                                                                         
016131      IF XLT00010-LOC-TYP-CDE = 'DS'                                      
016132         IF ((SU001-SCANNED-IND = 'Y') AND                                
016133         (SU001-MISDIRECT-IND = 'Y' OR SU001-FREE-ASTRAY-IND              
016134         = 'Y'))                                                          
016135            MOVE SHPUNT-DEST-OPER-UNT-ID TO SU001-STR-NBR                 
016136            PERFORM 8100-WRITE-UPDATE-RECORD1                             
016137         ELSE                                                             
016138            IF (SU001-SCANNED-IND = 'N' OR ' ')                           
016139               MOVE SHPUNT-DEST-OPER-UNT-ID TO SU001-STR-NBR              
016140               PERFORM 8100-WRITE-UPDATE-RECORD1                          
016141            END-IF                                                        
016142         END-IF                                                           
016143      ELSE                                                                
016152         IF (SU001-SCANNED-IND = 'N' OR ' ')                              
016153             MOVE SHPUNT-DEST-OPER-UNT-ID TO SU001-STR-NBR                
016154             PERFORM 8200-WRITE-UPDATE-RECORD2                            
016155         ELSE                                                             
016157             IF SU001-SCANNED-IND = 'Y'                                   
016158                 PERFORM 7200-OPEN-SULBXF-CSR                             
016159                 PERFORM 7300-FETCH-SULBXF-CSR                            
016160                 PERFORM 3500-CHECK-TSULBXF-CSR                           
016161             END-IF                                                       
016162         END-IF                                                           
016163      END-IF.                                                             
016165                                                                          
016166 3500-CHECK-TSULBXF-CSR.                                                  
016167*                                                                         
016168      IF PS-END-SULBXF-CSR                                                
016169         PERFORM 7400-CLOSE-SULBXF-CSR                                    
016170         MOVE SHPUNT-DEST-OPER-UNT-ID TO SU001-STR-NBR                    
016171         PERFORM 8100-WRITE-UPDATE-RECORD1                                
016172      ELSE                                                                
016173         MOVE SULBXF-DEST-OPER-UNT-ID TO SU001-STR-NBR                    
016174         MOVE SULBXF-BRCDE-NBR        TO SU001-BRCDE-NBR                  
016175         MOVE  'Y'                    TO SU001-TSULBXF-USED               
016176         PERFORM 8100-WRITE-UPDATE-RECORD1                                
016177         PERFORM 7100-PROCESS-FETCH                                       
016178            UNTIL PS-END-SULBXF-CSR                                       
016179         PERFORM 7400-CLOSE-SULBXF-CSR                                    
016180      END-IF.                                                             
016190*                                                                         
031700 7100-PROCESS-FETCH.                                                      
031801     PERFORM 7300-FETCH-SULBXF-CSR                                        
031803       IF PS-NOT-END-SULBXF-CSR                                           
031804          MOVE SULBXF-DEST-OPER-UNT-ID TO SU001-STR-NBR                   
031805          MOVE SULBXF-BRCDE-NBR        TO SU001-BRCDE-NBR                 
031806          MOVE  'Y'                    TO SU001-TSULBXF-USED              
031807          PERFORM 8100-WRITE-UPDATE-RECORD1                               
031809       ELSE                                                               
031810          SET PS-END-SULBXF-CSR        TO TRUE.                           
031811                                                                          
031812/                                                                         
031813 7200-OPEN-SULBXF-CSR.                                                    
031814     MOVE '7200-OPEN-SULBXF-CSR   '                                       
031815                                 TO PV-ABEND-PARAGRAPH.                   
031816     EXEC SQL                                                             
031817         OPEN SULBXF-CSR                                                  
031818     END-EXEC.                                                            
031819                                                                          
031820     EVALUATE TRUE                                                        
031821       WHEN SQLCODE = ZERO                                                
031822         SET PS-NOT-END-SULBXF-CSR  TO TRUE                               
031823       WHEN OTHER                                                         
031830         DISPLAY PV-ABEND-PARAGRAPH                                       
71             DISPLAY 'SHIPT-UNT-ID ' PV-SHIPT-UNT-ID                          
031840         PERFORM 9200-SQL-ERROR                                           
031850     END-EVALUATE.                                                        
034600/                                                                         
034700 7300-FETCH-SULBXF-CSR.                                                   
034701     MOVE '7300-FETCH-SULBXF-CSR '                                        
034710                                 TO PV-ABEND-PARAGRAPH.                   
034810     EXEC SQL                                                             
034820         FETCH  SULBXF-CSR                                                
034850         INTO                                                             
034860               :SULBXF-SHIPT-UNT-ID                                       
034870             , :SULBXF-DEST-OPER-UNT-ID                                   
034880             , :SULBXF-BRCDE-NBR                                          
034893     END-EXEC.                                                            
034894                                                                          
034895     EVALUATE TRUE                                                        
034896         WHEN SQLCODE = ZERO                                              
034897             CONTINUE                                                     
034898         WHEN SQLCODE = +100                                              
034900             SET PS-END-SULBXF-CSR    TO TRUE                             
034901         WHEN SQLCODE NOT EQUAL ZERO                                      
034902         WHEN SQLWARN0 NOT = SPACES                                       
034903             DISPLAY '** ABEND - SUKUT034 **'                             
034904             DISPLAY '** 7300-FETCH-SULBXF-CSR **'                        
                   DISPLAY 'SHIPT-UNT-ID ' PV-SHIPT-UNT-ID                      
034905             PERFORM 9300-SQL-WARNING                                     
034906     END-EVALUATE.                                                        
034915/                                                                         
034916 7400-CLOSE-SULBXF-CSR.                                                   
034917     MOVE '7400-CLOSE-SULBXF-CURSOR   '                                   
034918                                 TO PV-ABEND-PARAGRAPH.                   
034919     EXEC SQL                                                             
034920         CLOSE SULBXF-CSR                                                 
034921     END-EXEC.                                                            
034922                                                                          
034923     EVALUATE TRUE                                                        
034924       WHEN SQLCODE = ZEROS                                               
034925         CONTINUE                                                         
034930       WHEN OTHER                                                         
034931         DISPLAY PV-ABEND-PARAGRAPH                                       
               DISPLAY 'SHIPT-UNT-ID ' PV-SHIPT-UNT-ID                          
034932         PERFORM 9200-SQL-ERROR                                           
034933     END-EVALUATE.                                                        
034934/                                                                         
034943 7900-SELECT-TSHPUNT.                                                     
034944                                                                          
034945     INITIALIZE DCLTSHPUNT.                                               
034946                                                                          
034947     EXEC SQL                                                             
034948         SELECT                                                           
034950                A.DEST_OPER_UNT_ID                                        
034952             ,  B.LOC_TYP_CDE                                             
034953          INTO                                                            
034955                :SHPUNT-DEST-OPER-UNT-ID                                  
034957              , :XLT00010-LOC-TYP-CDE                                     
034958          FROM                                                            
034959                TSHPUNT A                                                 
034960               ,XLT_LOC B                                                 
034961         WHERE                                                            
034962                A.SHIPT_UNT_ID       = :PV-SHIPT-UNT-ID                   
034963           AND  A.DEST_OPER_UNT_ID   = B.LOC_NBR                          
034964     END-EXEC                                                             
034965                                                                          
034966     EVALUATE TRUE                                                        
034967         WHEN SQLCODE = ZERO                                              
034968             SET PS-SHPUNT-FOUND  TO TRUE                                 
034969         WHEN SQLCODE = +100                                              
034970             SET PS-SHPUNT-NOT-FOUND  TO TRUE                             
034971*            DISPLAY 'SHIPT-UNT-ID ' PV-SHIPT-UNT-ID                      
034972         WHEN SQLCODE NOT EQUAL ZERO                                      
034973         WHEN SQLWARN0 NOT = SPACES                                       
034974             DISPLAY '** ABEND - SUKUT034 **'                             
034975             DISPLAY '** 7900-SELECT-TSHPUNT **'                          
034971             DISPLAY 'SHIPT-UNT-ID ' PV-SHIPT-UNT-ID                      
034976             PERFORM 9300-SQL-WARNING                                     
034977     END-EVALUATE.                                                        
034978/                                                                         
034980*                                                                         
037960 8000-READ-EXT-FILE.                                                      
038000     READ TRAILER-EXTRACT-FILE                                            
038100          INTO SU001-EXTRACT-RECORD                                       
038200     AT END                                                               
038300         SET PS-EOF-EXTRACT           TO TRUE                             
038400     NOT AT END                                                           
038410         MOVE SU001-SHIPT-UNT-ID      TO PV-SHIPT-UNT-ID                  
038500         MOVE SU001-PO-NUMBER         TO PV-PO-NBR                        
038510         MOVE SU001-REC-SEQ-NBR       TO PV-REC-SEQ-NBR                   
038800         ADD 1                        TO PV-TOTAL-INPUT-READ              
038900     END-READ.                                                            
039000                                                                          
039010/                                                                         
039100 8100-WRITE-UPDATE-RECORD1.                                               
039200     WRITE CARTON-EXTRACT-RECORD1                                         
039300         FROM SU001-EXTRACT-RECORD                                        
039400     ADD 1                            TO PV-RECORD1-WRITTEN.              
042200                                                                          
045100/                                                                         
045110 8200-WRITE-UPDATE-RECORD2.                                               
045120     WRITE CARTON-EXTRACT-RECORD2                                         
045130         FROM SU001-EXTRACT-RECORD                                        
045140     ADD 1                            TO PV-RECORD2-WRITTEN.              
045150                                                                          
045160/                                                                         
045200 9000-EOJ-ROUTINE.                                                        
045300                                                                          
045400     CLOSE TRAILER-EXTRACT-FILE                                           
045500           CARTON-EXTRACT-FILE1                                           
045510           CARTON-EXTRACT-FILE2.                                          
045600                                                                          
045601     DISPLAY '**********************************'.                        
045602     DISPLAY '** SUKUT034 SUMMARY **'.                                    
045603     DISPLAY 'TOTAL INPUT RECS       = ' PV-TOTAL-INPUT-READ.             
045605     DISPLAY 'TOTAL REC1 WRITTEN     = ' PV-RECORD1-WRITTEN.              
045606     DISPLAY 'TOTAL REC2 WRITTEN     = ' PV-RECORD2-WRITTEN.              
045610     DISPLAY '**********************************'.                        
045700                                                                          
046610/                                                                         
046700 9200-SQL-ERROR.                                                          
046800                                                                          
046900     CLOSE TRAILER-EXTRACT-FILE                                           
047000           CARTON-EXTRACT-FILE1                                           
047010           CARTON-EXTRACT-FILE2                                           
047100                                                                          
047200     MOVE SQLCODE                     TO SQLCODE2.                        
047300     MOVE SQLERRD(3)                  TO SQLERRD3.                        
047400                                                                          
047500     DISPLAY '** ABEND **         ** = SQLERROR'.                         
047600     DISPLAY '** PROGRAM          ** = SUKUT034'.                         
047700     DISPLAY '** SQLCODE          ** = ' SQLCODE2.                        
047800     DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                        
047900     DISPLAY '** SQLERRM          ** = ' SQLERRM.                         
048000     DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                        
048100                                                                          
048200                                                                          
048300     MOVE +4013                       TO ABEND-CODE.                      
048400     CALL 'ILBOABN0' USING ABEND-CODE.                                    
048500                                                                          
048600 9300-SQL-WARNING.                                                        
048700                                                                          
048800     CLOSE TRAILER-EXTRACT-FILE                                           
048900           CARTON-EXTRACT-FILE1                                           
048910           CARTON-EXTRACT-FILE2                                           
049000                                                                          
049100     DISPLAY '9300-SQL-WARNING'.                                          
049200     MOVE SQLCODE                     TO SQLCODE2.                        
049300     MOVE SQLERRD(3)                  TO SQLERRD3.                        
049400     DISPLAY '** ABEND **       ** = SQLWARNING'.                         
049500     DISPLAY '** PROGRAM        ** = SUKUT034'.                           
049600     DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
049700     DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
049800     DISPLAY '** SQLWARN0       ** = ' SQLWARN0.                          
049900     DISPLAY '** SQLWARN1       ** = ' SQLWARN1.                          
050000     DISPLAY '** SQLWARN2       ** = ' SQLWARN2.                          
050100     DISPLAY '** SQLWARN3       ** = ' SQLWARN3.                          
050200     DISPLAY '** SQLWARN4       ** = ' SQLWARN4.                          
050300     DISPLAY '** SQLWARN5       ** = ' SQLWARN5.                          
050400     DISPLAY '** SQLWARN6       ** = ' SQLWARN6.                          
050500     DISPLAY '** SQLWARN7       ** = ' SQLWARN7.                          
050600                                                                          
050700     MOVE +4013                       TO ABEND-CODE.                      
050800     CALL 'ILBOABN0' USING ABEND-CODE.                                    
