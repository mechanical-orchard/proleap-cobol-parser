      ******************************************************************00010043
       IDENTIFICATION DIVISION.                                         00020043
      ******************************************************************00030043
                                                                        00040043
       PROGRAM-ID.             DBKUP005.                                00050043
       AUTHOR.                 BARBARA NEHRLING.                        00060043
       INSTALLATION.           KOHLS.                                   00070043
       DATE-WRITTEN            APRIL 1996.                              00080043
       DATE-COMPILED.                                                   00090043
                                                                        00100043
      ******************************************************************00110043
      * THIS PROGRAM MAINTAINS THE PLAN_TABLE.  PRODUCTION PROGRAMS     00120043
      * ARE BOUND WITH EXPLAIN(YES).  THIS PROGRAM DELETES OLD          00130043
      * ACCESS PATH INFO.  THE THREE MOST RECENT ONES ARE KEPT.         00140043
      ******************************************************************00171062
      * 05/20/02 - CODE ADDED TO DELETE CORRESPONDING ROWS FROM         00172062
      *            DSN_STATEMNT_TABLE.  ALSO ADDED CODE TO DELETE       00173062
      *            ROWS FROM PLAN_TABLE AND DSN_STATEMNT_TABLE WHERE    00174062
      *            THE PACKAGE NO LONGER EXISTS IN SYSIBM.SYSPACKAGE.   00175062
      *                                                    BRAD DORN    00176062
      *                                                                 00177062
      * 08/08/07 - CODE ADDED TO NOT DELETE ANY PLAN_TABLE ROWS         00178062
      *            THAT HAVE AN OPTHINT > ' '.                          00179062
      *                                               CRAIG K. MARTIN   00179162
      * 06/30/22 - REMOVED ALL CODE REFERENCING OPTHINTS.  PER IBM      00179281
      *            PLAN_TABLE ACCESS PATHS ARE NOT THE RECOMMENDED      00179381
      *            METHOD FOR ENFORCING EXISTING ACCESS PATHS AFTER     00179481
      *            REBIND.  PLAN MANAGEMENT SHOULD NOW BE USED.         00179581
      *                                               BRAD DORN         00179681
      ******************************************************************00179781
                                                                        00180043
      ******************************************************************00190043
       ENVIRONMENT DIVISION.                                            00200043
      ******************************************************************00210043
                                                                        00220043
       CONFIGURATION SECTION.                                           00230043
                                                                        00240043
       SOURCE-COMPUTER.        IBM-3090.                                00250043
       OBJECT-COMPUTER.        IBM-3090.                                00260043
                                                                        00270043
       INPUT-OUTPUT SECTION.                                            00280043
                                                                        00290043
       FILE-CONTROL.                                                    00300043
                                                                        00310043
      ******************************************************************00320043
       DATA DIVISION.                                                   00330043
      ******************************************************************00340043
                                                                        00350043
       FILE SECTION.                                                    00360043
                                                                        00370043
      ******************************************************************00380043
       WORKING-STORAGE SECTION.                                         00390043
      ******************************************************************00400043
                                                                        00410043
       01  WS-SWITCHES.                                                 00420043
           05  WS-CURSOR-EOF1-SW       PIC  X(01)  VALUE 'N'.           00430043
                                                                        00460043
       01  WS-COUNTERS.                                                 00470043
           05  WS-NBR-OF-OCCURENCES    PIC S9(04)  VALUE +1  COMP.      00480043
           05  WS-COUNT-PLAN-TBL       PIC  9(06)  VALUE 0.             00481069
           05  WS-COUNT-FUNC-TBL       PIC  9(06)  VALUE 0.             00482069
           05  WS-COUNT-STAT-TBL       PIC  9(06)  VALUE 0.             00483069
                                                                        00490043
       01  WS-SAVES.                                                    00500043
           05  WS-SAVE-APPLNAME        PIC  X(24)  VALUE SPACES.        00510063
           05  WS-SAVE-PROGNAME        PIC  X(128) VALUE SPACES.        00520064
           05  WS-SAVE-DATE            PIC  X(08)  VALUE SPACES.        00530043
           05  WS-SAVE-COLLID          PIC  X(128) VALUE SPACES.        00540063
                                                                        00590043
       01  WS-MISC.                                                     00600043
           05  WS-ABEND-CODE           PIC S9(04)  COMP SYNC VALUE +0.  00610043
           05  WS-COMMIT-TMST          PIC X(26)   VALUE SPACES.        00620043
           05  WS-CURRENT-TMST         PIC X(26)   VALUE SPACES.        00630043
                                                                        00640043
       01  WS-TIMESTAMP.                                                00650043
           05  WS-DATE                 PIC  X(08)  VALUE SPACES.        00660043
           05  FILLER                  PIC  X(08)  VALUE SPACES.        00670043
                                                                        00680043
       01  DP004-DB2-ERROR-FIELDS.                                      00690043
           05  DP004-ASTERISK-LINE       PIC  X(80)  VALUE ALL '*'.     00700043
           05  DP004-MAX-MESSAGE-LINES   PIC S9(04)  VALUE +12          00710043
                                                     COMP SYNC.         00720043
           05  DP004-ERROR-LINE-LENGTH   PIC S9(09)  VALUE +75          00730043
                                                     COMP SYNC.         00740043
           05  DP004-FORMATTED-MESSAGE.                                 00750043
               10  FILLER                PIC S9(04)  VALUE +900         00760043
                                                     COMP SYNC.         00770043
               10  DP004-MESSAGE-LINE    OCCURS 12 TIMES                00780043
                                         INDEXED BY DP004-MSG-IDX       00790043
                                         PIC  X(75).                    00800043
      *                                                                 00810043
      *  DCLGEN LAYOUT                                                  00820043
      *                                                                 00830043
           EXEC SQL                                                     00840043
               INCLUDE TCKRSTCN                                         00850043
           END-EXEC.                                                    00860043
                                                                        00870043
           EXEC SQL                                                     00880043
               INCLUDE PLANTBL                                          00890043
           END-EXEC.                                                    00900043
                                                                        00910043
           EXEC SQL                                                     00920043
               INCLUDE STATEMNT                                         00930043
           END-EXEC.                                                    00940043
                                                                        00950043
           EXEC SQL                                                     00960043
               INCLUDE FUNCTION                                         00970043
           END-EXEC.                                                    00980043
                                                                        00990043
           EXEC SQL                                                     01000043
               DECLARE PLANTBL_CSR CURSOR WITH HOLD FOR                 01010043
                 SELECT APPLNAME                                        01020043
                      , PROGNAME                                        01030043
                      , TIMESTAMP                                       01040043
                      , BIND_TIME                                       01050043
                      , QUERYNO                                         01070043
                      , COLLID                                          01080043
                 FROM   PLAN_TABLE                                      01090079
                 ORDER BY APPLNAME ASC                                  01100043
                        , PROGNAME ASC                                  01110043
                        , COLLID ASC                                    01120043
                        , TIMESTAMP DESC                                01130043
           END-EXEC.                                                    01140043
                                                                        01330043
           EXEC SQL                                                     01340043
               INCLUDE SQLCA                                            01350043
           END-EXEC.                                                    01360043
                                                                        01370043
      ******************************************************************01380043
       LINKAGE SECTION.                                                 01390043
      ******************************************************************01400043
                                                                        01410043
                                                                        01420043
      ******************************************************************01430043
       PROCEDURE DIVISION.                                              01440043
      ******************************************************************01450043
      * THIS PARAGRAPH PERFORMS THE FOLLOWING:                          01460043
      * - DELETES OBSOLETE PLANS                                        01470043
      * - DELETES OBSOLETE PACKAGES                                     01480043
      * - DOES INITIALIZATION                                           01490043
      * - FETCHS FIRST ROW OF PLANTBL CURSOR                            01500043
      * - PROCESSES CURSOR UNTIL END OF CURSOR                          01510043
      ******************************************************************01520043
                                                                        01530043
       0000-PROGRAM-MAINLINE.                                           01540043
                                                                        01550043
           PERFORM 0100-DEL-OBS-PLNS.                                   01560043
                                                                        01570043
           PERFORM 0200-DEL-OBS-PKGS.                                   01580043
                                                                        01590043
           PERFORM 1000-INITIALIZATION.                                 01600043
                                                                        01610043
           PERFORM 2100-FETCH-PLANTBL-CSR.                              01620043
                                                                        01630043
           MOVE PLANTBL-APPLNAME-TEXT TO WS-SAVE-APPLNAME.              01640063
           MOVE PLANTBL-PROGNAME-TEXT TO WS-SAVE-PROGNAME.              01650064
           MOVE PLANTBL-TIMESTAMP TO WS-TIMESTAMP.                      01660043
           MOVE WS-DATE           TO WS-SAVE-DATE.                      01670043
           MOVE PLANTBL-COLLID-TEXT TO WS-SAVE-COLLID.                  01680063
                                                                        01690043
           PERFORM 2000-PROCESS-PLANTBL-CURSOR                          01700043
              UNTIL WS-CURSOR-EOF1-SW EQUAL 'Y'.                        01710043
                                                                        01720043
           DISPLAY  '********************'                              01720169
           DISPLAY  'PLAN TBL RECS DELETED  : '                         01720269
             WS-COUNT-PLAN-TBL                                          01720369
           DISPLAY  'FUNC TBL RECS DELETED  : '                         01720469
             WS-COUNT-FUNC-TBL                                          01720569
           DISPLAY  'STAT TBL RECS DELETED  : '                         01720669
             WS-COUNT-STAT-TBL                                          01720769
           DISPLAY  '********************'                              01721469
           STOP RUN.                                                    01730043
                                                                        01740043
      ******************************************************************01750043
      * THIS PARAGRAPH PERFORMS THE FOLLOWING:                          01760043
      * - DELETES THE ROWS FOR OBSOLETE PLANS    (NO LONGER IN CATALOG) 01770043
      ******************************************************************01780043
                                                                        01790043
       0100-DEL-OBS-PLNS.                                               01800043
                                                                        01810043
           EXEC SQL                                                     01820043
            DELETE FROM PLAN_TABLE                                      01830079
             WHERE APPLNAME <> ' '                                      01840043
              AND  APPLNAME NOT IN                                      01850043
               (SELECT NAME                                             01860043
                  FROM SYSIBM.SYSPLAN)                                  01870043
           END-EXEC.                                                    01880043
                                                                        01890043
           IF SQLCODE = +0 OR SQLCODE = +100                            01900043
               ADD SQLERRD(3)  TO WS-COUNT-PLAN-TBL                     01901069
           ELSE                                                         01920043
               MOVE +4013      TO WS-ABEND-CODE                         01930043
               DISPLAY '*****************************************'      01940043
               DISPLAY '*** PROGRAM ERROR - DBKUP005          ***'      01950043
               DISPLAY '***                                   ***'      01960043
               DISPLAY '*** BAD DELETE ON PLAN_TABLE          ***'      01970043
               DISPLAY '*** PARAGRAPH 0100-DEL-OBS-PLNS       ***'      01980043
               DISPLAY '*****************************************'      01990043
               PERFORM 9999-SQL-ABEND                                   02000043
           END-IF.                                                      02010043
                                                                        02020043
      ******************************************************************02030043
      * THIS PARAGRAPH PERFORMS THE FOLLOWING:                          02040043
      * - DELETES THE ROWS FOR OBSOLETE PACKAGES (NO LONGER IN CATALOG) 02050043
      ******************************************************************02060043
                                                                        02070043
       0200-DEL-OBS-PKGS.                                               02080043
                                                                        02090043
           EXEC SQL                                                     02100043
            DELETE FROM DSN_FUNCTION_TABLE                              02110043
             WHERE COLLID <> ' '                                        02120043
              AND  PROGNAME NOT IN                                      02130043
              (SELECT NAME                                              02140043
                 FROM SYSIBM.SYSPACKAGE)                                02150043
           END-EXEC.                                                    02160043
                                                                        02170043
           IF SQLCODE = +0 OR SQLCODE = +100                            02180043
               ADD SQLERRD(3)  TO WS-COUNT-FUNC-TBL                     02181069
           ELSE                                                         02200043
               MOVE +4013      TO WS-ABEND-CODE                         02210043
               DISPLAY '*****************************************'      02220043
               DISPLAY '*** PROGRAM ERROR - DBKUP005          ***'      02230043
               DISPLAY '***                                   ***'      02240043
               DISPLAY '*** BAD DELETE ON DSN_FUNCTION_TABLE  ***'      02250048
               DISPLAY '*** PARAGRAPH 0200-DEL-OBS-PKGS       ***'      02260043
               DISPLAY '*****************************************'      02270043
               PERFORM 9999-SQL-ABEND                                   02280043
           END-IF.                                                      02290043
                                                                        02300043
           EXEC SQL                                                     02310061
            DELETE FROM DSN_STATEMNT_TABLE                              02320061
             WHERE COLLID <> ' '                                        02330061
              AND  PROGNAME NOT IN                                      02340061
              (SELECT NAME                                              02350061
                 FROM SYSIBM.SYSPACKAGE)                                02360061
           END-EXEC.                                                    02370061
                                                                        02380061
           IF SQLCODE = +0 OR SQLCODE = +100                            02390061
               ADD SQLERRD(3)  TO WS-COUNT-STAT-TBL                     02391069
           ELSE                                                         02410061
               MOVE +4013      TO WS-ABEND-CODE                         02420061
               DISPLAY '*****************************************'      02430061
               DISPLAY '*** PROGRAM ERROR - DBKUP005          ***'      02440061
               DISPLAY '***                                   ***'      02450061
               DISPLAY '*** BAD DELETE ON DSN_STATEMNT_TABLE  ***'      02460061
               DISPLAY '*** PARAGRAPH 0200-DEL-OBS-PKGS       ***'      02470061
               DISPLAY '*****************************************'      02480061
               PERFORM 9999-SQL-ABEND                                   02490061
           END-IF.                                                      02500061
                                                                        02510061
           EXEC SQL                                                     02520061
            DELETE FROM PLAN_TABLE                                      02530079
             WHERE COLLID <> ' '                                        02540061
              AND  PROGNAME NOT IN                                      02550061
              (SELECT NAME                                              02560061
                 FROM SYSIBM.SYSPACKAGE)                                02570061
           END-EXEC.                                                    02580061
                                                                        02590061
           IF SQLCODE = +0 OR SQLCODE = +100                            02600061
               ADD SQLERRD(3)  TO WS-COUNT-PLAN-TBL                     02610069
           ELSE                                                         02622061
               MOVE +4013      TO WS-ABEND-CODE                         02630061
               DISPLAY '*****************************************'      02640061
               DISPLAY '*** PROGRAM ERROR - DBKUP005          ***'      02650061
               DISPLAY '***                                   ***'      02660061
               DISPLAY '*** BAD DELETE ON PLAN_TABLE          ***'      02670061
               DISPLAY '*** PARAGRAPH 0200-DEL-OBS-PKGS       ***'      02680061
               DISPLAY '*****************************************'      02690061
               PERFORM 9999-SQL-ABEND                                   02700061
           END-IF.                                                      02710061
                                                                        02720043
      ******************************************************************02730043
      * THIS PARAGRAPH PERFORMS THE FOLLOWING:                          02740043
      * - OPENS THE CURSOR                                              02750043
      * - GETS THE COMMIT FREQUENCY                                     02760043
      ******************************************************************02770043
                                                                        02780043
       1000-INITIALIZATION.                                             02790043
                                                                        02800043
           PERFORM 1100-OPEN-PLANTBL-CSR.                               02810043
                                                                        02820043
           PERFORM 1200-GET-COMMIT-FREQ.                                02830043
                                                                        02840043
      ******************************************************************02850043
      * THIS PARAGRAPH PERFORMS THE FOLLOWING:                          02860043
      * - OPENS THE CURSOR                                              02870043
      ******************************************************************02880043
                                                                        02890043
       1100-OPEN-PLANTBL-CSR.                                           02900043
                                                                        02910043
           EXEC SQL                                                     02920043
              OPEN PLANTBL_CSR                                          02930043
           END-EXEC.                                                    02940043
                                                                        02950043
           IF  SQLCODE NOT = ZERO                                       02960043
               MOVE +4013      TO WS-ABEND-CODE                         02970043
               DISPLAY '*****************************************'      02980043
               DISPLAY '*** PROGRAM ERROR - DBKUP005          ***'      02990043
               DISPLAY '***                                   ***'      03000043
               DISPLAY '*** BAD OPEN OF CURSOR                ***'      03010043
               DISPLAY '*** PARAGRAPH 1100-OPEN-PLANTBL-CSR   ***'      03020043
               DISPLAY '*****************************************'      03030043
               PERFORM 9999-SQL-ABEND                                   03040043
           END-IF.                                                      03050043
                                                                        03060043
      ******************************************************************03070043
      * THIS PARAGRAPH PERFORMS THE FOLLOWING:                          03080043
      * - GETS THE COMMIT FREQUENCY                                     03090043
      ******************************************************************03100043
                                                                        03110043
       1200-GET-COMMIT-FREQ.                                            03120043
                                                                        03130043
           EXEC SQL                                                     03140043
              SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)      03150043
              INTO :WS-COMMIT-TMST                                      03160043
              FROM TCKRSTCNTL                                           03170043
              WHERE JOB_NAME  = 'DBK901'                                03180043
                AND PLAN_NAME = 'DBKUP005'                              03190043
           END-EXEC.                                                    03200043
                                                                        03210043
           IF  SQLCODE NOT = ZERO                                       03220043
               MOVE +4013      TO WS-ABEND-CODE                         03230043
               DISPLAY '*****************************************'      03240043
               DISPLAY '*** PROGRAM ERROR - DBKUP005          ***'      03250043
               DISPLAY '***                                   ***'      03260043
               DISPLAY '*** BAD SELECT FROM TCKRSTCNTL        ***'      03270043
               DISPLAY '*** PARAGRAPH 1200-GET-COMMIT-FREQ    ***'      03280043
               DISPLAY '*****************************************'      03290043
               PERFORM 9999-SQL-ABEND                                   03300043
           END-IF.                                                      03310043
                                                                        03320043
      ******************************************************************03330043
      * THIS PARAGRAPH PERFORMS THE FOLLOWING:                          03340043
      * - READS THRU THE PLANTBL CURSOR DETERMINING WHICH ROWS TO DELETE03350043
      * - FOR EACH PROGRAM. THE THREE MOST RECENT WILL BE KEPT AND      03360043
      * - ALL OTHERS WILL BE DELETED.                                   03370043
      ******************************************************************03400043
                                                                        03410043
       2000-PROCESS-PLANTBL-CURSOR.                                     03420043
                                                                        03430043
           IF PLANTBL-APPLNAME-TEXT EQUAL TO WS-SAVE-APPLNAME           03440063
               IF PLANTBL-PROGNAME-TEXT EQUAL TO WS-SAVE-PROGNAME       03450064
                   IF PLANTBL-COLLID-TEXT EQUAL TO WS-SAVE-COLLID       03460063
                       MOVE PLANTBL-TIMESTAMP TO WS-TIMESTAMP           03470043
                       IF WS-DATE EQUAL TO WS-SAVE-DATE                 03480043
                           CONTINUE                                     03490043
                       ELSE                                             03500043
                           ADD +1  TO WS-NBR-OF-OCCURENCES              03510051
                           IF WS-NBR-OF-OCCURENCES GREATER THAN +3      03520043
                               PERFORM 2300-DELETE-ROWS                 03690081
                           ELSE                                         03710043
                               MOVE PLANTBL-APPLNAME-TEXT               03720063
                                                     TO WS-SAVE-APPLNAME03721063
                               MOVE PLANTBL-PROGNAME-TEXT               03730064
                                                     TO WS-SAVE-PROGNAME03731064
                               MOVE PLANTBL-COLLID-TEXT                 03740063
                                                     TO WS-SAVE-COLLID  03741063
                               MOVE WS-DATE          TO WS-SAVE-DATE    03750043
                       END-IF                                           03760043
                   ELSE                                                 03770043
                       MOVE +1                TO WS-NBR-OF-OCCURENCES   03780043
                       MOVE PLANTBL-APPLNAME-TEXT                       03790063
                                              TO WS-SAVE-APPLNAME       03791063
                       MOVE PLANTBL-PROGNAME-TEXT                       03800064
                                              TO WS-SAVE-PROGNAME       03801064
                       MOVE PLANTBL-COLLID-TEXT TO WS-SAVE-COLLID       03810063
                       MOVE WS-DATE           TO WS-SAVE-DATE           03820043
                   END-IF                                               03830043
               ELSE                                                     03840043
                   PERFORM 2400-CHECK-COMMIT-TIME                       03850049
                   MOVE +1                TO WS-NBR-OF-OCCURENCES       03860043
                   MOVE PLANTBL-APPLNAME-TEXT                           03870063
                                          TO WS-SAVE-APPLNAME           03871063
                   MOVE PLANTBL-PROGNAME-TEXT                           03880064
                                          TO WS-SAVE-PROGNAME           03881064
                   MOVE PLANTBL-COLLID-TEXT                             03890063
                                          TO WS-SAVE-COLLID             03891063
                   MOVE PLANTBL-TIMESTAMP TO WS-TIMESTAMP               03900043
                   MOVE WS-DATE           TO WS-SAVE-DATE               03910043
               END-IF                                                   03920043
           ELSE                                                         03930043
               MOVE +1                TO WS-NBR-OF-OCCURENCES           03940043
               MOVE PLANTBL-APPLNAME-TEXT TO WS-SAVE-APPLNAME           03950063
               MOVE PLANTBL-PROGNAME-TEXT                               03960064
                                      TO WS-SAVE-PROGNAME               03961064
               MOVE PLANTBL-COLLID-TEXT TO WS-SAVE-COLLID               03970063
               MOVE PLANTBL-TIMESTAMP TO WS-TIMESTAMP                   03980043
               MOVE WS-DATE           TO WS-SAVE-DATE                   03990043
           END-IF.                                                      04000043
                                                                        04010043
           PERFORM 2100-FETCH-PLANTBL-CSR.                              04020043
                                                                        04030043
      ******************************************************************04040043
      * THIS PARAGRAPH PERFORMS THE FOLLOWING:                          04050043
      * - FETCHS A ROW OF THE PLANTBL CURSOR                            04060043
      ******************************************************************04070043
                                                                        04080043
       2100-FETCH-PLANTBL-CSR.                                          04090043
                                                                        04100060
           MOVE SPACES TO PLANTBL-APPLNAME-TEXT                         04101067
           MOVE SPACES TO PLANTBL-PROGNAME-TEXT                         04102067
           MOVE SPACES TO PLANTBL-COLLID-TEXT.                          04103067
                                                                        04104066
           EXEC SQL                                                     04110043
               FETCH  PLANTBL_CSR                                       04120043
                INTO  :PLANTBL-APPLNAME                                 04130043
                   ,  :PLANTBL-PROGNAME                                 04140043
                   ,  :PLANTBL-TIMESTAMP                                04150043
                   ,  :PLANTBL-BIND-TIME                                04160043
                   ,  :PLANTBL-QUERYNO                                  04180043
                   ,  :PLANTBL-COLLID                                   04190043
           END-EXEC.                                                    04200043
                                                                        04210043
           IF SQLCODE = +0                                              04220043
               NEXT SENTENCE                                            04230043
           ELSE                                                         04240043
           IF SQLCODE = +100                                            04250043
               MOVE 'Y' TO WS-CURSOR-EOF1-SW                            04260043
           ELSE                                                         04270043
               MOVE +4013      TO WS-ABEND-CODE                         04280043
               DISPLAY '*****************************************'      04290043
               DISPLAY '*** PROGRAM ERROR - DBKUP005          ***'      04300043
               DISPLAY '***                                   ***'      04310043
               DISPLAY '*** BAD FETCH OF PLANTBL CURSOR       ***'      04320043
               DISPLAY '*****************************************'      04330043
               PERFORM 9999-SQL-ABEND                                   04340043
           END-IF                                                       04350043
           END-IF.                                                      04360043
                                                                        04370043
      ******************************************************************05520043
      * THIS PARAGRAPH PERFORMS THE FOLLOWING:                          05530043
      * - DELETES THE ROWS FOR THAT DATE/PROGRAM                        05540043
      ******************************************************************05550043
                                                                        05560043
       2300-DELETE-ROWS.                                                05570043
                                                                        05580043
           EXEC SQL                                                     05590043
               DELETE                                                   05600043
               FROM  PLAN_TABLE                                         05610079
               WHERE APPLNAME        = :PLANTBL-APPLNAME                05620059
                 AND PROGNAME        = :PLANTBL-PROGNAME                05630059
                 AND TIMESTAMP       = :PLANTBL-TIMESTAMP               05640059
                 AND COLLID          = :PLANTBL-COLLID                  05650059
           END-EXEC.                                                    05660043
                                                                        05670043
           IF SQLCODE = +0                                              05680043
               ADD SQLERRD(3)  TO WS-COUNT-PLAN-TBL                     05681069
               PERFORM 2350-DELETE-STMT-TBL                             05690043
               PERFORM 2360-DELETE-FNCTN-TBL                            05700043
           ELSE                                                         05710043
               IF SQLCODE = +100                                        05720059
                   NEXT SENTENCE                                        05730059
               ELSE                                                     05740059
                   MOVE +4013      TO WS-ABEND-CODE                     05750059
                   DISPLAY '*****************************************'  05760059
                   DISPLAY '*** PROGRAM ERROR - DBKUP005          ***'  05770059
                   DISPLAY '***                                   ***'  05780059
                   DISPLAY '*** BAD DELETE ON PLAN_TABLE          ***'  05790059
                   DISPLAY '*****************************************'  05800059
                   PERFORM 9999-SQL-ABEND                               05810059
               END-IF                                                   05820059
           END-IF.                                                      05830043
                                                                        05840043
      ******************************************************************05850043
      * THIS PARAGRAPH PERFORMS THE FOLLOWING:                          05860043
      * - DELETES THE ROWS FOR THAT DATE/PROGRAM OFF DSN_STATEMNT_TABLE 05870043
      ******************************************************************05880043
                                                                        05890043
       2350-DELETE-STMT-TBL.                                            05900043
                                                                        05910043
           EXEC SQL                                                     05920043
               DELETE                                                   05930043
                 FROM DSN_STATEMNT_TABLE                                05940043
                   WHERE APPLNAME        = :PLANTBL-APPLNAME            05950043
                     AND PROGNAME        = :PLANTBL-PROGNAME            05960043
                     AND EXPLAIN_TIME    = :PLANTBL-BIND-TIME           05970043
                     AND COLLID          = :PLANTBL-COLLID              05980043
           END-EXEC.                                                    05990043
                                                                        06000043
           IF SQLCODE = +0 OR SQLCODE = +100                            06010043
               ADD SQLERRD(3)  TO WS-COUNT-STAT-TBL                     06011069
           ELSE                                                         06030043
               MOVE +4013      TO WS-ABEND-CODE                         06040043
               DISPLAY '*****************************************'      06050043
               DISPLAY '*** PROGRAM ERROR - DBKUP005          ***'      06060043
               DISPLAY '***                                   ***'      06070043
               DISPLAY '*** BAD DELETE ON DSN_STATEMNT_TABLE  ***'      06080043
               DISPLAY '*****************************************'      06090043
               PERFORM 9999-SQL-ABEND                                   06100043
           END-IF.                                                      06110043
                                                                        06120043
      ******************************************************************06130043
      * THIS PARAGRAPH PERFORMS THE FOLLOWING:                          06140043
      * - DELETES THE ROWS FOR THAT DATE/PROGRAM OFF DSN_FUNCTION_TABLE 06150043
      ******************************************************************06160043
                                                                        06170043
       2360-DELETE-FNCTN-TBL.                                           06180043
                                                                        06190043
           EXEC SQL                                                     06200043
               DELETE                                                   06210043
                 FROM DSN_FUNCTION_TABLE                                06220043
                   WHERE APPLNAME        = :PLANTBL-APPLNAME            06230043
                     AND PROGNAME        = :PLANTBL-PROGNAME            06240043
                     AND EXPLAIN_TIME    = :PLANTBL-BIND-TIME           06250043
                     AND COLLID          = :PLANTBL-COLLID              06260043
           END-EXEC.                                                    06270043
                                                                        06280043
           IF SQLCODE = +0 OR SQLCODE = +100                            06290043
               ADD SQLERRD(3)  TO WS-COUNT-FUNC-TBL                     06291069
           ELSE                                                         06310043
               MOVE +4013      TO WS-ABEND-CODE                         06320043
               DISPLAY '*****************************************'      06330043
               DISPLAY '*** PROGRAM ERROR - DBKUP005          ***'      06340043
               DISPLAY '***                                   ***'      06350043
               DISPLAY '*** BAD DELETE ON DSN_FUNCTION_TABLE  ***'      06360043
               DISPLAY '*****************************************'      06370043
               PERFORM 9999-SQL-ABEND                                   06380043
           END-IF.                                                      06390043
                                                                        06400043
      ******************************************************************06410043
      * THIS PARAGRAPH PERFORMS THE FOLLOWING:                          06420043
      * - CHECKS THE TIME TO SEE IF IT IS TIME TO TAKE A COMMIT         06430043
      ******************************************************************06440043
                                                                        06450043
       2400-CHECK-COMMIT-TIME.                                          06460043
                                                                        06470043
           EXEC SQL                                                     06480043
               SET :WS-CURRENT-TMST = CURRENT TIMESTAMP                 06490043
           END-EXEC.                                                    06500043
                                                                        06510043
           IF SQLCODE = +0                                              06520043
               IF WS-CURRENT-TMST GREATER THAN WS-COMMIT-TMST           06530043
                   PERFORM 2410-TAKE-COMMIT                             06540043
                   PERFORM 1200-GET-COMMIT-FREQ                         06550046
               ELSE                                                     06560043
                   CONTINUE                                             06570043
               END-IF                                                   06580043
           ELSE                                                         06590043
               MOVE +4013      TO WS-ABEND-CODE                         06600043
               DISPLAY '*****************************************'      06610043
               DISPLAY '*** PROGRAM ERROR - DBKUP005          ***'      06620043
               DISPLAY '***                                   ***'      06630043
               DISPLAY '*** BAD STATUS ON SET                 ***'      06640043
               DISPLAY '*****************************************'      06650043
               PERFORM 9999-SQL-ABEND                                   06660043
           END-IF.                                                      06670043
                                                                        06680043
      ******************************************************************06690043
      * THIS PARAGRAPH PERFORMS THE FOLLOWING:                          06700043
      * - TAKES THE COMMIT                                              06710043
      ******************************************************************06720043
                                                                        06730043
       2410-TAKE-COMMIT.                                                06740043
                                                                        06750043
           EXEC SQL                                                     06760043
               COMMIT                                                   06770043
           END-EXEC.                                                    06780043
                                                                        06790043
           IF  SQLCODE EQUAL +0                                         06800043
               DISPLAY 'COMMIT TAKEN AT ' WS-CURRENT-TMST               06810043
           ELSE                                                         06820043
               MOVE +4013      TO WS-ABEND-CODE                         06830043
               DISPLAY '*****************************************'      06840043
               DISPLAY '*** PROGRAM ERROR - DBKUP005          ***'      06850043
               DISPLAY '***                                   ***'      06860043
               DISPLAY '*** BAD COMMIT                        ***'      06870043
               DISPLAY '*****************************************'      06880043
               PERFORM 9999-SQL-ABEND                                   06890043
           END-IF.                                                      06900043
                                                                        06910043
      ************************************************************      06920043
      * THIS PARAGRAPH PERFORMS THE FOLLOWING:                   *      06930043
      * - DB2 ABEND PARAGRAPH                                    *      06940043
      ************************************************************      06950043
                                                                        06960043
       9999-SQL-ABEND.                                                  06970043
                                                                        06980043
           CALL 'DSNTIAR' USING SQLCA                                   06990043
                                DP004-FORMATTED-MESSAGE                 07000043
                                DP004-ERROR-LINE-LENGTH.                07010043
                                                                        07020043
           DISPLAY DP004-ASTERISK-LINE.                                 07030043
                                                                        07040043
           PERFORM                                                      07050043
               VARYING DP004-MSG-IDX                                    07060043
               FROM 1 BY 1                                              07070043
               UNTIL DP004-MSG-IDX > DP004-MAX-MESSAGE-LINES            07080043
                   DISPLAY '**'                                         07090043
                           DP004-MESSAGE-LINE (DP004-MSG-IDX)           07100043
                           ' **'                                        07110043
           END-PERFORM.                                                 07120043
                                                                        07130043
           DISPLAY DP004-ASTERISK-LINE.                                 07140043
                                                                        07150043
           CALL 'ILBOABN0' USING WS-ABEND-CODE.                         07160043
                                                                        07170043
