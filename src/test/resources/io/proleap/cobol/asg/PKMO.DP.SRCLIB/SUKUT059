      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    SUKUT059.                                                 
       AUTHOR.        SUDHAN VEERASINGH.                                        
       INSTALLATION.  KOHLS.                                                    
       DATE-WRITTEN   SEPTEMBER, 2011.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  THIS PROGRAM CREATES AN EXTRACT FILE OF DATA FROM THE MASTER           
      *  CONTAINER TABLE (SUT_MSTR_SHIPT_UNT & SUT_MSTR_SHIPT_CNT).             
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT LBL-PRT-DATA-EXTRACT                                          
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  LBL-PRT-DATA-EXTRACT                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  MSR-CTN-DATA-RECORD        PIC  X(030).                              
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *-----------------------------------------------------------------        
      *  DB2 FORMATTED MESSAGE AREA                                             
      *-----------------------------------------------------------------        
           COPY DPWS004.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  COPY BOOK FOR OUTPUT FILE                                              
      *-----------------------------------------------------------------        
           COPY SUWS074.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  COMMUNICATIONS AREA FOR DB2                                            
      *-----------------------------------------------------------------        
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      *-----------------------------------------------------------------        
      *  COBOL DECLARATION AND DCLGEN MEMBER FOR THE MASTER CONTAINER           
      *  TABLE                                                                  
      *-----------------------------------------------------------------        
           EXEC SQL                                                             
                INCLUDE SUT00098                                                
           END-EXEC.                                                            
                                                                                
      *-----------------------------------------------------------------        
      *  COBOL DECLARATION AND DCLGEN MEMBER FOR THE CHILDREN CONTAINER         
      *  TABLE                                                                  
      *-----------------------------------------------------------------        
           EXEC SQL                                                             
                INCLUDE SUT00099                                                
           END-EXEC.                                                            
                                                                                
      *-----------------------------------------------------------------        
      *  COBOL DECLARATION AND DCLGEN MEMBER FOR THE PARM TABLE                 
      *-----------------------------------------------------------------        
           EXEC SQL                                                             
                INCLUDE TKRPRPM                                                 
           END-EXEC.                                                            
      /                                                                         
      *-----------------------------------------------------------------        
      *                                                                         
      *-----------------------------------------------------------------        
       01  ABEND-CODE                    PIC S9(04) VALUE +0                    
                                                    COMP SYNC.                  
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EOF-MASTER-DATA-CSR-SW     PIC  X(01) VALUE 'N'.              
               88  PS-EOF-MASTER-DATA-CSR               VALUE 'Y'.              
               88  PS-MORE-MASTER-DATA-CSR              VALUE 'N'.              
           05  PS-EOF-CHILD-MSTR-DATA-CSR-SW PIC  X(01) VALUE 'N'.              
               88  PS-EOF-CHILD-MSTR-DATA-CSR           VALUE 'Y'.              
               88  PS-MORE-CHILD-MSTR-DATA-CSR          VALUE 'N'.              
           05  PS-EOF-CHILD-DATA-CSR-SW      PIC  X(01) VALUE 'N'.              
               88  PS-EOF-CHILD-DATA-CSR                VALUE 'Y'.              
               88  PS-MORE-CHILD-DATA-CSR               VALUE 'N'.              
                                                                                
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME   PIC  X(08) VALUE 'SUKUT059'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DISPLAY-PURGE-WEEKS      PIC  Z(09)9.                         
           05  PV-PURGE-DATE               PIC  X(10).                          
           05  PV-DISPLAY-CARTON           PIC ZZZZZZZZZZZZZZZZZ9.              
           05  PV-SHIPT-UNT-ID             PIC  S9(18)V COMP-3 VALUE +0.        
           05  PV-SHIPT-UNT-ID-1           PIC  S9(18)V COMP-3 VALUE +0.        
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
           05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
           05  PV-CART-FOUND-CNT           PIC S9(09)    VALUE ZERO.            
           05  PV-DISPLAY-COUNT            PIC S9(09)    VALUE ZERO             
                                                         COMP SYNC.             
           05  PV-WRITE-COUNTER            PIC S9(09)    VALUE ZERO             
                                                         COMP SYNC.             
           05  PV-PURGE-TIMESTAMP.                                              
               10  PV-PT-DATE              PIC  X(10).                          
               10  PV-PT-TIME              PIC  X(16)    VALUE                  
                   '-00.00.00.000000'.                                          
                                                                                
       01  DK-DB-KEYS.                                                          
           05  DK-PURGE-WEEKS              PIC S9(09)  COMP.                    
           05  DK-OPER-UNT-ID              PIC S9(04)  COMP.                    
           05  DK-SHIPT-UNT-ID             PIC S9(18)V COMP-3.                  
           05  DK-TIMESTAMP                PIC X(26).                           
                                                                                
      *-----------------------------------------------------------------        
      *  MASTER CARTON DATA TO BE DELETED CURSOR                                
      *-----------------------------------------------------------------        
           EXEC SQL                                                             
                DECLARE MASTER_DATA_CURSOR CURSOR FOR                           
                 SELECT DISTINCT                                                
                        MSTR_SHIPT_UNT_ID                                       
                   FROM SUT_MSTR_SHIPT_UNT                                      
                  WHERE CRE_TMST  < :DK-TIMESTAMP                               
                  ORDER BY MSTR_SHIPT_UNT_ID                                    
           END-EXEC.                                                            
                                                                                
      *-----------------------------------------------------------------        
      *  CHILD MASTER CARTON DATA TO BE DELETED CURSOR                          
      *-----------------------------------------------------------------        
           EXEC SQL                                                             
                DECLARE CHILD_MSTR_CSR CURSOR FOR                               
                 SELECT DISTINCT                                                
                        B.CHLD_MSTR_SHIPT_ID                                    
                       ,B.CRE_TMST                                              
                   FROM SUT_MSTR_SHIPT_UNT A                                    
                       ,SUT_MSTR_SHIPT_CNT B                                    
                 WHERE A.MSTR_SHIPT_UNT_ID = B.MSTR_SHIPT_UNT_ID                
                   AND B.MSTR_SHIPT_UNT_ID = :SUT00098-MSTR-SHIPT-UNT-ID        
                   AND CHLD_MSTR_SHIPT_ID  IS NOT NULL                          
                   AND B.CRE_TMST  > :DK-TIMESTAMP                              
                  ORDER BY B.CHLD_MSTR_SHIPT_ID                                 
           END-EXEC.                                                            
      *-----------------------------------------------------------------        
      *  CHILD MASTER CARTON DATA TO BE DELETED CURSOR                          
      *-----------------------------------------------------------------        
           EXEC SQL                                                             
                DECLARE CHILD_CSR CURSOR FOR                                    
                 SELECT DISTINCT                                                
                        B.CHLD_SHIPT_UNT_ID                                     
                       ,B.CRE_TMST                                              
                   FROM SUT_MSTR_SHIPT_UNT A                                    
                       ,SUT_MSTR_SHIPT_CNT B                                    
                 WHERE A.MSTR_SHIPT_UNT_ID = B.MSTR_SHIPT_UNT_ID                
                   AND B.MSTR_SHIPT_UNT_ID = :SUT00098-MSTR-SHIPT-UNT-ID        
                   AND CHLD_SHIPT_UNT_ID  IS NOT NULL                           
                   AND B.CRE_TMST  > :DK-TIMESTAMP                              
                  ORDER BY B.CHLD_SHIPT_UNT_ID                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *-----------------------------------------------------------------        
      * 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
      *      PROGRAM.                                                           
      *-----------------------------------------------------------------        
       0000-PROGRAM-MAINLINE.                                                   
                                                                                
           PERFORM 1000-INITIALIZE.                                             
           IF  PS-MORE-MASTER-DATA-CSR                                          
               PERFORM 2000-PROCESS-MSTR-CART-DATA                              
                 UNTIL PS-EOF-MASTER-DATA-CSR                                   
           END-IF.                                                              
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      *-----------------------------------------------------------------        
      *  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
      *    -- OPEN OUTPUT FILE                                                  
      *    -- OPEN CURSOR                                                       
      *    -- FETCH CURSOR                                                      
      *-----------------------------------------------------------------        
       1000-INITIALIZE.                                                         
                                                                                
           OPEN OUTPUT LBL-PRT-DATA-EXTRACT.                                    
                                                                                
           PERFORM 1100-GET-PURGE-WEEKS.                                        
                                                                                
           DISPLAY '** SUKUT059 EXECUTION STARTED   **'.                        
           DISPLAY '** PURGE BEFORE' PV-DISPLAY-PURGE-WEEKS ' WEEKS **'.        
           DISPLAY '** PURGE BEFORE DATE ' PV-PURGE-DATE ' **'.                 
                                                                                
           MOVE PV-PURGE-DATE       TO  PV-PT-DATE.                             
           MOVE PV-PURGE-TIMESTAMP  TO  DK-TIMESTAMP.                           
                                                                                
                                                                                
           PERFORM 7000-OPEN-MASTER-DATA-CURSOR.                                
           PERFORM 7100-FETCH-MASTER-DATA-CURSOR.                               
                                                                                
       1100-GET-PURGE-WEEKS.                                                    
                                                                                
           EXEC SQL                                                             
               SELECT  FUNC_QTY                                                 
                 INTO :DK-PURGE-WEEKS                                           
                 FROM TKRPRPM                                                   
                WHERE LOC_ID            =  90                                   
                  AND INTFC_SYS_CDE     = 'KHL'                                 
                  AND SYS_PRC_FUNC_CDE  = 'OTPURG'                              
                  AND FUNC_PRC_STAT_CDE = 'AC'                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROS                                             
                    MOVE  DK-PURGE-WEEKS  TO  PV-DISPLAY-PURGE-WEEKS            
                    PERFORM 1110-GET-PURGE-DATE                                 
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '1100-GET-PURGE-WEEKS'                                  
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE 'GET NUMBER OF WEEKS TO PURGE   '                       
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       1110-GET-PURGE-DATE.                                                     
                                                                                
           EXEC SQL                                                             
               SELECT  (CURRENT_DATE -                                          
                        (:DK-PURGE-WEEKS * 7) DAYS)                             
                 INTO :PV-PURGE-DATE                                            
                 FROM TKRPRPM                                                   
                WHERE LOC_ID            =  90                                   
                  AND INTFC_SYS_CDE     = 'KHL'                                 
                  AND SYS_PRC_FUNC_CDE  = 'OTPURG'                              
                  AND FUNC_PRC_STAT_CDE = 'AC'                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROS                                             
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '1110-GET-PURGE-DATE '                                  
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE 'CALCULATE PURGE DATE           '                       
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  LOOP THROUGH THE RESULTS TABLE.                                        
      *  FORMAT THE OUTPUT RECORD.  WRITE THE RECORDS THAT MEET CRITERIA        
      *-----------------------------------------------------------------        
       2000-PROCESS-MSTR-CART-DATA.                                             
           IF PV-CART-FOUND-CNT > 0                                             
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 8000-WRITE-MASTER-DATA-RECORD                             
           END-IF                                                               
           MOVE ZEROS TO PV-CART-FOUND-CNT                                      
           PERFORM 7100-FETCH-MASTER-DATA-CURSOR.                               
                                                                                
      *-----------------------------------------------------------------        
      *  OPEN THE CURSOR.                                                       
      *-----------------------------------------------------------------        
       7000-OPEN-MASTER-DATA-CURSOR.                                            
                                                                                
           EXEC SQL                                                             
                OPEN MASTER_DATA_CURSOR                                         
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '7000-OPEN-MASTER-DATA-CURSOR    '                      
                                       TO PV-PARAGRAPH-NAME                     
                   MOVE 'OPEN THE MASTER_DATA_CURSOR     '                      
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *-----------------------------------------------------------------        
      *  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.                 
      *-----------------------------------------------------------------        
       7100-FETCH-MASTER-DATA-CURSOR.                                           
                                                                                
           EXEC SQL                                                             
                FETCH MASTER_DATA_CURSOR                                        
                 INTO :SUT00098-MSTR-SHIPT-UNT-ID                               
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    DISPLAY ' MASTER : ' SUT00098-MSTR-SHIPT-UNT-ID             
                    PERFORM 7110-OPEN-CHILD-MASTER-CSR                          
                    INITIALIZE PV-SHIPT-UNT-ID                                  
                    SET PS-MORE-CHILD-MSTR-DATA-CSR TO TRUE                     
                    PERFORM 7120-FETCH-CHILD-MASTER-CSR                         
                      UNTIL PS-EOF-CHILD-MSTR-DATA-CSR                          
                    PERFORM 7130-CLOSE-CHILD-MASTER-CSR                         
                                                                                
                    PERFORM 7210-OPEN-CHILD-CSR                                 
                    INITIALIZE PV-SHIPT-UNT-ID-1                                
                    SET PS-MORE-CHILD-DATA-CSR TO TRUE                          
                    PERFORM 7220-FETCH-CHILD-CSR                                
                      UNTIL PS-EOF-CHILD-DATA-CSR                               
                    PERFORM 7230-CLOSE-CHILD-CSR                                
                                                                                
               WHEN SQLCODE = +100                                              
                   SET PS-EOF-MASTER-DATA-CSR TO  TRUE                          
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '7100-FETCH-MASTER-DATA-CURSOR  '                       
                                       TO PV-PARAGRAPH-NAME                     
                   MOVE 'FETCH THE MASTER_DATA_CURSOR   '                       
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  OPEN THE CURSOR.                                                       
      *-----------------------------------------------------------------        
       7110-OPEN-CHILD-MASTER-CSR.                                              
                                                                                
           EXEC SQL                                                             
                OPEN CHILD_MSTR_CSR                                             
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '7110-OPEN-CHILD-MASTER-CSR    '                        
                                       TO PV-PARAGRAPH-NAME                     
                   MOVE 'OPEN THE CHILD_MSTR_CSR         '                      
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.                 
      *-----------------------------------------------------------------        
       7120-FETCH-CHILD-MASTER-CSR.                                             
                                                                                
           EXEC SQL                                                             
                FETCH CHILD_MSTR_CSR                                            
                 INTO :SUT00099-CHLD-MSTR-SHIPT-ID                              
                     ,:SUT00099-CRE-TMST                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    ADD +1 TO PV-CART-FOUND-CNT                                 
                    MOVE SUT00099-CHLD-MSTR-SHIPT-ID TO                         
                         PV-SHIPT-UNT-ID                                        
                    DISPLAY 'CARTON CHILD MASTER : '                            
                            SUT00099-CHLD-MSTR-SHIPT-ID                         
               WHEN SQLCODE = +100                                              
                   SET PS-EOF-CHILD-MSTR-DATA-CSR TO TRUE                       
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '7120-FETCH-CHILD-MASTER-CSR    '                       
                                       TO PV-PARAGRAPH-NAME                     
                   MOVE 'FETCH THE CHILD_MSTR_CSR       '                       
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  CLOSE THE CHLID MASTER CURSOR.                                         
      *-----------------------------------------------------------------        
       7130-CLOSE-CHILD-MASTER-CSR.                                             
                                                                                
           EXEC SQL                                                             
                CLOSE CHILD_MSTR_CSR                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                    MOVE '7130-CLOSE-CHILD-MASTER-CSR    '                      
                                       TO PV-PARAGRAPH-NAME                     
                    MOVE 'CLOSE THE CHILD_MSTR_CSR         '                    
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                    PERFORM 9100-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  OPEN THE CURSOR.                                                       
      *-----------------------------------------------------------------        
       7210-OPEN-CHILD-CSR.                                                     
                                                                                
           EXEC SQL                                                             
                OPEN CHILD_CSR                                                  
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '7210-OPEN-CHILD-CSR    '                               
                                       TO PV-PARAGRAPH-NAME                     
                   MOVE 'OPEN THE CHILD_CSR         '                           
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.                 
      *-----------------------------------------------------------------        
       7220-FETCH-CHILD-CSR.                                                    
                                                                                
           EXEC SQL                                                             
                FETCH CHILD_CSR                                                 
                 INTO :SUT00099-CHLD-SHIPT-UNT-ID                               
                     ,:SUT00099-CRE-TMST                                        
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    ADD +1 TO PV-CART-FOUND-CNT                                 
                    MOVE SUT00099-CHLD-SHIPT-UNT-ID TO                          
                         PV-SHIPT-UNT-ID-1                                      
                    DISPLAY 'CARTON CHILD : '                                   
                            SUT00099-CHLD-SHIPT-UNT-ID                          
               WHEN SQLCODE = +100                                              
                   SET PS-EOF-CHILD-DATA-CSR TO TRUE                            
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '7220-FETCH-CHILD-CSR    '                              
                                       TO PV-PARAGRAPH-NAME                     
                   MOVE 'FETCH THE CHILD_CSR       '                            
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                   PERFORM 9100-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  CLOSE THE CHLID MASTER CURSOR.                                         
      *-----------------------------------------------------------------        
       7230-CLOSE-CHILD-CSR.                                                    
                                                                                
           EXEC SQL                                                             
                CLOSE CHILD_CSR                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                    MOVE '7230-CLOSE-CHILD-CSR    '                             
                                       TO PV-PARAGRAPH-NAME                     
                    MOVE 'CLOSE THE CHILD_CSR         '                         
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                    PERFORM 9100-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  WRITE THE SHIPMENT-UNIT-RECORD                                         
      *-----------------------------------------------------------------        
       8000-WRITE-MASTER-DATA-RECORD.                                           
                                                                                
           INITIALIZE MSR-CTN-DATA-RECORD.                                      
           MOVE SUT00098-MSTR-SHIPT-UNT-ID TO SU074-MST-CNTR-ID.                
                                                                                
           WRITE MSR-CTN-DATA-RECORD FROM SU074-MSR-CTN-DATA-RECORD             
           ADD +1 TO PV-WRITE-COUNTER.                                          
                                                                                
                                                                                
      *-----------------------------------------------------------------        
      *  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
      *-----------------------------------------------------------------        
       9000-EOJ-ROUTINE.                                                        
           CLOSE LBL-PRT-DATA-EXTRACT.                                          
                                                                                
           MOVE PV-WRITE-COUNTER TO PV-DISPLAY-COUNT.                           
           DISPLAY 'TOTAL MASTER BATCH RECORDS WRITTEN '                        
                    PV-DISPLAY-COUNT.                                           
           EXEC SQL                                                             
                CLOSE MASTER_DATA_CURSOR                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                    MOVE '9000-EOJ-ROUTINE               '                      
                                       TO PV-PARAGRAPH-NAME                     
                    MOVE 'CLOSE THE MASTER_DATA_CURSOR     '                    
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                    PERFORM 9100-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *-----------------------------------------------------------------        
      *  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
      *  ERROR OR WARNING CODE OCCURS.                                          
      *-----------------------------------------------------------------        
       9100-SQL-ABEND.                                                          
           DISPLAY '9100-SQL-ABEND'.                                            
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013                  TO ABEND-CODE.                           
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
      /                                                                         
