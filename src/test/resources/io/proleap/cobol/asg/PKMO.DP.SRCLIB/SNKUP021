00001  ID DIVISION.                                                     06/29/93
00002                                                                   SNKUP021
00003  PROGRAM-ID.    SNKUP021.                                            LV003
00004                                                                   SNKUP021
00005  AUTHOR.        ANNE YEKO.                                        SNKUP021
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          SNKUP021
00007  DATE-WRITTEN.  NOVEMBER, 1992.                                   SNKUP021
00008  DATE-COMPILED.                                                   SNKUP021
00009                                                                   SNKUP021
00010 ******************************************************************SNKUP021
00011 *   THIS PROGRAM WILL READ AN INPUT FILE CREATED IN EITHER      **SNKUP021
00012 *   SNKUP007 OR SNKUP008.  THE KEY INFORMATION FROM THE INPUT   **SNKUP021
00013 *   FILE IS USED TO DELETE ROWS FROM THE ASNHDR TABLE.          **SNKUP021
00014 ******************************************************************SNKUP021
00015 * CHANGED __/__/__ BY ____________                              **SNKUP021
00016 *   P_________  ________________________________________________**SNKUP021
00017 *               ________________________________________________**SNKUP021
00018 *                                                               **SNKUP021
00019 ******************************************************************SNKUP021
00020                                                                   SNKUP021
00021      EJECT                                                        SNKUP021
00022                                                                   SNKUP021
00023  ENVIRONMENT DIVISION.                                            SNKUP021
00024                                                                   SNKUP021
00025  INPUT-OUTPUT SECTION.                                            SNKUP021
00026                                                                   SNKUP021
00027  FILE-CONTROL.                                                    SNKUP021
00028                                                                   SNKUP021
00029      SELECT ASNHDR-DELETE-FILE                                    SNKUP021
00030             ASSIGN TO INP01.                                      SNKUP021
00031                                                                   SNKUP021
00032  DATA DIVISION.                                                   SNKUP021
00033                                                                   SNKUP021
00034  FILE SECTION.                                                    SNKUP021
00035                                                                   SNKUP021
00036  FD  ASNHDR-DELETE-FILE                                           SNKUP021
00037      RECORDING MODE IS F                                          SNKUP021
00038      LABEL RECORDS ARE STANDARD                                   SNKUP021
00039      RECORD CONTAINS 05 CHARACTERS                                   CL**3
00040      BLOCK CONTAINS 0 RECORDS.                                    SNKUP021
00041                                                                   SNKUP021
00042  01  ASNHDR-DELETE-RECORD        PIC X(05).                          CL**3
00043                                                                   SNKUP021
00044      EJECT                                                        SNKUP021
00045  WORKING-STORAGE SECTION.                                         SNKUP021
00046                                                                   SNKUP021
00047  01  FILLER.                                                      SNKUP021
00048      05  FILLER                  PIC  X(36)  VALUE                SNKUP021
00049          ' ***** WORKING STORAGE PGM=SNKUP021 '.                  SNKUP021
00050      05  PGM-COMPILED            PIC  X(20)  VALUE SPACE.         SNKUP021
00051      05  FILLER                  PIC  X(10)  VALUE                SNKUP021
00052          ' LOCATION='.                                            SNKUP021
00053      05  PGM-LOCATION            PIC  X(04)  VALUE SPACE.         SNKUP021
00054      05  FILLER                  PIC  X(06)  VALUE                SNKUP021
00055          ' *****'.                                                SNKUP021
00056                                                                   SNKUP021
00057  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO           SNKUP021
00058                                              COMP SYNC.           SNKUP021
00059                                                                   SNKUP021
00060  01  ABEND-PARAGRAPH             PIC  X(30)  VALUE SPACE.         SNKUP021
00061                                                                   SNKUP021
00062      EJECT                                                        SNKUP021
00063  01  PC-PROGRAM-CONSTANTS.                                        SNKUP021
00064      05  PC-Y                    PIC  X(01)  VALUE 'Y'.           SNKUP021
00065      05  PC-N                    PIC  X(01)  VALUE 'N'.           SNKUP021
00066                                                                   SNKUP021
00067  01  PS-PROGRAM-SWITCHES.                                         SNKUP021
00068      05  PS-END-OF-INPUT-SW      PIC  X(01)  VALUE 'N'.           SNKUP021
00069          88 PS-END-OF-INPUT                  VALUE 'Y'.           SNKUP021
00070                                                                   SNKUP021
00071  01  PS-PROGRAM-VARIABLES.                                        SNKUP021
00072      05  PV-RECS-READ            PIC  S9(7)  VALUE +0             SNKUP021
00073                                              COMP-3.              SNKUP021
00074      05  PV-RECS-DELETED         PIC  S9(7)  VALUE +0             SNKUP021
00075                                              COMP-3.              SNKUP021
00076      05  PV-DELETE-COUNTER       PIC  S9(3)  VALUE +0             SNKUP021
00077                                              COMP-3.              SNKUP021
00078                                                                   SNKUP021
00079 *    ASNHDR DELETE RECORD LAYOUT                                  SNKUP021
00080                                                                   SNKUP021
00081      COPY SNRD002I.                                               SNKUP021
00082      EJECT                                                        SNKUP021
00083 ******************************************************************SNKUP021
00084 *    DB2 DATABASE AREA                                            SNKUP021
00085 ******************************************************************SNKUP021
00086                                                                   SNKUP021
00087 *    ASN HEADER                                                   SNKUP021
00088                                                                   SNKUP021
00089      EXEC SQL                                                     SNKUP021
00090          INCLUDE TASNHDR                                          SNKUP021
00091      END-EXEC.                                                    SNKUP021
00092                                                                   SNKUP021
00093      EJECT                                                        SNKUP021
00094 *    SQL COMMUNICATION AREA                                       SNKUP021
00095                                                                   SNKUP021
00096      EXEC SQL                                                     SNKUP021
00097          INCLUDE SQLCA                                            SNKUP021
00098      END-EXEC.                                                    SNKUP021
00099      EJECT                                                        SNKUP021
00100 *    SQL COMMUNICATION AREA 2                                     SNKUP021
00101                                                                   SNKUP021
00102      COPY SQLCA2.                                                 SNKUP021
00103      EJECT                                                        SNKUP021
00104                                                                   SNKUP021
00105 ***************************************************************** SNKUP021
00106  PROCEDURE DIVISION.                                              SNKUP021
00107 ***************************************************************** SNKUP021
00108                                                                   SNKUP021
00109  A000-MAIN-DRIVER.                                                SNKUP021
00110                                                                   SNKUP021
00111      MOVE 'A000'                 TO PGM-LOCATION.                 SNKUP021
00112      MOVE WHEN-COMPILED          TO PGM-COMPILED.                 SNKUP021
00113                                                                   SNKUP021
00114      OPEN INPUT ASNHDR-DELETE-FILE.                               SNKUP021
00115                                                                   SNKUP021
00116      PERFORM B200-READ-INPUT-FILE.                                SNKUP021
00117                                                                   SNKUP021
00118      PERFORM B100-DELETE-ROWS                                     SNKUP021
00119        UNTIL PS-END-OF-INPUT.                                     SNKUP021
00120                                                                   SNKUP021
00121      DISPLAY 'SNKUP021 RECORDS READ:    ' PV-RECS-READ.           SNKUP021
00122      DISPLAY 'SNKUP021 RECORDS DELETED: ' PV-RECS-DELETED.        SNKUP021
00123                                                                   SNKUP021
00124      CLOSE ASNHDR-DELETE-FILE.                                    SNKUP021
00125                                                                   SNKUP021
00126      GOBACK.                                                      SNKUP021
00127                                                                   SNKUP021
00128      EJECT                                                        SNKUP021
00129                                                                   SNKUP021
00130 ***************************************************************** SNKUP021
00131 *    THIS PARAGRAPH CONTROLS THE MAIN PROCESSING OF THE PROGRAM   SNKUP021
00132 ***************************************************************** SNKUP021
00133  B100-DELETE-ROWS.                                                SNKUP021
00134                                                                   SNKUP021
00135      MOVE 'B100'                 TO PGM-LOCATION.                 SNKUP021
00136      MOVE 'B100-DELETE-ROWS          '                            SNKUP021
00137                                  TO ABEND-PARAGRAPH.              SNKUP021
00138                                                                   SNKUP021
00139      PERFORM B300-DELETE-ASNHDR.                                  SNKUP021
00140      IF PV-DELETE-COUNTER > +500                                     CL**2
00141          PERFORM B400-COMMIT                                      SNKUP021
00142          MOVE ZERO               TO PV-DELETE-COUNTER             SNKUP021
00143      END-IF.                                                      SNKUP021
00144      PERFORM B200-READ-INPUT-FILE.                                SNKUP021
00145                                                                   SNKUP021
00146      EJECT                                                        SNKUP021
00147                                                                   SNKUP021
00148 ***************************************************************** SNKUP021
00149 *    THIS PARAGRAPH WILL READ THE NEXT ASN TO BE DELETED.         SNKUP021
00150 ***************************************************************** SNKUP021
00151  B200-READ-INPUT-FILE.                                            SNKUP021
00152                                                                   SNKUP021
00153      MOVE 'B200'                 TO PGM-LOCATION.                 SNKUP021
00154      MOVE 'B200-READ-INPUT-FILE      '                            SNKUP021
00155                                  TO ABEND-PARAGRAPH.              SNKUP021
00156                                                                   SNKUP021
00157      READ ASNHDR-DELETE-FILE                                      SNKUP021
00158          INTO SN002I-ASN-DELETE-RECORD                            SNKUP021
00159          AT END                                                   SNKUP021
00160              SET PS-END-OF-INPUT TO TRUE.                         SNKUP021
00161                                                                   SNKUP021
00162      IF NOT PS-END-OF-INPUT                                       SNKUP021
00163          ADD 1                   TO PV-RECS-READ                  SNKUP021
00164      END-IF.                                                      SNKUP021
00165                                                                   SNKUP021
00166      EJECT                                                        SNKUP021
00167                                                                   SNKUP021
00168 ***************************************************************** SNKUP021
00169 *    THIS PARAGRAPH DELETES ALL ASNHDR ROWS THAT ARE EQUAL TO     SNKUP021
00170 *    THE CURRENT INPUTED ASN.                                     SNKUP021
00171 ***************************************************************** SNKUP021
00172  B300-DELETE-ASNHDR.                                              SNKUP021
00173                                                                   SNKUP021
00174      MOVE 'B300'                 TO PGM-LOCATION.                 SNKUP021
00175      MOVE 'B300-DELETE-ASNHDR        '                            SNKUP021
00176                                  TO ABEND-PARAGRAPH.              SNKUP021
00177                                                                   SNKUP021
00178      EXEC SQL                                                     SNKUP021
00179          DELETE                                                   SNKUP021
00180          FROM   TASNHDR                                           SNKUP021
00181          WHERE  ASNHDR_ID   = :SN002I-ASNHDR-ID                      CL**3
00182      END-EXEC.                                                    SNKUP021
00183                                                                   SNKUP021
00184      IF SQLCODE = ZERO                                            SNKUP021
00185          ADD SQLERRD (3)         TO PV-RECS-DELETED               SNKUP021
00186          ADD +1                  TO PV-DELETE-COUNTER             SNKUP021
00187      ELSE                                                         SNKUP021
00188          IF SQLCODE = +100                                        SNKUP021
00189              CONTINUE                                             SNKUP021
00190          ELSE                                                     SNKUP021
00191              PERFORM Z998-SQL-ERROR                               SNKUP021
00192          END-IF                                                   SNKUP021
00193      END-IF.                                                      SNKUP021
00194                                                                   SNKUP021
00195      EJECT                                                        SNKUP021
00196                                                                   SNKUP021
00197 ***************************************************************** SNKUP021
00198 *    THIS PARAGRAPH WILL COMMIT THE ROWS JUST DELETED             SNKUP021
00199 ***************************************************************** SNKUP021
00200  B400-COMMIT.                                                     SNKUP021
00201                                                                   SNKUP021
00202      MOVE 'B400'                 TO PGM-LOCATION.                 SNKUP021
00203      MOVE 'B400-COMMIT               '                            SNKUP021
00204                                  TO ABEND-PARAGRAPH.              SNKUP021
00205                                                                   SNKUP021
00206      EXEC SQL                                                     SNKUP021
00207          COMMIT                                                   SNKUP021
00208      END-EXEC.                                                    SNKUP021
00209                                                                   SNKUP021
00210      IF SQLCODE = ZERO                                            SNKUP021
00211          CONTINUE                                                 SNKUP021
00212      ELSE                                                         SNKUP021
00213          PERFORM Z998-SQL-ERROR                                   SNKUP021
00214      END-IF.                                                      SNKUP021
00215                                                                   SNKUP021
00216      EJECT                                                        SNKUP021
00217                                                                   SNKUP021
00218 ***************************************************************** SNKUP021
00219 *    THIS MODULE PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT  SNKUP021
00220 *    THAT AN SQL ERROR CODE OCCURS.                               SNKUP021
00221 ***************************************************************** SNKUP021
00222  Z998-SQL-ERROR.                                                  SNKUP021
00223                                                                   SNKUP021
00224      MOVE SQLCODE                TO SQLCODE2.                     SNKUP021
00225      MOVE SQLERRD(3)             TO SQLERRD3.                     SNKUP021
00226      DISPLAY '** ABEND **         ** = SQLERROR'.                 SNKUP021
00227      DISPLAY '** PROGRAM          ** = SNKUP021'.                 SNKUP021
00228      DISPLAY '** PARAGRAPH        ** = ' ABEND-PARAGRAPH.         SNKUP021
00229      DISPLAY '** SQLCODE          ** = ' SQLCODE2.                SNKUP021
00230      DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                SNKUP021
00231      DISPLAY '** SQLERRM          ** = ' SQLERRM.                 SNKUP021
00232      DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                SNKUP021
00233      MOVE +4013                  TO ABEND-CODE.                   SNKUP021
00234      CALL 'ILBOABN0'             USING ABEND-CODE.                SNKUP021
