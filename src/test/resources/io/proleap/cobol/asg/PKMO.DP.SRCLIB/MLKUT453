       IDENTIFICATION DIVISION.                                         00010039
       PROGRAM-ID.    MLKUT453.                                         00020039
       AUTHOR.        SCOTT JACKSON.                                    00030039
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040039
       DATE-WRITTEN.  05-01-2000.                                       00050039
       DATE-COMPILED.                                                   00060039
      ******************************************************************00070039
      * MLKUT453 - WEEKLY MAJOR CLASS TABLE (TWKMCLS) RECALC EXTRACT    00080039
      *                                                                 00090039
      * THIS PROGRAM WILL CREATE THE EXTRACT FOR THE RECALC PROGRAM     00100039
      * ON TABLE TWKMCLS. IT WILL FIND ALL DEPARTMENT/MAJOR CLASS/STORE 00110039
      * COMBINATIONS ON THE TWKMCLS DB2 TABLE FOR THE CURRENT AND       00120039
      * PREVIOUS FISCAL WEEKS WHICH WILL BE USED TO PROCESS THE TWKMCLS 00130039
      * TABLE RECALC.                                                   00140039
      *                                                                 00150039
      *CHANGE HISTORY:                                                  00160039
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                     00170039
      * -------  ----------  ----------------------------------------   00180039
21731 * 21731    06-18-2002  IMPLEMENT A SELECTIVE EXTACT/RECALC FOR    00190039
21731 *                      TABLE ROWS WHERE THE ACCUMULATED COLUMNS   00200039
21731 *                      HAVE CHANGED.   -ROD JANSSEN               00210039
      * W31475  06/01/2009  COGNIZANT                                  *00220039
      *                     MODIFIED CURSOR DECLARATION FOR MULTI ROW  *00230039
      *                     FETCH AS PART OF ML BATCH PERF TUNE PRJ    *00240039
      * 940372  09/28/2009  COGNIZANT                                  *00241039
      *                     FIXED BUG WITH WKMCLS DEPT, MAJ CL AND STR *00242039
      *                     VARIABLES BEING OVERWRITTEN DURING CUTOFF  *00243039
      ******************************************************************00250039
                                                                        00260039
       ENVIRONMENT DIVISION.                                            00270039
       CONFIGURATION SECTION.                                           00280039
       SOURCE-COMPUTER.        IBM-3090.                                00290039
       OBJECT-COMPUTER.        IBM-3090.                                00300039
       INPUT-OUTPUT SECTION.                                            00310039
       FILE-CONTROL.                                                    00320039
                                                                        00330039
21731      SELECT INPUT-FILE                                            00340039
21731          ASSIGN TO INP01.                                         00350039
21731                                                                   00360039
           SELECT WKMCLS-EXTR-FILE                                      00370039
               ASSIGN TO OUT01.                                         00380039
                                                                        00390039
       DATA DIVISION.                                                   00400039
       FILE SECTION.                                                    00410039
                                                                        00420039
21731  FD  INPUT-FILE                                                   00430039
21731      LABEL RECORDS ARE OMITTED                                    00440039
21731      RECORDING MODE IS F                                          00450039
21731      BLOCK CONTAINS 0 RECORDS.                                    00460039
21731  01  INPUT-RECORD.                                                00470039
21731      05  IR-DEPT                 PIC X(03).                       00480039
21731      05  IR-CLASS                PIC X(02).                       00490039
21731      05  IR-STORE                PIC S9(04) COMP.                 00500039
21731                                                                   00510039
       FD  WKMCLS-EXTR-FILE                                             00520039
           RECORDING MODE IS F                                          00530039
           LABEL RECORDS ARE STANDARD                                   00540039
           BLOCK CONTAINS 0 RECORDS.                                    00550039
       01  WKMCLS-EXTR-REC             PIC X(10).                       00560039
                                                                        00570039
      /*****************************************************************00580039
       WORKING-STORAGE SECTION.                                         00590039
      ******************************************************************00600039
                                                                        00610039
W31475 01  ROW-ID                      PIC S9(04) COMP VALUE 0.         00620039
  |    01  HOST-VARIABLES.                                              00630039
  |        05  HV-WKMCLS-DEPT-NBR      PIC X(03)   VALUE SPACES         00640039
  |                                    OCCURS 100  TIMES.               00650039
  |        05  HV-WKMCLS-MAJ-CL-NBR    PIC X(02)   VALUE SPACES         00660039
  |                                    OCCURS 100  TIMES.               00670039
  |        05  HV-WKMCLS-STR-NBR       PIC S9(4) COMP VALUE 0           00680039
W31475                                 OCCURS 100  TIMES.               00690039
                                                                        00700039
       01  PS-PROGRAM-SWITCHES.                                         00710039
21731      05  PS-INPUT-FILE-DONE-SW   PIC X       VALUE 'N'.           00720039
 |             88  PS-INPUT-FILE-DONE              VALUE 'Y'.           00730039
 |             88  PS-INPUT-FILE-NOT-DONE          VALUE 'N'.           00740039
 |         05  PS-SELECT-RECALC-SW     PIC X       VALUE 'N'.           00750039
 |             88  PS-SELECTIVE-RECALC             VALUE 'Y'.           00760039
21731          88  PS-EXTRACT-ALL                  VALUE 'N'.           00770039
           05  PS-TWKMCLS-DONE         PIC X       VALUE 'N'.           00780039
               88  PS-TWKMCLS-IS-DONE              VALUE 'Y'.           00790039
               88  PS-TWKMCLS-IS-NOT-DONE          VALUE 'N'.           00800039
                                                                        00810039
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.              00820039
           05  PA-TWKMCLS-ROWS-FETCHED PIC S9(13)  VALUE ZERO.          00830039
           05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.          00840039
           05  PA-INPUT-RECS-READ-CNT  PIC S9(13)  VALUE ZERO.          00850039
                                                                        00860039
       01  CD-COUNTER-DISPLAY.                                          00870039
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.           00880039
                                                                        00890039
       01 ABEND-AREAS.                                                  00900039
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO           00910039
                                                   COMP SYNC.           00920039
               88  AC-BAD-DATA                     VALUE +4003.         00930039
               88  AC-DB2-ERROR                    VALUE +4013.         00940039
           05  AA-ABEND-LIT            PIC X(40)   VALUE                00950039
                 '*****       ABEND'.                                   00960039
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                00970039
                   '*****   PROGRAM: MLKUT453'.                         00980039
           05  AA-PARAGRAPH-LIT.                                        00990039
               10  FILLER              PIC X(17)   VALUE                01000039
                   '***** PARAGRAPH: '.                                 01010039
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        01020039
           05  AA-MESSAGE-LINE.                                         01030039
               10  FILLER              PIC X(06)   VALUE '*****'.       01040039
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        01050039
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                01060039
                   '*****    DB2 ERROR'.                                01070039
           05  AA-DB2-OPERATION-LIT.                                    01080039
               10  FILLER              PIC X(17)   VALUE                01090039
                   '***** OPERATION: '.                                 01100039
               10  AA-DB2-OPERATION.                                    01110039
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        01120039
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        01130039
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        01140039
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        01150039
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        01160039
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        01170039
                                                                        01180039
       01  EXTR-RECORD.                                                 01190039
           05  EXTR-DEPT-NBR    PIC X(03)    VALUE SPACES.              01200039
           05  EXTR-MAJ-CL-NBR  PIC X(02)    VALUE SPACES.              01210039
           05  EXTR-STR-NBR     PIC S9(4)   COMP VALUE +0.              01220039
           05  FILLER           PIC X(03)    VALUE SPACES.              01230039
                                                                        01240039
       EJECT                                                            01250039
      ******************************************************************01260039
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01270039
      ******************************************************************01280039
           COPY DPWS004.                                                01290039
                                                                        01300039
      ******************************************************************01310039
      *  DB2 ERRORS                                                     01320039
      ******************************************************************01330039
           COPY SQLCA2.                                                 01340039
                                                                        01350039
      ******************************************************************01360039
      *  DB2 COMMUNICATION AREA                                         01370039
      ******************************************************************01380039
           EXEC SQL                                                     01390039
                INCLUDE SQLCA                                           01400039
           END-EXEC.                                                    01410039
                                                                        01420039
      ******************************************************************01430039
      *  DB2 TWKMCLS TABLE LAYOUT                                       01440039
      ******************************************************************01450039
           EXEC SQL                                                     01460039
                INCLUDE TWKMCLS                                         01470039
           END-EXEC.                                                    01480039
                                                                        01490039
      ****************************************************              01500039
      * M/L DATE TABLE AND CURRENT OPEN FISCAL DATE INFO *              01510039
      ****************************************************              01520039
           EXEC SQL                                                     01530039
               INCLUDE TDTATTR                                          01540039
           END-EXEC.                                                    01550039
                                                                        01560039
      ****************************************************              01570039
      * M/L DATE CONTROL INFO                            *              01580039
      ****************************************************              01590039
           EXEC SQL                                                     01600039
               INCLUDE TMLCNTL                                          01610039
           END-EXEC.                                                    01620039
                                                                        01630039
      ******************************************************************01640039
      *  DB2 TWKMCLS CURSOR                                             01650039
      ******************************************************************01660039
           EXEC SQL                                                     01670039
W31475         DECLARE TWKMCLS_CURSOR CURSOR                            01680039
W31475         WITH ROWSET POSITIONING FOR                              01690039
               SELECT DISTINCT DEPT_NBR                                 01700039
                              ,MAJ_CL_NBR                               01710039
                              ,STR_NBR                                  01720039
               FROM TWKMCLS                                             01730039
                   ,TMLCNTL                                             01740039
               WHERE (FSCL_WK_END_DTE BETWEEN                           01750039
WEEK                               OPN_FSCL_WK_DTE                      01760039
WEEK                           AND MXPSTG_FSCL_WK_DTE) OR               01770039
                    ((FSCL_WK_END_DTE BETWEEN                           01780039
                      :DTATTR-PFM-END-DTE AND                           01790039
                      MXPSTG_FSCL_MN_DTE) AND                           01800039
                       (END_INV_RTL_AMT  <> 0  OR                       01810039
                        END_INV_COST_AMT <> 0))                         01820039
               ORDER BY DEPT_NBR, MAJ_CL_NBR, STR_NBR                   01830039
           END-EXEC.                                                    01840039
                                                                        01850039
21731 ******************************************************************01860039
 |    *  DB2 TWKMCLS CURSOR - SELECTIVE EXTRACT TABLE CURSOR            01870039
 |    ******************************************************************01880039
 |         EXEC SQL                                                     01890039
 |             DECLARE TWKMCLS_SLCT_CSR CURSOR FOR                      01900039
 |             SELECT DISTINCT DEPT_NBR                                 01910039
 |                            ,MAJ_CL_NBR                               01920039
 |                            ,STR_NBR                                  01930039
 |             FROM TWKMCLS                                             01940039
 |                 ,TMLCNTL                                             01950039
 |             WHERE ((FSCL_WK_END_DTE BETWEEN                          01960039
 |                     OPN_FSCL_WK_DTE AND MXPSTG_FSCL_WK_DTE) AND      01970039
 |                    (DEPT_NBR   = :WKMCLS-DEPT-NBR    AND             01980039
 |                     MAJ_CL_NBR = :WKMCLS-MAJ-CL-NBR  AND             01990039
 |                     STR_NBR    = :WKMCLS-STR-NBR))                   02000039
 |                OR ((FSCL_WK_END_DTE BETWEEN                          02010039
 |                     :DTATTR-PFM-END-DTE AND MXPSTG_FSCL_MN_DTE) AND  02020039
 |                    (DEPT_NBR   = :WKMCLS-DEPT-NBR    AND             02030039
 |                     MAJ_CL_NBR = :WKMCLS-MAJ-CL-NBR  AND             02040039
 |                     STR_NBR    = :WKMCLS-STR-NBR)    AND             02050039
 |                    (END_INV_RTL_AMT  <> 0    OR                      02060039
 |                     END_INV_COST_AMT <> 0))                          02070039
 |             ORDER BY DEPT_NBR, MAJ_CL_NBR, STR_NBR                   02080039
 |         END-EXEC.                                                    02090039
21731                                                                   02100039
      /*****************************************************************02110039
       PROCEDURE DIVISION.                                              02120039
      ******************************************************************02130039
      ******************************************************************02140039
       A000-MAIN-MODULE.                                                02150039
      ******************************************************************02160039
      *                                                                 02170039
           PERFORM B000-INITIALIZE.                                     02180039
                                                                        02190039
           PERFORM B100-PROCESS-DATA.                                   02200039
                                                                        02210039
           PERFORM B900-EOJ-ROUTINE.                                    02220039
                                                                        02230039
           STOP RUN.                                                    02240039
                                                                        02250039
      /*****************************************************************02260039
       B000-INITIALIZE.                                                 02270039
      ******************************************************************02280039
      *                                                                 02290039
21731      OPEN INPUT  INPUT-FILE                                       02300039
                OUTPUT WKMCLS-EXTR-FILE.                                02310039
                                                                        02320039
           PERFORM 7100-SELECT-DB2-DATES.                               02330039
                                                                        02340039
21731      MOVE 'N'            TO PS-INPUT-FILE-DONE-SW.                02350039
 |         MOVE 'N'            TO PS-SELECT-RECALC-SW.                  02360039
 |         MOVE ZEROS          TO PA-INPUT-RECS-READ-CNT.               02370039
 |                                                                      02380039
 |         PERFORM 7000-READ-INPUT-FILE.                                02390039
 |                                                                      02400039
 |         DISPLAY ' '.                                                 02410039
 |         DISPLAY '  -------------------------------------------'.     02420039
 |         DISPLAY '  PGM MLKUT453'.                                    02430039
 |                                                                      02440039
 |         IF PS-INPUT-FILE-DONE                                        02450039
 |           DISPLAY '  ** NORMAL TWKMCLS PROCESS'                      02460039
 |           DISPLAY '  ** EXTRACT ALL DEPT/CL/STR INFO'                02470039
 |         ELSE                                                         02480039
 |           DISPLAY '  ** CUTOFF WEEK TWKMCLS PROCESS'                 02490039
 |           DISPLAY '  ** EXTRACT SELECTIVE DEPT/CL/STR INFO'          02500039
 |         END-IF.                                                      02510039
 |                                                                      02520039
 |         DISPLAY '  -------------------------------------------'.     02530039
 |         DISPLAY ' '.                                                 02540039
21731                                                                   02550039
      /*****************************************************************02560039
       B100-PROCESS-DATA.                                               02570039
      ******************************************************************02580039
      *                                                                 02590039
21731      IF PS-SELECTIVE-RECALC                                       02600039
 |             PERFORM C200-SELECTIVE-EXTRACT-TWKMCLS                   02610039
 |               UNTIL PS-INPUT-FILE-DONE                               02620039
 |         ELSE                                                         02630039
 |             PERFORM Z210-OPEN-CURSOR-TWKMCLS                         02640039
 |             PERFORM Z220-FETCH-TWKMCLS                               02650039
 |             PERFORM                                                  02660039
 |               UNTIL PS-TWKMCLS-IS-DONE                               02670039
W31475*            PERFORM C100-WRITE-EXTRACT                           02680039
  |                PERFORM VARYING ROW-ID FROM 1 BY 1                   02690039
  |                UNTIL ROW-ID > SQLERRD (3)                           02700039
  |                     PERFORM C100-WRITE-EXTRACT                      02710039
W31475             END-PERFORM                                          02720039
 |                 PERFORM Z220-FETCH-TWKMCLS                           02730039
 |             END-PERFORM                                              02740039
 |             PERFORM Z230-CLOSE-CURSOR-TWKMCLS                        02750039
 |         END-IF.                                                      02760039
 |                                                                      02770039
 |    *    PERFORM Z210-OPEN-CURSOR-TWKMCLS.                            02780039
 |    *                                                                 02790039
 |    *    PERFORM Z220-FETCH-TWKMCLS.                                  02800039
 |    *                                                                 02810039
 |    *    PERFORM                                                      02820039
 |    *      UNTIL PS-TWKMCLS-IS-DONE                                   02830039
 |    *        PERFORM C100-WRITE-EXTRACT                               02840039
 |    *        PERFORM Z220-FETCH-TWKMCLS                               02850039
 |    *    END-PERFORM.                                                 02860039
 |    *                                                                 02870039
 |    *    PERFORM Z230-CLOSE-CURSOR-TWKMCLS.                           02880039
21731 *                                                                 02890039
      /*****************************************************************02900039
       C100-WRITE-EXTRACT.                                              02910039
      ******************************************************************02920039
      *                                                                 02930039
940372     IF PS-SELECTIVE-RECALC                                       02940039
  |            MOVE WKMCLS-DEPT-NBR              TO EXTR-DEPT-NBR       02950039
  |            MOVE WKMCLS-MAJ-CL-NBR            TO EXTR-MAJ-CL-NBR     02960039
  |            MOVE WKMCLS-STR-NBR               TO EXTR-STR-NBR        02970039
  |        ELSE                                                         02980039
  |            MOVE HV-WKMCLS-DEPT-NBR(ROW-ID)   TO EXTR-DEPT-NBR       02990039
  |            MOVE HV-WKMCLS-MAJ-CL-NBR(ROW-ID) TO EXTR-MAJ-CL-NBR     03000039
  |            MOVE HV-WKMCLS-STR-NBR(ROW-ID)    TO EXTR-STR-NBR        03010039
940372     END-IF.                                                      03020039
                                                                        03030039
           WRITE WKMCLS-EXTR-REC                                        03040039
               FROM EXTR-RECORD.                                        03050039
           ADD 1                      TO PA-OUTPUT-RECS-WRITTEN.        03060039
                                                                        03070039
21731 ******************************************************************03080039
 |     C200-SELECTIVE-EXTRACT-TWKMCLS.                                  03090039
 |    ******************************************************************03100039
 |                                                                      03110039
 |         MOVE IR-DEPT        TO WKMCLS-DEPT-NBR.                      03120039
 |         MOVE IR-CLASS       TO WKMCLS-MAJ-CL-NBR.                    03130039
 |         MOVE IR-STORE       TO WKMCLS-STR-NBR.                       03140039
 |                                                                      03150039
 |         PERFORM Z500-OPEN-CURSOR-TWKMCLS.                            03160039
 |                                                                      03170039
 |         MOVE 'N'            TO PS-TWKMCLS-DONE.                      03180039
 |         PERFORM Z510-FETCH-TWKMCLS                                   03190039
 |             UNTIL PS-TWKMCLS-IS-DONE.                                03200039
 |                                                                      03210039
 |         PERFORM Z520-CLOSE-CURSOR-TWKMCLS.                           03220039
 |                                                                      03230039
 |         PERFORM 7000-READ-INPUT-FILE.                                03240039
21731                                                                   03250039
      /*****************************************************************03260039
       B900-EOJ-ROUTINE.                                                03270039
      ******************************************************************03280039
      *                                                                 03290039
           DISPLAY ' '.                                                 03300039
           DISPLAY '  PGM MLKUT453 PROCESSING SUMMARY'.                 03310039
           DISPLAY '  -----------------------------------'.             03320039
           DISPLAY ' '.                                                 03330039
      *                                                                 03340039
           MOVE PA-TWKMCLS-ROWS-FETCHED TO CD-COUNT-HOLD1.              03350039
           DISPLAY 'TWKMCLS ROWS FETCHED =     ' CD-COUNT-HOLD1.        03360039
      *                                                                 03370039
           MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.               03380039
           DISPLAY 'EXTRACT RECORDS WRITTEN = ' CD-COUNT-HOLD1.         03390039
      *                                                                 03400039
21731      IF PS-SELECTIVE-RECALC                                       03410039
 |             MOVE PA-INPUT-RECS-READ-CNT TO CD-COUNT-HOLD1            03420039
 |             DISPLAY '  -----------------------------------'          03430039
 |             DISPLAY '  SELECT EXTRACT - INPUT RECS READ = '          03440039
 |                                               CD-COUNT-HOLD1         03450039
 |         END-IF.                                                      03460039
 |    *                                                                 03470039
21731      DISPLAY '  -----------------------------------'.             03480039
           DISPLAY ' '.                                                 03490039
                                                                        03500039
21731      CLOSE INPUT-FILE.                                            03510039
           CLOSE WKMCLS-EXTR-FILE.                                      03520039
                                                                        03530039
      /*****************************************************************03540039
       Z210-OPEN-CURSOR-TWKMCLS.                                        03550039
      ******************************************************************03560039
      *                                                                 03570039
           EXEC SQL                                                     03580039
               OPEN TWKMCLS_CURSOR                                      03590039
           END-EXEC.                                                    03600039
                                                                        03610039
           EVALUATE TRUE                                                03620039
               WHEN SQLCODE < 0                                         03630039
                   MOVE 'Z210-OPEN-CURSOR-TWKMCLS'                      03640039
                                       TO AA-PARAGRAPH-NAME             03650039
                   MOVE 'TWKMCLS TABLE ERROR'                           03660039
                                       TO AA-MESSAGE                    03670039
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              03680039
                   PERFORM Z998-SQL-ERROR                               03690039
               WHEN SQLCODE > 0                                         03700039
               WHEN SQLWARN0 NOT = SPACES                               03710039
                   MOVE 'Z210-OPEN-CURSOR-TWKMCLS'                      03720039
                                       TO AA-PARAGRAPH-NAME             03730039
                   MOVE 'TWKMCLS TABLE WARNING'                         03740039
                                       TO AA-MESSAGE                    03750039
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              03760039
                   PERFORM Z998-SQL-ERROR                               03770039
           END-EVALUATE.                                                03780039
                                                                        03790039
      ******************************************************************03800039
       Z220-FETCH-TWKMCLS.                                              03810039
      ******************************************************************03820039
      *                                                                 03830039
W31475*    EXEC SQL                                                     03840039
  |   *        FETCH TWKMCLS_CURSOR                                     03850039
  |   *         INTO :WKMCLS-DEPT-NBR                                   03860039
  |   *             ,:WKMCLS-MAJ-CL-NBR                                 03870039
  |   *             ,:WKMCLS-STR-NBR                                    03880039
  |   *    END-EXEC.                                                    03890039
  |        EXEC SQL                                                     03900039
  |            FETCH NEXT ROWSET FROM TWKMCLS_CURSOR FOR 100 ROWS       03910039
  |             INTO :HV-WKMCLS-DEPT-NBR                                03920039
  |                 ,:HV-WKMCLS-MAJ-CL-NBR                              03930039
  |                 ,:HV-WKMCLS-STR-NBR                                 03940039
W31475     END-EXEC.                                                    03950039
                                                                        03960039
           EVALUATE TRUE                                                03970039
               WHEN SQLCODE = 0                                         03980039
W31475*            ADD +1              TO PA-TWKMCLS-ROWS-FETCHED       03990039
W31475             ADD +100            TO PA-TWKMCLS-ROWS-FETCHED       04000039
               WHEN SQLCODE = +100                                      04010039
                   MOVE 'Y'            TO PS-TWKMCLS-DONE               04020039
W31475             ADD SQLERRD (3)     TO PA-TWKMCLS-ROWS-FETCHED       04030039
  |                PERFORM VARYING ROW-ID FROM 1 BY 1                   04040039
  |                UNTIL ROW-ID > SQLERRD (3)                           04050039
  |                     PERFORM C100-WRITE-EXTRACT                      04060039
W31475             END-PERFORM                                          04070039
               WHEN SQLCODE < 0                                         04080039
                   MOVE 'Z220-FETCH-TWKMCLS'                            04090039
                                       TO AA-PARAGRAPH-NAME             04100039
                   MOVE 'TWKMCLS TABLE ERROR'                           04110039
                                       TO AA-MESSAGE                    04120039
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              04130039
                   PERFORM Z998-SQL-ERROR                               04140039
               WHEN ((SQLCODE > 0) AND (SQLCODE NOT = +100))            04150039
               WHEN SQLWARN0 NOT = SPACES                               04160039
                   MOVE 'Z220-FETCH-TWKMCLS'                            04170039
                                       TO AA-PARAGRAPH-NAME             04180039
                   MOVE 'TWKMCLS TABLE WARNING'                         04190039
                                       TO AA-MESSAGE                    04200039
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              04210039
                   PERFORM Z998-SQL-ERROR                               04220039
           END-EVALUATE.                                                04230039
                                                                        04240039
      ******************************************************************04250039
       Z230-CLOSE-CURSOR-TWKMCLS.                                       04260039
      ******************************************************************04270039
      *                                                                 04280039
           EXEC SQL                                                     04290039
               CLOSE TWKMCLS_CURSOR                                     04300039
           END-EXEC.                                                    04310039
                                                                        04320039
           EVALUATE TRUE                                                04330039
               WHEN SQLCODE < 0                                         04340039
                   MOVE 'Z230-CLOSE-CURSOR-TWKMCLS'                     04350039
                                       TO AA-PARAGRAPH-NAME             04360039
                   MOVE 'TWKMCLS TABLE ERROR'                           04370039
                                       TO AA-MESSAGE                    04380039
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              04390039
                   PERFORM Z998-SQL-ERROR                               04400039
               WHEN SQLCODE > 0                                         04410039
               WHEN SQLWARN0 NOT = SPACES                               04420039
                   MOVE 'Z230-CLOSE-CURSOR-TWKMCLS'                     04430039
                                       TO AA-PARAGRAPH-NAME             04440039
                   MOVE 'TWKMCLS TABLE WARNING'                         04450039
                                       TO AA-MESSAGE                    04460039
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              04470039
                   PERFORM Z998-SQL-ERROR                               04480039
           END-EVALUATE.                                                04490039
                                                                        04500039
21731 /*****************************************************************04510039
 |     Z500-OPEN-CURSOR-TWKMCLS.                                        04520039
 |    ******************************************************************04530039
 |    *                                                                 04540039
 |         EXEC SQL                                                     04550039
 |             OPEN TWKMCLS_SLCT_CSR                                    04560039
 |         END-EXEC.                                                    04570039
 |                                                                      04580039
 |         EVALUATE TRUE                                                04590039
 |             WHEN SQLCODE < 0                                         04600039
 |                 MOVE 'Z500-OPEN-CURSOR-TWKMCLS'                      04610039
 |                                     TO AA-PARAGRAPH-NAME             04620039
 |                 MOVE 'TWKMCLS TABLE ERROR - SELECTIVE EXTRACT'       04630039
 |                                     TO AA-MESSAGE                    04640039
 |                 MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              04650039
 |                 PERFORM Z998-SQL-ERROR                               04660039
 |             WHEN SQLCODE > 0                                         04670039
 |             WHEN SQLWARN0 NOT = SPACES                               04680039
 |                 MOVE 'Z210-OPEN-CURSOR-TWKMCLS'                      04690039
 |                                     TO AA-PARAGRAPH-NAME             04700039
 |                 MOVE 'TWKMCLS TABLE WARNING - SELECTIVE EXTRACT'     04710039
 |                                     TO AA-MESSAGE                    04720039
 |                 MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              04730039
 |                 PERFORM Z998-SQL-ERROR                               04740039
 |         END-EVALUATE.                                                04750039
 |                                                                      04760039
 |    ******************************************************************04770039
 |     Z510-FETCH-TWKMCLS.                                              04780039
 |    ******************************************************************04790039
 |    *                                                                 04800039
 |         EXEC SQL                                                     04810039
 |             FETCH TWKMCLS_SLCT_CSR                                   04820039
 |              INTO :WKMCLS-DEPT-NBR                                   04830039
 |                  ,:WKMCLS-MAJ-CL-NBR                                 04840039
 |                  ,:WKMCLS-STR-NBR                                    04850039
 |         END-EXEC.                                                    04860039
 |                                                                      04870039
 |         EVALUATE TRUE                                                04880039
 |             WHEN SQLCODE = 0                                         04890039
 |                 PERFORM C100-WRITE-EXTRACT                           04900039
 |                 ADD +1              TO PA-TWKMCLS-ROWS-FETCHED       04910039
 |             WHEN SQLCODE = +100                                      04920039
 |                 MOVE 'Y'            TO PS-TWKMCLS-DONE               04930039
 |             WHEN SQLCODE < 0                                         04940039
 |                 MOVE 'Z510-FETCH-TWKMCLS'                            04950039
 |                                     TO AA-PARAGRAPH-NAME             04960039
 |                 MOVE 'TWKMCLS TABLE ERROR - SELECTIVE EXTRACT'       04970039
 |                                     TO AA-MESSAGE                    04980039
 |                 MOVE 'FETCH'        TO AA-DB2-OPERATION              04990039
 |                 PERFORM Z998-SQL-ERROR                               05000039
 |             WHEN ((SQLCODE > 0) AND (SQLCODE NOT = +100))            05010039
 |             WHEN SQLWARN0 NOT = SPACES                               05020039
 |                 MOVE 'Z510-FETCH-TWKMCLS'                            05030039
 |                                     TO AA-PARAGRAPH-NAME             05040039
 |                 MOVE 'TWKMCLS TABLE WARNING - SELECTIVE EXTRACT'     05050039
 |                                     TO AA-MESSAGE                    05060039
 |                 MOVE 'FETCH'        TO AA-DB2-OPERATION              05070039
 |                 PERFORM Z998-SQL-ERROR                               05080039
 |         END-EVALUATE.                                                05090039
 |                                                                      05100039
 |    ******************************************************************05110039
 |     Z520-CLOSE-CURSOR-TWKMCLS.                                       05120039
 |    ******************************************************************05130039
 |    *                                                                 05140039
 |         EXEC SQL                                                     05150039
 |             CLOSE TWKMCLS_SLCT_CSR                                   05160039
 |         END-EXEC.                                                    05170039
 |                                                                      05180039
 |         EVALUATE TRUE                                                05190039
 |             WHEN SQLCODE < 0                                         05200039
 |                 MOVE 'Z520-CLOSE-CURSOR-TWKMCLS'                     05210039
 |                                     TO AA-PARAGRAPH-NAME             05220039
 |                 MOVE 'TWKMCLS TABLE ERROR - SELECTIVE EXTRACT'       05230039
 |                                     TO AA-MESSAGE                    05240039
 |                 MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              05250039
 |                 PERFORM Z998-SQL-ERROR                               05260039
 |             WHEN SQLCODE > 0                                         05270039
 |             WHEN SQLWARN0 NOT = SPACES                               05280039
 |                 MOVE 'Z520-CLOSE-CURSOR-TWKMCLS'                     05290039
 |                                     TO AA-PARAGRAPH-NAME             05300039
 |                 MOVE 'TWKMCLS TABLE WARNING - SELECTIVE EXTRACT'     05310039
 |                                     TO AA-MESSAGE                    05320039
 |                 MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              05330039
 |                 PERFORM Z998-SQL-ERROR                               05340039
 |         END-EVALUATE.                                                05350039
 |                                                                      05360039
 |    /*****************************************************************05370039
 |     7000-READ-INPUT-FILE.                                            05380039
 |    ******************************************************************05390039
 |    *                                                                 05400039
 |         READ INPUT-FILE NEXT                                         05410039
 |           AT END                                                     05420039
 |               MOVE 'Y' TO PS-INPUT-FILE-DONE-SW                      05430039
 |           NOT AT END                                                 05440039
 |               MOVE 'Y' TO PS-SELECT-RECALC-SW                        05450039
 |               ADD +1   TO PA-INPUT-RECS-READ-CNT                     05460039
 |         END-READ.                                                    05470039
21731                                                                   05480039
      ******************************************************************05490039
       7100-SELECT-DB2-DATES.                                           05500039
      ******************************************************************05510039
      *                                                                 05520039
           EXEC SQL                                                     05530039
               SELECT  PFM_END_DTE                                      05540039
 ---                  ,OPN_FSCL_WK_DTE                                  05550039
 ---                  ,OPN_FSCL_MN_DTE                                  05560039
                INTO  :DTATTR-PFM-END-DTE                               05570039
 ---                 ,:MLCNTL-OPN-FSCL-WK-DTE                           05580039
 ---                 ,:MLCNTL-OPN-FSCL-MN-DTE                           05590039
                FROM TDTATTR                                            05600039
                    ,TMLCNTL                                            05610039
WEEK            WHERE KY_DTE  = OPN_FSCL_WK_DTE                         05620039
           END-EXEC.                                                    05630039
      *                                                                 05640039
           EVALUATE SQLCODE                                             05650039
               WHEN 0                                                   05660039
                   DISPLAY 'DTATTR-PFM-END-DTE : ',                     05670039
                            DTATTR-PFM-END-DTE                          05680039
                   DISPLAY 'MLCNTL-OPN-FSCL-WK-DTE    : ',              05690039
                            MLCNTL-OPN-FSCL-WK-DTE                      05700039
                   DISPLAY 'MLCNTL-OPN-FSCL-MN-DTE    : ',              05710039
                            MLCNTL-OPN-FSCL-MN-DTE                      05720039
      *        WHEN +100                                                05730039
      *            DISPLAY 'NO ROWS QUALIFY FOR RECALCULATION'          05740039
               WHEN OTHER                                               05750039
                  MOVE '7100-SELECT-DB2-DATES'                          05760039
                                       TO AA-PARAGRAPH-NAME             05770039
                   PERFORM Z998-SQL-ERROR                               05780039
           END-EVALUATE.                                                05790039
                                                                        05800039
      ******************************************************************05810039
       Z998-SQL-ERROR.                                                  05820039
      ******************************************************************05830039
      *                                                                 05840039
21731      CLOSE INPUT-FILE.                                            05850039
           CLOSE WKMCLS-EXTR-FILE.                                      05860039
                                                                        05870039
           DISPLAY AA-ABEND-LIT.                                        05880039
           DISPLAY AA-DB2-ERROR-LIT.                                    05890039
           DISPLAY AA-PROGRAM-LIT.                                      05900039
           DISPLAY AA-PARAGRAPH-LIT.                                    05910039
           DISPLAY AA-MESSAGE-LINE.                                     05920039
           DISPLAY AA-DB2-OPERATION-LIT.                                05930039
           SET AC-DB2-ERROR TO TRUE.                                    05940039
           MOVE +4013                  TO ABEND-CODE.                   05950039
                                                                        05960039
           COPY DPPD004.                                                05970039
           CALL 'ILBOABN0' USING ABEND-CODE.                            05980039
                                                                        05990039
