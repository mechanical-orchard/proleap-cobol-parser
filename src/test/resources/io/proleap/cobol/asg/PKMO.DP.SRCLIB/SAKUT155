      ***************************************************************** 00010000
       IDENTIFICATION DIVISION.                                         00020000
      ***************************************************************** 00030000
                                                                        00040000
       PROGRAM-ID.             SAKUT155.                                00050000
       AUTHOR.                 JIM HUNTER.                              00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            AUGUST 2021.                             00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ***************************************************************** 00110000
      *                                                                 00120000
      *  THIS PROGRAM CREATES THE SALES TRANSACTIONS FOR THE DAY THAT   00130000
      *  ARE OUT OF BALANCE AND NEED ATTENTION FROM THE SALES AUDIT     00140000
      *  TEAM.                                                          00141000
      *                                                                 00150000
      *   INPUT: INP01 - CONTAINS THE PROCESSING DATE.                  00160000
      *  OUTPUT: OUT01 - SALES OUT OF BALANCE DETAIL FILE.              00180000
      *                                                                 00190000
      ***************************************************************** 00200000
       ENVIRONMENT DIVISION.                                            00210000
      ***************************************************************** 00220000
                                                                        00230000
       CONFIGURATION SECTION.                                           00240000
                                                                        00250000
       SOURCE-COMPUTER.        IBM-3090.                                00260000
                                                                        00270000
       OBJECT-COMPUTER.        IBM-3090.                                00280000
                                                                        00290000
       INPUT-OUTPUT SECTION.                                            00300000
                                                                        00310000
       FILE-CONTROL.                                                    00320000
                                                                        00330000
           SELECT POS-DATE-FILE                                         00340000
               ASSIGN TO INP01.                                         00350000
                                                                        00360000
           SELECT SALES-OUT-OF-BALANCE-CSV-FILE                         00370000
               ASSIGN TO OUT01.                                         00380000
                                                                        00390000
      ******************************************************************00400000
       DATA DIVISION.                                                   00410000
      ******************************************************************00420000
                                                                        00430000
       FILE SECTION.                                                    00440000
                                                                        00450000
       FD POS-DATE-FILE                                                 00460000
           RECORDING MODE IS F                                          00470000
           LABEL RECORDS ARE STANDARD                                   00480000
           RECORD CONTAINS 80 CHARACTERS                                00490000
           BLOCK CONTAINS 0 RECORDS                                     00500000
           DATA RECORD IS POS-DATE-RECORD.                              00510000
                                                                        00520000
       01 POS-DATE-RECORD              PIC  X(80).                      00521000
                                                                        00522000
       FD  SALES-OUT-OF-BALANCE-CSV-FILE                                00530000
           RECORDING MODE IS F                                          00540000
           BLOCK CONTAINS 0  RECORDS.                                   00550000
                                                                        00560000
       01  SALES-OUT-OF-BALANCE-CSV-REC    PIC  X(120).                 00570000
                                                                        00580000
      ******************************************************************00590000
       WORKING-STORAGE SECTION.                                         00600000
      ******************************************************************00610000
       01  FILLER                            PIC X(50)  VALUE           00620000
           ' *** WORKING STORAGE STARTS HERE FOR SAKUT155 *** '.        00630000
                                                                        00640000
      ******************************************************************00743000
      * RECORD LAYOUT FOR INPUT DATE CARD (POS DATE)                    00744000
      **  SACC004 = P.O.S. DATE = MMDDYY                                00744100
      ******************************************************************00745000
       01  CC-CONTROL-CARD.                                             00746000
           05  CC-CONTROL-CARD-DISPLAY.                                 00747000
               10  CC-CONTROL-NAME     PIC  X(14) VALUE SPACE.          00748000
               10  CC-POS-DATE-MMDDYY  PIC  9(06) VALUE ZERO.           00749000
               10  FILLER REDEFINES CC-POS-DATE-MMDDYY.                 00749100
                   15  CC-POS-DATE-MM  PIC  99.                         00749200
                   15  CC-POS-DATE-DD  PIC  99.                         00749300
                   15  CC-POS-DATE-YY  PIC  99.                         00749400
           05 FILLER                   PIC  X(60) VALUE SPACE.          00749500
                                                                        00749600
      ******************************************************************00750000
      *  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)             00760000
      ******************************************************************00770000
           COPY DPWS500.                                                00780000
                                                                        00790000
       01  AC-ABEND-CODE                     PIC S9(04) VALUE ZERO      00800000
                                                        COMP SYNC.      00810000
           88  AC-CONTROL-CARD-ERROR                    VALUE +4003.    00820000
           88  AC-DB2-ERROR                             VALUE +4013.    00830000
           88  AC-CALENDAR-ROUTINE-ERROR                VALUE +4019.    00840000
                                                                        00850000
       01  PS-PROGRAM-SWITCHES.                                         00860000
           05  PS-END-OF-LATE-INFO-SW        PIC X(01)  VALUE 'N'.      00870000
               88  PS-END-OF-LATE-INFO-YES              VALUE 'Y'.      00880000
           05  PS-END-OF-TODAY-INFO-SW       PIC X(01)  VALUE 'N'.      00881000
               88  PS-END-OF-TODAY-INFO-YES             VALUE 'Y'.      00882000
           05  PS-END-OF-CARD-FILE-SW        PIC X(01)  VALUE 'N'.      00890000
               88  PS-END-OF-CARD-FILE-YES              VALUE 'Y'.      00900000
           05  PS-ORIG-TRAN-FOUND-SW         PIC X(01)  VALUE 'Y'.      00910000
               88  PS-ORIG-TRAN-FOUND                   VALUE 'Y'.      00920000
               88  PS-ORIG-TRAN-NOT-FOUND               VALUE 'N'.      00930000
                                                                        00940000
       01  PC-CONSTANTS.                                                00950000
           05  PC-NOT-FOUND                PIC S9(04)    VALUE +100     00960000
                                                         COMP SYNC.     00970000
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE          00980000
                                                         'SAKUT155'.    00990000
                                                                        01000000
       01  DK-DB2-KEYS.                                                 01010000
           05  DK-BEGIN-DATE-ISO       PIC X(10)    VALUE SPACES.       01020000
           05  DK-END-DATE-ISO         PIC X(10)    VALUE SPACES.       01030000
                                                                        01040000
       01  PV-MISC-FIELDS.                                              01050000
           05  PV-POS-DATE-CYMD            PIC  9(08)     VALUE ZERO.   01060000
           05  PV-RUN-DATE-CCYY-MM-DD      PIC  X(10)     VALUE SPACE.  01060100
           05  PV-REC-CNT                  PIC  9(07)     VALUE ZERO.   01061000
           05  PV-ORIG-SLS-CORR-SEQ-NBR    PIC S9(4) COMP VALUE ZERO.   01070000
           05  PV-PARAGRAPH-NAME           PIC  X(30)     VALUE SPACE.  01090000
           05  PV-OPERATION-MSG-1          PIC  X(40)     VALUE SPACE.  01100000
           05  PV-OPERATION-MSG-2          PIC  X(40)     VALUE SPACE.  01110000
           05  PV-DB2-OPERATION            PIC  X(30)     VALUE SPACE.  01120000
           05  PV-SAVE-STORE               PIC  9(4)      VALUE ZERO.   01130000
           05  PV-SAVE-STORE-NAME          PIC  X(20)     VALUE SPACES. 01131000
           05  PV-SAVE-TERRITORY           PIC  9(4)      VALUE ZERO.   01140000
           05  PV-DPG52-LK-DATE-INPUT      PIC X(10)      VALUE SPACES. 01150000
                                                                        01160000
      ******************************************************************01170000
      * RECORD LAYOUT FOR OUTPUT SALES CORRECTION CSV FILE.             01180000
      ******************************************************************01190000
       01  SA-CORR-HEADER-RECORD.                                       01200000
           05  SA-HDR-SALES-DATE        PIC X(10) VALUE 'SALES DATE'.   01230000
           05  FILLER                   PIC X     VALUE ','.            01240000
           05  SA-HDR-TERR-NBR          PIC X(9)  VALUE 'TERRITORY'.    01241000
           05  FILLER                   PIC X     VALUE ','.            01242000
           05  SA-HDR-STORE             PIC X(5)  VALUE 'STORE'.        01250000
           05  FILLER                   PIC X     VALUE ','.            01260000
           05  SA-HDR-STORE-NAME        PIC X(20) VALUE 'STORE NAME'.   01261000
           05  FILLER                   PIC X     VALUE ','.            01262000
           05  SA-HDR-REG               PIC X(5)  VALUE 'REG'.          01270000
           05  FILLER                   PIC X     VALUE ','.            01280000
           05  SA-HDR-TRAN              PIC X(5)  VALUE 'TRAN'.         01290000
           05  FILLER                   PIC X     VALUE ','.            01300000
           05  SA-HDR-TRAN-TYPE         PIC X(9)  VALUE 'TRAN TYPE'.    01370000
           05  FILLER                   PIC X     VALUE ','.            01380000
           05  SA-HDR-TRAN-STATUS-CD    PIC X(11) VALUE 'STATUS CODE'.  01381000
           05  FILLER                   PIC X     VALUE ','.            01382000
           05  SA-HDR-OOB-MESSAGE       PIC X(25) VALUE 'ERROR MESSAGE'.01410000
           05  FILLER                   PIC X     VALUE ','.            01420000
                                                                        01490000
       01  SA-CORR-DETAIL-RECORD.                                       01500000
           05  SA-DTL-SALES-DATE        PIC X(10).                      01501000
           05  FILLER                   PIC X     VALUE ','.            01502000
           05  SA-DTL-TERR-NBR          PIC ZZZ9.                       01503000
           05  FILLER                   PIC X     VALUE ','.            01504000
           05  SA-DTL-STORE             PIC 9(4).                       01505000
           05  FILLER                   PIC X     VALUE ','.            01506000
           05  SA-DTL-STORE-NAME        PIC X(20).                      01507000
           05  FILLER                   PIC X     VALUE ','.            01508000
           05  SA-DTL-REG               PIC 9(4).                       01509000
           05  FILLER                   PIC X     VALUE ','.            01509100
           05  SA-DTL-TRAN              PIC 9(4).                       01509200
           05  FILLER                   PIC X     VALUE ','.            01509300
           05  SA-DTL-TRAN-TYPE         PIC X(2).                       01509400
           05  FILLER                   PIC X     VALUE ','.            01509500
           05  SA-DTL-TRAN-STATUS       PIC X.                          01509600
           05  FILLER                   PIC X     VALUE ','.            01509700
           05  SA-DTL-OOB-MESSAGE       PIC X(25).                      01509800
           05  FILLER                   PIC X     VALUE ','.            01509900
                                                                        01790000
                                                                        01800000
      ******************************************************************01810000
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                            01820000
      ******************************************************************01830000
           COPY DPWS004.                                                01840000
                                                                        01850000
      ******************************************************************01860000
      *  DB2 COMMUNICATION AREA.                                        01870000
      ******************************************************************01880000
           EXEC SQL                                                     01890000
                INCLUDE SQLCA                                           01900000
           END-EXEC.                                                    01910000
                                                                        01920000
      ******************************************************************01930000
      *  DB2 TABLE DB2PDBA.XST_LOC - LOCATION TABLE.                    01940000
      ******************************************************************01950000
           EXEC SQL                                                     01960000
                INCLUDE XST00001                                        01970000
           END-EXEC.                                                    01980000
                                                                        01990000
      ******************************************************************02000000
      *  DB2 TABLE DB2PDBA.XST_LOC_DIST - LOCATION / DISTRICT TABLE.    02010000
      ******************************************************************02020000
           EXEC SQL                                                     02030000
                INCLUDE XST00002                                        02040000
           END-EXEC.                                                    02050000
                                                                        02060000
      ******************************************************************02070000
      *  DB2 TABLE DB2PDBA.XST_DIST -  DISTRICT TABLE.                  02080000
      ******************************************************************02090000
           EXEC SQL                                                     02100000
                INCLUDE XST00003                                        02110000
           END-EXEC.                                                    02120000
                                                                        02130000
      ******************************************************************02140000
      *  DB2 TABLE DB2PDBA.XST_DIST_REGN - DISTRICT / REGION TABLE.     02150000
      ******************************************************************02160000
           EXEC SQL                                                     02170000
                INCLUDE XST00004                                        02180000
           END-EXEC.                                                    02190000
                                                                        02200000
      ******************************************************************02210000
      *  DB2 TABLE DB2PDBA.XST_REGN - REGION TABLE.                     02220000
      ******************************************************************02230000
           EXEC SQL                                                     02240000
                INCLUDE XST00005                                        02250000
           END-EXEC.                                                    02260000
                                                                        02270000
      ******************************************************************02280000
      *  DB2 TABLE DB2PDBA.XST_REGN_TERR - REGION / TERRITORY TABLE.    02290000
      ******************************************************************02300000
           EXEC SQL                                                     02310000
                INCLUDE XST00006                                        02320000
           END-EXEC.                                                    02330000
                                                                        02340000
      ******************************************************************02350000
      *  DB2 TABLE DB2PDBA.XST_TERR - TERRITORY TABLE.                  02360000
      ******************************************************************02370000
           EXEC SQL                                                     02380000
                INCLUDE XST00007                                        02390000
           END-EXEC.                                                    02400000
                                                                        02410000
      ******************************************************************02420000
      *  DB2 TABLE DB2PDBA.TEPTRN - TRANSACTION TABLE.                  02430000
      ******************************************************************02440000
           EXEC SQL                                                     02450000
                INCLUDE TEPTRN                                          02460000
           END-EXEC.                                                    02470000
                                                                        02480000
      ******************************************************************02490000
      *  DB2 TABLE DB2PDBA.TEPDKEY - CURRENT PROCESSING DAY KEYS TBL.   02500000
      ******************************************************************02510000
           EXEC SQL                                                     02520000
                INCLUDE TEPDKEY                                         02530000
           END-EXEC.                                                    02540000
                                                                        02550000
      ******************************************************************02630000
      *  DB2 TABLE DB2PDBA.TEPPART - SALES PARTITION TABLE.             02640000
      ******************************************************************02650000
           EXEC SQL                                                     02660000
                INCLUDE TEPPART                                         02670000
           END-EXEC.                                                    02680000
                                                                        02690000
      ******************************************************************02700000
      * LATE-INFO - LOOK FOR LATE OOB TRANSACTIONS PROCESSED TODAY.     02710000
      ******************************************************************02720000
           EXEC SQL                                                     02730000
              DECLARE LATE-INFO CURSOR FOR                              02740000
               SELECT B.PARTN_NBR,                                      02750000
                      B.STR_NBR,                                        02760000
                      B.RGST_ID,                                        02770000
                      B.TRN_NBR,                                        02780000
                      B.STRT_TM,                                        02790000
                      B.SLS_CORR_SEQ_NBR,                               02800000
                      B.VOID_ABRT_CDE,                                  02810000
                      B.TRN_STAT_CDE,                                   02820000
                      B.TRN_TYP_CDE,                                    02830000
                      B.SLS_DTE                                         02850000
                 FROM TEPDKEY  A,                                       02890000
                      TEPTRN   B,                                       02900000
                      TEPPART  D                                        02910000
                WHERE A.PRCG_DTE          = :PV-RUN-DATE-CCYY-MM-DD     02920000
                  AND A.SLS_DTE           = D.SLS_DTE                   02940000
                  AND B.PARTN_NBR         = D.PARTN_NBR                 02950000
                  AND A.STR_NBR           = B.STR_NBR                   02960000
                  AND A.RGST_ID           = B.RGST_ID                   02970000
                  AND A.TRN_NBR           = B.TRN_NBR                   02980000
                  AND A.STRT_TM           = B.STRT_TM                   02990000
                  AND A.SLS_CORR_SEQ_NBR  = B.SLS_CORR_SEQ_NBR          03000000
                  AND B.SLS_CORR_SEQ_NBR IN (00, -01)                   03010001
                  AND B.TRN_STAT_CDE NOT IN('V','Z','L','R','U')        03011000
                  AND B.VOID_ABRT_CDE     = 'N'                         03012000
             ORDER BY SLS_DTE ASC,                                      03020000
                      STR_NBR ASC,                                      03030000
                      TRN_DTE ASC,                                      03040000
                      RGST_ID ASC,                                      03050000
                      TRN_NBR ASC,                                      03060000
                      SLS_CORR_SEQ_NBR DESC                             03070000
                      WITH UR                                           03090000
           END-EXEC.                                                    03100000
                                                                        03110000
      ******************************************************************03112000
      * TODAY-INFO - LOOK FOR TODAY'S OOB TRANSACTIONS.                 03113000
      ******************************************************************03114000
           EXEC SQL                                                     03115000
              DECLARE TODAY-INFO CURSOR FOR                             03116000
               SELECT B.PARTN_NBR,                                      03117000
                      B.STR_NBR,                                        03118000
                      B.RGST_ID,                                        03119000
                      B.TRN_NBR,                                        03119100
                      B.STRT_TM,                                        03119200
                      B.SLS_CORR_SEQ_NBR,                               03119300
                      B.VOID_ABRT_CDE,                                  03119400
                      B.TRN_STAT_CDE,                                   03119500
                      B.TRN_TYP_CDE,                                    03119600
                      B.SLS_DTE                                         03119800
                 FROM TEPTRN   B,                                       03120300
                      TEPPART  D                                        03120400
                WHERE D.SLS_DTE           = :PV-RUN-DATE-CCYY-MM-DD     03120500
                  AND B.PARTN_NBR         = D.PARTN_NBR                 03120700
                  AND B.SLS_CORR_SEQ_NBR IN (00, -01)                   03121301
                  AND B.TRN_STAT_CDE NOT IN('V','Z','L','R','U')        03121400
                  AND B.VOID_ABRT_CDE     = 'N'                         03121500
             ORDER BY SLS_DTE ,                                         03121600
                      STR_NBR ,                                         03121700
                      TRN_DTE ,                                         03121800
                      RGST_ID ,                                         03121900
                      TRN_NBR ,                                         03122000
                      SLS_CORR_SEQ_NBR DESC                             03122100
                      WITH UR                                           03122200
           END-EXEC.                                                    03122300
                                                                        03122400
      ***************************************************************** 03123000
       PROCEDURE DIVISION.                                              03130000
      ******************************************************************03140000
      * 0000-MAINLINE-PARAGRAPH                                         03150000
      *      PRODUCE SALES AUDIT CORRECTIONS FILE                       03160000
      ******************************************************************03170000
       0000-MAINLINE-PARAGRAPH.                                         03180000
                                                                        03190000
           PERFORM 1000-HOUSE-KEEPING.                                  03200000
                                                                        03210000
           PERFORM 2000-OPEN-LATE-INFO.                                 03220000
                                                                        03230000
           PERFORM 2200-FETCH-LATE-INFO                                 03240000
             UNTIL PS-END-OF-LATE-INFO-YES.                             03250000
                                                                        03260000
           PERFORM 2400-CLOSE-LATE-INFO.                                03270000
                                                                        03280000
           PERFORM 3000-OPEN-TODAY-INFO.                                03281000
                                                                        03282000
           PERFORM 3200-FETCH-TODAY-INFO                                03283000
             UNTIL PS-END-OF-TODAY-INFO-YES.                            03284000
                                                                        03285000
           PERFORM 3400-CLOSE-TODAY-INFO.                               03286000
                                                                        03287000
           DISPLAY 'SAKUT155 OUTPUT RECORDS INCLUDING HEADER = '        03290000
                    PV-REC-CNT.                                         03300000
                                                                        03310000
           STOP RUN.                                                    03320000
                                                                        03330000
      ******************************************************************03340000
       1000-HOUSE-KEEPING.                                              03350000
      *     OPEN FILES, PROCESS CONTROL CARD, FORMAT REPORT HEADINGS.   03360000
      ******************************************************************03370000
                                                                        03380000
           OPEN INPUT  POS-DATE-FILE                                    03390000
                OUTPUT SALES-OUT-OF-BALANCE-CSV-FILE.                   03400000
                                                                        03410000
           PERFORM 1100-PROCESS-CONTROL-CARD.                           03420000
                                                                        03430000
           PERFORM 1200-WRITE-HEADER-RECORD.                            03440000
                                                                        03450000
      ******************************************************************03460000
       1100-PROCESS-CONTROL-CARD.                                       03470000
      *     READ CONTROL CARD TO GET START AND END DATES.               03480000
      *     CALL THE DATE ROUTINE TO VALIDATE BOTH.                     03490000
      ******************************************************************03500000
                                                                        03510000
           PERFORM 1110-READ-DATE-CTLCD-FILE.                           03520000
           CLOSE POS-DATE-FILE.                                         03530000
           IF PS-END-OF-CARD-FILE-YES                                   03540000
               MOVE '1100-PROCESS-CONTROL-CARD'  TO PV-PARAGRAPH-NAME   03550000
               MOVE 'NO CONTROL CARD TO PROCESS' TO PV-OPERATION-MSG-1  03560000
               SET AC-CONTROL-CARD-ERROR TO TRUE                        03570000
               PERFORM 9999-ABEND                                       03580000
           END-IF.                                                      03590000
           DISPLAY PC-CURRENT-PROGRAM-NAME                              03600000
                   ' - CONTROL CARD = (' CC-CONTROL-CARD-DISPLAY  ')'.  03610000
                                                                        03620000
      ******************************************************************03630000
       1110-READ-DATE-CTLCD-FILE.                                       03640000
      *     READ DATE CONTROL CARD.                                     03650000
      *     CALL THE DATE ROUTINE TO VALIDATE START AND END DATES.      03660000
      ******************************************************************03670000
                                                                        03680000
           READ POS-DATE-FILE          INTO CC-CONTROL-CARD             03690000
              AT END                                                    03700000
                 SET PS-END-OF-CARD-FILE-YES TO TRUE                    03710000
              NOT AT END                                                03720000
                  MOVE CC-POS-DATE-MMDDYY   TO PV-DPG52-LK-DATE-INPUT   03770000
                  PERFORM 1125-VALIDATE-INPUT-DATE.                     03780000
                                                                        03800000
                                                                        03810000
      ******************************************************************03820000
      * CALL KOHLS CALENDAR ROUTINE:                                    03830000
      * VALIDATE INPUT DATE AND CONVERT TO CCYY-MM-DD FORMAT.           03831000
      *     PASS: CONTROL CARD POS DATE (MM-DD-YY FORMAT).              03840000
      *   RETURN: ACTUAL CALENDAR DB2 ISO DATE (CCYY-MM-DD FORMAT).     03850000
      ******************************************************************03860000
       1125-VALIDATE-INPUT-DATE.                                        03870000
                                                                        03880000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03890000
                                                                        03900000
           SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   03910000
           SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   03920000
           SET  DPG52-LK-DTE-GREG            TO TRUE.                   03930000
                                                                        03940000
           MOVE CC-POS-DATE-MMDDYY           TO DPG52-LK-DATE-INPUT.    03950000
                                                                        03960000
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    03970000
                                                   DPG54 DPG55 DPG56.   03980000
           EVALUATE TRUE                                                03990000
               WHEN DPG54-SEVERE-ERROR                                  04000000
               WHEN DPG54-DATE-INVALID                                  04010000
                   MOVE '1125-VALIDATE-INPUT-DATE'                      04020000
                                             TO PV-PARAGRAPH-NAME       04030000
                   MOVE 'ERROR CONVERTING INPUT CARD DATE'              04040000
                                             TO PV-OPERATION-MSG-1      04050000
                   SET AC-CALENDAR-ROUTINE-ERROR  TO TRUE               04060000
                   MOVE DPG54-ERROR-MESSAGE  TO PV-OPERATION-MSG-2      04080000
                   PERFORM 9999-ABEND                                   04100000
           END-EVALUATE.                                                04110000
                                                                        04111000
           MOVE DPG55-DB2-ISO-DATE-FMT     TO PV-RUN-DATE-CCYY-MM-DD.   04120000
                                                                        04130000
      ******************************************************************04140000
       1200-WRITE-HEADER-RECORD.                                        04150000
      ******************************************************************04160000
                                                                        04170000
           WRITE SALES-OUT-OF-BALANCE-CSV-REC                           04180000
               FROM SA-CORR-HEADER-RECORD.                              04190000
                                                                        04200000
           ADD +1   TO PV-REC-CNT.                                      04210000
                                                                        04220000
      ******************************************************************04230000
       2000-OPEN-LATE-INFO.                                             04240000
      *     GET THE CURRENT PROCESSING DAY'S LATE TRANSACTIONS.         04250000
      ******************************************************************04260000
                                                                        04270000
           EXEC SQL                                                     04280000
                OPEN LATE-INFO                                          04290000
           END-EXEC.                                                    04300000
                                                                        04310000
           IF NOT SQLCODE = ZERO                                        04320000
              MOVE '2000-OPEN-LATE-INFO'    TO PV-PARAGRAPH-NAME        04330000
              MOVE SQLCODE                  TO PV-DB2-OPERATION         04340000
              PERFORM 9100-SQL-ABEND                                    04350000
           END-IF.                                                      04360000
                                                                        04370000
      ******************************************************************04380000
       2200-FETCH-LATE-INFO.                                            04390000
      ******************************************************************04400000
                                                                        04410000
           EXEC SQL                                                     04420000
                FETCH LATE-INFO                                         04430000
                 INTO :EPTRN-PARTN-NBR,                                 04440000
                      :EPTRN-STR-NBR,                                   04450000
                      :EPTRN-RGST-ID,                                   04460000
                      :EPTRN-TRN-NBR,                                   04470000
                      :EPTRN-STRT-TM,                                   04480000
                      :EPTRN-SLS-CORR-SEQ-NBR,                          04490000
                      :EPTRN-VOID-ABRT-CDE,                             04500000
                      :EPTRN-TRN-STAT-CDE,                              04510000
                      :EPTRN-TRN-TYP-CDE,                               04520000
                      :EPTRN-SLS-DTE                                    04540000
           END-EXEC.                                                    04580000
                                                                        04590000
           EVALUATE TRUE                                                04600000
              WHEN SQLCODE = ZERO                                       04610000
                   IF EPTRN-STR-NBR = PV-SAVE-STORE                     04640000
                       CONTINUE                                         04650000
                   ELSE                                                 04660000
                       PERFORM 4600-SELECT-TERRITORY                    04670000
                       MOVE EPTRN-STR-NBR TO PV-SAVE-STORE              04680000
                   END-IF                                               04690000
                   PERFORM 6000-FORMAT-DETAIL-RECORD                    04700000
              WHEN SQLCODE = PC-NOT-FOUND                               04710000
                   SET PS-END-OF-LATE-INFO-YES     TO TRUE              04720000
              WHEN OTHER                                                04730000
                   MOVE '2200-FETCH-LATE-INFO'     TO PV-PARAGRAPH-NAME 04740000
                   MOVE SQLCODE                    TO PV-DB2-OPERATION  04750000
                   PERFORM 9100-SQL-ABEND                               04760000
           END-EVALUATE.                                                04770000
                                                                        04780000
      ******************************************************************04790000
       2400-CLOSE-LATE-INFO.                                            04800000
      ******************************************************************04810000
                                                                        04820000
           EXEC SQL                                                     04830000
                CLOSE LATE-INFO                                         04840000
           END-EXEC.                                                    04850000
                                                                        04860000
           IF NOT SQLCODE = ZERO                                        04870000
              MOVE '2400-CLOSE-LATE-INFO'    TO PV-PARAGRAPH-NAME       04880000
              MOVE SQLCODE                   TO PV-DB2-OPERATION        04890000
              PERFORM 9100-SQL-ABEND                                    04900000
           END-IF.                                                      04910000
                                                                        04920000
                                                                        04921000
      ******************************************************************04922000
       3000-OPEN-TODAY-INFO.                                            04923000
      *     GET THE CURRENT PROCESSING DAY'S TRANSACTIONS.              04924000
      ******************************************************************04925000
                                                                        04926000
           EXEC SQL                                                     04927000
                OPEN TODAY-INFO                                         04928000
           END-EXEC.                                                    04929000
                                                                        04929100
           IF NOT SQLCODE = ZERO                                        04929200
              MOVE '3000-OPEN-TODAY-INFO'   TO PV-PARAGRAPH-NAME        04929300
              MOVE SQLCODE                  TO PV-DB2-OPERATION         04929400
              PERFORM 9100-SQL-ABEND                                    04929500
           END-IF.                                                      04929600
                                                                        04929700
      ******************************************************************04929800
       3200-FETCH-TODAY-INFO.                                           04929900
      ******************************************************************04930000
                                                                        04930100
           EXEC SQL                                                     04930200
                FETCH TODAY-INFO                                        04930300
                 INTO :EPTRN-PARTN-NBR,                                 04931800
                      :EPTRN-STR-NBR,                                   04931900
                      :EPTRN-RGST-ID,                                   04932000
                      :EPTRN-TRN-NBR,                                   04932100
                      :EPTRN-STRT-TM,                                   04932200
                      :EPTRN-SLS-CORR-SEQ-NBR,                          04932300
                      :EPTRN-VOID-ABRT-CDE,                             04932400
                      :EPTRN-TRN-STAT-CDE,                              04932500
                      :EPTRN-TRN-TYP-CDE,                               04932600
                      :EPTRN-SLS-DTE                                    04932700
           END-EXEC.                                                    04932800
                                                                        04932900
           EVALUATE TRUE                                                04933000
              WHEN SQLCODE = ZERO                                       04933100
                   IF EPTRN-STR-NBR = PV-SAVE-STORE                     04933400
                       CONTINUE                                         04933500
                   ELSE                                                 04933600
                       PERFORM 4600-SELECT-TERRITORY                    04933700
                       MOVE EPTRN-STR-NBR TO PV-SAVE-STORE              04933800
                   END-IF                                               04933900
                   PERFORM 6000-FORMAT-DETAIL-RECORD                    04934000
              WHEN SQLCODE = PC-NOT-FOUND                               04934100
                   SET PS-END-OF-TODAY-INFO-YES    TO TRUE              04934200
              WHEN OTHER                                                04934300
                   MOVE '3200-FETCH-TODAY-INFO'    TO PV-PARAGRAPH-NAME 04934400
                   MOVE SQLCODE                    TO PV-DB2-OPERATION  04934500
                   PERFORM 9100-SQL-ABEND                               04934600
           END-EVALUATE.                                                04934700
                                                                        04934800
      ******************************************************************04934900
       3400-CLOSE-TODAY-INFO.                                           04935000
      ******************************************************************04935100
                                                                        04935200
           EXEC SQL                                                     04935300
                CLOSE TODAY-INFO                                        04935400
           END-EXEC.                                                    04935500
                                                                        04935600
           IF NOT SQLCODE = ZERO                                        04935700
              MOVE '3400-CLOSE-TODAY-INFO'   TO PV-PARAGRAPH-NAME       04935800
              MOVE SQLCODE                   TO PV-DB2-OPERATION        04935900
              PERFORM 9100-SQL-ABEND                                    04936000
           END-IF.                                                      04936100
                                                                        04936200
      ******************************************************************05650000
       4600-SELECT-TERRITORY.                                           05660000
      ******************************************************************05670000
                                                                        05680000
           EXEC SQL                                                     05690000
               SELECT  LOC.LOC_NM                                       05700000
                      ,DIST.DIST_NBR                                    05701000
                      ,DIST.DIST_NM                                     05710000
                      ,REGN.REGN_NBR                                    05720000
                      ,REGN.REGN_NM                                     05730000
                      ,TERR.TERR_NBR                                    05740000
                      ,TERR.TERR_NM                                     05750000
                 INTO :XST00001-LOC-NM                                  05760000
                    , :XST00003-DIST-NBR                                05761000
                    , :XST00003-DIST-NM                                 05770000
                    , :XST00005-REGN-NBR                                05780000
                    , :XST00005-REGN-NM                                 05790000
                    , :XST00007-TERR-NBR                                05800000
                    , :XST00007-TERR-NM                                 05810000
                 FROM  XST_LOC             LOC                          05820000
                      ,XST_LOC_DIST        LD                           05821000
                      ,XST_DIST            DIST                         05830000
                      ,XST_DIST_REGN       DR                           05840000
                      ,XST_REGN            REGN                         05850000
                      ,XST_REGN_TERR       RT                           05860000
                      ,XST_TERR            TERR                         05870000
                WHERE  LOC.LOC_NBR          = :EPTRN-STR-NBR            05880000
                  AND  LOC.OPN_DTE         <= CURRENT DATE              05880100
                  AND (LOC.CLO_DTE         >= CURRENT DATE - 1 DAY      05880200
                   OR  LOC.CLO_DTE         IS NULL)                     05880300
                                                                        05880400
                  AND  LD.LOC_NBR           = LOC.LOC_NBR               05881000
                  AND  LD.BEG_DTE          <= CURRENT DATE              05890000
                  AND (LD.END_DTE          >= CURRENT DATE - 1 DAY      05900000
                   OR  LD.END_DTE          IS NULL)                     05910000
                                                                        05920000
                  AND  LD.DIST_NBR          = DIST.DIST_NBR             05930000
                  AND  DIST.DIST_NBR        = DR.DIST_NBR               05940000
                  AND  DR.BEG_DTE          <= CURRENT DATE              05950000
                  AND (DR.END_DTE          >= CURRENT DATE - 1 DAY      05960000
                   OR  DR.END_DTE          IS NULL)                     05970000
                                                                        05980000
                  AND  DR.REGN_NBR          = REGN.REGN_NBR             05990000
                  AND  REGN.REGN_NBR        = RT.REGN_NBR               06000000
                  AND  RT.BEG_DTE          <= CURRENT DATE              06010000
                  AND (RT.END_DTE          >= CURRENT DATE - 1 DAY      06020000
                   OR  RT.END_DTE          IS NULL)                     06030000
                                                                        06040000
                  AND  RT.TERR_NBR          = TERR.TERR_NBR             06050000
                                                                        06060000
                WITH UR                                                 06070000
           END-EXEC.                                                    06080000
                                                                        06090000
           EVALUATE SQLCODE                                             06100000
              WHEN ZERO                                                 06110000
                 MOVE XST00001-LOC-NM   TO  PV-SAVE-STORE-NAME          06120000
                 MOVE XST00007-TERR-NBR TO  PV-SAVE-TERRITORY           06121000
              WHEN PC-NOT-FOUND                                         06130000
                 MOVE SPACES            TO  PV-SAVE-STORE-NAME          06140000
                 MOVE ZEROES            TO  PV-SAVE-TERRITORY           06141000
              WHEN OTHER                                                06150000
                 MOVE '4600-SELECT-TERRITORY'     TO PV-PARAGRAPH-NAME  06160000
                 MOVE SQLCODE                     TO PV-DB2-OPERATION   06170000
                 PERFORM 9100-SQL-ABEND                                 06180000
           END-EVALUATE.                                                06190000
                                                                        06200000
                                                                        06210000
      ******************************************************************06220000
       6000-FORMAT-DETAIL-RECORD.                                       06230000
      *     FORMAT SALES CORRECTION DETAIL RECORD.                      06240000
      ******************************************************************06250000
                                                                        06260000
           MOVE EPTRN-SLS-DTE            TO SA-DTL-SALES-DATE.          06280000
           MOVE PV-SAVE-TERRITORY        TO SA-DTL-TERR-NBR.            06281000
           MOVE EPTRN-STR-NBR            TO SA-DTL-STORE.               06290000
           MOVE PV-SAVE-STORE-NAME       TO SA-DTL-STORE-NAME.          06291000
           MOVE EPTRN-RGST-ID            TO SA-DTL-REG.                 06300000
           MOVE EPTRN-TRN-NBR            TO SA-DTL-TRAN.                06310000
           MOVE EPTRN-TRN-TYP-CDE        TO SA-DTL-TRAN-TYPE.           06350000
           MOVE EPTRN-TRN-STAT-CDE       TO SA-DTL-TRAN-STATUS.         06351000
           PERFORM 6100-EVALUATE-TRN-STAT-CDE.                          06390000
                                                                        06460000
           PERFORM 8200-WRITE-DETAIL-RECORD.                            06470000
                                                                        06480000
      ******************************************************************06490000
       6100-EVALUATE-TRN-STAT-CDE.                                      06500000
      *     CONVERT TRN-STAT-CDE TO A LITERAL.                          06510000
      ******************************************************************06520000
                                                                        06530000
           EVALUATE TRUE                                                06540000
             WHEN EPTRN-TRN-STAT-CDE = 'A'                              06550000
               MOVE 'INVALID ACCT NBR LENGTH'   TO SA-DTL-OOB-MESSAGE   06560000
             WHEN EPTRN-TRN-STAT-CDE = 'B'                              06570000
               MOVE 'TENDER OUT OF BALANCE'     TO SA-DTL-OOB-MESSAGE   06580000
             WHEN EPTRN-TRN-STAT-CDE = 'C'                              06590000
               MOVE 'INVALID ACCT NBR CHKDIGIT' TO SA-DTL-OOB-MESSAGE   06600000
             WHEN EPTRN-TRN-STAT-CDE = 'D'                              06610000
               MOVE 'DB2 ERROR'                 TO SA-DTL-OOB-MESSAGE   06620000
             WHEN EPTRN-TRN-STAT-CDE = 'E'                              06630000
               MOVE 'ERROR'                     TO SA-DTL-OOB-MESSAGE   06640000
             WHEN EPTRN-TRN-STAT-CDE = 'H'                              06650000
               MOVE 'HARD TOTAL INCOMPLETE'     TO SA-DTL-OOB-MESSAGE   06660000
             WHEN EPTRN-TRN-STAT-CDE = 'I'                              06670000
               MOVE 'INVALID STORE NBR'         TO SA-DTL-OOB-MESSAGE   06680000
             WHEN EPTRN-TRN-STAT-CDE = 'L'                              06690000
               MOVE 'LATE DATA COLLECT'         TO SA-DTL-OOB-MESSAGE   06700000
             WHEN EPTRN-TRN-STAT-CDE = 'M'                              06710000
               MOVE 'MISSING ACCT NBR'          TO SA-DTL-OOB-MESSAGE   06720000
             WHEN EPTRN-TRN-STAT-CDE = 'N'                              06730000
               MOVE 'NON-NUMERIC ACCT NBR'      TO SA-DTL-OOB-MESSAGE   06740000
             WHEN EPTRN-TRN-STAT-CDE = 'O'                              06750000
               MOVE 'ITEM OUT OF BALANCE'       TO SA-DTL-OOB-MESSAGE   06760000
             WHEN EPTRN-TRN-STAT-CDE = 'P'                              06770000
               MOVE 'INVALID ACCT NBR PREFIX'   TO SA-DTL-OOB-MESSAGE   06780000
             WHEN EPTRN-TRN-STAT-CDE = 'R'                              06790000
               MOVE 'REVERSAL ENTRY'            TO SA-DTL-OOB-MESSAGE   06800000
             WHEN EPTRN-TRN-STAT-CDE = 'S'                              06810000
               MOVE 'MISSING TOTAL'             TO SA-DTL-OOB-MESSAGE   06820000
             WHEN EPTRN-TRN-STAT-CDE = 'T'                              06830000
               MOVE 'MISSING TENDER'            TO SA-DTL-OOB-MESSAGE   06840000
             WHEN EPTRN-TRN-STAT-CDE = 'U'                              06850000
               MOVE 'SUSPENDED TRAN'            TO SA-DTL-OOB-MESSAGE   06860000
             WHEN EPTRN-TRN-STAT-CDE = 'V'                              06870000
               MOVE 'VALID SALES CORRECTION'    TO SA-DTL-OOB-MESSAGE   06880000
             WHEN EPTRN-TRN-STAT-CDE = 'W'                              06890000
               MOVE 'INVALID SALES CORRECTION'  TO SA-DTL-OOB-MESSAGE   06900000
             WHEN EPTRN-TRN-STAT-CDE = 'Z'                              06910000
               MOVE 'VALID SALES TRANSACTION'   TO SA-DTL-OOB-MESSAGE   06920000
             WHEN OTHER                                                 06930000
                MOVE EPTRN-TRN-STAT-CDE         TO SA-DTL-OOB-MESSAGE   06940000
           END-EVALUATE.                                                06950000
                                                                        06960000
      ******************************************************************06970000
       8200-WRITE-DETAIL-RECORD.                                        06980000
      *     WRITE REPORT HEADINGS OTHER THAN THE FIRST LINE.            06990000
      ******************************************************************07000000
                                                                        07010000
           WRITE SALES-OUT-OF-BALANCE-CSV-REC                           07020000
               FROM SA-CORR-DETAIL-RECORD.                              07030000
                                                                        07040000
           ADD +1   TO PV-REC-CNT.                                      07050000
                                                                        07060000
                                                                        07070000
      ******************************************************************07080000
       9100-SQL-ABEND.                                                  07090000
      *     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.07100000
      ******************************************************************07110000
           DISPLAY '** ABEND     **'.                                   07120000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        07130000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              07140000
           DISPLAY '** SQLCODE   ** = ' PV-DB2-OPERATION.               07150000
           DISPLAY '** STORE NBR ** = ' EPTRN-STR-NBR.                  07160000
           DISPLAY '** REGISTER  ** = ' EPTRN-RGST-ID.                  07170000
           DISPLAY '** TRAN NBR  ** = ' EPTRN-TRN-NBR.                  07180000
                                                                        07190000
      ******************************************************************07200000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     07210000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      07220000
      ******************************************************************07230000
           COPY DPPD004.                                                07240000
                                                                        07250000
           SET AC-DB2-ERROR TO TRUE.                                    07260000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         07270000
                                                                        07280000
      ******************************************************************07290000
       9999-ABEND.                                                      07300000
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  07310000
      ******************************************************************07320000
           DISPLAY '** ABEND           **'.                             07330000
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  07340000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        07350000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       07360000
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       07370000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         07380000
      ******************************************************************07390000
      ******************************************************************07400000
