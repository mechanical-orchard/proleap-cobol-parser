       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT178.                                       
       AUTHOR.                  MARIA KLAMIK.                                   
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            05/18/99.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      *             INVENTORY VARIANCE EXTRACT                         *        
      *                                                                *        
      *   THIS PROGRAM HAS BEEN RE-WRITTEN AS OF 09/01/2004.           *        
      *                                                                *        
      *   THIS PROGRAM WILL READ IN A FILE CONTAINING A LIST OF STORES *        
      *   FOR THE CURRENT INVENTORY RUN.  THE PROGRAM WILL RETRIEVE    *        
      *   TSUMINV DATA BY LOCATION, SKU, UNIT RETAIL                   *        
      *   A HEADER RECORD WILL ALSO BE CREATED - SUMMING QTY AND       *        
      *   EXTENDED RETAIL - AND WILL BE ASSIGNED AN AREA '000000'      *        
      *   HIERARCHY INFORMATION (MBS DATA) IS NO LONGER RETRIEVED      *        
      *   JOB - INK302 AND PROC - INK3021.                             *        
      *                                                                *        
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
      * WR26600  09/01/2004  INITIAL INSTALL OF REVISED PROCESS                 
      *                      - SCOTT JACKSON                                    
      * WR27710  12/17/2004  CURSOR MODIFICATION TO INCLUDE TINENTY DATA        
      *                      - ERIN SEARL                                       
      * WR28545  08/10/2006  DEPARTMENT INVENTORY PROJECT CHANGES               
      *                      - B.GRAHAM - STRATAGEM                             
      * WR33304  08/18/2008  HILO CHANGES                                       
      *                      - P.JAEGER - CODEWORKS                             
SR3403* WR03403  04/06/2012  RECOMPILE-LARGE RETAIL PROJECT                     
SR3403*                      - ANUJ GUPTA                                       
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT STORE-PROCESS-FILE-IN                                         
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT STORE-INV-FILE-OUT                                            
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  STORE-PROCESS-FILE-IN                                                
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-PROCESS-REC     PIC X(080).                                    
                                                                                
       FD  STORE-INV-FILE-OUT                                                   
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-INV-REC         PIC X(100).                                    
                                                                                
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PROGRAM-NAME         PIC X(8)        VALUE 'INKUT178'.        
           05  PV-PARAGRAPH-NAME       PIC X(25)       VALUE SPACE.             
           05  PV-ABEND-MESSAGE        PIC X(50)       VALUE SPACE.             
           05  PV-SQL-SUB              PIC 9(03)       VALUE ZERO.      INKUT102
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)       VALUE SPACES.            
           05  PV-OUTPUT-RECS          PIC 9(8)        VALUE ZERO.              
           05  PV-HEADER-RECS          PIC 9(8)        VALUE ZERO.              
           05  PV-ERROR-RECS           PIC 9(8)        VALUE ZERO.              
           05  PV-DEPT-NBR             PIC 9(04)       VALUE ZERO.              
           05  PV-MAJOR-CLASS-NBR      PIC 9(04)       VALUE ZERO.              
           05  PV-HOLD-MAJOR-CLASS     PIC 9(04)       VALUE ZERO.              
           05  PV-HOLD-AREA-NBR        PIC S9(09) COMP VALUE +0.                
           05  PV-HOLD-DEPT-NBR        PIC 9(04)       VALUE ZERO.              
           05  PV-HOLD-SUB-CLASS       PIC 9(04)       VALUE ZERO.              
           05  PV-HOLD-SKU-NBR         PIC 9(08)       VALUE ZEROS.             
           05  PV-HOLD-LOC-NBR         PIC S9(4) COMP.                          
           05  PV-DISPLAY-LOC-NBR      PIC 9(4)        VALUE ZERO.              
           05  PV-HOLD-INV-UNIT-PR-AMT PIC S9(5)V9(2) COMP-3.                   
           05  PV-STR-NDX              PIC S9(05) COMP-3 VALUE ZEROS.           
           05  PV-LOC-NUMBER           PIC S9(04) COMP.                         
           05  PV-UNT-RTL-AMT          PIC S9(7)V9(2) COMP-3.                   
           05  PV-ALT-SUMINV-DEF.                                               
               10  PV-SUMINV-LOC       PIC S9(04) COMP.                         
               10  PV-SUMINV-AREA      PIC S9(09) COMP.                         
               10  PV-SUMINV-SKU       PIC S9(08)V COMP-3.                      
               10  PV-SUMINV-PRICE     PIC S9(05)V9(02) COMP-3.                 
               10  PV-SUMINV-QTY       PIC S9(09) COMP.                         
               10  PV-SUMINV-DEPT      PIC S9(04) COMP-3.                       
               10  PV-SUMINV-SUB-CLASS PIC S9(04) COMP-3.                       
               10  PV-SUMINV-MAJ-CLASS PIC S9(04) COMP-3.                       
           05  PV-AREA-HEADER-TOTAL.                                            
               10  PV-AREA-000000-QTY      PIC S9(05)     VALUE +0 COMP.        
               10  PV-AREA-000000-RTL      PIC S9(05)V99  VALUE +0 COMP.        
               10  PV-AREA-000000-TEMP     PIC S9(07)V99  VALUE +0 COMP.        
               10  PV-AREA-000000-EXTD-RTL PIC S9(11)V99  VALUE +0 COMP.        
           05  PV-GROUP-UNIT-RTL           PIC S9(07)V99  VALUE +0 COMP.        
           05  PV-VARIANCE-RTL.                                                 
               10  PV-VARIANCE-PRICE       PIC S9(11)V99  VALUE +0 COMP.        
               10  PV-ABSOLUTE-VAR-PRICE   PIC S9(11)V99  VALUE +0 COMP.        
           05  PV-TMST.                                                         
               10  PV-PLND-INV-DTE        PIC  X(10) VALUE SPACES.              
               10  FILLER                 PIC  X(16) VALUE                      
                                                 '-23.59.59.999999'.            
                                                                                
           05  PV-PLND-INV-TMST           PIC  X(26) VALUE SPACES.              
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-APLSYS-CDE           PIC X(02)      VALUE 'IN'.               
           05  PC-OPERCO-NBR           PIC X(02)      VALUE '03'.               
           05  PC-MCL-CDE              PIC X(03)      VALUE 'MCL'.              
           05  PC-SUB-CLASS-NBR        PIC X(03)      VALUE ZEROES.             
           05  PC-AREA-000000          PIC X(06)      VALUE '000000'.           
           05  PC-NEG-VALUE            PIC S9(01)     VALUE -1.                 
           05  PC-DEFAULT-LOC          PIC S9(04)     VALUE +0 COMP.            
           05  PC-MAX-RETRIES          PIC S9(03)     VALUE +03         INKUT102
                                                      COMP-3.           INKUT102
                                                                                
       01  SA-SAVE-AREA.                                                        
           05  SA-STORE-NBR            PIC 9(04)      VALUE ZERO.               
                                                                                
       01  PS-SWITCHES.                                                         
           05  PS-BYPASS-SKU-SW            PIC X      VALUE 'N'.                
               88  PS-BYPASS-SKU                      VALUE 'Y'.                
               88  PS-PROCESS-SKU                     VALUE 'N'.                
           05  PS-FIRST-STORE-RECORD-SW    PIC X      VALUE 'N'.                
               88  PS-FIRST-STORE-RECORD              VALUE 'Y'.                
           05  END-OF-RECS-IN-SW           PIC X      VALUE 'N'.                
               88  END-OF-RECS                        VALUE 'Y'.                
           05  END-OF-CURSOR-SW            PIC X      VALUE 'N'.                
               88  END-OF-CURSOR                      VALUE 'Y'.                
           05  PS-TINVPAR-DONE-SW         PIC X(01)    VALUE 'N'.               
               88  TINVPAR-PROCESS-COMPLETE            VALUE 'Y'.               
           05  PS-ITEM-FOUND-SW           PIC X(01)    VALUE 'N'.               
               88  PS-ITEM-FOUND                       VALUE 'Y'.               
               88  PS-NOT-ITEM-FOUND                   VALUE 'N'.               
                                                                                
       01  ABEND-CODE                  PIC S9(04)     VALUE +4003               
                                                      COMP SYNC.                
                                                                                
       01  WS-TINVPAR-TABLE.                                                    
           05  WS-TINVPAR-TABLE-ELEMENTS   OCCURS 9999 TIMES.                   
               10  WS-PLND-INV-DTE  PIC X(10)  VALUE SPACES.                    
               10  WS-MFSTNG-IND    PIC X(01)  VALUE SPACES.                    
                                                                                
       01  WS-EDIT-STORE-ALPH       PIC X(04)  VALUE ZEROS.                     
       01  WS-EDIT-STORE-NUM REDEFINES                                          
           WS-EDIT-STORE-ALPH       PIC 9(04).                                  
      *****************************************************************         
      *  STORE PROCESS FILE LAYOUT - INPUT                                      
      *****************************************************************         
           COPY INWS004.                                                        
                                                                                
      *****************************************************************         
      *  INVENTORY EXCEPTIONS BY STORE EXTRACT FILE LAYOUT - OUTPUT             
      *****************************************************************         
           COPY INRD024.                                                        
                                                                                
      *****************************************************************         
      *  COPYBOOK FOR NI-SKU FIELDS.                                            
      *****************************************************************         
           COPY ISWS006.                                                        
                                                                                
      *****************************************************************         
      * DB2 SUM INVENTORY TABLE                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSUMINV                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 TINENTY TABLE                                                       
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINENTY                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 TSKXREF TABLE                                                       
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
28545 *****************************************************************         
28545 * TINCNTL DCLGEN                                                          
28545 *****************************************************************         
28545      EXEC SQL                                                             
28545          INCLUDE TINCNTL                                                  
28545      END-EXEC.                                                            
28545                                                                           
      *****************************************************************         
      * TINVPAR DCLGEN                                                          
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 STANDARD COPYBOOK                                                   
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * TSUMINV CURSOR                                                          
      * THIS CURSOR WILL RETRIEVE ROWS FROM TSUMINV                             
      *****************************************************************         
           EXEC SQL                                                             
               DECLARE TSUMINV_CSR CURSOR FOR                                   
W33304          SELECT   A.LOC_NBR                                              
W33304                  ,A.AREA_NBR                                             
W33304                  ,A.SKU_NBR                                              
W33304                  ,A.UPC_NBR                                              
W33304                  ,A.INV_UNIT_PR_AMT                                      
W33304                  ,A.INV_QTY                                              
W33304                  ,A.DEPT_NBR                                             
W33304                  ,A.MAJ_CL_NBR                                           
W33304                  ,A.SUB_CL_NBR                                           
                        ,' '                                                    
W33304            FROM  TSUMINV A                                               
W33304                 ,TINVPAR C                                               
W33304                 ,TINCNTL D                                               
W33304           WHERE  A.LOC_NBR             =  :SUMINV-LOC-NBR                
W33304             AND  A.LOC_NBR             =  C.LOC_NBR                      
W33304             AND  C.ACTL_FIN_BK_DTE     = '9999-09-09'                    
W33304             AND  C.LOC_INV_STAT_CDE    = 'IN'                            
W33304             AND  C.SHEET_INV_CPLD_IND <> 'Y'                             
W33304             AND  C.INV_ID              = D.INV_ID                        
W33304             AND  D.ACTV_IND            = 'Y'                             
                                                                                
27710           UNION                                                           
27710                                                                           
27710           SELECT   A.STR_NBR                                              
27710                   ,SHEET_NBR                                              
27710                   ,SKU_NBR                                                
S27710                  ,UPC_NBR                                                
27710                   ,UNT_RTL_AMT                                            
27710                   ,INV_QTY                                                
27710                   ,DEPT                                                   
27710                   ,CLASS                                                  
27710                   ,SUBCLASS                                               
27710                   ,SHEET_INV_CPLD_IND                                     
27710             FROM  TINENTY A                                               
27710                  ,TSKXREF B                                               
27710                  ,TINVPAR C                                               
28545                  ,TINCNTL D                                               
27710            WHERE  STR_NBR    =  :SUMINV-LOC-NBR                           
27710              AND  A.SKU_NBR    =  B.SKU                                   
27710              AND  A.STR_NBR    =  C.LOC_NBR                               
                   AND  C.ACTL_FIN_BK_DTE = '9999-09-09'                        
W33304             AND  C.LOC_INV_STAT_CDE    = 'IN'                            
                   AND  C.SHEET_INV_CPLD_IND <> 'Y'                             
28545              AND  C.INV_ID = D.INV_ID                                     
28545              AND  D.ACTV_IND = 'Y'                                        
                ORDER BY 1, 3, 4                                                
                FOR FETCH ONLY                                                  
           END-EXEC.                                                            
                                                                                
                                                                                
      ******************************************************************        
      * TINVPAR CURSOR.                                                         
      ******************************************************************        
           EXEC SQL                                                             
             DECLARE TINVPAR_CURSOR CURSOR FOR                                  
28545          SELECT A.LOC_NBR                                                 
28545                ,A.PLND_INV_DTE                                            
28545                ,A.MFSTNG_IND                                              
               FROM TINVPAR A                                                   
28545              ,TINCNTL B                                                   
28545          WHERE A.ACTL_FIN_BK_DTE = '9999-09-09'                           
W33304           AND A.LOC_INV_STAT_CDE = 'IN'                                  
28545            AND A.INV_ID = B.INV_ID                                        
28545            AND B.ACTV_IND = 'Y'                                           
               FOR FETCH ONLY                                                   
           END-EXEC                                                             
                                                                                
           EJECT                                                                
       LINKAGE SECTION.                                                         
           EJECT                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* START OF INKUT178               '.                        
           DISPLAY '**********************************'.                        
                                                                                
           PERFORM 1000-HOUSEKEEPING.                                           
                                                                                
           IF END-OF-RECS                                                       
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 2000-PROCESS-INPUT                                        
                  UNTIL END-OF-RECS                                             
           END-IF.                                                              
                                                                                
           PERFORM 1100-EOJ.                                                    
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* END OF INKUT178                 '.                        
           DISPLAY '**********************************'.                        
                                                                                
           STOP RUN.                                                            
      ******************************************************************        
      *    OPEN FILES, READ FIRST STORE AND CHECK FOR END OF RECS               
      ******************************************************************        
       1000-HOUSEKEEPING.                                                       
                                                                                
           OPEN INPUT  STORE-PROCESS-FILE-IN                                    
                OUTPUT STORE-INV-FILE-OUT.                                      
                                                                                
      *** LOAD THE TINVPAR TABLE....                                            
           INITIALIZE      WS-TINVPAR-TABLE.                                    
           MOVE ZEROS      TO PV-STR-NDX.                                       
           MOVE 'N'        TO PS-TINVPAR-DONE-SW.                               
           PERFORM 8200-OPEN-TINVPAR.                                           
           PERFORM 8300-LOAD-TINVPAR-TABLE                                      
               UNTIL TINVPAR-PROCESS-COMPLETE  OR                               
                     PV-STR-NDX IS GREATER THAN 9999.                           
           PERFORM 8400-CLOSE-TINVPAR.                                          
                                                                                
           PERFORM 7000-READ-STORE-REC.                                         
                                                                                
           IF END-OF-RECS                                                       
               DISPLAY  '*** STORE INPUT FILE IS EMPTY ***'                     
               DISPLAY  '*** STORE INPUT FILE IS EMPTY ***'                     
               DISPLAY  '*** STORE INPUT FILE IS EMPTY ***'                     
           END-IF.                                                              
                                                                                
           MOVE 'Y' TO PS-FIRST-STORE-RECORD-SW.                                
           INITIALIZE  PV-AREA-HEADER-TOTAL.                                    
                                                                                
                                                                                
      ******************************************************************        
      *    CLOSE THE FILES.                                                     
      ******************************************************************        
       1100-EOJ.                                                                
                                                                                
           DISPLAY 'NUMBER OF OUTPUT RECORDS = ' PV-OUTPUT-RECS.                
           DISPLAY 'NBR OF SKU/STORE HEADERS = ' PV-HEADER-RECS.                
           DISPLAY 'NBR OF BYPASS/ERROR RECS = ' PV-ERROR-RECS.                 
                                                                                
           CLOSE STORE-PROCESS-FILE-IN                                          
                 STORE-INV-FILE-OUT.                                            
                                                                                
      ******************************************************************        
      *    PROCESS THE STORE INPUT FILE.                                        
      *    THIS PARAGRAPH WILL OPEN, FETCH, PROCESS AND CLOSE THE               
      *    TSUMINV CURSOR.                                                      
      ******************************************************************        
       2000-PROCESS-INPUT.                                                      
                                                                                
           MOVE 'N'   TO END-OF-CURSOR-SW.                                      
           MOVE ZERO  TO PV-HOLD-LOC-NBR.                                       
           MOVE ZERO  TO PV-HOLD-AREA-NBR.                                      
           MOVE ZERO  TO PV-HOLD-DEPT-NBR.                                      
           MOVE ZERO  TO PV-HOLD-MAJOR-CLASS.                                   
           MOVE ZERO  TO PV-HOLD-SUB-CLASS.                                     
           MOVE ZERO  TO PV-HOLD-SKU-NBR.                                       
           MOVE ZERO  TO PV-HOLD-INV-UNIT-PR-AMT.                               
           INITIALIZE PV-AREA-HEADER-TOTAL.                                     
                                                                                
           PERFORM 3000-OPEN-TSUMINV-CSR                                        
           PERFORM 3100-FETCH-TSUMINV-CSR                                       
           PERFORM 3200-PROCESS-TSUMINV-CSR                                     
               UNTIL END-OF-CURSOR                                              
           PERFORM 3150-CLOSE-TSUMINV-CSR                                       
                                                                                
           PERFORM 7000-READ-STORE-REC.                                         
           MOVE 'Y' TO PS-FIRST-STORE-RECORD-SW.                                
                                                                                
      ******************************************************************        
      *    OPEN THE TSUMINV CURSOR                                              
      ******************************************************************        
       3000-OPEN-TSUMINV-CSR.                                                   
                                                                                
           MOVE IN004-STR-NBR      TO PV-LOC-NUMBER.                            
           MOVE PV-LOC-NUMBER      TO SUMINV-LOC-NBR.                           
                                                                                
           EXEC SQL                                                             
               OPEN TSUMINV_CSR                                                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'OPEN TSUMINV_CSR' TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '3000-OPEN-TSUMINV-CSR' TO PV-PARAGRAPH-NAME                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *    FETCH ROWS FROM TSUMINV CURSOR                                       
      ******************************************************************        
       3100-FETCH-TSUMINV-CSR.                                                  
                                                                                
           INITIALIZE DCLTSUMINV.                                               
                                                                                
           EXEC SQL                                                             
              FETCH TSUMINV_CSR                                                 
               INTO  :SUMINV-LOC-NBR                                            
                    ,:SUMINV-AREA-NBR                                           
                    ,:SUMINV-SKU-NBR                                            
S27710              ,:SUMINV-UPC-NBR                                            
                    ,:SUMINV-INV-UNIT-PR-AMT                                    
                    ,:SUMINV-INV-QTY                                            
                    ,:SUMINV-DEPT-NBR                                           
                    ,:SUMINV-MAJ-CL-NBR                                         
                    ,:SUMINV-SUB-CL-NBR                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
                 MOVE SUMINV-SKU-NBR   TO NI-SKU-9                              
                 IF NI-SKU (6:3)   = '000' OR '999'                             
                    SET PS-BYPASS-SKU TO TRUE                                   
                 ELSE                                                           
                    SET PS-PROCESS-SKU TO TRUE                                  
                 END-IF                                                         
             WHEN SQLCODE = +100                                                
                 SET END-OF-CURSOR TO TRUE                                      
                 PERFORM 5200-WRITE-SKU-HEADER                                  
             WHEN OTHER                                                         
                 MOVE 'FETCH TSUMINV CURSOR'                                    
                               TO  PV-DB2-OPER-ATTEMPTED                        
                 MOVE '3100-FETCH-TSUMINV-CSR' TO PV-PARAGRAPH-NAME             
                 PERFORM 9999-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    CLOSE TSUMINV CURSOR                                                 
      ******************************************************************        
       3150-CLOSE-TSUMINV-CSR.                                                  
                                                                                
           EXEC SQL                                                             
              CLOSE TSUMINV_CSR                                                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'CLOSE TSUMINV_CSR' TO PV-DB2-OPER-ATTEMPTED                
               MOVE '3150-CLOSE-TSUMINV-CSR' TO PV-PARAGRAPH-NAME               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    PROCESS ALL ROWS FROM TSUMINV CURSOR                                 
      *                                                                         
      *   THIS PARAGRAPH WILL PROCESS EACH TSUMINV CURSOR ROW.                  
      *   EACH ROW IS VERIFIED TO SEE IF IT MATCHES THE PREVIOUS ROW.           
      *   IF A MATCH IS DETERMINED - THE ROW IS WRITTEN TO OUTPUT               
      *   WRITTEN AND NO FURTHER CURSOR PROCESSING IS NEEDED.                   
      *                                                                         
      *   IF A MATCH IS NOT FOUND - WRITE OUT THE PREVIOUS SKUS HEADER          
      *                                                                         
      ******************************************************************        
       3200-PROCESS-TSUMINV-CSR.                                                
                                                                                
           IF PS-BYPASS-SKU                                                     
               ADD +1 TO PV-ERROR-RECS                                          
           ELSE                                                                 
               IF (SUMINV-LOC-NBR         = PV-HOLD-LOC-NBR AND                 
                   SUMINV-SKU-NBR         = PV-HOLD-SKU-NBR AND                 
                   SUMINV-INV-UNIT-PR-AMT = PV-HOLD-INV-UNIT-PR-AMT)            
                   PERFORM 5000-MOVE-DATA-FIELDS                                
                   PERFORM 6000-WRITE-INV-OUT-REC                               
               ELSE                                                             
                   PERFORM 5500-PROCESS-HEADER                                  
      *            PERFORM 5200-WRITE-SKU-HEADER                                
      *            PERFORM 5000-MOVE-DATA-FIELDS                                
      *            PERFORM 6000-WRITE-INV-OUT-REC                               
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 3100-FETCH-TSUMINV-CSR.                                      
                                                                                
      *                                                                         
      ******************************************************************        
      *    MOVE DATA TO INRD024 COPYBOOK                                        
      ******************************************************************        
       5000-MOVE-DATA-FIELDS.                                                   
                                                                                
           INITIALIZE IN024-INV-EXCEPTS-STR-REC.                                
                                                                                
      * LOCATION                                                                
           MOVE SUMINV-LOC-NBR             TO PV-SUMINV-LOC                     
                                              PV-STR-NDX                        
           MOVE PV-SUMINV-LOC              TO IN024-STORE-NUMBER                
                                              PV-HOLD-LOC-NBR.                  
      * SKU                                                                     
           MOVE SUMINV-SKU-NBR             TO IN024-SKU                         
                                              PV-HOLD-SKU-NBR.                  
      * PHYSICAL DATA (AREA, QTY, RTL, EXTD)                                    
           MOVE SUMINV-AREA-NBR            TO IN024-AREA-NUMBER                 
                                              PV-HOLD-AREA-NBR.                 
           MOVE SUMINV-INV-QTY             TO IN024-QUANTITY.                   
           MOVE SUMINV-INV-UNIT-PR-AMT     TO IN024-PRICE                       
                                              PV-HOLD-INV-UNIT-PR-AMT.          
           COMPUTE IN024-SKU-STR-TOT-EXTD =                                     
                 ( SUMINV-INV-UNIT-PR-AMT * SUMINV-INV-QTY).                    
                                                                                
      * CUTOFF DATA (QTY, POST FLUSH RCPTS, INTRANSIT)                          
           MOVE ZEROES                     TO IN024-INV-CUTOFF-OH-QTY           
                                              IN024-POST-FL-AFT-REC             
                                              IN024-CUTOFF-INTRANSIT            
                                              IN024-CUTOFF-STR-EXPECT.          
      * SYSTEM PRICING DATA                                                     
           MOVE ZEROES                     TO IN024-SYSTEM-PRICE.               
                                                                                
      * SOURCE DATA SYSTEM (+01 - TSUMINV)                                      
           MOVE +01                        TO IN024-SOURCE-FOUND-FLAG.          
                                                                                
      * VARIANCE DATA                                                           
           MOVE ZEROES                     TO IN024-VARIANCE-PRICE              
                                              IN024-ABSOLUTE-VAR-PRICE          
                                              IN024-VARIANCE-QTY                
                                              IN024-ABSOLUTE-VAR-QTY.           
      * SKU ATTRIBUTES                                                          
           MOVE SUMINV-DEPT-NBR            TO IN024-DEPT                        
                                              PV-HOLD-DEPT-NBR.                 
           MOVE SUMINV-MAJ-CL-NBR          TO IN024-CLASS                       
                                              PV-HOLD-MAJOR-CLASS.              
           MOVE SUMINV-SUB-CL-NBR          TO IN024-SUB-CLASS                   
                                              PV-HOLD-SUB-CLASS.                
                                                                                
      * TINVPAR MANIFESTING INDICATOR                                           
           MOVE WS-MFSTNG-IND(PV-STR-NDX)  TO IN024-MFSTNG-IND.                 
           PERFORM 5100-ACCUM-SKUSTR-TOTALS.                                    
                                                                                
                                                                                
      ******************************************************************        
      * ACCUMULATE THE TOTALS FOR THE SKU / STORE COMBINATION                   
      ******************************************************************        
                                                                                
       5100-ACCUM-SKUSTR-TOTALS.                                                
                                                                                
      *    ADD THE INVENTORY QUANTITY TO THE 'AREA 000000' HEADER REC           
           ADD  SUMINV-INV-QTY            TO PV-AREA-000000-QTY.                
                                                                                
           COMPUTE PV-AREA-000000-TEMP =    (SUMINV-INV-QTY                     
                                          *  SUMINV-INV-UNIT-PR-AMT)            
      *                                                                         
           ADD PV-AREA-000000-TEMP        TO PV-AREA-000000-EXTD-RTL.           
                                                                                
      ******************************************************************        
      * CREATE AN AREA '000000' ROW CONTAINING TOTALS AT THE                    
      * SKU / STORE LEVEL                                                       
      ******************************************************************        
       5200-WRITE-SKU-HEADER.                                                   
                                                                                
           INITIALIZE IN024-INV-EXCEPTS-STR-REC.                                
                                                                                
           IF PS-FIRST-STORE-RECORD                                             
               MOVE 'N' TO PS-FIRST-STORE-RECORD-SW                             
           ELSE                                                                 
               MOVE +01                       TO IN024-SOURCE-FOUND-FLAG        
               MOVE PC-AREA-000000            TO IN024-AREA-NUMBER              
               MOVE PV-HOLD-LOC-NBR           TO IN024-STORE-NUMBER             
               MOVE PV-HOLD-DEPT-NBR          TO IN024-DEPT                     
               MOVE PV-HOLD-MAJOR-CLASS       TO IN024-CLASS                    
               MOVE PV-HOLD-SUB-CLASS         TO IN024-SUB-CLASS                
               MOVE PV-HOLD-SKU-NBR           TO IN024-SKU                      
                                                                                
      *        HEADER RECORD CONTAINS ONLY QTY AND EXTENDED (NOT RETAIL)        
               MOVE ZEROES                    TO IN024-PRICE                    
               MOVE PV-AREA-000000-QTY        TO IN024-QUANTITY                 
               MOVE PV-AREA-000000-EXTD-RTL   TO IN024-SKU-STR-TOT-EXTD         
                                                                                
               PERFORM 6050-WRITE-HEADER-REC                                    
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    WRITE THE SKU HEADER RECORD                                          
      *    IF THIS IS THE END OF CURSOR FOR THIS SKU, WRITE HEADER ONLY         
      ******************************************************************        
       5500-PROCESS-HEADER.                                                     
                                                                                
           PERFORM 5200-WRITE-SKU-HEADER.                                       
                                                                                
           IF NOT END-OF-CURSOR                                                 
               PERFORM 5000-MOVE-DATA-FIELDS                                    
               PERFORM 6000-WRITE-INV-OUT-REC                                   
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    WRITE STORE INVENTORY RECORD                                         
      ******************************************************************        
       6000-WRITE-INV-OUT-REC.                                                  
                                                                                
           WRITE STORE-INV-REC                                                  
               FROM IN024-INV-EXCEPTS-STR-REC.                                  
                                                                                
           ADD +1 TO PV-OUTPUT-RECS.                                            
                                                                                
      ******************************************************************        
      *    WRITE STORE INVENTORY RECORD                                         
      ******************************************************************        
       6050-WRITE-HEADER-REC.                                                   
                                                                                
           WRITE STORE-INV-REC                                                  
               FROM IN024-INV-EXCEPTS-STR-REC.                                  
                                                                                
           ADD +1 TO PV-HEADER-RECS.                                            
                                                                                
           INITIALIZE PV-AREA-HEADER-TOTAL.                                     
                                                                                
      ******************************************************************        
      *    WRITE ERROR RECORD                                                   
      ******************************************************************        
      *6100-WRITE-ERR-REC.                                                      
      *                                                                         
      *    WRITE ITEM-ERROR-REC                                                 
      *        FROM IN024-INV-EXCEPTS-STR-REC.                                  
      *                                                                         
      *    ADD +1 TO PV-ERROR-RECS.                                             
      *                                                                         
      ******************************************************************        
      *    READ STORE RECORD                                                    
      ******************************************************************        
       7000-READ-STORE-REC.                                                     
                                                                                
           READ STORE-PROCESS-FILE-IN                                           
               INTO IN004-INVENTORY-COUNT-RECORD                                
                 AT END SET END-OF-RECS TO TRUE.                                
                                                                                
           IF END-OF-RECS                                                       
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'INPUT FILE STORE IN PROCESS: ' IN004-STR-NBR             
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 8200-OPEN-TINVPAR.                                                      
      *****************************************************************         
       8200-OPEN-TINVPAR.                                                       
                                                                                
           EXEC SQL                                                             
               OPEN TINVPAR_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
              MOVE '8200-OPEN-TINVPAR' TO PV-PARAGRAPH-NAME                     
              MOVE 'OPEN CURSOR'       TO PV-DB2-OPER-ATTEMPTED                 
              MOVE 'Y'                 TO PS-TINVPAR-DONE-SW                    
              PERFORM 9999-SQL-ABEND                                            
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * 8300-LOAD-TINVPAR-TABLE.                                                
      *****************************************************************         
       8300-LOAD-TINVPAR-TABLE.                                                 
                                                                                
           EXEC SQL                                                             
               FETCH TINVPAR_CURSOR                                             
               INTO :INVPAR-LOC-NBR,                                            
                    :INVPAR-PLND-INV-DTE,                                       
                    :INVPAR-MFSTNG-IND                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               MOVE INVPAR-LOC-NBR      TO PV-SUMINV-LOC                        
               MOVE PV-SUMINV-LOC       TO PV-STR-NDX                           
               MOVE INVPAR-PLND-INV-DTE TO WS-PLND-INV-DTE (PV-STR-NDX)         
               MOVE INVPAR-MFSTNG-IND   TO WS-MFSTNG-IND   (PV-STR-NDX)         
             WHEN SQLCODE IS EQUAL TO +100                                      
               MOVE 'Y' TO PS-TINVPAR-DONE-SW                                   
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
               MOVE '8300-LOAD-TINVPAR-TABLE' TO PV-PARAGRAPH-NAME              
               MOVE 'LOAD TABLE'        TO PV-DB2-OPER-ATTEMPTED                
               MOVE 'Y'        TO PS-TINVPAR-DONE-SW                            
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * 8400-CLOSE-TINVPAR.                                                     
      *****************************************************************         
       8400-CLOSE-TINVPAR.                                                      
                                                                                
           EXEC SQL                                                             
               CLOSE TINVPAR_CURSOR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
              MOVE '8400-CLOSE-TINVPAR' TO PV-PARAGRAPH-NAME                    
              MOVE 'CLOSE CURSOR'       TO PV-DB2-OPER-ATTEMPTED                
              MOVE 'Y'                  TO PS-TINVPAR-DONE-SW                   
              PERFORM 9999-SQL-ABEND                                            
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                  TO ABEND-CODE.                           
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
