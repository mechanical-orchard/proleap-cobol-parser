      ******************************************************************00010079
       IDENTIFICATION DIVISION.                                         00020079
      ******************************************************************00030079
                                                                        00040079
       PROGRAM-ID.     MLKUT402.                                        00050079
       AUTHOR.         DAVE METZGER.                                    00060079
       INSTALLATION.   KOHLS DEPARTMENT STORES.                         00070079
       DATE-WRITTEN.   04/29/97.                                        00080079
       DATE-COMPILED.                                                   00090079
                                                                        00100079
      ******************************************************************00110079
      * THE DESIGN OF THIS PROGRAM HAS BEEN CHANGED.  THERE WILL        00110179
      * NOT BE A CONTROL CARD WITH A DATE RANGE FOR PROCESSING.         00110279
      * A PROCESSING DATE RANGE WILL BE DERIVED SYSTEMATICALLY          00110379
      * WHERE THE START DATE WILL COME FROM THE MAX KEY DATE + ONE DAY  00110479
      * FROM THE TDTATTR TABLE.  THE END DATE WILL BE THE FISCAL        00110579
      * YEAR END DATE OF THE START DATE.                                00110679
      *                                                                 00110779
      * THIS PROGRAM WILL ADD AT LEAST ONE YEAR OF DATE RECORDS         00110879
      * INTO THE TDTATTR TABLE.  NO RESTART LOGIC IS NEEDED BECAUSE     00110979
      * IN THE EVENT THAT THIS PROGRAM WOULD HAVE AN ISSUE, IT WILL     00111079
      * PICKUP WHERE IT LEFT OFF AND WILL FILL IN DATES THROUGH THE     00111179
      * END OF THE FISCAL YEAR.                                         00111279
      *                                                                 00111379
      * THE FOLLOWING STEPS ARE IN THIS PROGRAM:                        00111479
      * GET THE MAX KEY DATE IN THE TDTATTR                             00111579
      * ADD 1 DATE TO THE MAX DATE TO GET THE START DATE                00111679
      * THE END DATE WILL BE THE FISCAL YEAR END DATE.                  00111779
      *                                                                 00111879
      * THE REASON FOR THIS DESIGN OF ONE FISCL YEAR AT A TIME IS       00111979
      * SO THAT THE LAST YEAR THIS DATE IS ABLE TO GET LOOKED UP        00112079
      * AGAINST THE TDTATTR TABLE WHEN BUILDING THE RECORD TO INSERT    00112179
      * INTO TDTATTR.                                                   00112279
      * THIS PROCESS CAN RUN YEARLY, OR ON DEMAND IF THERE IS EVER      00112379
      * A NEED TO GET MORE FUTURE YEAR'S DATA OUT THERE.                00112479
      *                                                                 00112579
      * MAP THE NEEDED FIELDS FOR THE TDTATTR TABLE AND                 00112679
      * FOR THE LAST YEAR THIS DATE, SELECT THAT FROM TDTATTR TABLE     00112779
      * TO BE USED FOR THE RECORD BEING PROCESSED.                      00112879
      * BECAUSE THE MOST NUMBER OF RECORDS THAT WILL BE INSERTED        00112979
      * IS 366, THERE WILL BE NO COMMIT LOGIC, AND THE COMMIT           00113079
      * WILL HAPPEN AFTER A SUCCESSFUL COMPLETION OF THE PROGRAM        00113179
      * PROCESSING WILL STOP ON THE FISCAL YEAR END DATE OF THE         00113279
      *                                                                 00113379
      *                                                                *00113479
      ******************************************************************00113579
      *                                                                *00113679
      *================================================================*00113779
      *  CHANGED:  04/05/1999       BY:  AMY LAWSON                    *00113879
      *  COMMENTED OUT THE LY-THIS-DAY-KY-DTE LOGIC.  THIS LOGIC WILL  *00113979
      *  BE PERFORMED IN MLKUP327 AFTER THE TDTATTR TABLE HAS BEEN     *00114079
      *  LOADED.                                                       *00115079
      *                                                                *00116079
      *  CHANGED:  01/13/2000       BY:  TESS BESTE                    *00117079
      *     ADDED LOGIC TO POPULATE THE NEW COLUMN PFW_END_DTE.        *00118079
      *                                                                *00119079
RONNA *  CHANGED:  01/08/2024       BY:  RONNA MCDONALD                *00120079
RONNA *     SIMPLIFIED THE PROCESS OF ADDING RECORDS TO THE TDTATTR    *00130079
RONNA *     TABLE BY LIMITING THE SCOPE OF THIS PROGRAM TO NO MORE     *00140079
RONNA *     THAN ONE FISCAL YEAR OF RECORDS ADDED AT A TIME ALL        *00150079
RONNA *     IN ONE PROGRAM.                                            *00160079
RONNA *                                                                *00170079
RONNA *     MODIFIED PROGRAM WHERE THE CONTROL CARD AND OUTPUT FILE    *00180079
RONNA *     HAS BEEN REMOVED AND GOING FORWARD THE STARTING POINT      *00190079
RONNA *     FOR INSERTING RECORDS WILL BE DERIVED BY LOOKING AT THE    *00200079
RONNA *     MAX DATE IN THE TDTATTR TABLE, ADDING ONE DAY, AND THEN    *00210079
RONNA *     CREATE AND INSERT RECORDS THROUGH THE END OF THE FISCAL    *00220079
RONNA *     YEAR.  THE MOST NUMBER OF RECORDS THAT WILL BE ADDED       *00230079
RONNA *     IS 371 AND THAT IS FOR FISCAL YEARS THAT HAVE 53 WEEKS.    *00240079
RONNA *     THIS PROGRAM WILL BE SET UP TO RUN YEARLY.  IF FOR ANY     *00250079
RONNA *     REASON ADDITIONAL YEARS WOULD NEED TO BE ADDED, THIS       *00251079
RONNA *     CAN BE ALSO RUN ON DEMAND                                  *00252079
RONNA *                                                                *00253079
      *================================================================*00254079
      *                                                                 00255079
      ******************************************************************00256079
       ENVIRONMENT DIVISION.                                            00257079
      ******************************************************************00258079
                                                                        00259079
       CONFIGURATION SECTION.                                           00260079
                                                                        00270079
       SOURCE-COMPUTER.        IBM-3090.                                00280079
       OBJECT-COMPUTER.        IBM-3090.                                00290079
                                                                        00300079
       INPUT-OUTPUT SECTION.                                            00310079
                                                                        00320079
       FILE-CONTROL.                                                    00330079
                                                                        00340079
RONNA *    SELECT DATE-CONTROL-CARD                                     00350079
RONNA *        ASSIGN TO INP01.                                         00360079
RONNA *                                                                 00370079
RONNA *    SELECT ML-DATE-FILE                                          00380079
RONNA *        ASSIGN TO OUT01.                                         00390079
                                                                        00400079
      ******************************************************************00410079
       DATA DIVISION.                                                   00420079
      ******************************************************************00430079
                                                                        00440079
       FILE SECTION.                                                    00450079
                                                                        00460079
RONNA *FD  DATE-CONTROL-CARD                                            00470079
RONNA *    RECORDING MODE IS F                                          00480079
RONNA *    LABEL RECORDS ARE STANDARD                                   00490079
RONNA *    BLOCK CONTAINS 0 RECORDS.                                    00500079
RONNA *                                                                 00510079
RONNA *01  DATE-CONTROL-RECORD         PIC X(80).                       00520079
RONNA *                                                                 00530079
RONNA *FD  ML-DATE-FILE                                                 00540079
RONNA *    RECORDING MODE IS F                                          00550079
RONNA *    LABEL RECORDS ARE STANDARD                                   00560079
RONNA *    BLOCK CONTAINS 0 RECORDS.                                    00570079
RONNA *                                                                 00580079
RONNA *01  ML-DATE-REC                 PIC X(181).                      00590079
RONNA *                                                                 00600079
       WORKING-STORAGE SECTION.                                         00610079
                                                                        00620079
RONNA ******************************************************************00630079
RONNA *  MERCHANDISE LEDGER DATE CONTROL CARD                           00640079
RONNA ******************************************************************00650079
RONNA *01  CC-CONTROL-REC.                                              00660079
RONNA *    05  CC-FROM-DATE                   PIC X(10).                00670079
RONNA *    05  FILLER                         PIC X(01).                00680079
RONNA *    05  CC-TO-DATE                     PIC X(10).                00690079
RONNA *    05  FILLER                         PIC X(59).                00700079
                                                                        00710079
      ******************************************************************00720079
      * MERCHANDISE LEDGER DATE RECORD LAYOUT                           00730079
      * (THIS FILE WILL BE USED TO LOAD DB2 TABLE "TDTATTR")            00740079
      ******************************************************************00750079
RONNA *    COPY MLRD101.                                                00760079
                                                                        00770079
      ******************************************************************00780079
      *  CALENDAR SUBROUTINE INCLUDE                                    00790079
      ******************************************************************00800079
           COPY DPWS500.                                                00810079
                                                                        00820079
      ******************************************************************00830079
      *  MISC WORKING STORAGE FIELDS                                    00840079
      ******************************************************************00850079
       01  ABEND-CODE                    PIC S9(4)  VALUE ZEROS         00860079
                                         COMP SYNC.                     00870079
                                                                        00880079
       01  PS-PROGRAM-SWITCHES.                                         00890079
           05  PS-FIRST-TIME-SW          PIC X(1)   VALUE 'Y'.          00900079
               88  FIRST-TIME                       VALUE 'Y'.          00910079
               88  NOT-FIRST-TIME                   VALUE 'N'.          00920079
RONNA      05  PS-START-DATE-SW          PIC X(1)   VALUE 'N'.          00930079
RONNA          88  PS-START-DATE-SET                VALUE 'Y'.          00940079
RONNA          88  PS-START-DATE-NOT-SET            VALUE 'N'.          00950079
                                                                        00960079
       01  PC-PROGRAM-CONSTANTS.                                        00970079
           05  PC-CURRENT-PROGRAM-NAME   PIC X(8)   VALUE 'MLKUT402'.   00980079
                                                                        00990079
                                                                        00991079
       01  PV-PROGRAM-VARIABLES.                                        00992079
           05  PV-F2-FFP-CAL-MN-CONVERSION.                             00993079
               10  PV-F2-FISCAL-PRD      PIC 9(2)   VALUE ZERO.         00994079
                   88  PV-F2-FP01                   VALUE 01.           00995079
                   88  PV-F2-FP02                   VALUE 02.           00996079
                   88  PV-F2-FP03                   VALUE 03.           00997079
                   88  PV-F2-FP04                   VALUE 04.           00998079
                   88  PV-F2-FP05                   VALUE 05.           00999079
                   88  PV-F2-FP06                   VALUE 06.           01000079
                   88  PV-F2-FP07                   VALUE 07.           01010079
                   88  PV-F2-FP08                   VALUE 08.           01020079
                   88  PV-F2-FP09                   VALUE 09.           01030079
                   88  PV-F2-FP10                   VALUE 10.           01040079
                   88  PV-F2-FP11                   VALUE 11.           01050079
                   88  PV-F2-FP12                   VALUE 12.           01060079
               10  PV-FASHION-SALES-CAL-MN PIC 9(2) VALUE ZERO.         01070079
                   88  PV-FS-CAL-MN-01              VALUE 01.           01080079
                   88  PV-FS-CAL-MN-02              VALUE 02.           01090079
                   88  PV-FS-CAL-MN-03              VALUE 03.           01100079
                   88  PV-FS-CAL-MN-04              VALUE 04.           01110079
                   88  PV-FS-CAL-MN-05              VALUE 05.           01120079
                   88  PV-FS-CAL-MN-06              VALUE 06.           01130079
                   88  PV-FS-CAL-MN-07              VALUE 07.           01140079
                   88  PV-FS-CAL-MN-08              VALUE 08.           01150079
                   88  PV-FS-CAL-MN-09              VALUE 09.           01160079
                   88  PV-FS-CAL-MN-10              VALUE 10.           01170079
                   88  PV-FS-CAL-MN-11              VALUE 11.           01180079
                   88  PV-FS-CAL-MN-12              VALUE 12.           01190079
                                                                        01200079
           05  PV-CONV-DATE-CCYY-MO-DY.                                 01210079
               10  PV-CONV-DATE-CCYY     PIC X(04).                     01220079
               10  FILLER                PIC X(01) VALUE '-'.           01230079
               10  PV-CONV-DATE-MO       PIC X(02).                     01240079
               10  FILLER                PIC X(01) VALUE '-'.           01250079
               10  PV-CONV-DATE-DY       PIC X(02).                     01260079
                                                                        01270079
           05  PV-DT-PARA                PIC X(30)     VALUE SPACES.    01280079
           05  PV-NULL-IND               PIC S9(04) COMP VALUE ZERO.    01290079
                                                                        01300079
       01  PV-FSCL-WEEK-VARS.                                           01310079
RONNA      05 PV-WK-IN-MN-NBR.                                          01320079
RONNA          10 PV-WK-IN-MN-NBR-X1          PIC  X(1).                01330079
RONNA          10 PV-WK-IN-MN-NBR-X2          PIC  X(1).                01340079
                                                                        01350079
       01  PV-DATES.                                                    01360079
           05  PV-PROCESS-DATE           PIC   X(10).                   01370079
           05  PV-MAX-KEY-DATE           PIC   X(10).                   01380079
           05  PV-START-DATE             PIC   X(10).                   01390079
           05  PV-END-DATE               PIC   X(10).                   01400079
           05  PV-JULIAN-DATE            PIC   9(07)  VALUE ZEROS.      01410079
RONNA      05  PV-THIS-YEAR              PIC   9(04).                   01420079
RONNA      05  PV-NEXT-YEAR              PIC   9(04).                   01430079
RONNA      05  PV-LAST-YEAR              PIC   9(04).                   01440079
RONNA      05  PV-LAST-YEAR-CHAR REDEFINES PV-LAST-YEAR                 01441079
RONNA                                    PIC   X(04).                   01442079
                                                                        01443079
       01  PV-SUMMARY-COUNTS.                                           01444079
           05  PV-ML-TDTATTR-INSERTS     PIC  9(9)     VALUE ZERO.      01445079
                                                                        01446079
RONNA  01  PV-ABEND-AREA.                                               01447079
RONNA      05  PV-ABEND-MESSAGE             PIC  X(75)    VALUE SPACE.  01448079
RONNA      05  PV-SQL-SUB                   PIC  9(03)    VALUE ZERO.   01449079
RONNA      05  PV-DB2-OPER-ATTEMPTED        PIC  X(30)    VALUE SPACE.  01450079
RONNA                                                                   01460079
      *                                                                 01470079
      ******************************************************************01480079
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01490079
      ******************************************************************01490179
           COPY DPWS004.                                                01490279
      *                                                                 01490379
RONNA ******************************************************************01490479
RONNA *  USED FOR DB2 ERRORS                                            01490579
RONNA ******************************************************************01490679
RONNA      EXEC SQL                                                     01490779
RONNA          INCLUDE SQLCA                                            01490879
RONNA      END-EXEC.                                                    01490979
RONNA *                                                                 01491079
RONNA ******************************************************************01492079
RONNA *  DATE ATTRIBUTE TABLE                                           01493079
RONNA ******************************************************************01494079
RONNA      EXEC SQL                                                     01495079
RONNA          INCLUDE TDTATTR                                          01496079
RONNA      END-EXEC.                                                    01497079
      *                                                                 01498079
      ******************************************************************01499079
       PROCEDURE DIVISION.                                              01499179
      ******************************************************************01499279
       0000-MAINLINE.                                                   01499379
                                                                        01499479
           PERFORM 1000-INITIALIZE.                                     01499579
                                                                        01499679
           PERFORM 2000-PROCESS-DATES                                   01499779
RONNA          UNTIL DPG56-FISCAL-YEAR-CCYY    > PV-THIS-YEAR.          01499879
RONNA *        UNTIL PV-PROCESS-DATE > CC-TO-DATE.                      01499979
                                                                        01500079
RONNA      DISPLAY ' DPG56-FISCAL-YEAR-CCYY : ' DPG56-FISCAL-YEAR-CCYY. 01510079
           PERFORM 8000-EOJ-ROUTINE.                                    01520079
                                                                        01530079
           GOBACK.                                                      01540079
                                                                        01550079
      ******************************************************************01560079
      *  SET UP FOR PROGRAM PROCESSING BY OPENING ALL FILES, AND       *01570079
      *  READING THE LEDGER DATE CONTROL CARD FILE.                    *01580079
      ******************************************************************01590079
       1000-INITIALIZE.                                                 01600079
                                                                        01610079
RONNA *    PERFORM 1100-OPEN-FILES.                                     01620079
           PERFORM 1100-GET-START-DATE.                                 01630079
RONNA *    PERFORM 1200-BUILD-FIRST-RECORD.                             01640079
RONNA *    PERFORM 1300-GET-LAST-DATE.                                  01650079
                                                                        01660079
           PERFORM 1200-GET-INIT-DATE-INFO.                             01670079
                                                                        01680079
RONNA ******************************************************************01690079
RONNA *  SET UP FOR PROGRAM PROCESSING BY OPENING ALL FILES            *01700079
RONNA ******************************************************************01701079
RONNA *1100-OPEN-FILES.                                                 01702079
RONNA *                                                                 01703079
RONNA *    OPEN INPUT  DATE-CONTROL-CARD.                               01704079
RONNA *    OPEN OUTPUT ML-DATE-FILE.                                    01705079
                                                                        01706079
RONNA ******************************************************************01707079
RONNA *                                                                 01708079
RONNA *  GET THE MAX DATE FROM THE TDTATTR TABLE AND ADD ONE DAY        01709079
RONNA *  AND THAT WILL BE YOUR START DATE                               01710079
RONNA *                                                                 01720079
RONNA ******************************************************************01730079
RONNA  1100-GET-START-DATE.                                             01740079
RONNA                                                                   01750079
RONNA      MOVE '1100-GET-START-DATE' TO PV-DT-PARA.                    01760079
RONNA      SET PS-START-DATE-NOT-SET  TO TRUE.                          01770079
RONNA                                                                   01780079
RONNA      EXEC SQL                                                     01790079
RONNA         SELECT MAX(KY_DTE)                                        01800079
RONNA              , MAX(KY_DTE) + 1 DAY                                01810079
RONNA         INTO   :PV-MAX-KEY-DATE   :PV-NULL-IND                    01811079
RONNA              , :PV-START-DATE                                     01812079
RONNA         FROM TDTATTR                                              01813079
RONNA         WITH UR                                                   01814079
RONNA      END-EXEC.                                                    01815079
RONNA                                                                   01816079
RONNA      EVALUATE TRUE                                                01816179
RONNA          WHEN SQLCODE = ZERO                                      01816279
RONNA              IF PV-NULL-IND < 0                                   01816379
RONNA                  MOVE '6000-GET-START-DATE'                       01816479
RONNA                                           TO PV-DT-PARA           01816579
RONNA                  MOVE 'NO MAX ROW FROM TDTATTR FOUND'             01816679
RONNA                                           TO PV-DB2-OPER-ATTEMPTED01816779
RONNA                  PERFORM Z900-SQL-ABEND                           01816879
RONNA              ELSE                                                 01816979
RONNA                  SET PS-START-DATE-SET TO TRUE                    01817079
RONNA              END-IF                                               01817179
RONNA          WHEN OTHER                                               01817279
RONNA              MOVE '6000-GET-START-DATE'                           01817379
RONNA                                           TO PV-DT-PARA           01817479
RONNA              MOVE 'SELECT MAX ROW FROM TDTATTR'                   01817579
RONNA                                           TO PV-DB2-OPER-ATTEMPTED01817679
RONNA              PERFORM Z900-SQL-ABEND                               01817779
RONNA      END-EVALUATE.                                                01817879
RONNA                                                                   01817979
RONNA                                                                   01818079
RONNA                                                                   01818179
      ******************************************************************01818279
      *  GET CONTROL CARD DATES, VALIDATE START DATE                   *01818379
      ******************************************************************01818479
       1200-GET-INIT-DATE-INFO.                                         01818579
RONNA*     MOVE '1200-GET-INIT-DATE-INFO' TO PV-DT-PARA.                01818679
RONNA*                                                                  01818779
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              01818879
           SET DPG51-FISCAL-454-CALENDAR                                01818979
               DPG52-LK-DTE-ISO-FMT        TO TRUE.                     01819079
RONNA      MOVE PV-START-DATE    TO DPG52-LK-DATE-INPUT.                01820079
                                                                        01830079
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                          01840079
                                                                        01850079
RONNA      MOVE DPG56-FISCAL-YEAR-CCYY TO PV-THIS-YEAR.                 01860079
RONNA      DISPLAY 'THIS FISCAL YEAR IS : ' PV-THIS-YEAR.               01870079
RONNA      COMPUTE PV-NEXT-YEAR = PV-THIS-YEAR + 1.                     01880079
RONNA      DISPLAY 'NEXT FISCAL YEAR IS : ' PV-NEXT-YEAR.               01890079
RONNA      DISPLAY 'PROCESSING WILL BE FROM ' PV-START-DATE.            01900079
RONNA      DISPLAY 'TO THE END OF THIS FISCAL YEAR'.                    01910079
                                                                        01920079
      ******************************************************************01930079
      *  PROCESS DATES.                                                *01940079
      ******************************************************************01950079
       2000-PROCESS-DATES.                                              01960079
                                                                        01970079
           MOVE '2000-PROCESS-DATES'   TO PV-DT-PARA.                   01980079
RONNA ***  FORMAT AND INSERT RECORD INTO THE TDTATTR TABLE              01990079
RONNA ***  GET LAST YEAR DATE FOR THIS RECORD AND THEN                  02000079
RONNA ***  INSERT THE RECORD INTO THE TDTATTTR TABLE.                   02010079
RONNA ****                                                              02020079
           PERFORM 4000-CREATE-ML-DATE-REC.                             02030079
                                                                        02040079
RONNA ***  INSERT THE RECROD INTO THE TDTATTR TABLE                     02050079
           PERFORM 7000-INSERT-TDTATTR.                                 02060079
                                                                        02070079
      ***  GET THE NEXT DAY TO PROCESS                                  02080079
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              02090079
           SET DPG51-FISCAL-454-CALENDAR                                02100079
               DPG51-INCREMENT-DATE                                     02110079
               DPG52-LK-DTE-ISO-FMT    TO TRUE.                         02120079
           MOVE 1                      TO DPG51-INCR-DECR-DAYS-9.       02130079
RONNA      MOVE DTATTR-KY-DTE          TO DPG52-LK-DATE-INPUT.          02140079
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                          02150079
                                                                        02160079
           MOVE DPG55-DB2-ISO-DATE-FMT TO PV-PROCESS-DATE.              02170079
                                                                        02180079
      ******************************************************************02190079
      *  CREATE AND WRITE THE DW DATE RECORD.                          *02200079
      ******************************************************************02210079
       4000-CREATE-ML-DATE-REC.                                         02220079
           MOVE DPG55-DB2-ISO-DATE-FMT        TO DTATTR-KY-DTE.         02230079
           MOVE DPG56-FISCAL-YEAR-CCYY        TO DTATTR-FSCL-YR-NBR.    02240079
                                                                        02250079
           MOVE DPG56-FISCAL-WEEK-NBR         TO DTATTR-FSCL-WK-NBR.    02260079
RONNA      MOVE DPG56-FISCAL-PERIOD-WEEK-X    TO PV-WK-IN-MN-NBR.       02270079
RONNA      MOVE PV-WK-IN-MN-NBR-X2            TO DTATTR-WK-IN-MN-NBR.   02280079
           MOVE DPG55-ALPHA-WEEK-DAY          TO DTATTR-DAY-NM.         02290079
           MOVE DPG55-NUMERIC-WEEK-DAY        TO DTATTR-DAY-OF-WK-NBR.  02300079
           MOVE DPG56-FISCAL-QTR-NBR          TO DTATTR-FSCL-QTR-NBR.   02310079
                                                                        02320079
           MOVE DPG55-JULIAN-DATE-CCYYDDD     TO PV-JULIAN-DATE.        02330079
           MOVE PV-JULIAN-DATE                TO DTATTR-JLN-DTE.        02340079
                                                                        02350079
           MOVE DPG56-FIRST-FD-DB2-ISO-FMT    TO DTATTR-FSCL-YR-BEG-DTE.02360079
           MOVE DPG56-LAST-FD-DB2-ISO-FMT     TO DTATTR-FSCL-YR-END-DTE.02370079
           MOVE DPG56-FIRST-FP-DB2-ISO-FMT    TO DTATTR-FSCL-MN-BEG-DTE.02380079
           MOVE DPG56-LAST-FP-DB2-ISO-FMT     TO DTATTR-FSCL-MN-END-DTE.02390079
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT     TO DTATTR-FSCL-WK-BEG-DTE.02400079
           MOVE DPG55-LAST-WKDY-DB2ISO-FMT    TO DTATTR-FSCL-WK-END-DTE.02410079
           MOVE DPG56-CURR-FISCAL-SEASON      TO DTATTR-SEA-DESC.       02420079
                                                                        02430079
      ***  FORMAT SEASON BEGIN DATE TO CCYY-MM-DD                       02440079
           MOVE DPG56-FIRST-FS-DB2-ISO-FMT-YR TO PV-CONV-DATE-CCYY.     02450079
           MOVE DPG56-FIRST-FS-DB2-ISO-FMT-MO TO PV-CONV-DATE-MO.       02460079
           MOVE DPG56-FIRST-FS-DB2-ISO-FMT-DY TO PV-CONV-DATE-DY.       02470079
           MOVE PV-CONV-DATE-CCYY-MO-DY       TO DTATTR-SEA-BEG-DTE.    02480079
                                                                        02490079
      ***  FORMAT SEASON END DATE TO CCYY-MM-DD                         02500079
           MOVE DPG56-LAST-FS-DB2-ISO-FMT-YR  TO PV-CONV-DATE-CCYY.     02510079
           MOVE DPG56-LAST-FS-DB2-ISO-FMT-MO  TO PV-CONV-DATE-MO.       02520079
           MOVE DPG56-LAST-FS-DB2-ISO-FMT-DY  TO PV-CONV-DATE-DY.       02530079
           MOVE PV-CONV-DATE-CCYY-MO-DY       TO DTATTR-SEA-END-DTE.    02540079
                                                                        02550079
      ***  GET FISCAL AND CALENDAR MONTH NUMS AND MONTH DESC            02560079
           MOVE DPG56-FISCAL-PERIOD-NAME      TO DTATTR-FSCL-MN-NM.     02570079
           MOVE DPG56-FISCAL-PERIOD-NBR       TO DTATTR-FSCL-MN-NBR     02580079
                                                 PV-F2-FISCAL-PRD.      02590079
           EVALUATE TRUE                                                02600079
               WHEN PV-F2-FP12                                          02610079
                    SET PV-FS-CAL-MN-01       TO TRUE                   02620079
               WHEN PV-F2-FP01                                          02630079
                    SET PV-FS-CAL-MN-02       TO TRUE                   02640079
               WHEN PV-F2-FP02                                          02650079
                    SET PV-FS-CAL-MN-03       TO TRUE                   02660079
               WHEN PV-F2-FP03                                          02670079
                    SET PV-FS-CAL-MN-04       TO TRUE                   02680079
               WHEN PV-F2-FP04                                          02690079
                    SET PV-FS-CAL-MN-05       TO TRUE                   02700079
               WHEN PV-F2-FP05                                          02710079
                    SET PV-FS-CAL-MN-06       TO TRUE                   02720079
               WHEN PV-F2-FP06                                          02730079
                    SET PV-FS-CAL-MN-07       TO TRUE                   02740079
               WHEN PV-F2-FP07                                          02750079
                    SET PV-FS-CAL-MN-08       TO TRUE                   02760079
               WHEN PV-F2-FP08                                          02770079
                    SET PV-FS-CAL-MN-09       TO TRUE                   02780079
               WHEN PV-F2-FP09                                          02790079
                    SET PV-FS-CAL-MN-10       TO TRUE                   02800079
               WHEN PV-F2-FP10                                          02810079
                    SET PV-FS-CAL-MN-11       TO TRUE                   02820079
               WHEN PV-F2-FP11                                          02830079
                    SET PV-FS-CAL-MN-12       TO TRUE                   02840079
           END-EVALUATE.                                                02850079
           MOVE PV-FASHION-SALES-CAL-MN       TO DTATTR-CAL-MN-NBR.     02860079
                                                                        02870079
                                                                        02880079
      ***  GET PREVIOUS FISCAL WEEK                                     02890079
           PERFORM 4100-GET-PREV-FISCAL-WEEK.                           02900079
                                                                        02910079
      ***  GET PREVIOUS FISCAL MONTH                                    02920079
           PERFORM 4200-GET-PREV-FISCAL-MONTH.                          02930079
                                                                        02940079
      ***  GET PREVIOUS FISCAL SEASON                                   02950079
           PERFORM 4300-GET-PREV-FISCAL-SEASON.                         02960079
                                                                        02970079
      ***  GET NEXT FISCAL MONTH                                        02980079
           PERFORM 4400-GET-NEXT-FISCAL-MONTH.                          02990079
                                                                        03000079
RONNA **** GET THE LAST YEAR DATE FOR THIS KY DTE                       03010079
RONNA      PERFORM 4500-GET-LY-DATE.                                    03020079
                                                                        03030079
                                                                        03040079
                                                                        03050079
      ******************************************************************03060079
      *  GET PREVIOUS FISCAL WEEK END DATE.                            *03070079
      ******************************************************************03080079
       4100-GET-PREV-FISCAL-WEEK.                                       03090079
                                                                        03100079
           MOVE '4100-GET-PREV-FISCAL-WEEK' TO PV-DT-PARA.              03110079
                                                                        03120079
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03130079
           SET DPG51-FISCAL-454-CALENDAR                                03140079
               DPG51-DECREMENT-DATE                                     03150079
               DPG52-LK-DTE-ISO-FMT TO TRUE.                            03160079
           MOVE 1                   TO DPG51-INCR-DECR-DAYS-9.          03170079
           MOVE DTATTR-FSCL-WK-BEG-DTE                                  03180079
                                    TO DPG52-LK-DATE-INPUT.             03190079
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                          03200079
                                                                        03210079
           MOVE DPG55-LAST-WKDY-DB2ISO-FMT                              03220079
                                    TO DTATTR-PFW-END-DTE.              03230079
                                                                        03240079
      ******************************************************************03250079
      *  GET PREVIOUS FISCAL MONTH END-DATE                            *03260079
      ******************************************************************03270079
       4200-GET-PREV-FISCAL-MONTH.                                      03280079
           MOVE '4200-GET-PREV-FISCAL-MONTH' TO PV-DT-PARA.             03290079
                                                                        03300079
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03310079
           SET DPG51-FISCAL-454-CALENDAR                                03320079
               DPG51-DECREMENT-DATE                                     03330079
               DPG52-LK-DTE-ISO-FMT TO TRUE.                            03340079
           MOVE 1                   TO DPG51-INCR-DECR-DAYS-9.          03350079
           MOVE DTATTR-FSCL-MN-BEG-DTE                                  03360079
                                    TO DPG52-LK-DATE-INPUT.             03370079
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                          03380079
                                                                        03390079
           MOVE DPG56-LAST-FP-DB2-ISO-FMT                               03400079
                                    TO DTATTR-PFM-END-DTE.              03410079
                                                                        03411079
      ******************************************************************03412079
      *  GET PREVIOUS FISCAL SEASON END-DATE                           *03413079
      ******************************************************************03414079
       4300-GET-PREV-FISCAL-SEASON.                                     03415079
           MOVE '4300-GET-PREV-FISCAL-SEASON' TO PV-DT-PARA.            03416079
                                                                        03417079
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03418079
           SET DPG51-FISCAL-454-CALENDAR                                03419079
               DPG51-DECREMENT-DATE                                     03419179
               DPG52-LK-DTE-ISO-FMT        TO TRUE.                     03419279
           MOVE 1                          TO DPG51-INCR-DECR-DAYS-9.   03419379
           MOVE DTATTR-SEA-BEG-DTE         TO DPG52-LK-DATE-INPUT.      03419479
                                                                        03419579
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                          03419679
                                                                        03419779
           MOVE DPG56-LAST-FS-DB2-ISO-FMT-YR TO PV-CONV-DATE-CCYY       03419879
           MOVE DPG56-LAST-FS-DB2-ISO-FMT-MO TO PV-CONV-DATE-MO         03419979
           MOVE DPG56-LAST-FS-DB2-ISO-FMT-DY TO PV-CONV-DATE-DY         03420079
           MOVE PV-CONV-DATE-CCYY-MO-DY      TO DTATTR-PREV-SEA-END-DTE.03430079
                                                                        03440079
      ******************************************************************03450079
      *  GET NEXT FISCAL MONTH END-DATE                                *03460079
      ******************************************************************03470079
       4400-GET-NEXT-FISCAL-MONTH.                                      03480079
           MOVE '4400-GET-NEXT-FISCAL-MONTH' TO PV-DT-PARA.             03490079
                                                                        03500079
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03510079
           SET DPG51-FISCAL-454-CALENDAR                                03520079
               DPG51-INCREMENT-DATE                                     03530079
               DPG52-LK-DTE-ISO-FMT         TO TRUE.                    03540079
           MOVE 1                           TO DPG51-INCR-DECR-DAYS-9.  03550079
           MOVE DTATTR-FSCL-MN-END-DTE      TO DPG52-LK-DATE-INPUT.     03560079
                                                                        03570079
           PERFORM 7200-CALL-CALENDAR-ROUTINE.                          03580079
                                                                        03590079
           MOVE DPG56-LAST-FP-DB2-ISO-FMT   TO DTATTR-NFM-END-DTE.      03600079
                                                                        03610079
      ******************************************************************03620079
       4500-GET-LY-DATE.                                                03630079
      ******************************************************************03640079
      *                                                                 03650079
RONNA *    COMPUTE LAST YEAR NUMBER                                     03660079
RONNA      COMPUTE PV-LAST-YEAR = PV-THIS-YEAR - 1.                     03670079
RONNA *                                                                 03680079
           EXEC SQL                                                     03690079
             SELECT  A.KY_DTE                                           03700079
              INTO  :DTATTR-LY-THIS-DAY-KY-DTE                          03710079
              FROM TDTATTR A                                            03720079
RONNA         WHERE A.FSCL_YR_NBR        = :PV-LAST-YEAR-CHAR           03730079
              AND   A.FSCL_MN_NBR        = :DTATTR-FSCL-MN-NBR          03740079
              AND   A.FSCL_WK_NBR        = :DTATTR-FSCL-WK-NBR          03741079
              AND   A.DAY_OF_WK_NBR      = :DTATTR-DAY-OF-WK-NBR        03742079
           END-EXEC.                                                    03743079
      *                                                                 03744079
           EVALUATE SQLCODE                                             03745079
               WHEN 0                                                   03746079
                   CONTINUE                                             03747079
               WHEN +100                                                03748079
                   MOVE '9999-09-09'  TO DTATTR-LY-THIS-DAY-KY-DTE      03749079
               WHEN OTHER                                               03750079
                  MOVE '4500-GET-LY-DATE'                               03750179
                                       TO PV-DT-PARA                    03750279
                   PERFORM Z900-SQL-ABEND                               03750379
           END-EVALUATE.                                                03750479
      *                                                                 03750579
                                                                        03750679
                                                                        03750779
RONNA ******************************************************************03750879
RONNA *    INSERT NEW VENDOR DETAILS INTO EXPENSE VENDOR TABLE.         03750979
RONNA ******************************************************************03751079
RONNA  7000-INSERT-TDTATTR.                                             03751179
RONNA                                                                   03751279
RONNA          EXEC SQL                                                 03751379
RONNA              INSERT INTO TDTATTR                                  03751479
RONNA                  ( KY_DTE                                         03751579
RONNA                   ,FSCL_YR_NBR                                    03751679
RONNA                   ,FSCL_MN_NBR                                    03751779
RONNA                   ,FSCL_MN_NM                                     03751879
RONNA                   ,FSCL_WK_NBR                                    03751979
RONNA                   ,WK_IN_MN_NBR                                   03752079
RONNA                   ,DAY_NM                                         03753079
RONNA                   ,DAY_OF_WK_NBR                                  03754079
RONNA                   ,FSCL_QTR_NBR                                   03755079
RONNA                   ,CAL_MN_NBR                                     03756079
RONNA                   ,JLN_DTE                                        03757079
RONNA                   ,LY_THIS_DAY_KY_DTE                             03758079
RONNA                   ,FSCL_YR_BEG_DTE                                03759079
RONNA                   ,FSCL_YR_END_DTE                                03760079
RONNA                   ,FSCL_MN_BEG_DTE                                03770079
RONNA                   ,FSCL_MN_END_DTE                                03780079
RONNA                   ,FSCL_WK_BEG_DTE                                03790079
RONNA                   ,FSCL_WK_END_DTE                                03800079
RONNA                   ,SEA_DESC                                       03810079
RONNA                   ,SEA_BEG_DTE                                    03820079
RONNA                   ,SEA_END_DTE                                    03830079
RONNA                   ,PREV_SEA_END_DTE                               03840079
RONNA                   ,PFM_END_DTE                                    03850079
RONNA                   ,NFM_END_DTE                                    03860079
RONNA                   ,PFW_END_DTE )                                  03870079
RONNA              VALUES (:DTATTR-KY-DTE                               03880079
RONNA                     ,:DTATTR-FSCL-YR-NBR                          03890079
RONNA                     ,:DTATTR-FSCL-MN-NBR                          03900079
RONNA                     ,:DTATTR-FSCL-MN-NM                           03910079
RONNA                     ,:DTATTR-FSCL-WK-NBR                          03920079
RONNA                     ,:DTATTR-WK-IN-MN-NBR                         03930079
RONNA                     ,:DTATTR-DAY-NM                               03940079
RONNA                     ,:DTATTR-DAY-OF-WK-NBR                        03950079
RONNA                     ,:DTATTR-FSCL-QTR-NBR                         03960079
RONNA                     ,:DTATTR-CAL-MN-NBR                           03970079
RONNA                     ,:DTATTR-JLN-DTE                              03980079
RONNA                     ,:DTATTR-LY-THIS-DAY-KY-DTE                   03990079
RONNA                     ,:DTATTR-FSCL-YR-BEG-DTE                      04000079
RONNA                     ,:DTATTR-FSCL-YR-END-DTE                      04010079
RONNA                     ,:DTATTR-FSCL-MN-BEG-DTE                      04020079
RONNA                     ,:DTATTR-FSCL-MN-END-DTE                      04030079
RONNA                     ,:DTATTR-FSCL-WK-BEG-DTE                      04040079
RONNA                     ,:DTATTR-FSCL-WK-END-DTE                      04050079
RONNA                     ,:DTATTR-SEA-DESC                             04060079
RONNA                     ,:DTATTR-SEA-BEG-DTE                          04070079
RONNA                     ,:DTATTR-SEA-END-DTE                          04080079
RONNA                     ,:DTATTR-PREV-SEA-END-DTE                     04090079
RONNA                     ,:DTATTR-PFM-END-DTE                          04100079
RONNA                     ,:DTATTR-NFM-END-DTE                          04110079
RONNA                     ,:DTATTR-PFW-END-DTE)                         04120079
RONNA          END-EXEC                                                 04130079
RONNA                                                                   04140079
RONNA          EVALUATE SQLCODE                                         04150079
RONNA              WHEN 0                                               04160079
RONNA                  ADD +1  TO PV-ML-TDTATTR-INSERTS                 04170079
RONNA              WHEN OTHER                                           04180079
RONNA              DISPLAY '**************************************'     04181079
RONNA              DISPLAY '** ABEND                            **'     04182079
RONNA              DISPLAY '** PARA = 8400-INSERT-EXP-VND       **'     04183079
RONNA              DISPLAY '**************************************'     04184079
RONNA              PERFORM Z900-SQL-ABEND                               04185079
RONNA          END-EVALUATE.                                            04186079
RONNA                                                                   04186179
      ******************************************************************04186279
      *    CALL THE CALENDAR ROUTINE                                   *04186379
      ******************************************************************04186479
       7200-CALL-CALENDAR-ROUTINE.                                      04186579
                                                                        04186679
           CALL DP500-KOHLS-CALENDAR-ROUTINE  USING DPG51 DPG52         04186779
                                                    DPG53 DPG54         04186879
                                                    DPG55 DPG56.        04186979
                                                                        04187079
           EVALUATE TRUE                                                04188079
               WHEN DPG54-SEVERE-ERROR                                  04189079
               WHEN DPG54-WARNING-ERROR                                 04190079
                   DISPLAY '** ABEND - PROGRAM '                        04200079
                           PC-CURRENT-PROGRAM-NAME ' **'                04210079
                   DISPLAY '** BAD CALL TO CALENDAR ROUTINE'            04220079
                   DISPLAY '** CONSTRUCTED BY PARA ' PV-DT-PARA         04230079
                   DISPLAY DPG54-ERROR-MESSAGE                          04240079
                   DISPLAY 'DPG51 INPUT PARMS: ' DPG51                  04250079
                   DISPLAY 'DPG52 INPUT PARMS: ' DPG52                  04260079
                   DISPLAY 'DPG53 INPUT PARMS: ' DPG53                  04270079
                   DISPLAY 'DPG54 RESPONSE   : ' DPG54                  04280079
                   DISPLAY 'DATE RECS WRITTEN: ' PV-ML-TDTATTR-INSERTS  04290079
                   MOVE +4019 TO ABEND-CODE                             04300079
                   CALL 'ILBOABN0' USING ABEND-CODE                     04310079
           END-EVALUATE.                                                04320079
                                                                        04330079
           MOVE SPACES TO PV-DT-PARA.                                   04340079
                                                                        04350079
      ******************************************************************04360079
      *  CLOSE ALL FILES AND PRINT CONTROL TOTALS.                     *04370079
      ******************************************************************04380079
       8000-EOJ-ROUTINE.                                                04390079
                                                                        04400079
           DISPLAY 'DATE RECS WRITTEN: ' PV-ML-TDTATTR-INSERTS.         04410079
                                                                        04420079
RONNA *    CLOSE DATE-CONTROL-CARD                                      04430079
RONNA *          ML-DATE-FILE.                                          04440079
                                                                        04450079
RONNA ******************************************************************04460079
RONNA *  STANDARD DB2 ERROR ABEND ROUTINE                               04470079
RONNA *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             04480079
RONNA ******************************************************************04490079
RONNA  Z900-SQL-ABEND.                                                  04500079
RONNA                                                                   04510079
RONNA      MOVE +4013                 TO ABEND-CODE.                    04520079
RONNA      DISPLAY '**  ABEND     **'.                                  04530079
RONNA      DISPLAY '**  PROGRAM   **  =  MLKRP054'.                     04540079
RONNA      DISPLAY '**  PARAGRAPH **  = ' PV-DT-PARA.                   04550079
RONNA      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.        04551079
RONNA                                                                   04552079
RONNA *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         04553079
RONNA *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        04554079
RONNA                                                                   04555079
RONNA      COPY DPPD004.                                                04556079
RONNA                                                                   04557079
RONNA      CALL 'ILBOABN0'  USING ABEND-CODE.                           04558079
RONNA                                                                   04559079
      ******************************************************************04559179
      *****      E N D   O F   P R O G R A M   M L K U T 4 0 2     *****04559279
      ******************************************************************04559379
