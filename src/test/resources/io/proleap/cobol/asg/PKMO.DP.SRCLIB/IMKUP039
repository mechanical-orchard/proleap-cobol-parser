       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    IMKUP039.                                                 
       AUTHOR.        PAUL KEBER.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  09/24/2004.                                               
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *  IMKUP039 - PERM PRICING TUPCPLS UPDATE                                 
      *****************************************************************         
      *                                                                         
      *  THIS PROGRAM WILL UPDATE TUPCPLS WITH NEW PRICES AND STATUS            
      *  CODES WHEN A PRICE CHANGE GOES INTO EFFECT.  IT WILL ALSO              
      *  CREATE BUSINESS EVENT 075 ITEM MESSAGE RECORDS AND ALSO                
      *  BUSINESS EVENT 080 ITEM MESSAGE RECORDS WITH UP TO 40 STORES           
      *  CONTAINING THE SAME PRICE FOR A SKU.                                   
      *  THESE MESSAGES WILL BE WRITTEN TO AN XS ITEM CONT CLEARANCE            
      *  MQSERIES QUEUE AS WELL AS INSERTED TO IMT_EVNT_MSG_BRG.                
      *  CHECKPOINT RESTART LOGIC IS USED IN THIS PROGRAM.                      
      *                                                                         
      *  TABLES ACCESSED ARE:                                                   
      *      TUPC    - UPC TABLE                                                
      *      TUPCPLS - UPC PHYSICAL LOCATION STATUS                             
      *      TVNDSTL - VENDOR STYLE                                             
      *      TMDARVS - MERCHANDISE AREA VENDOR STYLE                            
      *      TEXTENT - EXTERNAL ENTERPRISE                                      
      *      TUPCSTA - SKU STATUS TABLE                                         
      *      TMITGPL - MERCHANDISE ITEM GROUP PRICE AND QTY TABLE               
      *      TCKRSTCNTL - DB2 CHECKPOINT RESTART CONTROL                        
      *      TCKRSTINF  - DB2 CHECKPOINT RESTART INFORMATION                    
      *  TABLES UPDATED ARE:                                                    
      *      TUPCPLS - UPC PHYSICAL LOCATION STATUS                             
      *      TCKRSTCNTL - DB2 CHECKPOINT RESTART CONTROL                        
      *      TCKRSTINF  - DB2 CHECKPOINT RESTART INFORMATION                    
      *      IMT_EVNT_MSG_BRG - MESSAGE BRIDGE TO MQ SERIES - VSM               
      *  FILES READ ARE:                                                        
      *      IMRD041 PRICE UPDATE FILE FROM IMKUT061, SORTED BY SKU,            
      *      UNIT RETAIL AMOUNT (DESCENDING), STORE NUMBER SEQUENCE.            
      *      THE FILE IS READ VIA THE DB2 CHECKPOINTING SYSTEM.                 
      *****************************************************************         
      * 12/07/04 - PAUL KEBER       -  WR NO. 27472                             
      *    - REMOVED LOGIC TO COMPRESS RCP TO CLEARANCE SKUS                    
      * 12/15/04 - PAUL KEBER       -  WR NO. 27472                             
      *    - ONLY UPDATE STATUS CHANGE DATE IF THE STATUS CHANGED               
      * 03/15/05 - PAUL KEBER       -  WR NO. 27472                             
      *    - CHANGE TO NOT CREATE A TYPE 080 RECORD WITH ZERO STORES.           
      *****************************************************************         
      * 05/02/05 - D THEEL          -  WR NO. 28048                             
      *    - PERFORMANCE ENHANCEMENTS.  THIS PROGRAM WILL NOW UPDATE            
      *      ONLY TUPC,TUPCPLS, AND TSKXREF.  NEW PROGRAM IMKUP047              
      *      WILL UPDATE TSK, TSKULOG, AND TPPCPRC. THE INPUT FILE WILL         
      *      BE SORTED BY INTR UPC ID, DESCENDING UNIT RETAIL AMOUNT,           
      *      AND STORE.                                                         
      * 06/24/05 - D THEEL          -  WR NO. 28048                             
      *    -  ADD CONTROL CARD FILE WITH PROCESS OLD DATE INDICATOR.            
      *       THIS WILL BE DEFAULTED TO N. IF IT IS A Y, WE WILL PROCESS        
      *       A PRICE CHANGE THAT IS IN THE PAST.  REMOVED ALL LOGIC            
      *       CHECKING IF WE ARE RUNNING BEFORE OR AFTER MIDNIGHT.              
      *       THE EFFECTIVE BEGIN TIMESTAMP ON TUPCPLS WILL BE THE              
      *       PRICE CHANGE EFFECTIVE DATE WITH A TIME OF MIDNIGHT.              
      *       WHEN INACTIVATING THE CURRENT ROW, THE EFFECTIVE END              
      *       TIMESTAMP WILL BE DERIVED BY SUBTRACTING ONE MICROSECOND          
      *       FROM THE EFFECTIVE BEGIN TIMESTAMP.  ADDED DISPLAY                
      *       OF CURRENT TIMESTAMP.  ADDED LOGIC TO DELETE FUTURE               
      *       TUPCPLS ROWS WHEN PROCESSING A CLEARANCE TO                       
      *       ACTIVE PRICE CHANGE.                                              
      * 09/28/05 - D THEEL          -  WR NO. 29434                             
      *    -  CHANGES FOR SAS MARKDOWN PROJECT.  ADDED NEW CURSOR               
      *       TO GET SAS TEST STORES AND KEEP THEM IN WORKING STORAGE           
      *       TABLE.  MODIFIED STORE SPECIFIC PROCESSING LOGIC IN               
      *       2200 TO CHECK IF WE ARE PROCESSING A SAS STORE AND A              
      *       TUPCPLS ROW IS NOT FOUND - INSERT ONE.  ABEND IF MARKDOWN         
      *       CODE OF 2 OR F AND TUPCPLS ROW NOT FOUND.  IF PROCESSING          
      *       A RCP SAS STORE SET MKDN CDE TO 1.  IF PROCESSING A               
      *       CLEARANCE TO ACTIVE SET MARKDOWN CODE AND ORIG CLR DTE            
      *       TO NULL. IF PRICE CHANGE IS RCP OR CLR - SET THE ORIG CLR         
      *       DTE TO THE UPC STAT CHG DTE.  IF PRICE CHANGE IS MK2 SET          
      *       MKDN CDE TO 2, IF IT IS FNL SET MKDN CDE TO F.  START A           
      *       NEW 80 RECORD IF THE MARKDOWN CDE OR RETAIL CHANGES.              
      *       POPULATE NEW IMRD008 MARKDOWN CODE.                               
      *****************************************************************         
      * 12/21/05 - PAUL KEBER       -  IR NO. P000833647                        
      *    -  DROP MK2 OR FNL PRICE CHANGES IF THE SKU IS NOT STATUS 25         
      *       OR 30.  DISPLAY ERRORS AND END WITH RETURN CODE 4095.             
      *****************************************************************         
      * 01/11/06 - PAUL KEBER       -  WR NO. 29434                             
      *    -  CHANGES FOR SAS B MARKDOWN PROJECT.  ADDED NEW "SAS B"            
      *       STORE GROUP NAME CHECK TO THE SAS_STR_CSR CURSOR.                 
      *****************************************************************         
      * 02/13/06 - PAUL KEBER       -  IR NO. P000850113                        
      *    -  DROP MK2 OR FNL PRICE CHANGES IF THE SKU IS STATUS 25             
      *       AND THERE IS NO STORE SPECIFIC ROW ON TUPCPLS.                    
      *****************************************************************         
      * 02/15/06 - PAUL KEBER       -  IR NO. P000850306                        
      *    -  DROP MK2 OR FNL PRICE CHANGES IF A CLEARANCE TO ACTIVE            
      *       PRICE CHANGE WAS MADE TO THE SKU ON THE SAME DAY.                 
      *****************************************************************         
      * 03/27/06 - PAUL KEBER       -  IR NO. P000863508                        
      *    -  CHANGE TO AVOID SETTING A STATUS 30 SKU TO STATUS 25 ON           
      *       AN RCP PRICE CHANGE.                                              
      *****************************************************************         
      * 05/17/06 - PAUL KEBER       -  IR NO. P000881344                        
      *    -  CHANGE TO HANDLE A SECOND PRICE CHANGE FOR THE SAME STORE         
      *       SPECIFIC ROW ON A MK2 OR FNL PRICE CHANGE.                        
      *****************************************************************         
      * 06/29/06 - PAUL KEBER       -  WR NO. 29434                             
      *    - PERFORMANCE ENHANCEMENTS.  THIS PROGRAM WILL NOW UPDATE            
      *      ONLY TUPCPLS.  NEW PROGRAM IMKUP059 WILL NOW UPDATE TUPC           
      *      AND TSKXREF.  VENDOR STYLE RESERVATION LOGIC WAS REMOVED.          
      *****************************************************************         
      * 07/13/06 - PAUL KEBER       -  WR NO. 29434                             
      *    - ALWAYS SET IM008-STR-RTL-SKU-STAT-DSC-CHG TO 'Y' FOR 080           
      *      ITEM MESSAGES.                                                     
      *****************************************************************         
      * 07/19/06 - PAUL KEBER       -  WR NO. 29434                             
      *    - DROP MK2 OR FNL PRICE CHANGES IF THE SKU IS STATUS 30 AND          
      *      THERE IS NO STORE SPECIFIC ROW ON TUPCPLS.                         
      *****************************************************************         
      * 07/23/06 - PAUL KEBER       -  IR NO. P000902878                        
      *    - CHANGE TO DO MQ COMMIT WITH A DB2 COMMIT.                          
      *****************************************************************         
      * 07/25/06 - PAUL KEBER       -  IR NO. P000903829                        
      *    - FIX TO PREVENT AN INCORRECT RETAIL PRICE ON 075 ITEM               
      *      MESSAGES GENERATED FROM RCP TRANSACTIONS.                          
      *****************************************************************         
      * 12/21/06 - PAUL KEBER       -  WR NO. 29434                             
      *    - REMOVE ALL SAS MARKDOWN STORES LOGIC SINCE ALL STORES WILL         
      *      BE SAS.  SET MKDN_CDE TO '1' FOR ALL RCP TRANS STORES AND          
      *      STORE ZERO.  DON'T BYPASS PRICE CHANGE IF NO TUPCPLS STORE         
      *      ROW IS FOUND.  COMPRESS TUPCPLS ROWS FOR MK2 OR FNL PRICE          
      *      CHANGES IF ALL STORES HAVE THE SAME PRICE.  FOR CLEARANCE          
      *      PRICED SKUS WITH A PRICE CHANGE, BOTH RETAIL AND STATUS            
      *      CHANGE DATES WILL BE UPDATED TO INDICATE THIS IS A YELLOW          
      *      TO YELLOW TICKET CHANGE.                                           
      *****************************************************************         
PK0608* 06/09/08 - PAUL KEBER       -  IR NO. INC000000384169                   
PK0608*    - CHANGE TO DROP A 2ND PRICE CHANGE FOR THE SAME STORE               
PK0608*      SPECIFIC ROW ON AN RCP PRICE CHANGE. ALSO DROP PRICE               
PK0608*      CHANGES FOR THE SAME DATE IN 6250-INSERT-TUPCPLS.                  
      *****************************************************************         
PK0708* 07/15/08 - PAUL KEBER       -  IR NO. INC000000441273                   
PK0708*    - CHANGE TO DROP DUPLICATE PERM PRICE CHANGES FOR ALL PRICE          
PK0708*      CHANGE TYPES.                                                      
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT PRICE-UPDATE-FILE            ASSIGN TO INP01.                 
           SELECT CONTROL-CARD-FILE            ASSIGN TO INP02.                 
           SELECT QM-CONTROL-CARD-FILE         ASSIGN TO INP03.                 
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
       FILE SECTION.                                                            
                                                                                
       FD  PRICE-UPDATE-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 56 CHARACTERS                                        
           DATA RECORD IS PRICE-UPDATE-RECORD.                                  
       01  PRICE-UPDATE-RECORD         PIC  X(56).                              
      *                                                                         
       FD  CONTROL-CARD-FILE                                            00590094
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                          00600094
           BLOCK CONTAINS 0 RECORDS.                                    00610094
       01  CONTROL-RECORD              PIC X(80).                       00620094
                                                                                
       FD  QM-CONTROL-CARD-FILE                                                 
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  QM-CONTROL-RECORD           PIC X(80).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME                PIC  X(08)   VALUE                
                                                           'IMKUP039'.          
           05  PC-OUT-MSGLENGTH               PIC S9(09)   VALUE +450           
                                                           COMP.                
           05  PC-MAXIMUM-RETRY-COUNT         PIC S9(04)   VALUE +2             
                                                           COMP SYNC.           
           05  PC-MAX-STORES                  PIC  9(02)   VALUE 40.            
           05  PC-CLEARANCE                   PIC  X(02)   VALUE '30'.          
           05  PC-MIXED                       PIC  X(02)   VALUE '25'.          
           05  PC-ORIGIN-CNTRY                PIC  X(03)   VALUE 'US '.         
           05  PC-DEFAULT-TIMESTAMP           PIC  X(26)   VALUE                
               '9999-09-09-23.59.00.000000'.                                    
           05  PC-TIMESTAMP-INACTIVE-TIME     PIC  X(16)   VALUE                
               '-23.59.59.999999'.                                              
           05  PC-TIMESTAMP-ACTIVE-TIME       PIC  X(16)   VALUE                
               '-00.00.00.000000'.                                              
                                                                                
      ******************************************************************        
      * PV PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01  PV-WORK-VARIABLES.                                                   
           05  PV-UPC-COMP-10                 PIC S9(10)   VALUE +0             
                                                           COMP.                
           05  PV-COMPLETION-CODE             PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-REASON                      PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-REASON-TEXT                 PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-UNT-COST-NULL-IND           PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-ORIG-CLR-NULL-IND           PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-MKDN-CDE-NULL-IND           PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-RETRY-COUNT                 PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-PREV-STR-NBR                PIC S9(04)   VALUE +0             
                                                           COMP.                
                                                                                
           05  PV-PARAGRAPH-NAME              PIC  X(30)   VALUE SPACES.        
           05  PV-OPERATION-MSG-1             PIC  X(40)   VALUE SPACES.        
           05  PV-DB2-OPERATION-ATTEMPTED     PIC  X(30)   VALUE SPACES.        
           05  PV-NEW-SKU-STATUS              PIC  X(02)   VALUE SPACES.        
               88  PV-NEW-SKU-STATUS-ACTIVE                VALUE '20'.          
               88  PV-NEW-SKU-STATUS-CLEARANCE             VALUE '30'.          
               88  PV-NEW-SKU-STATUS-MIXED                 VALUE '25'.          
           05  PV-HOLD-UIR-EFF-DATE.                                            
               10  PV-HOLD-UIR-EFF-CC         PIC  X(02)   VALUE SPACES.        
               10  PV-HOLD-UIR-EFF-YY         PIC  X(02)   VALUE SPACES.        
               10  PV-HOLD-UIR-EFF-DASH1      PIC  X(01)   VALUE '-'.           
               10  PV-HOLD-UIR-EFF-MM         PIC  X(02)   VALUE SPACES.        
               10  PV-HOLD-UIR-EFF-DASH2      PIC  X(01)   VALUE '-'.           
               10  PV-HOLD-UIR-EFF-DD         PIC  X(02)   VALUE SPACES.        
           05  PV-COMMIT-TIMESTAMP            PIC  X(26)   VALUE SPACES.        
           05  PV-CURRENT-TIMESTAMP           PIC  X(26)   VALUE SPACES.        
           05  PV-RUN-DATE                    PIC  X(10)   VALUE SPACES.        
           05  PV-SAVE-SKU                    PIC S9(08)   VALUE +0             
                                                           COMP-3.              
           05  PV-PREV-SKU                    PIC S9(08)   VALUE +0             
                                                           COMP-3.              
           05  PV-PREV-PRCHG-TYP-CDE          PIC  X(03)   VALUE SPACES.        
           05  PV-PREV-UNT-RTL-AMT            PIC S9(7)V99 VALUE +0             
                                                           COMP-3.              
           05  PV-OLD-UNT-RTL-AMT             PIC S9(7)V99 VALUE +0             
                                                           COMP-3.              
           05  PV-OLD-MEITGP-NBR              PIC S9(09)   VALUE +0             
                                                           COMP.                
           05  PV-SAVE-REGN-PRIC-IND          PIC  X(01)   VALUE SPACES.        
           05  PV-STORE0-UPC-STAT-CDE         PIC  X(02)   VALUE SPACES.        
           05  PV-STORE0-ORIG-CLR-DTE         PIC  X(10)   VALUE SPACES.        
           05  PV-RECS-READ-CNTR              PIC  9(09)   VALUE ZEROES.00830000
           05  PV-SQLCODE-DISPLAY             PIC +(8)9    VALUE ZEROES.00830000
           05  PV-UPCPLS-UPC-ID-NUM           PIC  9(10)   VALUE ZERO.          
           05  PV-DETAIL-KEY.                                                   
               10  PV-DK-SKU                  PIC  X(08)   VALUE SPACES.        
               10  PV-DK-STORE-NBR            PIC  9(04)   VALUE ZERO.          
           05  PV-DELAY-INTERVAL.                                               
               10  FILLER                   PIC X(02)   VALUE SPACES.           
               10  PV-DELAY-TIME            PIC 9(08)   VALUE 6000.             
           05  WS-SYSWAIT                   PIC X(08)   VALUE 'SYSWAIT'.        
           05  PV-UPCPLS-EFF-BEG-TMST       PIC X(26)   VALUE SPACES.           
           05  PV-UPCPLS-EFF-END-TMST       PIC X(26)   VALUE SPACES.           
           05  PV-DISPLAY-10                PIC 9(10)   VALUE ZERO.             
                                                                                
      ******************************************************************        
      * CR CHECKPOINT RESTART VARIABLES                                *        
      ******************************************************************        
       01  CR-CHECKPOINT-RESTART-AREA.                                          
           05  CR-AREA-LEN                    PIC S9(04) VALUE +138             
                                                         COMP.                  
           05  CR-CHECKPOINT-RESTART-VAR.                                       
               10  CR-DETAIL-KEY              PIC  X(12) VALUE SPACES.          
               10  CR-RECS-PROCESSED-CNTR     PIC  9(09) VALUE ZERO.            
               10  CR-RECS-READ-CNTR          PIC  9(09) VALUE ZERO.            
               10  CR-SKUS-READ-CNTR          PIC  9(09) VALUE ZERO.            
               10  CR-SKUS-NOT-FOUND-CNTR     PIC  9(09) VALUE ZERO.            
               10  CR-TUPCPLS-ROW-UPD-CNTR    PIC  9(09) VALUE ZERO.            
               10  CR-TUPCPLS-ROW-INS-CNTR    PIC  9(09) VALUE ZERO.            
               10  CR-TUPCPLS-ROW-INACTV-CNTR PIC  9(09) VALUE ZERO.            
               10  CR-TUPCPLS-ROW-DEL-CNTR    PIC  9(09) VALUE ZERO.            
               10  CR-MSGS-TO-XSITEM-QUEUE    PIC  9(09) VALUE ZERO.            
               10  CR-TYPE-75-MSGS-CREATED    PIC  9(09) VALUE ZERO.            
               10  CR-TYPE-80-MSGS-CREATED    PIC  9(09) VALUE ZERO.            
               10  CR-IMT-EVNT-MSG-ROW-INS-CNTR                                 
                                              PIC  9(09) VALUE ZERO.            
               10  CR-COMMIT-CNTR             PIC  9(09) VALUE ZERO.            
               10  CR-SKU-STRS-BYPASSED-CNTR  PIC  9(09) VALUE ZERO.            
                                                                                
      ******************************************************************        
      * PS PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-RETAIL-CHANGED-SW           PIC  X(01)   VALUE 'N'.           
               88  PS-RETAIL-CHANGED-YES                   VALUE 'Y'.           
               88  PS-RETAIL-CHANGED-NO                    VALUE 'N'.           
           05  PS-STATUS-CHANGED-SW           PIC  X(01)   VALUE 'N'.           
               88  PS-STATUS-CHANGED                       VALUE 'Y'.           
               88  PS-STATUS-CHANGED-NO                    VALUE 'N'.           
           05  PS-END-OF-FILE-SW              PIC  X(01)   VALUE 'N'.           
               88  PS-END-OF-FILE                          VALUE 'Y'.           
           05  PS-FIRST-COMMIT-SW             PIC  X(01)   VALUE 'N'.           
               88  PS-FIRST-COMMIT                         VALUE 'Y'.           
           05  PS-FIRST-SKU-STORE-SW          PIC  X(01)   VALUE 'N'.           
               88  PS-FIRST-SKU-STORE                      VALUE 'Y'.           
               88  PS-NOT-FIRST-SKU-STORE                  VALUE 'N'.           
           05  PS-NO-DOCUMENTS-SW             PIC  X(01)   VALUE 'N'.           
               88  PS-NO-DOCUMENTS                         VALUE 'Y'.           
           05  PS-CURRENT-ROW-FOUND-SW        PIC  X(01)   VALUE 'N'.           
               88  PS-CURRENT-ROW-FOUND                    VALUE 'Y'.           
               88  PS-CURRENT-ROW-NOT-FOUND                VALUE 'N'.           
           05  PS-HAVE-UPCPLS-COST-INFO-SW    PIC  X(01)   VALUE 'N'.           
               88  PS-HAVE-UPCPLS-COST-INFO                VALUE 'Y'.           
               88  PS-NOT-HAVE-UPCPLS-COST-INFO            VALUE 'N'.           
           05  PS-HAVE-STORE0-TUPCPLS-SW      PIC  X(01)   VALUE 'N'.           
               88  PS-HAVE-STORE0-TUPCPLS                  VALUE 'Y'.           
               88  PS-NOT-HAVE-STORE0-TUPCPLS              VALUE 'N'.           
           05  PS-IMT-EVNT-MSG-STARTED-SW     PIC  X(01)   VALUE 'N'.           
               88  PS-IMT-EVNT-MSG-STARTED                 VALUE 'Y'.           
               88  PS-IMT-EVNT-MSG-NOT-STARTED             VALUE 'N'.           
           05  PS-SKU-INFO-FOUND-SW           PIC  X(01)   VALUE 'N'.           
               88  PS-SKU-INFO-FOUND                       VALUE 'Y'.           
               88  PS-SKU-INFO-NOT-FOUND                   VALUE 'N'.           
           05  PS-NEW-CLR-RCP-SW              PIC  X(01)   VALUE 'N'.           
               88  PS-NEW-CLR-RCP                          VALUE 'Y'.           
               88  PS-NOT-NEW-CLR-RCP                      VALUE 'N'.           
           05  PS-CHECKPOINT-RESTART-SW       PIC  X(01)   VALUE 'N'.           
               88  PS-CHECKPOINT-RESTART                   VALUE 'Y'.           
               88  PS-NOT-CHECKPOINT-RESTART               VALUE 'N'.           
           05  PS-STRS-ALL-SAME-PRICE-SW      PIC  X(01)   VALUE 'N'.           
               88  PS-STRS-ALL-SAME-PRICE                  VALUE 'Y'.           
               88  PS-NOT-STRS-ALL-SAME-PRICE              VALUE 'N'.           
                                                                                
       01  ABEND-CODE                         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           88  PV-ABEND-4001                   VALUE +4001.                     
      *        NO CONTROL CARD 1                                                
           88  PV-ABEND-4002                   VALUE +4002.                     
      *        INCORRECT CONTROL CARD DATA                                      
           88  PV-ABEND-4003                   VALUE +4003.                     
      *        EFFECTIVE DATE LESS THAN CURRENT DATE                            
           88  PV-ABEND-4004                   VALUE +4004.                     
      *        INVALID CONTROL CARD JOB NAME                                    
           88  PV-ABEND-4005                   VALUE +4005.                     
      *        INVALID CONTROL CARD XS ITEM QUEUE NAME                          
           88  PV-ABEND-4006                   VALUE +4006.                     
      *        NO QUEUE MANAGER CONTROL CARD                                    
           88  PV-ABEND-4007                   VALUE +4007.                     
      *        INVALID CONTROL CARD QUEUE MANAGER NAME                          
           88  PV-ABEND-4008                   VALUE +4008.                     
      *        NO TUPCPLS ROW FOR FINAL OR MARK2                                
           88  PV-ABEND-4013                   VALUE +4013.                     
      *        DB2 SQL ABEND                                                    
           88  PV-ABEND-4020                   VALUE +4020.                     
      *        MQSERIES ABEND                                                   
                                                                                
       01  DISPLAY-EDITS.                                                       
           05  DI-RECS-PROCESSED-CNTR       PIC  Z(08)9  VALUE ZEROS.           
           05  DI-RECS-READ-CNTR            PIC  Z(08)9  VALUE ZEROS.           
           05  DI-SKUS-READ-CNTR            PIC  Z(08)9  VALUE ZEROS.           
           05  DI-SKUS-NOT-FOUND-CNTR       PIC  Z(08)9  VALUE ZEROS.           
           05  DI-SKU-STRS-BYPASSED-CNTR    PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPCPLS-ROW-UPDATE-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPCPLS-ROW-INSERT-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPCPLS-ROW-INACTV-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TUPCPLS-ROW-DELETE-CNTR   PIC  Z(08)9  VALUE ZEROS.           
           05  DI-MSGS-TO-XSITEM-QUEUE      PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TYPE-75-MSGS-CREATED      PIC  Z(08)9  VALUE ZEROS.           
           05  DI-TYPE-80-MSGS-CREATED      PIC  Z(08)9  VALUE ZEROS.           
           05  DI-IMT-EVNT-ROW-INSERT-CNTR  PIC  Z(08)9  VALUE ZEROS.           
           05  DI-COMMIT-CNTR               PIC  Z(08)9  VALUE ZEROS.           
           05  DI-INPUT-RECS-DISPLAY        PIC  9(06)   VALUE ZEROS.           
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR CONTROL CARD FILE.                                    
      * CC PROCESS OLD DATE WILL DEFAULT TO N.  IT WILL BE Y ONLY IF    00840094
      * WE NEED TO PROCESS A PRICE CHANGE THAT IS IN THE PAST.          00840094
      ****************************************************************          
       01  CC-CONTROL-CARD.                                                     
           05  FILLER                       PIC X(09)    VALUE                  
               'JOB NAME:'.                                                     
           05  CC-JOB-NAME                  PIC X(08)    VALUE SPACES.          
           05  FILLER                       PIC X(13)    VALUE                  
               ' PROCESS OLD:'.                                                 
           05  CC-PROCESS-OLD-DATE          PIC X(01)    VALUE SPACE.           
           05  FILLER                       PIC X(01)    VALUE SPACE.           
           05  CC-XSITEM-CC-QUEUE-NAME      PIC X(48)    VALUE SPACES.          
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR QUEUE MANAGER CONTROL CARD FILE.                      
      ****************************************************************          
       01  QM-CONTROL-CARD.                                                     
           05  QM-QUEUE-MANAGER-NAME        PIC X(48)    VALUE SPACES.          
           05  FILLER                       PIC X(32)    VALUE SPACES.          
                                                                                
      *****************************************************************         
      *    PERM PRICE UPDATE FILE LAYOUT.                                       
      *****************************************************************         
           COPY IMRD048.                                                        
                                                                                
      *****************************************************************         
      *  LEGACY ITEM MAINTENANCE INFO COPYBOOK                                  
      *  FOR ALL BUSINESS EVENTS                                                
      *****************************************************************         
           COPY IMRD008.                                                        
                                                                                
      *----------------------------------------------------------               
      * MQ WORKING STORAGE                                                      
      *----------------------------------------------------------               
       01  MQV-WORKING-STORAGE.                                                 
           05  MQV-MQCONN               PIC  X(08) VALUE 'CSQBCONN'.            
           05  MQV-MQOPEN               PIC  X(08) VALUE 'CSQBOPEN'.            
           05  MQV-MQPUT                PIC  X(08) VALUE 'CSQBPUT '.            
           05  MQV-MQGET                PIC  X(08) VALUE 'CSQBGET '.            
           05  MQV-MQCMIT               PIC  X(08) VALUE 'CSQBCOMM'.            
           05  MQV-MQBACK               PIC  X(08) VALUE 'CSQBBACK'.            
           05  MQV-MQCLOSE              PIC  X(08) VALUE 'CSQBCLOS'.            
           05  MQV-MQDISC               PIC  X(08) VALUE 'CSQBDISC'.            
           05  MQV-MQCHECK              PIC  X(08) VALUE 'MQR2C'.               
           05  MQV-HCONN                PIC S9(09) COMP   VALUE +0.             
           05  MQV-COMPLETION-CODE      PIC S9(09) COMP   VALUE +0.             
           05  MQV-REASON               PIC S9(09) COMP   VALUE +0.             
           05  MQV-OPEN-OPTIONS         PIC S9(09) COMP   VALUE +0.             
           05  MQV-PERM-HANDLE          PIC S9(09) COMP   VALUE +0.             
           05  MQV-REASON-TEXT          PIC S9(09) COMP   VALUE +0.             
                                                                                
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      *  TVNDSTL - VENDOR STYLE TABLE                                           
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TVNDSTL                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TUPCPLS - UPC PHYSICAL LOCATION STATUS TABLE                           
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TUPCPLS                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TUPC - UPC TABLE                                                       
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TUPC                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TMDARVS - MERCHANDISE AREA VENDOR STYLE TABLE                          
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TMDARVS                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TEXTENT - EXTERNAL ENTERPRISE TABLE                                    
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TEXTENT                                                 
           END-EXEC.                                                            
                                                                                
      ************************************************************              
      *  TUPCSTA - SKU STATUS TABLE                                             
      ************************************************************              
           EXEC SQL                                                             
               INCLUDE TUPCSTA                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  MERCHANDISE ITEM GROUP PRICE AND QTY TABLE                             
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMITGPL                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  IMT_EVNT_MSG_BRG - ITEM EVENT MESSAGE BRIDGE TABLE                     
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE IMT00001                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    COBOL DECLARATIONS FOR THE CHECKPOINT RESTART CONTROL TABLE          
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    COBOL DECL FOR THE CHECKPOINT RESTART INFO TABLE                     
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      *-----------------------------------------------------------              
      * OBJECT-DESCRIPTOR - MQ SERIES                                           
      *-----------------------------------------------------------              
       01  MQM-OBJECT-DESCRIPTOR.                                               
           COPY CMQODV.                                                         
                                                                                
      *-----------------------------------------------------------              
      * MESSAGE-DESCRIPTOR - MQ SERIES                                          
      *-----------------------------------------------------------              
       01  MQM-MESSAGE-DESCRIPTOR.                                              
           COPY CMQMDV.                                                         
                                                                                
      *-----------------------------------------------------------              
      * GET-MESSAGE-OPTIONS - MQ SERIES                                         
      *-----------------------------------------------------------              
       01  MQM-GET-MESSAGE-OPTIONS.                                             
           COPY CMQGMOV.                                                        
                                                                                
      *-----------------------------------------------------------              
      * PUT-MESSAGE-OPTIONS - MQ SERIES                                         
      *-----------------------------------------------------------              
       01  MQM-PUT-MESSAGE-OPTIONS.                                             
           COPY CMQPMOV.                                                        
                                                                                
      *-----------------------------------------------------------              
      * CONSTANTS - MQ SERIES                                                   
      *-----------------------------------------------------------              
       01  MQM-CONSTANTS.                                                       
           COPY CMQV.                                                           
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      * 0000-MAIN-MODULE.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
           MOVE '0000-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-PRICE-UPDATES                                   
             UNTIL PS-END-OF-FILE.                                              
                                                                                
           PERFORM 3000-EOJ-PROCESSING.                                         
                                                                                
           IF CR-SKUS-NOT-FOUND-CNTR > ZERO                                     
             OR CR-SKU-STRS-BYPASSED-CNTR > ZERO                                
               MOVE 4095 TO RETURN-CODE                                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZATION.                                           *        
      *  ==> PROCESSES:                                                *        
      *      - CHECKS FOR RESTART PROCESSING.                          *        
      *      - INITIALIZES VARIABLES.                                  *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
           MOVE '1000-INITIALIZATION ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           OPEN INPUT PRICE-UPDATE-FILE                                         
                      CONTROL-CARD-FILE                                         
                      QM-CONTROL-CARD-FILE.                                     
                                                                                
           PERFORM 4100-READ-CONTROL-CARD-1.                                    
           PERFORM 4200-READ-CONTROL-CARD-2.                                    
                                                                                
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                                 
                                                                                
      ******************************************************************        
      *    IF PROGRAM IS IN A RESTART STATUS.  SELECT SAVED DATA FROM  *        
      *    THE RESTART INFO TABLE.                                     *        
      ******************************************************************        
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
               PERFORM 7500-SELECT-RESTART-INFO                                 
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT                               
                                   TO CR-CHECKPOINT-RESTART-VAR                 
               MOVE CR-DETAIL-KEY  TO PV-DETAIL-KEY                             
               SET PS-CHECKPOINT-RESTART TO TRUE                                
           ELSE                                                                 
               SET PS-FIRST-COMMIT TO TRUE                                      
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                             
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-RECORD                                             
               UNTIL PV-RECS-READ-CNTR > CR-RECS-PROCESSED-CNTR                 
                  OR PS-END-OF-FILE.                                            
                                                                                
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
               DISPLAY '** RESTART RUN **'                              06720800
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD: '             02067000
                        PV-RECS-READ-CNTR                               02068000
               DISPLAY 'SKU NUMBER:   ' IM048-UIR-SKU                   06720900
               DISPLAY 'STORE NUMBER: ' IM048-UIR-STR-NBR               06720800
           END-IF.                                                              
                                                                                
           IF PS-END-OF-FILE                                                    
PK0708       AND TCKRSTCNTL-CKPT-STAT-CODE = '0'                                
               SET PS-NO-DOCUMENTS TO TRUE                                      
               DISPLAY ' '                                                      
               DISPLAY '***********************************'                    
               DISPLAY ' NO PRICE CHANGES EFFECTIVE TODAY.'                     
               DISPLAY '***********************************'                    
               DISPLAY ' '                                                      
           ELSE                                                                 
               PERFORM 7100-GET-COMMIT-TIMESTAMP                                
               PERFORM 7200-SET-CURRENT-TIMESTAMP                               
      *******  SET PV-UPCPLS-EFF-BEG-TMST = IM048-UIR-EFF-DTE MIDNIGHT          
               MOVE IM048-UIR-EFF-DTE TO                                        
                                   PV-UPCPLS-EFF-BEG-TMST (1:10)                
               MOVE PC-TIMESTAMP-ACTIVE-TIME TO                                 
                     PV-UPCPLS-EFF-BEG-TMST (11:16)                             
               PERFORM 7600-CALC-END-TMST                                       
               MOVE ZERO TO IM008-BUSINESS-EVENT-ID                             
               IF IM048-UIR-EFF-DTE < PV-RUN-DATE                               
                   IF CC-PROCESS-OLD-DATE = 'N'                                 
                       DISPLAY '*** EFF DATE LESS THAN CURRENT DATE ***'        
                       DISPLAY '*** INPUT FILE EFFECTIVE DATE: '                
                               IM048-UIR-EFF-DTE                                
                               ' RUN DATE: ' PV-RUN-DATE                        
                       MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME          
                       SET PV-ABEND-4003 TO TRUE                                
                       PERFORM 9999-ABEND                                       
                   ELSE                                                         
                       DISPLAY '*** PROCESSING EFFECTIVE DATE LESS THAN'        
                               ' CURRENT DATE ***'                              
                       DISPLAY '*** INPUT FILE EFFECTIVE DATE: '                
                               IM048-UIR-EFF-DTE                                
                               ' RUN DATE: ' PV-RUN-DATE                        
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 1100-CONNECT-TO-QMGR.                                        
           PERFORM 1200-OPEN-XSITEM-CC-QUEUE.                                   
                                                                                
      ******************************************************************        
      * 1100-CONNECT-TO-QMGR.                                                   
      *     CONNECT TO THE QUEUE MANAGER.                                       
      ******************************************************************        
       1100-CONNECT-TO-QMGR.                                                    
                                                                                
           CALL MQV-MQCONN USING QM-QUEUE-MANAGER-NAME                          
                                 MQV-HCONN                                      
                                 MQV-COMPLETION-CODE                            
                                 MQV-REASON.                                    
                                                                                
           EVALUATE TRUE                                                        
              WHEN MQV-COMPLETION-CODE = MQCC-OK                                
                   DISPLAY ' '                                                  
                   DISPLAY 'SUCCESSFULLY CONNECTED TO: '                        
                                                  QM-QUEUE-MANAGER-NAME         
              WHEN OTHER                                                        
                   CALL MQV-MQCHECK USING MQV-REASON                            
                                          MQV-REASON-TEXT                       
                   DISPLAY 'QUEUE CONNECTING TO ' QM-QUEUE-MANAGER-NAME         
                   MOVE '1100-CONNECT-TO-QMGR' TO PV-PARAGRAPH-NAME             
                   MOVE 'UNABLE TO CONNECT'    TO PV-OPERATION-MSG-1            
                   MOVE MQV-COMPLETION-CODE    TO PV-COMPLETION-CODE            
                   MOVE MQV-REASON             TO PV-REASON                     
                   MOVE MQV-REASON-TEXT        TO PV-REASON-TEXT                
                   PERFORM 9400-MQ-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 1200-OPEN-XSITEM-CC-QUEUE.                                              
      *     OPEN THE XS ITEM CONTINUED CLEARANCE QUEUE.                         
      ******************************************************************        
       1200-OPEN-XSITEM-CC-QUEUE.                                               
                                                                                
           MOVE CC-XSITEM-CC-QUEUE-NAME  TO MQOD-OBJECTNAME                     
           MOVE QM-QUEUE-MANAGER-NAME    TO MQOD-OBJECTQMGRNAME.                
                                                                                
           COMPUTE MQV-OPEN-OPTIONS      = MQOO-OUTPUT                          
                                         + MQOO-SET-ALL-CONTEXT                 
                                         + MQOO-FAIL-IF-QUIESCING.              
                                                                                
           CALL MQV-MQOPEN USING MQV-HCONN                                      
                                 MQM-OBJECT-DESCRIPTOR                          
                                 MQV-OPEN-OPTIONS                               
                                 MQV-PERM-HANDLE                                
                                 MQV-COMPLETION-CODE                            
                                 MQV-REASON.                                    
                                                                                
           EVALUATE TRUE                                                        
              WHEN MQV-COMPLETION-CODE = MQCC-OK                                
                   DISPLAY 'SUCCESSFULLY OPENED QUEUE: '                        
                                                CC-XSITEM-CC-QUEUE-NAME         
              WHEN OTHER                                                        
                   CALL MQV-MQCHECK USING MQV-REASON                            
                                         MQV-REASON-TEXT                        
                   DISPLAY 'CANNOT OPEN QUEUE ' CC-XSITEM-CC-QUEUE-NAME         
                   MOVE '1200-OPEN-XSITEM-CC-QUEUE'                             
                                             TO PV-PARAGRAPH-NAME               
                   MOVE 'CANNOT OPEN QUEUE'  TO PV-OPERATION-MSG-1              
                   MOVE MQV-COMPLETION-CODE  TO PV-COMPLETION-CODE              
                   MOVE MQV-REASON           TO PV-REASON                       
                   MOVE MQV-REASON-TEXT      TO PV-REASON-TEXT                  
                   PERFORM 8600-DISCONNECT-QMGR                                 
                   PERFORM 9400-MQ-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 2000-PROCESS-PRICE-UPDATES.                                    *        
      *  ==> PROCESSES:                                                *        
      *    - PROCESSES A PRICE UPDATE RECORD AND READS NEXT PRICE      *        
      *    - UPDATE RECORD                                             *        
      ******************************************************************        
       2000-PROCESS-PRICE-UPDATES.                                              
           MOVE '2000-PROCESS-PRICE-UPDATES' TO PV-PARAGRAPH-NAME.              
                                                                                
           INITIALIZE PV-MKDN-CDE-NULL-IND                                      
                      UPCPLS-MKDN-CDE.                                          
           IF IM048-UIR-SKU = PV-SAVE-SKU                                       
               SET PS-NOT-FIRST-SKU-STORE TO TRUE                               
           ELSE                                                                 
      *        DETERMINE IF A TYPE 80 IMT_EVNT_MSG_BRG REC IS NOT YET           
      *        INSERTED FOR THE LAST SKU PROCESSED                              
               IF PS-IMT-EVNT-MSG-STARTED                                       
                   IF IM008-BUS-EVENT-STR-RTL-ST-CHG                            
                     AND IM008-STR-RTL-STORE-COUNT > ZERO                       
                       ADD 1 TO CR-TYPE-80-MSGS-CREATED                         
                       PERFORM 4500-PUT-ON-XSITEM-CC-QUEUE                      
                       PERFORM 6800-INSERT-IMT-EVNT-MSG                         
                       MOVE ZERO       TO IM008-STR-RTL-STORE-COUNT             
                       MOVE LOW-VALUES TO IM008-STR-RTL-STORE-TABLE             
                   END-IF                                                       
                   SET PS-IMT-EVNT-MSG-NOT-STARTED                              
                                       TO TRUE                                  
               END-IF                                                           
               PERFORM 7000-COMMIT-PROCESSING                                   
      *        LOGIC TO BEGIN PROCESSING A NEW SKU                              
               PERFORM 5000-SELECT-STYL-SKU-INFO                                
               MOVE IM048-UIR-SKU          TO PV-SAVE-SKU                       
               MOVE IM048-UIR-STAT-CDE     TO PV-STORE0-UPC-STAT-CDE            
               IF PS-SKU-INFO-FOUND                                             
                   MOVE UPC-REGN-PRIC-IND  TO PV-SAVE-REGN-PRIC-IND             
                   MOVE UPC-UPC-ID         TO UPCPLS-UPC-ID                     
                   MOVE IM048-UIR-ITM-NBR  TO UPCPLS-ITM-NBR                    
                   MOVE SPACES             TO PV-NEW-SKU-STATUS                 
                   SET PS-FIRST-SKU-STORE  TO TRUE                              
                   SET PS-NOT-NEW-CLR-RCP  TO TRUE                              
                   SET PS-NOT-HAVE-UPCPLS-COST-INFO                             
                                           TO TRUE                              
                   SET PS-NOT-HAVE-STORE0-TUPCPLS                               
                                           TO TRUE                              
                   SET PS-NOT-STRS-ALL-SAME-PRICE                               
                                           TO TRUE                              
                   IF IM048-PRCHGTYP-CLR-TO-ACTIVE                              
                     OR IM048-PRCHGTYP-CLEARNCE                                 
                     OR IM048-PRCHGTYP-REG-CLEARNCE                             
      *                DETERMINE IF THE SKU STATUS IS CHANGING                  
                       PERFORM 2050-DETERMINE-NEW-SKU-STATUS                    
                   ELSE                                                         
                       IF IM048-PRCHGTYP-2ND-CLR-MRKDWN                         
                         OR IM048-PRCHGTYP-FINL-CLR-MRKDWN                      
                           IF IM048-UIR-STR-NBR = 0                             
                             AND IM048-UIR-ALL-SAME-PRICE                       
                               SET PS-STRS-ALL-SAME-PRICE TO TRUE               
                           ELSE                                                 
      *                        CHECK FOR SKU GOING FROM NON-RCP TO RCP          
      *                        BECAUSE A PREVIOUS MK2 OR FNL ALL SAME           
      *                        PRICED MADE THE SKU NON-RCP.                     
                               IF PV-SAVE-REGN-PRIC-IND = 'N'                   
                                   SET PS-NEW-CLR-RCP TO TRUE                   
                               END-IF                                           
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           SET PS-RETAIL-CHANGED-NO                                             
               PS-STATUS-CHANGED-NO TO TRUE.                                    
                                                                                
      *    SKU EXISTS                                                           
           IF PS-SKU-INFO-FOUND                                                 
      *        PERFORM STORE ZERO UPDATES                                       
               IF IM048-UIR-STR-NBR = 0                                         
                 OR (PV-NEW-SKU-STATUS-MIXED                                    
                     AND PS-FIRST-SKU-STORE)                                    
PK0708            IF IM048-UIR-SKU = PV-PREV-SKU                                
PK0708              AND IM048-UIR-STR-NBR = PV-PREV-STR-NBR                     
PK0708*               BYPASS A MARKDOWN IF THE SKU STORE IS A DUPLICATE         
PK0708                 DISPLAY '*** BYPASSING 2ND PRICE CHANGE FOR A S'         
PK0708                         'KU/STORE'                                       
PK0708                 DISPLAY 'SKU NBR: ' IM048-UIR-SKU                0672    
PK0708                         ' STORE: ' IM048-UIR-STR-NBR             0672    
PK0708                         ' 1ST PRCHG TYPE: ' PV-PREV-PRCHG-TYP-CDE0672    
PK0708                         ' 1ST PRICE: ' PV-PREV-UNT-RTL-AMT       0672    
PK0708                         ' DROPPED PRCHG TYPE: '                  0672    
PK0708                                  IM048-UIR-PRCHG-TYP-CDE                 
PK0708                         ' DROPPED PRICE: '                       0672    
PK0708                                  IM048-UIR-NEW-UNT-RTL-AMT               
PK0708                 ADD +1 TO CR-SKU-STRS-BYPASSED-CNTR                      
PK0708             ELSE                                                         
                       PERFORM 2100-SKU-PROCESSING                              
PK0708             END-IF                                                       
               END-IF                                                           
                                                                                
      *        PERFORM RCP STORE SPECIFIC UPDATES EXCEPT FOR A FINAL            
      *        MARKDOWN PRICE CHANGE WITH ALL STORES THE SAME PRICE.            
               IF IM048-UIR-STR-NBR > 0                                         
                 AND NOT PS-STRS-ALL-SAME-PRICE                                 
                   IF IM048-UIR-SKU = PV-PREV-SKU                               
                     AND IM048-UIR-STR-NBR = PV-PREV-STR-NBR                    
      *                BYPASS AN RCP, MK2 OR FNL MARKDOWN IF THE SKU            
      *                STORE IS A DUPLICATE                                     
                       DISPLAY '*** BYPASSING RCP, MK2, OR FNL - 2ND PR'        
                               'ICE CHANGE'                                     
                       DISPLAY 'SKU NBR: ' IM048-UIR-SKU                0672    
                               ' STORE: ' IM048-UIR-STR-NBR             0672    
                               ' 1ST PRCHG TYPE: ' PV-PREV-PRCHG-TYP-CDE0672    
                               ' 1ST PRICE: ' PV-PREV-UNT-RTL-AMT       0672    
                               ' DROPPED PRCHG TYPE: '                  0672    
                                        IM048-UIR-PRCHG-TYP-CDE                 
                               ' DROPPED PRICE: '                       0672    
                                        IM048-UIR-NEW-UNT-RTL-AMT               
                       ADD +1 TO CR-SKU-STRS-BYPASSED-CNTR                      
                   ELSE                                                         
                       PERFORM 2200-SKU-STORE-PROCESSING                        
                   END-IF                                                       
               END-IF                                                           
                                                                                
           ELSE                                                                 
      *        SKU DOESN'T EXIST                                                
               DISPLAY 'BYPASSING RECORD FOR SKU: ' IM048-UIR-SKU               
                       ' STORE: ' IM048-UIR-STR-NBR                             
           END-IF.                                                              
                                                                                
           ADD +1 TO CR-RECS-PROCESSED-CNTR.                                    
           SET PS-NOT-CHECKPOINT-RESTART TO TRUE.                               
                                                                                
           MOVE IM048-UIR-SKU TO PV-PREV-SKU.                                   
           MOVE IM048-UIR-STR-NBR TO PV-PREV-STR-NBR.                           
           MOVE IM048-UIR-PRCHG-TYP-CDE TO PV-PREV-PRCHG-TYP-CDE.               
           MOVE IM048-UIR-NEW-UNT-RTL-AMT TO PV-PREV-UNT-RTL-AMT.               
                                                                                
           PERFORM 4000-READ-RECORD.                                            
                                                                                
      ******************************************************************        
      * 2050-DETERMINE-NEW-SKU-STATUS.                                 *        
      *  ==> PROCESSES:                                                *        
      *    - DETERMINES IF THE SKU STATUS IS CHANGING                  *        
      ******************************************************************        
       2050-DETERMINE-NEW-SKU-STATUS.                                           
           MOVE '2050-DETERMINE-NEW-SKU-STATUS' TO PV-PARAGRAPH-NAME.           
                                                                                
           IF IM048-PRCHGTYP-CLR-TO-ACTIVE                                      
               IF IM048-UIR-STR-NBR = 0                                         
                   SET PV-NEW-SKU-STATUS-ACTIVE TO TRUE                         
               ELSE                                                             
                   DISPLAY '** ABEND **'                                        
                   DISPLAY 'IMKUP039 - ABEND'                                   
                   DISPLAY 'CLR TO ACTIVE REASON AND NON-ZERO STORE'            
                   DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME                     
                   DISPLAY 'INPUT SKU = ' IM048-UIR-SKU                         
                   DISPLAY 'STORE NBR = ' IM048-UIR-STR-NBR                     
                   DISPLAY 'EFFECTIVE DATE = ' IM048-UIR-EFF-DTE                
                   PERFORM 9990-DISPLAY-CONTROL-INFO                            
                   MOVE +4001 TO ABEND-CODE                                     
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF IM048-PRCHGTYP-CLEARNCE                                           
               IF IM048-UIR-STR-NBR = 0                                         
                   SET PV-NEW-SKU-STATUS-CLEARANCE TO TRUE                      
               ELSE                                                             
                   DISPLAY '** ABEND **'                                        
                   DISPLAY 'IMKUP039 - ABEND'                                   
                   DISPLAY 'CLEARANCE REASON AND NON-ZERO STORE'                
                   DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME                     
                   DISPLAY 'INPUT SKU = ' IM048-UIR-SKU                         
                   DISPLAY 'STORE NBR = ' IM048-UIR-STR-NBR                     
                   DISPLAY 'EFFECTIVE DATE = ' IM048-UIR-EFF-DTE                
                   PERFORM 9990-DISPLAY-CONTROL-INFO                            
                   MOVE +4002 TO ABEND-CODE                                     
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF IM048-PRCHGTYP-REG-CLEARNCE                                       
             AND IM048-UIR-ALL-RMNG-STRS                                        
               IF IM048-UIR-STR-NBR = 0                                         
                   SET PV-NEW-SKU-STATUS-CLEARANCE TO TRUE                      
                   IF IM048-UIR-STAT-CDE NOT = PC-MIXED                         
                       IF IM048-UIR-NOT-ALL-SAME-PRICE                          
                         AND PV-SAVE-REGN-PRIC-IND = 'N'                        
                           SET PS-NEW-CLR-RCP TO TRUE                           
                       ELSE                                                     
                           IF IM048-UIR-ALL-SAME-PRICE                          
                             AND IM048-UIR-STAT-CDE NOT = PC-CLEARANCE          
                               SET PS-STRS-ALL-SAME-PRICE TO TRUE               
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
               ELSE                                                             
                   DISPLAY '** ABEND **'                                        
                   DISPLAY 'IMKUP039 - ABEND'                                   
                   DISPLAY 'ALL REMAINING CLEARANCE AND NO ZERO STORE'          
                   DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME                     
                   DISPLAY 'INPUT SKU = ' IM048-UIR-SKU                         
                   DISPLAY 'STORE NBR = ' IM048-UIR-STR-NBR                     
                   DISPLAY 'EFFECTIVE DATE = ' IM048-UIR-EFF-DTE                
                   PERFORM 9990-DISPLAY-CONTROL-INFO                            
                   MOVE +4003 TO ABEND-CODE                                     
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF IM048-PRCHGTYP-REG-CLEARNCE                                       
             AND NOT IM048-UIR-ALL-RMNG-STRS                                    
               IF IM048-UIR-STR-NBR > 0                                         
                   IF NOT (IM048-UIR-STAT-CDE = PC-MIXED                        
                           OR PC-CLEARANCE)                                     
                       SET PV-NEW-SKU-STATUS-MIXED TO TRUE                      
                   END-IF                                                       
               ELSE                                                             
                   DISPLAY '** ABEND **'                                        
                   DISPLAY 'IMKUP039 - ABEND'                                   
                   DISPLAY 'NOT ALL REMAINING CLEARANCE AND ZERO STORE'         
                   DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME                     
                   DISPLAY 'INPUT SKU = ' IM048-UIR-SKU                         
                   DISPLAY 'STORE NBR = ' IM048-UIR-STR-NBR                     
                   DISPLAY 'EFFECTIVE DATE = ' IM048-UIR-EFF-DTE                
                   PERFORM 9990-DISPLAY-CONTROL-INFO                            
                   MOVE +4004 TO ABEND-CODE                                     
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2100-SKU-PROCESSING.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORM SKU AND TUPCPLS STORE ZERO ROW UPDATES            *        
      ******************************************************************        
       2100-SKU-PROCESSING.                                                     
           MOVE '2100-SKU-PROCESSING' TO PV-PARAGRAPH-NAME.                     
                                                                                
           IF IM048-UIR-STR-NBR = 0                                             
      *        PROCESS A PRICE CHANGE AND POSSIBLE STATUS CHANGE                
               MOVE IM048-UIR-STR-NBR TO UPCPLS-STR-NBR                         
               PERFORM 5100-SELECT-CURRENT-TUPCPLS                              
               SET PS-HAVE-UPCPLS-COST-INFO TO TRUE                             
               SET PS-HAVE-STORE0-TUPCPLS TO TRUE                               
               MOVE UPCPLS-UNT-RTL-AMT   TO PV-OLD-UNT-RTL-AMT                  
               MOVE UPCPLS-MEITGP-NBR    TO PV-OLD-MEITGP-NBR                   
               PERFORM 2700-PRICE-UPDATE-CHECK                                  
               PERFORM 2750-STATUS-UPDATE-CHECK                                 
               IF PS-RETAIL-CHANGED-YES                                         
                 OR PS-STATUS-CHANGED                                           
                   IF PS-NEW-CLR-RCP                                            
                       MOVE 'Y'       TO UPC-REGN-PRIC-IND                      
                   END-IF                                                       
                   IF PV-NEW-SKU-STATUS-ACTIVE                                  
                     OR (PS-STRS-ALL-SAME-PRICE                                 
                         AND (IM048-UIR-STAT-CDE = PC-MIXED                     
                              OR PC-CLEARANCE))                                 
      *                CLEARANCE TO ACTIVE PRICE CHANGE OR A MK2 OR FNL         
      *                MARKDOWN PRICE CHANGE WITH ALL STORES SAME PRICE.        
      *                DELETE ANY FUTURE ROWS ON TUPCPLS                        
                       PERFORM 5300-DEL-ALL-FUTURE                              
                       IF PV-SAVE-REGN-PRIC-IND = 'Y'                           
                           MOVE 'N' TO UPC-REGN-PRIC-IND                        
                       END-IF                                                   
                   END-IF                                                       
                   PERFORM 6200-INACTIVATE-TUPCPLS                              
                   PERFORM 6250-INSERT-TUPCPLS                                  
                   PERFORM 2900-BUILD-IMT-EVNT-MSG-75                           
                   IF (PV-NEW-SKU-STATUS-ACTIVE                                 
                       OR (PS-STRS-ALL-SAME-PRICE                               
                           AND (IM048-UIR-STAT-CDE = PC-MIXED                   
                                OR PC-CLEARANCE)))                              
                     AND PV-SAVE-REGN-PRIC-IND = 'Y'                            
      *                DB COMPRESS OF A CLEARANCE TO ACTIVE PRICE CHANGE        
      *                OR A FINAL MARKDOWN PRICE CHANGE WITH ALL STORES         
      *                THE SAME PRICE.                                          
                       PERFORM 6300-COMPRESS-TUPCPLS                            
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
      *        PROCESS A STATUS CHANGE TO 25 FOR THE SKU FOR THE RCP            
      *        FIRST TIER (PV-NEW-SKU-STATUS-MIXED IS SET)                      
               MOVE ZERO              TO UPCPLS-STR-NBR                         
               PERFORM 5100-SELECT-CURRENT-TUPCPLS                              
               SET PS-HAVE-UPCPLS-COST-INFO                                     
                                      TO TRUE                                   
               SET PS-HAVE-STORE0-TUPCPLS                                       
                                      TO TRUE                                   
               MOVE 'Y'               TO UPC-REGN-PRIC-IND                      
               PERFORM 2750-STATUS-UPDATE-CHECK                                 
               IF PS-STATUS-CHANGED                                             
                   PERFORM 6200-INACTIVATE-TUPCPLS                              
                   PERFORM 6250-INSERT-TUPCPLS                                  
                   PERFORM 2900-BUILD-IMT-EVNT-MSG-75                           
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2200-SKU-STORE-PROCESSING.                                     *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORM TUPCPLS STORE SPECIFIC UPDATES                    *        
      *    - CREATE SKU STORE RETAIL CHANGE ITEM MESSAGES WITH UP TO   *        
      *      40 STORES WITH THE SAME UNIT RETAIL AMOUNT PER MESSAGE    *        
      ******************************************************************        
       2200-SKU-STORE-PROCESSING.                                               
           MOVE '2200-SKU-STORE-PROCESSING' TO PV-PARAGRAPH-NAME.               
                                                                                
           MOVE IM048-UIR-STR-NBR TO UPCPLS-STR-NBR.                            
           PERFORM 5100-SELECT-CURRENT-TUPCPLS.                                 
                                                                                
      *    NO ACTIVE STORE SPECIFIC ROW ON TUPCPLS INDICATES THAT THIS          
      *    STORE WAS NOT PROCESSED IN A PREVIOUS RCP TIER.                      
           IF PS-CURRENT-ROW-NOT-FOUND                                          
               IF IM048-UIR-ALL-RMNG-STRS                                       
      *            WHEN PROCESSING A FINAL TIER STORE, DO NOT INSERT A          
      *            NEW ROW SINCE THE STORE ZERO PRICE WILL BE USED FOR          
      *            THIS STORE. A NEW STORE SPECIFIC ROW SHOULD STILL BE         
      *            INSERTED ON TPPCPRC (IN IMKUP047)                            
                   CONTINUE                                                     
               ELSE                                                             
                   IF IM048-PRCHGTYP-2ND-CLR-MRKDWN                             
                     OR IM048-PRCHGTYP-FINL-CLR-MRKDWN                          
                       IF PV-STORE0-UPC-STAT-CDE = PC-CLEARANCE                 
      *                    PROCESS A MK2 OR FNL MARKDOWN IF THE SKU IS          
      *                    CLEARANCE STATUS AND NO TUPCPLS ROW FOUND            
                           PERFORM 2300-INSERT-STR-SPECIFIC                     
                       ELSE                                                     
      *                    BYPASS A MK2 OR FNL MARKDOWN IF THE SKU IS           
      *                    NOT CLEARANCE STATUS                                 
                           DISPLAY '*** BYPASSING FINAL OR MK2 - NOT '          
                                   'CLEARANCE STATUS'                           
                           DISPLAY 'SKU NUMBER: ' IM048-UIR-SKU               06
                                   ' STORE: ' IM048-UIR-STR-NBR               06
                           ADD +1 TO CR-SKU-STRS-BYPASSED-CNTR                  
                       END-IF                                                   
                   ELSE                                                         
                       PERFORM 2300-INSERT-STR-SPECIFIC                         
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
      *        AN ACTIVE STORE SPECIFIC ROW ON TUPCPLS INDICATES THAT           
      *        THIS STORE WAS PROCESSED IN A PREVIOUS RCP TIER AND              
      *        NEEDS TO BE UPDATED WITH THE NEW PRICE. THE RETAIL CHANGE        
      *        DATE AND STATUS CHANGE DATE WILL BOTH BE UPDATED TO              
      *        INDICATE THAT THIS IS A YELLOW TO YELLOW TICKET CHANGE.          
               SET PS-HAVE-UPCPLS-COST-INFO TO TRUE                             
               IF IM048-UIR-NEW-UNT-RTL-AMT NOT = UPCPLS-UNT-RTL-AMT            
                   MOVE IM048-UIR-NEW-UNT-RTL-AMT                               
                                            TO UPCPLS-UNT-RTL-AMT               
                   MOVE IM048-UIR-NEW-GP-PRIC-ID                                
                                            TO UPCPLS-MEITGP-NBR                
                   MOVE IM048-UIR-EFF-DTE   TO UPCPLS-UNT-RTL-CHG-DTE           
                   MOVE PC-CLEARANCE        TO UPCPLS-UPC-STAT-CDE              
                   MOVE IM048-UIR-EFF-DTE   TO UPCPLS-UPC-STAT-CHG-DTE          
                   PERFORM 6200-INACTIVATE-TUPCPLS                              
                   PERFORM 6250-INSERT-TUPCPLS                                  
                   IF SQLCODE = ZERO                                            
                       PERFORM 2910-BUILD-IMT-EVNT-MSG-80                       
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * 2300-INSERT-STR-SPECIFIC.                                      *        
      ******************************************************************        
       2300-INSERT-STR-SPECIFIC.                                                
           MOVE '2300-INSERT-STR-SPECIFIC' TO PV-PARAGRAPH-NAME.                
           IF PS-NOT-HAVE-STORE0-TUPCPLS                                        
               MOVE ZERO            TO UPCPLS-STR-NBR                           
               PERFORM 5100-SELECT-CURRENT-TUPCPLS                              
               SET PS-HAVE-UPCPLS-COST-INFO                                     
                                    TO TRUE                                     
               SET PS-HAVE-STORE0-TUPCPLS                                       
                                    TO TRUE                                     
               MOVE IM048-UIR-STR-NBR TO UPCPLS-STR-NBR                         
           END-IF.                                                              
           MOVE IM048-UIR-NEW-UNT-RTL-AMT                                       
                                    TO UPCPLS-UNT-RTL-AMT.                      
           MOVE IM048-UIR-NEW-GP-PRIC-ID                                        
                                    TO UPCPLS-MEITGP-NBR.                       
           MOVE IM048-UIR-EFF-DTE   TO UPCPLS-UNT-RTL-CHG-DTE.                  
           MOVE PC-CLEARANCE        TO UPCPLS-UPC-STAT-CDE.                     
           MOVE IM048-UIR-EFF-DTE   TO UPCPLS-UPC-STAT-CHG-DTE.                 
           PERFORM 6250-INSERT-TUPCPLS.                                         
           IF SQLCODE = ZERO                                                    
               PERFORM 2910-BUILD-IMT-EVNT-MSG-80                               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2700-PRICE-UPDATE-CHECK.                                       *        
      *  ==> PROCESSES:                                                *        
      *    - CHECK FOR RETAIL PRICE UPDATES.                           *        
      ******************************************************************        
       2700-PRICE-UPDATE-CHECK.                                                 
           MOVE '2700-PRICE-UPDATE-CHECK' TO PV-PARAGRAPH-NAME.                 
                                                                                
           MOVE IM048-UIR-NEW-UNT-RTL-AMT                                       
                                   TO UPCPLS-UNT-RTL-AMT.                       
                                                                                
           IF PV-OLD-UNT-RTL-AMT NOT =                                          
                 IM048-UIR-NEW-UNT-RTL-AMT                                      
               SET PS-RETAIL-CHANGED-YES                                        
                                   TO TRUE                                      
           END-IF.                                                              
                                                                                
           MOVE IM048-UIR-NEW-GP-PRIC-ID TO UPCPLS-MEITGP-NBR.                  
           IF PV-OLD-MEITGP-NBR NOT = IM048-UIR-NEW-GP-PRIC-ID                  
               SET PS-RETAIL-CHANGED-YES TO TRUE                                
           END-IF.                                                              
                                                                                
           IF PS-RETAIL-CHANGED-YES                                             
               MOVE IM048-UIR-EFF-DTE    TO UPCPLS-UNT-RTL-CHG-DTE              
               IF PV-NEW-SKU-STATUS-CLEARANCE                                   
                 OR (PV-NEW-SKU-STATUS = SPACES                                 
                     AND UPCPLS-UPC-STAT-CDE = PC-CLEARANCE)                    
      *            FOR CLEARANCE PRICE SKU, THE RETAIL CHANGE DATE AND          
      *            STATUS CHANGE DATE WILL BOTH BE UPDATED TO INDICATE          
      *            THIS IS A YELLOW TO YELLOW TICKET CHANGE.                    
                   MOVE IM048-UIR-EFF-DTE TO UPCPLS-UPC-STAT-CHG-DTE            
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2750-STATUS-UPDATE-CHECK.                                      *        
      *  ==> PROCESSES:                                                *        
      *    - CHECK FOR SKU STATUS UPDATES.                             *        
      ******************************************************************        
       2750-STATUS-UPDATE-CHECK.                                                
                                                                                
           MOVE '2750-STATUS-UPDATE-CHECK' TO PV-PARAGRAPH-NAME.                
                                                                                
           IF PV-NEW-SKU-STATUS NOT = SPACES                                    
      *        SKU IS CLEARANCE, OR CLEARANCE TO ACTIVE, OR RCP MIXED           
               MOVE PV-NEW-SKU-STATUS  TO UPCPLS-UPC-STAT-CDE                   
                                          PV-STORE0-UPC-STAT-CDE                
               IF PV-NEW-SKU-STATUS NOT =                                       
                     IM048-UIR-STAT-CDE                                         
                   SET PS-STATUS-CHANGED TO TRUE                                
                   MOVE IM048-UIR-EFF-DTE TO UPCPLS-UPC-STAT-CHG-DTE            
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2900-BUILD-IMT-EVNT-MSG-75.                                    *        
      *  ==> PROCESSES:                                                *        
      *    - BUILD THE SKU RETAIL STATUS RECORD                        *        
      ******************************************************************        
       2900-BUILD-IMT-EVNT-MSG-75.                                              
           MOVE '2900-BUILD-IMT-EVNT-MSG-75' TO PV-PARAGRAPH-NAME.              
                                                                                
      ***  CHECK IF THE PREVIOUS MESSAGE WAS NOT A TYPE 75 (WAS TYPE 80)        
           IF NOT IM008-BUS-EVENT-RTL-STATUS-CHG                                
               IF IM008-STR-RTL-STORE-COUNT > ZERO                              
      ***          CREATE A STARTED TYPE 80 MESSAGE                             
                   ADD 1 TO CR-TYPE-80-MSGS-CREATED                             
                   PERFORM 4500-PUT-ON-XSITEM-CC-QUEUE                          
                   PERFORM 6800-INSERT-IMT-EVNT-MSG                             
               END-IF                                                           
               SET IM008-BUS-EVENT-RTL-STATUS-CHG TO TRUE                       
               MOVE IM008-BUSINESS-EVENT-ID TO IMT00001-BUS-EVNT-TYP-CDE        
               MOVE PC-PROGRAM-NAME TO IM008-RTL-CHG-CRE-BY-USR-ID              
               MOVE LOW-VALUES TO IM008-RTL-CHG-FILLER                          
      ***      DETERMINE LENGTH OF MESSAGE FOR SKU RETAIL/STATUS CHANGE         
      ***      ENTIRE LENGTH OF RECORD MINUS THE RTL/STAT CHG FILLER            
               COMPUTE IMT00001-MSG-TXT-LEN =                                   
                   LENGTH OF IMRD008-RECORD -                                   
                   LENGTH OF IM008-RTL-CHG-FILLER                               
           END-IF.                                                              
                                                                                
      ***  CHECK IF STARTING A NEW SKU                                          
           IF PS-IMT-EVNT-MSG-NOT-STARTED                                       
               SET PS-IMT-EVNT-MSG-STARTED                                      
                                       TO TRUE                                  
               PERFORM 2950-BUILD-MESSAGE-KEY                                   
               MOVE MDARVS-RMS-STYL-ID TO IMT00001-RMS-STYL-ID                  
           END-IF.                                                              
                                                                                
      ***  MOVE RETAIL AND MEITGP NUMBERS AND SET CHANGE INDICATORS             
      ***  ONLY FOR STORE 0 RECS                                                
           IF IM048-UIR-STR-NBR = 0                                             
               MOVE IM048-UIR-NEW-UNT-RTL-AMT                                   
                                       TO IM008-RTL-CHG-UNT-RTL-AMT-N           
               IF PV-OLD-UNT-RTL-AMT = IM048-UIR-NEW-UNT-RTL-AMT                
                   SET IM008-RTL-CHG-UNT-RTL-AMT-NOC TO TRUE                    
               ELSE                                                             
                   SET IM008-RTL-CHG-UNT-RTL-AMT-CHG TO TRUE                    
               END-IF                                                           
               MOVE PV-OLD-MEITGP-NBR    TO IM008-RTL-CHG-MEITGP-NBR            
               MOVE IM048-UIR-NEW-GP-PRIC-ID                                    
                                         TO IM008-RTL-CHG-MEITGP-NBR-N          
               IF PV-OLD-MEITGP-NBR = IM048-UIR-NEW-GP-PRIC-ID                  
                   SET IM008-RTL-CHG-MEITGP-NBR-NOC TO TRUE                     
               ELSE                                                             
                   SET IM008-RTL-CHG-MEITGP-NBR-CHG TO TRUE                     
               END-IF                                                           
               MOVE IM048-UIR-NEW-GP-RTL-QTY                                    
                                         TO IM008-RTL-CHG-GP-QTY-N              
               MOVE IM048-UIR-NEW-GP-RTL-AMT                                    
                                         TO IM008-RTL-CHG-GP-RTL-AMT-N          
           ELSE                                                                 
               MOVE UPCPLS-UNT-RTL-AMT TO IM008-RTL-CHG-UNT-RTL-AMT-N           
               SET IM008-RTL-CHG-UNT-RTL-AMT-NOC TO TRUE                        
               MOVE UPCPLS-MEITGP-NBR    TO IM008-RTL-CHG-MEITGP-NBR            
                                            IM008-RTL-CHG-MEITGP-NBR-N          
               SET IM008-RTL-CHG-MEITGP-NBR-NOC TO TRUE                         
               IF UPCPLS-MEITGP-NBR = 0                                         
                   MOVE ZEROS               TO MITGPL-MITGPL-QTY                
                                               MITGPL-GP-AMT                    
               ELSE                                                             
                   MOVE UPCPLS-MEITGP-NBR   TO MITGPL-MEITGP-NBR                
                   PERFORM 5800-SELECT-TMITGPL                                  
               END-IF                                                           
               MOVE MITGPL-MITGPL-QTY    TO IM008-RTL-CHG-GP-QTY-N              
               MOVE MITGPL-GP-AMT        TO IM008-RTL-CHG-GP-RTL-AMT-N          
           END-IF.                                                              
           MOVE UPCPLS-UNT-RTL-CHG-DTE TO IM008-RTL-CHG-UNT-RTL-CHG-DTE.        
                                                                                
                                                                                
      ***  FILL NEW UPC STATUS CODE AND DESCRIPTION                             
           MOVE IM048-UIR-STAT-CDE  TO IM008-RTL-CHG-UPC-STAT-CDE.              
           MOVE UPCPLS-UPC-STAT-CDE TO IM008-RTL-CHG-UPC-STAT-CDE-N             
                                       UPCSTA-UPC-STAT-CDE.                     
           MOVE UPCPLS-UPC-STAT-CHG-DTE                                         
                                      TO IM008-RTL-CHG-UPC-STAT-CHG-DTE.        
           PERFORM 5700-SELECT-TUPCSTA.                                         
           MOVE UPCSTA-UPC-STAT-DESC  TO IM008-RTL-CHG-SKU-STAT-DESC-N.         
           IF IM048-UIR-STAT-CDE = UPCPLS-UPC-STAT-CDE                          
               SET IM008-RTL-CHG-SKU-STAT-DSC-NOC TO TRUE                       
           ELSE                                                                 
               SET IM008-RTL-CHG-SKU-STAT-DSC-CHG TO TRUE                       
           END-IF.                                                              
                                                                                
           MOVE UPC-REGN-PRIC-IND     TO IM008-RTL-CHG-RGN-PRIC-IND-SUP.        
           MOVE UPCPLS-UNT-COST-AMT   TO IM008-RTL-CHG-UNT-COST-AMT-SUP.        
           MOVE PV-UPCPLS-EFF-BEG-TMST TO IM008-RTL-CHG-EFF-BEG-TMST.           
           MOVE UPCPLS-MKDN-CDE       TO IM008-RTL-CHG-MKDN-CDE.                
                                                                                
           ADD 1 TO CR-TYPE-75-MSGS-CREATED.                                    
           PERFORM 4500-PUT-ON-XSITEM-CC-QUEUE.                                 
           PERFORM 6800-INSERT-IMT-EVNT-MSG.                                    
                                                                                
      ******************************************************************        
      * 2910-BUILD-IMT-EVNT-MSG-80                                     *        
      *  ==> PROCESSES:                                                *        
      *    - BUILD THE SKU STORE RETAIL STATUS RECORD (80)             *        
      ******************************************************************        
       2910-BUILD-IMT-EVNT-MSG-80.                                              
           MOVE '2910-BUILD-IMT-EVNT-MSG-80' TO PV-PARAGRAPH-NAME.              
                                                                                
      ***  CHECK IF THE PREVIOUS MESSAGE WAS NOT A TYPE 80 (A TYPE 75)          
           IF NOT IM008-BUS-EVENT-STR-RTL-ST-CHG                                
               SET IM008-BUS-EVENT-STR-RTL-ST-CHG TO TRUE                       
               MOVE IM008-BUSINESS-EVENT-ID TO IMT00001-BUS-EVNT-TYP-CDE        
               INITIALIZE IM008-SKU-STR-RTL-ST-CHG-DATA                         
               SET IM008-STR-RTL-UNT-RTL-AMT-CHG TO TRUE                        
               MOVE IM048-UIR-STAT-CDE TO IM008-STR-RTL-UPC-STAT-CDE            
               MOVE PC-CLEARANCE TO IM008-STR-RTL-UPC-STAT-CDE-N                
                                    UPCSTA-UPC-STAT-CDE                         
               PERFORM 5700-SELECT-TUPCSTA                                      
               MOVE UPCSTA-UPC-STAT-DESC TO                                     
                                    IM008-STR-RTL-SKU-STAT-DESC-N               
               SET IM008-STR-RTL-SKU-STAT-DSC-CHG TO TRUE                       
               MOVE 'Y' TO IM008-STR-RTL-PERM-RUN-IND                           
               MOVE ZERO       TO IM008-STR-RTL-STORE-COUNT                     
               MOVE LOW-VALUES TO IM008-STR-RTL-STORE-TABLE                     
               MOVE LOW-VALUES TO IM008-STR-RTL-FILLER                          
      ***      DETERMINE LENGTH OF MESSAGE FOR SKU/STR RTL/STATUS CHANGE        
      ***      ENTIRE LENGTH OF RECORD MINUS THE SKU/STR CHG FILLER             
               COMPUTE IMT00001-MSG-TXT-LEN =                                   
                   LENGTH OF IMRD008-RECORD -                                   
                   LENGTH OF IM008-STR-RTL-FILLER                               
      ***      CHECK IF STARTING A TYPE 80 AFTER A TYPE 75 FOR A SKU            
               IF PS-IMT-EVNT-MSG-STARTED                                       
                   MOVE IM048-UIR-NEW-UNT-RTL-AMT                               
                                       TO IM008-STR-RTL-UNT-RTL-AMT-N           
                   MOVE UPCPLS-UNT-RTL-CHG-DTE                                  
                                       TO IM008-STR-RTL-UNT-RTL-CHG-DTE         
                   MOVE UPCPLS-UPC-STAT-CHG-DTE                                 
                                       TO IM008-STR-RTL-UPC-STAT-CHG-DTE        
                   MOVE UPCPLS-UNT-COST-AMT                                     
                                       TO IM008-STR-RTL-UNT-COST-AMT-SUP        
                   MOVE PV-UPCPLS-EFF-BEG-TMST                                  
                                       TO IM008-STR-RTL-EFF-BEG-TMST            
                   MOVE UPCPLS-MKDN-CDE                                         
                                       TO IM008-STR-RTL-MKDN-CDE                
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ***  CHECK IF STARTING A NEW SKU                                          
           IF PS-IMT-EVNT-MSG-NOT-STARTED                                       
               SET PS-IMT-EVNT-MSG-STARTED                                      
                                       TO TRUE                                  
               PERFORM 2950-BUILD-MESSAGE-KEY                                   
               MOVE MDARVS-RMS-STYL-ID TO IMT00001-RMS-STYL-ID                  
               MOVE IM048-UIR-NEW-UNT-RTL-AMT                                   
                                       TO IM008-STR-RTL-UNT-RTL-AMT-N           
               MOVE UPCPLS-UNT-RTL-CHG-DTE                                      
                                       TO IM008-STR-RTL-UNT-RTL-CHG-DTE         
               MOVE UPCPLS-UPC-STAT-CHG-DTE                                     
                                       TO IM008-STR-RTL-UPC-STAT-CHG-DTE        
               MOVE UPCPLS-UNT-COST-AMT                                         
                                       TO IM008-STR-RTL-UNT-COST-AMT-SUP        
               MOVE PV-UPCPLS-EFF-BEG-TMST                                      
                                       TO IM008-STR-RTL-EFF-BEG-TMST            
               MOVE UPCPLS-MKDN-CDE    TO IM008-STR-RTL-MKDN-CDE                
           ELSE                                                                 
               IF IM008-STR-RTL-STORE-COUNT = PC-MAX-STORES                     
                   ADD 1 TO CR-TYPE-80-MSGS-CREATED                             
                   PERFORM 4500-PUT-ON-XSITEM-CC-QUEUE                          
                   PERFORM 6800-INSERT-IMT-EVNT-MSG                             
                   MOVE ZERO             TO IM008-STR-RTL-STORE-COUNT           
                   MOVE LOW-VALUES       TO IM008-STR-RTL-STORE-TABLE           
               END-IF                                                           
      ***      CHECK IF THE UNIT RETAIL AMOUNT OR THE STATUS CHANGE             
      ***      DATE IS DIFFERENT FROM THE PREVIOUS STORE FOR THE SAME           
      ***      SKU.  IF SO, WRITE THE RECORD AND START A NEW BUSINESS           
      ***      EVENT 080 RECORD.                                                
               IF (IM048-UIR-NEW-UNT-RTL-AMT NOT =                              
                         IM008-STR-RTL-UNT-RTL-AMT-N)                           
                 OR UPCPLS-MKDN-CDE NOT = IM008-STR-RTL-MKDN-CDE                
                 OR (UPCPLS-UPC-STAT-CHG-DTE NOT =                              
                           IM008-STR-RTL-UPC-STAT-CHG-DTE)                      
                   IF IM008-STR-RTL-STORE-COUNT > ZERO                          
                       ADD 1 TO CR-TYPE-80-MSGS-CREATED                         
                       PERFORM 4500-PUT-ON-XSITEM-CC-QUEUE                      
                       PERFORM 6800-INSERT-IMT-EVNT-MSG                         
                       MOVE ZERO           TO IM008-STR-RTL-STORE-COUNT         
                       MOVE LOW-VALUES     TO IM008-STR-RTL-STORE-TABLE         
                   END-IF                                                       
                   MOVE IM048-UIR-NEW-UNT-RTL-AMT                               
                                       TO IM008-STR-RTL-UNT-RTL-AMT-N           
                   MOVE UPCPLS-MKDN-CDE TO IM008-STR-RTL-MKDN-CDE               
                   MOVE UPCPLS-UPC-STAT-CHG-DTE                                 
                                       TO IM008-STR-RTL-UPC-STAT-CHG-DTE        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           ADD 1                     TO IM008-STR-RTL-STORE-COUNT.              
           MOVE IM048-UIR-STR-NBR    TO IM008-STR-RTL-STORE                     
                                        (IM008-STR-RTL-STORE-COUNT).            
                                                                                
      ******************************************************************        
      * 2950-BUILD-IMT-EVNT-MSG.                                       *        
      *  ==> PROCESSES:                                                *        
      *    - BUILD THE BUSINESS EVENT KEY DATA FOR IM008               *        
      ******************************************************************        
       2950-BUILD-MESSAGE-KEY.                                                  
           MOVE '2950-BUILD-MESSAGE-KEY' TO PV-PARAGRAPH-NAME.                  
                                                                                
                                                                                
           MOVE PV-CURRENT-TIMESTAMP TO IM008-CRE-TMST.                         
           INITIALIZE IM008-ITEM-KEY-DATA.                                      
           MOVE UPC-VS-ID            TO IM008-ITEM-KEY-VS-ID.                   
           MOVE VNDSTL-ENT-ID        TO IM008-ITEM-KEY-ENT-ID.                  
           MOVE EXTENT-DUN-NBR       TO IM008-ITEM-KEY-DUN-NBR.                 
           MOVE VNDSTL-VS-NBR        TO IM008-ITEM-KEY-VS-NBR.                  
           MOVE UPC-SUB-CL-MDSEAR-ID TO IM008-ITEM-KEY-SUBCL-MDSE-ID.           
           MOVE MDARVS-DEPT-NBR      TO IM008-ITEM-KEY-DEPT.                    
           MOVE MDARVS-MAJ-CL-NBR    TO IM008-ITEM-KEY-CLASS.                   
           MOVE MDARVS-SUB-CL-NBR    TO IM008-ITEM-KEY-SUBCL.                   
           MOVE MDARVS-RMS-STYL-ID   TO IM008-ITEM-KEY-RMS-STYL-ID.             
           MOVE IM048-UIR-SKU        TO IM008-ITEM-KEY-SKU.                     
           MOVE UPC-UPC-ID           TO PV-UPC-COMP-10.                         
           MOVE PV-UPC-COMP-10       TO IM008-ITEM-KEY-INTR-UPC-ID              
                                        IM008-ITEM-KEY-UPC-ID.                  
           MOVE UPC-UPC-NBR          TO IM008-ITEM-KEY-UPC-NBR.                 
           MOVE PC-ORIGIN-CNTRY      TO IM008-ITEM-KEY-ORIGIN-CNTRY.            
                                                                                
      ******************************************************************        
      * 3000-EOJ-PROCESSING.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - CHECK IF LAST SKU PROCESSED SHOULD BE COMPRESSED          *        
      *    - DELETE STYLE RESERVATION FOR LAST SKU PROCESSED           *        
      *    - CLOSE FILES                                               *        
      *    - DISPLAYS PROCESSING TOTALS                                *        
      *    - TAKES A FINAL COMMIT AND UPDATES THE CHECKPOINT STATUS    *        
      *      TO INDICATE SUCESSFULL COMPLETION                         *        
      ******************************************************************        
       3000-EOJ-PROCESSING.                                                     
           MOVE '3000-EOJ-PROCESSING' TO PV-PARAGRAPH-NAME.                     
                                                                                
      *    DETERMINE IF A TYPE 80 IMT_EVNT_MSG_BRG REC NOT YET INSERTED         
           IF PS-IMT-EVNT-MSG-STARTED                                           
             AND IM008-BUS-EVENT-STR-RTL-ST-CHG                                 
             AND IM008-STR-RTL-STORE-COUNT > ZERO                               
               ADD 1 TO CR-TYPE-80-MSGS-CREATED                                 
               PERFORM 4500-PUT-ON-XSITEM-CC-QUEUE                              
               PERFORM 6800-INSERT-IMT-EVNT-MSG                                 
           END-IF.                                                              
                                                                                
           CLOSE PRICE-UPDATE-FILE                                              
                 CONTROL-CARD-FILE                                              
                 QM-CONTROL-CARD-FILE.                                          
                                                                                
           IF PS-NO-DOCUMENTS                                                   
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 7800-COMMIT                                              
               MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE                            
               PERFORM 7900-UPDATE-CHECKPOINT-STATUS                            
           END-IF.                                                              
                                                                                
      *    CREATE A 996 PERM CC SKU LOC END OF DAY MESSAGE                      
           INITIALIZE IMRD008-RECORD.                                           
           SET IM008-CC-SKU-LOC-END-OF-DAY TO TRUE.                             
           MOVE PV-CURRENT-TIMESTAMP TO IM008-CRE-TMST.                         
           MOVE LOW-VALUES TO IM008-ITEM-ACTION-DATA.                           
           COMPUTE IMT00001-MSG-TXT-LEN =                                       
               LENGTH OF IMRD008-RECORD -                                       
               LENGTH OF IM008-ITEM-ACTION-DATA.                                
           PERFORM 4500-PUT-ON-XSITEM-CC-QUEUE.                                 
                                                                                
           PERFORM 8700-COMMIT-MQ.                                              
           PERFORM 8500-CLOSE-XSITEM-CC-QUEUE.                                  
           PERFORM 8600-DISCONNECT-QMGR.                                        
                                                                                
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
      ******************************************************************        
      * 4000-READ-RECORD.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - READS THE NEXT RECORD IN THE INPUT FILE.                  *        
      ******************************************************************        
       4000-READ-RECORD.                                                        
           MOVE '4000-READ-RECORD' TO PV-PARAGRAPH-NAME.                        
                                                                                
           READ PRICE-UPDATE-FILE                                               
               INTO IM048-UPC-ID-PRICE-UPDATE-REC                               
                   AT END                                                       
                       MOVE 'Y'     TO PS-END-OF-FILE-SW                        
                   NOT AT END                                                   
                       ADD +1                  TO PV-RECS-READ-CNTR             
                       MOVE IM048-UIR-SKU      TO PV-DK-SKU                     
                       MOVE IM048-UIR-STR-NBR  TO PV-DK-STORE-NBR               
                       MOVE IM048-UIR-EFF-DTE  TO PV-HOLD-UIR-EFF-DATE          
                       ADD 1 TO DI-INPUT-RECS-DISPLAY                           
                       IF DI-INPUT-RECS-DISPLAY = 100000                        
                           DISPLAY '***100,000 INPUT RECS READ***'              
                           DISPLAY 'CURR TMST = ' PV-CURRENT-TIMESTAMP          
                           MOVE ZERO TO DI-INPUT-RECS-DISPLAY                   
                       END-IF                                                   
           END-READ.                                                            
                                                                                
      ******************************************************************07160094
      * READ CONTROL FILE FOR JOB NAME, XS ITEM QUEUE NAME, AND        *07170094
      * PROCESS OLD DATE INDICATOR.                                    *07170094
      ******************************************************************07180094
       4100-READ-CONTROL-CARD-1.                                        07140094
           MOVE '4100-READ-CONTROL-CARD-1'  TO PV-PARAGRAPH-NAME.       07150094
                                                                        07210094
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                          
               AT END                                                           
                   DISPLAY '*** NO CONTROL CARD 1 ***'                          
                   SET PV-ABEND-4001 TO TRUE                                    
                   PERFORM 9999-ABEND.                                          
                                                                                
           DISPLAY CC-CONTROL-CARD.                                             
           IF CC-PROCESS-OLD-DATE = 'N'                                         
               DISPLAY ' CC: DO NOT PROCESS OLD DATE DATA'                      
           ELSE                                                                 
               IF CC-PROCESS-OLD-DATE = 'Y'                                     
                   DISPLAY ' CC: PROCESS OLD DATE DATA'                         
               ELSE                                                             
                   DISPLAY '*** INCORRECT CONTROL CARD DATA ***'                
                   SET PV-ABEND-4002 TO TRUE                                    
                   PERFORM 9999-ABEND                                           
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF CC-JOB-NAME = SPACES                                              
               DISPLAY '*** INVALID CONTROL CARD JOB NAME ***'                  
               SET PV-ABEND-4004 TO TRUE                                        
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
           IF CC-XSITEM-CC-QUEUE-NAME = SPACES                                  
               DISPLAY '*** INVALID CONTROL CARD XS ITEM QUEUE NAME ***'        
               SET PV-ABEND-4005 TO TRUE                                        
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * READ QUEUE MANAGER CONTROL CARD FILE                                    
      ******************************************************************        
       4200-READ-CONTROL-CARD-2.                                                
           MOVE '4200-READ-CONTROL-CARD-2'  TO PV-PARAGRAPH-NAME.               
                                                                                
           READ QM-CONTROL-CARD-FILE INTO QM-CONTROL-CARD                       
               AT END                                                           
                   DISPLAY '*** NO QUEUE MANAGER CONTROL CARD ***'              
                   SET PV-ABEND-4006 TO TRUE                                    
                   PERFORM 9999-ABEND.                                          
                                                                                
           DISPLAY QM-CONTROL-CARD.                                             
           IF QM-QUEUE-MANAGER-NAME = SPACES                                    
               DISPLAY '*** INVALID CONTROL CARD QUEUE MANAGER NAME ***'        
               SET PV-ABEND-4007 TO TRUE                                        
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
      ******************************************************************07320094
      * 4500-PUT-ON-XSITEM-CC-QUEUE.                                            
      *     PUTS THE MESSAGE ON THE XS ITEM CONT CLEARANCE QUEUE.               
      ******************************************************************07320094
       4500-PUT-ON-XSITEM-CC-QUEUE.                                             
                                                                                
           MOVE CC-XSITEM-CC-QUEUE-NAME   TO MQOD-OBJECTNAME.                   
           MOVE QM-QUEUE-MANAGER-NAME     TO MQOD-OBJECTQMGRNAME.               
                                                                                
           MOVE MQMI-NONE                 TO MQMD-MSGID.                        
           MOVE MQCI-NONE                 TO MQMD-CORRELID.                     
           MOVE MQFMT-STRING              TO MQMD-FORMAT.                       
           MOVE MQPER-PERSISTENT          TO MQMD-PERSISTENCE.                  
           MOVE +5                        TO MQMD-PRIORITY.                     
           COMPUTE MQPMO-OPTIONS  =  MQPMO-SYNCPOINT +                          
                                     MQPMO-SET-ALL-CONTEXT.                     
                                                                                
           CALL MQV-MQPUT USING MQV-HCONN                                       
                                MQV-PERM-HANDLE                                 
                                MQM-MESSAGE-DESCRIPTOR                          
                                MQM-PUT-MESSAGE-OPTIONS                         
                                PC-OUT-MSGLENGTH                                
                                IMRD008-RECORD                                  
                                MQV-COMPLETION-CODE                             
                                MQV-REASON.                                     
                                                                                
           EVALUATE TRUE                                                        
              WHEN MQV-COMPLETION-CODE = MQCC-OK                                
                   ADD +1 TO CR-MSGS-TO-XSITEM-QUEUE                            
              WHEN OTHER                                                        
                   DISPLAY 'UNABLE TO WRITE TO XSITEM CC QUEUE'                 
                   CALL MQV-MQCHECK USING MQV-REASON                            
                                          MQV-REASON-TEXT                       
                   MOVE '4500-PUT-ON-XSITEM-CC-QUEUE'                           
                                               TO PV-PARAGRAPH-NAME             
                   MOVE 'MQBACK ERROR'         TO PV-OPERATION-MSG-1            
                   MOVE MQV-COMPLETION-CODE    TO PV-COMPLETION-CODE            
                   MOVE MQV-REASON             TO PV-REASON                     
                   MOVE MQV-REASON-TEXT        TO PV-REASON-TEXT                
                   PERFORM 8600-DISCONNECT-QMGR                                 
                   PERFORM 9400-MQ-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *5000-SELECT-STYL-SKU-INFO.                                      *        
      *  ==> PROCESSES:                                                *        
      *    - GET STYLE AND SKU INFO                                    *        
      ******************************************************************        
       5000-SELECT-STYL-SKU-INFO.                                               
           MOVE '5000-SELECT-STYL-SKU-INFO' TO PV-PARAGRAPH-NAME.               
                                                                                
           MOVE IM048-UIR-INTR-UPC-ID TO UPC-UPC-ID.                            
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR  SQLCODE = +100                                               
               OR  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                      
               EXEC SQL                                                         
                  SELECT  B.DEPT_NBR                                            
                        , B.MAJ_CL_NBR                                          
                        , B.SUB_CL_NBR                                          
                        , B.RMS_STYL_ID                                         
                        , A.UPC_NBR                                             
                        , A.SUB_CL_MDSEAR_ID                                    
                        , A.VS_ID                                               
                        , A.REGN_PRIC_IND                                       
                        , C.VS_NBR                                              
                        , C.ENT_ID                                              
                        , D.DUN_NBR                                             
                    INTO :MDARVS-DEPT-NBR                                       
                        ,:MDARVS-MAJ-CL-NBR                                     
                        ,:MDARVS-SUB-CL-NBR                                     
                        ,:MDARVS-RMS-STYL-ID                                    
                        ,:UPC-UPC-NBR                                           
                        ,:UPC-SUB-CL-MDSEAR-ID                                  
                        ,:UPC-VS-ID                                             
                        ,:UPC-REGN-PRIC-IND                                     
                        ,:VNDSTL-VS-NBR                                         
                        ,:VNDSTL-ENT-ID                                         
                        ,:EXTENT-DUN-NBR                                        
                   FROM  TUPC A                                                 
                        ,TMDARVS B                                              
                        ,TVNDSTL C                                              
                        ,TEXTENT D                                              
                   WHERE A.UPC_ID           = :UPC-UPC-ID                       
                     AND A.EFF_END_DTE IS NULL                                  
                     AND A.VS_ID            = B.VS_ID                           
                     AND A.SUB_CL_MDSEAR_ID = B.SUB_CL_MDSEAR_ID                
                     AND B.ACTV_IND         = 'Y'                               
                     AND A.VS_ID            = C.VS_ID                           
                     AND C.EFF_END_DTE IS NULL                                  
                     AND C.ENT_ID           = D.ENT_ID                          
                   WITH UR                                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       SET PS-SKU-INFO-FOUND TO TRUE                            
                       ADD +1      TO CR-SKUS-READ-CNTR                         
                   WHEN SQLCODE = +100                                          
                       SET PS-SKU-INFO-NOT-FOUND TO TRUE                        
                       ADD +1      TO CR-SKUS-NOT-FOUND-CNTR                    
                      DISPLAY 'SKU INFO MISSING FOR SKU: ' IM048-UIR-SKU        
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'TUPC,TMDARVS,TVNDSTL,TEXTENT'                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'TUPC,TMDARVS,TVNDSTL,TEXTENT'                              
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 5100-SELECT-CURRENT-TUPCPLS.                                   *        
      *  ==> PROCESSES:                                                *        
      *    - SELECT THE CURRENT ROW FROM THE TUPCPLS TABLE.            *        
      ******************************************************************        
       5100-SELECT-CURRENT-TUPCPLS.                                             
           MOVE '5100-SELECT-CURRENT-TUPCPLS' TO PV-PARAGRAPH-NAME.             
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
              OR   SQLCODE = +100                                               
              OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                        
               EXEC SQL                                                         
                   SELECT EFF_BEG_TMST                                          
                        , UPC_STAT_CDE                                          
                        , UNT_COST_AMT                                          
                        , UNT_RTL_AMT                                           
                        , MEITGP_NBR                                            
                        , UPC_STAT_CHG_DTE                                      
                        , UNT_RTL_CHG_DTE                                       
                        , ORIG_CLR_DTE                                          
                        , MKDN_CDE                                              
                    INTO :UPCPLS-EFF-BEG-TMST                                   
                        ,:UPCPLS-UPC-STAT-CDE                                   
                        ,:UPCPLS-UNT-COST-AMT :PV-UNT-COST-NULL-IND             
                        ,:UPCPLS-UNT-RTL-AMT                                    
                        ,:UPCPLS-MEITGP-NBR                                     
                        ,:UPCPLS-UPC-STAT-CHG-DTE                               
                        ,:UPCPLS-UNT-RTL-CHG-DTE                                
                        ,:UPCPLS-ORIG-CLR-DTE :PV-ORIG-CLR-NULL-IND             
                        ,:UPCPLS-MKDN-CDE :PV-MKDN-CDE-NULL-IND                 
                     FROM TUPCPLS                                               
                    WHERE UPC_ID       = :UPCPLS-UPC-ID                         
                      AND STR_NBR      = :UPCPLS-STR-NBR                        
                      AND EFF_BEG_TMST < :PV-UPCPLS-EFF-BEG-TMST                
                      AND (EFF_END_TMST IS NULL OR                              
                           EFF_END_TMST >= :PV-UPCPLS-EFF-BEG-TMST)             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       SET PS-CURRENT-ROW-FOUND       TO TRUE                   
                       IF PV-ORIG-CLR-NULL-IND < 0                              
                           MOVE SPACES TO UPCPLS-ORIG-CLR-DTE                   
                       END-IF                                                   
                       IF UPCPLS-STR-NBR = 0                                    
                           MOVE UPCPLS-ORIG-CLR-DTE                             
                                 TO PV-STORE0-ORIG-CLR-DTE                      
                       END-IF                                                   
      *                                                                         
      *  A 'NOT FOUND' IS VALID FOR A STORE SPECIFIC ROW, BUT SHOULD            
      *  NEVER HAPPEN FOR A '000' STORE ROW.                                    
      *                                                                         
                   WHEN SQLCODE = +100                                          
                       IF UPCPLS-STR-NBR  = ZEROES                              
                           MOVE 'READ TUPCPLS TABLE '                           
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                           MOVE IM048-UIR-INTR-UPC-ID TO PV-UPC-COMP-10         
                           MOVE PV-UPC-COMP-10        TO PV-DISPLAY-10          
                           DISPLAY '** ----> INVALID TUPCPLS UPC ID = '         
                                    PV-DISPLAY-10                               
                                    ' STORE = ' UPCPLS-STR-NBR                  
                           PERFORM 9999-SQL-ABEND                               
                       ELSE                                                     
                           SET PS-CURRENT-ROW-NOT-FOUND TO TRUE                 
                       END-IF                                                   
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'READ TUPCPLS TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'READ TUPCPLS TABLE'                                        
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 5300-DEL-ALL-FUTURE.                                                    
      *  ==> PROCESSES:                                                *        
      *    - DELETE ANY FUTURE TUPCPLS ROWS                            *        
      ******************************************************************        
       5300-DEL-ALL-FUTURE.                                                     
           MOVE '5300-DELETE-ALL-FUTURE' TO PV-PARAGRAPH-NAME.                  
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR  SQLCODE = +100                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
                 EXEC SQL                                                       
                    DELETE FROM TUPCPLS                                         
                     WHERE UPC_ID       = :UPCPLS-UPC-ID                        
                       AND EFF_BEG_TMST > CURRENT TIMESTAMP                     
                 END-EXEC                                                       
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD SQLERRD(3) TO CR-TUPCPLS-ROW-DEL-CNTR                
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'DELETE FROM TUPCPLS TABLE'                         
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'DELETE FROM TUPCPLS TABLE'                                 
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 5700-SELECT-TUPCSTA.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - READS TUPCSTA                                             *        
      ******************************************************************        
       5700-SELECT-TUPCSTA.                                                     
           MOVE '5700-SELECT-TUPCSTA' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                      
               EXEC SQL                                                         
                   SELECT UPC_STAT_DESC                                         
                    INTO :UPCSTA-UPC-STAT-DESC                                  
                    FROM  TUPCSTA                                               
                   WHERE  UPC_STAT_CDE = :UPCSTA-UPC-STAT-CDE                   
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                       MOVE 'READ TUPCSTA TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'READ TUPCSTA TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'READ TUPCSTA TABLE'                                        
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 5800-SELECT-TMITGPL.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - READS TMITGPL                                             *        
      ******************************************************************        
       5800-SELECT-TMITGPL.                                                     
           MOVE '5800-SELECT-TMITGPL' TO PV-PARAGRAPH-NAME.                     
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                      
               EXEC SQL                                                         
                   SELECT  MITGPL_QTY                                           
                          ,GP_AMT                                               
                     INTO :MITGPL-MITGPL-QTY                                    
                         ,:MITGPL-GP-AMT                                        
                     FROM  TMITGPL                                              
                    WHERE  OPERCO_NBR = '03'                                    
                      AND  MEITGP_NBR = :MITGPL-MEITGP-NBR                      
                      AND  ADLOC_ID   = '   '                                   
                    WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                       MOVE 'READ TMITGPL TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'READ TMITGPL TABLE'                                
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'READ TMITGPL TABLE'                                        
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
PK0608* 6150-DELETE-DUPL-TUPCPLS.                                      *        
PK0608*  ==> PROCESSES:                                                *        
PK0608*    - DELETE A DUPLICATE FUTURE TUPCPLS ROW.                    *        
PK0608******************************************************************        
PK0608*6150-DELETE-DUPL-TUPCPLS.                                                
PK0608*    MOVE '6150-DELETE-DUPL-TUPCPLS' TO PV-PARAGRAPH-NAME.                
PK0608*                                                                         
PK0608*    INITIALIZE PV-RETRY-COUNT.                                           
PK0608*    PERFORM  WITH TEST AFTER                                             
PK0608*      UNTIL SQLCODE = ZERO                                               
PK0608*        OR  SQLCODE = +100                                               
PK0608*        OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
PK0608*          EXEC SQL                                                       
PK0608*             DELETE FROM TUPCPLS                                         
PK0608*              WHERE UPC_ID       = :UPCPLS-UPC-ID                        
PK0608*                AND STR_NBR      = :UPCPLS-STR-NBR                       
PK0608*                AND EFF_BEG_TMST = :PV-UPCPLS-EFF-BEG-TMST               
PK0608*          END-EXEC                                                       
PK0608*                                                                         
PK0608*        EVALUATE TRUE                                                    
PK0608*            WHEN SQLCODE = ZERO                                          
PK0608*                ADD SQLERRD(3) TO CR-TUPCPLS-ROW-DEL-CNTR                
PK0608*            WHEN SQLCODE = +100                                          
PK0608*                CONTINUE                                                 
PK0608*            WHEN SQLCODE = -913                                          
PK0608*                ADD +1      TO PV-RETRY-COUNT                            
PK0608*            WHEN OTHER                                                   
PK0608*                MOVE 'DELETE FROM TUPCPLS TABLE'                         
PK0608*                            TO PV-DB2-OPERATION-ATTEMPTED                
PK0608*                PERFORM 9999-SQL-ABEND                                   
PK0608*        END-EVALUATE                                                     
PK0608*    END-PERFORM.                                                         
PK0608*                                                                         
PK0608*    IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
PK0608*        MOVE 'DELETE FROM TUPCPLS TABLE'                                 
PK0608*                            TO PV-DB2-OPERATION-ATTEMPTED                
PK0608*        PERFORM 9999-SQL-ABEND                                           
PK0608*    END-IF.                                                              
                                                                                
      ******************************************************************        
      * 6200-INACTIVATE-TUPCPLS.                                       *        
      *  ==> PROCESSES:                                                *        
      *    - INACTIVATE ALL ACTIVE ROWS FOR A UPC ID AND STORE ON      *        
      *      THE TUPCPLS TABLE.                                        *        
      ******************************************************************        
       6200-INACTIVATE-TUPCPLS.                                                 
           MOVE '6200-INACTIVATE-TUPCPLS' TO PV-PARAGRAPH-NAME.                 
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
                 EXEC SQL                                                       
                    UPDATE TUPCPLS                                              
                        SET EFF_END_TMST   = :PV-UPCPLS-EFF-END-TMST            
                        WHERE UPC_ID       = :UPCPLS-UPC-ID                     
                          AND STR_NBR      = :UPCPLS-STR-NBR                    
                          AND EFF_BEG_TMST = :UPCPLS-EFF-BEG-TMST               
                 END-EXEC                                                       
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD SQLERRD(3) TO CR-TUPCPLS-ROW-UPD-CNTR                
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'UPDATE TUPCPLS TABLE'                              
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'UPDATE TUPCPLS TABLE'                                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 6250-INSERT-TUPCPLS.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - INSERT A NEW STORE ROW FOR A UPC ID ON THE TUPCPLS TABLE. *        
      ******************************************************************        
       6250-INSERT-TUPCPLS.                                                     
           MOVE '6250-INSERT-TUPCPLS' TO PV-PARAGRAPH-NAME.                     
           IF IM048-PRCHGTYP-CLR-TO-ACTIVE                                      
               MOVE SPACES TO UPCPLS-ORIG-CLR-DTE                               
               MOVE SPACE  TO UPCPLS-MKDN-CDE                                   
           END-IF.                                                              
           IF UPCPLS-STR-NBR = 0                                                
             AND PV-NEW-SKU-STATUS-MIXED                                        
               MOVE SPACES TO UPCPLS-ORIG-CLR-DTE                               
               MOVE SPACE  TO UPCPLS-MKDN-CDE                                   
           ELSE                                                                 
               IF IM048-PRCHGTYP-CLEARNCE                                       
                 OR IM048-PRCHGTYP-REG-CLEARNCE                                 
                 OR IM048-PRCHGTYP-2ND-CLR-MRKDWN                               
                 OR IM048-PRCHGTYP-FINL-CLR-MRKDWN                              
                   IF PS-CURRENT-ROW-NOT-FOUND                                  
      *                NO ACTIVE STORE SPECIFIC ROW ON TUPCPLS                  
      *                SO USE STORE ZERO ORIGINAL CLEARANCE DATE                
                       MOVE PV-STORE0-ORIG-CLR-DTE                              
                             TO UPCPLS-ORIG-CLR-DTE                             
                   END-IF                                                       
                   IF UPCPLS-ORIG-CLR-DTE = SPACES                              
                       MOVE UPCPLS-UPC-STAT-CHG-DTE                             
                             TO UPCPLS-ORIG-CLR-DTE                             
                   END-IF                                                       
                   IF IM048-PRCHGTYP-CLEARNCE                                   
                     OR IM048-PRCHGTYP-REG-CLEARNCE                             
                       MOVE '1' TO UPCPLS-MKDN-CDE                              
                   ELSE                                                         
                       IF IM048-PRCHGTYP-2ND-CLR-MRKDWN                         
                           MOVE '2' TO UPCPLS-MKDN-CDE                          
                       ELSE                                                     
                           IF IM048-PRCHGTYP-FINL-CLR-MRKDWN                    
                               MOVE 'F' TO UPCPLS-MKDN-CDE                      
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
           IF UPCPLS-ORIG-CLR-DTE = SPACES                                      
               MOVE -1 TO PV-ORIG-CLR-NULL-IND                                  
           ELSE                                                                 
               MOVE 0  TO PV-ORIG-CLR-NULL-IND                                  
           END-IF.                                                              
           IF UPCPLS-MKDN-CDE = SPACE                                           
               MOVE -1 TO PV-MKDN-CDE-NULL-IND                                  
           ELSE                                                                 
               MOVE 0  TO PV-MKDN-CDE-NULL-IND                                  
           END-IF.                                                              
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
PK0608         OR SQLCODE = -803                                                
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                   INSERT INTO TUPCPLS                                          
                           (UPC_ID                                              
                           ,OPERCO_NBR                                          
                           ,STR_NBR                                             
                           ,EFF_BEG_TMST                                        
                           ,EFF_END_TMST                                        
                           ,UPC_STAT_CDE                                        
                           ,UNT_COST_AMT                                        
                           ,UNT_RTL_AMT                                         
                           ,MEITGP_NBR                                          
                           ,CRE_BY_USR_ID                                       
                           ,CRE_TMST                                            
                           ,ITM_NBR                                             
                           ,PLU_IND                                             
                           ,UPC_STAT_CHG_DTE                                    
                           ,UNT_RTL_CHG_DTE                                     
                           ,ORIG_CLR_DTE                                        
                           ,MKDN_CDE)                                           
                   VALUES (:UPCPLS-UPC-ID                                       
                          ,'03'                                                 
                          ,:UPCPLS-STR-NBR                                      
                          ,:PV-UPCPLS-EFF-BEG-TMST                              
                          ,NULL                                                 
                          ,:UPCPLS-UPC-STAT-CDE                                 
                          ,:UPCPLS-UNT-COST-AMT :PV-UNT-COST-NULL-IND           
                          ,:UPCPLS-UNT-RTL-AMT                                  
                          ,:UPCPLS-MEITGP-NBR                                   
                          ,:PC-PROGRAM-NAME                                     
                          ,CURRENT TIMESTAMP                                    
                          ,:UPCPLS-ITM-NBR                                      
                          ,'N'                                                  
                          ,:UPCPLS-UPC-STAT-CHG-DTE                             
                          ,:UPCPLS-UNT-RTL-CHG-DTE                              
                          ,:UPCPLS-ORIG-CLR-DTE :PV-ORIG-CLR-NULL-IND           
                          ,:UPCPLS-MKDN-CDE :PV-MKDN-CDE-NULL-IND)              
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD SQLERRD(3) TO CR-TUPCPLS-ROW-INS-CNTR                
                   WHEN SQLCODE = -803                                          
PK0608***              PERFORM 6150-DELETE-DUPL-TUPCPLS                         
PK0608***              MOVE -803 TO SQLCODE                                     
PK0608*                BYPASS AN RCP, MK2 OR FNL MARKDOWN IF THE SKU            
PK0608*                STORE IS A DUPLICATE (2 PRICE CHANGE ID'S)               
PK0608                 DISPLAY '*** BYPASSING 2ND PRICE CHANGE FOR SAME'        
PK0608                         ' EFFECTIVE DATE'                                
PK0608                 DISPLAY 'SKU NUMBER: ' IM048-UIR-SKU             0672    
PK0608                         ' STORE: ' IM048-UIR-STR-NBR             0672    
PK0608                         ' DROPPED PRCHG TYPE: '                  0672    
PK0608                                  IM048-UIR-PRCHG-TYP-CDE                 
PK0608                         ' DROPPED PRICE: '                       0672    
PK0608                                  IM048-UIR-NEW-UNT-RTL-AMT               
PK0608                 ADD +1 TO CR-SKU-STRS-BYPASSED-CNTR                      
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'INSERT TUPCPLS TABLE'                              
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'INSERT TUPCPLS TABLE'                                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 6300-COMPRESS-TUPCPLS                                          *        
      *  ==> PROCESSES:                                                *        
      *    - INACTIVATE ALL ACTIVE STORE SPECIFIC ROWS FOR AN ITEM NBR *        
      *      ON TUPCPLS FOR A CLEARANCE TO ACTIVE PRICE CHANGE OR FOR  *        
      *      A FINAL MARKDOWN PRICE CHANGE WITH ALL STORES THE SAME    *        
      *      PRICE.                                                    *        
      ******************************************************************        
       6300-COMPRESS-TUPCPLS.                                                   
           MOVE '6300-COMPRESS-TUPCPLS' TO PV-PARAGRAPH-NAME.                   
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR  SQLCODE = +100                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                  UPDATE TUPCPLS                                                
                      SET EFF_END_TMST  = :PV-UPCPLS-EFF-END-TMST               
                      WHERE UPC_ID      = :UPCPLS-UPC-ID                        
                        AND STR_NBR    ^= SMALLINT(0)                           
                        AND (EFF_END_TMST >= :PV-UPCPLS-EFF-BEG-TMST            
                         OR  EFF_END_TMST IS NULL)                              
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD SQLERRD(3) TO CR-TUPCPLS-ROW-INACTV-CNTR             
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'UPDATE TUPCPLS TABLE'                              
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'UPDATE TUPCPLS TABLE'                                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 6800-INSERT-IMT-EVNT-MSG.                                      *        
      *  ==> PROCESSES:                                                *        
      *    - INSERT A BUSINESS EVENT RECORD TO THE ITEM EVENT MESSAGE  *        
      *      BRIDGE DB2 TABLE.                                         *        
      ******************************************************************        
       6800-INSERT-IMT-EVNT-MSG.                                                
           MOVE '6800-INSERT-IMT-EVNT-MSG' TO PV-PARAGRAPH-NAME.                
                                                                                
           MOVE IMRD008-RECORD TO IMT00001-MSG-TXT-TEXT.                        
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                   INSERT INTO IMT_EVNT_MSG_BRG                                 
                         ( CRE_TMST                                             
                          ,RMS_STYL_ID                                          
                          ,BUS_EVNT_TYP_CDE                                     
                          ,MSG_TXT )                                            
                   VALUES                                                       
                         ( CURRENT TIMESTAMP                                    
                         ,:IMT00001-RMS-STYL-ID                                 
                         ,:IMT00001-BUS-EVNT-TYP-CDE                            
                         ,:IMT00001-MSG-TXT)                                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD SQLERRD(3)  TO CR-IMT-EVNT-MSG-ROW-INS-CNTR          
                   WHEN SQLCODE = -913                                          
                       ADD +1          TO PV-RETRY-COUNT                        
                   WHEN OTHER                                                   
                       MOVE 'INSERT IMT_EVNT_MSG_BRG'                           
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'IMT_EVNT_MSG_BRG' TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7000-COMMIT-PROCESSING.                                        *        
      *  ==> PROCESSES:                                                *        
      *    - CHECK IF WE NEED TO TAKE A COMMIT.                        *        
      *    - COMMIT THIS UNIT OF WORK.                                 *        
      ******************************************************************        
       7000-COMMIT-PROCESSING.                                                  
                                                                                
           PERFORM 7200-SET-CURRENT-TIMESTAMP.                                  
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                        
               IF PS-FIRST-COMMIT                                               
                   MOVE '9'        TO TCKRSTCNTL-CKPT-STAT-CODE                 
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                        
                   MOVE 'N'        TO PS-FIRST-COMMIT-SW                        
               END-IF                                                           
               PERFORM 7800-COMMIT                                              
               PERFORM 7100-GET-COMMIT-TIMESTAMP                                
               PERFORM 8700-COMMIT-MQ                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7100-GET-COMMIT-TIMESTAMP.                                     *        
      *  ==> PROCESSES:                                                *        
      *    - GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT CONTROL      *        
      *      TABLE.                                                    *        
      ******************************************************************        
       7100-GET-COMMIT-TIMESTAMP.                                               
           MOVE '7100-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME.               
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                   SELECT                                                       
                     (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)              
                   INTO                                                         
                     :PV-COMMIT-TIMESTAMP                                       
                   FROM TCKRSTCNTL                                              
                  WHERE JOB_NAME  = :CC-JOB-NAME                                
                    AND PLAN_NAME = :PC-PROGRAM-NAME                            
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'SELECT TCKRSTCNTL'                                 
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'SELECT TCKRSTCNTL'                                         
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7200-SET-CURRENT-TIMESTAMP.                                    *        
      *  ==> PROCESSES:                                                *        
      *    - GET THE CURRENT TIMESTAMP FROM DB2.                       *        
      ******************************************************************        
       7200-SET-CURRENT-TIMESTAMP.                                              
           MOVE '7200-SET-CURRENT-TIMESTAMP' TO PV-PARAGRAPH-NAME.              
                                                                                
           EXEC SQL                                                             
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   MOVE PV-CURRENT-TIMESTAMP (1:10) TO                          
                          PV-RUN-DATE                                           
               WHEN OTHER                                                       
                   MOVE 'ERROR ON SET OF TIMESTAMP '                            
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 7400-INIT-CHECKPOINT-SELECT.                                   *        
      *  ==> PROCESSES:                                                *        
      *    - SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE TO SEE  *        
      *      IF WE ARE IN A RESTART CONDITION.                         *        
      ******************************************************************        
       7400-INIT-CHECKPOINT-SELECT.                                             
           MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME.             
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                 SELECT                                                         
                      CKPT_STAT_CODE                                            
                   INTO                                                         
                     :TCKRSTCNTL-CKPT-STAT-CODE                                 
                   FROM  TCKRSTCNTL                                             
                  WHERE  JOB_NAME  = :CC-JOB-NAME                               
                    AND  PLAN_NAME = :PC-PROGRAM-NAME                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'SELECT TCKRSTCNTL'                                 
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'SELECT TCKRSTCNTL'                                         
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7500-SELECT-RESTART-INFO.                                      *        
      *  ==> PROCESSES:                                                *        
      *    - SELECTS FROM THE CHECKPOINT RESTART INFORMATION TABLE TO  *        
      *      OBTAIN THE PROGRAMS SAVED VARIABLES.                      *        
      ******************************************************************        
       7500-SELECT-RESTART-INFO.                                                
           MOVE '7500-SELECT-RESTART-INFO' TO PV-PARAGRAPH-NAME.                
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                   SELECT                                                       
                      WORK_STRG_DESC                                            
                   INTO                                                         
                     :TCKRSTINF-WORK-STRG-DESC                                  
                   FROM  TCKRSTINF                                              
                  WHERE  JOB_NAME  = :CC-JOB-NAME                               
                    AND  PLAN_NAME = :PC-PROGRAM-NAME                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'SELECT TCKRSTINF'                                  
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'SELECT TCKRSTINF' TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7600-CALC-END-TMST.                                            *        
      *    - CALC END TMST FOR TUPCPLS PROCESSING                      *        
      ******************************************************************        
       7600-CALC-END-TMST.                                                      
           MOVE '7600-CALC-END-TMST' TO PV-PARAGRAPH-NAME.                      
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                   SELECT                                                       
                     TIMESTAMP(:PV-UPCPLS-EFF-BEG-TMST) - 1 MICROSECOND         
                   INTO                                                         
                     :PV-UPCPLS-EFF-END-TMST                                    
                   FROM  SYSIBM.SYSDUMMY1                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'SELECT SYSDUMMY1'                                  
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'SELECT SYSDUMMY1' TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7800-COMMIT.                                                   *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.           *        
      *    - TAKE A COMMIT.                                            *        
      ******************************************************************        
       7800-COMMIT.                                                             
           MOVE '7800-COMMIT'      TO PV-PARAGRAPH-NAME.                        
                                                                                
           MOVE PV-DETAIL-KEY      TO CR-DETAIL-KEY.                            
           MOVE PV-RECS-READ-CNTR  TO CR-RECS-READ-CNTR.                        
           MOVE CR-CHECKPOINT-RESTART-AREA                                      
                                   TO TCKRSTINF-WORK-STRG-DESC.                 
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                 UPDATE TCKRSTINF                                               
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC              
                  WHERE JOB_NAME       = :CC-JOB-NAME                           
                    AND PLAN_NAME      = :PC-PROGRAM-NAME                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'UPDATE TCKRSTINF'                                  
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'UPDATE TCKRSTINF' TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               ADD +1              TO CR-COMMIT-CNTR                            
           ELSE                                                                 
               MOVE 'ERROR ON COMMIT'                                           
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7900-UPDATE-CHECKPOINT-STATUS.                                 *        
      *  ==> PROCESSES:                                                *        
      *    - UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE        *        
      ******************************************************************        
       7900-UPDATE-CHECKPOINT-STATUS.                                           
           MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME.           
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM  WITH TEST AFTER                                             
             UNTIL SQLCODE = ZERO                                               
               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                       
               EXEC SQL                                                         
                 UPDATE  TCKRSTCNTL                                             
                    SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE            
                  WHERE  JOB_NAME  = :CC-JOB-NAME                               
                    AND  PLAN_NAME = :PC-PROGRAM-NAME                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -913                                          
                       ADD +1      TO PV-RETRY-COUNT                            
                   WHEN OTHER                                                   
                       MOVE 'UPDATE TCKRSTCNTL'                                 
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE 'UPDATE TCKRSTCNTL'                                         
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 8500-CLOSE-XSITEM-CC-QUEUE.                                             
      *     CLOSE XS ITEM CONTINUED CLEARANCE QUEUE.                            
      ******************************************************************        
       8500-CLOSE-XSITEM-CC-QUEUE.                                              
                                                                                
           CALL MQV-MQCLOSE USING MQV-HCONN                                     
                                  MQV-PERM-HANDLE                               
                                  MQCO-NONE                                     
                                  MQV-COMPLETION-CODE                           
                                  MQV-REASON.                                   
                                                                                
           EVALUATE TRUE                                                        
              WHEN MQV-COMPLETION-CODE = MQCC-OK                                
                   CONTINUE                                                     
              WHEN MQV-COMPLETION-CODE NOT EQUAL MQCC-OK                        
                   CALL MQV-MQCHECK USING MQV-REASON                            
                                          MQV-REASON-TEXT                       
                   MOVE '8500-CLOSE-XSITEM-CC-QUEUE'                            
                                            TO PV-PARAGRAPH-NAME                
                   MOVE 'CANNOT CLOSE XSITEM CC QUEUE'                          
                                            TO PV-OPERATION-MSG-1               
                   MOVE MQV-COMPLETION-CODE TO PV-COMPLETION-CODE               
                   MOVE MQV-REASON          TO PV-REASON                        
                   MOVE MQV-REASON-TEXT     TO PV-REASON-TEXT                   
                   PERFORM 9400-MQ-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 8600-DISCONNECT-QMGR.                                                   
      *     DISCONNECT FROM THE QUEUE MANAGER.                                  
      ******************************************************************        
       8600-DISCONNECT-QMGR.                                                    
                                                                                
           CALL MQV-MQDISC USING MQV-HCONN                                      
                                 MQV-COMPLETION-CODE                            
                                 MQV-REASON.                                    
                                                                                
           EVALUATE TRUE                                                        
              WHEN MQV-COMPLETION-CODE = MQCC-OK                                
                   CONTINUE                                                     
              WHEN MQV-COMPLETION-CODE NOT EQUAL MQCC-OK                        
                   CALL MQV-MQCHECK USING MQV-REASON                            
                                          MQV-REASON-TEXT                       
                   MOVE '8600-DISCONNECT-QMGR' TO PV-PARAGRAPH-NAME             
                   MOVE 'MQDISC ERROR'         TO PV-OPERATION-MSG-1            
                   MOVE MQV-COMPLETION-CODE    TO PV-COMPLETION-CODE            
                   MOVE MQV-REASON             TO PV-REASON                     
                   MOVE MQV-REASON-TEXT        TO PV-REASON-TEXT                
                   PERFORM 9400-MQ-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  COMMIT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT       *        
      ******************************************************************        
       8700-COMMIT-MQ.                                                          
                                                                                
           CALL MQV-MQCMIT USING MQV-HCONN                                      
                                 MQV-COMPLETION-CODE                            
                                 MQV-REASON.                                    
                                                                                
           EVALUATE TRUE                                                        
              WHEN MQV-COMPLETION-CODE = MQCC-OK                                
                   CONTINUE                                                     
              WHEN MQV-COMPLETION-CODE NOT EQUAL MQCC-OK                        
                   CALL MQV-MQCHECK USING MQV-REASON                            
                                          MQV-REASON-TEXT                       
                   MOVE '8700-COMMIT-MQ'       TO PV-PARAGRAPH-NAME             
                   MOVE 'MQCMIT ERROR'         TO PV-OPERATION-MSG-1            
                   MOVE MQV-COMPLETION-CODE    TO PV-COMPLETION-CODE            
                   MOVE MQV-REASON             TO PV-REASON                     
                   MOVE MQV-REASON-TEXT        TO PV-REASON-TEXT                
                   PERFORM 8800-BACK-OUT-MQ                                     
                   PERFORM 9400-MQ-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * BACK OUT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT      *        
      ******************************************************************        
                                                                                
       8800-BACK-OUT-MQ.                                                        
                                                                                
           CALL MQV-MQBACK USING MQV-HCONN                                      
                                 MQV-COMPLETION-CODE                            
                                 MQV-REASON.                                    
                                                                                
           EVALUATE TRUE                                                        
              WHEN MQV-COMPLETION-CODE = MQCC-OK                                
                   CONTINUE                                                     
              WHEN MQV-COMPLETION-CODE NOT EQUAL MQCC-OK                        
                   CALL MQV-MQCHECK USING MQV-REASON                            
                                          MQV-REASON-TEXT                       
                   MOVE '8800-BACK-OUT-MQ'     TO PV-PARAGRAPH-NAME             
                   MOVE 'MQBACK ERROR'         TO PV-OPERATION-MSG-1            
                   MOVE MQV-COMPLETION-CODE    TO PV-COMPLETION-CODE            
                   MOVE MQV-REASON             TO PV-REASON                     
                   MOVE MQV-REASON-TEXT        TO PV-REASON-TEXT                
                   PERFORM 9400-MQ-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 9400-MQ-ABEND.                                                          
      *     MQ ABEND ROUTINE.                                                   
      ******************************************************************        
       9400-MQ-ABEND.                                                           
                                                                                
           SET PV-ABEND-4020 TO TRUE.                                           
           DISPLAY '**** ABEND *************************************'.          
           DISPLAY '** MQSERIES ERROR **'.                                      
           DISPLAY '** PROGRAM:    ' PC-PROGRAM-NAME.                           
           DISPLAY '** ABEND CODE: ' ABEND-CODE.                                
           DISPLAY '** PARAGRAPH:  ' PV-PARAGRAPH-NAME.                         
           DISPLAY '** MESSAGE:    ' PV-OPERATION-MSG-1.                        
           DISPLAY '** COMP CODE:  ' PV-COMPLETION-CODE.                        
           DISPLAY '** RSN CODE:   ' PV-REASON.                                 
           DISPLAY '** RSN TEXT:   ' PV-REASON-TEXT.                            
           DISPLAY '************************************************'.          
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * 9990-DISPLAY-CONTROL-INFO.                                     *        
      *  ==> PROCESSES:                                                *        
      *    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *        
      ******************************************************************        
       9990-DISPLAY-CONTROL-INFO.                                               
                                                                                
           MOVE CR-RECS-PROCESSED-CNTR                                          
                                   TO DI-RECS-PROCESSED-CNTR.                   
           MOVE CR-RECS-READ-CNTR  TO DI-RECS-READ-CNTR.                        
           MOVE CR-SKUS-READ-CNTR  TO DI-SKUS-READ-CNTR.                        
           MOVE CR-SKUS-NOT-FOUND-CNTR                                          
                                   TO DI-SKUS-NOT-FOUND-CNTR.                   
           MOVE CR-SKU-STRS-BYPASSED-CNTR                                       
                                   TO DI-SKU-STRS-BYPASSED-CNTR.                
           MOVE CR-TUPCPLS-ROW-UPD-CNTR                                         
                                   TO DI-TUPCPLS-ROW-UPDATE-CNTR.               
           MOVE CR-TUPCPLS-ROW-INS-CNTR                                         
                                   TO DI-TUPCPLS-ROW-INSERT-CNTR.               
           MOVE CR-TUPCPLS-ROW-INACTV-CNTR                                      
                                   TO DI-TUPCPLS-ROW-INACTV-CNTR.               
           MOVE CR-TUPCPLS-ROW-DEL-CNTR                                         
                                   TO DI-TUPCPLS-ROW-DELETE-CNTR.               
           MOVE CR-MSGS-TO-XSITEM-QUEUE                                         
                                   TO DI-MSGS-TO-XSITEM-QUEUE.                  
           MOVE CR-TYPE-75-MSGS-CREATED                                         
                                   TO DI-TYPE-75-MSGS-CREATED.                  
           MOVE CR-TYPE-80-MSGS-CREATED                                         
                                   TO DI-TYPE-80-MSGS-CREATED.                  
           MOVE CR-IMT-EVNT-MSG-ROW-INS-CNTR                                    
                                   TO DI-IMT-EVNT-ROW-INSERT-CNTR.              
           MOVE CR-COMMIT-CNTR     TO DI-COMMIT-CNTR.                           
                                                                                
           DISPLAY '********************************************'.              
           DISPLAY '**** RUN STATISTICS FOR PROGRAM IMKUP039 ***'.              
           DISPLAY '****'.                                                      
           DISPLAY 'INPUT RECS PROCESSED:  ' DI-RECS-PROCESSED-CNTR.            
           DISPLAY 'INPUT RECORDS READ:    ' DI-RECS-READ-CNTR.                 
           DISPLAY 'SKUS READ:             ' DI-SKUS-READ-CNTR.                 
           DISPLAY 'SKUS NOT FOUND:        ' DI-SKUS-NOT-FOUND-CNTR.            
           DISPLAY 'SKU/STORES BYPASSED:   ' DI-SKU-STRS-BYPASSED-CNTR.         
           DISPLAY 'TUPCPLS ROWS UPDATED:  ' DI-TUPCPLS-ROW-UPDATE-CNTR.        
           DISPLAY 'TUPCPLS ROWS INSERTED: ' DI-TUPCPLS-ROW-INSERT-CNTR.        
           DISPLAY 'TUPCPLS ROWS INACTIVD: ' DI-TUPCPLS-ROW-INACTV-CNTR.        
           DISPLAY 'TUPCPLS ROWS DELETED:  ' DI-TUPCPLS-ROW-DELETE-CNTR.        
           DISPLAY 'MSGS TO XSITEM QUEUE:  ' DI-MSGS-TO-XSITEM-QUEUE.           
           DISPLAY 'TYPE 75 MSGS CREATED:  ' DI-TYPE-75-MSGS-CREATED.           
           DISPLAY 'TYPE 80 MSGS CREATED:  ' DI-TYPE-80-MSGS-CREATED.           
           DISPLAY 'IMT EVNT ROWS INSERTD: '                                    
                                            DI-IMT-EVNT-ROW-INSERT-CNTR.        
           DISPLAY 'COMMITS TAKEN:         ' DI-COMMIT-CNTR.                    
           DISPLAY '****'.                                                      
           DISPLAY '**** RUN STATS END FOR PROGRAM IMKUP039  ***'.              
           DISPLAY '********************************************'.              
                                                                                
      ******************************************************************        
      * 9999-SQL-ABEND.                                                *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *        
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
                                                                                
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'IMKUP039 - ABEND'.                                          
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
           DISPLAY 'SQL CODE  = ' PV-SQLCODE-DISPLAY.                           
           DISPLAY 'INPUT SKU = ' IM048-UIR-SKU.                                
           DISPLAY 'STORE NBR = ' IM048-UIR-STR-NBR.                            
           DISPLAY 'EFFECTIVE DATE = ' IM048-UIR-EFF-DTE.                       
                                                                                
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       9999-ABEND.                                                      07730094
      ******************************************************************07740094
      * ABEND ROUTINE MISC ERRORS                                      *07750094
      ******************************************************************07760094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07770094
           DISPLAY '** PARAGRAPH **      = ' PV-PARAGRAPH-NAME.         07780094
           CALL 'ILBOABN0' USING ABEND-CODE.                            07800094
