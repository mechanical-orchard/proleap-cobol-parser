      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PMKUT082.                                        
       AUTHOR.                 COLLEEN CUMMINGS.                                
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           MAY 2003.                                        
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PMKUT082 - ELIMINATE S/D STAT CDE DUPLICATE 120'S FOR       *        
      *               DATA INTEGRITY PROCESS (PMKUT079).               *        
      *               INPUT FILE IS SORTED BY 'SORT-LAST-UPD-TMST'     *        
      *               TO COVER THE SITUATION OF A DUPLICATE D/C REC.   *        
      *    NOTE:  THIS IS A NON-DATABASE PROGRAM (NOT DB2 OR ORACLE).  *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      *    08/08/2003 - COLLEEN CUMMINGS    WR21770                    *        
      *     - REMOVED DISPLAYS - TOO MUCH SYSOUT.                      *        
      *       CAN ALWAYS LOOK AT SORTED FILE READ IN TO THIS PROGRAM   *        
      *       TO FIND OUT WHAT WAS SKIPPED.                            *        
      *                                                                *        
      *    08/26/2004 - COLLEEN CUMMINGS    P000686109                 *        
      *     - FIX TO REMOVE COMPARE OF PRICE VERSION IDS               *        
      *       (WHEN PULL PRICE VERSION IDS FOR SAME STORE GROUP AND    *        
      *        ONE OF THEM IS DELETED - IE: 1006T GRAND OPENING MAIN   *        
      *        INTFC ID 13817202).                                     *        
      *                                                                *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PRCG-STORE-GP-FILE                                            
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT PRCG-STORE-GP-FILE-OUT                                        
               ASSIGN                       TO OUT01.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PRCG-STORE-GP-FILE                                                   
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  PRCG-STORE-GP-LINE               PIC  X(275).                        
                                                                                
       FD  PRCG-STORE-GP-FILE-OUT                                               
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  PRCG-STORE-GP-LINE-OUT           PIC  X(235).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
          05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)     VALUE                 
                                                          'PMKUT082'.           
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-READ-FILE-SW           PIC  X(01)     VALUE 'N'.              
               88  PS-READ-FILE-END                     VALUE 'Y'.              
               88  PS-READ-FILE-START                   VALUE 'N'.              
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME            PIC  X(30)    VALUE SPACES.         
           05  PV-OPERATION-MSG             PIC  X(40)    VALUE SPACES.         
           05  PV-OPERATION-MSG-1           PIC  X(40)    VALUE SPACES.         
           05  PV-OPERATION-MSG-2           PIC  X(40)    VALUE SPACES.         
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-SKIPPED-CNT           PIC 9(11) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-RECS-WRITTEN              PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
                                                                                
       01  PV-PREV-PM710-RECORD.                                                
           05  PV-PREV-BUSINESS-EVNT-ID      PIC 9(03) VALUE ZEROS.             
           05  PV-PREV-ACTION-CDE            PIC X(01) VALUE SPACES.            
           05  PV-PREV-BUSINESS-EVNT-CRE-TMST PIC X(26) VALUE SPACES.           
           05  PV-PREV-PROMO-INTFC-ID        PIC 9(10) VALUE ZERO.              
           05  PV-PREV-MDSE-LNE-ID           PIC 9(10) VALUE ZERO.              
           05  PV-PREV-PROMO-PRIC-VER-ID     PIC 9(10) VALUE ZEROS.             
           05  PV-PREV-STR-SELN-TYP-CDE      PIC X(01) VALUE SPACES.            
           05  PV-PREV-STR-SELN-HIER-LVL-CDE PIC X(02) VALUE SPACES.            
           05  PV-PREV-ADHC-STR-GP-ID        PIC 9(10) VALUE ZEROS.             
           05  PV-PREV-STR-GP-TYP-CDE        PIC X(04) VALUE SPACES.            
           05  PV-PREV-STR-GP-ID             PIC 9(04) VALUE ZEROS.             
           05  PV-PREV-PRNT-OV-STR-SELN-CDE  PIC X(01) VALUE SPACES.            
           05  PV-PREV-PRNT-OV-STR-GP-CDE    PIC 9(04) VALUE ZEROS.             
           05  PV-PREV-PRNT-OV-STR-GP-ID     PIC 9(04) VALUE ZEROS.             
           05  PV-PREV-PRNT-ADHC-STR-GP-ID   PIC 9(10) VALUE ZEROS.             
           05  PV-PREV-STRT-TMST             PIC X(26) VALUE SPACES.            
           05  PV-PREV-END-TMST              PIC X(26) VALUE SPACES.            
           05  PV-PREV-ACT-STRT-TMST         PIC X(26) VALUE SPACES.            
           05  PV-PREV-ACT-END-TMST          PIC X(26) VALUE SPACES.            
           05  PV-PREV-STAT-CDE              PIC X(01) VALUE SPACES.            
           05  PV-PREV-LAST-UPD-TMST         PIC X(26) VALUE SPACES.            
           05  PV-PREV-SORT-LAST-UPD-TMST    PIC X(26) VALUE SPACES.            
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE PRICING STORE GROUP BUSINESS EVENT                  
      *    WITH THE SORT-LAST-UPD-TMST IN IT.                                   
      ******************************************************************        
           COPY PMRD710.                                                        
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE PRICING STORE GROUP BUSINESS EVENT.                 
      ******************************************************************        
           COPY PMRD709.                                                        
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE TOTAL RECORD                                        
      ******************************************************************        
           COPY PMRD700.                                                        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B150-READ-PRCG-STORE-GP-FILE.                                
           IF PS-READ-FILE-END                                                  
               MOVE 'NO STORE GP FILE TO PROCESS' TO PV-OPERATION-MSG-1         
               SET ABEND-4002-READ-ERROR   TO TRUE                              
               PERFORM Z910-ABEND                                               
           END-IF.                                                              
                                                                                
           PERFORM B200-PROCESS-AND-WRITE-FILE                                  
             UNTIL PS-READ-FILE-END.                                            
           DISPLAY 'RECORDS WRITTEN TO FILE = ' PA-RECS-WRITTEN.                
           DISPLAY 'RECORDS SKIPPED = ' PA-RECS-SKIPPED-CNT.                    
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES                                               *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE CONTROL CARD AND THE TWO OUTPUT FILES             *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  PRCG-STORE-GP-FILE                                       
                OUTPUT PRCG-STORE-GP-FILE-OUT.                                  
                                                                                
      *****************************************************************         
      * B150-READ-PRCG-STORE-GP-FILE                                  *         
      *****************************************************************         
       B150-READ-PRCG-STORE-GP-FILE.                                            
                                                                                
           MOVE 'B150-READ-PRCG-STORE-GP-FILE'  TO PV-PARAGRAPH-NAME.           
           SET PS-READ-FILE-START       TO TRUE.                                
                                                                                
           READ PRCG-STORE-GP-FILE INTO PM710-PRICING-STR-GP-RECORD             
               AT END                                                           
                   SET PS-READ-FILE-END TO TRUE                                 
           END-READ.                                                            
                                                                                
      *****************************************************************         
      * B200-PROCESS-AND-WRITE-FILE                                   *         
      *****************************************************************         
       B200-PROCESS-AND-WRITE-FILE.                                             
                                                                                
           IF PM710-ACTION-CDE = 'T'                                            
               MOVE PM710-BUSINESS-EVNT-ID TO PM700-BUSINESS-EVNT-ID            
               MOVE PA-RECS-WRITTEN TO PM700-TOTAL-NBR-OF-RECS                  
               WRITE PRCG-STORE-GP-LINE-OUT FROM PM700-TOTAL-RECORD             
           ELSE                                                                 
               IF PM710-PROMO-INTFC-ID =                                        
                      PV-PREV-PROMO-INTFC-ID                                    
                  AND PM710-MDSE-LNE-ID =                                       
                      PV-PREV-MDSE-LNE-ID                                       
      *           AND PM710-PROMO-PRIC-VER-ID =                                 
      *               PV-PREV-PROMO-PRIC-VER-ID                                 
                  AND PM710-STR-SELN-TYP-CDE =                                  
                      PV-PREV-STR-SELN-TYP-CDE                                  
                  AND PM710-STR-SELN-HIER-LVL-CDE =                             
                      PV-PREV-STR-SELN-HIER-LVL-CDE                             
                  AND PM710-ADHC-STR-GP-ID =                                    
                      PV-PREV-ADHC-STR-GP-ID                                    
                  AND PM710-STR-GP-TYP-CDE =                                    
                      PV-PREV-STR-GP-TYP-CDE                                    
                  AND PM710-STR-GP-ID =                                         
                      PV-PREV-STR-GP-ID                                         
                  AND PM710-PRNT-OV-STR-SELN-CDE =                              
                      PV-PREV-PRNT-OV-STR-SELN-CDE                              
                  AND PM710-PRNT-OV-STR-GP-CDE =                                
                      PV-PREV-PRNT-OV-STR-GP-CDE                                
                  AND PM710-PRNT-OV-STR-GP-ID =                                 
                      PV-PREV-PRNT-OV-STR-GP-ID                                 
                  AND PM710-PRNT-ADHC-STR-GP-ID =                               
                      PV-PREV-PRNT-ADHC-STR-GP-ID                               
                                                                                
                  ADD +1 TO PA-RECS-SKIPPED-CNT                                 
                                                                                
      *           DISPLAY 'SKIPPED THIS RECORD - DUP - DFFT STAT CDE:'          
      *           DISPLAY 'PM710-BUSINESS-EVNT-ID       = '                     
      *               PM710-BUSINESS-EVNT-ID                                    
      *           DISPLAY 'PM710-ACTION-CDE             = '                     
      *               PM710-ACTION-CDE                                          
      *           DISPLAY 'PM710-BUSINESS-EVNT-CRE-TMST = '                     
      *               PM710-BUSINESS-EVNT-CRE-TMST                              
      *           DISPLAY 'PM710-PROMO-INTFC-ID         = '                     
      *               PM710-PROMO-INTFC-ID                                      
      *           DISPLAY 'PM710-MDSE-LNE-ID            = '                     
      *               PM710-MDSE-LNE-ID                                         
      *           DISPLAY 'PM710-PROMO-PRIC-VER-ID      = '                     
      *               PM710-PROMO-PRIC-VER-ID                                   
      *           DISPLAY 'PM710-STR-SELN-TYP-CDE       = '                     
      *               PM710-STR-SELN-TYP-CDE                                    
      *           DISPLAY 'PM710-STR-SELN-HIER-LVL-CDE  = '                     
      *               PM710-STR-SELN-HIER-LVL-CDE                               
      *           DISPLAY 'PM710-ADHC-STR-GP-ID         = '                     
      *               PM710-ADHC-STR-GP-ID                                      
      *           DISPLAY 'PM710-STR-GP-TYP-CDE         = '                     
      *               PM710-STR-GP-TYP-CDE                                      
      *           DISPLAY 'PM710-STR-GP-ID              = '                     
      *               PM710-STR-GP-ID                                           
      *           DISPLAY 'PM710-PRNT-OV-STR-SELN-CDE   = '                     
      *               PM710-PRNT-OV-STR-SELN-CDE                                
      *           DISPLAY 'PM710-PRNT-OV-STR-GP-CDE     = '                     
      *               PM710-PRNT-OV-STR-GP-CDE                                  
      *           DISPLAY 'PM710-PRNT-OV-STR-GP-ID      = '                     
      *               PM710-PRNT-OV-STR-GP-ID                                   
      *           DISPLAY 'PM710-PRNT-ADHC-STR-GP-ID    = '                     
      *               PM710-PRNT-ADHC-STR-GP-ID                                 
      *           DISPLAY 'PM710-STRT-TMST              = '                     
      *               PM710-STRT-TMST                                           
      *           DISPLAY 'PM710-END-TMST               = '                     
      *               PM710-END-TMST                                            
      *           DISPLAY 'PM710-ACT-STRT-TMST          = '                     
      *               PM710-ACT-STRT-TMST                                       
      *           DISPLAY 'PM710-ACT-END-TMST           = '                     
      *               PM710-ACT-END-TMST                                        
      *           DISPLAY 'PM710-STAT-CDE               = '                     
      *               PM710-STAT-CDE                                            
      *           DISPLAY 'PM710-LAST-UPD-TMST          = '                     
      *               PM710-LAST-UPD-TMST                                       
      *           DISPLAY 'PM710-SORT-LAST-UPD-TMST     = '                     
      *               PM710-SORT-LAST-UPD-TMST                                  
               ELSE                                                             
                  MOVE PM710-PRICING-STR-GP TO                                  
                       PM709-PRICING-STR-GP-RECORD                              
                  WRITE PRCG-STORE-GP-LINE-OUT FROM                             
                        PM709-PRICING-STR-GP-RECORD                             
                  ADD +1 TO PA-RECS-WRITTEN                                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF PS-READ-FILE-START                                                
               MOVE PM710-PRICING-STR-GP-RECORD TO                              
                    PV-PREV-PM710-RECORD                                        
               PERFORM B150-READ-PRCG-STORE-GP-FILE                             
           ELSE                                                                 
               DISPLAY 'END'                                                    
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE CONTROL CARD AND TWO OUTPUT FILES.               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  PRCG-STORE-GP-FILE                                            
                  PRCG-STORE-GP-FILE-OUT.                                       
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
