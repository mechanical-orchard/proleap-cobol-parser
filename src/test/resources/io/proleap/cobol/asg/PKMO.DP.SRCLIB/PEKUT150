      *----------------------------------------------------------               
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------               
       PROGRAM-ID.             PEKUT150.                                        
       AUTHOR.                 JOHN BOLDT - STRATAGEM.                          
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           JANUARY 2008.                                    
       DATE-COMPILED.                                                           
      ******************************************************************        
      * CHANGE STATUS CODE FIELD ON CERTAIN PRICE CHANGE ID'S BASED ON          
      * VALUE PASSED IN FROM CONTROL CARDS.                                     
      ******************************************************************        
      *----------------------------------------------------------               
      * HISTORY LOG:                                                            
      *----------------------------------------------------------               
      * DATE:         01/14/2008                                                
      * WR/IR#:       WR#32742                                                  
      * PROGRAMMER:   JOHN BOLDT, STRATAGEM                                     
      * DESCRIPTION:  INITIAL CREATION.                                         
      *----------------------------------------------------------               
      * DATE:         07/30/2018                                                
      * WR/IR#:       CHG0204126                                                
      * PROGRAMMER:   COGNIZANT                                                 
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE                     
      *----------------------------------------------------------               
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------               
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT PRCHG-ID-FILE                                                 
             ASSIGN TO INP01.                                                   
                                                                                
           SELECT ORACLE-LOGON-FILE                                             
             ASSIGN TO ORALOGON.                                                
                                                                                
      *----------------------------------------------------------               
       DATA DIVISION.                                                           
      *----------------------------------------------------------               
       FILE SECTION.                                                            
                                                                                
       FD  PRCHG-ID-FILE                                                        
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PRCHG-ID-RECORD                 PIC  X(80).                          
                                                                                
       FD  ORACLE-LOGON-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
       01  OL-ORACLE-LOGON-RECORD      PIC X(80).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------               
      * ABEND- ABEND VARIABLES                                                  
      *----------------------------------------------------------               
       01  ABEND-CODE                      PIC S9(04)  COMP VALUE +0.           
           88  ABEND-4001-WRITE-ERROR                  VALUE +4001.             
           88  ABEND-4002-READ-ERROR                   VALUE +4002.             
           88  ABEND-4003-CTRL-CARD-ERROR              VALUE +4003.             
           88  ABEND-4004-TABLE-ERROR                  VALUE +4004.             
           88  ABEND-4005-OPEN-ERROR                   VALUE +4005.             
           88  ABEND-4006-CLOSE-ERROR                  VALUE +4006.             
           88  ABEND-4007-SEQUENCE-ERROR               VALUE +4007.             
           88  ABEND-4008-DATA-ERROR                   VALUE +4008.             
           88  ABEND-4009-KEY-DATA-ERROR               VALUE +4009.             
           88  ABEND-4013-SQL-ERROR                    VALUE +4013.             
           88  ABEND-4014-ORACLE-LOGON-ERROR           VALUE +4014.             
           88  ABEND-4019-DATE-ROUTINE-ERROR           VALUE +4019.             
           88  ABEND-4444-FORCED-ABEND                 VALUE +4444.             
                                                                                
      *----------------------------------------------------------               
      * PRICE CHANGE ID/STATUS CODE CONTROL CARD                                
      * (MULTIPLE PRICE CHANGE ID'S CAN BE RUN)                                 
      *----------------------------------------------------------               
       01  CC-PRCHG-ID-REC.                                                     
           05  CC-PRCHG-ID                 PIC  9(10)  VALUE ZEROS.        1-10 
           05  FILLER                      PIC  X(01)  VALUE SPACE.       11    
           05  CC-PRCHG-STAT-CDE           PIC  X(01)  VALUE SPACE.       12    
           05  FILLER                      PIC  X(68)  VALUE SPACE.       13-80 
                                                                                
      *----------------------------------------------------------               
      * PS- PROGRAM SWITCHES                                                    
      *----------------------------------------------------------               
       01  PS-PROGRAM-SWITCHES.                                                 
006510     05  PS-END-OF-LOGON-FILE-SW      PIC X(01)  VALUE 'N'.               
006530         88  PS-END-OF-LOGON-FILE                VALUE 'Y'.               
           05  PS-ORA-SRC-CTL-DONE-SW       PIC X(01)  VALUE 'N'.               
               88  PS-ORA-SRC-EMPTY                    VALUE 'Y'.               
           05  PS-END-OF-PRCHG-ID-INPUT-SW PIC  X(01)  VALUE 'N'.               
               88  PS-END-OF-PRCHG-ID                  VALUE 'Y'.               
               88  PS-NOT-END-OF-PRCHG-ID              VALUE 'N'.               
           05  PS-NOTHING-TO-PROCESS-SW    PIC  X(01)  VALUE 'N'.               
               88  PS-NOTHING-TO-DO                    VALUE 'Y'.               
               88  PS-SOMETHING-TO-DO                  VALUE 'N'.               
                                                                                
       01  CC-ORACLE-CONNECTION.                                                
           05  CC-ORACLE-USERID             PIC X(40).                          
               88 CC-AUTO-LOGON                         VALUE '/'.              
           05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
      *----------------------------------------------------------               
      * PC- PROGRAM CONSTANTS                                                   
      *----------------------------------------------------------               
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME             PIC  X(08)  VALUE                    
                                                       'PEKUT150'.              
           05  PC-SAS-ID                   PIC  X(08)  VALUE 'SAS'.             
           05  PC-D                        PIC  X(01)  VALUE 'D'.               
           05  PC-S                        PIC  X(01)  VALUE 'S'.               
                                                                                
      *----------------------------------------------------------               
      * PV- PROGRAM VARIABLES                                                   
      *----------------------------------------------------------               
       01  CT-CPU-TIME.                                                         
           05  CT-CPU-HR                   PIC  9(02)  VALUE ZEROS.             
           05  CT-CPU-MIN                  PIC  9(02)  VALUE ZEROS.             
           05  CT-CPU-SEC                  PIC  9(02)  VALUE ZEROS.             
           05  CT-CPU-HSEC                 PIC  9(02)  VALUE ZEROS.             
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-ORACLE-USERID-LEN        PIC S9(04)  VALUE ZEROS              
                                                       COMP SYNC.               
           05  PV-ORACLE-PASSWORD-LEN      PIC S9(04)  VALUE ZEROS              
                                                       COMP SYNC.               
           05  PV-TABLE-NAME               PIC  X(30)  VALUE SPACE.             
           05  PV-SQL-CODE                 PIC  9(09)  VALUE ZEROS.             
           05  PV-OPERATION-MSG            PIC  X(60)  VALUE SPACE.             
           05  PV-OPERATION-MSG-1          PIC  X(40)  VALUE SPACE.             
           05  PV-PARAGRAPH-NAME           PIC  X(30)  VALUE SPACE.             
           05  PV-CPU-TIME.                                                     
               10  PV-CPU-HR               PIC  9(02)  VALUE ZEROS.             
               10  FILLER                  PIC  X(01)  VALUE ":".               
               10  PV-CPU-MIN              PIC  9(02)  VALUE ZEROS.             
               10  FILLER                  PIC  X(01)  VALUE ":".               
               10  PV-CPU-SEC              PIC  9(02)  VALUE ZEROS.             
               10  FILLER                  PIC  X(01)  VALUE ":".               
               10  PV-CPU-HSEC             PIC  9(02)  VALUE ZEROS.             
                                                                                
                                                                                
      *----------------------------------------------------------               
      * PA- PROGRAM ACCUMULATORS                                                
      *----------------------------------------------------------               
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-PRCHG-ID-RECS-READ       PIC  9(09)  VALUE ZEROS.             
           05  PA-PRCHG-IDS-UPDATED        PIC  9(09)  VALUE ZEROS.             
                                                                                
                                                                                
      *----------------------------------------------------------               
      *                      O  R  A  C  L  E                                   
      *==========================================================               
      *                                                                         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *                                                                         
      *----------------------------------------------------------               
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *----------------------------------------------------------               
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *----------------------------------------------------------               
      *     COPY MOWS700.                                                       
                                                                                
       01  PC-ENT01-TYPE-CDE                PIC  X(02)  VALUE '01'.             
       01  PV-ORACLE-ID                     PIC  X(1)   VALUE '/'.              
                                                                                
       01  USERNAME                         PIC  X(10)  VARYING.                
       01  PASSWD                           PIC  X(10)  VARYING.                
                                                                                
       01  ORACLE-RETURN                    PIC  X(200) VALUE SPACE.            
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
                                                                                
      *----------------------------------------------------------               
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------               
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           IF PS-NOTHING-TO-DO                                                  
               DISPLAY 'INPUT CARD FILE EMPTY'                                  
           ELSE                                                                 
               PERFORM 2000-PROCESS-PRICE-CHANGES                               
                           UNTIL PS-END-OF-PRCHG-ID                             
               PERFORM 9000-COMMIT                                              
           END-IF.                                                              
                                                                                
           PERFORM 9999-WRAPUP.                                                 
                                                                                
           GOBACK.                                                              
                                                                                
      *----------------------------------------------------------               
      * 1000-INITIALIZATION.                                                    
      *----------------------------------------------------------               
       1000-INITIALIZATION.                                                     
                                                                                
           DISPLAY 'PROCESSING BEGINNING: ' PC-PROGRAM-NAME.                    
           PERFORM 9200-GET-TIME.                                               
           DISPLAY 'STARTING TIME: ' PV-CPU-TIME.                               
                                                                                
           OPEN INPUT  PRCHG-ID-FILE.                                           
                                                                                
           PERFORM 1010-ORACLE-SETUP.                                           
                                                                                
           PERFORM 1100-READ-PRCHG-ID.                                          
           IF PS-END-OF-PRCHG-ID                                                
               MOVE '1100-READ-PRCHG-ID'          TO PV-PARAGRAPH-NAME          
               MOVE 'EMPTY PC ID/STAT CDE FILE'   TO PV-OPERATION-MSG-1         
               SET   PS-NOTHING-TO-DO             TO TRUE                       
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------               
      * 1010-ORACLE-SETUP.                                                      
      *----------------------------------------------------------               
       1010-ORACLE-SETUP.                                                       
                                                                                
           OPEN INPUT ORACLE-LOGON-FILE.                                        
                                                                                
           PERFORM 1020-READ-ORACLE-LOGON.                                      
                                                                                
           CLOSE ORACLE-LOGON-FILE.                                             
                                                                                
           IF ((PS-END-OF-LOGON-FILE)                                           
                   OR (CC-ORACLE-CONNECTION = SPACES))                          
               DISPLAY '** ABEND **'                                            
               DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
                         'NOT PROVIDED'                                         
               SET PS-ORA-SRC-EMPTY TO TRUE                                     
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
           IF CC-AUTO-LOGON                                                     
               DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
               EXEC SQL                                                         
                    CONNECT :USERNAME                                           
               END-EXEC                                                         
           ELSE                                                                 
               INITIALIZE PV-ORACLE-USERID-LEN                                  
                          PV-ORACLE-PASSWORD-LEN                                
               INSPECT CC-ORACLE-USERID                                         
                   TALLYING PV-ORACLE-USERID-LEN                                
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            INSPECT CC-ORACLE-PASSWORD                                          
                TALLYING PV-ORACLE-PASSWORD-LEN                                 
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
                      USERNAME-ARR                                              
            MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
            MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
                      PASSWD-ARR                                                
            MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
            DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
                      USERNAME-ARR                                              
            EXEC SQL                                                            
              CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
            END-EXEC                                                            
            INITIALIZE CC-ORACLE-PASSWORD                                       
                       PASSWD                                                   
            END-IF.                                                             
                                                                                
020500     IF (SQLCODE NOT = 0)                                                 
020900         DISPLAY PV-PARAGRAPH-NAME                                        
020910         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
021000         PERFORM Z100-ABEND                                               
021200     END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------               
034504** READ THE ORACLE LOGON CONTROL CARD.                                    
      *----------------------------------------------------------               
034506 1020-READ-ORACLE-LOGON.                                                  
                                                                                
034510     READ ORACLE-LOGON-FILE                                               
034520         INTO CC-ORACLE-CONNECTION                                        
034530         AT END                                                           
034540             SET PS-END-OF-LOGON-FILE TO TRUE                             
034550     END-READ.                                                            
034600*                                                                         
                                                                                
      *----------------------------------------------------------               
      * 1100-READ-PRCHG-ID.                                                     
      *----------------------------------------------------------               
       1100-READ-PRCHG-ID.                                                      
                                                                                
           READ PRCHG-ID-FILE INTO CC-PRCHG-ID-REC                              
             AT END                                                             
               SET PS-END-OF-PRCHG-ID TO TRUE                                   
           END-READ.                                                            
                                                                                
           IF PS-NOT-END-OF-PRCHG-ID                                            
               DISPLAY 'PC/STAT CDE RECORD = ' CC-PRCHG-ID-REC                  
               ADD +1 TO PA-PRCHG-ID-RECS-READ                                  
           END-IF.                                                              
                                                                                
                                                                                
      *--------------------------------------------------------------           
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT              
      * LOGS ON TO ORACLE.                                                      
      *---------------------------------------------------------------          
      *    COPY MOPD700.                                                        
                                                                                
                                                                                
      *---------------------------------------------------------------          
      * 2000-PROCESS-PRICE-CHANGES.                                             
      *     PROCESS EACH PRICE CHANGE/MDSE SELN LINE IDS RECORD                 
      *---------------------------------------------------------------          
       2000-PROCESS-PRICE-CHANGES.                                              
                                                                                
           IF CC-PRCHG-ID = SPACE                                               
               MOVE '2000-PROCESS-PRICE-CHANGES' TO PV-PARAGRAPH-NAME           
               MOVE 'PRICE CHANGE ID MISSING'    TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                           
               PERFORM 9850-ABEND-DISPLAY                                       
               PERFORM 9800-ABEND                                               
           ELSE                                                                 
               IF CC-PRCHG-ID NUMERIC                                           
                 NEXT SENTENCE                                                  
               ELSE                                                             
                 MOVE '2000-PROCESS-PRICE-CHANGES' TO PV-PARAGRAPH-NAME         
                 MOVE 'PRICE CHANGE ID INVALID'    TO PV-OPERATION-MSG-1        
                 SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                         
                 PERFORM 9850-ABEND-DISPLAY                                     
                 PERFORM 9800-ABEND                                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF CC-PRCHG-STAT-CDE = SPACE                                         
               MOVE PC-D TO CC-PRCHG-STAT-CDE                                   
           ELSE                                                                 
             IF CC-PRCHG-STAT-CDE = PC-D OR PC-S                                
                 NEXT SENTENCE                                                  
             ELSE                                                               
               MOVE '2000-PROCESS-PRICE-CHANGES'  TO PV-PARAGRAPH-NAME          
               MOVE 'PRICE CHANGE STATUS INVALID' TO PV-OPERATION-MSG-1         
               SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                           
               PERFORM 9850-ABEND-DISPLAY                                       
               PERFORM 9800-ABEND                                               
           END-IF.                                                              
                                                                                
           PERFORM 3000-UPDATE-PRCHG.                                           
                                                                                
           PERFORM 1100-READ-PRCHG-ID.                                          
                                                                                
                                                                                
      *----------------------------------------------------------               
      * 3000-UPDATE-PRCHG.                                                      
      *     UPDATES THE SELECTED PRICE CHANGE WITH THE NEW STATUS.              
      *----------------------------------------------------------               
       3000-UPDATE-PRCHG.                                                       
                                                                                
           EXEC SQL                                                             
              UPDATE PET_PRCHG                                                  
                 SET STAT_CDE           = :CC-PRCHG-STAT-CDE                    
                    ,LAST_UPD_TMST      = SYSDATE                               
                    ,LAST_UPD_BY_USR_ID = :PC-SAS-ID                            
               WHERE PRCHG_ID           = :CC-PRCHG-ID                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
                  ADD +1 TO PA-PRCHG-IDS-UPDATED                                
             WHEN OTHER                                                         
                  MOVE SQLCODE      TO PV-SQL-CODE                              
                  SET ABEND-4013-SQL-ERROR TO TRUE                              
                  MOVE '3000-UPDATE-PRCHG'                                      
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'ERROR UPDATING PC W/NEW STATUS'                         
                                   TO PV-OPERATION-MSG                          
                  PERFORM 9900-ORACLE-ABEND                                     
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *****************************************************************         
      * 9000-COMMIT                                                   *         
      *****************************************************************         
       9000-COMMIT.                                                             
                                                                                
           DISPLAY 'COMMITTING...'                                              
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = ZEROS                                                   
               DISPLAY 'COMMITTED.'                                             
           ELSE                                                                 
               MOVE 'ERROR DURING COMMIT' TO PV-OPERATION-MSG                   
               MOVE '9000-COMMIT'         TO PV-PARAGRAPH-NAME                  
               PERFORM 9900-ORACLE-ABEND                                        
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------               
      * 9200-GET-TIME.                                                          
      *----------------------------------------------------------               
       9200-GET-TIME.                                                           
                                                                                
           ACCEPT CT-CPU-TIME FROM TIME.                                        
           MOVE CT-CPU-HR     TO   PV-CPU-HR.                                   
           MOVE CT-CPU-MIN    TO   PV-CPU-MIN.                                  
           MOVE CT-CPU-SEC    TO   PV-CPU-SEC.                                  
           MOVE CT-CPU-HSEC   TO   PV-CPU-HSEC.                                 
                                                                                
                                                                                
      *----------------------------------------------------------               
      * 9800-ABEND.                                                             
      *     PERFORMS A NON-SQL-ABEND.                                           
      *----------------------------------------------------------               
       9800-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
                                                                                
      *----------------------------------------------------------               
      * 9850-ABEND-DISPLAY.                                                     
      *     DISPLAYS INFORMATION FOR A NON-SQL-ABEND.                           
      *----------------------------------------------------------               
       9850-ABEND-DISPLAY.                                                      
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
                                                                                
      *----------------------------------------------------------               
      * 9900-ORACLE-ABEND.                                                      
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       9900-ORACLE-ABEND.                                                       
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' PV-SQL-CODE.                       
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      *-----------------------------------------------------------              
      * Z100-ABEND                                                              
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE                      
      *  - CALLED BY MOPD700!                                                   
      *-----------------------------------------------------------              
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                           
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      *----------------------------------------------------------               
      * 9999-WRAPUP.                                                            
      *     END OF PROGRAM ROUTINE.  CLOSES ALL FILES AND DISPLAYS              
      *     ANY DATA FROM THE PROGRAM.                                          
      *----------------------------------------------------------               
       9999-WRAPUP.                                                             
                                                                                
           CLOSE PRCHG-ID-FILE.                                                 
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'NUMBER OF RECS READ...................: '                   
                                        PA-PRCHG-ID-RECS-READ.                  
           DISPLAY 'NUMBER OF PRICE CHANGES UPDATED.......: '                   
                                        PA-PRCHG-IDS-UPDATED.                   
                                                                                
           PERFORM 9200-GET-TIME.                                               
           DISPLAY ' '.                                                         
           DISPLAY 'ENDING TIME: ' PV-CPU-TIME.                                 
