      ***************************************************************** 00030000
       IDENTIFICATION DIVISION.                                         00040000
      ***************************************************************** 00050000
       PROGRAM-ID.             RCKUT147.                                00060000
       AUTHOR.                 V.SUDHAN.                                00070000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00080000
       DATE-WRITTEN.           MARCH, 2010.                             00090000
       DATE-COMPILED.                                                   00100000
      ***************************************************************** 00110000
      *                                                               * 00120000
      *  THIS PROGRAM WILL USE A CURSOR TO DETERMINE THE ROWS ON THE  * 00130000
      *  TRACKING TABLE THAT CAN BE DELETED.                          * 00140000
      *                                                               * 00150000
      *  THIS MODULE USE THE OUTPUT FILE CREATED BY RCKUT146.         * 00160000
      *  THE INPUT RECORD LAYOUT IS RCRD089.                          * 00170000
      *  THE OUTPUT FILE RECORD LAYOUT IS RCRD090.                    * 00180000
      *                                                               * 00190000
      *  OUTPUT RECORDS WILL BE WRITTEN FOR EACH INPUT RECORD READ.   * 00200000
      *                                                               * 00210000
      ***************************************************************** 00250000
       ENVIRONMENT DIVISION.                                            00260000
      ***************************************************************** 00270000
      *                                                                 00280000
       CONFIGURATION SECTION.                                           00290000
      *                                                                 00300000
       SOURCE-COMPUTER.        IBM-3090.                                00310000
       OBJECT-COMPUTER.        IBM-3090.                                00320000
      *                                                                 00330000
       INPUT-OUTPUT SECTION.                                            00340000
      *                                                                 00350000
       FILE-CONTROL.                                                    00360000
      *                                                                 00370000
           SELECT RCVR-PURGE-EXT-FILE                                   00380000
                  ASSIGN TO INP01.                                      00390000
      *                                                                 00401000
           SELECT RCVR-PURGE-REC-FILE                                   00402000
                  ASSIGN TO OUT01.                                      00403000
      *                                                                 00404000
      ***************************************************************** 00410000
       DATA DIVISION.                                                   00420000
      ***************************************************************** 00430000
      *                                                                 00440000
       FILE SECTION.                                                    00450000
      *                                                                 00460000
       FD  RCVR-PURGE-EXT-FILE                                          00470000
           RECORDING MODE IS F                                          00480000
           BLOCK CONTAINS 0  RECORDS.                                   00490000
       01  RCVR-PURGE-EXT-REC              PIC  X(075).                 00500000
      *                                                                 00510000
       FD  RCVR-PURGE-REC-FILE                                          00511000
           RECORDING MODE IS F                                          00512000
           BLOCK CONTAINS 0  RECORDS.                                   00513000
       01  RCVR-PURGE-REC                  PIC  X(110).                 00514000
      *                                                                 00515000
                                                                        00520000
       WORKING-STORAGE SECTION.                                         00530000
      *                                                                 00540000
       01  ABEND-CODE                      PIC S9(04)    VALUE +0       00550000
                                                         COMP SYNC.     00560000
      *                                                                 00570000
       01  PROGRAM-SWITCHES.                                            00580000
           05  PS-EOF-RCVR-CSR-SW          PIC  X(01)    VALUE 'N'.     00590000
               88  PS-EOF-RCVR-DATA-CSR                  VALUE 'Y'.     00600000
               88  PS-NOT-EOF-RCVR-DATA-CSR              VALUE 'N'.     00610000
           05  PS-EOF-RCVR-CNTL-CSR-SW     PIC  X(01)    VALUE 'N'.     00610100
               88  PS-EOF-RCVR-CNTL-DATA-CSR             VALUE 'Y'.     00610200
               88  PS-NOT-EOF-RCVR-CNTL-DATA-CSR         VALUE 'N'.     00610300
           05  PS-EOF-RCVR-FILE-SW         PIC  X(01)    VALUE 'N'.     00611000
               88  PS-EOF-RCVR-FILE                      VALUE 'Y'.     00612000
               88  PS-NOT-EOF-RCVR-FILE                  VALUE 'N'.     00613000
      *                                                                 00620000
       01  PC-PROGRAM-CONSTANTS.                                        00630000
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE          00640000
                                                         'RCKUT147'.    00650000
           05  PC-FROM-DATE                PIC  X(10)  VALUE SPACES.    00660000
           05  PC-TO-DATE                  PIC  X(10)  VALUE SPACES.    00670000
           05  PC-VERIFY-B4-DATE           PIC  X(10)  VALUE SPACES.    00680000
      *                                                                 00690000
       01  PV-PROGRAM-VARIABLES.                                        00700000
           05  PV-ABEND-PARAGRAPH          PIC  X(30)  VALUE SPACES.    00710000
           05  PV-PARAGRAPH-NAME           PIC  X(30)  VALUE SPACES.    00720000
           05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)  VALUE SPACES.    00730000
           05  PV-DC-FUNC-CDE              PIC  X(02)  VALUE SPACES.    00731000
           05  PV-RCVR-DATA-CNT            PIC  9(09)  VALUE ZEROS.     00750000
           05  PV-RCVR-TOTAL-CNT           PIC  9(09)  VALUE ZEROS.     00760000
           05  PV-REC-READ-COUNT           PIC  9(09)  VALUE ZEROS.     00761000
           05  PV-REC-READ-CNTL-COUNT      PIC  9(09)  VALUE ZEROS.     00761100
           05  PV-PO-NBR                   PIC S9(9) COMP VALUE ZEROS.  00762000
           05  PV-RCVR-SEQ-NBR             PIC S9(9) COMP VALUE ZEROS.  00763000
           05  PV-PO-LINE-NBR              PIC S9(9) COMP VALUE ZEROS.  00764000
           05  PV-PREPK-ID                 PIC S9(4) COMP VALUE ZEROS.  00765000
           05  PV-CSR-REC-COUNT            PIC S9(9) COMP-3 VALUE +0.   00766000
           05  PV-WRITE-CNTL-COUNT         PIC S9(9) COMP-3 VALUE +0.   00767000
           05  PV-DISP-PO                  PIC  Z(9) VALUE ZERO.        00768001
           05  PV-DISP-RCVR                PIC  9(4) VALUE ZERO.        00769001
           05  PV-DISP-LINE                PIC  9(4) VALUE ZERO.        00769101
      *                                                                 00770000
       01  DN-DB2-NULL-INDICATORS.                                      00771000
           05  DN-DEST-OPER-UNT           PIC S9(04) COMP VALUE +0.     00772000
           05  DN-PKSL-SEQ-NBR            PIC S9(04) COMP VALUE +0.     00773000
           05  DN-PO-LNE-NBR              PIC S9(04) COMP VALUE +0.     00774000
           05  DN-PREPK-ID                PIC S9(04) COMP VALUE +0.     00775000
      ******************************************************************00780000
      *  COPY BOOK FOR THE RECEIVER TRACKING TABLE                      00790000
      ******************************************************************00800000
           COPY RCRD089.                                                00810000
      ******************************************************************00811000
      *  COPY BOOK FOR THE RECEIVER TRACKING TABLE                      00812000
      ******************************************************************00813000
           COPY RCRD090.                                                00814000
      ******************************************************************00820000
      *  DB2 FORMATTED MESSAGE AREA                                     00830000
      ******************************************************************00840000
           COPY DPWS004.                                                00850000
      ******************************************************************00860000
      *  DB2 COMMUNICATION AREA                                         00870000
      ******************************************************************00880000
           EXEC SQL                                                     00890000
               INCLUDE SQLCA                                            00900000
           END-EXEC.                                                    00910000
      ******************************************************************00920000
      *  DB2 COMMUNICATION AREA2                                        00930000
      ******************************************************************00940000
           COPY SQLCA2.                                                 00950000
      ******************************************************************00960000
      *  COPY BOOK FOR THE RECEIVER TABLE                               00970000
      ******************************************************************00980000
           EXEC SQL                                                     00990000
                INCLUDE WST00008                                        01000000
           END-EXEC.                                                    01010000
      *                                                                 01020000
           EXEC SQL                                                     01030000
                INCLUDE WST00009                                        01040000
           END-EXEC.                                                    01050000
      *                                                                 01060000
           EXEC SQL                                                     01070000
                INCLUDE WST00010                                        01080000
           END-EXEC.                                                    01090000
      *                                                                 01100000
           EXEC SQL                                                     01110000
                INCLUDE RCT00017                                        01120000
           END-EXEC.                                                    01130000
      *                                                                 01390000
           EXEC SQL                                                     01404100
             DECLARE RCVR-CNTL-DATA-CSR CURSOR FOR                      01404200
             SELECT                                                     01404300
                    E.PO_NBR                                            01404400
                  , E.RCVR_SEQ_NBR                                      01404500
                  , E.PO_LNE_NBR                                        01404600
                  , E.PREPK_ID                                          01404700
                  , E.DC_FUNC_CDE                                       01404800
                  , COUNT(PO_NBR)                                       01404900
             FROM WST_INTGRTN_TYP   A                                   01405700
                , WST_ATTR_DEFN     B                                   01405800
                , WST_ATTR_ELEM     C                                   01405900
                , WST_ATTR_ELEM     D                                   01406000
                , RCT_PO_RCVR_TRKG  E                                   01406100
             WHERE A.INTGRTN_TYP_CDE    =  'TRKGPURGE2'                 01406200
               AND A.INTGRTN_TYP_CDE    =  B.INTGRTN_TYP_CDE            01406300
               AND B.ATTR_DEFN_SEQ_NBR  =  1                            01406400
               AND A.INTGRTN_TYP_CDE    =  C.INTGRTN_TYP_CDE            01406500
               AND C.ATTR_DEFN_SEQ_NBR  =  1                            01406600
               AND A.INTGRTN_TYP_CDE    =  D.INTGRTN_TYP_CDE            01406700
               AND D.ATTR_DEFN_SEQ_NBR  =  2                            01406800
               AND C.ATTR_ELEM_SEQ_NBR  =  D.ATTR_ELEM_SEQ_NBR          01406900
               AND E.PO_NBR             =  :PV-PO-NBR                   01407000
               AND E.RCVR_SEQ_NBR       =  :PV-RCVR-SEQ-NBR             01407100
               AND E.PO_LNE_NBR         =  :PV-PO-LINE-NBR              01407200
               AND E.PREPK_ID           =  :PV-PREPK-ID                 01407300
               AND C.ATTR_ELEM_TXT      =  E.DC_FUNC_CDE                01407400
               GROUP BY                                                 01407500
                    E.PO_NBR                                            01407600
                  , E.RCVR_SEQ_NBR                                      01407700
                  , E.PO_LNE_NBR                                        01407800
                  , E.PREPK_ID                                          01407900
                  , E.DC_FUNC_CDE                                       01408000
               HAVING  COUNT(PO_NBR)  > 2                               01409000
           END-EXEC.                                                    01409900
      *                                                                 01410000
           EXEC SQL                                                     01410100
             DECLARE RCVR-DATA-CSR CURSOR FOR                           01410200
             SELECT                                                     01410300
                    F.PO_NBR                                            01410400
                  , F.RCVR_SEQ_NBR                                      01410500
                  , F.CRE_BY_USR_ID                                     01410600
                  , F.CRE_TMST                                          01410700
                  , F.DC_FUNC_CDE                                       01410800
                  , F.LOC_NBR                                           01410900
                  , F.PO_LNE_NBR                                        01411000
                  , F.PREPK_ID                                          01411100
                  , F.CRE_BY_PGM_NM                                     01411200
                  , F.QTY_TRKD_NBR                                      01411300
                  , F.LAST_UPD_TMST                                     01411400
                  , F.PKSL_SEQ_NBR                                      01411500
                  , F.DEST_LOC_NBR                                      01411600
             FROM RCT_PO_RCVR_TRKG  F                                   01411700
             WHERE F.PO_NBR             =  :PV-PO-NBR                   01413000
               AND F.RCVR_SEQ_NBR       =  :PV-RCVR-SEQ-NBR             01413100
               AND F.PO_LNE_NBR         =  :PV-PO-LINE-NBR              01413200
               AND F.PREPK_ID           =  :PV-PREPK-ID                 01413300
               AND F.DC_FUNC_CDE        =  :PV-DC-FUNC-CDE              01413400
             ORDER BY F.CRE_TMST                                        01413503
           END-EXEC.                                                    01415300
      *                                                                 01415400
       PROCEDURE DIVISION.                                              01415500
      *                                                                 01416000
      ******************************************************************01420000
      **  THIS IS THE MAIN DRIVER FOR THE PROGRAM.                    **01430000
      ******************************************************************01440000
      *                                                                 01450000
       0000-MAINLINE-PROCESSING.                                        01460000
      *                                                                 01470000
           PERFORM 1000-INITIALIZATION.                                 01480000
      *                                                                 01490000
           PERFORM 2000-PROCESS-RCVR-EXT-DATA UNTIL                     01500000
                   PS-EOF-RCVR-FILE.                                    01510000
      *                                                                 01520000
           DISPLAY '                                             '.     01530000
           DISPLAY '** RCKUT147-TOTAL NO OF RECORDS READ       : '      01540000
                   PV-REC-READ-COUNT                                    01550000
           DISPLAY '** RCKUT147-TOTAL NO OF RECORDS FETCHED    : '      01551000
                   PV-RCVR-DATA-CNT                                     01552000
           DISPLAY '** RCKUT147-TOTAL OUTPUT RECORDS WRITTEN   : '      01560000
                   PV-RCVR-TOTAL-CNT.                                   01570000
           DISPLAY '                                             '.     01580000
           DISPLAY '** PROGRAM RCKUT147 EXECUTION ENDED   **     '.     01590000
      *                                                                 01600000
           CLOSE RCVR-PURGE-EXT-FILE                                    01620000
                 RCVR-PURGE-REC-FILE.                                   01621000
      *                                                                 01630000
           GOBACK.                                                      01640000
      *                                                                 01650000
      ******************************************************************01660000
      ** PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.              **01670000
      ******************************************************************01680000
      *                                                                 01690000
       1000-INITIALIZATION.                                             01700000
      *                                                                 01710000
           DISPLAY '** PROGRAM RCKUT147 EXECUTION STARTED **'.          01720000
      *                                                                 01730000
           OPEN INPUT  RCVR-PURGE-EXT-FILE                              01740000
                OUTPUT RCVR-PURGE-REC-FILE.                             01741000
      *                                                                 01741100
           SET  PS-NOT-EOF-RCVR-FILE     TO TRUE.                       01742000
           PERFORM 8000-READ-RCVR-DATA-FILE.                            01750000
           IF PS-EOF-RCVR-FILE                                          01751000
              DISPLAY '                                       '         01752000
              DISPLAY '** RCKUT147-RECEIVER DATA FILE IS EMPTY'         01752100
              DISPLAY '                                       '         01752200
           END-IF                                                       01753000
      *                                                                 01760000
           INITIALIZE  RC090-PO-RCVR-TRKG-DELETE.                       01790000
      *                                                                 01820000
      ******************************************************************02530000
      **  THIS PARAGRAPH READS THE RECORDS FROM PO ORD NBR PURGE CSR  **02540000
      **  RCVR-DATA-CSR  AND WRITE A RECORD TO THE OUTPUT FILE.       **02550000
      ******************************************************************02560000
      *                                                                 02570000
       2000-PROCESS-RCVR-EXT-DATA.                                      02580000
      *                                                                 02590000
            SET  PS-NOT-EOF-RCVR-CNTL-DATA-CSR TO TRUE.                 02590500
            PERFORM UNTIL PS-EOF-RCVR-CNTL-DATA-CSR                     02590600
              PERFORM 8400-OPEN-RCVR-CNTL-DATA-CSR                      02590800
              PERFORM 8500-FETCH-RCVR-CNTL-DATA-CSR                     02590900
              SET  PS-NOT-EOF-RCVR-DATA-CSR TO TRUE                     02591100
              PERFORM 8100-OPEN-RCVR-DATA-CSR                           02591200
              PERFORM 8200-FETCH-RCVR-DATA-CSR UNTIL                    02591700
                      PS-EOF-RCVR-DATA-CSR                              02591800
              PERFORM 8300-CLOSE-RCVR-DATA-CSR                          02591900
              PERFORM 8600-CLOSE-RCVR-CNTL-DATA-CSR                     02592000
              MOVE ZEROES TO PV-WRITE-CNTL-COUNT                        02592100
            END-PERFORM                                                 02592200
            IF PV-REC-READ-CNTL-COUNT EQUAL 50000                       02592300
               MOVE RC089-ORD-NBR      TO  PV-DISP-PO                   02592401
               MOVE RC089-REC-SEQ-NBR  TO  PV-DISP-RCVR                 02592501
               MOVE RC089-ORD-LNE-NBR  TO  PV-DISP-LINE                 02592801
               DISPLAY 'THE RECORDS PROCESSED COUNT IS : '              02592900
                       PV-REC-READ-COUNT                                02593000
                       '  '                                             02593101
                       PV-DISP-PO '-'                                   02593201
                       PV-DISP-RCVR '-'                                 02593301
                       PV-DISP-LINE  '   '                              02593402
                       'OUTPUT COUNT:'  PV-RCVR-TOTAL-CNT               02593502
               MOVE ZEROES TO PV-REC-READ-CNTL-COUNT                    02593702
            END-IF                                                      02594000
            PERFORM 8000-READ-RCVR-DATA-FILE.                           02670000
      *                                                                 02680000
      ******************************************************************02941000
      ** THIS PARAGRAPH WILL SELECT THE DATE RANGE TO FETCH THE       **02942000
      ** RECORDS FROM THE RECEIVER TABLES (TRECEIV AND TRECITM)       **02943000
      ******************************************************************02944000
      *                                                                 02945000
       8000-READ-RCVR-DATA-FILE.                                        02946000
      *                                                                 02947000
           MOVE '8000-READ-RCVR-DATA-FILE' TO  PV-ABEND-PARAGRAPH.      02948000
      *                                                                 02949000
           INITIALIZE PV-PO-NBR                                         02949100
                      PV-RCVR-SEQ-NBR                                   02949200
                      PV-PO-LINE-NBR                                    02949300
                      PV-PREPK-ID                                       02949400
      *                                                                 02949500
           READ RCVR-PURGE-EXT-FILE                                     02949600
               INTO                                                     02949700
                   RC089-PO-RCVR-TRKG-DELETE                            02949800
               AT END                                                   02949900
                   SET PS-EOF-RCVR-FILE      TO TRUE                    02950000
               NOT AT END                                               02950200
                   ADD 1                     TO PV-REC-READ-COUNT       02950300
                                                PV-REC-READ-CNTL-COUNT  02950400
                   MOVE RC089-ORD-NBR        TO PV-PO-NBR               02950500
                   MOVE RC089-REC-SEQ-NBR    TO PV-RCVR-SEQ-NBR         02950600
                   MOVE RC089-ORD-LNE-NBR    TO PV-PO-LINE-NBR          02950700
                   MOVE RC089-PREPK-ID       TO PV-PREPK-ID             02950800
           END-READ.                                                    02950900
      *                                                                 02951000
      ******************************************************************02951100
      **  THIS PARAGRAPH OPENS THE CURSOR WHICH WILL CONTAIN THE NO   **02952000
      **  OF PO ORD THAT CAN BEOLD ENOUGH AND CAN BE PURGED.          **02953000
      ******************************************************************02954000
      *                                                                 02955000
       8100-OPEN-RCVR-DATA-CSR.                                         02956000
      *                                                                 02957000
           EXEC SQL                                                     02958000
                OPEN RCVR-DATA-CSR                                      02959000
           END-EXEC.                                                    02960000
      *                                                                 02970000
           EVALUATE TRUE                                                02980000
               WHEN SQLCODE = 0                                         02990000
                   CONTINUE                                             03000000
               WHEN SQLWARN0 NOT = SPACES                               03010000
               WHEN SQLCODE < ZERO                                      03020000
               WHEN SQLCODE > ZERO                                      03030000
                   MOVE '8100-OPEN-RCVR-DATA-CSR'                       03040000
                                   TO PV-PARAGRAPH-NAME                 03050000
                   MOVE 'OPEN THE RCVR-DATA-CSR        '                03051000
                                   TO PV-DB2-OPERATION-ATTEMPTED        03052000
                   PERFORM 9000-SQL-ABEND                               03053000
           END-EVALUATE.                                                03054000
      *                                                                 03055000
      ******************************************************************03060000
      **  THIS PARAGRAPH FETCHES THE CURSOR WHICH WILL CONTAIN THE PO **03070000
      **  ORDERS THAT CAN HAVE THE DATA WHICH CAN BE PURGED.          **03080000
      ******************************************************************03090000
      *                                                                 03100000
       8200-FETCH-RCVR-DATA-CSR.                                        03110000
      *                                                                 03120000
           EXEC SQL                                                     03130000
                FETCH RCVR-DATA-CSR                                     03140000
                INTO  :RCT00017-PO-NBR                                  03150000
                   ,  :RCT00017-RCVR-SEQ-NBR                            03160000
                   ,  :RCT00017-CRE-BY-USR-ID                           03170000
                   ,  :RCT00017-CRE-TMST                                03180000
                   ,  :RCT00017-DC-FUNC-CDE                             03190000
                   ,  :RCT00017-LOC-NBR                                 03200000
                   ,  :RCT00017-PO-LNE-NBR      :DN-PO-LNE-NBR          03201000
                   ,  :RCT00017-PREPK-ID        :DN-PREPK-ID            03202000
                   ,  :RCT00017-CRE-BY-PGM-NM                           03203000
                   ,  :RCT00017-QTY-TRKD-NBR                            03204000
                   ,  :RCT00017-LAST-UPD-TMST                           03205000
                   ,  :RCT00017-PKSL-SEQ-NBR    :DN-PKSL-SEQ-NBR        03206000
                   ,  :RCT00017-DEST-LOC-NBR    :DN-DEST-OPER-UNT       03207000
           END-EXEC.                                                    03210000
      *                                                                 03220000
           EVALUATE TRUE                                                03230000
             WHEN SQLCODE = ZEROS                                       03240000
               ADD 1 TO PV-WRITE-CNTL-COUNT                             03240100
               IF (PV-WRITE-CNTL-COUNT EQUAL 1 ) OR                     03240200
                  (PV-WRITE-CNTL-COUNT EQUAL  PV-CSR-REC-COUNT)         03240300
                  CONTINUE                                              03240400
               ELSE                                                     03240500
                  IF DN-DEST-OPER-UNT = -1                              03241000
                     MOVE ZEROES TO RCT00017-DEST-LOC-NBR               03242000
                  END-IF                                                03243000
                  IF DN-PKSL-SEQ-NBR  = -1                              03244000
                     MOVE ZEROES TO RCT00017-PKSL-SEQ-NBR               03245000
                  END-IF                                                03246000
                  IF DN-PO-LNE-NBR    = -1                              03247000
                     MOVE ZEROES TO RCT00017-PO-LNE-NBR                 03248000
                  END-IF                                                03249000
                  IF DN-PREPK-ID      = -1                              03249100
                     MOVE ZEROES TO RCT00017-PREPK-ID                   03249200
                  END-IF                                                03249300
                  ADD  1                      TO  PV-RCVR-DATA-CNT      03250000
                  MOVE RCT00017-PO-NBR        TO RC090-PO-NBR           03251000
                  MOVE RCT00017-RCVR-SEQ-NBR  TO RC090-RCVR-SEQ-NBR     03252000
                  MOVE RCT00017-CRE-BY-USR-ID TO RC090-CRE-BY-USR-ID    03253000
                  MOVE RCT00017-CRE-TMST      TO RC090-CRE-TMST         03254000
                  MOVE RCT00017-DC-FUNC-CDE   TO RC090-DC-FUNC-CDE      03255000
                  MOVE RCT00017-LOC-NBR       TO RC090-LOC-NBR          03256000
                  MOVE RCT00017-PO-LNE-NBR    TO RC090-PO-LNE-NBR       03257000
                  MOVE RCT00017-PREPK-ID      TO RC090-PREPK-ID         03258000
                  MOVE RCT00017-CRE-BY-PGM-NM TO RC090-CRE-BY-PGM-NM    03259000
                  MOVE RCT00017-QTY-TRKD-NBR  TO RC090-QTY-TRKD-NBR     03259100
                  MOVE RCT00017-LAST-UPD-TMST TO RC090-LAST-UPD-TMST    03259200
                  MOVE RCT00017-PKSL-SEQ-NBR  TO RC090-PKSL-SEQ-NBR     03259300
                  MOVE RCT00017-DEST-LOC-NBR  TO RC090-DEST-LOC-NBR     03259400
                  PERFORM 8210-WRITE-EXTRACT-RECORD                     03259900
               END-IF                                                   03260000
             WHEN SQLCODE = +100                                        03261000
                  SET PS-EOF-RCVR-DATA-CSR TO TRUE                      03270000
             WHEN SQLWARN0 NOT = SPACES                                 03280000
             WHEN SQLCODE < ZERO                                        03290000
             WHEN SQLCODE > ZERO                                        03300000
                 MOVE '8200-FETCH-RCVR-DATA-CSR'                        03310000
                                     TO PV-PARAGRAPH-NAME               03320000
                 MOVE 'FETCH RCVR-DATA-CSR CURSOR '                     03330000
                                     TO PV-DB2-OPERATION-ATTEMPTED      03340000
                 PERFORM 9000-SQL-ABEND                                 03350000
           END-EVALUATE.                                                03360000
      *                                                                 03370000
      ******************************************************************03371000
      **  THIS PARAGRAPH WRITES THE RECORDS IN TO THE OUTPUT FILE     **03372000
      **  IN AHRD002 LAYOUT.                                          **03373000
      ******************************************************************03374000
      *                                                                 03375000
       8210-WRITE-EXTRACT-RECORD.                                       03376000
      *                                                                 03377000
            WRITE RCVR-PURGE-REC                                        03378000
             FROM RC090-PO-RCVR-TRKG-DELETE.                            03379000
              ADD 1 TO PV-RCVR-TOTAL-CNT.                               03379100
      *                                                                 03379200
      ******************************************************************03379300
      **  THIS PARAGRAPH CLOSES THE CURSOR WHICH WILL CONTAIN THE PO  **03379400
      **  ORDERS THAT CAN HAVE THE DATA WHICH CAN BE PURGED.          **03379500
      ******************************************************************03379600
      *                                                                 03379700
       8300-CLOSE-RCVR-DATA-CSR.                                        03379800
      *                                                                 03379900
           EXEC SQL                                                     03380000
               CLOSE RCVR-DATA-CSR                                      03380100
           END-EXEC.                                                    03380200
      *                                                                 03380300
           EVALUATE TRUE                                                03380400
               WHEN SQLCODE = ZERO                                      03380500
               WHEN SQLCODE = -904                                      03380600
               WHEN SQLCODE = -911                                      03380700
                    CONTINUE                                            03380800
               WHEN SQLWARN0 NOT = SPACES                               03380900
               WHEN SQLCODE < ZERO                                      03381000
               WHEN SQLCODE > ZERO                                      03381100
                    MOVE '8300-CLOSE-RCVR-DATA-CSR'                     03381200
                                    TO PV-PARAGRAPH-NAME                03381300
                    MOVE 'CLOSE THE RCVR-DATA-CSR              '        03381400
                                    TO PV-DB2-OPERATION-ATTEMPTED       03381500
                    PERFORM 9000-SQL-ABEND                              03381600
           END-EVALUATE.                                                03381700
      *                                                                 03381800
      ******************************************************************03381900
      **  THIS PARAGRAPH OPENS THE CURSOR WHICH WILL CONTAIN THE NO   **03382000
      **  OF PO ORD THAT CAN BEOLD ENOUGH AND CAN BE PURGED.          **03383000
      ******************************************************************03384000
      *                                                                 03384100
       8400-OPEN-RCVR-CNTL-DATA-CSR.                                    03384200
      *                                                                 03384300
           EXEC SQL                                                     03384400
                OPEN RCVR-CNTL-DATA-CSR                                 03384500
           END-EXEC.                                                    03384600
      *                                                                 03384700
           EVALUATE TRUE                                                03384800
               WHEN SQLCODE = 0                                         03384900
                   CONTINUE                                             03385000
               WHEN SQLWARN0 NOT = SPACES                               03385100
               WHEN SQLCODE < ZERO                                      03385200
               WHEN SQLCODE > ZERO                                      03385300
                   MOVE '8400-OPEN-RCVR-CNTL-DATA-CSR'                  03385400
                                   TO PV-PARAGRAPH-NAME                 03385500
                   MOVE 'OPEN THE RCVR-CNTL-DATA-CSR        '           03385600
                                   TO PV-DB2-OPERATION-ATTEMPTED        03385700
                   PERFORM 9000-SQL-ABEND                               03385800
           END-EVALUATE.                                                03385900
      *                                                                 03386000
      ******************************************************************03386100
      **  THIS PARAGRAPH FETCHES THE CURSOR WHICH WILL CONTAIN THE PO **03386200
      **  ORDERS THAT CAN HAVE THE DATA WHICH CAN BE PURGED.          **03386300
      ******************************************************************03386400
      *                                                                 03386500
       8500-FETCH-RCVR-CNTL-DATA-CSR.                                   03386600
      *                                                                 03386700
           EXEC SQL                                                     03386800
                FETCH RCVR-CNTL-DATA-CSR                                03386900
                INTO  :PV-PO-NBR                                        03387000
                   ,  :PV-RCVR-SEQ-NBR                                  03387100
                   ,  :PV-PO-LINE-NBR                                   03387200
                   ,  :PV-PREPK-ID                                      03387300
                   ,  :PV-DC-FUNC-CDE                                   03387400
                   ,  :PV-CSR-REC-COUNT                                 03387500
           END-EXEC.                                                    03388300
      *                                                                 03388400
           EVALUATE TRUE                                                03388500
             WHEN SQLCODE = ZEROS                                       03388600
             WHEN SQLCODE = +100                                        03391900
                  SET PS-EOF-RCVR-CNTL-DATA-CSR TO TRUE                 03392000
             WHEN SQLWARN0 NOT = SPACES                                 03392100
             WHEN SQLCODE < ZERO                                        03392200
             WHEN SQLCODE > ZERO                                        03392300
                 MOVE '8500-FETCH-RCVR-CNTL-DATA-CSR'                   03392400
                                     TO PV-PARAGRAPH-NAME               03392500
                 MOVE 'FETCH RCVR-CNTL-DATA-CSR CURSOR '                03392600
                                     TO PV-DB2-OPERATION-ATTEMPTED      03392700
                 PERFORM 9000-SQL-ABEND                                 03392800
           END-EVALUATE.                                                03392900
      *                                                                 03393000
      ******************************************************************03394200
      **  THIS PARAGRAPH CLOSES THE CURSOR WHICH WILL CONTAIN THE PO  **03394300
      **  ORDERS THAT CAN HAVE THE DATA WHICH CAN BE PURGED.          **03394400
      ******************************************************************03394500
      *                                                                 03394600
       8600-CLOSE-RCVR-CNTL-DATA-CSR.                                   03394700
      *                                                                 03394800
           EXEC SQL                                                     03394900
               CLOSE RCVR-CNTL-DATA-CSR                                 03395000
           END-EXEC.                                                    03395100
      *                                                                 03395200
           EVALUATE TRUE                                                03395300
               WHEN SQLCODE = ZERO                                      03395400
               WHEN SQLCODE = -904                                      03395500
               WHEN SQLCODE = -911                                      03395600
                    CONTINUE                                            03395700
               WHEN SQLWARN0 NOT = SPACES                               03395800
               WHEN SQLCODE < ZERO                                      03395900
               WHEN SQLCODE > ZERO                                      03396000
                    MOVE '8600-CLOSE-RCVR-CNTL-DATA-CSR'                03396100
                                    TO PV-PARAGRAPH-NAME                03396200
                    MOVE 'CLOSE THE RCVR-CNTL-DATA-CSR'                 03396300
                                    TO PV-DB2-OPERATION-ATTEMPTED       03396400
                    PERFORM 9000-SQL-ABEND                              03396500
           END-EVALUATE.                                                03396600
      *                                                                 03396700
      ******************************************************************03396800
      *  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL   *03397000
      *  ERROR OR WARNING CODE OCCURS.                                 *03400000
      ******************************************************************03410000
      *                                                                 03420000
       9000-SQL-ABEND.                                                  03430000
      *                                                                 03440000
           DISPLAY '9000-SQL-ABEND'.                                    03450000
           DISPLAY '** ABEND     **'.                                   03460000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        03470000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              03480000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.     03490000
      *                                                                 03500000
      ******************************************************************03510000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *03520000
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *03530000
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *03540000
      * FORMAT.                                                        *03550000
      ******************************************************************03560000
      *                                                                 03570000
           COPY DPPD004.                                                03580000
      *                                                                 03590000
           MOVE +4013         TO ABEND-CODE.                            03600000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03610000
      *                                                                 03620000
