      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.      PEKUP081.                                               
       AUTHOR.          KARI SUTTON.                                            
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    NOVEMBER 1997.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM READS A PRICE CHANGE FILE THAT HAS BEEN SORTED    *        
      * BY:  MONTH, YEAR, DEPT., WEEK, CATEGORY CODE, MARKDOWN/UP TYPE,*        
      *      AND MARKDOWN/UP REASON                                    *        
      * AND SUMMED ON:  THE EXTENDED RETAIL AMOUNT (MARKDOWN/UP AMT)   *        
      *                 AND THE QUANTITY                               *        
      *                                                                *        
      * THESE RECORDS ARE THEN INSERTED INTO TWKMUMD.                  *        
      ******************************************************************        
      * 10/28/03 - MOYER    WR NO. WR0024472                           *        
      *    - RECOMPILE FOR MLRD023 CHANGE OVER A YEAR AGO              *        
      ******************************************************************        
      * 05/12/04 - TODD C. MICHALEK -  WR NO. WR26602                  *        
      *    - RECOMPILE PROGRAM FOR EXPANDED STORE NUMBER IN MLRD023    *        
W26606* 01/24/06 - JOHN SELWITSCHKA -  WR NO. WR28582                  *        
W26606*    - PRICE CHANGE COPYBOOKS EXPANSION.                         *        
W26606*      MODIFIED PRICE-CHANGE-FILE LRECL.                         *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT PRICE-CHANGE-FILE                                             
               ASSIGN                       TO INP01.                           
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PRICE-CHANGE-FILE                                                    
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
W26606 01  PRICE-CHANGE-RECORD          PIC X(130).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-JOB-NAME                  PIC X(08)      VALUE                
                                                           'PEK151  '.          
           05  PC-PROGRAM-NAME              PIC X(08)      VALUE                
                                                           'PEKUP081'.          
           05  PC-MAX-RETRIES               PIC S9(4)      VALUE +3             
                                                           COMP SYNC.           
                                                                                
      ******************************************************************        
      * PV PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME            PIC X(30)      VALUE SPACES.        
           05  PV-DB2-OPERATION-ATTEMPTED   PIC X(30)      VALUE SPACES.        
           05  PV-RETRY-COUNTER             PIC S9(4)      VALUE ZERO           
                                                           COMP SYNC.           
           05  PV-SYSTEM-DATE               PIC X(10)      VALUE SPACES.        
           05  PV-COMMIT-TIMESTAMP          PIC X(26)      VALUE SPACES.        
           05  PV-CURRENT-TIMESTAMP         PIC X(26)      VALUE SPACES.        
           05  PV-FSCL-WK.                                                      
               10  FILLER                   PIC X(01)      VALUE '0'.           
               10  PV-FSCL-WK-PART2         PIC X(01)      VALUE SPACES.        
                                                                                
      ******************************************************************        
      * CR CHECKPOINT RESTART VARIABLES                                *        
      ******************************************************************        
       01  CR-CHECKPOINT-RESTART-AREA.                                          
           05  CR-AREA-LEN                  PIC S9(04)     VALUE +125           
                                                           COMP.                
           05  CR-CHECKPOINT-RESTART-VAR.                                       
               10  CR-ML023-PC-RECORD       PIC X(110)     VALUE SPACES.        
               10  CR-COMMIT-COUNT          PIC 9(09)      VALUE ZEROS.         
               10  CR-TWKMUMD-ROWS-INSERTED PIC S9(11)     VALUE ZEROS          
                                                           COMP-3.              
                                                                                
      ******************************************************************        
      * PS PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-PRCHG-FILE-SW      PIC X(01)      VALUE 'N'.           
               88  PS-END-OF-PRCHG-FILE                    VALUE 'Y'.           
           05  PS-FIRST-COMMIT-SW           PIC X(01)      VALUE 'N'.           
               88  PS-FIRST-COMMIT                         VALUE 'Y'.           
                                                                                
      ******************************************************************        
      * PA PROGRAM ACCUMULATORS                                        *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-PRCHG-FILE-RECS-READ      PIC S9(11)     COMP-3               
                                                           VALUE ZEROES.        
           05  PA-TWKMUMD-ROWS-INSERTED     PIC S9(11)     COMP-3               
                                                           VALUE ZEROES.        
           05  PA-NBR-ROWS-TO-COMMIT        PIC S9(05)     COMP-3               
                                                           VALUE ZEROES.        
                                                                                
      ******************************************************************        
      *    MERCHANDISE LEDGER PRICE CHANGE RECORD                      *        
      ******************************************************************        
W26606     COPY MLRD023.                                                        
                                                                                
      ******************************************************************        
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *    PE-DEPARTMENT MARKUP/MARKDOWN TABLE                         *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TWKMUMD                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  COBOL DECLARATIONS FOR THE CHECKPOINT RESTART CONTROL TABLE            
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  COBOL DECLARATIONS FOR THE CHECKPOINT RESTART INFO TABLE               
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    SQL COMMUNICATIONS AREA DCLGEN                              *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
      * AC ABEND CODES                                                 *        
      ******************************************************************        
       01  ABEND-CODE                       PIC S9(04)     VALUE ZEROES         
                                                           COMP                 
                                                           SYNC.                
           88  AC-ABEND-4013-DB2-ERROR                     VALUE +4013.         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       A100-MAINLINE.                                                           
      ******************************************************************        
                                                                                
           PERFORM B100-INITIALIZE.                                             
                                                                                
           PERFORM B200-PROCESS-PRCHG-RECORDS                                   
               UNTIL PS-END-OF-PRCHG-FILE.                                      
                                                                                
           PERFORM B300-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
      *     B100-INITIALIZE                                            *        
      * ==> PROCESSES:                                                 *        
      *     - OPENS THE PRICE CHANGE FILE                              *        
      *     - READS THE FIRST RECORD FROM THE PRICE CHANGE FILE        *        
      * ==> PERFORMED FROM: A100-MAINLINE                              *        
      ******************************************************************        
       B100-INITIALIZE.                                                         
                                                                                
           PERFORM C100-INIT-CHECKPOINT-SELECT.                                 
                                                                                
      ******************************************************************        
      *     IF PROGRAM IS IN A RESTART STATUS.  SELECT SAVED DATA FROM *        
      *     THE RESTART INFO TABLE.                                    *        
      ******************************************************************        
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
               PERFORM C200-SELECT-RESTART-INFO                                 
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                            
                                  CR-CHECKPOINT-RESTART-VAR                     
               MOVE CR-TWKMUMD-ROWS-INSERTED                                    
                                        TO PA-TWKMUMD-ROWS-INSERTED             
           ELSE                                                                 
               SET PS-FIRST-COMMIT TO TRUE                                      
           END-IF.                                                              
                                                                                
           PERFORM D100-GET-COMMIT-FREQUENCY.                                   
                                                                                
           OPEN INPUT  PRICE-CHANGE-FILE.                                       
                                                                                
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
               PERFORM C300-READ-PRICE-CHANGE-FILE                              
                   UNTIL ML023-PC-RECORD = CR-ML023-PC-RECORD                   
           END-IF.                                                              
                                                                                
           PERFORM C300-READ-PRICE-CHANGE-FILE.                                 
                                                                                
           IF PS-END-OF-PRCHG-FILE                                              
               DISPLAY ' '                                                      
               DISPLAY 'MESSAGES FOR PROGRAM:  ' PC-PROGRAM-NAME                
               DISPLAY 'PRICE CHANGE FILE IS EMPTY'                             
               DISPLAY ' '                                                      
           ELSE                                                                 
               EXEC SQL                                                         
                   SET :PV-SYSTEM-DATE = CURRENT DATE                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       CONTINUE                                                 
                                                                                
                   WHEN OTHER                                                   
                       MOVE 'B100-INITIALIZE        '                           
                                        TO PV-PARAGRAPH-NAME                    
                       MOVE 'SELECTING CURRENT DATE    '                        
                                        TO PV-DB2-OPERATION-ATTEMPTED           
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
                                                                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *     B200-PROCESS-PRCHG-RECORDS                                 *        
      * ==> PROCESSES:                                                 *        
      *     - POPULATES AND INSERTS COLUMNS INTO TWKMUMD               *        
      *     - EXECUTES A COMMIT ON TWKMUMD IF THE LOGICAL FREQUENCY    *        
      *       QUANTITY IS MET                                          *        
      *     - READS THE NEXT PRICE CHANGE RECORD                       *        
      * ==> PERFORMED FROM: A100-MAINLINE                              *        
      ******************************************************************        
       B200-PROCESS-PRCHG-RECORDS.                                              
                                                                                
           MOVE ML023-PC-FSCL-PER      TO WKMUMD-FSCL-MN.                       
           MOVE ML023-PC-FSCL-YR       TO WKMUMD-FSCL-YR.                       
           MOVE ML023-PC-DEPT-NBR      TO WKMUMD-DEPT-NBR.                      
                                                                                
           MOVE ML023-PC-FSCL-WK       TO PV-FSCL-WK-PART2.                     
           MOVE PV-FSCL-WK             TO WKMUMD-FSCL-WK.                       
                                                                                
           MOVE ML023-PC-CATEGORY      TO WKMUMD-ORGN-CDE.                      
           MOVE ML023-PC-TYP-CDE       TO WKMUMD-PRIC-CHG-TYP-CDE.              
           MOVE ML023-PC-MKDN-MKUP-RSN-CDE                                      
                                       TO WKMUMD-RSN-CDE.                       
           MOVE ML023-PC-EXTD-RTL-AMT  TO WKMUMD-TOT-MU-MD-AMT.                 
           MOVE ML023-PC-QTY           TO WKMUMD-TOT-UNT-MU-MD-QTY.             
           MOVE PV-SYSTEM-DATE         TO WKMUMD-CRE-DTE.                       
                                                                                
           PERFORM C400-INSERT-TWKMUMD-ROW.                                     
                                                                                
           PERFORM C500-COMMIT-PROCESSING.                                      
                                                                                
           PERFORM C300-READ-PRICE-CHANGE-FILE.                                 
                                                                                
      ******************************************************************        
      *     B300-EOJ-ROUTINE                                           *        
      * ==> PROCESSES:                                                 *        
      *     - CLOSES THE PRICE-CHANGE-FILE.                            *        
      *     - DISPLAYS THE NUMBER OF PRICE CHANGE RECORDS READ AND     *        
      *       THE NUMBER OF TWKMUMD ROWS INSERTED                      *        
      * ==> PERFORMED FROM: A100-MAINLINE                              *        
      ******************************************************************        
       B300-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE PRICE-CHANGE-FILE.                                             
                                                                                
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                               
           PERFORM D950-UPDATE-CHECKPOINT-STATUS.                               
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'TOTAL NUMBER OF PRICE CHANGE RECORDS READ:  '               
                    PA-PRCHG-FILE-RECS-READ.                                    
                                                                                
           DISPLAY 'TOTAL NUMBER OF TWKMUMD ROW INSERTED     :  '               
                    PA-TWKMUMD-ROWS-INSERTED.                                   
                                                                                
           DISPLAY 'NUMBER OF COMMITS TAKEN:  '  CR-COMMIT-COUNT.               
           DISPLAY ' '.                                                         
                                                                                
      ******************************************************************        
      *      C100-INIT-CHECKPOINT-SELECT                               *        
      *  ==> PROCESSES:                                                *        
      *    - SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE TO SEE  *        
      *      IF WE ARE IN A RESTART CONDITION.                         *        
      *  ==> PERFORMED FROM: B100-INITIALIZE                           *        
      ******************************************************************        
       C100-INIT-CHECKPOINT-SELECT.                                             
                                                                                
           EXEC SQL                                                             
             SELECT  CKPT_STAT_CODE                                             
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                                 
               FROM  TCKRSTCNTL                                                 
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'C100-INIT-CHECKPOINT-SELECT     '                          
                                   TO PV-PARAGRAPH-NAME                         
               MOVE 'ERROR ON SELECT OF TCKRSTCNTL '                            
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *      C200-SELECT-RESTART-INFO                                  *        
      *  ==> PROCESSES:                                                *        
      *    - SELECTS FROM THE CHECKPOINT RESTART INFORMATION TABLE TO  *        
      *      OBTAIN THE PROGRAM'S SAVED VARIABLES.                     *        
      *  ==> PERFORMED FROM: B100-INITIALIZE                           *        
      ******************************************************************        
       C200-SELECT-RESTART-INFO.                                                
                                                                                
           EXEC SQL                                                             
             SELECT  WORK_STRG_DESC                                             
               INTO  :TCKRSTINF-WORK-STRG-DESC                                  
               FROM  TCKRSTINF                                                  
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'C200-SELECT-RESTART-INFO        '                          
                                   TO PV-PARAGRAPH-NAME                         
               MOVE 'ERROR ON SELECT OF TCKRSTINF  '                            
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *     C300-READ-PRICE-CHANGE-FILE                                *        
      * ==> PROCESSES:                                                 *        
      *     - READS A RECORD FROM THE PRICE CHANGE FILE                *        
      *     - COUNTS THE NUMBER OF RECORDS READ                        *        
      * ==> PERFORMED FROM: B100-INITIALIZE                            *        
      *                     B200-PROCESS-PRCHG-RECORDS                 *        
      ******************************************************************        
       C300-READ-PRICE-CHANGE-FILE.                                             
                                                                                
           READ PRICE-CHANGE-FILE INTO ML023-PC-RECORD                          
               AT END                                                           
                   SET PS-END-OF-PRCHG-FILE TO TRUE.                            
                                                                                
           IF PS-END-OF-PRCHG-FILE                                              
               CONTINUE                                                         
           ELSE                                                                 
               ADD +1     TO PA-PRCHG-FILE-RECS-READ                            
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *     C400-INSERT-TWKMUMD-ROW                                    *        
      * ==> PROCESSES:                                                 *        
      *     - INSERTS ROWS INTO TWKMUMD                                *        
      * ==> PERFORMED FROM: B200-PROCESS-PRCHG-RECORDS                 *        
      ******************************************************************        
       C400-INSERT-TWKMUMD-ROW.                                                 
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL (PV-RETRY-COUNTER > PC-MAX-RETRIES                         
                      OR SQLCODE = ZERO)                                        
                                                                                
               EXEC SQL                                                         
                   INSERT INTO TWKMUMD                                          
                      (FSCL_MN                                                  
                     , FSCL_YR                                                  
                     , DEPT_NBR                                                 
                     , FSCL_WK                                                  
                     , ORGN_CDE                                                 
                     , PRIC_CHG_TYP_CDE                                         
                     , RSN_CDE                                                  
                     , TOT_MU_MD_AMT                                            
                     , TOT_UNT_MU_MD_QTY                                        
                     , CRE_DTE)                                                 
                   VALUES                                                       
                      (:WKMUMD-FSCL-MN                                          
                     , :WKMUMD-FSCL-YR                                          
                     , :WKMUMD-DEPT-NBR                                         
                     , :WKMUMD-FSCL-WK                                          
                     , :WKMUMD-ORGN-CDE                                         
                     , :WKMUMD-PRIC-CHG-TYP-CDE                                 
                     , :WKMUMD-RSN-CDE                                          
                     , :WKMUMD-TOT-MU-MD-AMT                                    
                     , :WKMUMD-TOT-UNT-MU-MD-QTY                                
                     , :WKMUMD-CRE-DTE)                                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       ADD +1               TO PA-TWKMUMD-ROWS-INSERTED         
                       ADD +1               TO PA-NBR-ROWS-TO-COMMIT            
                                                                                
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                                                                                
                   WHEN OTHER                                                   
                       MOVE 'C400-INSERT-TWKMUMD-ROW'                           
                                        TO PV-PARAGRAPH-NAME                    
                       MOVE 'INSERT MARKUP/MARKDOWN ROW'                        
                                        TO PV-DB2-OPERATION-ATTEMPTED           
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                                 
               MOVE 'C400-INSERT-TWKMUMD-ROW'                                   
                                        TO PV-PARAGRAPH-NAME                    
               MOVE 'INSERT MARKUP/MARKDOWN ROW'                                
                                        TO PV-DB2-OPERATION-ATTEMPTED           
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *      C500-COMMIT-PROCESSING.                                   *        
      *  ==> PROCESSES:                                                *        
      *    - CHECK IF WE NEED TO TAKE A COMMIT.                        *        
      *    - COMMIT THIS UNIT OF WORK.                                 *        
      *  ==> PERFORMED FROM: B200-PROCESS-DOCUMENTS.                   *        
      ******************************************************************        
       C500-COMMIT-PROCESSING.                                                  
                                                                                
           IF PA-NBR-ROWS-TO-COMMIT = TCKRSTCNTL-CKPT-FRQNCY-QTY                
               MOVE ML023-PC-RECORD       TO CR-ML023-PC-RECORD                 
               IF PS-FIRST-COMMIT                                               
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                        
                   PERFORM D950-UPDATE-CHECKPOINT-STATUS                        
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                               
               END-IF                                                           
               PERFORM D900-COMMIT                                              
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *      D100-GET-COMMIT-FREQUENCY.                                *        
      *  ==> PROCESSES:                                                *        
      *    - GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT CONTROL      *        
      *      TABLE.                                                    *        
      *  ==> PERFORMED FROM: B100-INITIALIZE                           *        
      ******************************************************************        
       D100-GET-COMMIT-FREQUENCY.                                               
                                                                                
           EXEC SQL                                                             
             SELECT  CKPT_FRQNCY_QTY                                            
               INTO :TCKRSTCNTL-CKPT-FRQNCY-QTY                                 
               FROM TCKRSTCNTL                                                  
              WHERE JOB_NAME  = :PC-JOB-NAME                                    
                AND PLAN_NAME = :PC-PROGRAM-NAME                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'D100-GET-COMMIT-FREQUENCY       '                          
                                   TO PV-PARAGRAPH-NAME                         
               MOVE 'ERROR ON SELECT OF TIMESTAMP FREQUENCY'                    
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *      D900-COMMIT.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.           *        
      *    - TAKE A COMMIT.                                            *        
      *  ==> PERFORMED FROM: C500-COMMIT-PROCESSING.                   *        
      ******************************************************************        
       D900-COMMIT.                                                             
                                                                                
           MOVE CR-CHECKPOINT-RESTART-AREA TO TCKRSTINF-WORK-STRG-DESC.         
                                                                                
           EXEC SQL                                                             
             UPDATE TCKRSTINF                                                   
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
              WHERE JOB_NAME       = :PC-JOB-NAME                               
                AND PLAN_NAME      = :PC-PROGRAM-NAME                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'D900-COMMIT                     '                          
                                   TO PV-PARAGRAPH-NAME                         
               MOVE 'ERROR ON UPDATE OF TCKRSTINF  '                            
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               ADD +1    TO CR-COMMIT-COUNT                                     
               MOVE ZERO TO PA-NBR-ROWS-TO-COMMIT                               
           ELSE                                                                 
               MOVE 'D900-COMMIT                     '                          
                                   TO PV-PARAGRAPH-NAME                         
               MOVE 'ERROR ON COMMIT               '                            
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *      D950-UPDATE-CHECKPOINT-STATUS                             *        
      *  ==> PROCESSES:                                                *        
      *    - UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE        *        
      *  ==> PERFORMED FROM: C500-COMMIT-PROCESSING                    *        
      ******************************************************************        
       D950-UPDATE-CHECKPOINT-STATUS.                                           
                                                                                
           EXEC SQL                                                             
             UPDATE  TCKRSTCNTL                                                 
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'D950-UPDATE-CHECKPOINT-STATUS   '                          
                                   TO PV-PARAGRAPH-NAME                         
               MOVE 'ERROR ON UPDATE OF TCKRSTCNTL '                            
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *     Z900-SQL-ABEND                                             *        
      * ==> PROCESSES:                                                 *        
      *     - PERFORMS ABEND PROCESSING FOR SQL ERRORS.                *        
      * ==> PERFORMED FROM: B300-EOJ-ROUTINE                           *        
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           DISPLAY 'Z900-SQL-ABEND'.                                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPERATION-ATTEMPTED.           
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
      * FORMAT.                                                        *        
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           SET AC-ABEND-4013-DB2-ERROR TO TRUE.                                 
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
