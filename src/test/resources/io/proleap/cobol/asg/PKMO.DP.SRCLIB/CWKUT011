      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.     CWKUT011.                                                
       AUTHOR.         THOMAS DUFFY.                                            
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   05/20/03.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  OTHER LOCATION COMMON EXTRACT PROGRAM                                  
      *  THIS PROGRAM CREATES A CHANGE EXTRACT FILE BY COMPARING                
      *  TWO GENERATIONS OF AN RMS WAREHOUSE COMMON EXTRACT FILE.               
      ******************************************************************        
TD0804*    CHANGED: 08/20/04    BY: THOMAS DUFFY                                
TD0804*      LOCATION DIMENSION - CHANGE LRECL FOR INPUT RECORDS                
TD0804*               FROM 195 TO 194, AND OUTPUT RECORD FROM                   
TD0804*               194 TO 195 TO USE V2 OF LOCATION EXTRACTS.                
TD0804*               ADDRESS OUTPUT DECREASED TO 97.                           
TD0804******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CW-CURRENT-FILE                                               
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CW-PREVIOUS-FILE                                              
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT CW-CHANGE-FILE                                                
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT CW-ADDRESS-FILE                                               
               ASSIGN TO OUT02.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CW-CURRENT-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-CURRENT-RECORD                  PIC X(194).                       
                                                                                
       FD  CW-PREVIOUS-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-PREVIOUS-RECORD                 PIC X(194).                       
                                                                                
       FD  CW-CHANGE-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-CHANGE-RECORD                   PIC X(195).                       
                                                                                
       FD  CW-ADDRESS-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-ADDRESS-RECORD                  PIC X(97).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CURRENT-REC.                                                         
           05  CR-FILLER-1                    PIC   X(01).                      
           05  CR-CURR-KEY                    PIC   X(04).                      
           05  CR-CURR-DATA                   PIC   X(189).                     
           05  CR-ADDR-DATA  REDEFINES CR-CURR-DATA.                            
               10  CAD-FILLER-1               PIC   X(51).                      
               10  CAD-ADDRESS                PIC   X(92).                      
               10  CAD-FILLER-2               PIC   X(46).                      
                                                                                
       01  PREVIOUS-REC.                                                        
           05  PR-FILLER-1                    PIC   X(01).                      
           05  PR-PREV-KEY                    PIC   X(04).                      
           05  PR-PREV-DATA                   PIC   X(189).                     
           05  PR-ADDR-DATA  REDEFINES PR-PREV-DATA.                            
               10  PAD-FILLER-1               PIC   X(51).                      
               10  PAD-ADDRESS                PIC   X(92).                      
               10  PAD-FILLER-2               PIC   X(46).                      
                                                                                
       01  CHANGE-OUTPUT-REC.                                                   
           05  COR-FILLER-1                   PIC   X(01).                      
           05  COR-CHANGE-KEY                 PIC   X(04).                      
           05  COR-CHANGE-DATA                PIC   X(189).                     
           05  COR-CHANGE-INDICATOR           PIC   X(01).                      
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08) VALUE 'CWKUT011'.            
           05  PC-OPERCO                 PIC X(02) VALUE '03'.                  
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORDS          PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-PRE-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-CUR-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-OUTPUT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-INSERT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-UPDATE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-DELETE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-NO-CHANGE-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-ADDR-OUTPUT-RECORDS    PIC  9(9)       VALUE ZERO.            
           05  PA-ADDR-INSERT-RECORDS    PIC  9(9)       VALUE ZERO.            
           05  PA-ADDR-UPDATE-RECORDS    PIC  9(9)       VALUE ZERO.            
           05  PA-ADDR-DELETE-RECORDS    PIC  9(9)       VALUE ZERO.            
                                                                                
      ******************************************************************        
      * OUTPUT FILE LAYOUT FOR CW ADDRESS EXTRACT FILE                          
      ******************************************************************        
           COPY CWRD009.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-MAIN-ROUTINE                                            
               UNTIL CR-CURR-KEY = HIGH-VALUES                                  
                     AND                                                        
                     PR-PREV-KEY = HIGH-VALUES.                                 
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  INITIALIZE                                                             
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  CW-CURRENT-FILE                                          
                       CW-PREVIOUS-FILE                                         
                OUTPUT CW-CHANGE-FILE                                           
                       CW-ADDRESS-FILE.                                         
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 8000-READ-CURRENT.                                           
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      * MAIN PROCESSING.                                                        
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
           INITIALIZE CHANGE-OUTPUT-REC                                         
                      CWRD009-RECORD.                                           
                                                                                
           IF CR-CURR-KEY  =  PR-PREV-KEY                                       
               PERFORM 2100-CHECK-EQUALITY                                      
           ELSE                                                                 
               IF CR-CURR-KEY  <  PR-PREV-KEY                                   
                   MOVE 'I'           TO COR-CHANGE-INDICATOR                   
                                         CW009-ADDR-CHNGE-IND                   
                   MOVE CR-CURR-KEY   TO COR-CHANGE-KEY                         
                                         CW009-ADDR-NBR                         
                   MOVE CR-FILLER-1   TO COR-FILLER-1                           
                   MOVE CR-CURR-DATA  TO COR-CHANGE-DATA                        
                   MOVE CAD-ADDRESS   TO CW009-ADDRESS-DATA                     
                   ADD +1             TO PA-INSERT-RECORDS                      
                   ADD +1             TO PA-ADDR-INSERT-RECORDS                 
                   PERFORM 7100-WRITE-CHANGE                                    
                   PERFORM 7200-WRITE-ADDRESS                                   
                   PERFORM 8000-READ-CURRENT                                    
               ELSE                                                             
                   MOVE 'D'           TO COR-CHANGE-INDICATOR                   
                                         CW009-ADDR-CHNGE-IND                   
                   MOVE PR-PREV-KEY   TO COR-CHANGE-KEY                         
                                         CW009-ADDR-NBR                         
                   MOVE PR-FILLER-1   TO COR-FILLER-1                           
                   MOVE PR-PREV-DATA  TO COR-CHANGE-DATA                        
                   MOVE PAD-ADDRESS   TO CW009-ADDRESS-DATA                     
                   ADD +1             TO PA-DELETE-RECORDS                      
                   ADD +1             TO PA-ADDR-DELETE-RECORDS                 
                   PERFORM 7100-WRITE-CHANGE                                    
                   PERFORM 7200-WRITE-ADDRESS                                   
                   PERFORM 8100-READ-PREVIOUS                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CHECKS RECORD FOR EQUALITY                                             
      *  IF DATA IS NOT EQUAL, THEN RECORD IS AN UPDATE                         
      ******************************************************************        
       2100-CHECK-EQUALITY.                                                     
           IF CAD-FILLER-1    =  PAD-FILLER-1   AND                             
              CAD-FILLER-2    =  PAD-FILLER-2                                   
               ADD +1                 TO PA-NO-CHANGE-RECORDS                   
               PERFORM 2200-ADDRESS-CHECK                                       
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
           ELSE                                                                 
               MOVE 'U'               TO COR-CHANGE-INDICATOR                   
               MOVE CR-CURR-KEY       TO COR-CHANGE-KEY                         
               MOVE CR-FILLER-1       TO COR-FILLER-1                           
               MOVE CR-CURR-DATA      TO COR-CHANGE-DATA                        
               ADD +1                 TO PA-UPDATE-RECORDS                      
               PERFORM 7100-WRITE-CHANGE                                        
               PERFORM 2200-ADDRESS-CHECK                                       
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CHECKS ADDRESS FOR EQUALITY                                            
      *  IF DATA IS NOT EQUAL, THEN RECORD IS AN UPDATE                         
      ******************************************************************        
       2200-ADDRESS-CHECK.                                                      
           IF CAD-ADDRESS  =  PAD-ADDRESS                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE  'U'                TO CW009-ADDR-CHNGE-IND                 
               MOVE CAD-ADDRESS         TO CW009-ADDRESS-DATA                   
               MOVE CR-CURR-KEY         TO CW009-ADDR-NBR                       
               PERFORM 7200-WRITE-ADDRESS                                       
               ADD +1                   TO PA-ADDR-UPDATE-RECORDS               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  WRITE THE CHANGE RECORD                                                
      ******************************************************************        
       7100-WRITE-CHANGE.                                                       
           WRITE  CW-CHANGE-RECORD FROM CHANGE-OUTPUT-REC.                      
           ADD +1            TO PA-OUTPUT-RECORDS.                              
                                                                                
      ******************************************************************        
      *  WRITE THE ADDRESS RECORD                                               
      ******************************************************************        
       7200-WRITE-ADDRESS.                                                      
           WRITE  CW-ADDRESS-RECORD FROM CWRD009-RECORD.                        
           ADD +1            TO PA-ADDR-OUTPUT-RECORDS.                         
                                                                                
      ******************************************************************        
      *  READ THE CURRENT FILE.                                        *        
      ******************************************************************        
       8000-READ-CURRENT.                                                       
           READ CW-CURRENT-FILE                                                 
               INTO CURRENT-REC                                                 
               AT END                                                           
                   MOVE HIGH-VALUES  TO  CR-CURR-KEY                            
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-CUR-RECORDS                     
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *  READ THE PREVIOUS FILE.                                                
      ******************************************************************        
       8100-READ-PREVIOUS.                                                      
           READ CW-PREVIOUS-FILE                                                
               INTO PREVIOUS-REC                                                
               AT END                                                           
                   MOVE HIGH-VALUES  TO  PR-PREV-KEY                            
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-PRE-RECORDS                     
           END-READ.                                                            
                                                                                
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
           DISPLAY '   '.                                                       
                                                                                
           DISPLAY 'INPUT RECS READ    : ' PA-INPUT-RECORDS.                    
           DISPLAY 'INPUT PREVIOUS RECS: ' PA-INPUT-PRE-RECORDS.                
           DISPLAY 'INPUT CURRENT RECS : ' PA-INPUT-CUR-RECORDS.                
           DISPLAY '   '.                                                       
           DISPLAY 'OUTPUT RECS WRITTEN: ' PA-OUTPUT-RECORDS.                   
           DISPLAY 'INSERT RECS WRITTEN: ' PA-INSERT-RECORDS.                   
           DISPLAY 'UPDATE RECS WRITTEN: ' PA-UPDATE-RECORDS.                   
           DISPLAY 'DELETE RECS WRITTEN: ' PA-DELETE-RECORDS.                   
           DISPLAY 'NO CHANGE RECORDS  : ' PA-NO-CHANGE-RECORDS.                
           DISPLAY '   '.                                                       
           DISPLAY 'ADDRESS INSERT RECS WRITTEN: '                              
                                        PA-ADDR-INSERT-RECORDS.                 
           DISPLAY 'ADDRESS UPDATE RECS WRITTEN: '                              
                                        PA-ADDR-UPDATE-RECORDS.                 
           DISPLAY 'ADDRESS DELETE RECS WRITTEN: '                              
                                        PA-ADDR-DELETE-RECORDS.                 
                                                                                
           CLOSE CW-CURRENT-FILE                                                
                 CW-PREVIOUS-FILE                                               
                 CW-CHANGE-FILE                                                 
                 CW-ADDRESS-FILE.                                               
                                                                                
      ******************************************************************        
      ******************************************************************        
