       IDENTIFICATION DIVISION.                                         06/24/92
       PROGRAM-ID.         PMKUT642.                                    PMKRP001
       AUTHOR.             COGNIZANT.                                      LV002
       INSTALLATION.       KOHL CORPORATION.                            PMKRP001
       DATE-WRITTEN.       12/06/2010.                                  PMKRP001
       DATE-COMPILED.                                                   PMKRP001
                                                                                
      ******************************************************************        
      * Remarks                                                        *        
      * -------                                                        *        
      *                                                                *        
      * TITLE        - FUTURE RETAIL PRICE CHANGE REPORT                        
      *                                                                *        
      * DESCRIPTION  - THIS PROGRAM SELECTS ALL THE PROMOTIONS WITH    *        
      *                SKU'S HAVING MARK UP AND DOWN PRICE CHANGES AND *        
      *                CREATES AN OUTPUT FILE TO GENERATE THE FINAL    *        
      *                PROMO REPORT                                    *        
      * CHANGE HISTORY                                                 *        
      ******************************************************************        
      * WR NUMBER     PROGRAMMER       DATE                            *        
      * ---------------------------------------------------------------*        
      * WR39399       COGNIZANT        12/06/2010                               
      * ORIGINAL VERSION                                               *        
      ******************************************************************        
C93861* CHG0193861    COGNIZANT        03/27/2018                               
C93861* ADDED HINT IN THE SELECT QUERY TO FIX THE JOB LONG RUN DUE TO  *        
C93861* PET_STR_GP_PRCHG_ASSN TABLE PARTITION.                         *        
      ******************************************************************        
070941* DATE:         07/30/2018                                       *        
070941* WR/IR#:       CHG0204126                                       *        
070941* PROGRAMMER:   COGNIZANT                                        *        
070941* DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SMPL-STR-INFILE                                               
              ASSIGN                        TO INP01.                           
                                                                                
           SELECT REP-DATE-INFILE                                               
              ASSIGN                        TO INP02.                           
                                                                                
070941     SELECT ORACLE-LOGON-FILE                                             
070941        ASSIGN                        TO ORALOGON.                        
                                                                                
           SELECT PROMO-PRIC-CHGS-FILE                                          
              ASSIGN                        TO RPT01.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  SMPL-STR-INFILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS SMPL-STR-INFILE-REC.                                  
       01  SMPL-STR-INFILE-REC           PIC X(80).                             
                                                                                
       FD  REP-DATE-INFILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS REP-DATE-INFILE-REC.                                  
                                                                                
       01  REP-DATE-INFILE-REC           PIC X(80).                             
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD        PIC X(80).                             
                                                                                
       FD  PROMO-PRIC-CHGS-FILE                                                 
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS PROMO-PRIC-CHGS-REC.                                  
                                                                                
       01  PROMO-PRIC-CHGS-REC           PIC X(373).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                    PIC S9(04) COMP VALUE +0.              
           88  ABEND-4001-WRITE-ERROR                VALUE +4001.               
           88  ABEND-4002-READ-ERROR                 VALUE +4002.               
           88  ABEND-4003-CTRL-CARD-ERROR            VALUE +4003.               
           88  ABEND-4004-TABLE-ERROR                VALUE +4004.               
           88  ABEND-4005-OPEN-ERROR                 VALUE +4005.               
           88  ABEND-4006-CLOSE-ERROR                VALUE +4006.               
           88  ABEND-4007-SEQUENCE-ERROR             VALUE +4007.               
           88  ABEND-4008-DATA-ERROR                 VALUE +4008.               
           88  ABEND-4009-KEY-DATA-ERROR             VALUE +4009.               
           88  ABEND-4013-SQL-ERROR                  VALUE +4013.               
           88  ABEND-4014-ORACLE-LOGON-ERROR         VALUE +4014.               
           88  ABEND-4019-DATE-ROUTINE-ERROR         VALUE +4019.               
           88  ABEND-4444-FORCED-ABEND               VALUE +4444.               
                                                                                
      *****************************************************************         
      *    THE LAYOUT FOR INPUT FILE - SMPL-STR-INFILE                *         
      *****************************************************************         
       01  CC-SMPL-STR-INFILE-REC.                                              
           05  CC-STR-NBR                PIC  9(04)  VALUE 0.                   
           05  FILLER                    PIC  X(76)  VALUE SPACES.              
                                                                                
      *****************************************************************         
      *    THE LAYOUT FOR DATE INPUT FILE - REP-RUN-DT-FILE           *         
      *****************************************************************         
       01  CC-REP-RUN-GAP-REC.                                                  
           05  CC-REP-RUN-STRT-GAP       PIC +9(02).                            
           05  FILLER                    PIC  X(01)  VALUE SPACES.              
           05  CC-REP-RUN-END-GAP        PIC +9(02).                            
           05  FILLER                    PIC  X(73)  VALUE SPACES.              
                                                                                
                                                                                
      *****************************************************************         
      * PC PROGRAM CONSTANTS                                                    
      *****************************************************************         
       01  PC-PROGRAM-CONSTANTS.                                                
070941*    05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE 'PMKUT642'.          
070941     05  PC-AUTO-LOGON             PIC  X(1)   VALUE '/'.                 
070941     05  PC-PROGRAM-NAME           PIC  X(08)  VALUE 'PMKUT642'.          
                                                                                
      *****************************************************************         
      * PS PROGRAM SWITCHES                                                     
      *****************************************************************         
       01  PS-PROGRAM-SWITCHES.                                                 
070941     05  PS-END-OF-LOGON-FILE-SW   PIC X(01)   VALUE 'N'.                 
070941         88  PS-END-OF-LOGON-FILE              VALUE 'Y'.                 
070941     05  PS-ORA-SRC-CTL-DONE-SW    PIC  X(01)  VALUE 'N'.                 
070941         88  PS-ORA-SRC-EMPTY                  VALUE 'Y'.                 
           05  PS-PRIC-CHG-SW            PIC  X(01)  VALUE 'N'.                 
               88  PS-END-OF-PRICE-CHG-CSR           VALUE 'Y'.                 
               88  PS-NOT-END-OF-PRICE-CHG-CSR       VALUE 'N'.                 
           05  PS-CONTROL-CARD-REP-DT    PIC  X(01)  VALUE 'N'.                 
               88  PS-END-OF-REP-DT-FILE             VALUE 'Y'.                 
               88  PS-NOT-END-OF-REP-DT-FILE         VALUE 'N'.                 
           05  PS-CONTROL-CARD-STR-SW    PIC  X(01)  VALUE 'N'.                 
               88  PS-END-OF-CC-FILE-STR             VALUE 'Y'.                 
               88  PS-NOT-END-OF-CC-FILE-STR         VALUE 'N'.                 
                                                                                
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID          PIC X(40).                             
070941         88 CC-AUTO-LOGON                      VALUE '/'.                 
070941     05  CC-ORACLE-PASSWORD        PIC X(40).                             
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN      PIC S9(4)   VALUE ZEROS                
070941                                               COMP SYNC.                 
070941     05  PV-ORACLE-PASSWORD-LEN    PIC S9(4)   VALUE ZEROS                
070941                                               COMP SYNC.                 
070941     05  PC-AUTO-LOGON             PIC  X(01)  VALUE '/'.                 
           05  PV-PARAGRAPH-NAME         PIC  X(30)  VALUE SPACES.              
           05  PV-TABLE-NAME             PIC  X(30)  VALUE SPACES.              
           05  PV-OPERATION-MSG          PIC  X(60)  VALUE SPACES.              
           05  PV-OPERATION-MSG-1        PIC  X(40)  VALUE SPACES.              
           05  PV-OPERATION-MSG-2        PIC  X(40)  VALUE SPACES.              
           05  PV-START-DT               PIC  S9(02) VALUE 0.                   
           05  PV-END-DT                 PIC  S9(02) VALUE 0.                   
           05  PV-REP-STRT-DT            PIC  X(10)  VALUE SPACES.              
           05  PV-REP-END-DT             PIC  X(10)  VALUE SPACES.              
           05  PV-GMA-NBR                PIC  9(04)  VALUE 0.                   
           05  PV-GMM-FRST-NM            PIC  X(20)  VALUE SPACES.              
           05  PV-GMM-LAST-NM            PIC  X(30)  VALUE SPACES.              
           05  PV-DMA-NBR                PIC  9(04)  VALUE 0.                   
           05  PV-DMM-FRST-NM            PIC  X(20)  VALUE SPACES.              
           05  PV-DMM-LAST-NM            PIC  X(30)  VALUE SPACES.              
           05  PV-BYR-NBR                PIC  9(04)  VALUE 0.                   
           05  PV-BYR-FRST-NM            PIC  X(20)  VALUE SPACES.              
           05  PV-BYR-LAST-NM            PIC  X(30)  VALUE SPACES.              
           05  PV-DEPT-NBR               PIC  9(04)  VALUE 0.                   
           05  PV-DEPT-NM                PIC  X(20)  VALUE SPACES.              
           05  PV-SUB-CL-NBR             PIC  9(04)  VALUE 0.                   
           05  PV-GP-CDE                 PIC  X(03)  VALUE SPACES.              
           05  PV-SKU-NBR                PIC  9(08)  VALUE 0.                   
           05  PV-SKU-DESC               PIC  X(30)  VALUE SPACES.              
           05  PV-PRCHG-TYP-CDE          PIC  X(03)  VALUE SPACES.              
           05  PV-EFF-DTE                PIC  X(10)  VALUE SPACES.              
           05  PV-NW-MDSE-LNE-PRIC-AMT   PIC  9(07)V99 VALUE 0.                 
           05  PV-CURR-MDSE-LNE-PRIC-AMT PIC  9(07)V99 VALUE 0.                 
           05  PV-PROMO-ID               PIC  9(10)  VALUE 0.                   
           05  PV-PROMO-NM               PIC  X(40)  VALUE SPACES.              
           05  PV-PROMO-ITM-DESC         PIC  X(31)  VALUE SPACES.              
           05  PV-PROMO-PRIC-PT-AMT      PIC  9(07)V99 VALUE ZEROES.            
           05  PV-CNT-STRS               PIC  9(02)  VALUE ZEROES.              
           05  PV-PRCH-TYP-CD            PIC  X(01)  VALUE SPACES.              
           05  PV-NULL-VAR1              PIC  S9(04) VALUE 0 COMP.              
           05  PV-NULL-VAR2              PIC  S9(04) VALUE 0 COMP.              
           05  PV-NULL-VAR3              PIC  S9(04) VALUE 0 COMP.              
           05  PV-NULL-VAR4              PIC  S9(04) VALUE 0 COMP.              
           05  PV-NULL-VAR5              PIC  S9(04) VALUE 0 COMP.              
           05  PV-NULL-VAR6              PIC  S9(04) VALUE 0 COMP.              
           05  PV-NULL-VAR7              PIC  S9(04) VALUE 0 COMP.              
           05  PV-HOST-STRS.                                                    
               10  PV-HOST-STR-1         PIC  S9(04) VALUE +0 COMP-3.           
               10  PV-HOST-STR-2         PIC  S9(04) VALUE +0 COMP-3.           
               10  PV-HOST-STR-3         PIC  S9(04) VALUE +0 COMP-3.           
               10  PV-HOST-STR-4         PIC  S9(04) VALUE +0 COMP-3.           
               10  PV-HOST-STR-5         PIC  S9(04) VALUE +0 COMP-3.           
               10  PV-HOST-STR-6         PIC  S9(04) VALUE +0 COMP-3.           
               10  PV-HOST-STR-7         PIC  S9(04) VALUE +0 COMP-3.           
               10  PV-HOST-STR-8         PIC  S9(04) VALUE +0 COMP-3.           
               10  PV-HOST-STR-9         PIC  S9(04) VALUE +0 COMP-3.           
               10  PV-HOST-STR-10        PIC  S9(04) VALUE +0 COMP-3.           
               10  PV-HOST-STR-11        PIC  S9(04) VALUE +0 COMP-3.           
               10  PV-HOST-STR-12        PIC  S9(04) VALUE +0 COMP-3.           
               10  PV-HOST-STR-13        PIC  S9(04) VALUE +0 COMP-3.           
               10  PV-HOST-STR-14        PIC  S9(04) VALUE +0 COMP-3.           
               10  PV-HOST-STR-15        PIC  S9(04) VALUE +0 COMP-3.           
           05  PV-HOST-STRS-RED    REDEFINES PV-HOST-STRS.                      
               10  PV-STR                PIC  S9(04) COMP-3                     
                                                    OCCURS 15 TIMES.            
                                                                                
      *****************************************************************         
      *    FILE VARIABLES FOR REPORT RUN DATES                        *         
      *****************************************************************         
       01  FV-REP-RUN-DT-REC.                                                   
           05  FV-HDR-FLG                PIC  9(04)  VALUE ZERO.                
           05  FILLER                    PIC  X(01)  VALUE SPACES.              
           05  FV-REP-RUN-STRT-DT        PIC  X(10).                            
           05  FILLER                    PIC  X(01)  VALUE SPACES.              
           05  FV-REP-RUN-END-DT         PIC  X(10).                            
           05  FILLER                    PIC  X(347) VALUE SPACES.              
                                                                                
      *****************************************************************         
      *    THE LAYOUT FOR THE OUTPUT FILE TO BE SORTED.               *         
      *****************************************************************         
           COPY PMRD169.                                                        
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *----------------------------------------------------------------*        
      *    ORACLE DECLARE SECTION                                               
      *----------------------------------------------------------------*        
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
      * MAINLINE ROUTINE                                                        
      *****************************************************************         
       A100-MAINLINE.                                                           
           PERFORM B100-INITIALIZE.                                             
                                                                                
           PERFORM B300-PROCESS-SKU-PRICE-CHGS                                  
             UNTIL PS-END-OF-PRICE-CHG-CSR.                                     
                                                                                
           PERFORM B900-CLOSE-PARA.                                             
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * INITIALIZE ROUTINE                                                      
      *****************************************************************         
       B100-INITIALIZE.                                                         
                                                                                
070941*    PERFORM PM700-A000-ORACLE-SETUP.                                     
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PMPD700.                                                        
070941     DISPLAY 'ORACLE FILE OPEN'.                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     DISPLAY 'ORACLE FILE READ'.                                          
070941     PERFORM 7000-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     DISPLAY 'ORACLE FILE CLOSE'.                                         
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
070941                   'NOT PROVIDED'                                         
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
070941         PERFORM 9900-ORACLE-ABEND                                        
070941     END-IF.                                                              
                                                                                
           OPEN INPUT  REP-DATE-INFILE                                          
                INPUT  SMPL-STR-INFILE                                          
                OUTPUT PROMO-PRIC-CHGS-FILE.                                    
                                                                                
           PERFORM B200-READ-REP-DATE-FILE.                                     
                                                                                
           PERFORM B250-READ-SMPL-STR-FILE                                      
             UNTIL PS-END-OF-CC-FILE-STR.                                       
                                                                                
           PERFORM B400-WRITE-REP-DATE-VALUES.                                  
                                                                                
           PERFORM C100-DECLARE-PRICE-CHG-CSR.                                  
                                                                                
           PERFORM C200-OPEN-PRICE-CHG-CSR.                                     
                                                                                
           PERFORM C300-FETCH-PRICE-CHG-CSR.                                    
                                                                                
      ****************************************************************          
      * GETS THE REPORT START AND END DATE FROM THE CURRENT DATE     *          
      ****************************************************************          
       B200-READ-REP-DATE-FILE.                                                 
                                                                                
           READ REP-DATE-INFILE           INTO CC-REP-RUN-GAP-REC               
           AT END                                                               
              SET PS-END-OF-REP-DT-FILE     TO TRUE                             
           END-READ.                                                            
                                                                                
           IF (CC-REP-RUN-STRT-GAP = SPACES) OR                                 
                (CC-REP-RUN-END-GAP  = SPACES)                                  
                MOVE 'PMKUT642'             TO PC-PROGRAM-NAME                  
                MOVE 'B200-READ-REP-DATE-FILE'                                  
                                            TO PV-PARAGRAPH-NAME                
                MOVE 'READ                       '                              
                                            TO PV-OPERATION-MSG-1               
                MOVE 'INVALID START/END DATE IN CONTROL CARD'                   
                                            TO PV-OPERATION-MSG-2               
                SET ABEND-4003-CTRL-CARD-ERROR                                  
                                            TO TRUE                             
                PERFORM Z200-ABEND                                              
           END-IF.                                                              
                                                                                
           MOVE CC-REP-RUN-STRT-GAP         TO PV-START-DT                      
           MOVE CC-REP-RUN-END-GAP          TO PV-END-DT                        
                                                                                
           IF PV-START-DT > PV-END-DT                                           
                MOVE 'PMKUT642'             TO PC-PROGRAM-NAME                  
                MOVE 'B200-READ-REP-DATE-FILE'                                  
                                            TO PV-PARAGRAPH-NAME                
                MOVE 'READ                       '                              
                                            TO PV-OPERATION-MSG-1               
                MOVE 'STRT DT GAP > END DT GAP IN CONTROL CARD'                 
                                            TO PV-OPERATION-MSG-2               
                SET ABEND-4003-CTRL-CARD-ERROR                                  
                                            TO TRUE                             
                PERFORM Z200-ABEND                                              
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
                SELECT  TO_CHAR(TRUNC(SYSDATE + :PV-START-DT),                  
                                                'MM/DD/YYYY')                   
                       ,TO_CHAR(TRUNC(SYSDATE + :PV-END-DT),                    
                                                'MM/DD/YYYY')                   
                  INTO :PV-REP-STRT-DT                                          
                      ,:PV-REP-END-DT                                           
                  FROM  DUAL                                                    
           END-EXEC.                                                            
                                                                                
           DISPLAY ' ************************************'                      
           DISPLAY ' REPORT RUN START DATE : ' PV-REP-STRT-DT                   
           DISPLAY ' REPORT RUN END DATE   : ' PV-REP-END-DT                    
           DISPLAY ' ************************************'                      
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE 'B200-READ-REP-DATE-FILE'                              
                                            TO PV-PARAGRAPH-NAME                
                    MOVE 'DUAL            ' TO PV-TABLE-NAME                    
                    MOVE 'ERROR IN GETTING SYS DATE  '                          
                                            TO PV-OPERATION-MSG-1               
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
                                                                                
      ****************************************************************          
      * B250-READ-SMPL-STR-FILE.                                                
      *     READ IN SAMPLE STORE INFO FROM SMPL-STR-INFILE                      
      ****************************************************************          
       B250-READ-SMPL-STR-FILE.                                                 
                                                                                
           READ SMPL-STR-INFILE INTO CC-SMPL-STR-INFILE-REC                     
             AT END                                                             
                SET PS-END-OF-CC-FILE-STR   TO TRUE                             
           END-READ.                                                            
                                                                                
           ADD 1                            TO PV-CNT-STRS.                     
                                                                                
           IF  (PS-END-OF-CC-FILE-STR                                           
           AND PV-CNT-STRS = 1)                                                 
           OR  PV-CNT-STRS > 15                                                 
               MOVE 'B250-READ-SMPL-STR-FILE'                                   
                                            TO PV-PARAGRAPH-NAME                
               MOVE 'CONTROL CARD IS EMPTY / STORE COUNT > 15'                  
                                            TO PV-OPERATION-MSG-1               
               SET ABEND-4003-CTRL-CARD-ERROR                                   
                                            TO TRUE                             
               PERFORM Z200-ABEND                                               
           END-IF.                                                              
                                                                                
           IF NOT PS-END-OF-CC-FILE-STR                                         
              MOVE CC-STR-NBR                TO PV-STR(PV-CNT-STRS)             
           END-IF.                                                              
                                                                                
      *****************************************************************         
      *   B300-PROCESS-SKU-PRIC-CHGS.                                           
      *****************************************************************         
       B300-PROCESS-SKU-PRICE-CHGS.                                             
                                                                                
           PERFORM B700-WRITE-PROMO-PRIC-CHGS                                   
                                                                                
           PERFORM C300-FETCH-PRICE-CHG-CSR.                                    
                                                                                
      *****************************************************************         
      *   B400-WRITE-REP-DATE-VALUES.                                           
      *****************************************************************         
       B400-WRITE-REP-DATE-VALUES.                                              
                                                                                
           MOVE PV-REP-STRT-DT              TO FV-REP-RUN-STRT-DT               
           MOVE PV-REP-END-DT               TO FV-REP-RUN-END-DT                
           WRITE PROMO-PRIC-CHGS-REC      FROM FV-REP-RUN-DT-REC.               
                                                                                
      *****************************************************************         
      * B700-WRITE-PROMO-PRIC-CHGS.                                             
      *****************************************************************         
       B700-WRITE-PROMO-PRIC-CHGS.                                              
                                                                                
           IF  PV-PRCH-TYP-CD = 'T'                                             
               MOVE 'Y'                     TO PM169-TIER-PRIC-IND              
           ELSE                                                                 
               MOVE 'N'                     TO PM169-TIER-PRIC-IND              
           END-IF.                                                              
                                                                                
           WRITE PROMO-PRIC-CHGS-REC      FROM PM169-PROMO-PRIC-CHGS.           
                                                                                
      *****************************************************************         
      *    CLOSE FILES                                                          
      *****************************************************************         
       B900-CLOSE-PARA.                                                         
                                                                                
           CLOSE                            REP-DATE-INFILE                     
                                            SMPL-STR-INFILE                     
                                            PROMO-PRIC-CHGS-FILE.               
                                                                                
           PERFORM C700-CLOSE-PRIC-CHG-CSR.                                     
                                                                                
      ***********************************************************               
      * C100-DECLARE-PRICE-CHG-CSR.                                             
      ***********************************************************               
       C100-DECLARE-PRICE-CHG-CSR.                                              
                                                                                
           EXEC SQL                                                             
                DECLARE PRICE_CHG_CSR CURSOR FOR                                
                 SELECT TO_CHAR(VT1.EFF_DTE,'MM/DD/YYYY'),                      
                        VT1.PRCHG_TYP_CDE,                                      
                        VT1.CURR_MDSE_LNE_PRIC_AMT,                             
                        VT1.NW_MDSE_LNE_PRIC_AMT,                               
                        HIER.GMA_NBR,                                           
                        HIER.GMM_FRST_NM,                                       
                        HIER.GMM_LAST_NM,                                       
                        HIER.DMA_NBR,                                           
                        HIER.DMM_FRST_NM,                                       
                        HIER.DMM_LAST_NM,                                       
                        HIER.BYR_NBR,                                           
                        HIER.BYR_FRST_NM,                                       
                        HIER.BYR_LAST_NM,                                       
                        LOC.DEPT_NBR,                                           
                        HIER.DEPT_NM,                                           
                        ITM.PRCH_TYP_CDE,                                       
                        LOC.SKU_NBR,                                            
                        LOC.PROMO_ID,                                           
                        LOC.SUB_CL_NBR,                                         
                        SKU.SKU_DESC,                                           
                        SKU.GP_CDE,                                             
                        ITM.PROMO_ITM_DESC,                                     
                        DIM.PROMO_NM,                                           
                        MIN(LOC.PROMO_PRIC_PT_AMT)                              
                   FROM                                                         
C93861          (SELECT /*+ FULL(B) FULL(C) PARALLEL(8) */                      
                        DISTINCT A.EFF_DTE,                                     
                        C.PRCHG_TYP_CDE,                                        
                        E.SKU_NBR,                                              
                        D.CURR_MDSE_LNE_PRIC_AMT,                               
                        D.NW_MDSE_LNE_PRIC_AMT                                  
                   FROM PET_PERM_EVNT             A,                            
                        PET_STR_GP_PRCHG_ASSN     B,                            
                        PET_PRCHG                 C,                            
                        PET_STR_GP_MDSE_LNE_PRICG D,                            
                        PET_PRCHG_MDSE_SELN_LNE   E,                            
                        PET_PRCHG_MDSE_SELN       F                             
                  WHERE A.PERM_EVNT_ID       = B.PERM_EVNT_ID                   
                    AND B.PRCHG_ID           = C.PRCHG_ID                       
                    AND TRUNC(A.EFF_DTE)    >= TO_DATE(:PV-REP-STRT-DT,         
                                                       'MM/DD/YYYY')            
                    AND TRUNC(A.EFF_DTE)    <= TO_DATE(:PV-REP-END-DT,          
                                                       'MM/DD/YYYY')            
                    AND C.PRCHG_TYP_CDE     IN ('MUA','MDA')                    
                    AND C.PRCHG_ST_CDE       = 'APR'                            
                    AND D.STR_GP_PRCHG_ASSN_ID                                  
                                             = B.STR_GP_PRCHG_ASSN_ID           
                    AND D.PRCHG_MDSE_SELN_LNE_ID                                
                                             = E.PRCHG_MDSE_SELN_LNE_ID         
                    AND F.PRCHG_MDSE_SELN_ID = E.PRCHG_MDSE_SELN_ID             
                    AND F.MDSE_HIER_CDE      = 'SKU'                            
                    AND A.STAT_CDE           = 'S'                              
                    AND B.STAT_CDE           = 'S'                              
                    AND C.STAT_CDE           = 'S'                              
                    AND D.STAT_CDE           = 'S'                              
                    AND E.STAT_CDE           = 'S'                              
                    AND F.STAT_CDE           = 'S'                              
                  UNION                                                         
C93861           SELECT   /*+ FULL(B) FULL(C) PARALLEL(8) */                    
                        DISTINCT A.EFF_DTE,                                     
                        C.PRCHG_TYP_CDE,                                        
                        G.SKU_NBR,                                              
                        D.CURR_MDSE_LNE_PRIC_AMT,                               
                        D.NW_MDSE_LNE_PRIC_AMT                                  
                   FROM PET_PERM_EVNT             A,                            
                        PET_STR_GP_PRCHG_ASSN     B,                            
                        PET_PRCHG                 C,                            
                        PET_STR_GP_MDSE_LNE_PRICG D,                            
                        PET_PRCHG_MDSE_SELN_LNE   E,                            
                        PET_PRCHG_MDSE_SELN       F,                            
                        PET_PRCHG_SKU_SUBLNE      G                             
                  WHERE A.PERM_EVNT_ID       = B.PERM_EVNT_ID                   
                    AND B.PRCHG_ID           = C.PRCHG_ID                       
                    AND C.PRCHG_TYP_CDE     IN ('MUA','MDA')                    
                    AND C.PRCHG_ST_CDE       = 'APR'                            
                    AND D.STR_GP_PRCHG_ASSN_ID                                  
                                             = B.STR_GP_PRCHG_ASSN_ID           
                    AND D.PRCHG_MDSE_SELN_LNE_ID                                
                                             = E.PRCHG_MDSE_SELN_LNE_ID         
                    AND F.PRCHG_MDSE_SELN_ID = E.PRCHG_MDSE_SELN_ID             
                    AND TRUNC(A.EFF_DTE)     >= TO_DATE(:PV-REP-STRT-DT,        
                                                        'MM/DD/YYYY')           
                    AND TRUNC(A.EFF_DTE)     <= TO_DATE(:PV-REP-END-DT,         
                                                        'MM/DD/YYYY')           
                    AND F.MDSE_HIER_CDE      = 'STY'                            
                    AND G.PRCHG_MDSE_SELN_LNE_ID                                
                                             = E.PRCHG_MDSE_SELN_LNE_ID         
                    AND A.STAT_CDE           = 'S'                              
                    AND B.STAT_CDE           = 'S'                              
                    AND C.STAT_CDE           = 'S'                              
                    AND D.STAT_CDE           = 'S'                              
                    AND E.STAT_CDE           = 'S'                              
                    AND F.STAT_CDE           = 'S'                              
                    AND G.STAT_CDE           = 'S') VT1,                        
                    PMT_SKU_LVL_PRICG_BY_LOC LOC,                               
                    PMV_PROMO_DIM DIM,                                          
                    PMT_PROMO_ITM ITM,                                          
                    PMV_PRSN_MDSE_HIER HIER,                                    
                    XKT_SKU SKU                                                 
              WHERE LOC.PROMO_ID             = DIM.PROMO_ID                     
                AND TRUNC(LOC.PROMO_STRT_TMST) <=                               
                         TO_DATE(:PV-REP-END-DT,'MM/DD/YYYY')                   
                AND TRUNC(LOC.PROMO_END_TMST)  >=                               
                         TO_DATE(:PV-REP-STRT-DT,'MM/DD/YYYY')                  
                AND LOC.SKU_NBR              = VT1.SKU_NBR                      
                AND LOC.LOC_NBR NOT IN                                          
                   (SELECT LOC_NBR                                              
                      FROM XKT_LOC                                              
                     WHERE E_BUS_IND         = 'Y'                              
                       AND LOC_NBR IN ( :PV-HOST-STR-1                          
                                       ,:PV-HOST-STR-2                          
                                       ,:PV-HOST-STR-3                          
                                       ,:PV-HOST-STR-4                          
                                       ,:PV-HOST-STR-5                          
                                       ,:PV-HOST-STR-6                          
                                       ,:PV-HOST-STR-7                          
                                       ,:PV-HOST-STR-8                          
                                       ,:PV-HOST-STR-9                          
                                       ,:PV-HOST-STR-10                         
                                       ,:PV-HOST-STR-11                         
                                       ,:PV-HOST-STR-12                         
                                       ,:PV-HOST-STR-13                         
                                       ,:PV-HOST-STR-14                         
                                       ,:PV-HOST-STR-15                         
                                      )                                         
                   )                                                            
                AND LOC.PROMO_PRICG_TYP_CDE NOT IN ('GBL','GBD','GBP')          
                AND LOC.PROMO_PRIC_PT_AMT    > 0.00                             
                AND DIM.PROMO_STAT_CDE       = 'S'                              
                AND DIM.PROMO_TYP_CDE        = 'R'                              
                AND LOC.PROMO_ID             = ITM.PROMO_ID                     
                AND LOC.PROMO_ITM_ID         = ITM.PROMO_ITM_ID                 
                AND LOC.SKU_NBR              = SKU.SKU_NBR                      
                AND DIM.PROMO_ID             = ITM.PROMO_ID                     
                AND HIER.DEPT_NBR            = ITM.DEPT_NBR                     
            GROUP BY HIER.GMA_NBR,                                              
                     HIER.GMM_FRST_NM,                                          
                     HIER.GMM_LAST_NM,                                          
                     HIER.DMA_NBR,                                              
                     HIER.DMM_FRST_NM,                                          
                     HIER.DMM_LAST_NM,                                          
                     HIER.BYR_NBR,                                              
                     HIER.BYR_FRST_NM,                                          
                     HIER.BYR_LAST_NM,                                          
                     LOC.DEPT_NBR,                                              
                     HIER.DEPT_NM,                                              
                     ITM.PRCH_TYP_CDE,                                          
                     LOC.SKU_NBR,                                               
                     LOC.PROMO_ID,                                              
                     LOC.SUB_CL_NBR,                                            
                     SKU.SKU_DESC,                                              
                     SKU.GP_CDE,                                                
                     ITM.PROMO_ITM_DESC,                                        
                     DIM.PROMO_NM,                                              
                     VT1.PRCHG_TYP_CDE,                                         
                     VT1.EFF_DTE,                                               
                     VT1.CURR_MDSE_LNE_PRIC_AMT,                                
                     VT1.NW_MDSE_LNE_PRIC_AMT                                   
           END-EXEC.                                                            
                                                                                
      ***********************************************************               
      * C200-OPEN-PRICE_CHG_CSR.                                                
      ***********************************************************               
       C200-OPEN-PRICE-CHG-CSR.                                                 
                                                                                
           EXEC SQL                                                             
                OPEN PRICE_CHG_CSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE 'C200-OPEN-PRICE-CHG-CSR'                              
                                            TO PV-PARAGRAPH-NAME                
                    MOVE 'CURSOR PRICE_CHG_CSR '                                
                                            TO PV-TABLE-NAME                    
                    MOVE 'ERROR ON OPEN '                                       
                                            TO PV-OPERATION-MSG-1               
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
                                                                                
      ***********************************************************               
      * C300-FETCH-PRICE-CHG-CSR.                                               
      ***********************************************************               
       C300-FETCH-PRICE-CHG-CSR.                                                
                                                                                
           EXEC SQL                                                             
                FETCH PRICE_CHG_CSR                                             
                 INTO :PV-EFF-DTE                                               
                     ,:PV-PRCHG-TYP-CDE                                         
                     ,:PV-CURR-MDSE-LNE-PRIC-AMT                                
                     ,:PV-NW-MDSE-LNE-PRIC-AMT                                  
                     ,:PV-GMA-NBR                                               
                     ,:PV-GMM-FRST-NM:PV-NULL-VAR1                              
                     ,:PV-GMM-LAST-NM:PV-NULL-VAR2                              
                     ,:PV-DMA-NBR                                               
                     ,:PV-DMM-FRST-NM:PV-NULL-VAR3                              
                     ,:PV-DMM-LAST-NM:PV-NULL-VAR4                              
                     ,:PV-BYR-NBR                                               
                     ,:PV-BYR-FRST-NM:PV-NULL-VAR5                              
                     ,:PV-BYR-LAST-NM:PV-NULL-VAR6                              
                     ,:PV-DEPT-NBR                                              
                     ,:PV-DEPT-NM:PV-NULL-VAR7                                  
                     ,:PV-PRCH-TYP-CD                                           
                     ,:PV-SKU-NBR                                               
                     ,:PV-PROMO-ID                                              
                     ,:PV-SUB-CL-NBR                                            
                     ,:PV-SKU-DESC                                              
                     ,:PV-GP-CDE                                                
                     ,:PV-PROMO-ITM-DESC                                        
                     ,:PV-PROMO-NM                                              
                     ,:PV-PROMO-PRIC-PT-AMT                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    MOVE PV-EFF-DTE         TO PM169-EFF-DTE                    
                    MOVE PV-PRCHG-TYP-CDE   TO PM169-PRCH-TYP-CDE               
                    MOVE PV-SKU-NBR         TO PM169-SKU-NBR                    
                    MOVE PV-CURR-MDSE-LNE-PRIC-AMT                              
                                            TO PM169-CURR-LNE-PRIC-AMT          
                    MOVE PV-NW-MDSE-LNE-PRIC-AMT                                
                                            TO PM169-NW-LNE-PRIC-AMT            
                    MOVE PV-GMA-NBR         TO PM169-GMA-NBR                    
                    MOVE PV-GMM-FRST-NM     TO PM169-GMM-FRST-NM                
                    MOVE PV-GMM-LAST-NM     TO PM169-GMM-LAST-NM                
                    MOVE PV-DMA-NBR         TO PM169-DMA-NBR                    
                    MOVE PV-DMM-FRST-NM     TO PM169-DMM-FRST-NM                
                    MOVE PV-DMM-LAST-NM     TO PM169-DMM-LAST-NM                
                    MOVE PV-BYR-NBR         TO PM169-BYR-NBR                    
                    MOVE PV-BYR-FRST-NM     TO PM169-BYR-FRST-NM                
                    MOVE PV-BYR-LAST-NM     TO PM169-BYR-LAST-NM                
                    MOVE PV-DEPT-NBR        TO PM169-DEPT-NBR                   
                    MOVE PV-DEPT-NM         TO PM169-DEPT-NM                    
                    MOVE PV-PROMO-ID        TO PM169-PROMO-ID                   
                    MOVE PV-SUB-CL-NBR      TO PM169-SUB-CL-NBR                 
                    MOVE PV-SKU-DESC        TO PM169-SKU-DESC                   
                    MOVE PV-GP-CDE          TO PM169-GP-CDE                     
                    MOVE PV-PROMO-ITM-DESC  TO PM169-PROMO-ITM-DESC             
                    MOVE PV-PROMO-NM        TO PM169-PROMO-NM                   
                    MOVE PV-PROMO-PRIC-PT-AMT                                   
                                            TO PM169-PROMO-PRIC-PT-AMT          
               WHEN SQLCODE = 1403                                              
                    SET PS-END-OF-PRICE-CHG-CSR                                 
                                            TO TRUE                             
               WHEN OTHER                                                       
                    MOVE 'C300-FETCH-PRICE-CHG-CSR'                             
                                            TO PV-PARAGRAPH-NAME                
                    MOVE 'CURSOR PRICE_CHG_CSR'                                 
                                            TO PV-TABLE-NAME                    
                    MOVE 'ERROR ON FETCH '                                      
                                            TO PV-OPERATION-MSG-1               
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
                                                                                
      ***********************************************************               
      * C700-CLOSE-PRIC-CHG-CSR.                                                
      ***********************************************************               
       C700-CLOSE-PRIC-CHG-CSR.                                                 
                                                                                
            EXEC SQL                                                            
                 CLOSE PRICE_CHG_CSR                                            
            END-EXEC.                                                           
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE 'C700-CLOSE-PRIC-CHG-CSR'                              
                                            TO PV-PARAGRAPH-NAME                
                    MOVE 'CURSOR PRICE_CHG_CSR'                                 
                                            TO PV-TABLE-NAME                    
                    MOVE 'ERROR ON CLOSE'                                       
                                            TO PV-OPERATION-MSG-1               
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
                                                                                
070941****************************************************************          
070941** READ THE ORACLE LOGON CONTROL CARD.                        **          
070941****************************************************************          
070941 7000-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
                                                                                
      *----------------------------------------------------------               
      * 9900-ORACLE-ABEND.                                                      
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       9900-ORACLE-ABEND.                                                       
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** =   SQLERROR'.                         
070941*    DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
070941     DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                           
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG-1.                
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ************************************************************              
      * Z200-ABEND.                                                             
      *     PERFORMS A NON-SQL-ABEND.                                           
      ************************************************************              
       Z200-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
070941*    DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.               
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
