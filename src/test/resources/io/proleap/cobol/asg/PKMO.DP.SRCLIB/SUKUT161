      ******************************************************************00010013
       IDENTIFICATION DIVISION.                                         00020013
      ******************************************************************00030013
       PROGRAM-ID.    SUKUT161.                                         00040013
       AUTHOR.        JIM HUNTER.                                       00050013
       INSTALLATION.  KOHLS.                                            00060013
       DATE-WRITTEN   MARCH, 2017.                                      00070013
       DATE-COMPILED.                                                   00080013
      ******************************************************************00090013
      *   THIS PROGRAM READS THE INTRANSIT / STORE EXPECTED FILE FROM   00100013
      *   PROGRAM SUKUT046 AND SPLITS IT INTO 2 FILES:                  00110013
      *   1) CURRENT FILE CONTAINING ALL INTRANSIT RECORDS AS WELL AS   00120013
      *      STORE EXPECTED RECORDS FOR NON-SCANNING STORES,            00130013
      *   2) NEW FILE CONTAINING STORE EXPECTED RECORDS FOR SCANNING    00140013
      *      STORES.                                                    00150013
      ******************************************************************00160013
       ENVIRONMENT DIVISION.                                            00170013
       CONFIGURATION SECTION.                                           00180013
                                                                        00190013
       SOURCE-COMPUTER.        IBM-3090.                                00200013
       OBJECT-COMPUTER.        IBM-3090.                                00210013
                                                                        00220013
       INPUT-OUTPUT SECTION.                                            00230013
       FILE-CONTROL.                                                    00240013
                                                                        00250013
           SELECT CARTON-DTL-INP-FILE                                   00260013
               ASSIGN TO IN01.                                          00270013
                                                                        00280013
           SELECT CARTON-NO-SCAN-DTL-OUT-FILE                           00290013
               ASSIGN TO OUT01.                                         00300013
                                                                        00310013
           SELECT CARTON-SCANNED-DTL-OUT-FILE                           00320013
               ASSIGN TO OUT02.                                         00330013
                                                                        00340013
      ******************************************************************00350013
       DATA DIVISION.                                                   00360013
       FILE SECTION.                                                    00370013
                                                                        00380013
       FD  CARTON-DTL-INP-FILE                                          00390013
           RECORDING MODE IS F                                          00400013
           LABEL RECORDS ARE STANDARD                                   00410013
           BLOCK CONTAINS 0 RECORDS.                                    00420013
                                                                        00430013
       01  CARTON-DTL-INP-RECORD          PIC  X(100).                  00440013
      *                                                                 00450013
       FD  CARTON-NO-SCAN-DTL-OUT-FILE                                  00460013
           RECORDING MODE IS F                                          00470013
           LABEL RECORDS ARE STANDARD                                   00480013
           BLOCK CONTAINS 0 RECORDS.                                    00490013
                                                                        00500013
       01  CARTON-NO-SCAN-DTL-OUT-RECORD  PIC  X(100).                  00510013
      *                                                                 00520013
       FD  CARTON-SCANNED-DTL-OUT-FILE                                  00530013
           RECORDING MODE IS F                                          00540013
           LABEL RECORDS ARE STANDARD                                   00550013
           BLOCK CONTAINS 0 RECORDS.                                    00560013
                                                                        00570013
       01  CARTON-SCANNED-DTL-OUT-RECORD  PIC  X(100).                  00580013
      *                                                                 00590013
      ******************************************************************00600013
       WORKING-STORAGE SECTION.                                         00610013
      ******************************************************************00620013
      ******************************************************************00630013
      *  CARTON FILE LAYOUT (INPUT AND NOT SCANNED OUTPUT FILES)        00640013
      ******************************************************************00650013
           COPY PCRD010.                                                00660013
                                                                        00670013
      ******************************************************************00680013
      *  SCANNED OUTPUT FILE                                            00690013
      ******************************************************************00700013
           COPY SURD150.                                                00710013
                                                                        00720013
      ******************************************************************00730013
      *  COMMUNICATIONS AREA FOR DB2                                    00740013
      ******************************************************************00750013
           EXEC SQL                                                     00760013
                INCLUDE SQLCA                                           00770013
           END-EXEC.                                                    00780013
                                                                        00790013
      ******************************************************************00800013
      *  OUTBOUND TRAILER TRIP TABLE                                    00810013
      ******************************************************************00820013
           EXEC SQL                                                     00830013
                INCLUDE TSUOBTR                                         00840013
           END-EXEC.                                                    00850013
                                                                        00860013
      ******************************************************************00870013
      *  OUTBOUND TRAILER TRIP STATUS TABLE                             00880013
      ******************************************************************00890013
           EXEC SQL                                                     00900013
                INCLUDE TSUOBTS                                         00910013
           END-EXEC.                                                    00920013
                                                                        00930013
      ******************************************************************00940013
      *  PARAMATER TABLE                                                00950013
      ******************************************************************00960013
           EXEC SQL                                                     00970013
                INCLUDE TKRPRPM                                         00980013
           END-EXEC.                                                    00990013
                                                                        01000013
      ***************************************************************** 01010013
      *    WST_INTGRTN_TYP TABLE                                        01020013
      ***************************************************************** 01030013
           EXEC SQL                                                     01040013
                INCLUDE WST00008                                        01050013
           END-EXEC.                                                    01060013
                                                                        01070013
      ***************************************************************** 01080013
      *    WST_ATTR_ELEM TABLE                                          01090013
      ***************************************************************** 01100013
           EXEC SQL                                                     01110013
                INCLUDE WST00009                                        01120013
           END-EXEC.                                                    01130013
                                                                        01140013
      ***************************************************************** 01150013
      *    WST_ATTR_DEFN TABLE                                          01160013
      ***************************************************************** 01170013
           EXEC SQL                                                     01180013
                INCLUDE WST00010                                        01190013
           END-EXEC.                                                    01200013
                                                                        01210013
      ******************************************************************01220013
      ******************************************************************01230013
       01  PS-PROGRAM-SWITCHES.                                         01240013
           05  PS-END-OF-FILE-SW           PIC  X(01)    VALUE 'N'.     01250013
               88  PS-END-OF-FILE                        VALUE 'Y'.     01260013
               88  PS-NOT-END-OF-FILE                    VALUE 'N'.     01270013
                                                                        01280013
           05  PS-STORE-SCANNING-SW        PIC  X(01)    VALUE 'N'.     01290013
               88  PS-NON-SCANNING-STORE                 VALUE 'N'.     01300013
               88  PS-SCANNING-STORE                     VALUE 'S'.     01310013
                                                                        01320013
       01  PC-PROGRAM-CONSTANTS.                                        01330013
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08) VALUE 'SUKUT161'. 01340013
                                                                        01350013
       01  PV-PROGRAM-VARIABLES.                                        01360013
           05  PV-PARAGRAPH-NAME           PIC  X(30)     VALUE SPACES. 01370013
           05  PV-TOTAL-READ-COUNT         PIC  9(9) COMP VALUE ZEROS.  01380013
           05  PV-IT-READ-COUNT            PIC  9(9) COMP VALUE ZEROS.  01390013
           05  PV-SE-READ-COUNT            PIC  9(9) COMP VALUE ZEROS.  01400013
           05  PV-NO-SCAN-WRITE-COUNT      PIC  9(9) COMP VALUE ZEROS.  01410013
           05  PV-SCANNED-WRITE-COUNT      PIC  9(9) COMP VALUE ZEROS.  01420013
           05  PV-TOTAL-WRITE-COUNT        PIC  9(9) COMP VALUE ZEROS.  01430013
           05  PV-DISPLAY-COUNT            PIC  ZZZ,ZZZ,ZZ9.            01440013
           05  PV-DISPLAY-SQLCODE          PIC  +999.                   01450013
           05  PV-DISPLAY-TRIP-ID          PIC  ZZZZZZZZ9.              01460013
           05  PV-PREV-STORE               PIC S9(04) COMP VALUE ZEROS. 01470013
           05  PV-PREV-TRIP-ID             PIC  9(9)  VALUE ZEROES.     01480013
           05  PV-FUNC-TXT                 PIC  X(25) VALUE SPACES.     01490016
                                                                        01500013
       01  PV-ABEND-CODE                    PIC S9(04)   VALUE ZERO     01510013
                                                         COMP SYNC.     01520013
       01  PV-ABEND-FIELDS.                                             01530013
           05  PV-ABEND-PROGRAM             PIC X(8)   VALUE 'SUKUT161'.01540013
           05  PV-ABEND-PARAGRAPH           PIC X(50)  VALUE SPACES.    01550013
           05  PV-ABEND-OPERATION           PIC X(50)  VALUE SPACES.    01560013
           05  PV-ABEND-STRUCTURE-1         PIC X(8)   VALUE SPACES.    01570013
           05  PV-ABEND-STRUCTURE-2         PIC X(20)  VALUE SPACES.    01580013
           05  PV-ABEND-STRUCTURE-3         PIC X(20)  VALUE SPACES.    01590013
           05  PV-ABEND-STATUS              PIC XX     VALUE SPACES.    01600013
           05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.    01610013
           05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.    01620013
           05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.    01630013
           05  PV-DB2-OPERATION-MESSAGE-4   PIC  X(79) VALUE SPACES.    01640013
      *  USED FOR DB2 ERROR MESSAGE BUILDING                            01650013
           COPY DPWS004.                                                01660013
      ******************************************************************01670013
       PROCEDURE DIVISION.                                              01680013
      ******************************************************************01690013
      ******************************************************************01700013
      * 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE     01710013
      *      PROGRAM.                                                   01720013
      ******************************************************************01730013
       0000-PROGRAM-MAINLINE.                                           01740013
                                                                        01750013
           DISPLAY '***** SUKUT161 STARTED *****'.                      01760013
                                                                        01770013
           PERFORM 1000-INITIALIZE.                                     01780013
           PERFORM 2000-PROCESS-CARTON-FILE                             01790013
               UNTIL PS-END-OF-FILE.                                    01800013
           PERFORM 9000-EOJ-ROUTINE.                                    01810013
                                                                        01820013
           STOP RUN.                                                    01830013
                                                                        01840013
      ******************************************************************01850013
      *  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                    01860013
      *    -- OPEN INPUT FILE                                           01870013
      *    -- OPEN OUTPUT FILES                                         01880013
      *    -- READ INPUT FILE                                           01890013
      ******************************************************************01900013
       1000-INITIALIZE.                                                 01910013
                                                                        01920013
           OPEN INPUT  CARTON-DTL-INP-FILE.                             01930013
           OPEN OUTPUT CARTON-NO-SCAN-DTL-OUT-FILE.                     01940013
           OPEN OUTPUT CARTON-SCANNED-DTL-OUT-FILE.                     01950013
                                                                        01960013
           PERFORM 1100-READ-TKRPRPM-TABLE.                             01970013
                                                                        01980013
           PERFORM 7000-READ-FILE.                                      01990013
                                                                        02000013
                                                                        02010013
      ******************************************************************02020018
      *  READ PARM TABLE TO DETERMINE IF ALL STORES, NO STORES OR       02030018
      *  SOME STORES ARE SCANNING.                                      02040018
      ******************************************************************02050018
       1100-READ-TKRPRPM-TABLE.                                         02060013
                                                                        02070013
           EXEC SQL                                                     02080013
               SELECT  FUNC_TXT                                         02090013
                 INTO  :PV-FUNC-TXT                                     02100013
                 FROM  TKRPRPM                                          02110013
                WHERE  LOC_ID            = 90                           02120013
                  AND  INTFC_SYS_CDE     = 'KHL'                        02130013
                  AND  SYS_PRC_FUNC_CDE  = 'STRSCN'                     02140013
                  AND  FUNC_PRC_STAT_CDE = 'AC'                         02150013
           END-EXEC.                                                    02160013
                                                                        02170013
           EVALUATE TRUE                                                02180013
               WHEN SQLCODE = ZERO                                      02190013
                   IF PV-FUNC-TXT = 'A' OR 'E' OR 'N'                   02200016
                       DISPLAY 'STORES SCANNING OPTION = ' PV-FUNC-TXT  02210017
                   ELSE                                                 02220013
                       DISPLAY 'PARA 1100-READ-TKRPRPM-TABLE '          02230013
                               'FOUND INVALID OPTION (NOT A, E, N)'     02240013
                       DISPLAY 'SYS_PRC_FUNC_CDE = STRSCN'              02250013
                       DISPLAY 'PV-FUNC-TXT = ' PV-FUNC-TXT             02260016
                       DISPLAY 'DEFAULTING TO OPTION E'                 02270013
                       MOVE 'E' TO PV-FUNC-TXT                          02280016
                   END-IF                                               02290013
               WHEN SQLCODE = +100                                      02300013
                       DISPLAY 'PARA 1100-READ-TKRPRPM-TABLE '          02310013
                               'PARM NOT FOUND'                         02320013
                       DISPLAY 'SYS_PRC_FUNC_CDE = STRSCN'              02330013
                       DISPLAY 'DEFAULTING TO OPTION E'                 02340013
                       MOVE 'E' TO PV-FUNC-TXT                          02350016
               WHEN OTHER                                               02360013
                   MOVE  SQLCODE         TO PV-DISPLAY-SQLCODE          02370013
                   MOVE  '1100-READ-TKRPRPM-TABLE'                      02380013
                                         TO PV-ABEND-PARAGRAPH          02390013
                   MOVE 'SYS_PRC_FUNC_CDE  = STRSCN'                    02400013
                                         TO PV-ABEND-OPERATION          02410013
                   MOVE 'CHECKING STORE SCAN PARM '                     02420013
                                         TO PV-DB2-OPERATION-MESSAGE-1  02430013
                   MOVE 'TKRPRPM'        TO PV-ABEND-STRUCTURE-1        02440013
                   PERFORM 9900-DB2-ABEND                               02450013
           END-EVALUATE.                                                02460013
                                                                        02470013
                                                                        02480013
      ******************************************************************02490013
      *  RETRIEVING INTR-UPC-ID AND WRITE RECORD TO OUTPUT              02500013
      *  - ALL INTRANSIT RECORDS WRITTEN TO OUTPUT 1 (NON-SCANNING).    02510013
      *  - STORE EXPECTED FOR NON SCANNING STORES WRITTEN TO OUTPUT 1.  02520013
      *  - STORE EXPECTED FOR SCANNING STORES WRITTEN TO OUTPUT 2.      02530013
      ******************************************************************02540013
       2000-PROCESS-CARTON-FILE.                                        02550013
                                                                        02560013
           IF  PC010-TRN-IN-TRANSIT                                     02570013
               ADD +1  TO  PV-IT-READ-COUNT                             02580013
               PERFORM 8000-WRITE-NO-SCAN-OUTPUT                        02590013
           END-IF                                                       02600013
                                                                        02610013
           IF  PC010-TRN-STR-EXPECTED                                   02620013
               ADD +1  TO  PV-SE-READ-COUNT                             02630013
               IF PV-FUNC-TXT = 'A'                                     02640016
     ***           ALL STORES ARE SCANNING                              02650013
                   PERFORM 8100-WRITE-SCANNED-OUTPUT                    02660013
               ELSE                                                     02670013
                   IF PV-FUNC-TXT = 'N'                                 02680016
     ***               NO STORES ARE SCANNING                           02690013
                       PERFORM 8000-WRITE-NO-SCAN-OUTPUT                02700013
                   ELSE                                                 02710013
     ***               EXCEPTION STORES ARE SCANNING ('E')              02720013
                       IF  PC010-STR-NBR = PV-PREV-STORE                02730013
                       AND PC010-TRIP-ID = PV-PREV-TRIP-ID              02740013
                           IF PS-NON-SCANNING-STORE                     02750013
                               PERFORM 8000-WRITE-NO-SCAN-OUTPUT        02760013
                           ELSE                                         02770013
                               PERFORM 8100-WRITE-SCANNED-OUTPUT        02780013
                           END-IF                                       02790013
                       ELSE                                             02800013
                           PERFORM 2500-CHECK-IF-SCANNING-STORE         02810013
                           IF PS-NON-SCANNING-STORE                     02820013
                               PERFORM 8000-WRITE-NO-SCAN-OUTPUT        02830013
                           ELSE                                         02840013
                               PERFORM 8100-WRITE-SCANNED-OUTPUT        02850013
                           END-IF                                       02860013
                           MOVE PC010-STR-NBR TO PV-PREV-STORE          02870013
                           MOVE PC010-TRIP-ID TO PV-PREV-TRIP-ID        02880013
                       END-IF                                           02890013
                   END-IF                                               02900013
               END-IF                                                   02910013
           END-IF.                                                      02920013
                                                                        02930013
           PERFORM 7000-READ-FILE.                                      02940013
                                                                        02950013
                                                                        02960013
      ******************************************************************02970013
      *  CHECK IF SCANNING HAS BEEN TURNED ON FOR THE STORE.            02980013
      *  ONLY STORE EXPECTED RECORDS WILL BE PROCESSED IN THIS PARA.    02990013
      ******************************************************************03000013
       2500-CHECK-IF-SCANNING-STORE.                                    03010013
                                                                        03020013
           EXEC SQL                                                     03030013
              SELECT  E.OTBND_STAT_TMST                                 03040013
                INTO :SUOBTS-OTBND-STAT-TMST                            03050013
                FROM  WST_INTGRTN_TYP   A                               03060013
                   ,  WST_ATTR_DEFN     B                               03070013
                   ,  WST_ATTR_ELEM     C                               03080013
                   ,  WST_ATTR_ELEM     D                               03090013
                   ,  TSUOBTS           E                               03100013
                   ,  TSUOBTR           F                               03110013
               WHERE  C.ATTR_ELEM_NBR      = F.DEST_OPER_UNT_ID         03120013
                 AND  A.INTGRTN_TYP_CDE    = 'STOTBNDUPD'               03130013
                 AND  A.INTGRTN_TYP_CDE    = B.INTGRTN_TYP_CDE          03140013
                 AND  B.ATTR_DEFN_SEQ_NBR  = 1                          03150013
                 AND  B.INTGRTN_TYP_CDE    = C.INTGRTN_TYP_CDE          03160013
                 AND  C.ATTR_DEFN_SEQ_NBR  = 1                          03170013
                 AND  B.INTGRTN_TYP_CDE    = D.INTGRTN_TYP_CDE          03180013
                 AND  D.ATTR_DEFN_SEQ_NBR  = 2                          03190013
                 AND  C.INTGRTN_TYP_CDE    = D.INTGRTN_TYP_CDE          03200013
                 AND  C.ATTR_ELEM_SEQ_NBR  = D.ATTR_ELEM_SEQ_NBR        03210013
                 AND  E.OTBND_TRIP_ID      = :PC010-TRIP-ID             03220013
                 AND  E.OTBND_TRIP_ID      = F.OTBND_TRIP_ID            03230013
                 AND  E.TRIP_STAT_CDE      = 'DS'                       03240013
                 AND  D.ATTR_ELEM_TMST     <= E.OTBND_STAT_TMST         03250013
               WITH UR                                                  03260013
             END-EXEC.                                                  03270013
                                                                        03280013
           EVALUATE TRUE                                                03290013
               WHEN SQLCODE = ZERO                                      03300013
                    SET PS-SCANNING-STORE      TO TRUE                  03310013
               WHEN SQLCODE = -811                                      03320013
                    SET PS-SCANNING-STORE      TO TRUE                  03330013
               WHEN SQLCODE = +100                                      03340013
                    SET PS-NON-SCANNING-STORE  TO TRUE                  03350013
               WHEN SQLWARN0 NOT = SPACES                               03360013
               WHEN SQLCODE < ZERO                                      03370013
               WHEN SQLCODE > ZERO                                      03380013
                   MOVE  SQLCODE         TO PV-DISPLAY-SQLCODE          03390013
                   MOVE  '2500-CHECK-IF-SCANNING-STORE'                 03400013
                                         TO PV-ABEND-PARAGRAPH          03410013
                   MOVE 'SELECT WST_* TABLES '                          03420013
                                         TO PV-ABEND-OPERATION          03430013
                   MOVE 'TRIP ID BEING PROCESSED: '                     03440013
                                         TO PV-DB2-OPERATION-MESSAGE-1  03450013
                   MOVE PC010-TRIP-ID    TO PV-DISPLAY-TRIP-ID          03460013
                   MOVE PV-DISPLAY-TRIP-ID                              03470013
                                         TO PV-DB2-OPERATION-MESSAGE-2  03480013
                   MOVE  'CHECK TO SEE IF STORE SCANS'                  03490013
                                         TO PV-DB2-OPERATION-MESSAGE-3  03500013
                   MOVE 'TSUOBTS'        TO PV-ABEND-STRUCTURE-1        03510013
                   MOVE 'TSUOBTR'        TO PV-ABEND-STRUCTURE-2        03520013
                   PERFORM 9900-DB2-ABEND                               03530013
           END-EVALUATE.                                                03540013
                                                                        03550013
                                                                        03560013
                                                                        03570013
      ******************************************************************03580013
      *  READ INPUT FILE.                                               03590013
      ******************************************************************03600013
       7000-READ-FILE.                                                  03610013
            INITIALIZE PC010-PCT-INTRANSIT-REC.                         03620013
                                                                        03630013
            READ CARTON-DTL-INP-FILE INTO                               03640013
                PC010-PCT-INTRANSIT-REC                                 03650013
                  AT END SET PS-END-OF-FILE TO TRUE.                    03660013
                                                                        03670013
            IF PS-END-OF-FILE                                           03680013
               CONTINUE                                                 03690013
            ELSE                                                        03700013
               ADD +1 TO PV-TOTAL-READ-COUNT                            03710013
            END-IF.                                                     03720013
                                                                        03730013
      ******************************************************************03740013
      *  WRITE NOT SCANNED OUTPUT FILE.                                 03750013
      ******************************************************************03760013
       8000-WRITE-NO-SCAN-OUTPUT.                                       03770013
                                                                        03780013
           WRITE CARTON-NO-SCAN-DTL-OUT-RECORD FROM                     03790013
               PC010-PCT-INTRANSIT-REC.                                 03800013
                                                                        03810013
           ADD +1 TO PV-NO-SCAN-WRITE-COUNT                             03820013
                     PV-TOTAL-WRITE-COUNT.                              03830013
                                                                        03840013
      ******************************************************************03850013
      *  WRITE SCANNED OUTPUT FILE (ONLY CONTAINS TRIP ID)              03860013
      ******************************************************************03870013
       8100-WRITE-SCANNED-OUTPUT.                                       03880013
                                                                        03890013
           MOVE PC010-TRIP-ID    TO  SU150-TRIP-ID.                     03900013
           MOVE PC010-EVENT-TMST TO  SU150-EVENT-TMST.                  03910013
                                                                        03920013
           WRITE CARTON-SCANNED-DTL-OUT-RECORD FROM                     03930013
               SU150-SCANNED-TRIP-ID-REC.                               03940013
                                                                        03950013
           ADD +1 TO PV-SCANNED-WRITE-COUNT                             03960013
                     PV-TOTAL-WRITE-COUNT.                              03970013
                                                                        03980013
      ******************************************************************03990013
      *  CLOSE INPUT FILE.  DISPLAY COUNTS.                             04000013
      ******************************************************************04010013
       9000-EOJ-ROUTINE.                                                04020013
           CLOSE CARTON-DTL-INP-FILE                                    04030013
                 CARTON-NO-SCAN-DTL-OUT-FILE                            04040013
                 CARTON-SCANNED-DTL-OUT-FILE.                           04050013
                                                                        04060013
           MOVE PV-IT-READ-COUNT           TO  PV-DISPLAY-COUNT.        04070013
           DISPLAY 'IT RECORDS READ......: '   PV-DISPLAY-COUNT.        04080013
           MOVE PV-SE-READ-COUNT           TO  PV-DISPLAY-COUNT.        04090013
           DISPLAY 'SE RECORDS READ......: '   PV-DISPLAY-COUNT.        04100013
           MOVE PV-TOTAL-READ-COUNT        TO  PV-DISPLAY-COUNT.        04110013
           DISPLAY 'TOTAL RECORDS READ...: '   PV-DISPLAY-COUNT.        04120013
           DISPLAY '                       '.                           04130013
                                                                        04140013
           MOVE PV-NO-SCAN-WRITE-COUNT     TO  PV-DISPLAY-COUNT.        04150013
           DISPLAY 'TOTAL NO SCAN WRITTEN: '   PV-DISPLAY-COUNT.        04160013
           MOVE PV-SCANNED-WRITE-COUNT     TO  PV-DISPLAY-COUNT.        04170013
           DISPLAY 'TOTAL SCANNED WRITTEN: '   PV-DISPLAY-COUNT.        04180013
           MOVE PV-TOTAL-WRITE-COUNT       TO  PV-DISPLAY-COUNT.        04190013
           DISPLAY 'TOTAL RECORDS WRITTEN: '   PV-DISPLAY-COUNT.        04200013
           DISPLAY '                       '.                           04210013
           DISPLAY '*****  SUKUT161 ENDED  *****'.                      04220013
                                                                        04230013
                                                                        04240013
       9900-DB2-ABEND.                                                  04250013
                                                                        04260013
           DISPLAY '*** DB2 ERROR '.                                    04270013
           DISPLAY '*** SQLCODE   = ' PV-DISPLAY-SQLCODE                04280013
           DISPLAY '*** PROGRAM   = ' PV-ABEND-PROGRAM                  04290013
           DISPLAY '*** PARAGRAPH = ' PV-ABEND-PARAGRAPH.               04300013
           DISPLAY '*** OPERATION = ' PV-ABEND-OPERATION.               04310013
           DISPLAY '*** MESSAGE   = ' PV-DB2-OPERATION-MESSAGE-1.       04320013
           DISPLAY '***           = ' PV-DB2-OPERATION-MESSAGE-2.       04330013
           DISPLAY '***           = ' PV-DB2-OPERATION-MESSAGE-3.       04340013
           DISPLAY '*** TABLE 1   = ' PV-ABEND-STRUCTURE-1.             04350013
           DISPLAY '*** TABLE 2   = ' PV-ABEND-STRUCTURE-2.             04360013
                                                                        04370013
                                                                        04380013
           COPY DPPD004.                                                04390013
                                                                        04400013
           IF SQLCODE NOT = -911                                        04410013
               EXEC SQL                                                 04420013
                    ROLLBACK                                            04430013
               END-EXEC                                                 04440013
           END-IF.                                                      04450013
                                                                        04460013
           MOVE +4013                  TO PV-ABEND-CODE.                04470013
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         04480013
                                                                        04490013
