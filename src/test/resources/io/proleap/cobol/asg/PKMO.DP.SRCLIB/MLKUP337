       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP337                                          00030000
       AUTHOR.        JULIE SEIDLER.                                    00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  11-2-1999.                                        00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *           MLKUP337 - MAJOR CLASS/STORE MERCHANDISE LEDGER       00100000
      *                      TABLE UPDATE (TMNMCLS)                     00110000
W26603******************************************************************00120000
W26603* CHG ID   DATE        DESCRIPTION                               *00130000
W26603* ------   ----------  ----------------------------------------- *00140000
W26603* W26603   04-14-2004  STORE EXPANSION.  EXPANDED SOME PROGRAM   *00150000
W26603*                      VARIABLES TO ACCOMODATE INCREASED LENGTH  *00160000
W26603*                      OF DOLLAR AMOUNTS.  ADDED 'WITH UR' TO    *00170000
W26603*                      A DB2 SELECT.                             *00180000
W26603*                                                                *00190000
W26603*                      - RONNA MCDONALD                          *00200000
      ******************************************************************00210000
                                                                        00220000
                                                                        00230000
       ENVIRONMENT DIVISION.                                            00240000
                                                                        00250000
       CONFIGURATION SECTION.                                           00260000
       SOURCE-COMPUTER.        IBM-3090.                                00270000
       OBJECT-COMPUTER.        IBM-3090.                                00280000
       INPUT-OUTPUT SECTION.                                            00290000
       FILE-CONTROL.                                                    00300000
           SELECT CONDENSED-FILE                                        00310000
               ASSIGN TO INP01.                                         00320000
                                                                        00330000
                                                                        00340000
       DATA DIVISION.                                                   00350000
       FILE SECTION.                                                    00360000
                                                                        00370000
       FD  CONDENSED-FILE                                               00380000
           LABEL RECORDS ARE STANDARD                                   00390000
           RECORDING MODE IS F                                          00400000
           BLOCK CONTAINS 0 RECORDS.                                    00410000
       01  CONDENSED-RECORD            PIC X(100).                      00420000
      *                                                                 00430000
                                                                        00440000
       WORKING-STORAGE SECTION.                                         00450000
                                                                        00460000
       01  FILLER                      PIC X(28)   VALUE                00470000
                'BEGINNING OF WORKING STORAGE'.                         00480000
                                                                        00490000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00500000
                                                                        00510000
       01  PC-PROGRAM-CONSTANTS.                                        00520000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK280'.    00530000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP337'.  00540000
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00550000
                                                                        00560000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00570000
           05  PE-MSG-01                       PIC X(50) VALUE          00580000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00590000
           05  PE-MSG-02                       PIC X(50) VALUE          00600000
                'ATTEMPT TO UPDATE ROW ON TABLE TMNMCLS FAILED    '.    00610000
           05  PE-MSG-03                       PIC X(50) VALUE          00620000
                'ATTEMPT TO INSERT ROW ON TABLE TMNMCLS FAILED    '.    00630000
           05  PE-MSG-04                       PIC X(50) VALUE          00640000
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00650000
           05  PE-MSG-05                       PIC X(50) VALUE          00660000
                'UPDATE TO TCKRSTINF FAILED                      '.     00670000
           05  PE-MSG-06                       PIC X(50) VALUE          00680000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00690000
           05  PE-MSG-07                       PIC X(50) VALUE          00700000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00710000
      *                                                                 00720000
       01  PS-PROGRAM-SWITCHES.                                         00730000
           05  PS-EOF-CONDENSED-FILE-SW     PIC X(01)     VALUE 'N'.    00740000
               88  PS-EOF-CONDENSED-FILE                  VALUE 'Y'.    00750000
           05  PS-MNMCLS-CURSOR-EOF-SW      PIC X(01)     VALUE 'N'.    00760000
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00770000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00780000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00790000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00800000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00810000
                                                                        00820000
       01  PI-PROGRAM-INDICATORS.                                       00830000
           05  PI-PROCESSING-OPTIONS-IND    PIC X(01)     VALUE 'P'.    00840000
               88  PI-UPDATE-MNMCLS-ROW                   VALUE 'U'.    00850000
               88  PI-INSERT-MNMCLS-ROW                   VALUE 'I'.    00860000
      *                                                                 00870000
       01  PROGRAM-VARIABLES.                                           00880000
           05  PV-RETRY-COUNT                  PIC S9(04) VALUE +0.     00890000
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     00900000
      *                                                                 00910000
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 00920000
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 00930000
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 00940000
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 00950000
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 00960000
           05  PV-DISPLAY-STR                  PIC  9(09) VALUE ZEROES. 00970000
      *                                                                 00980000
       01  PA-PROGRAM-ACCUMULATORS.                                     00990000
           05  PA-RECORD-COUNTS.                                        01000000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      01010000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      01020000
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      01030000
               10  PA-MAJ-CL-PROCESSED   PIC  9(09)  VALUE ZEROES.      01040000
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      01050000
                                                                        01060000
       01  PD-PROGRAM-DISPLAYS.                                         01070000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01080000
       01  SAVE-AREA.                                                   01090000
           05  SV-PRIMARY-KEY.                                          01100000
               10  SV-FSCL-MN-NBR        PIC X(02)  VALUE SPACES.       01110000
               10  SV-FSCL-MN-END-DTE    PIC X(10)  VALUE SPACES.       01120000
               10  SV-DEPT-NBR           PIC X(03)  VALUE SPACES.       01130000
               10  SV-MAJ-CL-NBR         PIC X(02)  VALUE SPACES.       01140000
               10  SV-STR-NBR     COMP   PIC S9(04) VALUE ZEROES.       01150000
           05  SV-UPDATE-FIELDS.                                        01160000
W26603         10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01170000
W26603         10  SV-RCPT-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01180000
W26603         10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01190000
W26603         10  SV-PADJ-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01200000
W26603         10  SV-SLS-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01210000
W26603         10  SV-MKDN-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01220000
W26603         10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01230000
W26603         10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01240000
W26603         10  SV-XFER-OUT-RTL       PIC S9(11)V99  COMP-3 VALUE +0.01250000
W26603         10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01260000
      *                                                                 01270000
      ******************************************************************01280000
      * MLRD118 - M/L MONTHLY MCL STORE DATABASE UPDATE TRANS LAYOUT    01290000
      ******************************************************************01300000
           COPY MLRD118.                                                01310000
      *                                                                 01320000
      ******************************************************************01330000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01340000
      ******************************************************************01350000
           COPY DPWS004.                                                01360000
      *                                                                 01370000
      ******************************************************************01380000
      *  USED FOR DB2 ERRORS                                            01390000
      ******************************************************************01400000
           EXEC SQL                                                     01410000
               INCLUDE SQLCA                                            01420000
           END-EXEC.                                                    01430000
      *                                                                 01440000
      ******************************************************************01450000
      *  M/L MONTH-TO-DATE MAJOR CLASS STORE TABLE                      01460000
      ******************************************************************01470000
           EXEC SQL                                                     01480000
               INCLUDE TMNMCLS                                          01490000
           END-EXEC.                                                    01500000
      *                                                                 01510000
      ***************************************************************** 01520000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01530000
      ***************************************************************** 01540000
           EXEC SQL                                                     01550000
               INCLUDE TCKRSTCN                                         01560000
           END-EXEC.                                                    01570000
      *                                                                 01580000
           EXEC SQL                                                     01590000
               INCLUDE TCKRSTIN                                         01600000
           END-EXEC.                                                    01610000
      *                                                                 01620000
      ******************************************************************01630000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01640000
      ******************************************************************01650000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01660000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +28  COMP. 01670000
           05  CR-CHECKPOINT-RESTART-VAR.                               01680000
               10  CR-PREV-DTL-KEY          PIC  X(19) VALUE SPACES.    01690000
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01700000
                   15  CR-FISCAL-NBR        PIC  X(02).                 01710000
                   15  CR-FISCAL-MN-END-DTE PIC  X(10).                 01720000
                   15  CR-DEPT-NBR          PIC  X(03).                 01730000
                   15  CR-MAJ-CL-NBR        PIC  X(02).                 01740000
                   15  CR-STR-NBR           PIC  S9(04) COMP.           01750000
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01760000
      *                                                                 01770000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01780000
           05  CK-SAVE-PREV-KEY         PIC  X(19) VALUE SPACES.        01790000
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       01800000
               10  CK-FISCAL-NBR        PIC  X(02).                     01810000
               10  CK-FISCAL-MN-END-DTE PIC  X(10).                     01820000
               10  CK-DEPT-NBR          PIC  X(03).                     01830000
               10  CK-MAJ-CL-NBR        PIC  X(02).                     01840000
               10  CK-STR-NBR           PIC  S9(04) COMP.               01850000
                                                                        01860000
                                                                        01870000
      *-------------------*                                             01880000
       PROCEDURE DIVISION.                                              01890000
      *-------------------*                                             01900000
                                                                        01910000
       0000-MLKUP337.                                                   01920000
                                                                        01930000
           PERFORM 1000-INITIALIZATION.                                 01940000
      *                                                                 01950000
           PERFORM 2000-PROCESS-INPUT                                   01960000
                UNTIL PS-EOF-CONDENSED-FILE.                            01970000
      *                                                                 01980000
           PERFORM 8000-EOJ-ROUTINE.                                    01990000
                                                                        02000000
           STOP RUN.                                                    02010000
                                                                        02020000
      ******************************************************************02030000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02040000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02050000
      ******************************************************************02060000
      *                                                                 02070000
       1000-INITIALIZATION.                                             02080000
      *                                                                 02090000
           OPEN INPUT CONDENSED-FILE.                                   02100000
                                                                        02110000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02120000
                                                                        02130000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02140000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02150000
               PERFORM 7500-SELECT-RESTART-INFO                         02160000
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02170000
                                            CR-CHECKPOINT-RESTART-VAR   02180000
               MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY        02190000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                02200000
               DISPLAY 'MONTH NBR       ' CR-FISCAL-NBR                 02210000
               DISPLAY 'MONTH END DATE  ' CR-FISCAL-MN-END-DTE          02220000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   02230000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 02240000
               DISPLAY 'STORE NBR       ' CR-STR-NBR                    02250000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           02260000
           ELSE                                                         02270000
               SET PS-FIRST-COMMIT TO TRUE                              02280000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02290000
           END-IF.                                                      02300000
      *                                                                 02310000
           PERFORM 4000-READ-CONDENSED-FILE UNTIL                       02320000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02330000
            OR PS-EOF-CONDENSED-FILE.                                   02340000
      *                                                                 02350000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02360000
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                02370000
                        PA-INPUT-COUNT                                  02380000
               DISPLAY 'FISCAL MONTH NBR      ' ML118-FSCL-MN-NBR       02390000
               DISPLAY 'FISCAL MONTH END DATE ' ML118-FSCL-MN-END-DTE   02400000
               DISPLAY 'DEPARTMENT NBR        ' ML118-DEPT-NBR          02410000
               DISPLAY 'MAJOR CLASS NBR       ' ML118-MAJ-CL-NBR        02420000
               DISPLAY 'STORE NBR             ' ML118-STR-NBR           02430000
           END-IF.                                                      02440000
                                                                        02450000
           IF PS-EOF-CONDENSED-FILE                                     02460000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02470000
                   CONTINUE                                             02480000
               ELSE                                                     02490000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02500000
           ELSE                                                         02510000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02520000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02530000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02540000
           END-IF.                                                      02550000
                                                                        02560000
      ******************************************************************02570000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02580000
      ******************************************************************02590000
       2000-PROCESS-INPUT.                                              02600000
      *                                                                 02610000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02620000
      *                                                                 02630000
           PERFORM 7000-SELECT-DB-ROW.                                  02640000
      *                                                                 02650000
           IF PS-PROGRAM-DEADLOCKED                                     02660000
               CONTINUE                                                 02670000
           ELSE                                                         02680000
               EVALUATE TRUE                                            02690000
                   WHEN PI-UPDATE-MNMCLS-ROW                            02700000
                       PERFORM 7100-UPDATE-MNMCLS-ROW                   02710000
                   WHEN PI-INSERT-MNMCLS-ROW                            02720000
                       PERFORM 7200-INSERT-MNMCLS-ROW                   02730000
                   WHEN OTHER                                           02740000
                       MOVE '7000-SELECT-DB-ROW' TO PV-PARAGRAPH-NAME   02750000
                       MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED         02760000
                       PERFORM 9999-SQL-ABEND                           02770000
               END-EVALUATE                                             02780000
           END-IF.                                                      02790000
      *                                                                 02800000
           IF PS-PROGRAM-DEADLOCKED                                     02810000
               CONTINUE                                                 02820000
           ELSE                                                         02830000
               PERFORM 7700-COMMIT-PROCESSING                           02840000
               PERFORM 4000-READ-CONDENSED-FILE                         02850000
           END-IF.                                                      02860000
      *                                                                 02870000
      *                                                                 02880000
      ******************************************************************02890000
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         02900000
      ******************************************************************02910000
       2100-CALCULATE-UPDATES.                                          02920000
      * ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES     02930000
      *                                                                 02940000
           COMPUTE SV-SLS-RTL-AMT       =                               02950000
                   MNMCLS-SLS-RTL-AMT      + ML118-SLS-RTL-AMT.         02960000
           COMPUTE SV-MKDN-RTL-AMT      =                               02970000
                   MNMCLS-MKDN-RTL-AMT     + ML118-MKDN-RTL-AMT.        02980000
           COMPUTE SV-MKUP-RTL-AMT      =                               02990000
                   MNMCLS-MKUP-RTL-AMT     + ML118-MKUP-RTL-AMT.        03000000
           COMPUTE SV-XFER-IN-RTL-AMT   =                               03010000
                   MNMCLS-XFER-IN-RTL-AMT  + ML118-XFER-IN-RTL-AMT.     03020000
           COMPUTE SV-XFER-OUT-RTL      =                               03030000
                   MNMCLS-XFER-OUT-RTL-AMT + ML118-XFER-OUT-RTL-AMT.    03040000
           COMPUTE SV-RCPT-RTL-AMT      =                               03050000
                   MNMCLS-RCPT-RTL-AMT + ML118-RCPT-RTL-AMT.            03060000
           COMPUTE SV-RCPT-NET-COST     =                               03070000
                   MNMCLS-RCPT-NET-COST-AMT + ML118-RCPT-NET-COST-AMT.  03080000
           COMPUTE SV-PADJ-RTL-AMT      =                               03090000
                   MNMCLS-PADJ-RTL-AMT     + ML118-PADJ-RTL-AMT.        03100000
           COMPUTE SV-PADJ-NET-COST     =                               03110000
                   MNMCLS-PADJ-NET-COST-AMT + ML118-PADJ-NET-COST-AMT   03120000
           COMPUTE SV-INV-ADJ-RTL-AMT   =                               03130000
                    MNMCLS-INV-ADJ-RTL-AMT + ML118-INV-ADJ-RTL-AMT.     03140000
      *                                                                 03150000
      ******************************************************************03160000
      * READS THE CONDENSED FILE INTO THE TMNMCLS LAYOUT                03170000
      ******************************************************************03180000
       4000-READ-CONDENSED-FILE.                                        03190000
           READ CONDENSED-FILE INTO ML118-UPDATE-REC                    03200000
              AT END                                                    03210000
                MOVE 'Y' TO PS-EOF-CONDENSED-FILE-SW.                   03220000
     *                                                                  03230000
           IF PS-EOF-CONDENSED-FILE                                     03240000
               MOVE ML118-FSCL-MN-NBR         TO CR-FISCAL-NBR          03250000
               MOVE ML118-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   03260000
               MOVE ML118-DEPT-NBR            TO CR-DEPT-NBR            03270000
               MOVE ML118-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          03280000
               MOVE ML118-STR-NBR             TO CR-STR-NBR             03290000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    03300000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03310000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03320000
                                             TCKRSTINF-WORK-STRG-DESC   03330000
               EXEC SQL                                                 03340000
                 UPDATE TCKRSTINF                                       03350000
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03360000
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03370000
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03380000
               END-EXEC                                                 03390000
               IF SQLCODE = 0                                           03400000
                   CONTINUE                                             03410000
               ELSE                                                     03420000
                   MOVE PE-MSG-05          TO PV-OPERATION-ATTEMPTED    03430000
                   MOVE '* 4000-READ-CONDENSED *' TO PV-PARAGRAPH-NAME  03440000
                   PERFORM 9999-SQL-ABEND                               03450000
               END-IF                                                   03460000
            ELSE                                                        03470000
                ADD 1                        TO PA-INPUT-COUNT          03480000
            END-IF.                                                     03490000
      *                                                                 03500000
      ******************************************************************03510000
      * SELECT A ROW FROM THE MTHLY MCL STR TAB  THAT MATCHES INPUT KEY 03520000
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      03530000
      ******************************************************************03540000
       7000-SELECT-DB-ROW.                                              03550000
      *                                                                 03560000
           EXEC SQL                                                     03570000
               SELECT SLS_RTL_AMT                                       03580000
                     ,MKDN_RTL_AMT                                      03590000
                     ,MKUP_RTL_AMT                                      03600000
                     ,XFER_IN_RTL_AMT                                   03610000
                     ,XFER_OUT_RTL_AMT                                  03620000
                     ,RCPT_RTL_AMT                                      03630000
                     ,RCPT_NET_COST_AMT                                 03640000
                     ,PADJ_RTL_AMT                                      03650000
                     ,PADJ_NET_COST_AMT                                 03660000
                     ,INV_ADJ_RTL_AMT                                   03670000
                 INTO :MNMCLS-SLS-RTL-AMT                               03680000
                     ,:MNMCLS-MKDN-RTL-AMT                              03690000
                     ,:MNMCLS-MKUP-RTL-AMT                              03700000
                     ,:MNMCLS-XFER-IN-RTL-AMT                           03710000
                     ,:MNMCLS-XFER-OUT-RTL-AMT                          03720000
                     ,:MNMCLS-RCPT-RTL-AMT                              03730000
                     ,:MNMCLS-RCPT-NET-COST-AMT                         03740000
                     ,:MNMCLS-PADJ-RTL-AMT                              03750000
                     ,:MNMCLS-PADJ-NET-COST-AMT                         03760000
                     ,:MNMCLS-INV-ADJ-RTL-AMT                           03770000
               FROM TMNMCLS                                             03780000
              WHERE   FSCL_MN_NBR         = :ML118-FSCL-MN-NBR          03790000
                AND   FSCL_MN_END_DTE     = :ML118-FSCL-MN-END-DTE      03800000
                AND   DEPT_NBR            = :ML118-DEPT-NBR             03810000
                AND   MAJ_CL_NBR          = :ML118-MAJ-CL-NBR           03820000
                AND   STR_NBR             = :ML118-STR-NBR              03830000
              WITH UR                                                   03840000
           END-EXEC.                                                    03850000
      *                                                                 03860000
           EVALUATE SQLCODE                                             03870000
               WHEN 0                                                   03880000
                   SET PI-UPDATE-MNMCLS-ROW       TO TRUE               03890000
               WHEN +100                                                03900000
                   SET PI-INSERT-MNMCLS-ROW       TO TRUE               03910000
               WHEN -911                                                03920000
                   DISPLAY '*** -911 OCCURRED ON SELECT ***'            03930000
                   ADD +1             TO PV-DEADLOCK-COUNT              03940000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              03950000
                       MOVE '7000-SELECT-DB-ROW' TO                     03960000
                                                    PV-PARAGRAPH-NAME   03970000
                       PERFORM 9000-DEADLOCK-ERROR                      03980000
                   ELSE                                                 03990000
                       PERFORM 7999-RESTART-PROCESS                     04000000
                   END-IF                                               04010000
               WHEN OTHER                                               04020000
                  MOVE ML118-STR-NBR TO PV-DISPLAY-STR                  04030000
                  DISPLAY ML118-FSCL-MN-NBR          ' '                04040000
                          ML118-FSCL-MN-END-DTE      ' '                04050000
                          ML118-DEPT-NBR             ' '                04060000
                          ML118-MAJ-CL-NBR           ' '                04070000
                          PV-DISPLAY-STR                                04080000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     04090000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             04100000
                   PERFORM 9999-SQL-ABEND                               04110000
           END-EVALUATE.                                                04120000
      *                                                                 04130000
      *                                                                 04140000
      ***************************************************************** 04150000
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO, DEPT  04160000
      * AND MAJOR CLASS. CURRENT TABLE VALUES UPDATED TO INCLUDE VALUES 04170000
      * FROM INPUT REC MLRD105                                          04180000
      ***************************************************************** 04190000
       7100-UPDATE-MNMCLS-ROW.                                          04200000
      *                                                                 04210000
           PERFORM 2100-CALCULATE-UPDATES.                              04220000
      *                                                                 04230000
           EXEC SQL                                                     04240000
               UPDATE TMNMCLS                                           04250000
                   SET RCPT_RTL_AMT        = :SV-RCPT-RTL-AMT           04260000
                     , RCPT_NET_COST_AMT   = :SV-RCPT-NET-COST          04270000
                     , PADJ_RTL_AMT        = :SV-PADJ-RTL-AMT           04280000
                     , PADJ_NET_COST_AMT   = :SV-PADJ-NET-COST          04290000
                     , SLS_RTL_AMT         = :SV-SLS-RTL-AMT            04300000
                     , MKDN_RTL_AMT        = :SV-MKDN-RTL-AMT           04310000
                     , MKUP_RTL_AMT        = :SV-MKUP-RTL-AMT           04320000
                     , XFER_IN_RTL_AMT     = :SV-XFER-IN-RTL-AMT        04330000
                     , XFER_OUT_RTL_AMT    = :SV-XFER-OUT-RTL           04340000
                     , INV_ADJ_RTL_AMT     = :SV-INV-ADJ-RTL-AMT        04350000
              WHERE   FSCL_MN_NBR          = :ML118-FSCL-MN-NBR         04360000
                AND   FSCL_MN_END_DTE      = :ML118-FSCL-MN-END-DTE     04370000
                AND   DEPT_NBR             = :ML118-DEPT-NBR            04380000
                AND   MAJ_CL_NBR           = :ML118-MAJ-CL-NBR          04390000
                AND   STR_NBR              = :ML118-STR-NBR             04400000
           END-EXEC                                                     04410000
      *                                                                 04420000
           EVALUATE SQLCODE                                             04430000
               WHEN 0                                                   04440000
                   CONTINUE                                             04450000
               WHEN -911                                                04460000
                   ADD +1             TO PV-DEADLOCK-COUNT              04470000
                   DISPLAY '*** -911 OCCURRED ON UPDATE ***'            04480000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04490000
                       MOVE '7100-UPDATE-MNMCLS-ROW' TO                 04500000
                                                    PV-PARAGRAPH-NAME   04510000
                       PERFORM 9000-DEADLOCK-ERROR                      04520000
                   ELSE                                                 04530000
                       PERFORM 7999-RESTART-PROCESS                     04540000
                   END-IF                                               04550000
               WHEN OTHER                                               04560000
                   MOVE '7100-UPDATE-MNMCLS-ROW' TO PV-PARAGRAPH-NAME   04570000
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED04580000
                   PERFORM 9999-SQL-ABEND                               04590000
           END-EVALUATE.                                                04600000
      *                                                                 04610000
           ADD 1 TO PA-UPDATE-COUNT.                                    04620000
      *                                                                 04630000
      ***************************************************************** 04640000
      * THERE WAS NO MATCHING ROW ON THE MONTHLY MCL STR TAB. INITIAL * 04650000
      * - ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW            * 04660000
      * - CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN RECALC04670000
      ***************************************************************** 04680000
       7200-INSERT-MNMCLS-ROW.                                          04690000
      *                                                                 04700000
      *                                                                 04710000
           EXEC SQL                                                     04720000
               INSERT INTO TMNMCLS                                      04730000
                   (   FSCL_MN_NBR                                      04740000
                     , FSCL_MN_END_DTE                                  04750000
                     , DEPT_NBR                                         04760000
                     , MAJ_CL_NBR                                       04770000
                     , STR_NBR                                          04780000
                     , RCPT_RTL_AMT                                     04790000
                     , RCPT_NET_COST_AMT                                04800000
                     , PADJ_RTL_AMT                                     04810000
                     , PADJ_NET_COST_AMT                                04820000
                     , SLS_RTL_AMT                                      04830000
                     , MKDN_RTL_AMT                                     04840000
                     , MKUP_RTL_AMT                                     04850000
                     , XFER_IN_RTL_AMT                                  04860000
                     , XFER_OUT_RTL_AMT                                 04870000
                     , INV_ADJ_RTL_AMT                                  04880000
                     , SHNK_RTL_AMT                                     04890000
                     , END_INV_RTL_AMT                                  04900000
                     , CUM_MKUP_PCT                                     04910000
                     , END_INV_COST_AMT                                 04920000
                     , SLS_COST_AMT                                     04930000
                     , GRO_MRGN_AMT      )                              04940000
               VALUES                                                   04950000
                   (   :ML118-FSCL-MN-NBR                               04960000
                     , :ML118-FSCL-MN-END-DTE                           04970000
                     , :ML118-DEPT-NBR                                  04980000
                     , :ML118-MAJ-CL-NBR                                04990000
                     , :ML118-STR-NBR                                   05000000
                     , :ML118-RCPT-RTL-AMT                              05010000
                     , :ML118-RCPT-NET-COST-AMT                         05020000
                     , :ML118-PADJ-RTL-AMT                              05030000
                     , :ML118-PADJ-NET-COST-AMT                         05040000
                     , :ML118-SLS-RTL-AMT                               05050000
                     , :ML118-MKDN-RTL-AMT                              05060000
                     , :ML118-MKUP-RTL-AMT                              05070000
                     , :ML118-XFER-IN-RTL-AMT                           05080000
                     , :ML118-XFER-OUT-RTL-AMT                          05090000
                     , :ML118-INV-ADJ-RTL-AMT                           05100000
                     , 0                                                05110000
                     , 0                                                05120000
                     , 0                                                05130000
                     , 0                                                05140000
                     , 0                                                05150000
                     , 0                       )                        05160000
           END-EXEC                                                     05170000
      *                                                                 05180000
           EVALUATE SQLCODE                                             05190000
               WHEN 0                                                   05200000
                   CONTINUE                                             05210000
               WHEN -911                                                05220000
                   ADD +1             TO PV-DEADLOCK-COUNT              05230000
                   DISPLAY '*** -911 OCCURRED ON INSERT ***'            05240000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05250000
                       MOVE '7200-INSERT-MNMCLS-ROW' TO                 05260000
                                                    PV-PARAGRAPH-NAME   05270000
                       PERFORM 9000-DEADLOCK-ERROR                      05280000
                   ELSE                                                 05290000
                       PERFORM 7999-RESTART-PROCESS                     05300000
                   END-IF                                               05310000
               WHEN OTHER                                               05320000
                  MOVE ML118-STR-NBR TO PV-DISPLAY-STR                  05330000
                  DISPLAY ML118-FSCL-MN-NBR          ' '                05340000
                          ML118-FSCL-MN-END-DTE      ' '                05350000
                          ML118-DEPT-NBR             ' '                05360000
                          ML118-MAJ-CL-NBR           ' '                05370000
                          PV-DISPLAY-STR                                05380000
                   MOVE '7200-INSERT-MNMCLS-ROW' TO PV-PARAGRAPH-NAME   05390000
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED05400000
                   PERFORM 9999-SQL-ABEND                               05410000
           END-EVALUATE.                                                05420000
                                                                        05430000
           ADD 1 TO PA-INSERT-COUNT.                                    05440000
                                                                        05450000
      ******************************************************************05460000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05470000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05480000
      *            CONTROL TABLE                                       *05490000
      ******************************************************************05500000
       7300-GET-COMMIT-TIMESTAMP.                                       05510000
                                                                        05520000
           EXEC SQL                                                     05530000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        05540000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05550000
               INTO :PV-COMMIT-TIMESTAMP                                05560000
               FROM TCKRSTCNTL                                          05570000
              WHERE JOB_NAME  = :PC-JOB-NAME                            05580000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05590000
           END-EXEC.                                                    05600000
                                                                        05610000
           EVALUATE TRUE                                                05620000
               WHEN SQLCODE = 0                                         05630000
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  05640000
                   CONTINUE                                             05650000
               WHEN SQLCODE NOT EQUAL ZERO                              05660000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME05670000
                   PERFORM 9999-SQL-ABEND                               05680000
           END-EVALUATE.                                                05690000
      *                                                                 05700000
      *                                                                 05710000
      ******************************************************************05720000
      * 7400-INIT-CHECKPOINT-SELECT                                    *05730000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05740000
      *            IF WE ARE IN A RESTART CONDITION.                   *05750000
      ******************************************************************05760000
       7400-INIT-CHECKPOINT-SELECT.                                     05770000
                                                                        05780000
           EXEC SQL                                                     05790000
             SELECT  CKPT_STAT_CODE                                     05800000
                    ,CKPT_FRQNCY_QTY                                    05810000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         05820000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05830000
               FROM  TCKRSTCNTL                                         05840000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05850000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05860000
           END-EXEC.                                                    05870000
                                                                        05880000
           IF SQLCODE = 0                                               05890000
               CONTINUE                                                 05900000
           ELSE                                                         05910000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  05920000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     05930000
               PERFORM 9999-SQL-ABEND                                   05940000
           END-IF.                                                      05950000
      *                                                                 05960000
      *                                                                 05970000
      ******************************************************************05980000
      * 7500-SELECT-RESTART-INFO                                       *05990000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *06000000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *06010000
      ******************************************************************06020000
       7500-SELECT-RESTART-INFO.                                        06030000
                                                                        06040000
           EXEC SQL                                                     06050000
             SELECT  WORK_STRG_DESC                                     06060000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          06070000
               FROM  TCKRSTINF                                          06080000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06090000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06100000
           END-EXEC.                                                    06110000
                                                                        06120000
           IF SQLCODE = 0                                               06130000
               CONTINUE                                                 06140000
           ELSE                                                         06150000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   06160000
               PERFORM 9999-SQL-ABEND                                   06170000
           END-IF.                                                      06180000
                                                                        06190000
      ******************************************************************06200000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *06210000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *06220000
      ******************************************************************06230000
       7600-SET-CURRENT-TIMESTAMP.                                      06240000
                                                                        06250000
           EXEC SQL                                                     06260000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           06270000
           END-EXEC.                                                    06280000
                                                                        06290000
           IF SQLCODE = 0                                               06300000
               CONTINUE                                                 06310000
           ELSE                                                         06320000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 06330000
               PERFORM 9999-SQL-ABEND                                   06340000
           END-IF.                                                      06350000
      *                                                                 06360000
      *                                                                 06370000
      ******************************************************************06380000
      * 7700-COMMIT-PROCESSING.                                        *06390000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *06400000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*06410000
      ******************************************************************06420000
       7700-COMMIT-PROCESSING.                                          06430000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06440000
                                                                        06450000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06460000
                                                                        06470000
      * PREPARE COMMIT RECORD FOR UPDATE                                06480000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                06490000
               MOVE ML118-FSCL-MN-NBR         TO CR-FISCAL-NBR          06500000
               MOVE ML118-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   06510000
               MOVE ML118-DEPT-NBR            TO CR-DEPT-NBR            06520000
               MOVE ML118-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          06530000
               MOVE ML118-STR-NBR             TO CR-STR-NBR             06540000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    06550000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  06560000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       06570000
                                             TCKRSTINF-WORK-STRG-DESC   06580000
               IF PS-FIRST-COMMIT                                       06590000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                06600000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                06610000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       06620000
               END-IF                                                   06630000
               PERFORM 7800-COMMIT-CHANGES                              06640000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        06650000
           END-IF.                                                      06660000
      *                                                                 06670000
      *                                                                 06680000
      ******************************************************************06690000
      * 7800-COMMIT-CHANGES.                                            06700000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      06710000
      *            TAKE A COMMIT.                                       06720000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    06730000
      ******************************************************************06740000
       7800-COMMIT-CHANGES.                                             06750000
                                                                        06760000
           EXEC SQL                                                     06770000
             UPDATE TCKRSTINF                                           06780000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          06790000
              WHERE JOB_NAME       = :PC-JOB-NAME                       06800000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   06810000
           END-EXEC.                                                    06820000
                                                                        06830000
           IF SQLCODE = 0                                               06840000
               CONTINUE                                                 06850000
           ELSE                                                         06860000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED06870000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06880000
               PERFORM 9999-SQL-ABEND                                   06890000
           END-IF.                                                      06900000
                                                                        06910000
           EXEC SQL                                                     06920000
               COMMIT                                                   06930000
           END-EXEC.                                                    06940000
                                                                        06950000
           IF SQLCODE = 0                                               06960000
               CONTINUE                                                 06970000
           ELSE                                                         06980000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED06990000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07000000
               PERFORM 9999-SQL-ABEND                                   07010000
           END-IF.                                                      07020000
      *                                                                 07030000
      ******************************************************************07040000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   07050000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   07060000
      ******************************************************************07070000
       7900-UPDATE-CHECKPOINT-STATUS.                                   07080000
                                                                        07090000
           EXEC SQL                                                     07100000
             UPDATE  TCKRSTCNTL                                         07110000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        07120000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07130000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07140000
           END-EXEC.                                                    07150000
                                                                        07160000
           IF SQLCODE = 0                                               07170000
               CONTINUE                                                 07180000
           ELSE                                                         07190000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME07200000
               PERFORM 9999-SQL-ABEND                                   07210000
           END-IF.                                                      07220000
                                                                        07230000
           EJECT                                                        07240000
      *                                                                 07250000
      *                                                                 07260000
      ******************************************************************07270000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TMNMCLS ROW FOR UPDATE07280000
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  07290000
      ******************************************************************07300000
       7999-RESTART-PROCESS.                                            07310000
      *                                                                 07320000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           07330000
      *                                                                 07340000
           CLOSE CONDENSED-FILE.                                        07350000
      *                                                                 07360000
           PERFORM 7500-SELECT-RESTART-INFO.                            07370000
      *                                                                 07380000
           DISPLAY '**** RESTART **** '.                                07390000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 07400000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   07410000
                                                                        07420000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      07430000
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        07440000
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       07450000
           IF PS-FIRST-COMMIT                                           07460000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)               07470000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     07480000
                      ' BEGINNING'                                      07490000
           ELSE                                                         07500000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           07510000
                        TCKRSTINF-WORK-STRG-DESC (1:30)                 07520000
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 07530000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         07540000
                    CR-CHECKPOINT-RESTART-AREA                          07550000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                07560000
               DISPLAY 'MONTH NBR       ' CR-FISCAL-NBR                 07570000
               DISPLAY 'MONTH END DATE  ' CR-FISCAL-MN-END-DTE          07580000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   07590000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 07600000
               DISPLAY 'STORE NBR       ' CR-STR-NBR                    07610000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           07620000
           END-IF.                                                      07630000
                                                                        07640000
           OPEN INPUT CONDENSED-FILE.                                   07650000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      07660000
      *                                                                 07670000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          07680000
           PERFORM 4000-READ-CONDENSED-FILE                             07690000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              07700000
           DISPLAY 'INPUT RECORD RESTARTED ON RECORD '                  07710000
                    PA-INPUT-COUNT                                      07720000
           DISPLAY 'FISCAL MONTH NBR      ' ML118-FSCL-MN-NBR.          07730000
           DISPLAY 'FISCAL MONTH END DATE ' ML118-FSCL-MN-END-DTE.      07740000
           DISPLAY 'DEPARTMENT NBR        ' ML118-DEPT-NBR.             07750000
           DISPLAY 'MAJOR CLASS NBR       ' ML118-MAJ-CL-NBR.           07760000
           DISPLAY 'STORE NBR             ' ML118-STR-NBR.              07770000
      ******************************************************************07780000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *07790000
      ******************************************************************07800000
       8000-EOJ-ROUTINE.                                                07810000
      *                                                                 07820000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       07830000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       07840000
                                                                        07850000
      *                                                                 07860000
           DISPLAY SPACES.                                              07870000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         07880000
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        07890000
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        07900000
           DISPLAY SPACES.                                              07910000
      *                                                                 07920000
           CLOSE CONDENSED-FILE.                                        07930000
      *                                                                 07940000
      *                                                                 07950000
      ******************************************************************07960000
      *  PERFROMED BY 7100-UPDATE AND 7200-INSERT                       07970000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   07980000
      ******************************************************************07990000
       9000-DEADLOCK-ERROR.                                             08000000
      *                                                                 08010000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     08020000
               PERFORM 9999-SQL-ABEND.                                  08030000
      *                                                                 08040000
      ******************************************************************08050000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               08060000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             08070000
      ******************************************************************08080000
       9999-SQL-ABEND.                                                  08090000
                                                                        08100000
           MOVE +4013                 TO ABEND-CODE.                    08110000
           DISPLAY '**  ABEND     **'.                                  08120000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              08130000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            08140000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       08150000
                                                                        08160000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         08170000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        08180000
                                                                        08190000
           COPY DPPD004.                                                08200000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           08210000
