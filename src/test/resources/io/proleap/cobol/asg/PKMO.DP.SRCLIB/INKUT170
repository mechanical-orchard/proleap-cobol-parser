      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
      *                                                                         
       PROGRAM-ID.    INKUT170.                                                 
       AUTHOR.        JULIE SEIDLER.                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  10/06/98.                                                 
       DATE-COMPILED.                                                           
      *                                                                         
      *****************************************************************         
      *    CREATE SYCSORT CONTROL CARDS TO BE USED AGAINST THE                  
      *    POS FILE.                                                            
      *****************************************************************         
      * 08/09/99                                                                
      *    CHANGE MADE: REMOVED REFERENCE TO DPG51-SYSTEM-DATE                  
      *    CHANGED BY: JULIE SEIDLER                                            
      * 06/26/00                                                                
      *    CHANGE MADE: NEED TO DETERMINE TOTAL SALES FOR THE STORE.            
      *                 CHANGED OUTREC CARD TO INCLUDE SLS-AMT AND              
      *                 THE QUAL-CODE, OWNED-RTL-AMT                            
      *                                                                         
      *                 ALSO CHANGE TINVPAR CURSOR TO LOOK AT THE               
      *                 PHY-CNT-TAKEN INDICATOR AND THE SCAN-IND                
      *                 TO EXTRACT STORES                                       
      *                 INSTEAD OF THE SLS-ADJ-COMPL INDICATOR                  
      *    CHANGED BY: JULIE SEIDLER                                            
RB1002*                                                                         
RB1002* 10/11/02 - RONNA BUTZ                                                   
RB1002*            CHANGE THIS PROGRAM TO CREATE SORT CARDS THAT                
RB1002*            WILL MAP COPYBOOK KDU1806 TO INRD016.  THIS CHANGE           
RB1002*            IS TO USE THE NEW COSA DAILY EXTRACT FILE.                   
RB1002*            CHANGE CODE IS RB1002                                        
RB1002*                                                                         
W21732* 01/18/03 - J.S.NAVIS RAJA,IGSI        WR# W21732                        
W21732*            ADDED DEPT AND CLASS IN THE SORT FIELDS.CHANGED              
W21732*            THE INREC FIELDS POSITION SINCE INRD016 COPYBOOK             
W21732*            WAS CHANGED.                                                 
W21732*                                                                         
      * 08/18/0004 V. LEE YANGE               WR# W26600                        
      *            STORE NUMBER EXPANSION - REPLACE STORE-NBR REF               
      *            WITH LOC-NBR.                                                
28545 *                                                                         
28545 * 08/10/2006 B. GRAHAM - STRATAGEM      WR# W28545                        
28545 *            DEPARTMENT LEVEL INVENTORY PROJECT                           
28545 *            JOIN TINVPAR TO TINCNTL                                      
      *                                                                         
W33304*                                                                         
W33304* 08/26/2008 THERESE RAMSEYER           WR# W33304                        
W33304*            HILO - HOLD INVENTORY LEDGER OPEN                            
W33304*            --> CHANGE 'IND' TO 'CDE' FOR THE SCH_PHY_CNT_CDE            
W33304*            --> CHANGE THE VALUE OF THE CODE FROM 'Y' TO 'PI'            
W33304*                FOR THE SCHD_PHY_CNT_CDE IN TINVPAR_CSR                  
W33304*            --> ADD NON-DELETED LOCATION FILTER TO THE WHERE             
W33304*                CLAUSE OF THE TINVPAR_CSR                                
W33304*                "AND A.LOC_LOC_STAT_CDE = 'IN' "  IN CSR                 
      *                                                                         
RONNA2* 05/06/2022 RONNA MCDONALD             CHG#99999999999                   
RONNA2*            ADD ANOTHER OUTPUT FILE THAT WILL LIST                       
RONNA2*            STORES THAT HAVE A PLANNED INVENTORY DATE                    
RONNA2*            MATCHING THE DATE OF THE CONTROL CARD.                       
RONNA2*            THIS LIST OF STORES WILL BE USED TO BUILD                    
RONNA2*            A CONTROL CARD FOR MASS ADD/DELETE ADJUSTMENTS               
RONNA2*            FOR STORES COUNTING THE SALES FLOOR BEFORE THE               
RONNA2*            PLANNED INVENTORY DATE                                       
      *                                                                         
      *****************************************************************         
                                                                                
      ******************************************************************        
      *                                                                *        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
      *                                                                         
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
      *                                                                         
       DATA DIVISION.                                                           
      *                                                                         
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
      *                                                                         
       01  INPUT-RECORD                       PIC X(80).                        
      *                                                                         
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
      *                                                                         
       01  OUTPUT-RECORD                      PIC X(80).                        
      *                                                                         
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  ABEND-CODE                          PIC S9(04)  VALUE +0             
                                                           COMP SYNC.           
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES       PIC S9(04)  VALUE +3                        
                                                COMP SYNC.                      
           05  PC-CALENDAR-ROUTINE               PIC  X(08) VALUE               
                                                     'DPKUT500'.                
           05  PC-PROGRAM-NAME      PIC  X(08)  VALUE 'INKUT170'.               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-TINVPAR-CSR-SW    PIC X           VALUE 'N'.           
               88  PS-END-OF-TINVPAR-CSR                   VALUE 'Y'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-SQL-SUB                      PIC S9(4)   VALUE ZERO           
                                                           COMP.                
           05  PV-PARAGRAPH-NAME               PIC X(30)   VALUE SPACE.         
           05  PV-DB2-OPER-ATTEMPTED           PIC X(30)   VALUE SPACE.         
           05  PV-MAX-RETRIES                  PIC S9(3)   VALUE +3.            
                                                                                
       01  CC-CONTROL-CARD                      VALUE SPACE.                    
           05  CC-DATE                          PIC X(10).                      
           05  FILLER                           PIC X(70).                      
                                                                                
W26600 01  SC-SORT-CARD-AREA.                                                   
W26600     05  SC-SORT-FIELD-1.                                                 
W26600         10  FILLER                  PIC X(43)       VALUE                
W26600             '  SORT FIELDS=(01,04,CH,A,'.                                
W26600         10  FILLER                  PIC X(37)       VALUE                
W26600             '* IN016-STR-NBR'.                                           
W26600     05  SC-SORT-FIELD-2.                                                 
W26600         10  FILLER                     PIC X(43)    VALUE                
W26600             '               29,08,CH,A,'.                                
W26600         10  FILLER                     PIC X(37)    VALUE                
W26600             '* IN016-DEPT-NBR/IN016-CL-NBR'.                             
W26600     05  SC-SORT-FIELD-3.                                                 
W26600         10  FILLER                     PIC X(43)    VALUE                
W26600             '               05,08,CH,A,'.                                
W26600         10  FILLER                     PIC X(37)    VALUE                
W26600             '* IN016-SKU'.                                               
W26600     05  SC-SORT-FIELD-4.                                                 
W26600         10  FILLER                     PIC X(43)    VALUE                
W26600             '               13,04,PD,A,'.                                
W26600         10  FILLER                     PIC X(37)    VALUE                
W26600             '* IN016-TRAN-DTE'.                                          
W26600     05  SC-SORT-FIELD-5.                                                 
W26600         10  FILLER                     PIC X(43)    VALUE                
W26600             '               17,03,PD,A)'.                                
W26600         10  FILLER                     PIC X(37)    VALUE                
W26600             '* IN016-TRAN-TIMESTAMP'.                                    
W26600     05  SC-INCLUDE-COND-1.                                               
W26600         10  FILLER                     PIC X(30)    VALUE                
W26600             '  INCLUDE COND=((52,01,CH,EQ,C'.                            
W26600         10  FILLER                     PIC X(01)    VALUE QUOTE.         
W26600         10  FILLER                     PIC X(01)    VALUE SPACE.         
W26600         10  FILLER                     PIC X(01)    VALUE QUOTE.         
W26600         10  FILLER                     PIC X(10)    VALUE                
W26600             '),AND,'.                                                    
W26600         10  FILLER                     PIC X(37)    VALUE                
W26600             '* D040-NORMAL-TRANS'.                                       
W26600     05  SC-INCLUDE-COND-2.                                               
W26600         10  FILLER                     PIC X(17)    VALUE SPACES.        
W26600             88  SC-FIRST-STORE                      VALUE                
W26600                 '                ('.                                     
W26600             88  SC-REMAINING-STORE                  VALUE SPACES.        
W26600         10  FILLER                     PIC X(13)    VALUE                
W26600                 '02,04,CH,EQ,C'.                                         
W26600         10  FILLER                     PIC X(01)    VALUE QUOTE.         
W26600         10  SC-INCLUDE-LOC-NBR         PIC 9999     VALUE ZEROES.        
W26600         10  FILLER                     PIC X(01)    VALUE QUOTE.         
W26600         10  FILLER                     PIC X(07)    VALUE SPACES.        
W26600             88  SC-MORE-STORE                       VALUE                
W26600                 ',OR,'.                                                  
W26600             88  SC-NO-MORE-STORE                    VALUE                
W26600                 '))   '.                                                 
W26600         10  FILLER                     PIC X(37)    VALUE                
W26600             '* D040-STORENO'.                                            
RB1002     05  SC-SORT-INREC-FIELDS-1.                                          
RB1002         10  FILLER                     PIC X(43) VALUE                   
W26600             '  INREC FIELDS=(02,04,'.                                    
RB1002         10  FILLER                     PIC X(37) VALUE                   
RB1002             '* D040-STORENO            '.                                
RB1002     05  SC-SORT-INREC-FIELDS-2.                                          
RB1002         10  FILLER                     PIC X(43) VALUE                   
RB1002             '                30,08,'.                                    
RB1002         10  FILLER                     PIC X(37) VALUE                   
RB1002             '* D040-SKU                '.                                
RB1002     05  SC-SORT-INREC-FIELDS-3.                                          
RB1002         10  FILLER                     PIC X(43) VALUE                   
RB1002             '                19,06,ZD,PD,'.                              
RB1002         10  FILLER                     PIC X(37) VALUE                   
RB1002             '* D040-DATE               '.                                
RB1002     05  SC-SORT-INREC-FIELDS-4.                                          
RB1002         10  FILLER                     PIC X(43) VALUE                   
RB1002             '                25,03,'.                                    
RB1002         10  FILLER                     PIC X(37) VALUE                   
RB1002             '* D040-TIME               '.                                
RB1002     05  SC-SORT-INREC-FIELDS-5.                                          
RB1002         10  FILLER                     PIC X(43) VALUE                   
RB1002             '                46,02,'.                                    
RB1002         10  FILLER                     PIC X(37) VALUE                   
RB1002             '* D040-SALE-TYPE          '.                                
RB1002     05  SC-SORT-INREC-FIELDS-6.                                          
RB1002         10  FILLER                     PIC X(43) VALUE                   
RB1002             '                39,03,'.                                    
RB1002         10  FILLER                     PIC X(37) VALUE                   
RB1002             '* D040-SALE-UNITS         '.                                
RB1002     05  SC-SORT-INREC-FIELDS-7.                                          
RB1002         10  FILLER                     PIC X(43) VALUE                   
RB1002             '                67,04,'.                                    
RB1002         10  FILLER                     PIC X(37) VALUE                   
RB1002             '* D040-OWNED-UNIT-RTL-AMT '.                                
RB1002     05  SC-SORT-INREC-FIELDS-8.                                          
RB1002         10  FILLER                     PIC X(43) VALUE                   
W21732             '                X''F0'',57,03,'.                            
RB1002         10  FILLER                     PIC X(37) VALUE                   
RB1002             '* D040-DEPT               '.                                
RB1002     05  SC-SORT-INREC-FIELDS-9.                                          
RB1002         10  FILLER                     PIC X(43) VALUE                   
W21732             '                2X''F0'',60,02,'.                           
RB1002         10  FILLER                     PIC X(37) VALUE                   
RB1002             '* D040-CLASS              '.                                
RB1002     05  SC-SORT-INREC-FIELDS-10.                                         
RB1002         10  FILLER                     PIC X(43) VALUE                   
W21732             '                62,04,'.                                    
RB1002         10  FILLER                     PIC X(37) VALUE                   
RB1002             '* D040-MAJOR-CLASS        '.                                
RB1002     05  SC-SORT-INREC-FIELDS-11.                                         
RB1002         10  FILLER                     PIC X(43) VALUE                   
W21732             '                66,01)'.                                    
RB1002         10  FILLER                     PIC X(37) VALUE                   
RB1002             '* D040-SALE-RETURN-CODE   '.                                
           05  SC-SORT-FIELD-END.                                               
RB1002         10  FILLER                     PIC X(80) VALUE                   
RB1002             '  END'.                                                     
                                                                                
W26600 01  PT-TABLE-AREA.                                                       
W26600     05  PT-STR-MAX                  PIC S9(4)       VALUE +9999          
W26600                         COMP.                                            
W26600     05  PT-STR-INDEX                PIC S9(4)       VALUE ZEROES         
W26600                         COMP.                                            
W26600     05  PT-STR-TBL OCCURS 9999 TIMES.                                    
W26600         10  PT-LOC-NBR              PIC 9(04)       VALUE ZEROES.        
W26600                                                                          
                                                                                
                                                                                
      ******************************************************************        
      * CALENDAR ROUTINE WORK AREA.                                             
      ******************************************************************        
                                                                                
           COPY DPWS500.                                                        
28545 *                                                                 01380000
28545 ******************************************************************01320000
28545 *  DCLGEN FOR THE INVENTORY CONTROL TABLE.                        01330000
28545 ******************************************************************01340000
28545      EXEC SQL                                                     01350000
28545          INCLUDE TINCNTL                                          01360000
28545      END-EXEC.                                                    01370000
28545 *                                                                 01380000
                                                                                
      ******************************************************************        
      *  DCLGEN FOR THE INVENTORY PARAMETERS DB2 TABLE.                         
      ****************************************************************          
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 COMM AREA - DECLARE CURSOR TINVPAR_CSR                              
      *                                                                         
      *                 GET SCANNING STORES THAT ARE ACTUALLY                   
      *                 TAKING A PHYSICAL COUNT IN THE STORES                   
      *****************************************************************         
      *                                                                         
            EXEC SQL                                                            
              DECLARE TINVPAR_CSR CURSOR FOR                                    
W26600            SELECT  LOC_NBR                                               
                        , ACTL_INV_DTE                                          
28545             FROM TINVPAR A                                                
28545                 ,TINCNTL B                                                
                  WHERE ACTL_INV_DTE = :CC-DATE                                 
W33304              AND SCHD_PHY_CNT_CDE = 'PI'                                 
                    AND SCAN_INV_IND = 'Y'                                      
28545               AND A.INV_ID = B.INV_ID                                     
28545               AND B.ACTV_IND = 'Y'                                        
W33304              AND A.LOC_INV_STAT_CDE = 'IN'                               
W26600            ORDER BY LOC_NBR                                              
            END-EXEC.                                                           
28545 ******************************************************************01320000
      *                                                                         
      *  DB2 FORMATTED ERROR MESSAGE                                            
      *                                                                         
28545 ******************************************************************01320000
           COPY DPWS004.                                                        
                                                                                
      *                                                                         
      ******************************************************************        
      *  COMMUNICATIONS AREA FOR DB2                                   *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *                                                                         
      ******************************************************************        
      *  CONTROLS THE MAIN PROCESSING OF THE PROGRAM                            
      ******************************************************************        
       A100-PROGRAM-MAINLINE.                                                   
                                                                                
           PERFORM B100-OPEN-FILE.                                              
                                                                                
           PERFORM B200-READ-INPUT-DATE.                                        
                                                                                
           PERFORM B300-VALIDATE-DATE.                                          
                                                                                
           PERFORM Z350-OPEN-CURSOR.                                            
                                                                                
           PERFORM Z400-FETCH-CURSOR.                                           
                                                                                
           PERFORM C200-PROCESS-TINVPAR                                         
              VARYING PT-STR-INDEX                                              
              FROM 1 BY 1                                                       
W26600        UNTIL PT-STR-INDEX > PT-STR-MAX                                   
                 OR PS-END-OF-TINVPAR-CSR.                                      
                                                                                
W26600     IF PT-STR-INDEX > PT-STR-MAX                                         
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PGM = INKUT170'                                      
               DISPLAY '** PARAGRAPH = A100-PROGRAM-MAIN'                       
W26600         DISPLAY '** GT THAN ' PT-STR-MAX ' STORES SELECTED'              
               DISPLAY '** MUST EXPAND WORKING STORAGE TABLE'                   
               MOVE +4003 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE.                                
                                                                                
           MOVE ZERO TO PT-STR-INDEX.                                           
           PERFORM C300-WRITE-SORT-CARDS.                                       
                                                                                
           PERFORM Z850-CLOSE-CURSOR.                                           
           PERFORM D400-CLOSE-FILE.                                             
      *                                                                         
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *  B100 - OPENS INPUT FILE                                                
      ******************************************************************        
       B100-OPEN-FILE.                                                          
                                                                                
            OPEN INPUT INPUT-FILE                                               
                 OUTPUT OUTPUT-FILE.                                            
                                                                                
      ******************************************************************        
      *  B200  READS DATE FROM INPUT FILE                                       
      ******************************************************************        
                                                                                
       B200-READ-INPUT-DATE.                                                    
            READ INPUT-FILE                                                     
               INTO CC-CONTROL-CARD.                                            
                                                                                
                                                                                
      ******************************************************************        
      *   CHECKS DATE IS NOT SPACES AND IS A VALID CALENDAR DATE                
      ******************************************************************        
                                                                                
       B300-VALIDATE-DATE.                                                      
                                                                                
           DISPLAY '*** INKUT170 INPUT CONTROL CARD:' CC-CONTROL-CARD.          
                                                                                
           IF CC-DATE = SPACE                                                   
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PGM = INKUT170'                                      
               DISPLAY '** PARAGRAPH = B300-VALIDATE-DATE'                      
               DISPLAY '** INPUT CONTROL CARD MISSING OR BLANK'                 
               MOVE +4003 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
                                                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                               
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
           MOVE CC-DATE                  TO DPG52-LK-DATE-INPUT.                
                                                                                
           CALL PC-CALENDAR-ROUTINE                                             
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-SEVERE-ERROR                                          
                   DISPLAY '** ABEND **'                                        
                   DISPLAY '** PGM = INKUT170'                                  
                   DISPLAY '** PARAGRAPH = B300-VALIDATE-DATE'                  
                   DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'            
                   DISPLAY '** USING CONTROL CARD DATE AS INPUT.'               
                   DISPLAY '** ERROR MESSAGE = ' DPG54-ERROR-MESSAGE            
                   DISPLAY '** DPG51 = ' DPG51                                  
                   DISPLAY '** DPG52 = ' DPG52                                  
                   DISPLAY '** DPG53 = ' DPG53                                  
                   DISPLAY '** DPG54 = ' DPG54                                  
                   MOVE +4003 TO ABEND-CODE                                     
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               WHEN OTHER                                                       
                   CONTINUE                                                     
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  FETCHES STORES, WITH THE ACTUAL INVENTORY DATE INDICATED               
      *  ON THE CONTROL CARD.                                                   
      ******************************************************************        
       C200-PROCESS-TINVPAR.                                                    
W26600     MOVE INVPAR-LOC-NBR   TO PT-LOC-NBR(PT-STR-INDEX)                    
           PERFORM Z400-FETCH-CURSOR.                                           
                                                                                
      ******************************************************************        
      *  C300  CREATE THE SYCSORT CONTROL CARDS.                                
      ******************************************************************        
                                                                                
       C300-WRITE-SORT-CARDS.                                                   
           WRITE OUTPUT-RECORD  FROM SC-SORT-FIELD-1.                           
           DISPLAY SC-SORT-FIELD-1.                                             
                                                                                
W26600     WRITE OUTPUT-RECORD  FROM SC-SORT-FIELD-2.                           
W26600     DISPLAY SC-SORT-FIELD-2.                                             
                                                                                
W26600     WRITE OUTPUT-RECORD  FROM SC-SORT-FIELD-3.                           
W26600     DISPLAY SC-SORT-FIELD-3.                                             
                                                                                
RB1002     WRITE OUTPUT-RECORD  FROM SC-SORT-FIELD-4.                           
RB1002     DISPLAY SC-SORT-FIELD-4.                                             
RB1002                                                                          
RB1002     WRITE OUTPUT-RECORD  FROM SC-SORT-FIELD-5.                           
RB1002     DISPLAY SC-SORT-FIELD-5.                                             
RB1002                                                                          
W26600     SET SC-MORE-STORE     TO TRUE.                                       
W26600     PERFORM C350-WRITE-INCLUDE-CARDS                                     
               VARYING PT-STR-INDEX FROM 1 BY 1                                 
               UNTIL SC-NO-MORE-STORE.                                          
                                                                                
           WRITE OUTPUT-RECORD  FROM SC-SORT-INREC-FIELDS-1.                    
RB1002     DISPLAY SC-SORT-INREC-FIELDS-1.                                      
                                                                                
RB1002     WRITE OUTPUT-RECORD  FROM SC-SORT-INREC-FIELDS-2.                    
RB1002     DISPLAY SC-SORT-INREC-FIELDS-2.                                      
RB1002                                                                          
RB1002     WRITE OUTPUT-RECORD  FROM SC-SORT-INREC-FIELDS-3.                    
RB1002     DISPLAY SC-SORT-INREC-FIELDS-3.                                      
RB1002                                                                          
RB1002     WRITE OUTPUT-RECORD  FROM SC-SORT-INREC-FIELDS-4.                    
RB1002     DISPLAY SC-SORT-INREC-FIELDS-4.                                      
RB1002                                                                          
RB1002     WRITE OUTPUT-RECORD  FROM SC-SORT-INREC-FIELDS-5.                    
RB1002     DISPLAY SC-SORT-INREC-FIELDS-5.                                      
RB1002                                                                          
RB1002     WRITE OUTPUT-RECORD  FROM SC-SORT-INREC-FIELDS-6.                    
RB1002     DISPLAY SC-SORT-INREC-FIELDS-6.                                      
RB1002                                                                          
RB1002     WRITE OUTPUT-RECORD FROM SC-SORT-INREC-FIELDS-7.                     
RB1002     DISPLAY SC-SORT-INREC-FIELDS-7.                                      
RB1002                                                                          
RB1002      WRITE OUTPUT-RECORD                                                 
RB1002         FROM SC-SORT-INREC-FIELDS-8.                                     
RB1002      DISPLAY SC-SORT-INREC-FIELDS-8.                                     
RB1002                                                                          
RB1002      WRITE OUTPUT-RECORD                                                 
RB1002         FROM SC-SORT-INREC-FIELDS-9.                                     
RB1002      DISPLAY SC-SORT-INREC-FIELDS-9.                                     
RB1002                                                                          
RB1002      WRITE OUTPUT-RECORD                                                 
RB1002         FROM SC-SORT-INREC-FIELDS-10.                                    
RB1002      DISPLAY SC-SORT-INREC-FIELDS-10.                                    
RB1002                                                                          
RB1002      WRITE OUTPUT-RECORD                                                 
RB1002         FROM SC-SORT-INREC-FIELDS-11.                                    
RB1002      DISPLAY SC-SORT-INREC-FIELDS-11.                                    
                                                                                
            WRITE OUTPUT-RECORD                                                 
               FROM SC-SORT-FIELD-END.                                          
            DISPLAY SC-SORT-FIELD-END.                                          
                                                                                
      ******************************************************************        
      *  C350  CREATE THE SYNCSORT CONTROL CARDS.                               
      ******************************************************************        
       C350-WRITE-INCLUDE-CARDS.                                                
           IF PT-STR-INDEX = 1                                                  
               WRITE OUTPUT-RECORD  FROM SC-INCLUDE-COND-1                      
               DISPLAY SC-INCLUDE-COND-1                                        
               SET SC-FIRST-STORE      TO TRUE                                  
           ELSE                                                                 
               SET SC-REMAINING-STORE  TO TRUE                                  
           END-IF.                                                              
                                                                                
           MOVE PT-LOC-NBR(PT-STR-INDEX)  TO SC-INCLUDE-LOC-NBR.                
                                                                                
           IF PT-LOC-NBR(PT-STR-INDEX + 1) = ZEROES                             
               SET SC-NO-MORE-STORE    TO TRUE                                  
           ELSE                                                                 
               SET SC-MORE-STORE       TO TRUE                                  
           END-IF.                                                              
                                                                                
           WRITE OUTPUT-RECORD  FROM SC-INCLUDE-COND-2.                         
           DISPLAY SC-INCLUDE-COND-2.                                           
                                                                                
      ****************************************************************          
      *  D400 - CLOSES INPUT FILE                                               
      ****************************************************************          
                                                                                
       D400-CLOSE-FILE.                                                         
            CLOSE INPUT-FILE.                                                   
                                                                                
                                                                                
       Z350-OPEN-CURSOR.                                                        
      ****************************************************************          
      *  OPEN STORE CURSOR                                                      
      ****************************************************************          
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PV-MAX-RETRIES) OR                           
                     (SQLCODE = +100) OR                                        
                     (SQLCODE = 0)                                              
                                                                                
               EXEC SQL                                                         
                  OPEN TINVPAR_CSR                                              
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +100                                          
                       SET PS-END-OF-TINVPAR-CSR TO TRUE                        
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < 0                                             
                   WHEN SQLCODE > 0                                             
                        MOVE 'Z350-OPEN-CURSOR'                                 
                              TO PV-PARAGRAPH-NAME                              
                        MOVE 'OPEN OF CURSOR'                                   
                              TO PV-DB2-OPER-ATTEMPTED                          
                        PERFORM Z900-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB > PV-MAX-RETRIES                                       
                MOVE 'Z350-OPEN-TINVPAR TABLE' TO PV-PARAGRAPH-NAME             
                MOVE 'EXCEEDED MAX RETRIES' TO PV-DB2-OPER-ATTEMPTED            
                PERFORM Z900-SQL-ABEND                                          
           END-IF.                                                              
      *                                                                         
                                                                                
      ******************************************************************        
      *   FETCH TINVPAR CURSOR                                                  
      ******************************************************************        
       Z400-FETCH-CURSOR.                                                       
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 100                                       
                         OR SQLCODE = 0)                                        
                                                                                
               EXEC SQL                                                         
                   FETCH TINVPAR_CSR                                            
W26600             INTO  :INVPAR-LOC-NBR,                                       
                         :INVPAR-ACTL-INV-DTE                                   
               END-EXEC                                                         
      *                                                                         
               DISPLAY 'INVPAR-LOC-NBR ' INVPAR-LOC-NBR                         
               DISPLAY 'INVPAR-ACTL-INV-DTE ' INVPAR-ACTL-INV-DTE               
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +100                                          
                       SET PS-END-OF-TINVPAR-CSR TO TRUE                        
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z400-FETCH TINVPAR'                                
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'FETCH CURSOR'                                      
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z400-FETCH TINVPAR'                                        
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'OPEN RETRIES EXCEEDED'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
                                                                                
                                                                                
       Z850-CLOSE-CURSOR.                                                       
      ****************************************************************          
      *  CLOSE TINVPAR_CSR CURSOR                                               
      ****************************************************************          
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PV-MAX-RETRIES) OR                           
                     (SQLCODE = +100) OR                                        
                     (SQLCODE = 0)                                              
                                                                                
               EXEC SQL                                                         
                  CLOSE TINVPAR_CSR                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < 0                                             
                   WHEN SQLCODE > 0                                             
                        MOVE 'Z850-CLOSE-CURSOR'                                
                              TO PV-PARAGRAPH-NAME                              
                        MOVE 'CLOSE OF TINVPAR TABLE'                           
                              TO PV-DB2-OPER-ATTEMPTED                          
                        PERFORM Z900-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB > PV-MAX-RETRIES                                       
                MOVE 'Z850-CLOSE-CURSOR' TO PV-PARAGRAPH-NAME                   
                MOVE 'EXCEEDED MAX RETRIES' TO PV-DB2-OPER-ATTEMPTED            
                PERFORM Z900-SQL-ABEND                                          
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * PERFORMS THE ABEND FOR THE PROGRAM IN THE                               
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                         
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = INKUT170'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.                  
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           IF SQLCODE NOT = -911                                                
               EXEC SQL                                                         
                   ROLLBACK                                                     
               END-EXEC                                                         
           END-IF.                                                              
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
