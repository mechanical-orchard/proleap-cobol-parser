000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    PCKUT106.                                                 
000600 AUTHOR.        DENISE HAHN                                               
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   JULY, 2012.                                               
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM CREATES AN EXTRACT FILE FROM TLBPTDA FOR GENERIC          
001300*  SML'S CREATED FOR THE CURRENT DATE                                     
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
002900     SELECT CONTROL-FILE                                                  
002910            ASSIGN TO INP01.                                              
003000                                                                          
003300     SELECT SML-REQ-DATA-EXTRACT                                          
003400            ASSIGN TO OUT02.                                              
003410                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
004700                                                                          
004720 FD  CONTROL-FILE                                                         
004730     RECORDING MODE IS F                                                  
004740     LABEL RECORDS ARE STANDARD                                           
004750     RECORD CONTAINS 080                                                  
004760     BLOCK CONTAINS 0 RECORDS                                             
004770     DATA RECORD IS OLD-CONTROL-RECORD.                                   
004780 01  CONTROL-RECORD             PIC X(080).                               
004790                                                                          
004791                                                                          
004800 FD  SML-REQ-DATA-EXTRACT                                                 
004900     RECORDING MODE IS F                                                  
005000     LABEL RECORDS ARE STANDARD                                           
005100     BLOCK CONTAINS 0 RECORDS.                                            
005200                                                                          
005300 01  SML-REQ-DATA-RECORD        PIC X(100).                               
005470                                                                          
005500                                                                          
005600******************************************************************        
005700 WORKING-STORAGE SECTION.                                                 
005800******************************************************************        
005900                                                                          
007110*-----------------------------------------------------------------        
007120*  PKT_TXT LAYOUT IN "G" RECORDS FROM TUPLDER                             
007130*-----------------------------------------------------------------        
007140     COPY PCWS263.                                                        
007150                                                                          
007160*-----------------------------------------------------------------        
007170*  CONTROL CART INPUT                                                     
007180*-----------------------------------------------------------------        
007181 01  PV-CONTROL-CARD.                                                     
007182     05  PV-RUN-TYPE                  PIC X(01).                          
007183         88  PV-CURRENT-DAY                     VALUE 'C'.                
007184         88  PV-BATCH-PROCESS-DAY               VALUE 'B'.                
007186     05  FILLER                       PIC X(79).                          
007191                                                                          
007192*-----------------------------------------------------------------        
007193*  OUTPUT RECORD LAYOUT                                                   
007194*-----------------------------------------------------------------        
007195     COPY PCWS264.                                                        
007196                                                                          
007200*-----------------------------------------------------------------        
007500*  DB2 FORMATTED MESSAGE AREA                                             
007510*-----------------------------------------------------------------        
007700     COPY DPWS004.                                                        
007800                                                                          
007806*-----------------------------------------------------------------        
007807*  COMMUNICATIONS AREA FOR DB2                                            
007808*-----------------------------------------------------------------        
007809     EXEC SQL                                                             
007810          INCLUDE SQLCA                                                   
007811     END-EXEC.                                                            
007812                                                                          
007813*-----------------------------------------------------------------        
007820*  UPLOAD ERROR TABLE                                                     
007840*-----------------------------------------------------------------        
007850     EXEC SQL                                                             
007860          INCLUDE TUPLDER                                                 
007870     END-EXEC.                                                            
007880                                                                          
007890*-----------------------------------------------------------------        
007900*  COMPANY CONTROL TABLE                                                  
008000*-----------------------------------------------------------------        
008010     EXEC SQL                                                             
008020          INCLUDE TCOCNTL                                                 
008030     END-EXEC.                                                            
008040                                                                          
008100/                                                                         
010320*-----------------------------------------------------------------        
010330*                                                                         
010340*-----------------------------------------------------------------        
011200 01  ABEND-CODE                    PIC S9(04) VALUE +0                    
011300                                              COMP SYNC.                  
011400                                                                          
011500 01  PS-PROGRAM-SWITCHES.                                                 
011600     05  PS-EOF-LABEL-DATA-CSR-SW  PIC  X(01) VALUE 'N'.                  
011700         88  PS-EOF-LABEL-DATA-CSR            VALUE 'Y'.                  
011800         88  PS-MORE-LABEL-DATA-CSR           VALUE 'N'.                  
011801     05  PS-SML-REQUESTED-SW       PIC  X(01) VALUE 'N'.                  
011802         88  PS-ALL-SML-REQUESTED             VALUE 'Y'.                  
011803         88  PS-NOT-ALL-SML-REQUESTED         VALUE 'N'.                  
011804     05  PS-END-CONTROL-FILE-SW    PIC  X(01) VALUE 'N'.                  
011805         88  PS-END-CONTROL-FILE              VALUE 'Y'.                  
011806         88  PS-END-CONTROL-FILE-NO           VALUE 'N'.                  
011900                                                                          
012000                                                                          
012100 01  PC-PROGRAM-CONSTANTS.                                                
012400     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08) VALUE 'PCKUT106'.           
013000     05  PC-CURRENT-TIMESTAMP      PIC  X(26) VALUE SPACES.               
013100                                                                          
013200 01  PV-PROGRAM-VARIABLES.                                                
013400     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
013500     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
014200     05  PV-DISPLAY-COUNT            PIC S9(09)    VALUE ZERO             
014300                                                   COMP SYNC.             
014400     05  PV-WRITE-COUNTER            PIC S9(09)    VALUE ZERO             
014500                                                   COMP SYNC.             
014600     05  PV-PROCESS-DATE             PIC  X(10)    VALUE SPACES.          
014700     05  PV-CURRENT-DATE             PIC  X(10)    VALUE SPACES.          
015900                                                                          
015910*-----------------------------------------------------------------        
015920*  LABEL PRINT DATA TO BE DELETED CURSOR                                  
015930*-----------------------------------------------------------------        
016000     EXEC SQL                                                             
016100          DECLARE LABEL_DATA_CURSOR CURSOR FOR                            
016200           SELECT OPER_UNT_ID                                             
016210                 ,PRC_TMST                                                
016300                 ,PKT_TXT                                                 
017700             FROM TUPLDER                                                 
018000            WHERE DATE(PRC_TMST) =  :PV-PROCESS-DATE                      
018100              AND UPLD_TRN_CDE   =  'G'                                   
018110            ORDER BY OPER_UNT_ID                                          
018120                    ,PRC_TMST                                             
018200     END-EXEC.                                                            
018300                                                                          
018400                                                                          
018500******************************************************************        
018600 PROCEDURE DIVISION.                                                      
018700******************************************************************        
018800                                                                          
018801                                                                          
018810*-----------------------------------------------------------------        
018820* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
018830*      PROGRAM.                                                           
018840*-----------------------------------------------------------------        
018900 0000-PROGRAM-MAINLINE.                                                   
019400                                                                          
019500     PERFORM 1000-INITIALIZE.                                             
019510                                                                          
019520     DISPLAY '********************************************'.              
019530     DISPLAY '*                                          *'.              
019540     DISPLAY '*                                          *'.              
019600     IF  PS-MORE-LABEL-DATA-CSR                                           
019700         PERFORM 2000-PROCESS-LABEL-DATA                                  
019800           UNTIL PS-EOF-LABEL-DATA-CSR                                    
019900     END-IF.                                                              
020000     PERFORM 9000-EOJ-ROUTINE.                                            
020100                                                                          
020120     DISPLAY '*                                          *'.              
020130     DISPLAY '*                                          *'.              
020131     DISPLAY '********************************************'.              
020140                                                                          
020200     STOP RUN.                                                            
020300                                                                          
020310                                                                          
020320*-----------------------------------------------------------------        
020330*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
020340*    -- OPEN OUTPUT FILE                                                  
020350*    -- OPEN CURSOR                                                       
020360*    -- FETCH CURSOR                                                      
020370*-----------------------------------------------------------------        
020400 1000-INITIALIZE.                                                         
021100                                                                          
021400     OPEN INPUT  CONTROL-FILE.                                            
021500     OPEN OUTPUT SML-REQ-DATA-EXTRACT.                                    
021600                                                                          
021610     READ CONTROL-FILE  INTO                                              
021620           PV-CONTROL-CARD                                                
021630     AT END                                                               
021640         SET PS-END-CONTROL-FILE      TO TRUE                             
021650     NOT AT END                                                           
021670         DISPLAY 'CONTROL CARD:'                                          
021680         DISPLAY PV-CONTROL-CARD                                          
021690         DISPLAY SPACES                                                   
021691     END-READ.                                                            
021692                                                                          
021700     PERFORM 1100-GET-PROCESS-DATE.                                       
021710                                                                          
021800     PERFORM 7000-OPEN-LABEL-DATA-CURSOR.                                 
021900     PERFORM 7100-FETCH-LABEL-DATA-CURSOR.                                
022000                                                                          
022010                                                                          
022100*-----------------------------------------------------------------        
022200*                                                                         
022300*-----------------------------------------------------------------        
025400 1100-GET-PROCESS-DATE.                                                   
025401                                                                          
025404     EXEC SQL                                                             
025405          SELECT DISTINCT(DATE(BTCH_PRC_DTE))                             
025406                ,CURRENT DATE                                             
025408            INTO                                                          
025409                 :COCNTL-BTCH-PRC-DTE                                     
025410                ,:PV-CURRENT-DATE                                         
025411            FROM                                                          
025412                 TCOCNTL                                                  
025413     END-EXEC                                                             
025414                                                                          
025415     EVALUATE TRUE                                                        
025416         WHEN SQLCODE = ZERO                                              
025417              CONTINUE                                                    
025418         WHEN SQLCODE = -904                                              
025419         WHEN SQLCODE = -911                                              
025420              CONTINUE                                                    
025421         WHEN SQLWARN0 NOT = SPACES                                       
025422         WHEN SQLCODE < ZERO                                              
025423         WHEN SQLCODE > ZERO                                              
025424             MOVE '1100-GET-PROCESS-DATE          '                       
025425                                 TO PV-PARAGRAPH-NAME                     
025426             MOVE 'GET BATCH PROCESS DATE         '                       
025427                                 TO PV-DB2-OPERATION-ATTEMPTED            
025428             PERFORM 9100-SQL-ABEND                                       
025429     END-EVALUATE.                                                        
025430                                                                          
025431     IF   PV-CURRENT-DAY                                                  
025432          MOVE PV-CURRENT-DATE      TO  PV-PROCESS-DATE                   
025434     ELSE                                                                 
025435          MOVE COCNTL-BTCH-PRC-DTE  TO  PV-PROCESS-DATE                   
025436     END-IF.                                                              
025437                                                                          
025439     DISPLAY 'PCKUT106 PROCESS DATE -- '  PV-PROCESS-DATE.                
025440                                                                          
025441                                                                          
025449*-----------------------------------------------------------------        
025450*  LOOP THROUGH THE RESULTS TABLE.                                        
025451*  FORMAT THE OUTPUT RECORD.  WRITE THE RECORDS THAT MEET CRITERIA.       
025452*-----------------------------------------------------------------        
025460 2000-PROCESS-LABEL-DATA.                                                 
026000                                                                          
026001     SET  PS-NOT-ALL-SML-REQUESTED  TO  TRUE.                             
026002     MOVE  UPLDER-PKT-TXT  TO  PC263-UPLOAD-ERROR-RECORD.                 
026010     INITIALIZE PC264-UPLOAD-ERROR-RECORD.                                
026040     MOVE  PC263-LBL-REQ-TYPE    TO  PC264-LBL-REQ-TYPE.                  
026090     MOVE  PC263-USER-ID         TO  PC264-USER-ID.                       
026092     MOVE  PC263-STR-NBR         TO  PC264-STR-NBR.                       
026094     MOVE  PC263-PRTR-ID         TO  PC264-PRTR-ID.                       
026096     MOVE  PC263-NO-OF-LBLS      TO  PC264-NO-OF-LBLS.                    
026098     MOVE  PC263-START-MFST-NBR  TO  PC264-START-MFST-NBR.                
026104                                                                          
026105     IF  PC263-START-MFST-NBR   =  PC263-END-MFST-NBR                     
026106         PERFORM 8000-WRITE-LABEL-DATA-RECORD                             
026107     ELSE                                                                 
026108         PERFORM 2100-EXPAND-REQUEST                                      
026109           UNTIL PS-ALL-SML-REQUESTED                                     
026110     END-IF.                                                              
026120                                                                          
026200     PERFORM 7100-FETCH-LABEL-DATA-CURSOR.                                
026300                                                                          
026310                                                                          
026311**  IF 10 SML'S WERE REQUESTED - THEN 10 RECORDS WILL BE WRITTEN          
026312 2100-EXPAND-REQUEST.                                                     
026313                                                                          
026314     PERFORM 8000-WRITE-LABEL-DATA-RECORD.                                
026315     ADD 1  TO  PC264-START-MFST-NBR                                      
026316     IF  PC264-START-MFST-NBR   >  PC263-END-MFST-NBR                     
026317         SET  PS-ALL-SML-REQUESTED  TO  TRUE                              
026318     END-IF.                                                              
026319                                                                          
026320                                                                          
026321*-----------------------------------------------------------------        
026330*  OPEN THE CURSOR.                                                       
026340*-----------------------------------------------------------------        
026400 7000-OPEN-LABEL-DATA-CURSOR.                                             
027300                                                                          
027400     EXEC SQL                                                             
027500          OPEN LABEL_DATA_CURSOR                                          
027600     END-EXEC                                                             
027700                                                                          
027800     EVALUATE TRUE                                                        
027810         WHEN SQLCODE = ZERO                                              
027820              CONTINUE                                                    
027900         WHEN SQLCODE = -904                                              
028000         WHEN SQLCODE = -911                                              
028100              CONTINUE                                                    
028200         WHEN SQLWARN0 NOT = SPACES                                       
028300         WHEN SQLCODE < ZERO                                              
028400         WHEN SQLCODE > ZERO                                              
028500             MOVE '7000-OPEN-LABEL-DATA-CURSOR    '                       
028600                                 TO PV-PARAGRAPH-NAME                     
028700             MOVE 'OPEN THE LABEL-DATA-CSR        '                       
028800                                 TO PV-DB2-OPERATION-ATTEMPTED            
028900             PERFORM 9100-SQL-ABEND                                       
029200     END-EVALUATE.                                                        
029400                                                                          
030200                                                                          
030210*-----------------------------------------------------------------        
030220*  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.                 
030230*-----------------------------------------------------------------        
030300 7100-FETCH-LABEL-DATA-CURSOR.                                            
030500                                                                          
030700     EXEC SQL                                                             
030800          FETCH LABEL_DATA_CURSOR                                         
031000           INTO :UPLDER-OPER-UNT-ID                                       
031100               ,:UPLDER-PRC-TMST                                          
031200               ,:UPLDER-PKT-TXT                                           
032300     END-EXEC.                                                            
032400                                                                          
032500     EVALUATE TRUE                                                        
032600         WHEN SQLCODE = ZERO                                              
032700             CONTINUE                                                     
032800         WHEN SQLCODE = +100                                              
032900             SET PS-EOF-LABEL-DATA-CSR  TO  TRUE                          
033000                                                                          
033100         WHEN SQLWARN0 NOT = SPACES                                       
033200         WHEN SQLCODE < ZERO                                              
033300         WHEN SQLCODE > ZERO                                              
033400             MOVE '7100-FETCH-LABEL-DATA-CURSOR  '                        
033500                                 TO PV-PARAGRAPH-NAME                     
033600             MOVE 'FETCH THE UPLDER-CURSOR        '                       
033700                                 TO PV-DB2-OPERATION-ATTEMPTED            
033800             PERFORM 9100-SQL-ABEND                                       
033900     END-EVALUATE.                                                        
034000                                                                          
034120                                                                          
034121*-----------------------------------------------------------------        
034122*  WRITE THE SHIPMENT-UNIT-RECORD                                         
034123*-----------------------------------------------------------------        
034130 8000-WRITE-LABEL-DATA-RECORD.                                            
034140                                                                          
034150     MOVE UPLDER-OPER-UNT-ID  TO  PC264-OPER.                             
034160     MOVE UPLDER-PRC-TMST     TO  PC264-PRC-TMST.                         
034200                                                                          
034600     WRITE SML-REQ-DATA-RECORD                                            
034700      FROM PC264-UPLOAD-ERROR-RECORD.                                     
034800     ADD +1 TO PV-WRITE-COUNTER.                                          
035690                                                                          
035700                                                                          
035710*-----------------------------------------------------------------        
035711*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
035720*-----------------------------------------------------------------        
035800 9000-EOJ-ROUTINE.                                                        
036200     CLOSE CONTROL-FILE                                                   
036300           SML-REQ-DATA-EXTRACT.                                          
036400                                                                          
036500     MOVE PV-WRITE-COUNTER TO PV-DISPLAY-COUNT.                           
036600     DISPLAY 'TOTAL LABEL BATCH RECORDS WRITTEN ' PV-DISPLAY-COUNT.       
036700                                                                          
037400     EXEC SQL                                                             
037500          CLOSE LABEL_DATA_CURSOR                                         
037600     END-EXEC.                                                            
037700                                                                          
037800     EVALUATE TRUE                                                        
037810         WHEN SQLCODE = ZERO                                              
037820              CONTINUE                                                    
037900         WHEN SQLCODE = -904                                              
038000         WHEN SQLCODE = -911                                              
038100              CONTINUE                                                    
038200         WHEN SQLWARN0 NOT = SPACES                                       
038300         WHEN SQLCODE < ZERO                                              
038400         WHEN SQLCODE > ZERO                                              
038500              MOVE '9000-EOJ-ROUTINE               '                      
038600                                 TO PV-PARAGRAPH-NAME                     
038700              MOVE 'CLOSE THE LABEL_DATA_CURSOR     '                     
038800                                 TO PV-DB2-OPERATION-ATTEMPTED            
038900              PERFORM 9100-SQL-ABEND                                      
039200     END-EVALUATE.                                                        
039400                                                                          
040200                                                                          
040210*-----------------------------------------------------------------        
040220*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
040230*  ERROR OR WARNING CODE OCCURS.                                          
040240*-----------------------------------------------------------------        
040300 9100-SQL-ABEND.                                                          
040800     DISPLAY '9100-SQL-ABEND'.                                            
040900     DISPLAY '** ABEND     **'.                                           
041000     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
041100     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
041200     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
041300                                                                          
041400******************************************************************        
041500* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
041600* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
041700* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
041800* FORMAT.                                                                 
041900******************************************************************        
042000     COPY DPPD004.                                                        
042100                                                                          
042200     MOVE +4013                  TO ABEND-CODE.                           
042300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
042310/                                                                         
