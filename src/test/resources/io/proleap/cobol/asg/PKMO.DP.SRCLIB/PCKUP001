000100******************************************************************02/19/96
000200 IDENTIFICATION DIVISION.                                         RCK00190
000300******************************************************************   LV014
000400                                                                     CL**2
000500 PROGRAM-ID.    PCKUP001.                                            CL**2
000600 AUTHOR.        VIRGENE LENNEMAN.                                    CL**2
000700 INSTALLATION.  KOHLS.                                               CL**2
000800 DATE-WRITTEN   DECEMBER, 1997.                                      CL**2
000900 DATE-COMPILED.                                                      CL**2
001000                                                                     CL**2
001100******************************************************************   CL**2
001200*  THIS PROGRAM UPDATES ROWS ON THE TDISTHD TABLE FOR DELETED        CL**2
001210*  RECEIVERS                                                         CL**2
001300******************************************************************   CL**2
001400                                                                     CL**2
001500******************************************************************   CL**2
001600 ENVIRONMENT DIVISION.                                               CL**2
001700******************************************************************   CL**2
001800                                                                     CL**2
001900 CONFIGURATION SECTION.                                              CL**2
002000                                                                     CL**2
002100 SOURCE-COMPUTER.        IBM-3090.                                   CL**2
002200 OBJECT-COMPUTER.        IBM-3090.                                   CL**2
002300                                                                     CL**2
002400 INPUT-OUTPUT SECTION.                                               CL**2
002500                                                                     CL**2
002600 FILE-CONTROL.                                                       CL**2
002700                                                                     CL**2
002800                                                                     CL**2
002900     SELECT RECEIVER-EXT-FILE                                        CL**2
003000         ASSIGN TO IN01.                                             CL**2
003100                                                                     CL**2
003200******************************************************************   CL**2
003300 DATA DIVISION.                                                      CL**2
003400******************************************************************   CL**2
003500                                                                     CL**2
003600 FILE SECTION.                                                       CL**2
003700                                                                     CL**2
003800 FD  RECEIVER-EXT-FILE                                               CL**2
003900     RECORDING MODE IS F                                             CL**2
004000     LABEL RECORDS ARE STANDARD                                      CL**2
004100     BLOCK CONTAINS 0 RECORDS.                                       CL**2
004200                                                                     CL**2
004300 01  RCVR-EXT-RECORD            PIC  X(200).                         CL**2
004400                                                                     CL**2
004500*                                                                    CL**2
004600******************************************************************   CL**2
004700 WORKING-STORAGE SECTION.                                            CL**2
004800******************************************************************   CL**2
004900*                                                                    CL**2
005000*                                                                    CL**2
005500*                                                                    CL**2
005600*                                                                    CL**2
005700******************************************************************   CL**2
005800*  RECORD LAYOUT FOR THE RECEIVER-EXT-FILE                           CL**2
005900******************************************************************   CL**2
006000     COPY PCRD003.                                                   CL**2
006100*                                                                    CL**2
006200******************************************************************   CL**2
006300*  DB2 FORMATTED MESSAGE AREA                                        CL**2
006400******************************************************************   CL**2
006500     COPY DPWS004.                                                   CL**2
006600*                                                                    CL**2
006700*                                                                    CL**2
006800     COPY SQLCA2.                                                    CL**2
006900*                                                                    CL**2
007000******************************************************************   CL**2
007100*  COMMUNICATIONS AREA FOR DB2                                       CL**2
007200******************************************************************   CL**2
007300*                                                                    CL**2
007400     EXEC SQL                                                        CL**2
007500          INCLUDE SQLCA                                              CL**2
007600     END-EXEC.                                                       CL**2
007700*                                                                    CL**2
007800*                                                                    CL**2
007900******************************************************************   CL**2
008000*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE DISTRIBUTION          CL**2
008100*  TRANSACTION HEADER TABLE                                          CL**2
008200******************************************************************   CL**2
008300     EXEC SQL                                                        CL**2
008400          INCLUDE TDISTHD                                            CL**2
008500     END-EXEC.                                                       CL**2
008600*                                                                    CL**2
008700 01  ABEND-CODE                      PIC S9(04)    VALUE +0          CL**2
008800                                                   COMP SYNC.        CL**2
008900                                                                     CL**2
009000 01  PS-PROGRAM-SWITCHES.                                            CL**2
009300     05  PS-EOF-SW                   PIC  X(01)    VALUE 'N'.        CL**2
009400         88  PS-EOF                                VALUE 'Y'.        CL**2
009500         88  PS-NOT-EOF                            VALUE 'N'.        CL**2
009600                                                                     CL**2
009700 01  PC-PROGRAM-CONSTANTS.                                           CL**2
010000     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE             CL**2
010100         'PCKUP001'.                                                 CL**2
010400     05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3          CL**2
010500                                                   COMP SYNC.        CL**2
010800                                                                     CL**2
011100     05  PC-COMMIT-HOLD              PIC 9(3)      VALUE 100.        CL*14
011110     05  PC-C                        PIC X(1)      VALUE 'C'.        CL*14
011111     05  PC-D                        PIC X(1)      VALUE 'D'.        CL*14
011112     05  PC-V                        PIC X(1)      VALUE 'V'.        CL*14
011200                                                                     CL**2
011300 01  PV-PROGRAM-VARIABLES.                                           CL**2
011500     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.     CL**2
011600     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.     CL**2
012500     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                CL**2
012600                                                                     CL**2
013700     05  PV-NOTFND-COUNT             PIC S9(04)  VALUE +0            CL**2
013800                                                    COMP SYNC.       CL**2
013900     05  PV-READ-COUNT               PIC S9(04)  VALUE +0            CL**2
014000                                                    COMP SYNC.       CL**2
014001     05  PV-UPDATE-COUNT             PIC S9(04)  VALUE +0            CL**2
014002                                                    COMP SYNC.       CL**2
014003     05  PV-UPDATE-COUNTER           PIC S9(04)  VALUE +0            CL**2
014004                                                    COMP SYNC.       CL**2
014100     05  PV-COMMIT-COUNT             PIC S9(04)  VALUE +0            CL**2
014200                                                    COMP SYNC.       CL**2
014300     05  PV-COMMIT-COUNTER           PIC 9(03)     VALUE ZERO.       CL**2
014400                                                                     CL**2
014500 01  PV-COMMIT-INFO.                                                 CL**2
014600     05  FILLER                      PIC X(25) VALUE                 CL**2
014700        ' ORD/SEQ/LINE/: '.                                          CL**2
014800     05  PV-COMMIT-KEY.                                              CL**2
014900         10  PV-COMMIT-ORD-NBR       PIC X(09).                      CL**5
015000         10  FILLER                  PIC X(01) VALUE SPACE.          CL**9
015100         10  PV-COMMIT-SEQ-NBR       PIC X(09).                      CL*11
015200         10  FILLER                  PIC X(01) VALUE SPACE.          CL**9
015300         10  PV-COMMIT-LNE-NBR       PIC X(09).                      CL*11
015700     05  FILLER                      PIC X(24) VALUE SPACE.          CL*12
015800     05  FILLER                      PIC X(22) VALUE                 CL*12
015900        ' # RECS UPDATED     : '.                                    CL**2
016000     05  PV-RECS-UPDATED-COUNT       PIC 9(7)  VALUE 0.              CL**2
016100*                                                                    CL**2
016200*                                                                    CL**2
020600******************************************************************   CL**2
020700 PROCEDURE DIVISION.                                                 CL**2
020800******************************************************************   CL**2
020900                                                                     CL**2
021000 0000-PROGRAM-MAINLINE.                                              CL**2
021100******************************************************************   CL**2
021200* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE        CL**2
021300*      PROGRAM.                                                      CL**2
021400******************************************************************   CL**2
021500                                                                     CL**2
021600     PERFORM 1000-INITIALIZE.                                        CL**2
021700     PERFORM 2000-PROCESS-INPUT                                      CL**2
021800       UNTIL PS-EOF.                                                 CL**2
021900     PERFORM 9000-EOJ-ROUTINE.                                       CL**2
022000                                                                     CL**2
022100     STOP RUN.                                                       CL**2
022200*0000-EXIT.                                                          CL**2
022300*                                                                    CL**2
022400*                                                                    CL**2
022500 1000-INITIALIZE.                                                    CL**2
022600******************************************************************   CL**2
022700*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                       CL**2
023000******************************************************************   CL**2
023100                                                                     CL**2
023700     OPEN INPUT  RECEIVER-EXT-FILE.                                  CL**2
023800                                                                     CL**2
024100     MOVE +0 TO PV-COMMIT-COUNTER                                    CL**2
024200                                                                     CL**2
024300     PERFORM 7000-READ-INPUT.                                        CL**2
024400                                                                     CL**2
024600*1000-EXIT.                                                          CL**2
024700*                                                                    CL**2
026100 2000-PROCESS-INPUT.                                                 CL**2
026200******************************************************************   CL**2
026300*  PROCESS THE INPUT FILE AND UPDATE TDISTHD.PRCG_STAT_CDE TO A      CL**2
026400*  'C'  WHEN PURCHASEORDER/RECEIVER SEQ AND LINE # MATCH INPUT REC   CL**2
026500*  AND PRCG_STAT_CDE NOT CURRENTLY "V" OR "D"                        CL**2
026600******************************************************************   CL**2
026700                                                                     CL**2
           IF PC003-ORD-LNE-NBR = 0                                             
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 8100-UPDATE-TDISTHD                                     CL
              ADD +1 TO PV-COMMIT-COUNTER                                     CL
           END-IF.                                                              
027100                                                                     CL**2
027200     IF PV-COMMIT-COUNTER > PC-COMMIT-HOLD                           CL**2
027300         EXEC SQL                                                    CL**2
027400            COMMIT                                                   CL**2
027500         END-EXEC                                                    CL**2
027600                                                                     CL**2
027700         IF SQLCODE NOT = +0                                         CL**2
027800             DISPLAY '### COMMIT FAILED ! ###'                       CL**2
027900             PERFORM 9100-SQL-ABEND                                  CL**2
028000         END-IF                                                      CL**2
028100                                                                     CL**2
028200         ADD PV-COMMIT-COUNTER           TO PV-COMMIT-COUNT          CL**2
028300         MOVE DISTHD-ORD-NBR             TO PV-COMMIT-ORD-NBR        CL**2
028400         MOVE DISTHD-RCVR-SEQ-NBR        TO PV-COMMIT-SEQ-NBR        CL**2
028500         MOVE DISTHD-ORD-LNE-NBR         TO PV-COMMIT-LNE-NBR        CL**2
028700                                                                     CL**2
028800         MOVE PV-COMMIT-COUNT    TO PV-RECS-UPDATED-COUNT            CL**2
028900         DISPLAY 'COMMIT TAKEN: ' PV-COMMIT-INFO                     CL**2
029000                                                                     CL**2
029100         MOVE +0 TO PV-COMMIT-COUNTER                                CL**2
029200     END-IF.                                                         CL**2
029300                                                                     CL**2
029400     PERFORM 7000-READ-INPUT.                                        CL**2
029500                                                                     CL**2
029600*2000-EXIT.                                                          CL**2
029700                                                                     CL**2
029701 7000-READ-INPUT.                                                    CL**2
029702******************************************************************   CL**2
029703*  READ   THE INPUT FILE                                             CL**2
029704******************************************************************   CL**2
029705     READ RECEIVER-EXT-FILE INTO                                     CL**2
029706          PC003-RECEIVER-EXT-RECORD                                  CL**2
029707          AT END SET PS-EOF  TO TRUE.                                CL**2
029708                                                                     CL**2
029709     IF PS-NOT-EOF                                                   CL**2
029710         ADD +1  TO PV-READ-COUNT                                    CL**2
029711         MOVE PC003-PO-NBR         TO DISTHD-ORD-NBR                 CL**2
029712         MOVE PC003-REC-SEQ-NBR    TO DISTHD-RCVR-SEQ-NBR            CL**2
029713         MOVE PC003-ORD-LNE-NBR    TO DISTHD-ORD-LNE-NBR             CL**2
029800     END-IF.                                                         CL**2
029900*                                                                    CL**2
043500*7000-EXIT.  EXIT.                                                   CL**2
043501                                                                     CL**2
043502 8100-UPDATE-TDISTHD.                                                CL**2
043503     PERFORM                                                         CL**2
043504         WITH TEST AFTER                                             CL**2
043505         VARYING PV-UPDATE-COUNTER                                   CL**2
043506         FROM 1 BY 1                                                 CL**2
043507         UNTIL (PV-UPDATE-COUNTER > PC-MAX-RETRIES                   CL**2
043508                   OR SQLCODE = 0                                    CL**2
043509                   OR SQLCODE = +100)                                CL**2
043510                                                                     CL**2
043511         EXEC SQL                                                    CL**2
043512             UPDATE TDISTHD                                          CL**2
043513             SET PRCG_STAT_CDE = :PC-C                               CL**2
043514                 WHERE ORD_NBR      = :DISTHD-ORD-NBR                CL**2
043515                   AND RCVR_SEQ_NBR = :DISTHD-RCVR-SEQ-NBR           CL**2
043516                   AND ORD_LNE_NBR  = :DISTHD-ORD-LNE-NBR            CL**2
043517                   AND PRCG_STAT_CDE NOT IN (':PC-V', ':PC-D')       CL**2
043518         END-EXEC                                                    CL**2
043519                                                                     CL**2
043520         EVALUATE TRUE                                               CL**2
043521             WHEN SQLCODE = 0                                        CL**2
043522                 ADD +1           TO PV-UPDATE-COUNT                 CL**2
043523             WHEN SQLCODE = +100                                     CL**2
043524                 ADD +1           TO PV-NOTFND-COUNT                 CL**2
043525             WHEN SQLCODE = -904                                     CL**2
043526             WHEN SQLCODE = -913                                     CL**2
043527                 CONTINUE                                            CL**2
043528             WHEN SQLCODE < 0                                        CL**2
043529             WHEN SQLCODE > 0                                        CL**2
043530             WHEN SQLWARN0 = 'W'                                     CL**2
043531                 MOVE '8100-UPDATE-TDISTHD'                          CL**6
043532                             TO PV-PARAGRAPH-NAME                    CL**2
043533                 MOVE 'UPDATE OF TDISTHD'                            CL**2
043534                             TO PV-DB2-OPERATION-ATTEMPTED           CL**2
043535                 PERFORM 9100-SQL-ABEND                              CL**2
043536         END-EVALUATE                                                CL**2
043537     END-PERFORM.                                                    CL**2
043538                                                                     CL**2
043539     IF PV-UPDATE-COUNTER > PC-MAX-RETRIES                           CL**2
043540         MOVE '8100-UPDATE-TDISTHD'                                  CL**6
043541             TO PV-PARAGRAPH-NAME                                    CL**2
043542         MOVE 'DEADLOCK * UPDATE TDISTHD'                            CL**2
043543             TO PV-DB2-OPERATION-ATTEMPTED                           CL**2
043544         PERFORM 9100-SQL-ABEND                                      CL**2
043545     END-IF.                                                         CL**2
043550                                                                     CL**2
043600 9000-EOJ-ROUTINE.                                                   CL**2
043700******************************************************************   CL**2
043800*  CLOSE OUTPUT FILE.  DISPLAY RECORD COUNTS                         CL**2
043900******************************************************************   CL**2
044000     CLOSE RECEIVER-EXT-FILE.                                        CL**2
044100     MOVE PV-READ-COUNT   TO PV-DISPLAY-COUNT.                       CL**2
044101     DISPLAY 'TOTAL RECORDS READ   : '  PV-DISPLAY-COUNT.            CL**2
044102     MOVE PV-UPDATE-COUNT TO PV-DISPLAY-COUNT.                       CL**2
044103     DISPLAY 'TOTAL RECORDS UPDATED: '  PV-DISPLAY-COUNT.            CL**2
044104     MOVE PV-NOTFND-COUNT TO PV-DISPLAY-COUNT.                       CL**2
044110     DISPLAY 'TOTAL RECORDS NOT FOUND: '  PV-DISPLAY-COUNT.          CL**2
047600*9000-EXIT.                                                          CL**2
047700*                                                                    CL**2
047800*                                                                    CL**2
047900 9100-SQL-ABEND.                                                     CL**2
048000******************************************************************   CL**2
048100*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL       CL**2
048200*  ERROR OR WARNING CODE OCCURS.                                     CL**2
048300******************************************************************   CL**2
048400     DISPLAY '9100-SQL-ABEND'.                                       CL**2
048500     DISPLAY '** ABEND     **'.                                      CL**2
048600     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.           CL**2
048700     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                 CL**2
048800     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.        CL**2
048900     DISPLAY '***'                                                   CL**2
049000     DISPLAY '*** LAST COMMIT TAKEN **' PV-COMMIT-INFO               CL**2
049100     DISPLAY '***'                                                   CL**2
049200     DISPLAY '*** COMMITTED UPDATE COUNTS FOLLOW: ***'               CL**2
049300     DISPLAY '***'                                                   CL**2
049400     EXEC SQL                                                        CL**2
049500          ROLLBACK                                                   CL**2
049600     END-EXEC.                                                       CL**2
049700******************************************************************   CL**2
049800* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-       CL**2
049900* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE            CL**2
050000* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE        CL**2
050100* FORMAT.                                                            CL**2
050200******************************************************************   CL**2
050300     COPY DPPD004.                                                   CL**2
050400                                                                     CL**2
050500     MOVE +4013                  TO ABEND-CODE.                      CL**2
050600     CALL 'ILBOABN0' USING ABEND-CODE.                               CL**2
050700*9100-EXIT.                                                          CL**2
