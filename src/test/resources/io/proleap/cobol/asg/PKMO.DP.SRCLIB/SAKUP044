      ****************************************************************  00010001
       IDENTIFICATION DIVISION.                                         00020001
      ****************************************************************  00030001
                                                                        00040001
       PROGRAM-ID.             SAKUP044.                                00050004
       AUTHOR.                 DAN PAYNTER.                             00060001
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070001
       DATE-WRITTEN            OCTOBER 2013.                            00080001
       DATE-COMPILED.                                                   00090001
                                                                        00100001
      ****************************************************************  00110001
      *                                                                 00120001
      *   ***  OFFLINE ASSOCIATE DISCOUNT UPDATE PROGRAM  ***           00130001
      *                                                                 00140001
      *  RELEASE 1 OF THE POINT OF COMMERCE PROJECT HAS NO IN-STORE     00150001
      *  SOLUTION FOR CUSTOMER MDM BEING OFFLINE.  THAT MEANS THAT IT'S 00160001
      *  POSSIBLE FOR AN ASSOCIATE CUSTOMER TO FAIL TO RECEIVE THEIR    00170001
      *  ASSOCIATE DISCOUNT AT THE REGISTER.  THE SALES HUB DOESN'T     00180001
      *  EXECUTE ANY BUSINESS LOGIC FOR THE POC XML MESSAGES, THEREFORE,00190001
      *  THIS BATCH PROGRAM WILL PROCESS MISSED ASSOCIATE DISCOUNTS     00200001
      *  FOR POC TRANSACTIONS AND WILL UPDATE THE APPROPRIATE TEPITM    00210001
      *  COLUMNS AS WELL AS INSERTING THE REQUIRED TEPEID ENTRIES.      00220001
      *                                                                 00230001
      *   INPUT: INP01 - TEPITM AND TEPEID ASSOCIATE DISCOUNT UPDATE    00240001
      *                  FILE CREATED BY SAKUT042 AND PASSING THROUGH   00250004
      *                  SAKUT043                                       00260004
      ****************************************************************  00270001
      *                                                                 00280001
      * CHANGE LOG:                                                     00290001
      * DATE:  2013-10-15                                               00300001
      * PRGMR: DAN PAYNTER                                              00310001
      * WHY:   NEW PROGRAM TO ADD MISSED POC ASSOCIATE DISCOUNTS        00320001
      *                                                                 00320106
260107* CHG0260107  JIM HUNTER  08-19-21  ADD COPYBOOK DPWS991 TO MAKE  00321006
260107*                                   THE CALL TO DPKUT901 DYNAMIC. 00322006
      ******************************************************************00330001
      ****************************************************************  00340001
       ENVIRONMENT DIVISION.                                            00350001
      ****************************************************************  00360001
                                                                        00370001
       CONFIGURATION SECTION.                                           00380001
                                                                        00390001
       SOURCE-COMPUTER.        IBM-3090.                                00400001
                                                                        00410001
       OBJECT-COMPUTER.        IBM-3090.                                00420001
                                                                        00430001
       INPUT-OUTPUT SECTION.                                            00440001
                                                                        00450001
       FILE-CONTROL.                                                    00460001
                                                                        00470001
      ****************************************************************  00480001
       DATA DIVISION.                                                   00490001
      ****************************************************************  00500001
                                                                        00510001
       FILE SECTION.                                                    00520001
                                                                        00530001
                                                                        00540001
      ***************************************************************** 00550001
       WORKING-STORAGE SECTION.                                         00560001
      ***************************************************************** 00570001
       01  FILLER                            PIC X(50)  VALUE           00580001
           ' *** WORKING STORAGE STARTS HERE FOR SAKUP044 *** '.        00590004
                                                                        00600001
      ***************************************************************** 00610001
      *                         SWITCHES                                00620001
      ***************************************************************** 00630001
       01  PROGRAM-SWITCHES.                                            00640001
           05  PS-RESTART-REQUIRED-SW       PIC  X(01) VALUE 'N'.       00650001
               88  PS-RESTART-REQUIRED                 VALUE 'Y'.       00660001
               88  PS-RESTART-NOT-REQUIRED             VALUE 'N'.       00670001
                                                                        00680001
      ******************************************************************00690001
      *                       ACCUMULATORS                              00700001
      ******************************************************************00710001
       01  PROGRAM-ACCUM.                                               00720001
           05  PA-READ                      PIC  9(11) VALUE ZEROS.     00730001
           05  PA-CKPT                      PIC  9(11) VALUE ZEROS.     00740001
                                                                        00750001
      ******************************************************************00760001
      *                           CONSTANTS                             00770001
      ******************************************************************00780001
       01  PROGRAM-CONSTANTS.                                           00790001
           05  PC-MAX-RETRIES               PIC S9(04) VALUE +3         00800001
                                                       COMP SYNC.       00810001
           05  PC-MAX-DEADLOCKS             PIC S9(04) VALUE +3         00820001
                                                       COMP SYNC.       00830001
                                                                        00840001
           05  PC-CKPT-JOB-NAME             PIC  X(08) VALUE 'SAK079  '.00850004
           05  PC-CKPT-PLAN-NAME            PIC  X(08) VALUE 'SAKUP044'.00860004
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'SAKUP044'.00870004
                                                                        00880001
                                                                        00890001
           05  PC-TRAN-PRES-FUNC-CODES.                                 00900001
               10  PC-TRAN-PRES-FUNC-OPEN   PIC  X(05) VALUE 'OPEN '.   00910001
               10  PC-TRAN-PRES-FUNC-TRANS  PIC  X(05) VALUE 'TRANS'.   00920001
               10  PC-TRAN-PRES-FUNC-RSTRT  PIC  X(05) VALUE 'RSTRT'.   00930001
               10  PC-TRAN-PRES-FUNC-CLOSE  PIC  X(05) VALUE 'CLOSE'.   00940001
                                                                        00950001
           05  PC-MINUS-ONE                 PIC S9(01) VALUE -1.        00960001
           05  PC-SYSTEM                    PIC  X(08) VALUE 'SYSTEM  '.00970001
                                                                        00980001
      ******************************************************************00990001
      *                          VARIABLES                              01000001
      ******************************************************************01010001
       01  PROGRAM-VARIABLES.                                           01020001
           05  PV-SQL-SUB                   PIC  9(03) VALUE ZERO.      01030001
           05  PV-DEADLOCK-COUNTER          PIC  9(03) VALUE ZERO.      01040001
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.     01050001
           05  PV-DB2-OPERATION             PIC  X(30) VALUE SPACE.     01060001
           05  PV-OPERATION-MSG             PIC  X(35) VALUE SPACE.     01070001
           05  PV-RET-CODE                  PIC  9(04) VALUE ZEROS.     01080001
                                                                        01090001
       01  PV-DB2-VARIABLES.                                            01100001
           05  PV-DB2-PARTN-NBR             PIC S9(04) COMP             01110001
                                                       VALUE ZERO.      01120001
                                                                        01130001
       01  PV-ABEND-DISPLAYS.                                           01140001
           05  PV-ABEND-PARTN-NBR           PIC  9(04) VALUE ZERO.      01150001
           05  PV-ABEND-STR-NBR             PIC  9(04) VALUE ZERO.      01160001
           05  PV-ABEND-RGST-ID             PIC  9(03) VALUE ZERO.      01170001
           05  PV-ABEND-TRN-NBR             PIC  9(04) VALUE ZERO.      01180001
                                                                        01190001
      ******************************************************************01200001
      *               SYSTEM TIME AND DATE VARIABLES                    01210001
      ******************************************************************01220001
       01  SYS-DATE-TIME.                                               01230001
           05  SYS-DATE                     PIC  X(08) VALUE SPACES.    01240001
           05  SYS-TIME                     PIC  X(08) VALUE SPACES.    01250001
                                                                        01260001
       01  ST-BEG-TIME.                                                 01270001
           05  ST-BEG-HR                    PIC  9(02) VALUE ZEROS.     01280001
           05  FILLER                       PIC  X(01) VALUE ':'.       01290001
           05  ST-BEG-MN                    PIC  9(02) VALUE ZEROS.     01300001
                                                                        01310001
       01  ST-END-TIME.                                                 01320001
           05  ST-END-HR                    PIC  9(02) VALUE ZEROS.     01330001
           05  FILLER                       PIC  X(01) VALUE ':'.       01340001
           05  ST-END-MN                    PIC  9(02) VALUE ZEROS.     01350001
                                                                        01360001
       01  SD-EDIT-DATE.                                                01370001
           05  SD-MONTH                     PIC  9(02) VALUE ZEROS.     01380001
           05  FILLER                       PIC  X(01) VALUE '-'.       01390001
           05  SD-DAY                       PIC  9(02) VALUE ZEROS.     01400001
           05  FILLER                       PIC  X(01) VALUE '-'.       01410001
           05  SD-CENT                      PIC  9(02) VALUE ZEROS.     01420001
           05  SD-YEAR                      PIC  9(02) VALUE ZEROS.     01430001
                                                                        01440001
      ******************************************************************01450001
      *                        ERROR HANDLING                           01460001
      ******************************************************************01470001
       01  ABEND-CODE                       PIC S9(04) VALUE ZEROS      01480001
                                                       COMP SYNC.       01490001
           88  ABEND-4002-READ-ERROR                   VALUE +4002.     01500001
           88  ABEND-4013-DB2-ERROR                    VALUE +4013.     01510001
           88  ABEND-4019-CAL-SUB-ERROR                VALUE +4019.     01520001
                                                                        01530001
                                                                        01540003
      ***************************************************************** 01550003
      *  WS FIELDS FOR TEPITM AND TEPEID ASSOCIATE DISCOUNT UPDATES     01560003
      ***************************************************************** 01570003
           COPY SARD045.                                                01580005
                                                                        01590001
      ******************************************************************01600001
      *  DB2 FORMATTED ERROR MESSAGE                                    01610001
      ******************************************************************01620001
           COPY DPWS004.                                                01630001
                                                                        01640001
      ******************************************************************01650001
      *  SQL COMMUNICATIONS WORK AREA                                   01660001
      ******************************************************************01670001
           COPY SQLCA2.                                                 01680001
                                                                        01690001
      ******************************************************************01700001
      *  COMMUNICATIONS AREA FOR DB2                                    01710001
      ******************************************************************01720001
           EXEC SQL                                                     01730001
               INCLUDE SQLCA                                            01740001
           END-EXEC.                                                    01750001
                                                                        01760001
      ***************************************************************** 01770001
      *  DB2 TABLE TEPITM - TRANSACTION ITEM TABLE                      01780001
      ***************************************************************** 01790001
           EXEC SQL                                                     01800001
                INCLUDE TEPITM                                          01810001
           END-EXEC.                                                    01820001
                                                                        01830001
      ***************************************************************** 01840001
      *  DB2 TABLE TEPEID - TRANSACTION EMPLOYEE DISCOUNT TABLE         01850001
      ***************************************************************** 01860001
           EXEC SQL                                                     01870001
                INCLUDE TEPEID                                          01880001
           END-EXEC.                                                    01890001
                                                                        01900001
      ******************************************************************01910001
      *  TRANSACTION PREPARATION MODULE COMMUNICATIONS AREA             01920001
      ******************************************************************01930001
260107     COPY DPWS991.                                                01940006
                                                                        01940106
           COPY DPLS001.                                                01941006
           05  WORK-STORAGE-PASSED.                                     01950001
                                                                        01960001
      ******************************************************************01970001
      *PA - PROGRAM ACCUMULATORS                                        01980001
      ******************************************************************01990001
               10  PROGRAM-ACCUMULATORS.                                02000001
                   15  PA-TEPEID-ROWS-INSERTED   PIC S9(9) VALUE ZERO   02010003
                                                           COMP-3.      02020002
                   15  PA-TEPITM-ROWS-UPDATED    PIC S9(9) VALUE ZERO   02030002
                                                           COMP-3.      02040002
                                                                        02050001
       01  FILLER                           PIC X(4096)  VALUE SPACE.   02060002
                                                                        02070001
      ***************************************************************** 02080001
      * PROCEDURE DIVISION.                                           * 02090001
      ***************************************************************** 02100001
       PROCEDURE DIVISION.                                              02110001
                                                                        02120001
      ***************************************************************** 02130001
      * 0000-MAINLINE-PARAGRAPH                                         02140001
      ***************************************************************** 02150001
       0000-MAINLINE-PARAGRAPH.                                         02160001
                                                                        02170001
           PERFORM A100-INITIALIZE.                                     02180001
                                                                        02190001
           PERFORM B100-PROCESS-UPDATE-RECORDS                          02200001
             UNTIL DP001-PREP-TRANS-EOF.                                02210001
                                                                        02220001
           PERFORM Z990-EOJ-ROUTINE.                                    02230001
                                                                        02240001
           GOBACK.                                                      02250001
                                                                        02260001
                                                                        02270001
      ******************************************************************02280001
      * PREFORMS TASK INITIATION                                        02290001
      ******************************************************************02300001
       A100-INITIALIZE.                                                 02310001
                                                                        02320001
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 02330001
                                                                        02340001
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            02350001
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            02360001
                                                                        02370001
           MOVE SYS-DATE (1:2) TO SD-CENT.                              02380001
           MOVE SYS-DATE (3:2) TO SD-YEAR.                              02390001
           MOVE SYS-DATE (5:2) TO SD-MONTH.                             02400001
           MOVE SYS-DATE (7:2) TO SD-DAY.                               02410001
                                                                        02420001
           MOVE PC-TRAN-PRES-FUNC-OPEN TO DP001-FUNC-CODE.              02430001
                                                                        02440001
           PERFORM X100-BUILD-COMM-AREA-TRAN-PRES.                      02450001
                                                                        02460001
                                                                        02470001
      ******************************************************************02480001
      * PROCESS UPDATE RECORDS                                          02490001
      ******************************************************************02500001
       B100-PROCESS-UPDATE-RECORDS.                                     02510001
                                                                        02520001
           SET PS-RESTART-NOT-REQUIRED TO TRUE.                         02530001
                                                                        02540001
           PERFORM C100-UPDATE-FIELDS.                                  02550001
                                                                        02560001
      *----------------------------------------------------------------*02570001
      *  IF A RESTART IS REQUIRED, ISSUE A RESTART CALL RATHER THAN A  *02580001
      *  TRANS CALL.  THIS WILL ALLOW PROCESSING TO RESUME WITH THE    *02590001
      *  FIRST TRANSACTION AFTER THE LAST COMMIT.                      *02600001
      *----------------------------------------------------------------*02610001
           IF  PS-RESTART-REQUIRED                                      02620001
               MOVE PC-TRAN-PRES-FUNC-RSTRT TO DP001-FUNC-CODE          02630001
           ELSE                                                         02640001
               MOVE PC-TRAN-PRES-FUNC-TRANS TO DP001-FUNC-CODE          02650001
           END-IF.                                                      02660001
                                                                        02670001
           PERFORM X100-BUILD-COMM-AREA-TRAN-PRES.                      02680001
                                                                        02690001
           ADD 1 TO PA-READ.                                            02700001
                                                                        02710001
                                                                        02720001
      ******************************************************************02730001
      * MOVE THE UPDATE FIELDS TO THE TEPITM AND TEPEID LAYOUTS.        02740001
      ******************************************************************02750001
       C100-UPDATE-FIELDS.                                              02760001
                                                                        02770001
           MOVE SA045-EPITM-PARTN-NBR        TO EPITM-PARTN-NBR         02780005
                                                EPEID-PARTN-NBR.        02790003
           MOVE SA045-EPITM-STR-NBR          TO EPITM-STR-NBR           02800005
                                                EPEID-STR-NBR.          02810003
           MOVE SA045-EPITM-RGST-ID          TO EPITM-RGST-ID           02820005
                                                EPEID-RGST-ID.          02830003
           MOVE SA045-EPITM-TRN-NBR          TO EPITM-TRN-NBR           02840005
                                                EPEID-TRN-NBR.          02850003
           MOVE SA045-EPITM-STRT-TM          TO EPITM-STRT-TM           02860005
                                                EPEID-STRT-TM.          02870003
           MOVE SA045-EPITM-SLS-CORR-SEQ-NBR TO EPITM-SLS-CORR-SEQ-NBR  02880005
                                                EPEID-SLS-CORR-SEQ-NBR. 02890003
           MOVE SA045-EPITM-LNE-ITM-SEQ-NBR  TO EPITM-LNE-ITM-SEQ-NBR   02900005
                                                EPEID-LNE-ITM-SEQ-NBR.  02910003
           MOVE SA045-EPITM-SBJCT-TO-TX-AMT  TO EPITM-SBJCT-TO-TX-AMT.  02920005
           MOVE SA045-EPITM-NET-CHRGD-AMT    TO EPITM-NET-CHRGD-AMT.    02930005
           MOVE SA045-EPEID-EMP-DISC-PCT     TO EPEID-EMP-DISC-PCT.     02940005
           MOVE SA045-EPEID-EMP-DISC-AMT     TO EPEID-EMP-DISC-AMT.     02950005
           MOVE SA045-EPEID-EMP-DISC-SRC-CDE TO EPEID-EMP-DISC-SRC-CDE. 02960005
           MOVE SA045-EPEID-EMP-DISC-CTG-CDE TO EPEID-EMP-DISC-CTG-CDE. 02970005
                                                                        02980001
           PERFORM 5000-INSERT-TEPEID-TABLE.                            02990001
           PERFORM 5100-UPDATE-ITM-NET-CHRGD-AMT.                       03000001
                                                                        03010001
                                                                        03020001
      ***************************************************************** 03030001
      * 5000-INSERT-TEPEID-TABLE                                        03040001
      *     INSERT THE TEPEID ROW FOR THE ASSOCIATE DISCOUNT            03050001
      ***************************************************************** 03060001
       5000-INSERT-TEPEID-TABLE.                                        03070001
                                                                        03080001
           PERFORM WITH TEST AFTER                                      03090001
             VARYING PV-SQL-SUB                                         03100001
             FROM 1 BY 1                                                03110001
             UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                         03120001
                    OR SQLCODE = -911                                   03130001
                    OR SQLCODE = 0)                                     03140001
                                                                        03150001
             EXEC SQL                                                   03160001
               INSERT INTO TEPEID  ( PARTN_NBR                          03170001
                                    ,STR_NBR                            03180001
                                    ,RGST_ID                            03190001
                                    ,TRN_NBR                            03200001
                                    ,STRT_TM                            03210001
                                    ,SLS_CORR_SEQ_NBR                   03220001
                                    ,LNE_ITM_SEQ_NBR                    03230001
                                    ,EMP_DISC_CTG_CDE                   03240001
                                    ,EMP_DISC_AMT                       03250001
                                    ,EMP_DISC_PCT                       03260001
                                    ,EMP_DISC_SRC_CDE )                 03270001
                           VALUES  (:EPEID-PARTN-NBR,                   03280001
                                    :EPEID-STR-NBR,                     03290001
                                    :EPEID-RGST-ID,                     03300001
                                    :EPEID-TRN-NBR,                     03310001
                                    :EPEID-STRT-TM,                     03320001
                                    :EPEID-SLS-CORR-SEQ-NBR,            03330001
                                    :EPEID-LNE-ITM-SEQ-NBR,             03340001
                                    :EPEID-EMP-DISC-CTG-CDE,            03350001
                                    :EPEID-EMP-DISC-AMT,                03360001
                                    :EPEID-EMP-DISC-PCT,                03370001
                                    :EPEID-EMP-DISC-SRC-CDE )           03380001
             END-EXEC                                                   03390002
                                                                        03400001
           EVALUATE TRUE                                                03410001
               WHEN SQLCODE = 0                                         03420001
                   ADD +1 TO PA-TEPEID-ROWS-INSERTED                    03430003
                                                                        03440001
               WHEN SQLCODE = -911                                      03450001
                   ADD +1 TO PV-DEADLOCK-COUNTER                        03460001
                   DISPLAY '                   '                        03470001
                   DISPLAY 'DEADLOCK INCOUNTERED -911'                  03480001
                   DISPLAY 'DEADLOCK-COUNTER = ' PV-DEADLOCK-COUNTER    03490001
                   IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS            03500001
                       MOVE 'C200-TSASUM-UPDATE'                        03510001
                                            TO PV-PARAGRAPH-NAME        03520001
                       MOVE 'MAXIMUM DEADLOCKS EXCEEDED    '            03530001
                                            TO PV-DB2-OPERATION         03540002
                       PERFORM Z200-SQL-ABEND                           03550001
                   ELSE                                                 03560001
                       SET PS-RESTART-REQUIRED TO TRUE                  03570001
                   END-IF                                               03580001
                                                                        03590001
               WHEN OTHER                                               03600001
                  DISPLAY '               '                             03610001
                  DISPLAY 'PARTN-NBR    = ' EPEID-PARTN-NBR             03620001
                  DISPLAY 'STR-NBR      = ' EPEID-STR-NBR               03630001
                  DISPLAY 'RGST-ID      = ' EPEID-RGST-ID               03640001
                  DISPLAY 'TRN-NBR      = ' EPEID-TRN-NBR               03650001
                  DISPLAY 'STRT-TM      = ' EPEID-STRT-TM               03660001
                  DISPLAY 'CORR-SEQ-NBR = ' EPEID-SLS-CORR-SEQ-NBR      03670001
                  DISPLAY 'LNE-ITM-SEQ  = ' EPEID-LNE-ITM-SEQ-NBR       03680001
                  DISPLAY 'EMP-DISC-CTG = ' EPEID-EMP-DISC-CTG-CDE      03690001
                  DISPLAY 'EMP-DISC-AMT = ' EPEID-EMP-DISC-AMT          03700001
                  DISPLAY 'EMP-DISC-PCT = ' EPEID-EMP-DISC-PCT          03710001
                  DISPLAY '               '                             03720001
                  MOVE '5000-INSERT-TEPEID-TABLE' TO PV-PARAGRAPH-NAME  03730001
                  MOVE 'ERROR ON TEPEID INSERT'  TO PV-DB2-OPERATION    03740001
                  PERFORM Z200-SQL-ABEND                                03750002
           END-EVALUATE                                                 03760001
           END-PERFORM.                                                 03770001
                                                                        03780001
           IF PV-SQL-SUB > PC-MAX-RETRIES                               03790001
               MOVE '5000-INSERT-TEPEID-TABLE' TO PV-PARAGRAPH-NAME     03800001
               MOVE 'VERIFY RETRIES EXCEEDED'  TO PV-DB2-OPERATION      03810002
               PERFORM Z200-SQL-ABEND                                   03820001
           END-IF.                                                      03830001
                                                                        03840001
                                                                        03850001
      ***************************************************************** 03860001
      * 5100-UPDATE-ITM-NET-CHRGD-AMT                                   03870001
      *     UPDATE THE ITEM FOR THE ASSOCIATE DISCOUNT                  03880001
      ***************************************************************** 03890001
       5100-UPDATE-ITM-NET-CHRGD-AMT.                                   03900001
                                                                        03910001
           PERFORM WITH TEST AFTER                                      03920001
             VARYING PV-SQL-SUB                                         03930001
             FROM 1 BY 1                                                03940001
             UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                         03950001
                    OR SQLCODE = -911                                   03960001
                    OR SQLCODE = +100                                   03970001
                    OR SQLCODE = 0)                                     03980001
                                                                        03990001
             EXEC SQL                                                   04000001
              UPDATE TEPITM                                             04010001
                 SET NET_CHRGD_AMT    = :EPITM-NET-CHRGD-AMT            04020001
                    ,SBJCT_TO_TX_AMT  = :EPITM-SBJCT-TO-TX-AMT          04030001
                 WHERE                                                  04040001
                     PARTN_NBR        = :EPITM-PARTN-NBR        AND     04050001
                     STR_NBR          = :EPITM-STR-NBR          AND     04060001
                     RGST_ID          = :EPITM-RGST-ID          AND     04070001
                     TRN_NBR          = :EPITM-TRN-NBR          AND     04080001
                     STRT_TM          = :EPITM-STRT-TM          AND     04090001
                     SLS_CORR_SEQ_NBR = :EPITM-SLS-CORR-SEQ-NBR AND     04100001
                     LNE_ITM_SEQ_NBR  = :EPITM-LNE-ITM-SEQ-NBR          04110001
             END-EXEC                                                   04120002
                                                                        04130001
           EVALUATE TRUE                                                04140001
               WHEN SQLCODE = 0                                         04150001
                   ADD +1 TO PA-TEPITM-ROWS-UPDATED                     04160001
                                                                        04170001
               WHEN SQLCODE = -911                                      04180001
                   ADD +1 TO PV-DEADLOCK-COUNTER                        04190001
                   DISPLAY '                   '                        04200001
                   DISPLAY 'DEADLOCK INCOUNTERED -911'                  04210001
                   DISPLAY 'DEADLOCK-COUNTER = ' PV-DEADLOCK-COUNTER    04220001
                   IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS            04230001
                       MOVE '5100-UPDATE-ITM-NET-CHRGD-AMT'             04240001
                                            TO PV-PARAGRAPH-NAME        04250001
                       MOVE 'MAXIMUM DEADLOCKS EXCEEDED    '            04260001
                                            TO PV-DB2-OPERATION         04270002
                       PERFORM Z200-SQL-ABEND                           04280001
                   ELSE                                                 04290001
                       SET PS-RESTART-REQUIRED TO TRUE                  04300001
                   END-IF                                               04310001
                                                                        04320001
               WHEN OTHER                                               04330001
                  DISPLAY '               '                             04340001
                  DISPLAY 'PARTN-NBR    = ' EPITM-PARTN-NBR             04350001
                  DISPLAY 'STR-NBR      = ' EPITM-STR-NBR               04360001
                  DISPLAY 'RGST-ID      = ' EPITM-RGST-ID               04370001
                  DISPLAY 'TRN-NBR      = ' EPITM-TRN-NBR               04380001
                  DISPLAY 'STRT-TM      = ' EPITM-STRT-TM               04390001
                  DISPLAY 'CORR-SEQ-NBR = ' EPITM-SLS-CORR-SEQ-NBR      04400001
                  DISPLAY 'LNE-ITM-SEQ  = ' EPITM-LNE-ITM-SEQ-NBR       04410001
                  DISPLAY '               '                             04420001
                                                                        04430001
                  MOVE '5100-UPDATE-ITM-NET-CHRGD-AMT'                  04440001
                                                  TO PV-PARAGRAPH-NAME  04450001
                  MOVE 'ERROR ON TEPITM UPDATE'  TO PV-DB2-OPERATION    04460001
                  PERFORM Z200-SQL-ABEND                                04470002
           END-EVALUATE                                                 04480001
           END-PERFORM.                                                 04490001
                                                                        04500001
           IF PV-SQL-SUB > PC-MAX-RETRIES                               04510001
               MOVE '5100-UPDATE-ITM-NET-CHRGD-AMT'                     04520001
                                              TO PV-PARAGRAPH-NAME      04530001
               MOVE 'VERIFY RETRIES EXCEEDED' TO PV-DB2-OPERATION       04540002
               PERFORM Z200-SQL-ABEND                                   04550001
           END-IF.                                                      04560001
                                                                        04570001
                                                                        04580001
      ******************************************************************04590001
      * BUILD THE COMMUNICATION AREA FOR A CALL TO THE TRANSACTION      04600001
      * PRESENTATION MODULE                                             04610001
      ******************************************************************04620001
       X100-BUILD-COMM-AREA-TRAN-PRES.                                  04630001
                                                                        04640001
           MOVE LENGTH OF                                               04650001
                WORK-STORAGE-PASSED TO DP001-WORK-STRG-LENGTH.          04660001
           MOVE PC-CKPT-JOB-NAME    TO DP001-JOB-NAME.                  04670001
           MOVE PC-CKPT-PLAN-NAME   TO DP001-PLAN-NAME.                 04680001
                                                                        04690001
           PERFORM X200-CALL-TRANS-PRES-MODULE.                         04700001
                                                                        04710001
           MOVE DP001-TRANS-RECORD  TO SA045-TRAN-UPDT-RECORD.          04720005
                                                                        04730001
           IF  DP001-CKPT-TAKEN                                         04740001
               ADD 1 TO PA-CKPT                                         04750001
                                                                        04760001
               INITIALIZE PV-DEADLOCK-COUNTER                           04770001
           END-IF.                                                      04780001
                                                                        04790001
                                                                        04800001
      ******************************************************************04810001
      * VERIFY THE RETURN CODE ON THE CALLED MODULE                     04820001
      ******************************************************************04830001
       X200-CALL-TRANS-PRES-MODULE.                                     04840001
                                                                        04850001
           PERFORM Z900-CALL-TRANS-PRES-MODULE.                         04860001
                                                                        04870001
           IF  DP001-GOOD-RET-CODE                                      04880001
           OR  DP001-CKPT-TAKEN                                         04890001
           OR  DP001-RESTART                                            04900001
               CONTINUE                                                 04910001
                                                                        04920001
           ELSE                                                         04930001
               MOVE 'X200-CALL-TRANS-PRES-MODULE' TO PV-PARAGRAPH-NAME  04940001
               MOVE '** BAD CALL TO TRANS PREP MODULE **'               04950001
                                                  TO PV-OPERATION-MSG   04960001
               MOVE DP001-RET-CODE                TO PV-RET-CODE        04970001
                                                                        04980001
               SET ABEND-4019-CAL-SUB-ERROR TO TRUE                     04990001
                                                                        05000001
               PERFORM Z100-ABEND                                       05010001
           END-IF.                                                      05020001
                                                                        05030001
                                                                        05040001
      ******************************************************************05050001
      * NON-DB2 ABEND                                                   05060001
      ******************************************************************05070001
       Z100-ABEND.                                                      05080001
                                                                        05090001
           PERFORM Z999-CONTROLS.                                       05100001
                                                                        05110001
           DISPLAY '                     '.                             05120001
           DISPLAY '** ABEND           **'.                             05130001
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          05140001
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        05150001
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.         05160001
           DISPLAY '** RETURN CODE     ** = ' PV-RET-CODE.              05170001
                                                                        05180001
           CALL 'ILBOABN0' USING ABEND-CODE.                            05190001
                                                                        05200001
                                                                        05210001
      ******************************************************************05220001
      * ABEND THE PROGRAM IN THE EVENT THAT A SQL ERROR OR WARNING      05230001
      * CODE OCCURS.                                                    05240001
      ******************************************************************05250001
       Z200-SQL-ABEND.                                                  05260001
                                                                        05270001
           PERFORM Z999-CONTROLS.                                       05280001
                                                                        05290001
           DISPLAY '** ABEND     **'.                                   05300001
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                05310001
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              05320001
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               05330002
                                                                        05340001
      ******************************************************************05350001
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    05360001
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         05370001
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     05380001
      * FORMAT.                                                         05390001
      ******************************************************************05400001
           COPY DPPD004.                                                05410001
                                                                        05420001
           SET ABEND-4013-DB2-ERROR TO TRUE.                            05430001
                                                                        05440001
           CALL 'ILBOABN0' USING ABEND-CODE.                            05450001
                                                                        05460001
                                                                        05470001
      ******************************************************************05480001
      * PERFORMS TASK TERMINATION PROCESSING                            05490001
      ******************************************************************05500001
       Z990-EOJ-ROUTINE.                                                05510001
                                                                        05520001
           MOVE PC-TRAN-PRES-FUNC-CLOSE TO DP001-FUNC-CODE.             05530001
                                                                        05540001
           PERFORM X100-BUILD-COMM-AREA-TRAN-PRES.                      05550001
                                                                        05560001
           PERFORM Z999-CONTROLS.                                       05570001
                                                                        05580001
                                                                        05590001
      ******************************************************************05600001
      * DISPLAY CONTROLS FROM THE PROGRAM                               05610001
      ******************************************************************05620001
       Z999-CONTROLS.                                                   05630001
                                                                        05640001
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 05650001
                                                                        05660001
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            05670001
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            05680001
                                                                        05690001
           DISPLAY '                    '.                              05700001
           DISPLAY '**********************'.                            05710001
           DISPLAY '   SAKUP044 CONTROLS  '.                            05720004
           DISPLAY '**********************'.                            05730001
           DISPLAY 'RUN DATE  = ' SD-EDIT-DATE.                         05740001
           DISPLAY 'BEG TIME  = ' ST-BEG-TIME.                          05750001
           DISPLAY 'END TIME  = ' ST-END-TIME.                          05760001
           DISPLAY '======================'.                            05770001
           DISPLAY 'RECORDS READ   = ' PA-READ.                         05780001
           DISPLAY 'CHECKPOINTS    = ' PA-CKPT.                         05790001
           DISPLAY '                '.                                  05800001
           DISPLAY 'TEPEID ROWS INSERTED = ' PA-TEPEID-ROWS-INSERTED.   05810002
           DISPLAY 'TEPITM ROWS UPDATED  = ' PA-TEPITM-ROWS-UPDATED.    05820002
           DISPLAY '                '.                                  05830001
                                                                        05840001
                                                                        05850001
      ******************************************************************05860001
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    05870001
      * MENTS THAT ARE REQUIRED TO INVOKE THE TRANSACTION PRESENTATION  05880001
      * MODULE (DPKUT901) OF THE DB2 CHECKPOINT RESTART SYSTEM.         05890001
      ******************************************************************05900001
           COPY DPPD001.                                                05910001
                                                                        05920001
