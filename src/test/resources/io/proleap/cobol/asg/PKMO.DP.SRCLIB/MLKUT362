       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              MLKUT362.                                       
       AUTHOR.                  JOHN SELWITSCHKA.                               
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            06/19/2007.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  RE-ALLOCATE UNKNOWN BRANDS AND VENDORS.                       *        
      *                                                                *        
      *  THIS PROGRAM WILL RE-ALLOCATE SALES RETAIL AND ENDING         *        
      *  INVENTORY RETAIL FOR UNKNOWN VENDORS AND BRANDS.              *        
      *                                                                *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * WR28582  2007-06-19  INITIAL IMPLEMENTATION.                   *        
      *                      J SELWITSCHKA                             *        
      *                                                                *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATE-FILE                                                     
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT ADJUSTMENT-FILE                                               
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  DATE-FILE                                                            
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DATE-REC                        PIC X(80).                           
                                                                                
       FD  ADJUSTMENT-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ADJUSTMENT-REC                  PIC X(450).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      *****************************************************************         
      *    CONTROL CARD                                                         
      *****************************************************************         
       01  CC-CONTROL-CARD.                                                     
           05  CC-FSCL-BEG-DTE           PIC  X(10).                            
           05  FILLER                    PIC  X(01).                            
           05  CC-FSCL-END-DTE           PIC  X(10).                            
           05  FILLER                    PIC  X(59).                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-MONTHLY-TM-LVL-CDE       PIC X           VALUE 'M'.           
           05  PC-WEEKLY-TM-LVL-CDE        PIC X           VALUE 'W'.           
           05  PC-UNKNOWN-BRAND            PIC  S9(4) COMP VALUE +2230.         
           05  PC-UNKNOWN-VENDOR           PIC  S9(4) COMP VALUE +5161.         
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-DATES             PIC X           VALUE 'N'.           
               88  END-OF-DATES                            VALUE 'Y'.           
           05  PS-END-OF-UNKN-CSR          PIC X           VALUE 'N'.           
               88  NOT-END-OF-UNKN-CSR                     VALUE 'N'.           
               88  END-OF-UNKN-CSR                         VALUE 'Y'.           
           05  PS-END-OF-ACTLS-SUM-CSR     PIC X           VALUE 'N'.           
               88  NOT-END-OF-ACTLS-SUM-CSR                VALUE 'N'.           
               88  END-OF-ACTLS-SUM-CSR                    VALUE 'Y'.           
           05  PS-END-OF-DEPT-INV-ADJ-CSR  PIC X           VALUE 'N'.           
               88  NOT-END-OF-DEPT-INV-ADJ-CSR             VALUE 'N'.           
               88  END-OF-DEPT-INV-ADJ-CSR                 VALUE 'Y'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** REPORT REQUEST CONTROL REC ***'.                                
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-PREV-VENDOR-ID           PIC 9(09)       VALUE ZEROES.        
           05  PV-PREV-DEPT-NBR            PIC S9(4) COMP-3                     
                                                           VALUE +0.            
           05  PV-DEPT-NBR-9               PIC 9(03)       VALUE ZEROES.        
           05  PV-DEPT-NBR REDEFINES PV-DEPT-NBR-9                              
                                           PIC X(03).                           
           05  PV-MAJ-CL-NBR-9             PIC 9(02)       VALUE ZEROES.        
           05  PV-MAJ-CL-NBR REDEFINES PV-MAJ-CL-NBR-9                          
                                           PIC X(02).                           
           05  PV-FETCHED                  PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-FETCHED              PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACTLS-FETCHED            PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-INV-ADJ-FETCHED          PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-KNWN-SUMS-NOT-FOUND      PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-TRANS-WRITTEN        PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-REVERSALS-WRITTEN        PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-REALLOCATIONS-WRITTEN    PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-UNKN-INV-ADJ-WRITTEN     PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-KNWN-SALE-RE-WRITTEN     PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-KNWN-INV-ADJ-RE-WRITTEN  PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-PCT                      PIC S9(03)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-INVA-DEPT-NBR            PIC S9(04) COMP-3 VALUE +0.          
           05  PV-INVA-MAJ-CL-NBR          PIC S9(04) COMP-3 VALUE +0.          
           05  PV-INVA-SUB-CL-NBR          PIC S9(04) COMP-3 VALUE +0.          
           05  PV-INVA-LOC-NBR             PIC S9(04) COMP   VALUE +0.          
           05  PV-INVA-ML-LO-MDSE-LVL-ID   PIC S9(09) COMP   VALUE +0.          
           05  PV-NEW-SLS-RTL-AMT          PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-INVA-SLS-REG-RTL-AMT     PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-INVA-SLS-PROMO-RTL-AMT   PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-INVA-SLS-PROCLR-RTL-AMT  PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-INVA-SLS-CLR-RTL-AMT     PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-INVA-SLS-OTH-RTL-AMT     PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-INVA-END-INV-RTL-AMT     PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ML254-SLS-REG-RTL-AMT    PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ML254-SLS-PROMO-RTL-AMT  PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ML254-SLS-PROCLR-RTL-AMT PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ML254-SLS-CLR-RTL-AMT    PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ML254-SLS-OTH-RTL-AMT    PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ML254-INV-ADJ-RTL-AMT    PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-KNWN-SLS-REG-RTL-AMT     PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-KNWN-SLS-PROMO-RTL-AMT   PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-KNWN-SLS-PROCLR-RTL-AMT  PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-KNWN-SLS-CLR-RTL-AMT     PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-KNWN-SLS-OTH-RTL-AMT     PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-UNKN-SLS-REG-RTL-AMT     PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-UNKN-SLS-PROMO-RTL-AMT   PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-UNKN-SLS-PROCLR-RTL-AMT PIC S9(11)V9(7) VALUE ZEROES          
                               COMP-3.                                          
           05  PV-UNKN-SLS-CLR-RTL-AMT     PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-UNKN-SLS-OTH-RTL-AMT     PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-KNWN-TOT-SLS-RTL-AMT     PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-KNWN-END-INV-RTL-AMT     PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-UNKN-INV-ADJ-RTL-AMT     PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-UNKN-TOT-SLS-RTL-AMT     PIC S9(11)V9(7) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-SLS-REG-RTL-AMT-REM      PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-SLS-PROMO-RTL-AMT-REM    PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-SLS-PROCLR-RTL-AMT-REM   PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-SLS-CLR-RTL-AMT-REM      PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-SLS-OTH-RTL-AMT-REM      PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-INV-ADJ-RTL-AMT-REM      PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-END-INV-RTL-AMT-REM      PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-INV-ADJ-RTL-AMT-TOT      PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-END-INV-RTL-AMT-TOT      PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-DR-ALW-RTL-AMT-REM       PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-RTV-RTL-AMT-REM          PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-DISPLAY-AMT              PIC ----------9.99.                  
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-PARAGRAPH-NAME           PIC X(30)       VALUE SPACES.        
           05  PV-SQLCODE                  PIC -999        VALUE ZEROES.        
           05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-CC        PIC X(02).                           
               10  PV-PROCESSING-YY        PIC X(02).                           
               10  PV-PROC-DSH1            PIC X(01).                           
               10  PV-PROCESSING-MM        PIC X(02).                           
               10  PV-PROC-DSH2            PIC X(01).                           
               10  PV-PROCESSING-DD        PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
           05  PV-TODAYS-TIME.                                                  
               10  PV-TODAYS-TIME-HR       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-MIN      PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-SEC      PIC 9(02)       VALUE ZEROES.        
               10  FILLER                  PIC 9(02)       VALUE ZEROES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** SQL WORK AREA BEGIN ***'.                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  ABEND-4003                                  VALUE +4003.         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MLT_PARTN_BAL  - ML'S PARTITION BALANCE TABLE              *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE MLT00002                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MLT_PARTN_CNTL - ML'S PARTITION CONTROL TABLE              *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE MLT00003                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MLT_MNLY_SUM - MONTHLY SUMMARY TABLE                       *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE MLT00004                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MLV_BRND_VND TABLE                                         *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE MLV00000                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MLT_UNKN_BRND_VND                                          *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE MLT00011                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DATE ATTRIBUTE TABLE                                       *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DETAILED MERCH LEDGER UPDATE TRANSACTION COPYBOOK          *         
      *---------------------------------------------------------------*         
           COPY MLRD254.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    DEPT_CSR CURSOR                                            *         
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL   DECLARE UNKN_CSR CURSOR FOR                               
               SELECT ML_LO_MDSE_LVL_ID                                         
                     ,DEPT_NBR                                                  
                     ,LBL_SEQ_NBR                                               
                     ,ENT_ID                                                    
                     ,EFF_BEG_DTE                                               
                     ,EFF_END_DTE                                               
               FROM  MLV_BRND_VND                                               
               WHERE ENT_ID      = :PC-UNKNOWN-VENDOR                           
               ORDER BY                                                         
                      DEPT_NBR                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    ACTLS_SUM_CSR CURSOR                                       *         
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL   DECLARE ACTLS_SUM_CSR CURSOR FOR                          
               SELECT DEPT_NBR                                                  
                     ,ML_LO_MDSE_LVL_ID                                         
                     ,VALUE(SUM(SLS_REG_RTL_AMT),0)                             
                     ,VALUE(SUM(SLS_PROMO_RTL_AMT),0)                           
                     ,VALUE(SUM(SLS_PROCLR_RTL_AMT),0)                          
                     ,VALUE(SUM(SLS_CLR_RTL_AMT),0)                             
                     ,VALUE(SUM(SLS_OTH_RTL_AMT),0)                             
                     ,VALUE(SUM(END_INV_RTL_AMT),0)                             
               FROM MLT_MNLY_SUM                                                
               WHERE PARTN_NBR IN                                               
                        (SELECT PARTN_NBR                                       
                           FROM MLT_PARTN_CNTL                                  
                          WHERE TBL_TM_LVL_CDE = :PC-MONTHLY-TM-LVL-CDE         
                            AND FSCL_DTE BETWEEN :CC-FSCL-BEG-DTE               
                                             AND :CC-FSCL-END-DTE)              
                 AND DEPT_NBR          = :MLT00004-DEPT-NBR                     
                 AND ML_LO_MDSE_LVL_ID = :MLT00004-ML-LO-MDSE-LVL-ID            
               GROUP BY DEPT_NBR                                                
                       ,ML_LO_MDSE_LVL_ID                                       
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DEPT_INV_ADJ_CSR CURSOR                                    *         
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL   DECLARE DEPT_INV_ADJ_CSR CURSOR FOR                       
               SELECT DEPT_NBR                                                  
                     ,ML_LO_MDSE_LVL_ID                                         
                     ,SLS_REG_RTL_AMT                                           
                     ,SLS_PROMO_RTL_AMT                                         
                     ,SLS_PROCLR_RTL_AMT                                        
                     ,SLS_CLR_RTL_AMT                                           
                     ,SLS_OTH_RTL_AMT                                           
                     ,END_INV_RTL_AMT                                           
               FROM MLT_MNLY_SUM                                                
               WHERE PARTN_NBR IN                                               
                        (SELECT PARTN_NBR                                       
                           FROM MLT_PARTN_CNTL                                  
                          WHERE TBL_TM_LVL_CDE = :PC-MONTHLY-TM-LVL-CDE         
                            AND FSCL_DTE BETWEEN :CC-FSCL-BEG-DTE               
                                             AND :CC-FSCL-END-DTE)              
                 AND DEPT_NBR          = :MLT00004-DEPT-NBR                     
                 AND ML_LO_MDSE_LVL_ID NOT IN                                   
                        (SELECT A.UNKN_BRND_VND_ID                              
                           FROM MLT_UNKN_BRND_VND A                             
                          WHERE A.DEPT_NBR = DEPT_NBR)                          
               ORDER BY DEPT_NBR                                                
                       ,ML_LO_MDSE_LVL_ID                                       
           END-EXEC.                                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM B200-PROCESS-UNKN-VENDORS                                    
               UNTIL END-OF-UNKN-CSR.                                           
                                                                                
           PERFORM B900-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
           INITIALIZE ML254-UPDATE-REC.                                         
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
                                                                                
           MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
           MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
           MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
                                                                                
           MOVE '-' TO PV-PROC-DSH1.                                            
           MOVE '-' TO PV-PROC-DSH2.                                            
                                                                                
           IF PV-TODAYS-DATE-YY > '80'                                          
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC                                    
           END-IF.                                                              
                                                                                
           PERFORM Z100-OPEN-UNKN-CSR.                                          
           MOVE ZEROES         TO PV-FETCHED.                                   
           PERFORM Z110-FETCH-UNKN-CSR.                                         
           IF END-OF-UNKN-CSR                                                   
               DISPLAY                                                          
                   'NO UNKNOWN VENDORS FOUND ON MLV_BRND_VND'                   
               DISPLAY 'VENDOR: ' PC-UNKNOWN-VENDOR                             
           END-IF.                                                              
                                                                                
           OPEN INPUT  DATE-FILE.                                               
           OPEN OUTPUT ADJUSTMENT-FILE.                                         
                                                                                
           PERFORM C100-PROCESS-RQ-CNTL.                                        
                                                                                
      ******************************************************************        
      *    PROCESS UNKNOWN VENDORS                                              
      ******************************************************************        
       B200-PROCESS-UNKN-VENDORS.                                               
                                                                                
           INITIALIZE DCLMLT00004                                               
                      ML254-UPDATE-REC.                                         
                                                                                
           MOVE MLV00000-DEPT-NBR          TO MLT00004-DEPT-NBR                 
           MOVE MLV00000-ML-LO-MDSE-LVL-ID TO MLT00004-ML-LO-MDSE-LVL-ID        
           IF MLV00000-DEPT-NBR NOT = PV-PREV-DEPT-NBR                          
               PERFORM Z150-GET-KNOWN-VENDOR-SUM                                
           END-IF.                                                              
           MOVE MLV00000-DEPT-NBR TO PV-PREV-DEPT-NBR.                          
           PERFORM Z200-OPEN-ACTLS-SUM-CSR.                                     
           PERFORM Z210-FETCH-ACTLS-SUM-CSR.                                    
           PERFORM B300-PROCESS-ACTUALS                                         
             UNTIL END-OF-ACTLS-SUM-CSR.                                        
                                                                                
           PERFORM Z220-CLOSE-ACTLS-SUM-CSR.                                    
                                                                                
           PERFORM Z110-FETCH-UNKN-CSR.                                         
                                                                                
      ******************************************************************        
      *    CREATE UNKNOWN VENDOR REVERSAL TRANSACTION. CREATE                   
      *    RE-ALLOCATION TRANSACTIONS.                                          
      ******************************************************************        
       B300-PROCESS-ACTUALS.                                                    
                                                                                
           PERFORM Z230-OPEN-DEPT-INV-ADJ-CSR.                                  
           PERFORM Z240-FETCH-DEPT-INV-ADJ-CSR.                                 
           IF NOT-END-OF-DEPT-INV-ADJ-CSR                                       
               MOVE MLT00004-SLS-REG-RTL-AMT                                    
                 TO PV-SLS-REG-RTL-AMT-REM                                      
               MOVE MLT00004-SLS-PROMO-RTL-AMT                                  
                 TO PV-SLS-PROMO-RTL-AMT-REM                                    
               MOVE MLT00004-SLS-PROCLR-RTL-AMT                                 
                 TO PV-SLS-PROCLR-RTL-AMT-REM                                   
               MOVE MLT00004-SLS-CLR-RTL-AMT                                    
                 TO PV-SLS-CLR-RTL-AMT-REM                                      
               MOVE MLT00004-SLS-OTH-RTL-AMT                                    
                 TO PV-SLS-OTH-RTL-AMT-REM                                      
               PERFORM B310-REVERSE-UNKN-SUM                                    
               PERFORM B400-INV-ADJ-REALLOCATE                                  
                 UNTIL END-OF-DEPT-INV-ADJ-CSR                                  
           END-IF.                                                              
                                                                                
           PERFORM Z250-CLOSE-DEPT-INV-ADJ-CSR.                                 
                                                                                
           PERFORM Z210-FETCH-ACTLS-SUM-CSR.                                    
                                                                                
      ******************************************************************        
      *    CREATE UNKNOWN VENDOR REVERSAL TRANSACTION.                          
      ******************************************************************        
       B310-REVERSE-UNKN-SUM.                                                   
                                                                                
           MOVE MLT00004-DEPT-NBR          TO ML254-DEPT-NBR                    
           MOVE 10                         TO ML254-MAJ-CL-NBR                  
           MOVE 10                         TO ML254-SUB-CL-NBR                  
           MOVE 400                        TO ML254-LOC-NBR                     
           MOVE MLT00004-ML-LO-MDSE-LVL-ID TO ML254-ML-LWST-ID                  
           MOVE CC-FSCL-END-DTE            TO MLT00003-FSCL-DTE.                
           MOVE MLT00004-DEPT-NBR          TO MLT00002-DEPT-NBR.                
           MOVE PC-MONTHLY-TM-LVL-CDE      TO MLT00003-TBL-TM-LVL-CDE.          
           PERFORM Z300-GET-PARTN-INFO.                                         
           MOVE MLT00003-PARTN-NBR         TO ML254-MTH-PARTN-NBR.              
           MOVE MLT00003-DEPT-GP-CDE       TO ML254-MTH-DEPT-GP-CDE.            
           MOVE PC-WEEKLY-TM-LVL-CDE       TO MLT00003-TBL-TM-LVL-CDE.          
           PERFORM Z300-GET-PARTN-INFO.                                         
           MOVE MLT00003-PARTN-NBR         TO ML254-WK-PARTN-NBR                
           MOVE MLT00003-DEPT-GP-CDE       TO ML254-WK-DEPT-GP-CDE              
           MOVE DTATTR-FSCL-WK-NBR         TO ML254-FSCL-WK-NBR                 
           MOVE DTATTR-FSCL-WK-END-DTE     TO ML254-FSCL-WK-END-DTE             
           MOVE CC-FSCL-END-DTE            TO ML254-FSCL-MN-END-DTE             
           MOVE DTATTR-FSCL-MN-NBR         TO ML254-FSCL-MN-NBR                 
           MOVE MLT00004-SLS-REG-RTL-AMT                                        
             TO PV-UNKN-SLS-REG-RTL-AMT                                         
           MOVE MLT00004-SLS-PROMO-RTL-AMT                                      
             TO PV-UNKN-SLS-PROMO-RTL-AMT                                       
           MOVE MLT00004-SLS-PROCLR-RTL-AMT                                     
             TO PV-UNKN-SLS-PROCLR-RTL-AMT                                      
           MOVE MLT00004-SLS-CLR-RTL-AMT                                        
             TO PV-UNKN-SLS-CLR-RTL-AMT                                         
           MOVE MLT00004-SLS-OTH-RTL-AMT                                        
             TO PV-UNKN-SLS-OTH-RTL-AMT                                         
           COMPUTE ML254-SLS-REG-RTL-AMT       =                                
                   MLT00004-SLS-REG-RTL-AMT    * -1                             
           COMPUTE ML254-SLS-PROMO-RTL-AMT     =                                
                   MLT00004-SLS-PROMO-RTL-AMT  * -1                             
           COMPUTE ML254-SLS-PROCLR-RTL-AMT    =                                
                   MLT00004-SLS-PROCLR-RTL-AMT * -1                             
           COMPUTE ML254-SLS-CLR-RTL-AMT       =                                
                   MLT00004-SLS-CLR-RTL-AMT    * -1                             
           COMPUTE ML254-SLS-OTH-RTL-AMT       =                                
                   MLT00004-SLS-OTH-RTL-AMT    * -1                             
           COMPUTE ML254-END-INV-RTL-AMT       =                                
                   PV-UNKN-TOT-SLS-RTL-AMT     +                                
                   MLT00004-END-INV-RTL-AMT                                     
           MOVE ZERO                                                            
             TO PV-END-INV-RTL-AMT-TOT                                          
           MOVE ML254-END-INV-RTL-AMT                                           
             TO PV-END-INV-RTL-AMT-REM                                          
           SET ML254-SALE                  TO TRUE.                             
                                                                                
      * WRITE REVERSAL SALE TRANSACTION                                         
           PERFORM Z310-WRITE-TRANSACTION.                                      
           ADD 1 TO PV-REVERSALS-WRITTEN.                                       
                                                                                
           COMPUTE ML254-SLS-REG-RTL-AMT       =                                
                   ML254-SLS-REG-RTL-AMT       * -1                             
           COMPUTE ML254-SLS-PROMO-RTL-AMT     =                                
                   ML254-SLS-PROMO-RTL-AMT     * -1                             
           COMPUTE ML254-SLS-PROCLR-RTL-AMT    =                                
                   ML254-SLS-PROCLR-RTL-AMT    * -1                             
           COMPUTE ML254-SLS-CLR-RTL-AMT       =                                
                   ML254-SLS-CLR-RTL-AMT       * -1                             
           COMPUTE ML254-SLS-OTH-RTL-AMT       =                                
                   ML254-SLS-OTH-RTL-AMT       * -1                             
           COMPUTE ML254-INV-ADJ-RTL-AMT       =                                
                   ML254-SLS-REG-RTL-AMT       +                                
                   ML254-SLS-PROMO-RTL-AMT     +                                
                   ML254-SLS-PROCLR-RTL-AMT    +                                
                   ML254-SLS-CLR-RTL-AMT       +                                
                   ML254-SLS-OTH-RTL-AMT       +                                
                   MLT00004-END-INV-RTL-AMT.                                    
           MOVE ZERO                                                            
             TO PV-INV-ADJ-RTL-AMT-TOT.                                         
           MOVE ML254-INV-ADJ-RTL-AMT                                           
             TO PV-INV-ADJ-RTL-AMT-REM                                          
                PV-UNKN-INV-ADJ-RTL-AMT.                                        
           MOVE ZERO                                                            
             TO ML254-SLS-REG-RTL-AMT                                           
                ML254-SLS-PROMO-RTL-AMT                                         
                ML254-SLS-PROCLR-RTL-AMT                                        
                ML254-SLS-CLR-RTL-AMT                                           
                ML254-SLS-OTH-RTL-AMT                                           
                ML254-END-INV-RTL-AMT.                                          
                                                                                
           SET ML254-INV-ADJ TO TRUE.                                           
      * WRITE UNKNOWN INVENTORY ADJUSTMENT                                      
           PERFORM Z310-WRITE-TRANSACTION.                                      
           ADD 1 TO PV-UNKN-INV-ADJ-WRITTEN.                                    
                                                                                
      ******************************************************************        
      *    CREATE INVENTORY ADJUSTMENT RE-ALLOCATION TRANSACTIONS               
      ******************************************************************        
       B400-INV-ADJ-REALLOCATE.                                                 
                                                                                
           MOVE PV-INVA-DEPT-NBR           TO ML254-DEPT-NBR.                   
           MOVE 10                         TO ML254-MAJ-CL-NBR.                 
           MOVE 10                         TO ML254-SUB-CL-NBR.                 
           MOVE 400                        TO ML254-LOC-NBR.                    
           MOVE PV-INVA-ML-LO-MDSE-LVL-ID  TO ML254-ML-LWST-ID.                 
           IF PV-KNWN-SLS-REG-RTL-AMT NOT = ZERO                                
               COMPUTE PV-ML254-SLS-REG-RTL-AMT ROUNDED =                       
                      (PV-INVA-SLS-REG-RTL-AMT /                                
                       PV-KNWN-SLS-REG-RTL-AMT) *                               
                       PV-UNKN-SLS-REG-RTL-AMT                                  
               COMPUTE ML254-SLS-REG-RTL-AMT ROUNDED =                          
                       PV-ML254-SLS-REG-RTL-AMT * 1                             
           ELSE                                                                 
               MOVE ZERO TO ML254-SLS-REG-RTL-AMT                               
           END-IF                                                               
           IF PV-KNWN-SLS-PROMO-RTL-AMT NOT = ZERO                              
               COMPUTE PV-ML254-SLS-PROMO-RTL-AMT ROUNDED =                     
                      (PV-INVA-SLS-PROMO-RTL-AMT /                              
                       PV-KNWN-SLS-PROMO-RTL-AMT) *                             
                       PV-UNKN-SLS-PROMO-RTL-AMT                                
               COMPUTE ML254-SLS-PROMO-RTL-AMT ROUNDED =                        
                       PV-ML254-SLS-PROMO-RTL-AMT * 1                           
           ELSE                                                                 
               MOVE ZERO TO ML254-SLS-PROMO-RTL-AMT                             
           END-IF                                                               
           IF PV-KNWN-SLS-PROCLR-RTL-AMT NOT = ZERO                             
               COMPUTE PV-ML254-SLS-PROCLR-RTL-AMT ROUNDED =                    
                      (PV-INVA-SLS-PROCLR-RTL-AMT /                             
                       PV-KNWN-SLS-PROCLR-RTL-AMT) *                            
                       PV-UNKN-SLS-PROCLR-RTL-AMT                               
               COMPUTE ML254-SLS-PROCLR-RTL-AMT ROUNDED =                       
                       PV-ML254-SLS-PROCLR-RTL-AMT * 1                          
           ELSE                                                                 
               MOVE ZERO TO ML254-SLS-PROCLR-RTL-AMT                            
           END-IF                                                               
           IF PV-KNWN-SLS-CLR-RTL-AMT NOT = ZERO                                
               COMPUTE PV-ML254-SLS-CLR-RTL-AMT ROUNDED =                       
                      (PV-INVA-SLS-CLR-RTL-AMT /                                
                       PV-KNWN-SLS-CLR-RTL-AMT) *                               
                       PV-UNKN-SLS-CLR-RTL-AMT                                  
               COMPUTE ML254-SLS-CLR-RTL-AMT ROUNDED =                          
                       PV-ML254-SLS-CLR-RTL-AMT * 1                             
           ELSE                                                                 
               MOVE ZERO TO ML254-SLS-CLR-RTL-AMT                               
           END-IF                                                               
           IF PV-KNWN-SLS-OTH-RTL-AMT NOT = ZERO                                
               COMPUTE PV-ML254-SLS-OTH-RTL-AMT ROUNDED =                       
                      (PV-INVA-SLS-OTH-RTL-AMT /                                
                       PV-KNWN-SLS-OTH-RTL-AMT) *                               
                       PV-UNKN-SLS-OTH-RTL-AMT                                  
               COMPUTE ML254-SLS-OTH-RTL-AMT ROUNDED =                          
                       PV-ML254-SLS-OTH-RTL-AMT * 1                             
           ELSE                                                                 
               MOVE ZERO TO ML254-SLS-OTH-RTL-AMT                               
           END-IF                                                               
           MOVE ZERO TO ML254-INV-ADJ-RTL-AMT.                                  
           COMPUTE PV-NEW-SLS-RTL-AMT          =                                
                   ML254-SLS-REG-RTL-AMT       +                                
                   ML254-SLS-PROMO-RTL-AMT     +                                
                   ML254-SLS-PROCLR-RTL-AMT    +                                
                   ML254-SLS-CLR-RTL-AMT       +                                
                   ML254-SLS-CLR-RTL-AMT.                                       
           COMPUTE ML254-END-INV-RTL-AMT    =                                   
                  (PV-INVA-END-INV-RTL-AMT +                                    
                   PV-NEW-SLS-RTL-AMT) * -1                                     
           COMPUTE PV-SLS-REG-RTL-AMT-REM =                                     
                   PV-SLS-REG-RTL-AMT-REM -                                     
                   ML254-SLS-REG-RTL-AMT.                                       
           COMPUTE PV-SLS-PROMO-RTL-AMT-REM  =                                  
                   PV-SLS-PROMO-RTL-AMT-REM  -                                  
                   ML254-SLS-PROMO-RTL-AMT.                                     
           COMPUTE PV-SLS-PROCLR-RTL-AMT-REM  =                                 
                   PV-SLS-PROCLR-RTL-AMT-REM  -                                 
                   ML254-SLS-PROCLR-RTL-AMT.                                    
           COMPUTE PV-SLS-CLR-RTL-AMT-REM  =                                    
                   PV-SLS-CLR-RTL-AMT-REM  -                                    
                   ML254-SLS-CLR-RTL-AMT.                                       
           COMPUTE PV-SLS-OTH-RTL-AMT-REM  =                                    
                   PV-SLS-OTH-RTL-AMT-REM  -                                    
                   ML254-SLS-OTH-RTL-AMT.                                       
           COMPUTE PV-END-INV-RTL-AMT-TOT  =                                    
                   PV-END-INV-RTL-AMT-TOT  -                                    
                   ML254-END-INV-RTL-AMT.                                       
           SET ML254-SALE TO TRUE.                                              
      * WRITE RE-ALLOCATION TRANSACTION                                         
           PERFORM Z310-WRITE-TRANSACTION.                                      
           ADD 1 TO PV-KNWN-SALE-RE-WRITTEN.                                    
           MOVE ZERO                                                            
             TO ML254-SLS-REG-RTL-AMT                                           
                ML254-SLS-PROMO-RTL-AMT                                         
                ML254-SLS-PROCLR-RTL-AMT                                        
                ML254-SLS-CLR-RTL-AMT                                           
                ML254-SLS-OTH-RTL-AMT.                                          
           IF PV-KNWN-END-INV-RTL-AMT NOT = ZERO                                
               COMPUTE PV-ML254-INV-ADJ-RTL-AMT ROUNDED =                       
                      (PV-INVA-END-INV-RTL-AMT /                                
                       PV-KNWN-END-INV-RTL-AMT) *                               
                       PV-UNKN-INV-ADJ-RTL-AMT                                  
               COMPUTE ML254-INV-ADJ-RTL-AMT ROUNDED =                          
                       PV-ML254-INV-ADJ-RTL-AMT * -1                            
           ELSE                                                                 
               MOVE ZERO TO ML254-INV-ADJ-RTL-AMT                               
           END-IF.                                                              
           COMPUTE ML254-END-INV-RTL-AMT =                                      
                  (ML254-END-INV-RTL-AMT -                                      
                   ML254-INV-ADJ-RTL-AMT) * -1                                  
      * WRITE RE-ALLOCATION TRANSACTION                                         
           SET ML254-INV-ADJ TO TRUE.                                           
           PERFORM Z240-FETCH-DEPT-INV-ADJ-CSR.                                 
           IF END-OF-DEPT-INV-ADJ-CSR                                           
               MOVE PV-SLS-REG-RTL-AMT-REM                                      
                 TO ML254-SLS-REG-RTL-AMT                                       
               MOVE PV-SLS-PROMO-RTL-AMT-REM                                    
                 TO ML254-SLS-PROMO-RTL-AMT                                     
               MOVE PV-SLS-PROCLR-RTL-AMT-REM                                   
                 TO ML254-SLS-PROCLR-RTL-AMT                                    
               MOVE PV-SLS-CLR-RTL-AMT-REM                                      
                 TO ML254-SLS-CLR-RTL-AMT                                       
               MOVE PV-SLS-OTH-RTL-AMT-REM                                      
                 TO ML254-SLS-OTH-RTL-AMT                                       
               COMPUTE ML254-INV-ADJ-RTL-AMT  =                                 
                       PV-INV-ADJ-RTL-AMT-TOT -                                 
                       PV-INV-ADJ-RTL-AMT-REM                                   
               COMPUTE ML254-END-INV-RTL-AMT  =                                 
                       PV-END-INV-RTL-AMT-TOT -                                 
                       PV-END-INV-RTL-AMT-REM                                   
           ELSE                                                                 
               COMPUTE PV-INV-ADJ-RTL-AMT-TOT  =                                
                       PV-INV-ADJ-RTL-AMT-TOT  -                                
                       ML254-INV-ADJ-RTL-AMT                                    
               COMPUTE PV-END-INV-RTL-AMT-TOT  =                                
                       PV-END-INV-RTL-AMT-TOT  -                                
                       ML254-END-INV-RTL-AMT                                    
           END-IF                                                               
           PERFORM Z310-WRITE-TRANSACTION.                                      
           ADD 1 TO PV-KNWN-INV-ADJ-RE-WRITTEN.                                 
                                                                                
      ******************************************************************        
      *    FINALIZE AND CLOSE OUT FILES                                         
      ******************************************************************        
       B900-FINALIZE.                                                           
                                                                                
           PERFORM Z120-CLOSE-UNKN-CSR.                                         
                                                                                
           DISPLAY ' **** TOTAL FETCHED                : '                      
                          PV-TOT-FETCHED                                        
           DISPLAY ' **** TOTAL ACTLS FETCHED          : '                      
                          PV-ACTLS-FETCHED                                      
           DISPLAY ' **** TOTAL KNOWN SUMS NOT FOUND   : '                      
                          PV-KNWN-SUMS-NOT-FOUND                                
           DISPLAY ' **** TOTAL REVERSALS WRITTEN      : '                      
                          PV-REVERSALS-WRITTEN                                  
           DISPLAY ' **** TOTAL UNKNOWN INV ADJ WRITTEN: '                      
                          PV-UNKN-INV-ADJ-WRITTEN                               
           DISPLAY ' **** TOTAL INV ADJ FETCHED        : '                      
                          PV-INV-ADJ-FETCHED                                    
           DISPLAY ' **** TOTAL KNOWN SALE WRITTEN     : '                      
                          PV-KNWN-SALE-RE-WRITTEN                               
           DISPLAY ' **** TOTAL KNOWN INV ADJ WRITTEN  : '                      
                          PV-KNWN-INV-ADJ-RE-WRITTEN                            
                                                                                
           CLOSE   DATE-FILE                                                    
                   ADJUSTMENT-FILE.                                             
                                                                                
                                                                                
      ******************************************************************        
      *    PROCESS REQUEST CONTROL CARD AND/OR GET REQUEST DATES                
      ******************************************************************        
       C100-PROCESS-RQ-CNTL.                                                    
                                                                                
           PERFORM Z500-READ-DATES.                                             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'FISCAL BEG DATE: ' CC-FSCL-BEG-DTE                          
           DISPLAY 'FISCAL END DATE: ' CC-FSCL-END-DTE                          
           DISPLAY ' '.                                                         
                                                                                
      ******************************************************************        
      *    OPEN DEPARTMENT CURSOR                                               
      *    - GET UNKNOWN VENDORS AND UNKNOWN BRANDS                             
      ******************************************************************        
       Z100-OPEN-UNKN-CSR.                                                      
                                                                                
           EXEC SQL                                                             
               OPEN UNKN_CSR                                                    
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               SET NOT-END-OF-UNKN-CSR TO TRUE                                  
           ELSE                                                                 
               DISPLAY 'ENT_ID     : ' PC-UNKNOWN-BRAND                         
               MOVE SQLCODE            TO PV-SQLCODE                            
               DISPLAY 'SQLCODE    : ' PV-SQLCODE                               
               MOVE 'OPEN UNKN_CSR   ' TO PV-OPER-ATTEMPTED                     
               PERFORM Z950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    FETCH THE NEXT ROW FROM THE UNKN_CSR CURSOR                          
      ******************************************************************        
       Z110-FETCH-UNKN-CSR.                                                     
                                                                                
           EXEC SQL                                                             
             FETCH UNKN_CSR                                                     
              INTO  :MLV00000-ML-LO-MDSE-LVL-ID                                 
                   ,:MLV00000-DEPT-NBR                                          
                   ,:MLV00000-LBL-SEQ-NBR                                       
                   ,:MLV00000-ENT-ID                                            
                   ,:MLV00000-EFF-BEG-DTE                                       
                   ,:MLV00000-EFF-END-DTE                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   ADD +1                  TO PV-FETCHED                        
                                              PV-TOT-FETCHED                    
               WHEN +100                                                        
                   SET END-OF-UNKN-CSR TO TRUE                                  
               WHEN OTHER                                                       
                   MOVE 'FETCH UNKN_CSR' TO    PV-OPER-ATTEMPTED                
                   PERFORM Z950-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    CLOSE THE UNKN_CSR                                                   
      ******************************************************************        
       Z120-CLOSE-UNKN-CSR.                                                     
                                                                                
           EXEC SQL                                                             
                CLOSE UNKN_CSR                                                  
           END-EXEC                                                             
           IF SQLCODE NOT = +0                                                  
               MOVE 'CLOSE UNKN_CSR    ' TO PV-OPER-ATTEMPTED                   
               PERFORM Z950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    GET KNOWN VENDOR SUM FOR A DEPARTMENT                                
      ******************************************************************        
       Z150-GET-KNOWN-VENDOR-SUM.                                               
           DISPLAY 'Z150-GET-KNOWN-VENDOR-SUM.'                                 
                                                                                
           EXEC SQL                                                             
             SELECT VALUE(SUM(SLS_REG_RTL_AMT),0)                               
                   ,VALUE(SUM(SLS_PROMO_RTL_AMT),0)                             
                   ,VALUE(SUM(SLS_PROCLR_RTL_AMT),0)                            
                   ,VALUE(SUM(SLS_CLR_RTL_AMT),0)                               
                   ,VALUE(SUM(SLS_OTH_RTL_AMT),0)                               
                   ,VALUE(SUM(END_INV_RTL_AMT),0)                               
              INTO  :PV-KNWN-SLS-REG-RTL-AMT                                    
                   ,:PV-KNWN-SLS-PROMO-RTL-AMT                                  
                   ,:PV-KNWN-SLS-PROCLR-RTL-AMT                                 
                   ,:PV-KNWN-SLS-CLR-RTL-AMT                                    
                   ,:PV-KNWN-SLS-OTH-RTL-AMT                                    
                   ,:PV-KNWN-END-INV-RTL-AMT                                    
              FROM MLT_MNLY_SUM                                                 
             WHERE PARTN_NBR IN                                                 
                      (SELECT PARTN_NBR                                         
                         FROM MLT_PARTN_CNTL                                    
                        WHERE TBL_TM_LVL_CDE = :PC-MONTHLY-TM-LVL-CDE           
                          AND FSCL_DTE BETWEEN :CC-FSCL-BEG-DTE                 
                                           AND :CC-FSCL-END-DTE)                
               AND DEPT_NBR          = :MLT00004-DEPT-NBR                       
               AND ML_LO_MDSE_LVL_ID NOT IN                                     
                      (SELECT A.UNKN_BRND_VND_ID                                
                         FROM MLT_UNKN_BRND_VND A                               
                        WHERE A.DEPT_NBR = DEPT_NBR)                            
             GROUP BY DEPT_NBR                                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   COMPUTE PV-KNWN-TOT-SLS-RTL-AMT     =                        
                           PV-KNWN-SLS-REG-RTL-AMT     +                        
                           PV-KNWN-SLS-PROMO-RTL-AMT   +                        
                           PV-KNWN-SLS-PROCLR-RTL-AMT  +                        
                           PV-KNWN-SLS-CLR-RTL-AMT     +                        
                           PV-KNWN-SLS-OTH-RTL-AMT                              
               WHEN +100                                                        
                   MOVE ZERO TO PV-KNWN-TOT-SLS-RTL-AMT                         
                                PV-KNWN-SLS-REG-RTL-AMT                         
                                PV-KNWN-SLS-PROMO-RTL-AMT                       
                                PV-KNWN-SLS-PROCLR-RTL-AMT                      
                                PV-KNWN-SLS-CLR-RTL-AMT                         
                                PV-KNWN-SLS-OTH-RTL-AMT                         
                   ADD 1 TO PV-KNWN-SUMS-NOT-FOUND                              
               WHEN OTHER                                                       
                   MOVE 'GET KNOWN SUMS' TO    PV-OPER-ATTEMPTED                
                   PERFORM Z950-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    OPEN ACTUALS SUMS CURSOR                                             
      ******************************************************************        
       Z200-OPEN-ACTLS-SUM-CSR.                                                 
                                                                                
           EXEC SQL                                                             
               OPEN ACTLS_SUM_CSR                                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               SET NOT-END-OF-ACTLS-SUM-CSR TO TRUE                             
           ELSE                                                                 
               DISPLAY '*** DEPT_NBR          ' MLT00004-DEPT-NBR               
               DISPLAY '*** ML_LO_MDSE_LVL_ID '                                 
                                              MLT00004-ML-LO-MDSE-LVL-ID        
               MOVE 'OPEN ACTLS_SUM_CSR ' TO PV-OPER-ATTEMPTED                  
               PERFORM Z950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    FETCH THE NEXT ROW FROM THE ACTLS_SUM_CSR CURSOR                     
      ******************************************************************        
       Z210-FETCH-ACTLS-SUM-CSR.                                                
                                                                                
           EXEC SQL                                                             
             FETCH ACTLS_SUM_CSR                                                
               INTO  :MLT00004-DEPT-NBR                                         
                    ,:MLT00004-ML-LO-MDSE-LVL-ID                                
                    ,:MLT00004-SLS-REG-RTL-AMT                                  
                    ,:MLT00004-SLS-PROMO-RTL-AMT                                
                    ,:MLT00004-SLS-PROCLR-RTL-AMT                               
                    ,:MLT00004-SLS-CLR-RTL-AMT                                  
                    ,:MLT00004-SLS-OTH-RTL-AMT                                  
                    ,:MLT00004-END-INV-RTL-AMT                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   ADD +1                  TO PV-ACTLS-FETCHED                  
                   COMPUTE PV-UNKN-TOT-SLS-RTL-AMT     =                        
                           MLT00004-SLS-REG-RTL-AMT    +                        
                           MLT00004-SLS-PROMO-RTL-AMT  +                        
                           MLT00004-SLS-PROCLR-RTL-AMT +                        
                           MLT00004-SLS-CLR-RTL-AMT    +                        
                           MLT00004-SLS-OTH-RTL-AMT                             
               WHEN +100                                                        
                   SET END-OF-ACTLS-SUM-CSR TO TRUE                             
               WHEN OTHER                                                       
                   MOVE 'FETCH ACTLS_SUM_CSR' TO   PV-OPER-ATTEMPTED            
                   PERFORM Z950-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    CLOSE THE ACTLS_SUM_CSR                                              
      ******************************************************************        
       Z220-CLOSE-ACTLS-SUM-CSR.                                                
                                                                                
           EXEC SQL                                                             
                CLOSE ACTLS_SUM_CSR                                             
           END-EXEC                                                             
           IF SQLCODE NOT = +0                                                  
               MOVE 'CLOSE ACTLS_SUM_CSR' TO PV-OPER-ATTEMPTED                  
               PERFORM Z950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    OPEN DEPARTMENT INVENTORY ADJUSTMENTS CURSOR                         
      ******************************************************************        
       Z230-OPEN-DEPT-INV-ADJ-CSR.                                              
                                                                                
           EXEC SQL                                                             
               OPEN DEPT_INV_ADJ_CSR                                            
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               SET NOT-END-OF-DEPT-INV-ADJ-CSR TO TRUE                          
           ELSE                                                                 
               DISPLAY '*** DEPT_NBR          ' MLT00004-DEPT-NBR               
               DISPLAY '*** ML_LO_MDSE_LVL_ID '                                 
                                              MLT00004-ML-LO-MDSE-LVL-ID        
               MOVE 'OPEN DEPT_INV_ADJ_CSR ' TO PV-OPER-ATTEMPTED               
               PERFORM Z950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    FETCH THE NEXT ROW FROM THE DEPT_INV_ADJ_CSR CURSOR                  
      ******************************************************************        
       Z240-FETCH-DEPT-INV-ADJ-CSR.                                             
                                                                                
           EXEC SQL                                                             
             FETCH DEPT_INV_ADJ_CSR                                             
               INTO  :PV-INVA-DEPT-NBR                                          
                    ,:PV-INVA-ML-LO-MDSE-LVL-ID                                 
                    ,:PV-INVA-SLS-REG-RTL-AMT                                   
                    ,:PV-INVA-SLS-PROMO-RTL-AMT                                 
                    ,:PV-INVA-SLS-PROCLR-RTL-AMT                                
                    ,:PV-INVA-SLS-CLR-RTL-AMT                                   
                    ,:PV-INVA-SLS-OTH-RTL-AMT                                   
                    ,:PV-INVA-END-INV-RTL-AMT                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   ADD +1                  TO PV-INV-ADJ-FETCHED                
               WHEN +100                                                        
                   SET END-OF-DEPT-INV-ADJ-CSR TO TRUE                          
               WHEN OTHER                                                       
                   MOVE 'FETCH DEPT_INV_ADJ_CSR' TO PV-OPER-ATTEMPTED           
                   PERFORM Z950-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    CLOSE THE DEPT_INV_ADJ_CSR                                           
      ******************************************************************        
       Z250-CLOSE-DEPT-INV-ADJ-CSR.                                             
                                                                                
           EXEC SQL                                                             
                CLOSE DEPT_INV_ADJ_CSR                                          
           END-EXEC                                                             
           IF SQLCODE NOT = +0                                                  
               MOVE 'CLOSE DEPT_INV_ADJ_CSR' TO PV-OPER-ATTEMPTED               
               PERFORM Z950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  GET PARTITION INFORMATION FOR A GIVEN FISCAL DATE                      
      ******************************************************************        
       Z300-GET-PARTN-INFO.                                                     
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                       B.PARTN_NBR                                              
                      ,B.DEPT_GP_CDE                                            
                      ,D.FSCL_MN_NBR                                            
                      ,D.FSCL_WK_NBR                                            
                      ,D.PFM_END_DTE                                            
                      ,D.PFW_END_DTE                                            
                      ,D.FSCL_MN_END_DTE                                        
                      ,D.FSCL_WK_END_DTE                                        
                      ,D.SEA_END_DTE                                            
                 INTO                                                           
                       :MLT00003-PARTN-NBR                                      
                      ,:MLT00003-DEPT-GP-CDE                                    
                      ,:DTATTR-FSCL-MN-NBR                                      
                      ,:DTATTR-FSCL-WK-NBR                                      
                      ,:DTATTR-PFM-END-DTE                                      
                      ,:DTATTR-PFW-END-DTE                                      
                      ,:DTATTR-FSCL-MN-END-DTE                                  
                      ,:DTATTR-FSCL-WK-END-DTE                                  
                      ,:DTATTR-SEA-END-DTE                                      
                 FROM                                                           
                       MLT_PARTN_CNTL  B                                        
                      ,TDTATTR         D                                        
                 WHERE                                                          
                       B.TBL_TM_LVL_CDE = :MLT00003-TBL-TM-LVL-CDE              
                   AND B.FSCL_DTE = :MLT00003-FSCL-DTE                          
                   AND B.DEPT_GP_CDE IN (                                       
                         SELECT  C.DEPT_GP_CDE                                  
                           FROM  MLT_PARTN_BAL  C                               
                           WHERE C.TBL_TM_LVL_CDE                               
                                     = :MLT00003-TBL-TM-LVL-CDE                 
                             AND C.DEPT_NBR = :MLT00002-DEPT-NBR                
                             AND :MLT00003-FSCL-DTE                             
                                 BETWEEN C.EFF_BEG_DTE AND                      
                                     VALUE (C.EFF_END_DTE,                      
                                         '9999-12-30')                          
                           )                                                    
                   AND KY_DTE = :MLT00003-FSCL-DTE                              
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   DISPLAY ' TABLE LEVEL: ' MLT00003-TBL-TM-LVL-CDE             
                           ' FISCAL DATE: ' MLT00003-FSCL-DTE                   
                   MOVE 'SELECT CURR PARTN_CNTL'                                
                                               TO PV-OPER-ATTEMPTED             
                   MOVE 'Z300-GET-PARTN-INFO ' TO PV-PARAGRAPH-NAME             
                   PERFORM Z950-SQL-ABEND                                       
               WHEN OTHER                                                       
                   DISPLAY ' TABLE LEVEL: ' MLT00003-TBL-TM-LVL-CDE             
                           ' FISCAL DATE: ' MLT00003-FSCL-DTE                   
                   MOVE 'SELECT CURR PARTN_CNTL'                                
                                               TO PV-OPER-ATTEMPTED             
                   MOVE 'Z300-GET-PARTN-INFO ' TO PV-PARAGRAPH-NAME             
                   PERFORM Z950-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * WRITE ADJUSTMENT TRANSACTION (MLRD254 FORMAT)                           
      ******************************************************************        
       Z310-WRITE-TRANSACTION.                                                  
                                                                                
           WRITE ADJUSTMENT-REC FROM ML254-UPDATE-REC.                          
           ADD 1 TO PV-TOT-TRANS-WRITTEN.                                       
                                                                                
      ******************************************************************        
      *    READ DATE CONTROL CARD                                               
      ******************************************************************        
       Z500-READ-DATES.                                                         
                                                                                
           READ DATE-FILE                                                       
               INTO CC-CONTROL-CARD                                             
               AT END SET END-OF-DATES TO TRUE.                                 
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                                    
      ******************************************************************        
       Z950-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  MLKUT362'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
           DISPLAY '**  PARAGRAPH **  = '  PV-PARAGRAPH-NAME                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM Z999-ABEND.                                                  
                                                                                
      ******************************************************************        
      *    ABEND CALL                                                           
      ******************************************************************        
       Z999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
