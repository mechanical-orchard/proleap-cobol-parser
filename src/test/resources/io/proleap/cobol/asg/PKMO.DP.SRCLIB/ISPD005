      *****************************************************************         
      **                                                                        
      **   MQSERIES COMPLETION & REASON CODE ROUTINES FOR CICS.                 
      **   USE IN CONJUCTION WITH ISWS005.                                      
      **   BATCH VERSION OF THIS IS ISPD002.                                    
      **                                                                        
      *****************************************************************         
       9001-MQSERIES-ERROR.                                                     
                                                                                
           PERFORM  VARYING IS-COMPLETION FROM 1 BY 1                           
                      UNTIL PV-COMPLETION-CODE                                  
                          = IS-COMPLETION-CODE (IS-COMPLETION)                  
                         OR IS-COMPLETION >= IS-COMPLETION-MAX                  
               CONTINUE                                                         
           END-PERFORM.                                                         
           IF IS-COMPLETION >= IS-COMPLETION-MAX                                
               MOVE IS-COMPLETION-MAX TO IS-COMPLETION.                         
                                                                                
           CALL IS-MQCHECK USING PV-REASON                                      
                                 IS-REASON-TEXT.                                
                                                                                
                                                                                
           EXEC CICS ASKTIME                                                    
               ABSTIME (IS-ABSTIME)                                             
           END-EXEC.                                                            
           EXEC CICS FORMATTIME                                                 
               ABSTIME  (IS-ABSTIME)                                            
               MMDDYYYY (IS-LOG-DATE)                                           
               DATESEP                                                          
               TIME     (IS-LOG-TIME)                                           
               TIMESEP                                                          
           END-EXEC.                                                            
                                                                                
                                                                                
           PERFORM 9002-ENQUEUE.                                                
                                                                                
           MOVE SPACES TO IS-LOG-LINE.                                          
           STRING ' ' PC-PGM-NAME ' ' IS-LOG-DATE ' ' IS-LOG-TIME ' '           
                  '*** MQSERIES ERROR: ' PV-ACTION                              
             DELIMITED BY SIZE INTO IS-LOG-LINE.                                
           PERFORM 9003-WRITEQ-TD.                                              
                                                                                
           MOVE SPACES TO IS-LOG-LINE.                                          
           STRING ' ' PC-PGM-NAME ' ' IS-LOG-DATE ' ' IS-LOG-TIME ' '           
                  '    COMPLETION CODE = '                                      
                        IS-COMPLETION-CODE      (IS-COMPLETION)                 
                  ' - ' IS-COMPLETION-CODE-DESC (IS-COMPLETION)                 
             DELIMITED BY SIZE INTO IS-LOG-LINE.                                
           PERFORM 9003-WRITEQ-TD.                                              
                                                                                
           MOVE SPACES    TO IS-LOG-LINE.                                       
           MOVE PV-REASON TO IS-REASON.                                         
           STRING ' ' PC-PGM-NAME ' ' IS-LOG-DATE ' ' IS-LOG-TIME ' '           
                  '             REASON = ' IS-REASON                            
                  ' - ' IS-REASON-TEXT                                          
             DELIMITED BY SIZE INTO IS-LOG-LINE.                                
           PERFORM 9003-WRITEQ-TD.                                              
                                                                                
           PERFORM 9004-DEQUEUE.                                                
                                                                                
           EXEC CICS ABEND                                                      
               ABCODE(IS-REASON)                                                
           END-EXEC.                                                            
                                                                                
                                                                                
       9002-ENQUEUE.                                                            
           EXEC CICS ENQ                                                        
               RESOURCE (IS-LOG-QUEUE-RESOURCE)                                 
           END-EXEC.                                                            
                                                                                
                                                                                
       9003-WRITEQ-TD.                                                          
           EXEC CICS WRITEQ TD                                                  
               QUEUE (IS-LOG-QUEUE)                                             
               FROM  (IS-LOG-LINE)                                              
           END-EXEC.                                                            
                                                                                
                                                                                
       9004-DEQUEUE.                                                            
           EXEC CICS DEQ                                                        
               RESOURCE (IS-LOG-QUEUE-RESOURCE)                                 
           END-EXEC.                                                            
