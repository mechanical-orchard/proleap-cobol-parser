      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.             INKUT149.                                        
       AUTHOR.                 KRISTIN PETERSON.                                
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            10-19-09.                                        
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *       THIS PROGRAM READS THE STORE SHORTAGE RANKING FILE                
      *       AND ADDS THE STORE'S PRIME DC TO EACH RECORD.                     
      *                                                                         
W33304*       THIS PROGRAM WILL ONLY BE RUN AFTER FINANCIAL BOOKING TO          
W33304*       CREATE FINAL STORE SHORTAGE REPORTS INK90105 & INK90110.          
W33304*                                                                         
W33304*       THIS PROGRAM IS A COPY OF PROGRAM INKUT159 THAT WAS               
W33304*       MODIFIED TO ONLY WRITE OUTPUT RECORDS FOR STORES THAT             
W33304*       HAVE FINANCIALLY BOOKED.                                          
W33304*                                                                         
      *  CHANGES:                                                               
W33304* W33304  12-12-2008 SATHISH/     HOLDING INVENTORY LEDGER OPEN           
W33304*                    INFOSYS      (HILO) CHANGES.  LENGTH CHANGE          
W33304*                                 FOR INPUT & OUTPUT FILES.  ALSO         
W33304*                                 FILL THREE ADDTIONAL FIELDS ON          
W33304*                                 OUTPUT REC.                             
W33304* W33304  10-19-2009 K. PETERSON  HILO - CHANGE TO ONLY WRITE             
W33304*                                 OUTPUT RECS FOR LOCS THAT HAVE          
W33304*                                 FINANCIALLY BOOKED WHEN THIS            
W33304*                                 PROGRAM IS CREATING THE FINAL           
W33304*                                 INK901* REPORTS FOR THE STORES.         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT IN-DEPT-STR-RANK                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUT-DEPT-STR-RANK-DC                                          
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  IN-DEPT-STR-RANK                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
      *    RECORD CONTAINS 94 CHARACTERS                                        
           DATA RECORD IS DEPT-STR-RANK-RECORD.                                 
                                                                                
W33304 01  IN-DEPT-STR-RANK-RECORD        PIC X(126).                           
                                                                                
       FD  OUT-DEPT-STR-RANK-DC                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
      *    RECORD CONTAINS 96 CHARACTERS                                        
           DATA RECORD IS DEPT-STR-RANK-DC-RECORD.                              
                                                                                
W33304 01  OUT-DEPT-STR-RANK-DC-RECORD     PIC X(128).                          
                                                                                
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
      *01  IN-DEPT-STORE-LAYOUT.                                                
      *    05  FILLER                 PIC X(25).                                
      *    05  IN-STORE               PIC X(03).                                
      *    05  IN-REST-OF-RECORD      PIC X(95).                                
                                                                                
      *01  OUT-DEPT-STORE-LAYOUT.                                               
      *    05  OUT-DEPT-STR-RANK-RECORD PIC X(113).                             
      *    05  OUT-REST-OF-RECORD.                                              
      *        10  FILLER              PIC X(01)    VALUE ','.                  
      *        10  OUT-PRIME-DC        PIC X(02).                               
                                                                                
       01  ABEND-CODE                  PIC S9(4)    VALUE ZERO                  
                                       COMP SYNC.                               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-DEPTST-FILE-IND                                        
                                       PIC X(1)     VALUE 'N'.                  
               88  PS-NOT-END-OF-DEPTST-FILE        VALUE 'N'.                  
               88  PS-END-OF-DEPTST-FILE            VALUE 'Y'.                  
           05  PS-END-OF-XMTLOC-CURSOR-IND                                      
                                       PIC X(1)     VALUE 'N'.                  
               88  PS-END-OF-XMTLOC-CURSOR          VALUE 'Y'.                  
W33304     05  PS-LOC-FINCL-BOOKED-IND PIC X(1)     VALUE 'N'.                  
W33304         88  PS-LOC-FINCL-BOOKED              VALUE 'Y'.                  
W33304     05  PS-VALID-RECORD-IND     PIC X(1)     VALUE 'N'.                  
W33304         88  PS-VALID-RECORD                  VALUE 'Y'.                  
       01  PC-PROGRAM-CONSTANTS.                                                
      *    05  PC-SUB                  PIC 9(3)     VALUE ZERO.                 
           05  PC-SUB                  PIC 9(4)     VALUE ZERO.                 
      *    05  PRIME-DC-SUB            PIC 9(3)     VALUE ZERO.                 
           05  PRIME-DC-SUB            PIC 9(4)     VALUE ZERO.                 
      *    05  STORE-SUB               PIC 9(3)     VALUE ZERO.                 
           05  STORE-SUB               PIC 9(4)     VALUE ZERO.                 
W33304     05  PC-INITIAL-STATUS       PIC X(02)    VALUE 'IN'.                 
W33304     05  PC-DEFAULT-DTE          PIC X(10)    VALUE '9999-09-09'.         
W33304     05  PC-MAXIMUM-RETRY-COUNT  PIC S9(04)   VALUE +3                    
W33304                                              COMP SYNC.                  
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PREV-STORE           PIC X(04)    VALUE SPACES.               
      *    05  PV-PREV-STORE           PIC X(03)    VALUE SPACES.               
      **** 05  PV-PREV-STORE           PIC 9(3)     VALUE ZERO.                 
           05  PV-PARAGRAPH-NAME       PIC X(30)    VALUE SPACES.               
           05  PV-DB2-OPERATION-ATTEMPTED                                       
                                       PIC X(30)    VALUE SPACES.               
W33304     05  PV-PREV-LOC-NBR         PIC 9(05)    VALUE ZEROES.               
W33304     05  PV-RETRY-COUNT          PIC S9(04)   VALUE ZEROES.               
W33304     05  PV-NBR-OF-TINVPAR-SELECTS                                        
W33304                                 PIC 9(09)    VALUE ZEROES                
W33304                                              COMP SYNC.                  
W33304     05  PV-DISPLAY-COUNT        PIC ZZZ,ZZZ,ZZ9                          
W33304                                              VALUE ZEROES.               
           05  PV-CURRENT-DATE.                                                 
               10  PV-CD-CCYY.                                                  
                   15  PC-CD-CC        PIC 9(02)    VALUE ZERO.                 
                   15  PV-CD-YY        PIC 9(02)    VALUE ZERO.                 
               10  PV-CD-MM            PIC 9(02)    VALUE ZERO.                 
               10  PV-CD-DD            PIC 9(02)    VALUE ZERO.                 
           05  PV-CURRENT-DATE-DB2.                                             
               10  PV-CD-DB2-CCYY.                                              
                   15  PV-CD-DB2-CC    PIC 9(02)    VALUE ZERO.                 
                   15  PV-CD-DB2-YY    PIC 9(02)    VALUE ZERO.                 
               10  FILLER              PIC X(01)    VALUE '-'.                  
               10  PV-CD-DB2-MM        PIC 9(02)    VALUE ZERO.                 
               10  FILLER              PIC X(01)    VALUE '-'.                  
               10  PV-CD-DB2-DD        PIC 9(02)    VALUE ZERO.                 
                                                                                
       01  WS-STORE-TABLE.                                                      
           05  WS-TABLE OCCURS 9999 TIMES.                                      
               10  TBL-STORE-NMBR          PIC X(4).                            
               10  TBL-CHAR5-NAME.                                              
                   15 FILLER               PIC X(2).                            
                   15 TBL-NAME-DASH        PIC X(1).                            
                   15 TBL-CHAR2-NAME       PIC X(2).                            
               10  FILLER REDEFINES TBL-CHAR5-NAME.                             
                   15 FILLER               PIC X(2).                            
                   15 TBL-CHAR2-NAME-OTH   PIC X(2).                            
                   15 FILLER               PIC X(1).                            
               10  TBL-DC-NMBR             PIC X(4).                            
                                                                                
      ******************************************************************        
      *  INVENTORY STORE SHORTAGE FILE....                                      
      ******************************************************************        
           COPY INRD003I.                                                       
                                                                                
      ******************************************************************        
      *  INVENTORY STORE SHORTAGE FILE WITH THE PRIMARY DC LOCATION....         
      ******************************************************************        
           COPY INRD003O.                                                       
                                                                                
      ******************************************************************        
      *  DB2 ERROR MESSAGES FORMAT AREA.                                        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  USED FOR DB2 ERRORS                                                    
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  LAYOUT USED FOR XMT LOC TABLE                                          
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  LAYOUT USED FOR XMT DC LOC ASGMT TABLE                                 
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE XMT00002                                                 
           END-EXEC.                                                            
                                                                                
W33304******************************************************************        
W33304*    LAYOUT USED FOR TINVPAR TABLE                                        
W33304******************************************************************        
W33304     EXEC SQL                                                             
W33304         INCLUDE TINVPAR                                                  
W33304     END-EXEC.                                                            
W33304                                                                          
      ******************************************************************        
      *  SQL DB2 XMT_LOC CURSOR DECLARATION                                     
      ******************************************************************        
           EXEC SQL                                                             
             DECLARE XMT_LOC_CSR CURSOR FOR                                     
                SELECT X.LOC_NBR,                                               
                       X.LOC_5_ABBR_NM,                                         
                       X.CLO_DTE,                                               
                       DC_ASGMNT.DC_NBR,                                        
                       DC_ASGMNT.PO_STRT_DTE                                    
                FROM XMT_LOC X,                                                 
                (SELECT A.LOC_NBR, A.DC_NBR, TEMP.PO_STRT_DTE                   
                    FROM XMT_DC_LOC_ASGMT A,                                    
                    (SELECT B.LOC_NBR, MAX(B.PO_STRT_DTE) AS PO_STRT_DTE        
                    FROM XMT_DC_LOC_ASGMT B                                     
                    GROUP BY B.LOC_NBR) TEMP                                    
                    WHERE A.LOC_NBR = TEMP.LOC_NBR                              
                    AND A.PO_STRT_DTE = TEMP.PO_STRT_DTE) DC_ASGMNT             
                WHERE X.LOC_NBR = DC_ASGMNT.LOC_NBR                             
                AND X.LOC_TYP_CDE <> 'OP'                                       
                ORDER BY LOC_NBR                                                
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS-EXTRACT THRU 2000-EXIT                          
               UNTIL PS-END-OF-DEPTST-FILE.                                     
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  IN-DEPT-STR-RANK                                         
                OUTPUT OUT-DEPT-STR-RANK-DC.                                    
                                                                                
W33304     INITIALIZE DCLTINVPAR.                                               
W33304                                                                          
           SET PS-NOT-END-OF-DEPTST-FILE TO TRUE.                               
                                                                                
           ACCEPT PV-CURRENT-DATE FROM DATE YYYYMMDD.                           
           MOVE PV-CD-CCYY TO PV-CD-DB2-CCYY.                                   
           MOVE PV-CD-MM TO PV-CD-DB2-MM.                                       
           MOVE PV-CD-DD TO PV-CD-DB2-DD.                                       
                                                                                
           DISPLAY 'CURRENT DATE: ' PV-CURRENT-DATE-DB2                         
           INITIALIZE WS-STORE-TABLE.                                           
           PERFORM 6300-PROCESS-XMT-LOC-CSR.                                    
                                                                                
W33304     PERFORM 7100-READ-DEPTST-RANK-FILE                                   
W33304       UNTIL PS-VALID-RECORD                                              
W33304          OR PS-END-OF-DEPTST-FILE.                                       
W33304                                                                          
           IF PS-END-OF-DEPTST-FILE                                             
               DISPLAY '* * * * * * * * * * * * * * * * * * * *'                
               DISPLAY 'INPUT DEPT STORE RANKING FILE IS EMPTY'                 
               DISPLAY '* * * * * * * * * * * * * * * * * * * *'.               
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
                                                                                
       2000-PROCESS-EXTRACT.                                                    
                                                                                
           MOVE IN003I-STORE-NUMB                                       INRD003I
                               TO IN003O-STORE-NUMB.                            
           MOVE IN003I-DIVISION-NUMB                                       LV001
                               TO IN003O-DIVISION-NUMB.                         
           MOVE IN003I-DEPARTMENT-NUMB                                  INRD003I
                               TO IN003O-DEPARTMENT-NUMB.                       
           MOVE IN003I-CLASS-NUMB                                       INRD003I
                               TO IN003O-CLASS-NUMB.                            
           MOVE IN003I-BUYER-NUMB                                       INRD003I
                               TO IN003O-BUYER-NUMB.                            
           MOVE IN003I-STORE-NAME                                       INRD003I
                               TO IN003O-STORE-NAME.                            
           MOVE IN003I-DEPARTMENT-NAME                                  INRD003I
                               TO IN003O-DEPARTMENT-NAME.                       
           MOVE IN003I-FINANCE-CORP-NMBR                                INRD003I
                               TO IN003O-FINANCE-CORP-NMBR.                     
           MOVE IN003I-BOOK-INV                                         INRD003I
                               TO IN003O-BOOK-INV.                              
           MOVE IN003I-SHRINK-ALLOWANCE                                 INRD003I
                               TO IN003O-SHRINK-ALLOWANCE.                      
           MOVE IN003I-TOTAL-RETAIL                                     INRD003I
                               TO IN003O-TOTAL-RETAIL.                          
           MOVE IN003I-TOTAL-COST                                       INRD003I
                               TO IN003O-TOTAL-COST.                            
           MOVE IN003I-PHY-INV-RETAIL                                   INRD003I
                               TO IN003O-PHY-INV-RETAIL.                        
           MOVE IN003I-SHORT-RETAIL                                     INRD003I
                               TO IN003O-SHORT-RETAIL.                          
           MOVE IN003I-SHORT-COST                                       INRD003I
                               TO IN003O-SHORT-COST.                            
           MOVE IN003I-TOTAL-SALES                                      INRD003I
                               TO IN003O-TOTAL-SALES.                           
W33304     MOVE IN003I-OPN-FSCL-MN-DTE                                  INRD003I
W33304                         TO IN003O-OPN-FSCL-MN-DTE.                       
W33304     MOVE IN003I-INV-ID                                           INRD003I
W33304                         TO IN003O-INV-ID.                                
W33304     MOVE IN003I-FINCL-INV-STAT-CDE                               INRD003I
W33304                         TO IN003O-FINCL-INV-STAT-CDE.                    
                                                                                
           IF IN003I-STORE-NUMB NUMERIC                                         
               MOVE IN003I-STORE-NUMB TO STORE-SUB                              
               IF TBL-STORE-NMBR(STORE-SUB) = SPACES                            
                   MOVE 'NA' TO IN003O-PRIME-DC                                 
               ELSE                                                             
               IF TBL-DC-NMBR(STORE-SUB) NUMERIC                                
                   MOVE TBL-DC-NMBR(STORE-SUB) TO PRIME-DC-SUB                  
                   IF TBL-NAME-DASH(PRIME-DC-SUB) EQUAL '-'                     
                       MOVE TBL-CHAR2-NAME(PRIME-DC-SUB)                        
                                   TO IN003O-PRIME-DC                           
                   ELSE                                                         
                       MOVE TBL-CHAR2-NAME-OTH(PRIME-DC-SUB)                    
                                   TO IN003O-PRIME-DC                           
                   END-IF                                                       
               ELSE                                                             
                   MOVE 'NA' TO IN003O-PRIME-DC                                 
               END-IF                                                           
           ELSE                                                                 
               MOVE 'NA' TO IN003O-PRIME-DC                                     
           END-IF.                                                              
                                                                                
           WRITE OUT-DEPT-STR-RANK-DC-RECORD                                    
                       FROM IN003O-STORE-SHORTAGE-REC.                          
                                                                                
W33304     MOVE 'N' TO PS-VALID-RECORD-IND.                                     
W33304     PERFORM 7100-READ-DEPTST-RANK-FILE                                   
W33304       UNTIL PS-VALID-RECORD                                              
W33304          OR PS-END-OF-DEPTST-FILE.                                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
W33304******************************************************************        
W33304*    GET LOCATION'S FINANCIAL BOOK DATE                                   
W33304*                                                                         
W33304*    NOTE:  THE INK90105 REPORT IS CREATED AFTER THE CYCLE                
W33304*           DEACTIVATION PROCESS HAS BEEN RUN, SO THIS QUERY              
W33304*           SHOULD NOT CHECK FOR AN ACTIVE CYCLE.                         
W33304******************************************************************        
W33304 5000-SELECT-TINVPAR.                                                     
W33304                                                                          
W33304     MOVE IN003I-INV-ID     TO INVPAR-INV-ID.                             
W33304     MOVE IN003I-STORE-NUMB TO INVPAR-LOC-NBR.                            
W33304                                                                          
W33304     PERFORM  WITH TEST AFTER                                             
W33304       VARYING PV-RETRY-COUNT FROM 1 BY 1                                 
W33304         UNTIL SQLCODE = +0                                               
W33304            OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                    
W33304                                                                          
W33304         EXEC SQL                                                         
W33304             SELECT ACTL_FIN_BK_DTE                                       
W33304               INTO :INVPAR-ACTL-FIN-BK-DTE                               
W33304               FROM TINVPAR                                               
W33304              WHERE INV_ID  = :INVPAR-INV-ID                              
W33304                AND LOC_NBR = :INVPAR-LOC-NBR                             
W33304                AND LOC_INV_STAT_CDE = 'IN'                               
W33304         END-EXEC                                                         
W33304                                                                          
W33304         EVALUATE TRUE                                                    
W33304             WHEN SQLCODE = +0                                            
W33304                 ADD +1 TO PV-NBR-OF-TINVPAR-SELECTS                      
W33304                 IF  INVPAR-ACTL-FIN-BK-DTE NOT = PC-DEFAULT-DTE          
W33304                     SET  PS-LOC-FINCL-BOOKED TO TRUE                     
W33304                 END-IF                                                   
W33304             WHEN SQLCODE = -911                                          
W33304             WHEN SQLCODE = -913                                          
W33304                  CONTINUE                                                
W33304             WHEN OTHER                                                   
W33304                DISPLAY '**********************************'              
W33304                DISPLAY 'INV_ID  = ' INVPAR-INV-ID                        
W33304                DISPLAY 'LOC_NBR = ' INVPAR-LOC-NBR                       
W33304                DISPLAY '**********************************'              
W33304                  MOVE '5000-SELECT-TINVPAR'                              
W33304                               TO PV-PARAGRAPH-NAME                       
W33304                  MOVE 'SELECT FROM TINVPAR'                              
W33304                               TO PV-DB2-OPERATION-ATTEMPTED              
W33304                  PERFORM 9999-SQL-ABEND                                  
W33304         END-EVALUATE                                                     
W33304     END-PERFORM.                                                         
W33304                                                                          
W33304     IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
W33304         MOVE '5000-SELECT-TINVPAR'                                       
W33304                               TO PV-PARAGRAPH-NAME                       
W33304         MOVE 'MAX RETRIES EXCEEDED'                                      
W33304                               TO PV-DB2-OPERATION-ATTEMPTED              
W33304         PERFORM 9999-SQL-ABEND                                           
W33304     END-IF.                                                              
W33304                                                                          
       6300-PROCESS-XMT-LOC-CSR.                                                
           EXEC SQL                                                             
               OPEN XMT_LOC_CSR                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'OPEN XMT_LOC CURSOR' TO                                
                       PV-DB2-OPERATION-ATTEMPTED                               
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
           PERFORM 6400-FETCH-XMT-LOC-TABLE.                                    
                                                                                
           PERFORM 6500-LOAD-WS-STORE-TABLE                                     
                  UNTIL PS-END-OF-XMTLOC-CURSOR.                                
                                                                                
           EXEC SQL                                                             
               CLOSE XMT_LOC_CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'CLOSE XMTLOC  CURSOR' TO                               
                       PV-DB2-OPERATION-ATTEMPTED                               
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  FETCH THE DB2 XMT_LOC TABLE INTO THE DCLGEN                            
      ******************************************************************        
                                                                                
       6400-FETCH-XMT-LOC-TABLE.                                                
                                                                                
           EXEC SQL                                                             
               FETCH XMT_LOC_CSR                                                
               INTO :XMT00001-LOC-NBR,                                          
                    :XMT00001-LOC-5-ABBR-NM,                                    
                    :XMT00001-CLO-DTE,                                          
                    :XMT00002-DC-NBR,                                           
                    :XMT00002-PO-STRT-DTE                                       
           END-EXEC.                                                            
                                                                                
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   SET PS-END-OF-XMTLOC-CURSOR TO TRUE                          
               WHEN OTHER                                                       
                   MOVE '6400-FETCH-XMT-LOC-TABLE'                              
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'FETCH XMT_LOC RECORD' TO                               
                       PV-DB2-OPERATION-ATTEMPTED                               
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  LOAD THE STORE DATA INTO THE WORKING STORAGE TABLE                     
      *  WHEN DETERMINE WHICH DC TO USES (DC1 OR DC2), MUST LOCK                
      *  AT THE PO-START-DATE.                                                  
      ******************************************************************        
                                                                                
       6500-LOAD-WS-STORE-TABLE.                                                
           MOVE XMT00001-LOC-NBR          TO PC-SUB.                            
                                                                                
           MOVE XMT00001-LOC-NBR         TO TBL-STORE-NMBR(PC-SUB).             
           MOVE XMT00001-LOC-5-ABBR-NM   TO TBL-CHAR5-NAME(PC-SUB).             
                                                                                
           IF XMT00002-DC-NBR > ZERO                                            
               IF PV-CURRENT-DATE-DB2 > XMT00002-PO-STRT-DTE                    
                 MOVE XMT00002-DC-NBR    TO TBL-DC-NMBR(PC-SUB)                 
               ELSE                                                             
                 DISPLAY '************************************'                 
                 DISPLAY 'FOR STORE ' XMT00001-LOC-NBR                          
                 DISPLAY 'STRT DTE ' XMT00002-PO-STRT-DTE                       
                 DISPLAY 'CLOSED DATE ' XMT00001-CLO-DTE                        
                 DISPLAY '************************************'                 
                 MOVE SPACE              TO TBL-DC-NMBR(PC-SUB)                 
               END-IF                                                           
           ELSE                                                                 
               MOVE XMT00002-DC-NBR      TO TBL-DC-NMBR(PC-SUB)                 
           END-IF.                                                              
           PERFORM 6400-FETCH-XMT-LOC-TABLE.                                    
                                                                                
      ******************************************************************        
      *  READ A STORE EXTRACT RECORD.                                           
W33304*                                                                         
W33304*    NOTE:  THIS PARAGRAPH IS PERFORMED UNTIL A VALID RECORD IS           
W33304*           READ.  A VALID RECORD IS ONE FOR A LOCATION THAT HAS          
W33304*           FINANCIALLY BOOKED, AS ONLY THE LOCATIONS THAT HAVE           
W33304*           FINANCIALLY BOOKED SHOULD APPEAR ON THE STORE VERSION         
W33304*           OF THE INK90105 RPT.                                          
      ******************************************************************        
                                                                                
       7100-READ-DEPTST-RANK-FILE.                                              
           READ IN-DEPT-STR-RANK                                                
               INTO IN003I-STORE-SHORTAGE-REC                                   
               AT END                                                           
                   SET PS-END-OF-DEPTST-FILE TO TRUE                            
W33304         NOT AT END                                                  CL**3
W33304             IF  IN003I-STORE-NUMB = PV-PREV-LOC-NBR                      
W33304                 IF  PS-LOC-FINCL-BOOKED                                  
W33304                     SET PS-VALID-RECORD TO TRUE                          
W33304                 ELSE                                                     
W33304                     CONTINUE                                             
W33304             ELSE                                                         
W33304                 MOVE IN003I-STORE-NUMB TO PV-PREV-LOC-NBR                
W33304                 MOVE 'N' TO PS-LOC-FINCL-BOOKED-IND                      
W33304                 PERFORM 5000-SELECT-TINVPAR                              
W33304                 IF  PS-LOC-FINCL-BOOKED                                  
W33304                     SET PS-VALID-RECORD TO TRUE                          
W33304                 END-IF                                                   
W33304             END-IF                                                       
           END-READ.                                                            
                                                                                
      ******************************************************************        
W33304*  DISPLAY TOTAL COUNTS AND CLOSE ALL FILES                               
      ******************************************************************        
                                                                                
       9000-EOJ-ROUTINE.                                                        
                                                                                
W33304     DISPLAY SPACES.                                                      
W33304     DISPLAY '*********************************************'.             
W33304     DISPLAY '***** TOTAL COUNTS FOR PROGRAM INKRP006 *****'.             
W33304     DISPLAY '*********************************************'.             
W33304                                                                          
W33304     MOVE PV-NBR-OF-TINVPAR-SELECTS  TO PV-DISPLAY-COUNT.                 
W33304     DISPLAY 'TOT NBR OF SELECTS ON TINVPAR = ' PV-DISPLAY-COUNT.         
W33304     DISPLAY SPACES.                                                      
W33304                                                                          
           CLOSE IN-DEPT-STR-RANK                                               
                 OUT-DEPT-STR-RANK-DC.                                          
                                                                                
      ******************************************************************        
      * 9999-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE              
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                         
      ******************************************************************        
      *                                                                         
       9999-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = INKUT149'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
