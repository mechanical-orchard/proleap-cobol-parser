       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              MRKUT152.                                       
       AUTHOR.                  JACK KRAMER.                                    
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            02/08/11.                                       
       DATE-COMPILED.                                                           
      *----------------------------------------------------------------*        
      *       CONVERT WAREHOUSE EXTRACT E-LOCATIONS TO ECOMM CHANNEL   *        
      *  THIS PROGRAM READS A SORTED WAREHOUSE EXTRACT FILE AND        *        
      *  SEARCHES ALL RECORDS FOR ECOMM 'DS' STORE NUMBERS, CONVERTING *        
      *  ALL ECOMM STORE NUMBERS TO THE ECOMM CHANNEL LEVEL (873)      *        
      *----------------------------------------------------------------*        
      *                        MODFICATION LOG                         *        
      *----------------------------------------------------------------*        
      *  02/08/2011 - WR#38972 (M.L.O.F.) NEW PROGRAM.                 *        
      *----------------------------------------------------------------*        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT WH-EXT-FILE-IN                                                
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT EFCLOC-INPUT-FILE                                             
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT WH-EXT-FILE-OUT                                               
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  WH-EXT-FILE-IN                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 43 CHARACTERS.                                       
       01  WH-EXT-REC-IN               PIC  X(43).                              
                                                                                
       FD  EFCLOC-INPUT-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 005 CHARACTERS.                                      
       01  EFC-LOC-REC.                                                         
           05  EFCLOC                  PIC  9(05).                              
                                                                                
       FD  WH-EXT-FILE-OUT                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 43 CHARACTERS.                                       
       01  WH-EXT-REC-OUT              PIC  X(43).                              
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME         PIC X(8)  VALUE 'MRKUT152'.              
           05  PC-ECOMM-CHANNEL        PIC 9(05) VALUE 873.                     
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-MIN-EFC-LOC-NBR      PIC 9(05).                               
           05  PV-MAX-EFC-LOC-NBR      PIC 9(05).                               
           05  PV-PREV-STORE-NBR       PIC 9(05)       VALUE ZERO.              
           05  PV-WH-EXT-IN-CNT        PIC 9(09)       VALUE ZERO               
                                                       COMP-3.                  
           05  PV-EFCLOC-READ-CNT      PIC 9(09)       VALUE ZERO               
                                                       COMP-3.                  
           05  PV-WH-EXT-OUT-CNT       PIC 9(09)       VALUE ZERO               
                                                       COMP-3.                  
           05  PV-CNVRTED-CNT          PIC 9(09)       VALUE ZERO               
                                                       COMP-3.                  
           05  PV-EFCLOC-SUB           PIC 9(03)       VALUE ZERO.              
           05  PV-EFCLOC-MAX           PIC 9(02)       VALUE 99.                
      *                                                                         
       01  PS-SWITCHES.                                                         
           05  PS-WH-EXT-IN-EOF-SW     PIC X(01)       VALUE 'N'.               
                88  PS-WH-EXT-EOF-NO                   VALUE 'N'.               
                88  PS-WH-EXT-EOF-YES                  VALUE 'Y'.               
           05  PS-FIRST-EFC-LOC-SW     PIC X(01)       VALUE 'Y'.               
               88  PS-FIRST-EFC-LOC-NO                 VALUE 'N'.               
               88  PS-FIRST-EFC-LOC-YES                VALUE 'Y'.               
           05  PS-EFC-LOC-EOF-SW       PIC X(01)       VALUE 'N'.               
               88  PS-EFC-LOC-EOF-NO                   VALUE 'N'.               
               88  PS-EFC-LOC-EOF-YES                  VALUE 'Y'.               
           05  PS-E-BUS-IND-SW         PIC X(01)       VALUE 'N'.               
               88  PS-E-BUS-IND-NO                     VALUE 'N'.               
               88  PS-E-BUS-IND-YES                    VALUE 'Y'.               
      *                                                                         
       01  TABLES.                                                              
           05  EFC-LOC-ARRAY OCCURS 99 TIMES.                                   
               10  EFC-LOC-NBR         PIC 9(05).                               
      *                                                                         
       01  ABEND-CODE                  PIC S9(4) COMP SYNC VALUE ZERO.          
           88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
           88  ABEND-4002-READ-ERROR                       VALUE +4002.         
           88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
           88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
           88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
           88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
           88  ABEND-4008-DATA-ERROR                       VALUE +4008.         
           88  ABEND-4009-KEY-DATA-ERROR                   VALUE +4009.         
           88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
      *                                                                         
      *----------------------------------------------------------------*        
      *  COPY MEMBER FOR THE WAREHOUSE EXTRACT INPUT FILE.             *        
      *----------------------------------------------------------------*        
           COPY MRRD051.                                                        
      *                                                                         
      *----------------------------------------------------------------*        
      *  COPY MEMBER FOR THE WAREHOUSE EXTRACT OUTPUT FILE.            *        
      *----------------------------------------------------------------*        
           COPY MRRD051 REPLACING MR051 BY WHOUT.                               
      *                                                                         
           EJECT                                                                
       LINKAGE SECTION.                                                         
                                                                                
           EJECT                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
      *----------------------------------------------------------------*        
      *  MAIN PROCESSING.                                              *        
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 1000-HOUSEKEEPING.                                           
                                                                                
           PERFORM 2000-PROCESS.                                                
                                                                                
           PERFORM 8000-CLOSE.                                                  
                                                                                
           STOP RUN.                                                            
      *----------------------------------------------------------------*        
      *  OPEN FILES & INITIALIZE.                                      *        
      *----------------------------------------------------------------*        
       1000-HOUSEKEEPING.                                                       
                                                                                
           DISPLAY '*-------------------*'.                                     
           DISPLAY '* START OF MRKUT152 *'.                                     
           DISPLAY '*-------------------*'.                                     
           DISPLAY ' '                                                          
                                                                                
           OPEN INPUT  WH-EXT-FILE-IN                                           
                       EFCLOC-INPUT-FILE.                                       
           OPEN OUTPUT WH-EXT-FILE-OUT.                                         
                                                                                
           PERFORM 6300-READ-EFCLOC-FILE                                        
           PERFORM 1100-LOAD-EFC                                                
             UNTIL PS-EFC-LOC-EOF-YES                                           
                                                                                
           MOVE  PV-EFCLOC-SUB    TO PV-EFCLOC-MAX                              
                                                                                
           PERFORM 6100-READ-WH-EXT-FILE.                                       
      *----------------------------------------------------------------*        
      *  LOAD THE EFC STORE LOCATION NUMBER IN AN INTERNAL ARRAY.      *        
      *----------------------------------------------------------------*        
       1100-LOAD-EFC.                                                           
                                                                                
           ADD  +1            TO PV-EFCLOC-SUB                                  
           IF PV-EFCLOC-SUB > PV-EFCLOC-MAX                                     
               DISPLAY '*********** ABEND ************************'             
               DISPLAY ' TABLE OVERFLOW ON EFC-TABLE              '             
               DISPLAY ' INCREASE PV-EFCLOC-MAX LIMIT             '             
               DISPLAY ' CURRENT LIMIT = ' PV-EFCLOC-MAX                        
               DISPLAY '******************************************'             
               DISPLAY '  '                                                     
               MOVE +4004 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
           MOVE EFCLOC                 TO EFC-LOC-NBR (PV-EFCLOC-SUB)           
                                                                                
           IF PS-FIRST-EFC-LOC-YES                                              
               MOVE EFCLOC             TO PV-MIN-EFC-LOC-NBR                    
               SET PS-FIRST-EFC-LOC-NO TO TRUE                                  
               DISPLAY 'MIN EFC = ' PV-MIN-EFC-LOC-NBR                          
           END-IF.                                                              
                                                                                
           PERFORM 6300-READ-EFCLOC-FILE.                                       
      *----------------------------------------------------------------*        
      *  PROCESS THE INPUT FILE.                                                
      *----------------------------------------------------------------*        
       2000-PROCESS.                                                            
                                                                                
           PERFORM 2100-PROCESS-WH-EXT-FILE                                     
             UNTIL PS-WH-EXT-EOF-YES.                                           
                                                                                
      *----------------------------------------------------------------*        
      *  PROCESS THE INVENTORY BALANCE FILE.                                    
      *----------------------------------------------------------------*        
       2100-PROCESS-WH-EXT-FILE.                                                
                                                                                
           PERFORM 2300-MOVE-DATA.                                              
                                                                                
           PERFORM 2500-CONVERT-EFC.                                            
                                                                                
           MOVE WHOUT-EXTRACT-RECORD TO WH-EXT-REC-OUT                          
           PERFORM 6500-WRITE-WH-EXT-FILE.                                      
                                                                                
           PERFORM 6100-READ-WH-EXT-FILE.                                       
      *----------------------------------------------------------------*        
      *  MOVE DATA FROM THE INPUT FILE TO THE OUTPUT FILE                       
      *----------------------------------------------------------------*        
       2300-MOVE-DATA.                                                          
                                                                                
           INITIALIZE WHOUT-EXTRACT-RECORD.                                     
           MOVE MR051-EXTRACT-RECORD TO WHOUT-EXTRACT-RECORD.                   
      *----------------------------------------------------------------*        
      *  CHECK IF THE LOCATION IS THE SAME AS THE PREVIOUS STORE.               
      *  IF THE STORE IS A EFC LOCATION THEN REPLACE THE SAME BY                
      *  PC-CHANNEL-LEVEL ELSE WRITE IT AS IT IS.                               
      *----------------------------------------------------------------*        
       2500-CONVERT-EFC.                                                        
                                                                                
           IF MR051-STORE-NBR = PV-PREV-STORE-NBR                               
              IF PS-E-BUS-IND-YES                                               
                 MOVE PC-ECOMM-CHANNEL TO WHOUT-STORE-NBR                       
                 ADD +1 TO PV-CNVRTED-CNT                                       
              END-IF                                                            
           ELSE                                                                 
              PERFORM 2550-CHECK-FOR-EFC                                        
              MOVE MR051-STORE-NBR TO PV-PREV-STORE-NBR                         
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *  SEARCH WHETHER THE GIVEN LOCATION IS A EFC LOCATION OR NOT.            
      *----------------------------------------------------------------*        
       2550-CHECK-FOR-EFC.                                                      
                                                                                
           SET PS-E-BUS-IND-NO     TO TRUE.                                     
           IF    WHOUT-STORE-NBR >= PV-MIN-EFC-LOC-NBR                          
             AND WHOUT-STORE-NBR <= PV-MAX-EFC-LOC-NBR                          
              PERFORM 2555-LOOP-EFC-LOC VARYING PV-EFCLOC-SUB FROM              
                   1 BY 1 UNTIL PV-EFCLOC-SUB > PV-EFCLOC-MAX                   
                             OR PS-E-BUS-IND-YES                                
           END-IF.                                                              
           IF PS-E-BUS-IND-YES                                                  
              MOVE PC-ECOMM-CHANNEL  TO WHOUT-STORE-NBR                         
              ADD +1 TO PV-CNVRTED-CNT                                          
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *  SET THE E-BUS-IND FLAG IF THE LOCATION NUMBER MATCHES WITH             
      *  THE ENTRY IN THE ARRAY.                                                
      *----------------------------------------------------------------*        
       2555-LOOP-EFC-LOC.                                                       
                                                                                
           IF EFC-LOC-NBR (PV-EFCLOC-SUB) = WHOUT-STORE-NBR                     
              SET  PS-E-BUS-IND-YES   TO TRUE                                   
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *  READ THE NEXT RECORD OF THE INV BALANCE FILE.                          
      *----------------------------------------------------------------*        
       6100-READ-WH-EXT-FILE.                                                   
                                                                                
           READ WH-EXT-FILE-IN INTO MR051-EXTRACT-RECORD                        
               AT END                                                           
                   SET PS-WH-EXT-EOF-YES TO TRUE                                
               NOT AT END                                                       
                   ADD +1 TO PV-WH-EXT-IN-CNT.                                  
      *----------------------------------------------------------------*        
      *  READ THE NEXT RECORD ON THE EFCLOC-INPUT-FILE.                         
      *----------------------------------------------------------------*        
       6300-READ-EFCLOC-FILE.                                                   
                                                                                
           READ EFCLOC-INPUT-FILE                                               
               AT END                                                           
                   SET PS-EFC-LOC-EOF-YES TO TRUE                               
                   MOVE EFCLOC   TO PV-MAX-EFC-LOC-NBR                          
                   DISPLAY 'MAX EFC = ' PV-MAX-EFC-LOC-NBR                      
                   DISPLAY 'EFC COUNT =  ' PV-EFCLOC-READ-CNT                   
               NOT AT END                                                       
                   ADD +1 TO PV-EFCLOC-READ-CNT.                                
      *----------------------------------------------------------------*        
      *  WRITE WH EXT FILE.                                                     
      *----------------------------------------------------------------*        
       6500-WRITE-WH-EXT-FILE.                                                  
                                                                                
           WRITE WH-EXT-REC-OUT.                                                
           ADD +1 TO PV-WH-EXT-OUT-CNT.                                         
      *----------------------------------------------------------------*        
      *  CLOSE THE FILES.                                              *        
      *----------------------------------------------------------------*        
       8000-CLOSE.                                                              
                                                                                
           CLOSE WH-EXT-FILE-IN                                                 
                 EFCLOC-INPUT-FILE                                              
                 WH-EXT-FILE-OUT.                                               
                                                                                
           DISPLAY '*-----------------------------------*'.                     
           DISPLAY '*    MRKUT152 PROGRAM STATISTICS    *'.                     
           DISPLAY '*-----------------------------------*'.                     
           DISPLAY '* WH EXT RECORDS READ    : ' PV-WH-EXT-IN-CNT.              
           DISPLAY '* TOTAL EFC-LOCS READ    : ' PV-EFCLOC-READ-CNT.            
           DISPLAY '* WH EXT RECORDS WRITTEN : ' PV-WH-EXT-OUT-CNT.             
           DISPLAY '* E_LOCATIONS CONVERTED  : ' PV-CNVRTED-CNT.                
           DISPLAY '*-----------------------------------*'.                     
           DISPLAY ' '                                                          
           DISPLAY '*-----------------*'.                                       
           DISPLAY '* END OF MRKUT152 *'.                                       
           DISPLAY '*-----------------*'.                                       
