000010 IDENTIFICATION DIVISION.                                                 
000020                                                                          
000030 PROGRAM-ID.    MLKRP015.                                                 
000040 AUTHOR.        TESS BESTE.                                               
000050 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000060 DATE-WRITTEN.  04-18-94.                                                 
000070 DATE-COMPILED.                                                           
000080                                                                          
000090******************************************************************        
000100*           MLKRP015 - ACCUM PURCHASES                                    
000110*                                                                         
RONNA * COPIED MLTRP015 AND MADE CHANGES TO SHOW TOTALS BY DEPT                 
RONNA * NOTE THAT THE INPUT FILE MUST BE IN ORDER BY DEPARTMENT.                
000110*                                                                         
000120* THIS PROGRAM ACCUMULATES THE AMOUNTS FROM A FILE OF PURCHASE            
000130* TRANSACTIONS IN THE MLRD015 FORMAT, WHICH IS INPUT TO MLKUP020          
000140* IN JOB MLK140, ON DD'S INP01, INP02, INP03, AND INP04. THE              
000150* TRANSACTION COUNT, TOTAL COST, AND TOTAL RETAIL ARE DISPLAYED.          
000160******************************************************************        
000170                                                                 *        
000180                                                                          
000190 ENVIRONMENT DIVISION.                                                    
000200                                                                          
000210 CONFIGURATION SECTION.                                                   
000220                                                                          
000230 SOURCE-COMPUTER.    IBM-3090.                                            
000240 OBJECT-COMPUTER.    IBM-3090.                                            
000250                                                                          
000260                                                                          
000270 INPUT-OUTPUT SECTION.                                                    
000280                                                                          
000290 FILE-CONTROL.                                                            
000300     SELECT INPUT-FILE                                                    
000310         ASSIGN TO INP01.                                                 
000320                                                                          
000350 DATA DIVISION.                                                           
000360                                                                          
000370 FILE SECTION.                                                            
000380                                                                          
000390 FD  INPUT-FILE                                                           
000400     LABEL RECORDS ARE STANDARD                                           
000410     BLOCK CONTAINS 0 RECORDS                                             
000420     RECORDING  MODE IS F.                                                
W26603*01  INPUT-RECORD                PIC X(80).                               
W26603     COPY MLRD015.                                                        
000440                                                                          
000450 WORKING-STORAGE SECTION.                                                 
000460                                                                          
000470******************************************************************        
000480* INPUT RECORD LAYOUT                                                     
000490******************************************************************        
000500                                                                          
000530 01  PS-PROGRAM-SWITCHES.                                                 
000540     05  PS-END-OF-INPUT-FILE-SW      PIC X         VALUE 'N'.            
000550         88  PS-END-OF-INPUT-FILE                   VALUE 'Y'.            
000560         88  PS-NOT-END-OF-INPUT-FILE               VALUE 'N'.            
000570                                                                          
000580 01  PV-PROGRAM-VARIABLES.                                                
000590     05  PV-RECORD-COUNT              PIC S9(13)    VALUE +0              
000600                                                    COMP-3.               
000610     05  PV-ACCUM-CR-COST-AMT         PIC S9(13)V99 VALUE +0              
000620                                                    COMP-3.               
000630     05  PV-ACCUM-CR-RTL-AMT          PIC S9(13)V99 VALUE +0              
000640                                                    COMP-3.               
000610     05  PV-ACCUM-DR-COST-AMT         PIC S9(13)V99 VALUE +0              
000620                                                    COMP-3.               
000630     05  PV-ACCUM-DR-RTL-AMT          PIC S9(13)V99 VALUE +0              
000640                                                    COMP-3.               
000610     05  PV-ACCUM-COST-AMT            PIC S9(13)V99 VALUE +0              
000620                                                    COMP-3.               
000630     05  PV-ACCUM-RTL-AMT             PIC S9(13)V99 VALUE +0              
000640                                                    COMP-3.               
000650     05  PV-DISPLAY-COUNT             PIC ZZZ,ZZZ,ZZZ,ZZZ-.               
000660     05  PV-DISPLAY-AMT               PIC ZZZ,ZZZ,ZZZ,ZZZ.99-.            
000670 EJECT                                                                    
000680 PROCEDURE DIVISION.                                                      
000690                                                                          
000700                                                                          
000710 A100-PROGRAM-MAINLINE.                                                   
000720                                                                          
000730     OPEN INPUT INPUT-FILE.                                               
W26603     READ INPUT-FILE                                                      
000750         AT END SET PS-END-OF-INPUT-FILE TO TRUE.                         
000760                                                                          
000770     PERFORM UNTIL PS-END-OF-INPUT-FILE                                   
000780         ADD +1 TO PV-RECORD-COUNT                                        
000790         IF ML015-PURCH-TRANS-CDE = '01' OR '07'                          
000800             COMPUTE PV-ACCUM-CR-COST-AMT                                 
000810                   = PV-ACCUM-CR-COST-AMT                                 
000820                   + ML015-PURCH-NET                                      
000830             COMPUTE PV-ACCUM-CR-RTL-AMT                                  
000840                   = PV-ACCUM-CR-RTL-AMT                                  
000850                   + ML015-PURCH-RETAIL                                   
000860         ELSE                                                             
000870             COMPUTE PV-ACCUM-DR-COST-AMT                                 
000880                   = PV-ACCUM-DR-COST-AMT                                 
000890                   - ML015-PURCH-NET                                      
000900             COMPUTE PV-ACCUM-DR-RTL-AMT                                  
000910                   = PV-ACCUM-DR-RTL-AMT                                  
000920                   - ML015-PURCH-RETAIL                                   
000930         END-IF                                                           
W26603         READ INPUT-FILE                                                  
000950             AT END SET PS-END-OF-INPUT-FILE TO TRUE                      
000960         END-READ                                                         
000970     END-PERFORM.                                                         
000980                                                                          
000990     MOVE PV-RECORD-COUNT TO PV-DISPLAY-COUNT.                            
001000     DISPLAY 'TRANSACTION COUNT: ' PV-DISPLAY-COUNT.                      
                                                                                
001010     DISPLAY '*** CREDITS ***'.                                           
001020     MOVE PV-ACCUM-CR-COST-AMT TO PV-DISPLAY-AMT.                         
001030     DISPLAY '    COST:        ' PV-DISPLAY-AMT.                          
001040     MOVE PV-ACCUM-CR-RTL-AMT TO PV-DISPLAY-AMT.                          
001050     DISPLAY '    RETAIL:      ' PV-DISPLAY-AMT.                          
                                                                                
           DISPLAY ' '.                                                         
001010     DISPLAY '*** DEBITS ***'.                                            
001020     MOVE PV-ACCUM-DR-COST-AMT TO PV-DISPLAY-AMT.                         
001030     DISPLAY '    COST:        ' PV-DISPLAY-AMT.                          
001040     MOVE PV-ACCUM-DR-RTL-AMT TO PV-DISPLAY-AMT.                          
001050     DISPLAY '    RETAIL:      ' PV-DISPLAY-AMT.                          
                                                                                
           DISPLAY ' '.                                                         
001010     DISPLAY '*** TOTAL  ***'.                                            
           COMPUTE PV-ACCUM-RTL-AMT = PV-ACCUM-CR-RTL-AMT                       
                                    + PV-ACCUM-DR-RTL-AMT.                      
           COMPUTE PV-ACCUM-COST-AMT = PV-ACCUM-CR-COST-AMT                     
                                    + PV-ACCUM-DR-COST-AMT.                     
001020     MOVE PV-ACCUM-COST-AMT   TO PV-DISPLAY-AMT.                          
001030     DISPLAY '    COST:       ' PV-DISPLAY-AMT.                           
001040     MOVE PV-ACCUM-RTL-AMT    TO PV-DISPLAY-AMT.                          
001050     DISPLAY '    RETAIL:     ' PV-DISPLAY-AMT.                           
                                                                                
001060     CLOSE INPUT-FILE.                                                    
001070                                                                          
001080     STOP RUN.                                                            
