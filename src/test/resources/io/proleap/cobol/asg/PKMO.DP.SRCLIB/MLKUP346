       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    MLKUP346.                                         00020000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030000
       DATE-WRITTEN.  10-01-2000.                                       00040000
       DATE-COMPILED.                                                   00050000
                                                                        00060000
      ******************************************************************00070000
      * MLKUP346 - MTHLY MAJOR CLASS CHANNEL MERCHANDISE TABLE UPDATE   00080000
      *            - TMNMCLC                                            00090000
      *                                                                 00100000
      *     THIS PROGRAM WILL MAINTAIN THE MTHLY MAJOR CLASS/CHNL DB2   00110000
      * TABLE WITH THE CURRENT MTH'S DATA.   THE TABLE IS READ USING    00120000
      * THE TABLE KEY FROM EACH INPUT FILE RECORD.  IF THE ROW IS       00130000
      * FOUND, THE ROW IS UPDATED WITH DATA FROM THE INPUT FILE RECORD. 00140000
      * IF THE ROW IS NOT FOUND, A NEW ROW IS INSERTED ON THE DB2       00150000
      * TABLE (TMNMCLC). COMMITS ARE TAKEN EVERY TEN SECONDS, AND       00160000
      * CHECKPOINT RESTART LOGIC IS INCORPORATED.                       00170000
      *                                                                 00180000
      ******************************************************************00190000
      ******************************************************************00200000
      *                                                                 00210000
      *PROJECT HISTORY:                                                 00220000
      *  DATE  11/13/2000                                               00230000
99999 *      CHANGED BY:  ROD JANSSEN          WR#99999 PITS# XXXXXX    00240000
99999 *        DATA INTEGRITY - OUT OF BALANCE (MLK991 - 11/13/2000)    00250000
W26603******************************************************************00260000
W26603* CHG ID   DATE        DESCRIPTION                               *00270000
W26603* ------   ----------  ----------------------------------------- *00280000
W26603* W26603   04-14-2004  STORE EXPANSION.  EXPANDED SOME PROGRAM   *00290000
W26603*                      VARIABLES TO ACCOMODATE INCREASED LENGTH  *00300000
W26603*                      OF DOLLAR AMOUNTS.  ADDED 'WITH UR' TO    *00310000
W26603*                      A DB2 SELECT.                             *00320000
W26603*                                                                *00330000
W26603*                      - RONNA MCDONALD                          *00340000
      *                                                                 00350000
      ******************************************************************00360000
      *                                                                 00370000
                                                                        00380000
                                                                        00390000
       ENVIRONMENT DIVISION.                                            00400000
       CONFIGURATION SECTION.                                           00410000
       SOURCE-COMPUTER.        IBM-3090.                                00420000
       OBJECT-COMPUTER.        IBM-3090.                                00430000
       INPUT-OUTPUT SECTION.                                            00440000
       FILE-CONTROL.                                                    00450000
                                                                        00460000
           SELECT UPDATE-FILE                                           00470000
               ASSIGN TO INP01.                                         00480000
                                                                        00490000
       DATA DIVISION.                                                   00500000
       FILE SECTION.                                                    00510000
                                                                        00520000
       FD  UPDATE-FILE                                                  00530000
           LABEL RECORDS ARE STANDARD                                   00540000
           RECORDING MODE IS F                                          00550000
           BLOCK CONTAINS 0 RECORDS.                                    00560000
W26603 01  UPDATE-RECORD               PIC X(170).                      00570000
                                                                        00580000
      ******************************************************************00590000
       WORKING-STORAGE SECTION.                                         00600000
      ******************************************************************00610000
                                                                        00620000
       01  FILLER                      PIC X(28)   VALUE                00630000
                'BEGINNING OF WORKING STORAGE'.                         00640000
                                                                        00650000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00660000
                                                                        00670000
       01  PC-PROGRAM-CONSTANTS.                                        00680000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK292'.    00690000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP346'.  00700000
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00710000
                                                                        00720000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00730000
           05  PE-MSG-01                       PIC X(50) VALUE          00740000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00750000
           05  PE-MSG-02                       PIC X(50) VALUE          00760000
                'ATTEMPT TO UPDATE ROW ON TABLE TMNMCLC FAILED   '.     00770000
           05  PE-MSG-03                       PIC X(50) VALUE          00780000
                'ATTEMPT TO INSERT ROW ON TABLE TMNMCLC FAILED   '.     00790000
           05  PE-MSG-04                       PIC X(50) VALUE          00800000
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00810000
           05  PE-MSG-05                       PIC X(50) VALUE          00820000
                'UPDATE TO TCKRSTINF FAILED                      '.     00830000
           05  PE-MSG-06                       PIC X(50) VALUE          00840000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00850000
           05  PE-MSG-07                       PIC X(50) VALUE          00860000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00870000
      *                                                                 00880000
       01  PS-PROGRAM-SWITCHES.                                         00890000
           05  PS-EOF-UPDATE-FILE-SW        PIC X(01)     VALUE 'N'.    00900000
               88  PS-EOF-UPDATE-FILE                     VALUE 'Y'.    00910000
           05  PS-MNMCLC-CURSOR-EOF-SW      PIC X(01)     VALUE 'N'.    00920000
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00930000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00940000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00950000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00960000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00970000
                                                                        00980000
       01  PI-PROGRAM-INDICATORS.                                       00990000
           05  PI-PROCESSING-OPTIONS-IND    PIC X(01)     VALUE 'P'.    01000000
               88  PI-UPDATE-MNMCLC-ROW                   VALUE 'U'.    01010000
               88  PI-INSERT-MNMCLC-ROW                   VALUE 'I'.    01020000
      *                                                                 01030000
       01  PROGRAM-VARIABLES.                                           01040000
           05  PV-RETRY-COUNT                  PIC S9(04) VALUE +0.     01050000
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     01060000
      *                                                                 01070000
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 01080000
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 01090000
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 01100000
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 01110000
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 01120000
      *                                                                 01130000
       01  PA-PROGRAM-ACCUMULATORS.                                     01140000
           05  PA-RECORD-COUNTS.                                        01150000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      01160000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      01170000
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      01180000
               10  PA-MAJ-CL-PROCESSED   PIC  9(09)  VALUE ZEROES.      01190000
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      01200000
                                                                        01210000
       01  PD-PROGRAM-DISPLAYS.                                         01220000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01230000
                                                                        01240000
       01  SAVE-AREA.                                                   01250000
           05  SV-PRIMARY-KEY.                                          01260000
               10  SV-FSCL-MN-NBR        PIC X(02)  VALUE SPACES.       01270000
               10  SV-FSCL-MN-END-DTE    PIC X(10)  VALUE SPACES.       01280000
               10  SV-DEPT-NBR           PIC X(03)  VALUE SPACES.       01290000
               10  SV-MAJ-CL-NBR         PIC X(02)  VALUE SPACES.       01300000
               10  SV-CHNL-CDE           PIC X(01)  VALUE SPACE.        01310000
           05  SV-UPDATE-FIELDS.                                        01320000
W26603         10  SV-SLS-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01330000
W26603         10  SV-MKDN-PLU-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01340000
W26603         10  SV-MKDN-RGST-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01350000
W26603         10  SV-MKDN-UMTCH-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01360000
W26603         10  SV-MKDN-BYR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01370000
W26603         10  SV-MKDN-STR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01380000
W26603         10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01390000
W26603         10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01400000
W26603         10  SV-XFER-OUT-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01410000
W26603         10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01420000
W26603         10  SV-RCPT-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01430000
W26603         10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01440000
W26603         10  SV-PADJ-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01450000
W26603         10  SV-PADJ-FRGT-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01460000
W26603         10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01470000
                                                                        01480000
      ******************************************************************01490000
      * MLRD151 - M/L MTHLY MAJOR CLASS CHANNEL CODE UPDATE REC LAYOUT  01500000
      ******************************************************************01510000
           COPY MLRD151.                                                01520000
                                                                        01530000
      ******************************************************************01540000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01550000
      ******************************************************************01560000
           COPY DPWS004.                                                01570000
                                                                        01580000
      ******************************************************************01590000
      *  USED FOR DB2 ERRORS                                            01600000
      ******************************************************************01610000
           EXEC SQL                                                     01620000
               INCLUDE SQLCA                                            01630000
           END-EXEC.                                                    01640000
                                                                        01650000
      ******************************************************************01660000
      *  M/L MTH-TO-DATE MAJOR CLASS TABLE                              01670000
      ******************************************************************01680000
           EXEC SQL                                                     01690000
               INCLUDE TMNMCLC                                          01700000
           END-EXEC.                                                    01710000
                                                                        01720000
      ***************************************************************** 01730000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01740000
      ***************************************************************** 01750000
           EXEC SQL                                                     01760000
               INCLUDE TCKRSTCN                                         01770000
           END-EXEC.                                                    01780000
      *                                                                 01790000
           EXEC SQL                                                     01800000
               INCLUDE TCKRSTIN                                         01810000
           END-EXEC.                                                    01820000
      *                                                                 01830000
      ******************************************************************01840000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01850000
      ******************************************************************01860000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01870000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +28  COMP. 01880000
           05  CR-CHECKPOINT-RESTART-VAR.                               01890000
               10  CR-PREV-DTL-KEY          PIC  X(18) VALUE SPACES.    01900000
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01910000
                   15  CR-FISCAL-NBR        PIC  X(02).                 01920000
                   15  CR-FISCAL-MN-END-DTE PIC  X(10).                 01930000
                   15  CR-DEPT-NBR          PIC  X(03).                 01940000
                   15  CR-MAJ-CL-NBR        PIC  X(02).                 01950000
                   15  CR-CHNL-CDE          PIC  X(01).                 01960000
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01970000
      *                                                                 01980000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01990000
           05  CK-SAVE-PREV-KEY         PIC  X(18) VALUE SPACES.        02000000
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       02010000
               10  CK-FISCAL-NBR        PIC  X(02).                     02020000
               10  CK-FISCAL-MN-END-DTE PIC  X(10).                     02030000
               10  CK-DEPT-NBR          PIC  X(03).                     02040000
               10  CK-MAJ-CL-NBR        PIC  X(02).                     02050000
               10  CK-CHNL-CDE          PIC  X(01).                     02060000
                                                                        02070000
      ******************************************************************02080000
      ******************************************************************02090000
       PROCEDURE DIVISION.                                              02100000
      ******************************************************************02110000
                                                                        02120000
       0000-MLKUP346.                                                   02130000
                                                                        02140000
           PERFORM 1000-INITIALIZATION.                                 02150000
                                                                        02160000
           PERFORM 2000-PROCESS-INPUT                                   02170000
                UNTIL PS-EOF-UPDATE-FILE.                               02180000
                                                                        02190000
           PERFORM 8000-EOJ-ROUTINE.                                    02200000
                                                                        02210000
           STOP RUN.                                                    02220000
                                                                        02230000
      ******************************************************************02240000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02250000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02260000
      ******************************************************************02270000
      *                                                                 02280000
       1000-INITIALIZATION.                                             02290000
      *                                                                 02300000
           OPEN INPUT UPDATE-FILE.                                      02310000
                                                                        02320000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02330000
                                                                        02340000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02350000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02360000
               PERFORM 7500-SELECT-RESTART-INFO                         02370000
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02380000
                                            CR-CHECKPOINT-RESTART-VAR   02390000
               MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY        02400000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                02410000
               DISPLAY 'MTH NBR        ' CR-FISCAL-NBR                  02420000
               DISPLAY 'MTH END DATE   ' CR-FISCAL-MN-END-DTE           02430000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   02440000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 02450000
               DISPLAY 'CHANNEL CODE    ' CR-CHNL-CDE                   02460000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           02470000
           ELSE                                                         02480000
               SET PS-FIRST-COMMIT TO TRUE                              02490000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02500000
           END-IF.                                                      02510000
      *                                                                 02520000
           PERFORM 4000-READ-UPDATE-FILE UNTIL                          02530000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02540000
            OR PS-EOF-UPDATE-FILE.                                      02550000
      *                                                                 02560000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02570000
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                02580000
                        PA-INPUT-COUNT                                  02590000
               DISPLAY 'FISCAL MTH NBR       ' ML151-FSCL-MN-NBR        02600000
               DISPLAY 'FISCAL MTH END DATE  ' ML151-FSCL-MN-END-DTE    02610000
               DISPLAY 'DEPARTMENT NBR        ' ML151-DEPT-NBR          02620000
               DISPLAY 'MAJOR CLASS NBR       ' ML151-MAJ-CL-NBR        02630000
               DISPLAY 'CHANNEL CODE          ' ML151-CHNL-CDE          02640000
           END-IF.                                                      02650000
                                                                        02660000
           IF PS-EOF-UPDATE-FILE                                        02670000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02680000
                   CONTINUE                                             02690000
               ELSE                                                     02700000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02710000
           ELSE                                                         02720000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02730000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02740000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02750000
           END-IF.                                                      02760000
                                                                        02770000
      ******************************************************************02780000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02790000
      ******************************************************************02800000
       2000-PROCESS-INPUT.                                              02810000
      *                                                                 02820000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02830000
      *                                                                 02840000
           PERFORM 7000-SELECT-DB-ROW.                                  02850000
      *                                                                 02860000
           IF PS-PROGRAM-DEADLOCKED                                     02870000
               CONTINUE                                                 02880000
           ELSE                                                         02890000
               EVALUATE TRUE                                            02900000
                   WHEN PI-UPDATE-MNMCLC-ROW                            02910000
                       PERFORM 7100-UPDATE-MNMCLC-ROW                   02920000
                   WHEN PI-INSERT-MNMCLC-ROW                            02930000
                       PERFORM 7200-INSERT-MNMCLC-ROW                   02940000
                   WHEN OTHER                                           02950000
                       MOVE '7000-SELECT-DB-ROW' TO PV-PARAGRAPH-NAME   02960000
                       MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED         02970000
                       PERFORM 9999-SQL-ABEND                           02980000
               END-EVALUATE                                             02990000
           END-IF.                                                      03000000
      *                                                                 03010000
           IF PS-PROGRAM-DEADLOCKED                                     03020000
               CONTINUE                                                 03030000
           ELSE                                                         03040000
               PERFORM 7700-COMMIT-PROCESSING                           03050000
               PERFORM 4000-READ-UPDATE-FILE                            03060000
           END-IF.                                                      03070000
      *                                                                 03080000
      *                                                                 03090000
      ******************************************************************03100000
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         03110000
      ******************************************************************03120000
       2100-CALCULATE-UPDATES.                                          03130000
      * ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES     03140000
      *                                                                 03150000
           COMPUTE SV-SLS-RTL-AMT        = MNMCLC-SLS-RTL-AMT           03160000
                                         + ML151-SLS-RTL-AMT.           03170000
           COMPUTE SV-MKDN-PLU-RTL-AMT   = MNMCLC-MKDN-PLU-RTL-AMT      03180000
                                         + ML151-MKDN-PLU-RTL-AMT.      03190000
           COMPUTE SV-MKDN-RGST-RTL-AMT  = MNMCLC-MKDN-RGST-RTL-AMT     03200000
                                         + ML151-MKDN-RGST-RTL-AMT.     03210000
           COMPUTE SV-MKDN-UMTCH-RTL-AMT = MNMCLC-MKDN-UMTCH-RTL-AMT    03220000
                                         + ML151-MKDN-UNMTCH-RTL-AMT.   03230000
           COMPUTE SV-MKDN-BYR-RTL-AMT   = MNMCLC-MKDN-BYR-RTL-AMT      03240000
                                         + ML151-MKDN-BYR-RTL-AMT.      03250000
           COMPUTE SV-MKDN-STR-RTL-AMT   = MNMCLC-MKDN-STR-RTL-AMT      03260000
                                         + ML151-MKDN-STR-RTL-AMT.      03270000
           COMPUTE SV-MKUP-RTL-AMT       = MNMCLC-MKUP-RTL-AMT          03280000
                                         + ML151-MKUP-RTL-AMT.          03290000
           COMPUTE SV-XFER-IN-RTL-AMT    = MNMCLC-XFER-IN-RTL-AMT       03300000
                                         + ML151-XFER-IN-RTL-AMT.       03310000
           COMPUTE SV-XFER-OUT-RTL-AMT   = MNMCLC-XFER-OUT-RTL-AMT      03320000
                                         + ML151-XFER-OUT-RTL-AMT.      03330000
           COMPUTE SV-RCPT-RTL-AMT       = MNMCLC-RCPT-RTL-AMT          03340000
                                         + ML151-RCPT-RTL-AMT.          03350000
           COMPUTE SV-RCPT-NET-COST-AMT  = MNMCLC-RCPT-NET-COST-AMT     03360000
                                         + ML151-RCPT-NET-COST-AMT.     03370000
           COMPUTE SV-PADJ-RTL-AMT       = MNMCLC-PADJ-RTL-AMT          03380000
                                         + ML151-PADJ-RTL-AMT.          03390000
           COMPUTE SV-PADJ-NET-COST-AMT  = MNMCLC-PADJ-NET-COST-AMT     03400000
                                         + ML151-PADJ-NET-COST-AMT.     03410000
           COMPUTE SV-PADJ-FRGT-COST-AMT = MNMCLC-PADJ-FRGT-COST-AMT    03420000
                                         + ML151-PADJ-FRGT-COST-AMT.    03430000
           COMPUTE SV-INV-ADJ-RTL-AMT    = MNMCLC-INV-ADJ-RTL-AMT       03440000
                                         + ML151-INV-ADJ-RTL-AMT.       03450000
      *                                                                 03460000
      ******************************************************************03470000
      * READS THE CONDENSED FILE INTO THE TMNMCLC LAYOUT                03480000
      ******************************************************************03490000
       4000-READ-UPDATE-FILE.                                           03500000
           READ UPDATE-FILE INTO ML151-REC                              03510000
              AT END                                                    03520000
                MOVE 'Y' TO PS-EOF-UPDATE-FILE-SW.                      03530000
     *                                                                  03540000
           IF PS-EOF-UPDATE-FILE                                        03550000
               MOVE ML151-FSCL-MN-NBR         TO CR-FISCAL-NBR          03560000
               MOVE ML151-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   03570000
               MOVE ML151-DEPT-NBR            TO CR-DEPT-NBR            03580000
               MOVE ML151-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          03590000
               MOVE ML151-CHNL-CDE            TO CR-CHNL-CDE            03600000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    03610000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03620000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03630000
                                             TCKRSTINF-WORK-STRG-DESC   03640000
               EXEC SQL                                                 03650000
                 UPDATE TCKRSTINF                                       03660000
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03670000
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03680000
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03690000
               END-EXEC                                                 03700000
               IF SQLCODE = 0                                           03710000
                   CONTINUE                                             03720000
               ELSE                                                     03730000
                   MOVE PE-MSG-05          TO PV-OPERATION-ATTEMPTED    03740000
                   MOVE '* 4000-READ-CONDENSED *' TO PV-PARAGRAPH-NAME  03750000
                   PERFORM 9999-SQL-ABEND                               03760000
               END-IF                                                   03770000
            ELSE                                                        03780000
                ADD 1                        TO PA-INPUT-COUNT          03790000
            END-IF.                                                     03800000
      *                                                                 03810000
      ******************************************************************03820000
      * SELECT A ROW FROM THE MTHLY MCL CHNL TAB THAT MATCHES INPUT KEY 03830000
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      03840000
      ******************************************************************03850000
       7000-SELECT-DB-ROW.                                              03860000
      *                                                                 03870000
           EXEC SQL                                                     03880000
               SELECT SLS_RTL_AMT                                       03890000
                     ,MKDN_PLU_RTL_AMT                                  03900000
                     ,MKDN_RGST_RTL_AMT                                 03910000
                     ,MKDN_UMTCH_RTL_AMT                                03920000
                     ,MKDN_BYR_RTL_AMT                                  03930000
                     ,MKDN_STR_RTL_AMT                                  03940000
                     ,MKUP_RTL_AMT                                      03950000
                     ,XFER_IN_RTL_AMT                                   03960000
                     ,XFER_OUT_RTL_AMT                                  03970000
                     ,RCPT_RTL_AMT                                      03980000
                     ,RCPT_NET_COST_AMT                                 03990000
                     ,PADJ_RTL_AMT                                      04000000
                     ,PADJ_NET_COST_AMT                                 04010000
                     ,PADJ_FRGT_COST_AMT                                04020000
                     ,INV_ADJ_RTL_AMT                                   04030000
                 INTO :MNMCLC-SLS-RTL-AMT                               04040000
                     ,:MNMCLC-MKDN-PLU-RTL-AMT                          04050000
                     ,:MNMCLC-MKDN-RGST-RTL-AMT                         04060000
                     ,:MNMCLC-MKDN-UMTCH-RTL-AMT                        04070000
                     ,:MNMCLC-MKDN-BYR-RTL-AMT                          04080000
                     ,:MNMCLC-MKDN-STR-RTL-AMT                          04090000
                     ,:MNMCLC-MKUP-RTL-AMT                              04100000
                     ,:MNMCLC-XFER-IN-RTL-AMT                           04110000
                     ,:MNMCLC-XFER-OUT-RTL-AMT                          04120000
                     ,:MNMCLC-RCPT-RTL-AMT                              04130000
                     ,:MNMCLC-RCPT-NET-COST-AMT                         04140000
                     ,:MNMCLC-PADJ-RTL-AMT                              04150000
                     ,:MNMCLC-PADJ-NET-COST-AMT                         04160000
                     ,:MNMCLC-PADJ-FRGT-COST-AMT                        04170000
                     ,:MNMCLC-INV-ADJ-RTL-AMT                           04180000
               FROM TMNMCLC                                             04190000
              WHERE   FSCL_MN_NBR         = :ML151-FSCL-MN-NBR          04200000
                AND   FSCL_MN_END_DTE     = :ML151-FSCL-MN-END-DTE      04210000
                AND   DEPT_NBR            = :ML151-DEPT-NBR             04220000
                AND   MAJ_CL_NBR          = :ML151-MAJ-CL-NBR           04230000
                AND   CHNL_CDE            = :ML151-CHNL-CDE             04240000
W26603        WITH UR                                                   04250000
           END-EXEC.                                                    04260000
      *                                                                 04270000
           EVALUATE TRUE                                                04280000
               WHEN SQLCODE IS EQUAL TO ZERO                            04290000
                   MOVE 'U' TO PI-PROCESSING-OPTIONS-IND                04300000
               WHEN SQLCODE IS EQUAL TO +100                            04310000
                   MOVE 'I' TO PI-PROCESSING-OPTIONS-IND                04320000
               WHEN SQLCODE IS EQUAL TO -911                            04330000
                   DISPLAY '*** -911 OCCURRED ON SELECT ***'            04340000
                   ADD +1             TO PV-DEADLOCK-COUNT              04350000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04360000
                       MOVE '7000-SELECT-DB-ROW' TO                     04370000
                                                    PV-PARAGRAPH-NAME   04380000
                       PERFORM 9000-DEADLOCK-ERROR                      04390000
                   ELSE                                                 04400000
                       PERFORM 7999-RESTART-PROCESS                     04410000
                   END-IF                                               04420000
               WHEN SQLWARN IS NOT EQUAL TO SPACES                      04430000
               WHEN SQLCODE IS NOT EQUAL TO ZERO                        04440000
                  DISPLAY ML151-FSCL-MN-NBR          ' '                04450000
                          ML151-FSCL-MN-END-DTE      ' '                04460000
                          ML151-DEPT-NBR             ' '                04470000
                          ML151-MAJ-CL-NBR           ' '                04480000
                          ML151-CHNL-CDE             ' '                04490000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     04500000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             04510000
                   PERFORM 9999-SQL-ABEND                               04520000
           END-EVALUATE.                                                04530000
      *                                                                 04540000
      *                                                                 04550000
      ***************************************************************** 04560000
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO, DEPT  04570000
      * AND MAJOR CLASS. CURRENT TABLE VALUES UPDATED TO INCLUDE VALUES 04580000
      * FROM INPUT REC MLRD151                                          04590000
      ***************************************************************** 04600000
       7100-UPDATE-MNMCLC-ROW.                                          04610000
      *                                                                 04620000
           PERFORM 2100-CALCULATE-UPDATES.                              04630000
      *                                                                 04640000
           EXEC SQL                                                     04650000
               UPDATE TMNMCLC                                           04660000
                  SET SLS_RTL_AMT          = :SV-SLS-RTL-AMT            04670000
                     ,MKDN_PLU_RTL_AMT     = :SV-MKDN-PLU-RTL-AMT       04680000
                     ,MKDN_RGST_RTL_AMT    = :SV-MKDN-RGST-RTL-AMT      04690000
                     ,MKDN_UMTCH_RTL_AMT   = :SV-MKDN-UMTCH-RTL-AMT     04700000
                     ,MKDN_BYR_RTL_AMT     = :SV-MKDN-BYR-RTL-AMT       04710000
                     ,MKDN_STR_RTL_AMT     = :SV-MKDN-STR-RTL-AMT       04720000
                     ,MKUP_RTL_AMT         = :SV-MKUP-RTL-AMT           04730000
                     ,XFER_IN_RTL_AMT      = :SV-XFER-IN-RTL-AMT        04740000
                     ,XFER_OUT_RTL_AMT     = :SV-XFER-OUT-RTL-AMT       04750000
                     ,RCPT_RTL_AMT         = :SV-RCPT-RTL-AMT           04760000
                     ,RCPT_NET_COST_AMT    = :SV-RCPT-NET-COST-AMT      04770000
                     ,PADJ_RTL_AMT         = :SV-PADJ-RTL-AMT           04780000
                     ,PADJ_NET_COST_AMT    = :SV-PADJ-NET-COST-AMT      04790000
                     ,PADJ_FRGT_COST_AMT   = :SV-PADJ-FRGT-COST-AMT     04800000
                     ,INV_ADJ_RTL_AMT      = :SV-INV-ADJ-RTL-AMT        04810000
              WHERE   FSCL_MN_NBR          = :ML151-FSCL-MN-NBR         04820000
                AND   FSCL_MN_END_DTE      = :ML151-FSCL-MN-END-DTE     04830000
                AND   DEPT_NBR             = :ML151-DEPT-NBR            04840000
                AND   MAJ_CL_NBR           = :ML151-MAJ-CL-NBR          04850000
                AND   CHNL_CDE             = :ML151-CHNL-CDE            04860000
           END-EXEC                                                     04870000
      *                                                                 04880000
           EVALUATE SQLCODE                                             04890000
               WHEN 0                                                   04900000
                   CONTINUE                                             04910000
               WHEN -911                                                04920000
                   ADD +1             TO PV-DEADLOCK-COUNT              04930000
                   DISPLAY '*** -911 OCCURRED ON UPDATE ***'            04940000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04950000
                       MOVE '7100-UPDATE-MNMCLC-ROW' TO                 04960000
                                                    PV-PARAGRAPH-NAME   04970000
                       PERFORM 9000-DEADLOCK-ERROR                      04980000
                   ELSE                                                 04990000
                       PERFORM 7999-RESTART-PROCESS                     05000000
                   END-IF                                               05010000
               WHEN OTHER                                               05020000
                   MOVE '7100-UPDATE-MNMCLC-ROW' TO PV-PARAGRAPH-NAME   05030000
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED05040000
                   PERFORM 9999-SQL-ABEND                               05050000
           END-EVALUATE.                                                05060000
      *                                                                 05070000
           ADD 1 TO PA-UPDATE-COUNT.                                    05080000
      *                                                                 05090000
      ***************************************************************** 05100000
      * THERE WAS NO MATCHING ROW ON THE MTHLY MCLS TABLE. INITIAL    * 05110000
      * - ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW            * 05120000
      * - CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN     * 05130000
      *   RECALC                                                      * 05140000
      ***************************************************************** 05150000
       7200-INSERT-MNMCLC-ROW.                                          05160000
      *                                                                 05170000
           EXEC SQL                                                     05180000
               INSERT INTO TMNMCLC                                      05190000
                     (FSCL_MN_NBR                                       05200000
                     ,FSCL_MN_END_DTE                                   05210000
                     ,DEPT_NBR                                          05220000
                     ,MAJ_CL_NBR                                        05230000
                     ,CHNL_CDE                                          05240000
                     ,SLS_RTL_AMT                                       05250000
                     ,MKDN_PLU_RTL_AMT                                  05260000
                     ,MKDN_RGST_RTL_AMT                                 05270000
                     ,MKDN_UMTCH_RTL_AMT                                05280000
                     ,MKDN_BYR_RTL_AMT                                  05290000
                     ,MKDN_STR_RTL_AMT                                  05300000
                     ,MKUP_RTL_AMT                                      05310000
                     ,XFER_IN_RTL_AMT                                   05320000
                     ,XFER_OUT_RTL_AMT                                  05330000
                     ,RCPT_RTL_AMT                                      05340000
                     ,RCPT_NET_COST_AMT                                 05350000
                     ,PADJ_RTL_AMT                                      05360000
                     ,PADJ_NET_COST_AMT                                 05370000
                     ,PADJ_FRGT_COST_AMT                                05380000
                     ,INV_ADJ_RTL_AMT                                   05390000
                     ,SHNK_RTL_AMT                                      05400000
                     ,END_INV_RTL_AMT                                   05410000
                     ,CUM_MKUP_PCT                                      05420000
                     ,END_INV_COST_AMT                                  05430000
                     ,SLS_COST_AMT                                      05440000
                     ,GRO_MRGN_AMT)                                     05450000
               VALUES                                                   05460000
                     (:ML151-FSCL-MN-NBR                                05470000
                     ,:ML151-FSCL-MN-END-DTE                            05480000
                     ,:ML151-DEPT-NBR                                   05490000
                     ,:ML151-MAJ-CL-NBR                                 05500000
                     ,:ML151-CHNL-CDE                                   05510000
                     ,:ML151-SLS-RTL-AMT                                05520000
                     ,:ML151-MKDN-PLU-RTL-AMT                           05530000
                     ,:ML151-MKDN-RGST-RTL-AMT                          05540000
                     ,:ML151-MKDN-UNMTCH-RTL-AMT                        05550000
                     ,:ML151-MKDN-BYR-RTL-AMT                           05560000
                     ,:ML151-MKDN-STR-RTL-AMT                           05570000
                     ,:ML151-MKUP-RTL-AMT                               05580000
                     ,:ML151-XFER-IN-RTL-AMT                            05590000
                     ,:ML151-XFER-OUT-RTL-AMT                           05600000
                     ,:ML151-RCPT-RTL-AMT                               05610000
                     ,:ML151-RCPT-NET-COST-AMT                          05620000
                     ,:ML151-PADJ-RTL-AMT                               05630000
                     ,:ML151-PADJ-NET-COST-AMT                          05640000
                     ,:ML151-PADJ-FRGT-COST-AMT                         05650000
                     ,:ML151-INV-ADJ-RTL-AMT                            05660000
                     ,0                                                 05670000
                     ,0                                                 05680000
                     ,0                                                 05690000
                     ,0                                                 05700000
                     ,0                                                 05710000
                     ,0)                                                05720000
           END-EXEC                                                     05730000
      *                                                                 05740000
           EVALUATE SQLCODE                                             05750000
               WHEN 0                                                   05760000
                   CONTINUE                                             05770000
               WHEN -911                                                05780000
                   ADD +1             TO PV-DEADLOCK-COUNT              05790000
                   DISPLAY '*** -911 OCCURRED ON INSERT ***'            05800000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05810000
                       MOVE '7200-INSERT-MNMCLC-ROW' TO                 05820000
                                                    PV-PARAGRAPH-NAME   05830000
                       PERFORM 9000-DEADLOCK-ERROR                      05840000
                   ELSE                                                 05850000
                       PERFORM 7999-RESTART-PROCESS                     05860000
                   END-IF                                               05870000
               WHEN OTHER                                               05880000
                  DISPLAY ML151-FSCL-MN-NBR          ' '                05890000
                          ML151-FSCL-MN-END-DTE      ' '                05900000
                          ML151-DEPT-NBR             ' '                05910000
                          ML151-MAJ-CL-NBR           ' '                05920000
                          ML151-CHNL-CDE             ' '                05930000
                   MOVE '7200-INSERT-MNMCLC-ROW' TO PV-PARAGRAPH-NAME   05940000
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED05950000
                   PERFORM 9999-SQL-ABEND                               05960000
           END-EVALUATE.                                                05970000
                                                                        05980000
           ADD 1 TO PA-INSERT-COUNT.                                    05990000
                                                                        06000000
      ******************************************************************06010000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *06020000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *06030000
      *            CONTROL TABLE                                       *06040000
      ******************************************************************06050000
       7300-GET-COMMIT-TIMESTAMP.                                       06060000
                                                                        06070000
           EXEC SQL                                                     06080000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        06090000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       06100000
               INTO :PV-COMMIT-TIMESTAMP                                06110000
               FROM TCKRSTCNTL                                          06120000
              WHERE JOB_NAME  = :PC-JOB-NAME                            06130000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        06140000
           END-EXEC.                                                    06150000
                                                                        06160000
           EVALUATE TRUE                                                06170000
               WHEN SQLCODE = 0                                         06180000
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  06190000
                   CONTINUE                                             06200000
               WHEN SQLCODE NOT EQUAL ZERO                              06210000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME06220000
                   PERFORM 9999-SQL-ABEND                               06230000
           END-EVALUATE.                                                06240000
      *                                                                 06250000
      *                                                                 06260000
      ******************************************************************06270000
      * 7400-INIT-CHECKPOINT-SELECT                                    *06280000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *06290000
      *            IF WE ARE IN A RESTART CONDITION.                   *06300000
      ******************************************************************06310000
       7400-INIT-CHECKPOINT-SELECT.                                     06320000
                                                                        06330000
           EXEC SQL                                                     06340000
             SELECT  CKPT_STAT_CODE                                     06350000
                    ,CKPT_FRQNCY_QTY                                    06360000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         06370000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        06380000
               FROM  TCKRSTCNTL                                         06390000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06400000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06410000
           END-EXEC.                                                    06420000
                                                                        06430000
           IF SQLCODE = 0                                               06440000
               CONTINUE                                                 06450000
           ELSE                                                         06460000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  06470000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     06480000
               PERFORM 9999-SQL-ABEND                                   06490000
           END-IF.                                                      06500000
      *                                                                 06510000
      *                                                                 06520000
      ******************************************************************06530000
      * 7500-SELECT-RESTART-INFO                                       *06540000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *06550000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *06560000
      ******************************************************************06570000
       7500-SELECT-RESTART-INFO.                                        06580000
                                                                        06590000
           EXEC SQL                                                     06600000
             SELECT  WORK_STRG_DESC                                     06610000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          06620000
               FROM  TCKRSTINF                                          06630000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06640000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06650000
           END-EXEC.                                                    06660000
                                                                        06670000
           IF SQLCODE = 0                                               06680000
               CONTINUE                                                 06690000
           ELSE                                                         06700000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   06710000
               PERFORM 9999-SQL-ABEND                                   06720000
           END-IF.                                                      06730000
                                                                        06740000
      ******************************************************************06750000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *06760000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *06770000
      ******************************************************************06780000
       7600-SET-CURRENT-TIMESTAMP.                                      06790000
                                                                        06800000
           EXEC SQL                                                     06810000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           06820000
           END-EXEC.                                                    06830000
                                                                        06840000
           IF SQLCODE = 0                                               06850000
               CONTINUE                                                 06860000
           ELSE                                                         06870000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 06880000
               PERFORM 9999-SQL-ABEND                                   06890000
           END-IF.                                                      06900000
      *                                                                 06910000
      *                                                                 06920000
      ******************************************************************06930000
      * 7700-COMMIT-PROCESSING.                                        *06940000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *06950000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*06960000
      ******************************************************************06970000
       7700-COMMIT-PROCESSING.                                          06980000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06990000
                                                                        07000000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          07010000
                                                                        07020000
      * PREPARE COMMIT RECORD FOR UPDATE                                07030000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                07040000
               MOVE ML151-FSCL-MN-NBR         TO CR-FISCAL-NBR          07050000
               MOVE ML151-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   07060000
               MOVE ML151-DEPT-NBR            TO CR-DEPT-NBR            07070000
               MOVE ML151-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          07080000
               MOVE ML151-CHNL-CDE            TO CR-CHNL-CDE            07090000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    07100000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  07110000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       07120000
                                             TCKRSTINF-WORK-STRG-DESC   07130000
               IF PS-FIRST-COMMIT                                       07140000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                07150000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                07160000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       07170000
               END-IF                                                   07180000
               PERFORM 7800-COMMIT-CHANGES                              07190000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        07200000
           END-IF.                                                      07210000
      *                                                                 07220000
      *                                                                 07230000
      ******************************************************************07240000
      * 7800-COMMIT-CHANGES.                                            07250000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      07260000
      *            TAKE A COMMIT.                                       07270000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    07280000
      ******************************************************************07290000
       7800-COMMIT-CHANGES.                                             07300000
                                                                        07310000
           EXEC SQL                                                     07320000
             UPDATE TCKRSTINF                                           07330000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          07340000
              WHERE JOB_NAME       = :PC-JOB-NAME                       07350000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   07360000
           END-EXEC.                                                    07370000
                                                                        07380000
           IF SQLCODE = 0                                               07390000
               CONTINUE                                                 07400000
           ELSE                                                         07410000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED07420000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07430000
               PERFORM 9999-SQL-ABEND                                   07440000
           END-IF.                                                      07450000
                                                                        07460000
           EXEC SQL                                                     07470000
               COMMIT                                                   07480000
           END-EXEC.                                                    07490000
                                                                        07500000
           IF SQLCODE = 0                                               07510000
               CONTINUE                                                 07520000
           ELSE                                                         07530000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED07540000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07550000
               PERFORM 9999-SQL-ABEND                                   07560000
           END-IF.                                                      07570000
      *                                                                 07580000
      ******************************************************************07590000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   07600000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   07610000
      ******************************************************************07620000
       7900-UPDATE-CHECKPOINT-STATUS.                                   07630000
                                                                        07640000
           EXEC SQL                                                     07650000
             UPDATE  TCKRSTCNTL                                         07660000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        07670000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07680000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07690000
           END-EXEC.                                                    07700000
                                                                        07710000
           IF SQLCODE = 0                                               07720000
               CONTINUE                                                 07730000
           ELSE                                                         07740000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME07750000
               PERFORM 9999-SQL-ABEND                                   07760000
           END-IF.                                                      07770000
                                                                        07780000
           EJECT                                                        07790000
      *                                                                 07800000
      *                                                                 07810000
      ******************************************************************07820000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TMNMCLC ROW FOR UPDATE07830000
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  07840000
      ******************************************************************07850000
       7999-RESTART-PROCESS.                                            07860000
      *                                                                 07870000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           07880000
      *                                                                 07890000
           CLOSE UPDATE-FILE.                                           07900000
      *                                                                 07910000
           PERFORM 7500-SELECT-RESTART-INFO.                            07920000
      *                                                                 07930000
           DISPLAY '**** RESTART **** '.                                07940000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 07950000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   07960000
                                                                        07970000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      07980000
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        07990000
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       08000000
           IF PS-FIRST-COMMIT                                           08010000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)               08020000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     08030000
                      ' BEGINNING'                                      08040000
           ELSE                                                         08050000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           08060000
                        TCKRSTINF-WORK-STRG-DESC (1:28)                 08070000
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 08080000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         08090000
                    CR-CHECKPOINT-RESTART-AREA                          08100000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                08110000
               DISPLAY 'MTH NBR        ' CR-FISCAL-NBR                  08120000
               DISPLAY 'MTH END DATE   ' CR-FISCAL-MN-END-DTE           08130000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   08140000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 08150000
               DISPLAY 'CHANNEL CODE    ' CR-CHNL-CDE                   08160000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           08170000
           END-IF.                                                      08180000
                                                                        08190000
           OPEN INPUT UPDATE-FILE.                                      08200000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      08210000
      *                                                                 08220000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          08230000
           PERFORM 4000-READ-UPDATE-FILE                                08240000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT               08250000
                 OR PS-EOF-UPDATE-FILE.                                 08260000
           IF PS-EOF-UPDATE-FILE                                        08270000
               DISPLAY 'END OF INPUT FILE ON RESTART'                   08280000
           ELSE                                                         08290000
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              08300000
                        PA-INPUT-COUNT                                  08310000
               DISPLAY 'FISCAL MTH NBR       ' ML151-FSCL-MN-NBR        08320000
               DISPLAY 'FISCAL MTH END DATE  ' ML151-FSCL-MN-END-DTE    08330000
               DISPLAY 'DEPARTMENT NBR        ' ML151-DEPT-NBR          08340000
               DISPLAY 'MAJOR CLASS NBR       ' ML151-MAJ-CL-NBR        08350000
               DISPLAY 'CHANNEL CODE          ' ML151-CHNL-CDE          08360000
           END-IF.                                                      08370000
      *                                                                 08380000
      ******************************************************************08390000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *08400000
      ******************************************************************08410000
       8000-EOJ-ROUTINE.                                                08420000
      *                                                                 08430000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       08440000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       08450000
                                                                        08460000
      *                                                                 08470000
           DISPLAY SPACES.                                              08480000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         08490000
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        08500000
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        08510000
           DISPLAY SPACES.                                              08520000
      *                                                                 08530000
           CLOSE UPDATE-FILE.                                           08540000
      *                                                                 08550000
      *                                                                 08560000
      ******************************************************************08570000
      *  PERFROMED BY 7100-UPDATE AND 7200-INSERT                       08580000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   08590000
      ******************************************************************08600000
       9000-DEADLOCK-ERROR.                                             08610000
      *                                                                 08620000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     08630000
               PERFORM 9999-SQL-ABEND.                                  08640000
      *                                                                 08650000
      ******************************************************************08660000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               08670000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             08680000
      ******************************************************************08690000
       9999-SQL-ABEND.                                                  08700000
                                                                        08710000
           MOVE +4013                 TO ABEND-CODE.                    08720000
           DISPLAY '**  ABEND     **'.                                  08730000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              08740000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            08750000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       08760000
                                                                        08770000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         08780000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        08790000
                                                                        08800000
           COPY DPPD004.                                                08810000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           08820000
