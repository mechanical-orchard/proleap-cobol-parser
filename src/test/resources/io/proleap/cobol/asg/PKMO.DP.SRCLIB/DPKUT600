00001  IDENTIFICATION DIVISION.                                         04/26/93
00002  PROGRAM-ID.     DPKUT600                                         DPKUT600
00003  AUTHOR.         URSULA MCCLOSKEY.                                   LV069
00004  INSTALLATION.   KOHLS.                                           DPKUT600
00005  DATE-WRITTEN.   MARCH 1993.                                         CL*12
00006 *                                                                 DPKUT600
00007  ENVIRONMENT DIVISION.                                            DPKUT600
00008  INPUT-OUTPUT SECTION.                                            DPKUT600
00009                                                                   DPKUT600
00010 *-----------------------------------------------------------*        CL*38
00011 *  THIS PROGRAM READS THE RACF USER BASE DATA DB2 TABLE.    *        CL*38
00012 *  OUTPUT IS ONE SEQUENTIAL FILE CONTAINGING USER DATA      *        CL*38
00013 *  FOR CROSS REFERENCE TO THE HR SYSTEM.                    *        CL*38
00014 *-----------------------------------------------------------*        CL*38
00015                                                                      CL*38
00016  FILE-CONTROL.                                                    DPKUT600
00017                                                                   DPKUT600
00018      SELECT RACFUSER-EXTRACT-FILE ASSIGN TO RACFDATA.                CL*14
00019                                                                   DPKUT600
00020  DATA DIVISION.                                                   DPKUT600
00021  FILE SECTION.                                                    DPKUT600
00022  FD  RACFUSER-EXTRACT-FILE                                           CL*14
00023      RECORDING MODE IS F                                          DPKUT600
00024      LABEL RECORDS ARE STANDARD                                   DPKUT600
00025      RECORD CONTAINS 109 CHARACTERS                                  CL*66
00026      BLOCK CONTAINS 0 RECORDS                                     DPKUT600
00027      DATA RECORD IS RACFUSER-RECORD.                                 CL*14
00028  01  RACFUSER-EXTRACT-RECORD PIC X(109).                             CL*67
00029                                                                   DPKUT600
00030 *                                                                 DPKUT600
00031  WORKING-STORAGE SECTION.                                         DPKUT600
00032 *                                                                    CL*23
00033  77  INSTALL-DATA-WORK1      PIC X(255).                             CL*62
00034  77  INSTALL-DATA-WORK2      PIC X(255).                             CL*62
00035  01  PS-DATA-SWITCH                PIC X(1)  VALUE 'N'.              CL*23
00036      88  PS-HAVE-DATA                        VALUE 'Y'.              CL*23
00037      88  PS-NO-DATA                          VALUE 'N'.              CL*23
00038  01  PS-USER-CURSOR-SWITCH         PIC X(1)  VALUE 'N'.              CL*23
00039      88  PS-END-OF-USER-CURSOR               VALUE 'Y'.              CL*23
00040      88  PS-NOT-END-OF-USER-CURSOR           VALUE 'N'.              CL*23
00041  01  PS-DB2-ERROR-INDICATOR        PIC X(1)  VALUE 'N'.              CL*23
00042      88  PS-DB2-ERROR                        VALUE 'Y'.              CL*23
00043      88  PS-NO-DB2-ERROR                     VALUE 'N'.              CL*23
00044 *                                                                    CL*14
00045      COPY DPRD600.                                                   CL*19
00046 *                                                                    CL*19
00047  01  PV-VARIABLES.                                                   CL*19
00048      05  PV-ABEND-CODE       PIC S9(4)   VALUE +0                    CL*27
00049                                          COMP SYNC.                  CL*14
00050      05  PV-SQL-CODE         PIC S9(4)   .                           CL*27
00051  01  NI-VARIABLES.                                                   CL*20
00052      05  USBD-INSTALL-DATA-NI    PIC S9(4)    COMP.                  CL*20
00053      05  USBD-LASTJOB-DATE-NI    PIC S9(4)    COMP.                  CL*41
00054      05  USBD-LASTJOB-TIME-NI    PIC S9(4)    COMP.                  CL*41
00055  01  SUBR-PARAMETER.                                                 CL*46
00056      05  SUBR-OPTION             PIC X(4)   VALUE 'DES,'.            CL*50
00057      05  SUBR-SSN                PIC X(9).                           CL*44
00058 *----------------------------------------------------------*         CL*23
00059 *    SQL COMMUNICATIONS AREA DCLGEN                        *         CL*23
00060 *----------------------------------------------------------*         CL*23
00061 *                                                                    CL*23
00062      EXEC SQL                                                        CL*23
00063          INCLUDE SQLCA                                               CL*23
00064      END-EXEC.                                                       CL*23
00065 *                                                                 DPKUT600
00066 *----------------------------------------------------------*      DPKUT600
00067 *    USER_BD USER INFORMATION  DCLGEN                      *      DPKUT600
00068 *----------------------------------------------------------*      DPKUT600
00069      EXEC SQL                                                     DPKUT600
00070          INCLUDE TRCFUBD                                          DPKUT600
00071      END-EXEC.                                                    DPKUT600
00072 *                                                                 DPKUT600
00073 *----------------------------------------------------------*      DPKUT600
00074 *    DECLARE USERID-RECORD CURSOR                          *      DPKUT600
00075 *----------------------------------------------------------*      DPKUT600
00076 *                                                                 DPKUT600
00077      EXEC SQL                                                     DPKUT600
00078         DECLARE USERID_RECORD   CURSOR                            DPKUT600
00079         FOR    SELECT                                             DPKUT600
00080                   USBD_NAME                                          CL*29
00081                ,  USBD_DEFGRP_ID                                     CL*30
00082                ,  USBD_OWNER_ID                                      CL*52
00083                ,  USBD_INSTALL_DATA                                  CL*24
00084                ,  USBD_REVOKE                                        CL*24
00085                ,  USBD_LASTJOB_DATE                                  CL*41
00086                ,  USBD_LASTJOB_TIME                                  CL*41
00087                ,  USBD_CREATE_DATE                                   CL*51
00088         FROM                                                      DPKUT600
00089              DB2TKMS.USER_BD                                         CL*22
00090    END-EXEC.                                                      DPKUT600
00091 *                                                                 DPKUT600
00092  PROCEDURE DIVISION.                                              DPKUT600
00093      PERFORM 1000-OPEN-FILES            THRU 1000-EXIT.              CL*14
00094      PERFORM 2000-READ-WRITE-FILES      THRU 2000-EXIT               CL*33
00095          UNTIL PS-END-OF-USER-CURSOR.                                CL*33
00096      PERFORM 3000-CLOSE-FILES           THRU 3000-EXIT.              CL*34
00097      PERFORM 9999-TERMINATE             THRU 9999-EXIT.              CL*18
00098      GOBACK.                                                      DPKUT600
00099                                                                   DPKUT600
00100 *                                                                 DPKUT600
00101 *----------------------------------------------------------*      DPKUT600
00102 *    OPEN FILES                                            *         CL*14
00103 *----------------------------------------------------------*      DPKUT600
00104 *                                                                 DPKUT600
00105  1000-OPEN-FILES.                                                    CL*14
00106      OPEN OUTPUT RACFUSER-EXTRACT-FILE.                              CL*14
00107      EXEC SQL                                                        CL*14
00108           OPEN USERID_RECORD                                         CL*14
00109       END-EXEC                                                       CL*14
00110       EVALUATE TRUE                                                  CL*14
00111           WHEN SQLCODE = 0                                           CL*14
00112               CONTINUE                                               CL*14
00113           WHEN SQLCODE > 0                                           CL*14
00114           WHEN SQLCODE < 0                                           CL*14
00115               SET PS-DB2-ERROR TO TRUE                               CL*14
00116               DISPLAY '**************************'                   CL*14
00117               DISPLAY '***** DPKUT600 ABEND *****'                   CL*14
00118               DISPLAY '**************************'                   CL*14
00119               MOVE SQLCODE TO PV-SQL-CODE                            CL*27
00120               MOVE +16 TO PV-ABEND-CODE                              CL*27
00121               CALL 'ILBOABN0' USING PV-ABEND-CODE                    CL*27
00122       END-EVALUATE.                                                  CL*14
00123  1000-EXIT.                                                          CL*14
00124      EXIT.                                                        DPKUT600
00125 *----------------------------------------------------------*      DPKUT600
00126 * WRITE THE RACF XTRACT FILE                               *         CL*14
00127 *----------------------------------------------------------*      DPKUT600
00128  2000-READ-WRITE-FILES.                                              CL*14
00129       EXEC SQL                                                    DPKUT600
00130           FETCH USERID_RECORD                                     DPKUT600
00131           INTO                                                    DPKUT600
00132                   :USBD-NAME                                         CL*29
00133                 , :USBD-DEFGRP-ID                                    CL*31
00134                 , :USBD-OWNER-ID                                     CL*52
00135                 , :USBD-INSTALL-DATA   :USBD-INSTALL-DATA-NI      DPKUT600
00136                 , :USBD-REVOKE                                    DPKUT600
00137                 , :USBD-LASTJOB-DATE   :USBD-LASTJOB-DATE-NI         CL*41
00138                 , :USBD-LASTJOB-TIME   :USBD-LASTJOB-TIME-NI         CL*41
00139                 , :USBD-CREATE-DATE                                  CL*51
00140        END-EXEC.                                                  DPKUT600
00141 *                                                                 DPKUT600
00142        IF SQLCODE = 0                                             DPKUT600
00143            SET PS-HAVE-DATA       TO TRUE                            CL*29
00144            MOVE USBD-NAME         TO DP600-RACFUSER-USERID           CL*29
00145            MOVE USBD-DEFGRP-ID    TO DP600-RACFUSER-DEFGRPID         CL*29
00146            MOVE USBD-OWNER-ID     TO DP600-RACFUSER-OWNER            CL*55
00147            MOVE USBD-REVOKE       TO DP600-RACFUSER-REVOKE           CL*36
00148            MOVE USBD-CREATE-DATE  TO DP600-RACFUSER-CREADATE         CL*51
00149            IF USBD-INSTALL-DATA-NI <  0                              CL*29
00150                MOVE SPACES TO DP600-RACFUSER-NAME                    CL*29
00151                MOVE SPACES TO DP600-RACFUSER-CATEGORY                CL*39
00152                MOVE SPACES TO DP600-RACFUSER-SSN                     CL*32
00153                MOVE SPACES TO DP600-RACFUSER-AGENCY                  CL*65
00154            ELSE                                                      CL*61
00155                PERFORM 7100-MOVE-USER-NAME THRU 7100-EXIT            CL*57
00156                PERFORM 7200-MOVE-USER-CAT  THRU 7200-EXIT            CL*62
00157                PERFORM 7300-MOVE-USER-SSN  THRU 7300-EXIT            CL*62
00158                PERFORM 7400-MOVE-USER-AGENCY THRU 7400-EXIT          CL*65
00159            END-IF                                                    CL*29
00160            IF USBD-LASTJOB-DATE-NI < 0                               CL*41
00161               MOVE ' -------- '      TO  DP600-RACFUSER-LASTUSED     CL*42
00162            ELSE                                                      CL*41
00163               MOVE USBD-LASTJOB-DATE TO  DP600-RACFUSER-LASTUSED     CL*42
00164            END-IF                                                    CL*41
00165            WRITE RACFUSER-EXTRACT-RECORD                             CL*29
00166                FROM DP600-RACFUSER-RECORD                            CL*29
00167        ELSE                                                       DPKUT600
00168        IF SQLCODE = +100                                          DPKUT600
00169            SET PS-END-OF-USER-CURSOR TO TRUE                      DPKUT600
00170        ELSE                                                       DPKUT600
00171            SET PS-DB2-ERROR TO TRUE                                  CL*14
00172            DISPLAY '**************************'                      CL*14
00173            DISPLAY '***** DPKUT600 ABEND *****'                      CL*14
00174            DISPLAY '**************************'                      CL*14
00175            MOVE SQLCODE TO PV-SQL-CODE                               CL*27
00176            MOVE +16 TO PV-ABEND-CODE                                 CL*27
00177            CALL 'ILBOABN0' USING PV-ABEND-CODE.                      CL*27
00178 *                                                                 DPKUT600
00179   2000-EXIT.                                                         CL*16
00180      EXIT.                                                        DPKUT600
00181 *----------------------------------------------------------*      DPKUT600
00182 * CLOSE USERID RECORD                                      *         CL*34
00183 *----------------------------------------------------------*      DPKUT600
00184 *                                                                 DPKUT600
00185  3000-CLOSE-FILES.                                                   CL*34
00186      CLOSE RACFUSER-EXTRACT-FILE.                                    CL*16
00187      EXEC SQL                                                     DPKUT600
00188          CLOSE USERID_RECORD                                      DPKUT600
00189      END-EXEC                                                     DPKUT600
00190      EVALUATE TRUE                                                DPKUT600
00191          WHEN SQLCODE = 0                                         DPKUT600
00192              CONTINUE                                             DPKUT600
00193          WHEN SQLCODE > 0                                         DPKUT600
00194          WHEN SQLCODE < 0                                         DPKUT600
00195            SET PS-DB2-ERROR TO TRUE                                  CL*16
00196            DISPLAY '**************************'                      CL*16
00197            DISPLAY '***** DPKUT600 ABEND *****'                      CL*16
00198            DISPLAY '**************************'                      CL*16
00199            MOVE SQLCODE TO PV-SQL-CODE                               CL*27
00200            MOVE +16 TO PV-ABEND-CODE                                 CL*27
00201            CALL 'ILBOABN0' USING PV-ABEND-CODE                       CL*27
00202      END-EVALUATE.                                                DPKUT600
00203 *                                                                    CL*34
00204 *                                                                    CL*34
00205      EXEC SQL                                                        CL*34
00206          COMMIT                                                      CL*34
00207      END-EXEC.                                                       CL*34
00208      EVALUATE TRUE                                                   CL*34
00209          WHEN SQLCODE = 0                                            CL*34
00210              CONTINUE                                                CL*34
00211          WHEN SQLCODE > 0                                            CL*34
00212          WHEN SQLCODE < 0                                            CL*34
00213            SET PS-DB2-ERROR TO TRUE                                  CL*34
00214            DISPLAY '**************************'                      CL*34
00215            DISPLAY '***** DPKUT600 ABEND *****'                      CL*34
00216            DISPLAY '**************************'                      CL*34
00217            MOVE SQLCODE TO PV-SQL-CODE                               CL*34
00218            MOVE +16 TO PV-ABEND-CODE                                 CL*34
00219            CALL 'ILBOABN0' USING PV-ABEND-CODE                       CL*34
00220      END-EVALUATE.                                                   CL*34
00221 *                                                                 DPKUT600
00222  3000-EXIT.                                                          CL*34
00223      EXIT.                                                        DPKUT600
00224 *----------------------------------------------------------*      DPKUT600
00225 * MOVE-USER-NAME                                           *      DPKUT600
00226 *----------------------------------------------------------*      DPKUT600
00227 *                                                                 DPKUT600
00228  7100-MOVE-USER-NAME.                                                CL*56
00229      MOVE SPACES TO DP600-RACFUSER-NAME.                             CL*56
00230      MOVE SPACES TO INSTALL-DATA-WORK1.                              CL*62
00231      MOVE SPACES TO INSTALL-DATA-WORK2.                              CL*62
00232      UNSTRING USBD-INSTALL-DATA                                      CL*56
00233         DELIMITED BY 'NAME('                                         CL*56
00234         INTO INSTALL-DATA-WORK1                                      CL*62
00235              INSTALL-DATA-WORK2.                                     CL*62
00236      UNSTRING INSTALL-DATA-WORK2                                     CL*62
00237         DELIMITED BY ')'                                             CL*56
00238         INTO DP600-RACFUSER-NAME.                                    CL*56
00239  7100-EXIT.                                                          CL*56
00240      EXIT.                                                        DPKUT600
00241 *----------------------------------------------------------*         CL*62
00242 * MOVE-USER-CAT                                            *         CL*62
00243 *----------------------------------------------------------*         CL*62
00244 *                                                                    CL*62
00245  7200-MOVE-USER-CAT.                                                 CL*62
00246      MOVE SPACES TO DP600-RACFUSER-CATEGORY.                         CL*62
00247      MOVE SPACES TO INSTALL-DATA-WORK1.                              CL*62
00248      MOVE SPACES TO INSTALL-DATA-WORK2.                              CL*62
00249      UNSTRING USBD-INSTALL-DATA                                      CL*62
00250         DELIMITED BY 'CAT('                                          CL*62
00251         INTO INSTALL-DATA-WORK1                                      CL*62
00252              INSTALL-DATA-WORK2.                                     CL*62
00253      UNSTRING INSTALL-DATA-WORK2                                     CL*62
00254         DELIMITED BY ')'                                             CL*62
00255         INTO DP600-RACFUSER-CATEGORY.                                CL*62
00256  7200-EXIT.                                                          CL*62
00257      EXIT.                                                           CL*62
00258 *----------------------------------------------------------*         CL*62
00259 * MOVE-USER-SSN                                            *         CL*62
00260 *----------------------------------------------------------*         CL*62
00261 *                                                                    CL*62
00262  7300-MOVE-USER-SSN.                                                 CL*62
00263      MOVE SPACES TO DP600-RACFUSER-SSN.                              CL*62
00264      MOVE SPACES TO INSTALL-DATA-WORK1.                              CL*62
00265      MOVE SPACES TO INSTALL-DATA-WORK2.                              CL*62
00266      UNSTRING USBD-INSTALL-DATA                                      CL*62
00267         DELIMITED BY 'SS('                                           CL*62
00268         INTO INSTALL-DATA-WORK1                                      CL*62
00269              INSTALL-DATA-WORK2.                                     CL*62
00270      UNSTRING INSTALL-DATA-WORK2                                     CL*62
00271         DELIMITED BY ')'                                             CL*62
00272         INTO DP600-RACFUSER-SSN.                                     CL*62
00273      IF DP600-RACFUSER-SSN NOT EQUAL '         '                     CL*62
00274         MOVE DP600-RACFUSER-SSN TO SUBR-SSN                          CL*62
00275         CALL 'CMNERTR' USING SUBR-PARAMETER                          CL*62
00276         MOVE SUBR-SSN TO DP600-RACFUSER-SSN                          CL*62
00277      END-IF.                                                         CL*62
00278  7300-EXIT.                                                          CL*62
00279      EXIT.                                                           CL*62
00280 *----------------------------------------------------------*         CL*65
00281 * MOVE-USER-AGENCY                                         *         CL*65
00282 *----------------------------------------------------------*         CL*65
00283 *                                                                    CL*65
00284  7400-MOVE-USER-AGENCY.                                              CL*65
00285      MOVE SPACES TO DP600-RACFUSER-AGENCY.                           CL*65
00286      MOVE SPACES TO INSTALL-DATA-WORK1.                              CL*65
00287      MOVE SPACES TO INSTALL-DATA-WORK2.                              CL*65
00288      UNSTRING USBD-INSTALL-DATA                                      CL*65
00289         DELIMITED BY 'AGENCY('                                       CL*65
00290         INTO INSTALL-DATA-WORK1                                      CL*65
00291              INSTALL-DATA-WORK2.                                     CL*65
00292      UNSTRING INSTALL-DATA-WORK2                                     CL*65
00293         DELIMITED BY ')'                                             CL*65
00294         INTO DP600-RACFUSER-AGENCY.                                  CL*69
00295  7400-EXIT.                                                          CL*65
00296      EXIT.                                                           CL*65
00297 *----------------------------------------------------------*      DPKUT600
00298 * TERMINATE PROGRAM                                        *      DPKUT600
00299 *----------------------------------------------------------*      DPKUT600
00300 *                                                                 DPKUT600
00301  9999-TERMINATE.                                                  DPKUT600
00302      GOBACK.                                                      DPKUT600
00303  9999-EXIT.                                                       DPKUT600
00304      EXIT.                                                        DPKUT600
