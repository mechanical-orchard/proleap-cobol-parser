       IDENTIFICATION DIVISION.                                         00010005
       PROGRAM-ID.    ICKUT012.                                         00020005
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030005
       DATE-WRITTEN.  06-24-2008.                                       00040005
       DATE-COMPILED.                                                   00050005
                                                                        00060005
      ******************************************************************00070005
      *PURPOSE:   THE PROGRAM IS DESIGNED TO FORMAT THE ICT_CNT_STUP    00080005
      *           UNLOAD FROM THE USA INVENTORY COUNTS SYSTEM TO EDW    00090005
      *           LOAD STANDARDS.                                       00100005
      *                (SEE THE FLOW SECTION FOR MORE DETAILS)          00110005
      *                                                                 00120005
      *CHANGE HISTORY:                                                  00130005
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00140005
      * ------- ---------- ----------- -------------------------------- 00150005
      *         12-14-2010 J.HELING    INITIAL WRITE                    00160005
      ******************************************************************00170005
                                                                        00180005
       ENVIRONMENT DIVISION.                                            00190005
       CONFIGURATION SECTION.                                           00200005
       SOURCE-COMPUTER. IBM-3090.                                       00210005
       OBJECT-COMPUTER. IBM-3090.                                       00220005
       INPUT-OUTPUT SECTION.                                            00230005
       FILE-CONTROL.                                                    00240005
                                                                        00250005
           SELECT STUP-INPUT                                            00260005
               ASSIGN TO INP01.                                         00270005
                                                                        00280005
           SELECT STUP-CONTROL                                          00290005
               ASSIGN TO INP02.                                         00300005
                                                                        00310005
           SELECT STUP-OUTPUT                                           00320005
               ASSIGN TO OUT01.                                         00330005
                                                                        00340005
       DATA DIVISION.                                                   00350005
                                                                        00360005
       FILE SECTION.                                                    00370005
                                                                        00380005
       FD  STUP-INPUT                                                   00390005
           RECORDING MODE IS F                                          00400005
           LABEL RECORDS ARE STANDARD.                                  00410005
       01  STUP-INPUT-RECORD            PIC X(109).                     00420005
                                                                        00430005
       FD  STUP-CONTROL                                                 00440005
           RECORDING MODE IS F                                          00450005
           LABEL RECORDS ARE STANDARD.                                  00460005
       01  STUP-CONTROL-RECORD.                                         00470005
           05  SCR-CNT-STUP-ID          PIC S9(09) COMP.                00480005
           05  SCR-REMAINDER-RECORD     PIC X(76).                      00490005
                                                                        00500005
       FD  STUP-OUTPUT                                                  00510005
           RECORDING MODE IS F                                          00520005
           LABEL RECORDS ARE STANDARD.                                  00530005
       01  STUP-OUTPUT-RECORD           PIC X(113).                     00540005
                                                                        00550005
      ******************************************************************00560005
       WORKING-STORAGE SECTION.                                         00570005
      ******************************************************************00580005
       01  PC-PROGRAM-CONSTANTS.                                        00590005
           05  PC-PROGRAM-NAME               PIC X(08) VALUE 'ICKUT012'.00600005
                                                                        00610005
       01  PV-PROGRAM-VARIABLES.                                        00620005
           05  PV-CONTROL-RECS                PIC 9(15)   VALUE ZERO.   00630005
           05  PV-INPUT-RECS                  PIC 9(15)   VALUE ZERO.   00640005
           05  PV-OUTPUT-RECS                 PIC 9(15)   VALUE ZERO.   00650005
           05  PV-PARA-NAME                   PIC X(30)   VALUE ZERO.   00660005
                                                                        00670005
       01  PS-PROGRAM-SWITCHES.                                         00680005
           05  PS-STUP-CONTROL-SW              PIC X(01)   VALUE 'N'.   00690005
               88  PS-STUP-CONTROL-EOF                     VALUE 'Y'.   00700005
           05  PS-STUP-INPUT-SW                PIC X(01)   VALUE 'N'.   00710005
               88  PS-STUP-INPUT-EOF                       VALUE 'Y'.   00720005
                                                                        00730005
       01  ABEND-CODE                          PIC S9(04)  VALUE +0     00740005
                                                           COMP SYNC.   00750005
                                                                        00760005
      ******************************************************************00770007
           COPY ICRD150.                                                00780005
                                                                        00790005
      ******************************************************************00800007
           COPY ICRD070.                                                00810005
                                                                        00820005
      ******************************************************************00830005
      * WORKING-STORAGE FOR CALENDAR ROUTINE                            00840005
      ******************************************************************00850005
           COPY DPWS500.                                                00860005
                                                                        00870005
      ******************************************************************00880005
      *FLOW:  THE PROGRAM IS DESIGNED TO FORMAT THE ICT_CNT_STUP        00890005
      *       UNLOAD FROM THE USA INVENTORY COUNTS SYSTEM TO EDW        00900005
      *       LOAD STANDARDS.                                           00910005
      *                                                                 00920005
      ******************************************************************00930005
      ******************************************************************00940005
      ******************************************************************00950005
       PROCEDURE DIVISION.                                              00960005
                                                                        00970005
       0000-MAINLINE.                                                   00980005
                                                                        00990005
           PERFORM 1000-INITIALIZE.                                     01000005
                                                                        01010005
           PERFORM 2000-MAIN-PROCESS                                    01020005
               UNTIL PS-STUP-CONTROL-EOF.                               01030005
                                                                        01040005
           PERFORM 9000-END-OF-JOB-ROUTINE.                             01050005
                                                                        01060005
           STOP RUN.                                                    01070005
                                                                        01080005
      ******************************************************************01090005
       1000-INITIALIZE.                                                 01100005
      ******************************************************************01110005
           DISPLAY '   '.                                               01120005
           DISPLAY '****************************************'.          01130005
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                      01140005
                                                                        01150005
           OPEN INPUT  STUP-INPUT                                       01160005
                INPUT  STUP-CONTROL                                     01170005
               OUTPUT  STUP-OUTPUT.                                     01180005
                                                                        01190005
           PERFORM 6000-READ-STUP-CONTROL.                              01200005
                                                                        01210005
           IF (NOT PS-STUP-CONTROL-EOF)                                 01220005
               PERFORM 6100-READ-STUP-INPUT                             01230005
           END-IF.                                                      01240005
                                                                        01250005
      ***************************************************************** 01260005
       2000-MAIN-PROCESS.                                               01270005
      ******************************************************************01280005
           IF SCR-CNT-STUP-ID EQUAL ICRD150-CNT-STUP-ID                 01290005
               PERFORM 3000-FORMAT                                      01300005
               PERFORM 6100-READ-STUP-INPUT                             01310005
           ELSE                                                         01320005
           IF SCR-CNT-STUP-ID GREATER ICRD150-CNT-STUP-ID               01330005
               PERFORM 6100-READ-STUP-INPUT                             01340005
           ELSE                                                         01350005
               PERFORM 6000-READ-STUP-CONTROL                           01360005
           END-IF.                                                      01370005
                                                                        01380005
      ***************************************************************** 01390005
       3000-FORMAT.                                                     01400005
      ******************************************************************01410005
           INITIALIZE IC070-STUP-OUTPUT-RECORD.                         01420006
                                                                        01430005
           MOVE ICRD150-CNT-STUP-ID     TO IC070-CNT-STUP-ID.           01440006
           MOVE ICRD150-LAST-UPD-TMST   TO IC070-LAST-UPD-TMST.         01450006
           MOVE ICRD150-LAST-UPD-USR-ID TO IC070-LAST-UPD-USR-ID.       01460006
                                                                        01470005
           IF SUGG-EXEC-DTE-IS-NULL                                     01480005
               MOVE '9999-09-09'          TO IC070-SUGG-EXEC-DTE        01490006
           ELSE                                                         01500005
               MOVE ICRD150-SUGG-EXEC-DTE TO IC070-SUGG-EXEC-DTE        01510006
           END-IF.                                                      01520005
                                                                        01530005
           IF EXEC-WND-STRT-DTE-IS-NULL                                 01540005
             MOVE '9999-09-09'              TO IC070-EXEC-WND-STRT-DTE  01550006
           ELSE                                                         01560005
             MOVE ICRD150-EXEC-WND-STRT-DTE TO IC070-EXEC-WND-STRT-DTE  01570006
           END-IF.                                                      01580005
                                                                        01590005
           IF EXEC-WND-END-DTE-IS-NULL                                  01600005
               MOVE '9999-09-09'             TO IC070-EXEC-WND-END-DTE  01610006
           ELSE                                                         01620005
               MOVE ICRD150-EXEC-WND-END-DTE TO IC070-EXEC-WND-END-DTE  01630006
           END-IF.                                                      01640005
                                                                        01650005
           MOVE ICRD150-CNT-DESC        TO IC070-CNT-DESC.              01660006
           MOVE ICRD150-EXEC-LVL-CDE    TO IC070-EXEC-LVL-CDE.          01670006
           MOVE ICRD150-EXCL-MDSE-IND   TO IC070-EXCL-MDSE-IND.         01680006
           MOVE ICRD150-FS-UPD-IND      TO IC070-FS-UPD-IND.            01690006
           MOVE ICRD150-CNT-SCHD-BY-CDE TO IC070-CNT-SCHD-BY-CDE.       01700006
                                                                        01710005
           PERFORM 5000-WRITE-STUP-OUTPUT.                              01720005
                                                                        01730005
      ******************************************************************01740005
       5000-WRITE-STUP-OUTPUT.                                          01750005
      ******************************************************************01760005
                                                                        01770005
           WRITE STUP-OUTPUT-RECORD                                     01780005
                 FROM IC070-STUP-OUTPUT-RECORD.                         01790006
                                                                        01800005
           ADD +1 TO PV-OUTPUT-RECS.                                    01810005
                                                                        01820005
      ******************************************************************01830005
       6000-READ-STUP-CONTROL.                                          01840005
      ******************************************************************01850005
           READ STUP-CONTROL                                            01860005
                 AT END SET PS-STUP-CONTROL-EOF TO TRUE                 01870005
                 NOT AT END ADD +1 TO PV-CONTROL-RECS                   01880005
           END-READ.                                                    01890005
                                                                        01900005
      ******************************************************************01910005
       6100-READ-STUP-INPUT.                                            01920005
      ******************************************************************01930005
           READ STUP-INPUT                                              01940005
               INTO ICRD150-IC-COUNTS-FILE                              01950005
                 AT END SET PS-STUP-INPUT-EOF   TO TRUE                 01960005
                        SET PS-STUP-CONTROL-EOF TO TRUE                 01970005
                 NOT AT END ADD +1 TO PV-INPUT-RECS                     01980005
           END-READ.                                                    01990005
                                                                        02000005
      ******************************************************************02010005
       9000-END-OF-JOB-ROUTINE.                                         02020005
      ******************************************************************02030005
                                                                        02040005
           CLOSE STUP-INPUT                                             02050005
                 STUP-CONTROL                                           02060005
                 STUP-OUTPUT.                                           02070005
                                                                        02080005
           DISPLAY '****************************************'.          02090005
           DISPLAY '*** SUMMARY FOR '             PC-PROGRAM-NAME.      02100005
           DISPLAY '*** CONTROL RECORD READ     = ' PV-CONTROL-RECS.    02110005
           DISPLAY '*** INPUT RECORDS READ      = ' PV-INPUT-RECS.      02120005
           DISPLAY '*** OUTPUT RECORDS WRITTEN  = ' PV-OUTPUT-RECS.     02130005
           DISPLAY '****************************************'.          02140005
                                                                        02150005
      ***************************************************************** 02160005
      ***************************************************************** 02170005
