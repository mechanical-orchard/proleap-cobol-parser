00001  ID DIVISION.                                                     07/16/93
00002                                                                   SNKUP019
00003  PROGRAM-ID.    SNKUP019.                                            LV004
00004                                                                   SNKUP019
00005  AUTHOR.        ANNE YEKO.                                        SNKUP019
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          SNKUP019
00007  DATE-WRITTEN.  NOVEMBER, 1992.                                   SNKUP019
00008  DATE-COMPILED.                                                   SNKUP019
00009                                                                   SNKUP019
00010 ******************************************************************SNKUP019
00011 *   THIS PROGRAM WILL READ AN INPUT FILE CREATED IN EITHER      **SNKUP019
00012 *   SNKUP007 OR SNKUP008.  THE KEY INFORMATION FROM THE INPUT   **SNKUP019
00013 *   FILE IS USED TO DELETE ROWS FROM THE ASNORD TABLE.          **SNKUP019
00014 ******************************************************************SNKUP019
00015 * CHANGED __/__/__ BY ____________                              **SNKUP019
00016 *   P_________  ________________________________________________**SNKUP019
00017 *               ________________________________________________**SNKUP019
00018 *                                                               **SNKUP019
00019 ******************************************************************SNKUP019
00020                                                                   SNKUP019
00021      EJECT                                                        SNKUP019
00022                                                                   SNKUP019
00023  ENVIRONMENT DIVISION.                                            SNKUP019
00024                                                                   SNKUP019
00025  INPUT-OUTPUT SECTION.                                            SNKUP019
00026                                                                   SNKUP019
00027  FILE-CONTROL.                                                    SNKUP019
00028                                                                   SNKUP019
00029      SELECT ASNHDR-DELETE-FILE                                    SNKUP019
00030             ASSIGN TO INP01.                                      SNKUP019
00031                                                                   SNKUP019
00032  DATA DIVISION.                                                   SNKUP019
00033                                                                   SNKUP019
00034  FILE SECTION.                                                    SNKUP019
00035                                                                   SNKUP019
00036  FD  ASNHDR-DELETE-FILE                                           SNKUP019
00037      RECORDING MODE IS F                                          SNKUP019
00038      LABEL RECORDS ARE STANDARD                                   SNKUP019
00039      RECORD CONTAINS 05 CHARACTERS                                   CL**3
00040      BLOCK CONTAINS 0 RECORDS.                                    SNKUP019
00041                                                                   SNKUP019
00042  01  ASNHDR-DELETE-RECORD        PIC X(05).                          CL**3
00043                                                                   SNKUP019
00044      EJECT                                                        SNKUP019
00045  WORKING-STORAGE SECTION.                                         SNKUP019
00046                                                                   SNKUP019
00047  01  FILLER.                                                      SNKUP019
00048      05  FILLER                  PIC  X(36)  VALUE                SNKUP019
00049          ' ***** WORKING STORAGE PGM=SNKUP019 '.                  SNKUP019
00050      05  PGM-COMPILED            PIC  X(20)  VALUE SPACE.         SNKUP019
00051      05  FILLER                  PIC  X(10)  VALUE                SNKUP019
00052          ' LOCATION='.                                            SNKUP019
00053      05  PGM-LOCATION            PIC  X(04)  VALUE SPACE.         SNKUP019
00054      05  FILLER                  PIC  X(06)  VALUE                SNKUP019
00055          ' *****'.                                                SNKUP019
00056                                                                   SNKUP019
00057  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO           SNKUP019
00058                                              COMP SYNC.           SNKUP019
00059                                                                   SNKUP019
00060  01  ABEND-PARAGRAPH             PIC  X(30)  VALUE SPACE.         SNKUP019
00061                                                                   SNKUP019
00062      EJECT                                                        SNKUP019
00063  01  PC-PROGRAM-CONSTANTS.                                        SNKUP019
00064      05  PC-Y                    PIC  X(01)  VALUE 'Y'.           SNKUP019
00065      05  PC-N                    PIC  X(01)  VALUE 'N'.           SNKUP019
00066                                                                   SNKUP019
00067  01  PS-PROGRAM-SWITCHES.                                         SNKUP019
00068      05  PS-END-OF-INPUT-SW      PIC  X(01)  VALUE 'N'.           SNKUP019
00069          88 PS-END-OF-INPUT                  VALUE 'Y'.           SNKUP019
00070                                                                   SNKUP019
00071  01  PS-PROGRAM-VARIABLES.                                        SNKUP019
00072      05  PV-RECS-READ            PIC  S9(7)  VALUE +0             SNKUP019
00073                                              COMP-3.              SNKUP019
00074      05  PV-RECS-DELETED         PIC  S9(7)  VALUE +0             SNKUP019
00075                                              COMP-3.              SNKUP019
00076      05  PV-DELETE-COUNTER       PIC  S9(3)  VALUE +0             SNKUP019
00077                                              COMP-3.              SNKUP019
00078                                                                   SNKUP019
00079 *    ASNHDR DELETE RECORD LAYOUT                                  SNKUP019
00080                                                                   SNKUP019
00081      COPY SNRD002I.                                               SNKUP019
00082      EJECT                                                        SNKUP019
00083 ******************************************************************SNKUP019
00084 *    DB2 DATABASE AREA                                            SNKUP019
00085 ******************************************************************SNKUP019
00086                                                                   SNKUP019
00087 *    ASN ORDER                                                    SNKUP019
00088                                                                   SNKUP019
00089      EXEC SQL                                                     SNKUP019
00090          INCLUDE TASNORD                                          SNKUP019
00091      END-EXEC.                                                    SNKUP019
00092                                                                   SNKUP019
00093      EJECT                                                        SNKUP019
00094 *    SQL COMMUNICATION AREA                                       SNKUP019
00095                                                                   SNKUP019
00096      EXEC SQL                                                     SNKUP019
00097          INCLUDE SQLCA                                            SNKUP019
00098      END-EXEC.                                                    SNKUP019
00099      EJECT                                                        SNKUP019
00100 *    SQL COMMUNICATION AREA 2                                     SNKUP019
00101                                                                   SNKUP019
00102      COPY SQLCA2.                                                 SNKUP019
00103      EJECT                                                        SNKUP019
00104                                                                   SNKUP019
00105 ***************************************************************** SNKUP019
00106  PROCEDURE DIVISION.                                              SNKUP019
00107 ***************************************************************** SNKUP019
00108                                                                   SNKUP019
00109  A000-MAIN-DRIVER.                                                SNKUP019
00110                                                                   SNKUP019
00111      MOVE 'A000'                 TO PGM-LOCATION.                 SNKUP019
00112      MOVE WHEN-COMPILED          TO PGM-COMPILED.                 SNKUP019
00113                                                                   SNKUP019
00114      OPEN INPUT ASNHDR-DELETE-FILE.                               SNKUP019
00115                                                                   SNKUP019
00116      PERFORM B200-READ-INPUT-FILE.                                SNKUP019
00117                                                                   SNKUP019
00118      PERFORM B100-DELETE-ROWS                                     SNKUP019
00119        UNTIL PS-END-OF-INPUT.                                     SNKUP019
00120                                                                   SNKUP019
00121      DISPLAY 'SNKUP019 RECORDS READ:    ' PV-RECS-READ.           SNKUP019
00122      DISPLAY 'SNKUP019 RECORDS DELETED: ' PV-RECS-DELETED.        SNKUP019
00123                                                                   SNKUP019
00124      CLOSE ASNHDR-DELETE-FILE.                                    SNKUP019
00125                                                                   SNKUP019
00126      GOBACK.                                                      SNKUP019
00127                                                                   SNKUP019
00128      EJECT                                                        SNKUP019
00129                                                                   SNKUP019
00130 ***************************************************************** SNKUP019
00131 *    THIS PARAGRAPH CONTROLS THE MAIN PROCESSING OF THE PROGRAM   SNKUP019
00132 ***************************************************************** SNKUP019
00133  B100-DELETE-ROWS.                                                SNKUP019
00134                                                                   SNKUP019
00135      MOVE 'B100'                 TO PGM-LOCATION.                 SNKUP019
00136                                                                      CL**4
00137      MOVE 'B100-DELETE-ROWS          '                            SNKUP019
00138                                  TO ABEND-PARAGRAPH.              SNKUP019
00139                                                                   SNKUP019
00140      PERFORM B300-DELETE-ASNORD.                                  SNKUP019
00141      IF PV-DELETE-COUNTER > +500                                     CL**2
00142          PERFORM B400-COMMIT                                      SNKUP019
00143          MOVE ZERO               TO PV-DELETE-COUNTER             SNKUP019
00144      END-IF.                                                      SNKUP019
00145      PERFORM B200-READ-INPUT-FILE.                                SNKUP019
00146                                                                   SNKUP019
00147      EJECT                                                        SNKUP019
00148                                                                   SNKUP019
00149 ***************************************************************** SNKUP019
00150 *    THIS PARAGRAPH WILL READ THE NEXT ASN TO BE DELETED.         SNKUP019
00151 ***************************************************************** SNKUP019
00152  B200-READ-INPUT-FILE.                                            SNKUP019
00153                                                                   SNKUP019
00154      MOVE 'B200'                 TO PGM-LOCATION.                 SNKUP019
00155                                                                      CL**4
00156      MOVE 'B200-READ-INPUT-FILE      '                            SNKUP019
00157                                  TO ABEND-PARAGRAPH.              SNKUP019
00158                                                                   SNKUP019
00159      READ ASNHDR-DELETE-FILE                                      SNKUP019
00160          INTO SN002I-ASN-DELETE-RECORD                            SNKUP019
00161          AT END                                                   SNKUP019
00162              SET PS-END-OF-INPUT TO TRUE.                         SNKUP019
00163                                                                   SNKUP019
00164                                                                   SNKUP019
00165      IF NOT PS-END-OF-INPUT                                       SNKUP019
00166 *        DISPLAY SN002I-ASN-DELETE-RECORD                            CL**4
00167          ADD 1                   TO PV-RECS-READ                  SNKUP019
00168      END-IF.                                                      SNKUP019
00169                                                                   SNKUP019
00170      EJECT                                                        SNKUP019
00171                                                                   SNKUP019
00172 ***************************************************************** SNKUP019
00173 *    THIS PARAGRAPH DELETES ALL ASNORD ROWS THAT ARE EQUAL TO     SNKUP019
00174 *    THE CURRENT INPUTED ASN.                                     SNKUP019
00175 ***************************************************************** SNKUP019
00176  B300-DELETE-ASNORD.                                              SNKUP019
00177                                                                   SNKUP019
00178      MOVE 'B300'                 TO PGM-LOCATION.                 SNKUP019
00179                                                                      CL**4
00180      MOVE 'B300-DELETE-ASNORD        '                            SNKUP019
00181                                  TO ABEND-PARAGRAPH.              SNKUP019
00182                                                                   SNKUP019
00183      EXEC SQL                                                     SNKUP019
00184          DELETE                                                   SNKUP019
00185          FROM   TASNORD                                           SNKUP019
00186          WHERE  ASNHDR_ID   = :SN002I-ASNHDR-ID                      CL**3
00187      END-EXEC.                                                    SNKUP019
00188                                                                   SNKUP019
00189      MOVE SQLCODE TO SQLCODE2.                                    SNKUP019
00190                                                                      CL**4
00191                                                                   SNKUP019
00192      IF SQLCODE = ZERO                                            SNKUP019
00193          ADD SQLERRD (3)         TO PV-RECS-DELETED               SNKUP019
00194          ADD +1                  TO PV-DELETE-COUNTER             SNKUP019
00195      ELSE                                                         SNKUP019
00196          IF SQLCODE = +100                                        SNKUP019
00197              CONTINUE                                             SNKUP019
00198          ELSE                                                     SNKUP019
00199              PERFORM Z998-SQL-ERROR                               SNKUP019
00200          END-IF                                                   SNKUP019
00201      END-IF.                                                      SNKUP019
00202                                                                   SNKUP019
00203      EJECT                                                        SNKUP019
00204                                                                   SNKUP019
00205 ***************************************************************** SNKUP019
00206 *    THIS PARAGRAPH WILL COMMIT THE ROWS JUST DELETED             SNKUP019
00207 ***************************************************************** SNKUP019
00208  B400-COMMIT.                                                     SNKUP019
00209                                                                   SNKUP019
00210      MOVE 'B400'                 TO PGM-LOCATION.                 SNKUP019
00211                                                                      CL**4
00212      MOVE 'B400-COMMIT               '                            SNKUP019
00213                                  TO ABEND-PARAGRAPH.              SNKUP019
00214                                                                   SNKUP019
00215      EXEC SQL                                                     SNKUP019
00216          COMMIT                                                   SNKUP019
00217      END-EXEC.                                                    SNKUP019
00218                                                                   SNKUP019
00219      MOVE SQLCODE TO SQLCODE2.                                    SNKUP019
00220                                                                   SNKUP019
00221      IF SQLCODE = ZERO                                            SNKUP019
00222          CONTINUE                                                 SNKUP019
00223      ELSE                                                         SNKUP019
00224          PERFORM Z998-SQL-ERROR                                   SNKUP019
00225      END-IF.                                                      SNKUP019
00226                                                                   SNKUP019
00227      EJECT                                                        SNKUP019
00228                                                                   SNKUP019
00229 ***************************************************************** SNKUP019
00230 *    THIS MODULE PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT  SNKUP019
00231 *    THAT AN SQL ERROR CODE OCCURS.                               SNKUP019
00232 ***************************************************************** SNKUP019
00233  Z998-SQL-ERROR.                                                  SNKUP019
00234                                                                   SNKUP019
00235      MOVE SQLCODE                TO SQLCODE2.                     SNKUP019
00236      MOVE SQLERRD(3)             TO SQLERRD3.                     SNKUP019
00237      DISPLAY '** ABEND **         ** = SQLERROR'.                 SNKUP019
00238      DISPLAY '** PROGRAM          ** = SNKUP019'.                 SNKUP019
00239      DISPLAY '** PARAGRAPH        ** = ' ABEND-PARAGRAPH.         SNKUP019
00240      DISPLAY '** SQLCODE          ** = ' SQLCODE2.                SNKUP019
00241      DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                SNKUP019
00242      DISPLAY '** SQLERRM          ** = ' SQLERRM.                 SNKUP019
00243      DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                SNKUP019
00244      MOVE +4013                  TO ABEND-CODE.                   SNKUP019
00245      CALL 'ILBOABN0'             USING ABEND-CODE.                SNKUP019
