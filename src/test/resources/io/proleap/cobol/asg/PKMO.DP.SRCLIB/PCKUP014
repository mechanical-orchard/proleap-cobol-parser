000100 IDENTIFICATION DIVISION.                                                 
000200 TITLE 'INTRANSIT DETAIL DELETES'.                                        
000300                                                                          
000400 PROGRAM-ID.             PCKUP014.                                        
000500 AUTHOR.                 DENNIS STEFFEN.                                  
000600 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000700 DATE-WRITTEN            WINTER 2005.                                     
000800 DATE-COMPILED.                                                           
000900                                                                          
001000 ENVIRONMENT DIVISION.                                                    
001100 CONFIGURATION SECTION.                                                   
001200                                                                          
001300 SOURCE-COMPUTER.        IBM-3090.                                        
001400 OBJECT-COMPUTER.        IBM-3090.                                        
001500                                                                          
001600 INPUT-OUTPUT SECTION.                                                    
001700                                                                          
001800 FILE-CONTROL.                                                            
001900                                                                          
002000     SELECT IN-TRANSIT-EXTRACT-FILE                                       
002100         ASSIGN TO INP01.                                                 
002200                                                                          
002300 DATA DIVISION.                                                           
002400 FILE SECTION.                                                            
002500                                                                          
002600 FD  IN-TRANSIT-EXTRACT-FILE                                              
002700     RECORDING MODE IS F                                                  
002800     LABEL RECORDS ARE STANDARD                                           
002900     BLOCK CONTAINS 0 RECORDS.                                            
003000                                                                          
003100 01  IN-TRANSIT-RECORD    PIC  X(026).                                    
003200                                                                          
003300 WORKING-STORAGE SECTION.                                                 
003400                                                                          
003500 01  FILLER.                                                              
003600     05  FILLER                      PIC  X(36)    VALUE                  
003700         ' ***** WORKING STORAGE PGM=PCKUP014 '.                          
003800     05  PGM-COMPILED                PIC  X(20)    VALUE SPACE.           
003900     05  FILLER                      PIC  X(10)    VALUE                  
004000         ' LOCATION='.                                                    
004100     05  PGM-LOCATION                PIC  X(04)    VALUE SPACE.           
004200     05  FILLER                      PIC  X(06)    VALUE                  
004300         ' *****'.                                                        
004400                                                                          
004500 01  PV-CUTOFF-DATE-REC.                                                  
004600     05  PV-CUTOFF-DATE               PIC X(10).                          
004700     05  FILLER                       PIC X(70).                          
004800                                                                          
004900 01  PV-DISP-KEY-MSG.                                                     
005000     05  FILLER                      PIC X(14) VALUE                      
005100         'INTR-UPC-ID = '.                                                
005200     05  PV-KEY-INTR-UPC-ID          PIC 9(09) VALUE ZEROES.              
005300     05  FILLER                      PIC X(12) VALUE                      
005400         '; LOC-NBR = '.                                                  
005500     05  PV-KEY-LOC-NBR              PIC 9(04) VALUE ZEROES.              
005600     05  FILLER                      PIC X(13) VALUE                      
005700         '; CLSN-CDE = '.                                                 
005800     05  PV-KEY-CLSN-CDE             PIC X(02) VALUE ZEROES.              
005900                                                                          
006000 01  PS-SWITCHES.                                                         
006100     05  PS-EOF-SW                    PIC X(01) VALUE 'N'.                
006200         88  PS-EOF-EXTRACT-YES                 VALUE 'Y'.                
006300         88  PS-EOF-EXTRACT-NO                  VALUE 'N'.                
006400     05  PS-COMMIT-SW                 PIC X(01) VALUE SPACE.              
006500         88  PS-COMMIT-NOW                      VALUE 'Y'.                
006600         88  PS-AINT-TIME-YET-TO-COMMIT         VALUE 'N'.                
006700                                                                          
006800 01  PC-CONSTANTS.                                                        
006900     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
007000                                                   'PCKUP014'.            
007100     05  PV-CKPT-JOB-NAME            PIC X(08)   VALUE 'PCK115'.          
007200     05  PC-ILBOABN0                 PIC  X(08)    VALUE                  
007300                                                   'ILBOABN0'.            
007400     05  PC-MAX-RETRIES              PIC S9(04)    VALUE  +3              
007500                                                   COMP.                  
007600     05  PC-OPEN-FUNC                PIC X(05)     VALUE 'OPEN '.         
007700     05  PC-TRANS-FUNC               PIC X(05)     VALUE 'TRANS'.         
007800     05  PC-CLOSE-FUNC               PIC X(05)     VALUE 'CLOSE'.         
007900     05  PC-RSTRT-FUNC               PIC X(05)     VALUE 'RSTRT'.         
008000     05  PC-CKPT-PLAN-NAME           PIC X(08)   VALUE 'PCKUP014'.        
008100                                                                          
008200 01  PV-MISC-STUFF.                                                       
008300     05  PV-TMST                      PIC X(26).                          
008400     05  PV-HOLD-TMST                 PIC X(26).                          
008500     05  PV-ABEND-PARAGRAPH           PIC  X(30) VALUE SPACES.            
008600     05  PV-DB2-OPERATION-ATTEMPTED   PIC  X(60) VALUE SPACE.             
008700     05  PV-SQL-CODE                  PIC  9(04) VALUE ZERO.              
008800     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
008900                                                   COMP SYNC.             
009000     05  PV-RETRY-COUNTER             PIC S9(04) VALUE ZERO               
009100                                                 COMP SYNC.               
009200     05  PV-READ-COUNTER              PIC S9(09) COMP-3                   
009300                                              VALUE ZERO.                 
009600     05 PV-DELETE-COUNTER             PIC S9(09) COMP-3                   
009700                                              VALUE ZERO.                 
009800     05 PV-DELETE-BAL-COUNTER         PIC S9(09) COMP-3                   
009900                                              VALUE ZERO.                 
010000     05 PV-BYPASS-COUNT               PIC S9(09) COMP-3                   
010100                                              VALUE ZERO.                 
010200     05  PV-DISP-NBR                  PIC ZZZ,ZZZ,ZZ9.                    
010300*    05  PV-PREV-COUNT                PIC S9(09) COMP-3                   
010400*                                             VALUE ZERO.                 
010500*    05  PV-DIFF-COUNT                PIC S9(09) COMP-3                   
010600*                                             VALUE ZERO.                 
010700     05  PV-ABEND-OPERATION           PIC X(20) VALUE SPACES.             
010800     05  PV-ABEND-STRUCTURE-1         PIC X(20) VALUE SPACES.             
010900                                                                          
011000 01  ABEND-CODE                       PIC S9(04)     VALUE +0             
011100                                                     COMP SYNC.           
011200     88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
011300     88  ABEND-4002-READ-ERROR                       VALUE +4002.         
011400     88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
011500     88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
011600     88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
011700     88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
011800     88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
011900     88  ABEND-4008-DATS-ERROR                       VALUE +4008.         
012000     88  ABEND-4009-KEY-DATS-ERROR                   VALUE +4009.         
012100     88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
012200                                                                          
012300                                                                          
012400*    IN-TRANSIT EXTRACT RECORD LAYOUT                                     
012500     COPY PCRD033.                                                        
012600/                                                                         
012700* TRANSACTION PRESENTATION MODULE PARAMETERS                              
260107     COPY DPWS991.                                                        
012800                                                                          
012800     COPY DPLS001.                                                        
012900         05  WORK-STRG-AREA          PIC X(022).                          
013000/                                                                         
013100*  IN-TRANSIT BALANCE                                                     
013200     EXEC SQL                                                             
013300         INCLUDE PCT00014                                                 
013400     END-EXEC.                                                            
013500/                                                                         
013600*  IN-TRANSIT DETAIL                                                      
013700     EXEC SQL                                                             
013800         INCLUDE PCT00015                                                 
013900     END-EXEC.                                                            
014000                                                                          
014100*  PARM TABLE                                                             
014200     EXEC SQL                                                             
014300         INCLUDE TKRPRPM                                                  
014400     END-EXEC.                                                            
014500                                                                          
014600     EXEC SQL                                                             
014700         INCLUDE SQLCA                                                    
014800     END-EXEC.                                                            
014900                                                                          
015000     COPY SQLCA2.                                                         
015100                                                                          
015200     COPY DPWS004.                                                        
015300/                                                                         
015400 PROCEDURE DIVISION.                                                      
015500                                                                          
015600     PERFORM 1000-INITIALIZATION.                                         
015700                                                                          
015800     PERFORM 2000-PROCESS UNTIL PS-EOF-EXTRACT-YES                        
015900                                                                          
016000     PERFORM 9000-QUIT.                                                   
016100                                                                          
016200     GOBACK.                                                              
016300/                                                                         
016400 1000-INITIALIZATION.                                                     
016500     MOVE '1000-INITIALIZATION         '  TO PV-ABEND-PARAGRAPH.          
016600                                                                          
016700     ACCEPT PV-CKPT-JOB-NAME FROM SYSIN.                                  
016800                                                                          
016900*    OPEN THE EXTRACT FILE                                                
017000     PERFORM 8000-OPEN-EXTRACT.                                           
017100                                                                          
017200*    PERFORM 8100-READ-EXTRACT.                                           
017300                                                                          
017400     IF DP001-PREP-TRANS-EOF                                              
017500         SET PS-EOF-EXTRACT-YES       TO TRUE                             
017600         DISPLAY SPACES                                                   
017700         DISPLAY ALL '*'                                                  
017800         DISPLAY '  INPUT FILE IS EMPTY '                                 
017900         DISPLAY ALL '*'                                                  
018000         DISPLAY SPACES                                                   
018100     ELSE                                                                 
018200         MOVE DP001-TRANS-RECORD      TO                                  
018300                                  PC033-INTRANSIT-DTL-PURGE-REC           
018400                                         WORK-STRG-AREA                   
018500     END-IF.                                                              
018600/                                                                         
018700 2000-PROCESS.                                                            
018800                                                                          
018900     MOVE '2000-PROCESS'                  TO PV-ABEND-PARAGRAPH.          
019000                                                                          
019100     PERFORM 7000-DELETE-INTRANSIT-DETAIL                                 
019200     IF SQLCODE = ZERO                                                    
019300         PERFORM 7100-DELETE-INTRANSIT-BALANCE                            
019400     END-IF                                                               
019500                                                                          
019600     PERFORM 8100-READ-EXTRACT.                                           
019700/                                                                         
019800 7000-DELETE-INTRANSIT-DETAIL.                                            
019900                                                                          
020000     MOVE '7000-DELETE-INTRANSIT-DETAIL'  TO PV-ABEND-PARAGRAPH.          
020100                                                                          
020200     MOVE PC033-INTR-UPC-ID           TO PCT00015-INTR-UPC-ID             
020300     MOVE PC033-LOC-NBR               TO PCT00015-LOC-NBR                 
020400     MOVE PC033-PO-NBR                TO PCT00015-PO-NBR                  
020500     MOVE PC033-RCVR-SEQ-NBR          TO PCT00015-RCVR-SEQ-NBR            
020600     MOVE PC033-OTBND-TRIP-ID         TO PCT00015-OTBND-TRIP-ID           
020700                                                                          
020800                                                                          
020900     PERFORM                                                              
021000         WITH TEST AFTER                                                  
021100         VARYING PV-RETRY-COUNTER                                         
021200         FROM 1 BY 1                                                      
021300         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
021400            OR  (SQLCODE = +100)                                          
021500            OR  (SQLCODE = +0))                                           
021600                                                                          
021700         EXEC SQL                                                         
021800             DELETE                                                       
021900              FROM  PCT_TRN_CLSN_DTL                                      
022000             WHERE                                                        
022100                   INTR_UPC_ID   = :PCT00015-INTR-UPC-ID                  
022200               AND LOC_NBR       = :PCT00015-LOC-NBR                      
022300               AND TRN_CLSN_CDE  = 'IT'                                   
022400               AND                                                        
022500                  (PO_NBR        = :PCT00015-PO-NBR                       
022600                     OR PO_NBR IS NULL)                                   
022700               AND                                                        
022800                  (RCVR_SEQ_NBR  = :PCT00015-RCVR-SEQ-NBR                 
022900                     OR RCVR_SEQ_NBR IS NULL)                             
023000               AND                                                        
023100                  (OTBND_TRIP_ID = :PCT00015-OTBND-TRIP-ID                
023200                     OR OTBND_TRIP_ID IS NULL)                            
023300         END-EXEC                                                         
023400                                                                          
023500         EVALUATE TRUE                                                    
023600             WHEN SQLCODE = ZERO                                          
023700                 ADD SQLERRD(3)       TO PV-DELETE-COUNTER                
023800*                IF SQLERRD(3) NOT EQUAL 2                                
023900*                    DISPLAY ' UPC= '    PCT00015-INTR-UPC-ID             
024000*                       '/ LOC= '       PCT00015-LOC-NBR                  
024100*                       '/ PO= '        PCT00015-PO-NBR                   
024200*                       '/ RCVR= '      PCT00015-RCVR-SEQ-NBR             
024300*                       '/ TRIP= '      PCT00015-OTBND-TRIP-ID            
024400*                       ' ROWS DELETED= ' SQLERRD(3)                      
024500*                END-IF                                                   
024600             WHEN SQLCODE = +100                                          
024700                 ADD 1                TO PV-BYPASS-COUNT                  
024800*                DISPLAY ' UPC= '    PCT00015-INTR-UPC-ID                 
024900*                   '/ LOC= '       PCT00015-LOC-NBR                      
025000*                   '/ PO= '        PCT00015-PO-NBR                       
025100*                   '/ RCVR= '      PCT00015-RCVR-SEQ-NBR                 
025200*                   '/ TRIP= '      PCT00015-OTBND-TRIP-ID                
025300*                   ' / ROW NOT FOUND '                                   
025400             WHEN SQLCODE = -904                                          
025500             WHEN SQLCODE = -911                                          
025600             WHEN SQLCODE = -913                                          
025700                  CONTINUE                                                
025800                                                                          
025900             WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
026000                 DISPLAY '**************************************'         
026100                 DISPLAY 'WARNING ISSUED '                                
026200                 DISPLAY 'DELETE PCT_TRN_CLSN_DTL               '         
026300                 DISPLAY '**************************************'         
026400                 PERFORM 9800-SQL-WARNING                                 
026500                                                                          
026600             WHEN SQLCODE < ZERO                                          
026700                 DISPLAY '**************************************'         
026800                 DISPLAY 'ERROR ISSUED ON                       '         
026900                 DISPLAY 'DELETE PCT_TRN_CLSN_DTL               '         
027000                 DISPLAY '**************************************'         
027100                 PERFORM 9900-SQL-ERROR                                   
027200                                                                          
027300         END-EVALUATE                                                     
027400     END-PERFORM.                                                         
027500                                                                          
027600     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
027700         SQLCODE NOT = ZERO                                               
027800         DISPLAY '**************************************'                 
027900         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
028000         DISPLAY 'DELETE PCT_TRN_CLSN_DTL'                                
028100         DISPLAY '**************************************'                 
028200         PERFORM 9900-SQL-ERROR                                           
028300     END-IF.                                                              
028400/                                                                         
028500 7100-DELETE-INTRANSIT-BALANCE.                                           
028600                                                                          
028700     MOVE '7100-DELETE-INTRANSIT-BALANCE' TO PV-ABEND-PARAGRAPH.          
028800                                                                          
028900     MOVE PC033-INTR-UPC-ID           TO PCT00014-INTR-UPC-ID             
029000     MOVE PC033-LOC-NBR               TO PCT00014-LOC-NBR                 
029100                                                                          
029200     PERFORM                                                              
029300         WITH TEST AFTER                                                  
029400         VARYING PV-RETRY-COUNTER                                         
029500         FROM 1 BY 1                                                      
029600         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
029700            OR  (SQLCODE = +100)                                          
029800            OR  (SQLCODE = +0))                                           
029900                                                                          
030000         EXEC SQL                                                         
030100             DELETE                                                       
030200              FROM  PCT_TRN_CLSN_BAL                                      
030300             WHERE                                                        
030400                   INTR_UPC_ID   = :PCT00014-INTR-UPC-ID                  
030500               AND LOC_NBR       = :PCT00014-LOC-NBR                      
030600               AND TRN_CLSN_CDE  = 'IT'                                   
030700               AND NOT EXISTS                                             
030800             (                                                            
030900               SELECT *                                                   
031000                 FROM PCT_TRN_CLSN_DTL                                    
031100             WHERE                                                        
031200                   INTR_UPC_ID   = :PCT00014-INTR-UPC-ID                  
031300               AND LOC_NBR       = :PCT00014-LOC-NBR                      
031400               AND TRN_CLSN_CDE  = 'IT'                                   
031500             )                                                            
031600         END-EXEC                                                         
031700                                                                          
031800         EVALUATE TRUE                                                    
031900             WHEN SQLCODE = ZERO                                          
032000                 ADD SQLERRD(3)       TO PV-DELETE-BAL-COUNTER            
032100                                                                          
032200*                    DISPLAY ' UPC= '    PCT00014-INTR-UPC-ID             
032300*                       '/ LOC= '       PCT00014-LOC-NBR                  
032400*                       ' BAL ROWS DELETED= ' SQLERRD(3)                  
032500                                                                          
032600             WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
032700*                ADD 1                TO PV-BYPASS-COUNT                  
032800*                DISPLAY ' UPC= '    PCT00014-INTR-UPC-ID                 
032900*                   '/ LOC= '       PCT00014-LOC-NBR                      
033000*                   ' / BAL ROW NOT FOUND '                               
033100             WHEN SQLCODE = -904                                          
033200             WHEN SQLCODE = -911                                          
033300             WHEN SQLCODE = -913                                          
033400                  CONTINUE                                                
033500                                                                          
033600             WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
033700                 DISPLAY '**************************************'         
033800                 DISPLAY 'WARNING ISSUED '                                
033900                 DISPLAY 'DELETE PCT_TRN_CLSN_DTL               '         
034000                 DISPLAY '**************************************'         
034100                 PERFORM 9800-SQL-WARNING                                 
034200                                                                          
034300             WHEN SQLCODE < ZERO                                          
034400                 DISPLAY '**************************************'         
034500                 DISPLAY 'ERROR ISSUED ON                       '         
034600                 DISPLAY 'DELETE PCT_TRN_CLSN_DTL               '         
034700                 DISPLAY '**************************************'         
034800                 PERFORM 9900-SQL-ERROR                                   
034900                                                                          
035000         END-EVALUATE                                                     
035100     END-PERFORM.                                                         
035200                                                                          
035300     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
035400         SQLCODE NOT = ZERO                                               
035500         DISPLAY '**************************************'                 
035600         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
035700         DISPLAY 'DELETE PCT_TRN_CLSN_DTL'                                
035800         DISPLAY '**************************************'                 
035900         PERFORM 9900-SQL-ERROR                                           
036000     END-IF.                                                              
036100/                                                                         
040300/                                                                         
040400 8000-OPEN-EXTRACT.                                                       
040500                                                                          
040600     MOVE PC-OPEN-FUNC           TO DP001-FUNC-CODE.                      
040700     MOVE '8000-OPEN-EXTRACT   '  TO PV-ABEND-PARAGRAPH.                  
040800*                                                                         
040900     MOVE LENGTH OF PC033-INTRANSIT-DTL-PURGE-REC                         
041000                                     TO DP001-WORK-STRG-LENGTH.           
041100     MOVE PV-CKPT-JOB-NAME           TO DP001-JOB-NAME.                   
041200     MOVE PC-CKPT-PLAN-NAME          TO DP001-PLAN-NAME.                  
041300                                                                          
041400*    THIS WILL ALSO READ THE FIRST RECORD!                                
041500     PERFORM Z900-CALL-TRANS-PRES-MODULE.                                 
041600                                                                          
041700 8100-READ-EXTRACT.                                                       
041800                                                                          
041900     MOVE PC-TRANS-FUNC          TO DP001-FUNC-CODE.                      
042000     MOVE '8100-READ-EXTRACT   '  TO PV-ABEND-PARAGRAPH.                  
042100*                                                                         
042200     MOVE LENGTH OF PC033-INTRANSIT-DTL-PURGE-REC                         
042300                                     TO DP001-WORK-STRG-LENGTH.           
042400     MOVE PV-CKPT-JOB-NAME           TO DP001-JOB-NAME.                   
042500     MOVE PC-CKPT-PLAN-NAME          TO DP001-PLAN-NAME.                  
042600                                                                          
042700     PERFORM Z900-CALL-TRANS-PRES-MODULE.                                 
042800                                                                          
042900     MOVE DP001-TRANS-RECORD          TO                                  
043000                                  PC033-INTRANSIT-DTL-PURGE-REC           
043100                                         WORK-STRG-AREA                   
043200                                                                          
043300     IF DP001-PREP-TRANS-EOF                                              
043400         SET PS-EOF-EXTRACT-YES       TO TRUE                             
043500     ELSE                                                                 
043600         ADD 1                        TO PV-READ-COUNTER                  
043700     END-IF.                                                              
043800                                                                          
043900 8200-CLOSE-EXTRACT.                                                      
044000                                                                          
044100     MOVE PC-CLOSE-FUNC               TO DP001-FUNC-CODE.                 
044200     MOVE '8200-CLOSE-EXTRACT   '     TO PV-ABEND-PARAGRAPH.              
044300*                                                                         
044400     MOVE PV-CKPT-JOB-NAME           TO DP001-JOB-NAME.                   
044500     MOVE PC-CKPT-PLAN-NAME          TO DP001-PLAN-NAME.                  
044600                                                                          
044700     PERFORM Z900-CALL-TRANS-PRES-MODULE.                                 
044800/                                                                         
044900 9000-QUIT.                                                               
045000                                                                          
045100     PERFORM 8200-CLOSE-EXTRACT.                                          
045200                                                                          
045300     MOVE PV-READ-COUNTER             TO PV-DISP-NBR                      
045400     DISPLAY 'RECORDS READ               = ' PV-DISP-NBR                  
045500                                                                          
045600     MOVE PV-DELETE-COUNTER           TO PV-DISP-NBR                      
045700     DISPLAY 'PCT_TRN_CLSN_DTL DELETES   = '  PV-DISP-NBR.                
045800                                                                          
045900     MOVE PV-DELETE-BAL-COUNTER       TO PV-DISP-NBR                      
046000     DISPLAY 'PCT_TRN_CLSN_BAL DELETES   = '  PV-DISP-NBR.                
046100                                                                          
046200     MOVE PV-BYPASS-COUNT             TO PV-DISP-NBR                      
046300     DISPLAY 'RECORDS BYPASSED           = '  PV-DISP-NBR.                
046400                                                                          
046700/                                                                         
046800 9100-SQL-ABEND.                                                          
046900******************************************************************        
047000*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
047100*  ERROR OR WARNING CODE OCCURS.                                          
047200******************************************************************        
047300     DISPLAY '9100-SQL-ABEND'.                                            
047400     DISPLAY '** ABEND     **'.                                           
047500     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
047600     DISPLAY '** PARAGRAPH ** = ' PV-ABEND-PARAGRAPH.                     
047700     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
047800     DISPLAY '** INTR-UPC-ID  = ' PC033-INTR-UPC-ID                       
047900     DISPLAY '** LOC-NBR      ** = ' PC033-LOC-NBR                        
048000     DISPLAY '**              ** = '                                      
048100     MOVE SQLCODE TO PV-SQL-CODE.                                         
048200     DISPLAY '** SQL CODE  ** = ' PV-SQL-CODE.                            
048300     MOVE +4013 TO PV-ABEND-CODE.                                         
048400     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
048500                                                                          
048600******************************************************************        
048700*  THIS MODULE PERFORMS THE SQL WARNING.                         *        
048800******************************************************************        
048900                                                                          
049000 9800-SQL-WARNING.                                                        
049100                                                                          
049200     MOVE SQLCODE               TO SQLCODE2.                              
049300     MOVE SQLERRD(3)            TO SQLERRD3.                              
049400     DISPLAY '** ABEND **       ** = SQLWARNING'.                         
049500     DISPLAY '** PROGRAM        ** = PCKUP014'.                           
049600     DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
049700     DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
049800     DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
049900                                                                          
050000     COPY DPPD004.                                                        
050100     SET ABEND-4013-DB2-ERROR   TO TRUE                                   
050200     CALL 'ILBOABN0' USING ABEND-CODE.                                    
050300/                                                                         
050400******************************************************************        
050500* THIS MODULE PERFORMS THE SQL ERROR.                            *        
050600******************************************************************        
050700                                                                          
050800 9900-SQL-ERROR.                                                          
050900                                                                          
051000     MOVE SQLCODE               TO SQLCODE2.                              
051100     MOVE SQLERRD(3)            TO SQLERRD3.                              
051200     DISPLAY '** ABEND **       ** = SQLERROR'.                           
051300     DISPLAY '** PROGRAM        ** = PCKUP014'.                           
051400     DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
051500     DISPLAY '** OPERATION      ** = ' PV-ABEND-OPERATION                 
051600     DISPLAY '** TABLE          ** = ' PV-ABEND-STRUCTURE-1               
051700     DISPLAY SPACES                                                       
051800                                                                          
051900     COPY DPPD004.                                                        
052000     SET ABEND-4013-DB2-ERROR   TO TRUE                                   
052100     CALL 'ILBOABN0' USING ABEND-CODE.                                    
052200/                                                                         
052300     COPY DPPD001.                                                        
