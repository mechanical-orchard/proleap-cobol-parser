       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    XSKUP019.                                         00020011
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  09/29/2002.                                       00050024
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070024
      **       VSM ITEM MESSAGE - NUMBER 100 - VENDOR UPC MOVE          00080024
      ******************************************************************00090024
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO MOVE A VENDOR UPC TO   00100031
      ***     A DIFFERENT SKU ASSOCIATION (CHANGE INTR_UPC_ID).         00110031
      ******************************************************************00120024
      **09/29/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00130024
121402**12/14/2002 CHANGED BY:JULIE SMITH  ADDITIONAL TIMESTAMP         00140029
      ******************************************************************00150024
       ENVIRONMENT DIVISION.                                            00160000
       CONFIGURATION SECTION.                                           00170000
       SOURCE-COMPUTER.  IBM-3090.                                      00180000
       OBJECT-COMPUTER.  IBM-3090.                                      00190000
       DATA DIVISION.                                                   00200000
                                                                        00210005
       WORKING-STORAGE SECTION.                                         00220000
      ******************************************************************00230005
      **PROGRAM SWITCHES                                                00240005
      ******************************************************************00250005
       01  PS-PROGRAM-SWITCHES.                                         00260005
           05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.        00270005
               88  PS-PROCESS-CONTINUE                VALUE 'Y'.        00280005
               88  PS-PROCESS-COMPLETE                VALUE 'N'.        00290005
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00300026
               88  PS-ACTION-REVERSE                  VALUE 01.         00310026
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00320026
               88  PS-ACTION-NOTHING                  VALUE 02.         00330026
      ******************************************************************00340000
      **PROGRAM VARIABLES                                               00350000
      ******************************************************************00360000
       01  PC-PROGRAM-COUNTERS.                                         00370007
           05  PC-RETRY-COUNTER            PIC 9(01)  VALUE ZEROS.      00380007
           05  PC-RETRY-MAX                PIC 9(01)  VALUE 5.          00390007
      ******************************************************************00400017
      **PROGRAM CALCULATIONS                                            00410017
      ******************************************************************00420017
       01  PC-PROGRAM-CALCULATIONS.                                     00430017
           05  PC-10-DIGIT                 PIC 9(10)  VALUE ZEROS.      00440017
           05  PC-18-DIGIT                 PIC S9(18) COMP VALUE ZEROS. 00450017
           05  PC-09-DIGIT                 PIC S9(09) COMP VALUE ZEROS. 00460017
      ******************************************************************00470000
      **PROGRAM VARIABLES                                               00480025
      ******************************************************************00490000
       01  PV-PROGRAM-VARIABLES.                                        00500025
           05  PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACE.      00510025
      ******************************************************************00520000
      **COPYBOOKS                                                       00530000
      ******************************************************************00540000
      ***VSM ITEM COPYBOOK                                              00550005
           COPY IMRD008.                                                00560032
      ***ABEND COPYBOOK                                                 00570005
           COPY DPWS004.                                                00580032
      ***DB2 MESSAGE AREA                                               00590000
           COPY SQLCA2.                                                 00600032
      ***DATE ROUTINE COPYBOOK                                          00610005
           COPY DPWS500.                                                00620032
      ******************************************************************00630000
      **TABLE INCLUSIONS                                                00640000
      ******************************************************************00650000
      ***DB2 COMMUNICATIONS AREA                                        00660000
           EXEC SQL                                                     00670000
                INCLUDE SQLCA                                           00680000
           END-EXEC.                                                    00690000
      ***UPC TABLE                                                      00700013
           EXEC SQL                                                     00710000
               INCLUDE XST00012                                         00720015
           END-EXEC.                                                    00730000
                                                                        00740000
       LINKAGE SECTION.                                                 00750000
      ******************************************************************00760000
      **LINKAGE VARIABLES                                               00770000
      ******************************************************************00780000
       01  LV-IMRD008-RECORD               PIC X(450).                  00790005
       01  LV-RETURN-CODE                  PIC S9(04).                  00800005
       01  LV-ACTION-IND                   PIC 9(02).                   00810026
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00820026
                                                                        00830000
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      00840005
                                LV-RETURN-CODE,                         00850026
                                LV-ACTION-IND,                          00860026
                                LV-CURRENT-TIMESTAMP.                   00870026
      ******************************************************************00880000
       0000-MAINLINE.                                                   00890000
                                                                        00900000
            PERFORM 1000-INITIALIZATION.                                00910000
            PERFORM 2000-UPDATE-XST-UPC                                 00920015
                    UNTIL PS-PROCESS-COMPLETE.                          00930005
            PERFORM 9999-WRAPUP.                                        00940000
                                                                        00950000
       EJECT.                                                           00960005
      ***************************************************************** 00970000
      **MOVES ALL COPYBOOK FIELDS TO THE DCLGEN FIELDS                  00980007
      ***************************************************************** 00990000
       1000-INITIALIZATION.                                             01000000
                                                                        01010000
           MOVE '1000-INITIALIZATION'          TO PV-PARAGRAPH-NAME.    01020025
                                                                        01030000
           INITIALIZE PS-PROCESS-CONTINUE-SW.                           01040008
           INITIALIZE PC-PROGRAM-COUNTERS.                              01050024
                                                                        01060008
           MOVE LV-IMRD008-RECORD              TO IMRD008-RECORD.       01070008
                                                                        01080020
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     01090018
      *     COMP FIELDS.                                                01100018
           MOVE IM008-UPC-MOVE-INTR-UPC-ID-NEW TO PC-10-DIGIT.          01110020
           MOVE PC-10-DIGIT                    TO PC-18-DIGIT.          01120020
           MOVE PC-18-DIGIT                    TO PC-09-DIGIT.          01130020
           MOVE PC-09-DIGIT                    TO XST00012-INTR-UPC-ID. 01140020
                                                                        01150020
           MOVE IM008-ITEM-KEY-UPC-ID      TO PC-10-DIGIT.              01160023
           MOVE PC-10-DIGIT                TO PC-18-DIGIT.              01170023
           MOVE PC-18-DIGIT                TO PC-09-DIGIT.              01180023
           MOVE PC-09-DIGIT                TO XST00012-UPC-ID.          01190023
                                                                        01200023
121402     MOVE LV-CURRENT-TIMESTAMP        TO XST00012-LAST-UPD-TMST.  01210029
121402     MOVE IM008-CRE-TMST          TO XST00012-SOR-LAST-MSG-TMST.  01220029
                                                                        01230000
       EJECT.                                                           01240005
      ***************************************************************** 01250000
      **UPDATES UPC TO NEW INTR_UPC_ID                                  01260031
      ***************************************************************** 01270000
       2000-UPDATE-XST-UPC.                                             01280015
                                                                        01290000
           MOVE '2000-UPDATE-XST-UPC' TO PV-PARAGRAPH-NAME.             01300025
                                                                        01310005
           EXEC SQL                                                     01320005
              UPDATE XST_UPC                                            01330015
                  SET   INTR_UPC_ID   = :XST00012-INTR-UPC-ID           01340015
                      , LAST_UPD_TMST = :XST00012-LAST-UPD-TMST         01350015
121402                , SOR_LAST_MSG_TMST = :XST00012-SOR-LAST-MSG-TMST 01360029
121402                , LAST_ACTN_CDE = 'U'                             01370030
                  WHERE UPC_ID        = :XST00012-UPC-ID                01380015
           END-EXEC.                                                    01390005
                                                                        01400005
           EVALUATE TRUE                                                01410005
      ***SUCCESSFUL PROCESSING                                          01420031
              WHEN SQLCODE = ZERO                                       01430005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01440008
      ***ROW/TABLE LOCK                                                 01450031
              WHEN SQLCODE = -911                                       01460005
              WHEN SQLCODE = -913                                       01470007
                   ADD +1                      TO PC-RETRY-COUNTER      01480007
                   IF  PC-RETRY-COUNTER = PC-RETRY-MAX                  01490007
                       SET PS-PROCESS-COMPLETE TO TRUE                  01500007
                   ELSE                                                 01510007
                       ADD +1                  TO PC-RETRY-COUNTER      01520007
                   END-IF                                               01530007
              WHEN OTHER                                                01540005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01550005
           END-EVALUATE.                                                01560005
                                                                        01570000
       EJECT.                                                           01580005
      ******************************************************************01590000
      *MOVE RETURN CODE TO LINKAGE VARIABLE AND EXIT MODULE             01600000
      ******************************************************************01610000
       9999-WRAPUP.                                                     01620000
                                                                        01630000
           MOVE '9999-WRAPUP'         TO PV-PARAGRAPH-NAME              01640028
                                                                        01650000
           MOVE SQLCODE               TO LV-RETURN-CODE.                01660028
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 01670026
                                                                        01680026
           EXIT PROGRAM.                                                01690000
                                                                        01700000
       EJECT.                                                           01710005
