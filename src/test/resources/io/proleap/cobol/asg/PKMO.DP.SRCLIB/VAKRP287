       IDENTIFICATION DIVISION.                                                 
W34517 PROGRAM-ID.         VAKRP287                                             
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       04/28/2003.                                          
       DATE-COMPILED.                                                           
                                                                                
W34517****IMPORTANT*****************************************************        
W34517* THIS IS A CLONE OF THE VAKRP287 CREATED FOR THE VA PROJECT     *        
W34517* THE MAJOR CHANGES ARE .....                                    *        
W34517******************************************************************        
      ******************************************************************        
W34517*     VAKRP287 - DAILY DETAILED ADJUSTED PHYSICAL INVENTORY FOR           
      *                STORE CORRECTIONS.                                       
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
21732 * 21732    04/28/2003  DAILY DETAIL STOCKROOM WITHDRAWAL & STORE          
21732 *                      CORRECTIONS REPORT CREATION. - ROD JANSSEN         
W26600* 26600    04/28/2004  P. KEBER - STORE NUMBER EXPANSION PROJECT          
W26600*                      CHANGES TO REPLACE TSTR000 WITH XMT_LOC AND        
W26600*                      TO ALLOW A 5 DIGIT STORE.                          
W27710* 27710    01/28/2005  R. JANSSEN - REMOVE CONTENTION ISSUES WITH         
W27710*                      OTHER INVENTORY JOBS.                              
W34517* W34517  10-14-2008 IN-VA INVENTORY STREAM SPLIT                         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PHY-INVENTORY-SUMMARY-FILE                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT DATE-CONTROL-CARD-FILE                                        
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT ADJ-PHY-INVENTORY-REPORT                                      
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  PHY-INVENTORY-SUMMARY-FILE                                           
           RECORDING MODE IS F                                          INKUT115
           BLOCK CONTAINS 0 CHARACTERS                                  INKUT115
           LABEL RECORDS ARE STANDARD.                                  INKUT115
       01  PHY-INVENTORY-SUMMARY-REC     PIC X(100).                       CL*12
                                                                        INKUT115
       FD  DATE-CONTROL-CARD-FILE                                               
           RECORDING MODE IS F                                          INKUT115
           BLOCK CONTAINS 0 CHARACTERS                                  INKUT115
           LABEL RECORDS ARE STANDARD.                                  INKUT115
       01  DATE-CONTROL-CARD-RECORD.                                       CL*12
           05  DCCR-CONTROL-DATE.                                               
               10  DCCR-YEAR             PIC X(04).                             
               10  DCCR-FILLER1          PIC X(01).                             
               10  DCCR-MTH              PIC X(02).                             
               10  DCCR-FILLER2          PIC X(01).                             
               10  DCCR-DAY              PIC X(02).                             
           05  DCCR-REST-OF-RECORD       PIC X(70).                             
                                                                        INKUT115
       FD  ADJ-PHY-INVENTORY-REPORT                                             
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           DATA RECORD IS REPORT-LINE.                                          
       01  REPORT-LINE                   PIC X(132).                            
                                                                                
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
       01  PS-SWITCH-AREA.                                                      
           05  PS-INP01-DONE-IND     PIC X(01) VALUE 'N'.                       
               88  PS-INP01-DONE-FILE          VALUE 'Y'.                       
           05  PS-INP02-DONE-IND     PIC X(01) VALUE 'N'.                       
               88  PS-INP02-DONE-FILE          VALUE 'Y'.                       
           05  PS-PRINT-HEADERS-SW   PIC X(01) VALUE 'N'.                       
               88  PS-PRINT-HEADERS            VALUE 'N'.                       
               88  PS-HEADERS-PRINTED          VALUE 'Y'.                       
           05  PS-FIRST-TIME-SW      PIC X(01) VALUE 'N'.                       
               88  FIRST-TIME-THRU             VALUE 'N'.                       
               88  CONTINUED-PROCESSING        VALUE 'Y'.                       
                                                                                
       01  PC-PRINT-CONTROL.                                                    
           05  PC-PAGE-COUNT           PIC 9(05) VALUE ZEROS.                   
           05  PC-LINE-COUNT           PIC 9(05) VALUE ZEROS.                   
           05  PC-EDIT-YEAR            PIC X(02) VALUE SPACES.                  
               88  TWENTY-FIRST-CENTURY          VALUE '03' THRU '99'.          
           05  PC-TAB-MAX            PIC S9(03) COMP-3 VALUE 999.               
                                                                                
       01  PV-STR-NDX                PIC S9(03) COMP-3 VALUE ZEROS.             
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PROCESS-DATE.                                                 
               10  PV-DB2-YEAR.                                                 
                   15  PV-DB2-CC       PIC 9(2)  VALUE ZEROES.                  
                   15  PV-DB2-YY       PIC 9(2)  VALUE ZEROES.                  
               10  FILLER              PIC X(1)  VALUE '-'.                     
               10  PV-DB2-MM           PIC 9(2)  VALUE ZEROES.                  
               10  FILLER              PIC X(1)  VALUE '-'.                     
               10  PV-DB2-DD           PIC 9(2)  VALUE ZEROES.                  
W26600     05  PV-SAVE-STORE         PIC X(05) VALUE SPACES.                    
W26600     05  PV-STORE-NUM          PIC 9(05) VALUE ZERO.                      
           05  PV-PARA               PIC X(30) VALUE SPACES.                    
           05  PV-DB2-OPER-ATTEMPTED PIC X(30) VALUE SPACES.                    
           05  PV-TADJDTL-AMT        PIC S9(13)V99 COMP-3 VALUE ZEROS.          
           05  PV-TOT-TADJDTL-AMT    PIC S9(13)V99 COMP-3 VALUE ZEROS.          
           05  PV-COMP-TADJDTL-AMT   PIC S9(13)V99 COMP-3 VALUE ZEROS.          
           05  PV-RECS-READ-ACCUM    PIC S9(09)    COMP VALUE ZEROS.            
           05  PV-RECS-PROC-ACCUM    PIC S9(09)    COMP VALUE ZEROS.            
           05  PV-STOR-RECS-ACCUM    PIC S9(09)    COMP VALUE ZEROS.            
W34517     05  PV-JOB-INFO.                                                     
W34517         10  FILLER                  PIC X(01)       VALUE '('.           
W34517         10  PV-JOB-NAME             PIC X(08)       VALUE SPACES.        
W34517         10  FILLER                  PIC X(01)       VALUE ')'.           
                                                                                
       01  CPU-DATE.                                                            
           05  CPU-YEAR                    PIC X(02)     VALUE SPACES.          
           05  CPU-MONTH                   PIC X(02)     VALUE SPACES.          
           05  CPU-DAY                     PIC X(02)     VALUE SPACES.          
                                                                                
       01  CPU-TIME.                                                            
           05  CPU-HOUR                    PIC X(02)     VALUE SPACES.          
           05  CPU-MINUTE                  PIC X(02)     VALUE SPACES.          
           05  FILLER                      PIC X(02)     VALUE SPACES.          
                                                                                
       01  H1-HEADING.                                                          
           05  FILLER              PIC X(33)   VALUE SPACES.                    
           05  FILLER              PIC X(44)   VALUE                            
              'DAILY STORE CORRECTIONS PHYSICAL ADJUSTMENT '.                   
           05  FILLER              PIC X(12)   VALUE                            
              'SUMMARY FOR '.                                                   
           05  H1-DATE-CONTROL.                                                 
               10  H1-MTH          PIC X(02).                                   
               10  H1-SLASH1       PIC X(01)   VALUE '/'.                       
               10  H1-DAY          PIC X(02).                                   
               10  H1-SLASH2       PIC X(01)   VALUE '/'.                       
               10  H1-YEAR         PIC X(04).                                   
                                                                                
       01  H2-HEADING.                                                          
           05  FILLER              PIC X(27)   VALUE SPACES.                    
W26600     05  FILLER              PIC X(08)   VALUE 'STORE   '.                
W26600     05  H2-STORE            PIC 99999.                                   
           05  FILLER              PIC X(03)   VALUE SPACES.                    
W26600     05  H2-DESCRIP          PIC X(40).                                   
                                                                                
       01  H3-HEADING.                                                          
           05  FILLER              PIC X(43)   VALUE SPACES.                    
           05  FILLER              PIC X(24)   VALUE                            
      **        ----5----0----5----0----5----0----5----0----5----0----5         
               'AREA           INVENTORY'.                                      
                                                                                
       01  H4-HEADING.                                                          
           05  FILLER              PIC X(43)   VALUE SPACES.                    
           05  FILLER              PIC X(25)   VALUE                            
      **        ----5----0----5----0----5----0----5----0----5----0----5         
               'NUMBER         ADJUSTMENT'.                                     
                                                                                
       01  H5-HEADING.                                                          
           05  FILLER              PIC X(43)   VALUE SPACES.                    
           05  FILLER              PIC X(22)   VALUE                            
      **        ----5----0----5----0----5----0----5----0----5----0----5         
               '               DOLLARS'.                                        
                                                                                
       01  D1-DETAIL-LINE.                                                      
           10  FILLER               PIC X(43) VALUE SPACES.                     
           10  D1-AREA-NBR          PIC ZZZZZ9.                                 
           10  FILLER               PIC X(07) VALUE SPACES.                     
           10  D1-TADJDTL-AMT       PIC Z,ZZZ,ZZ9.99-.                          
                                                                                
       01  T1-TOTAL-LINE.                                                       
           10  FILLER               PIC X(27) VALUE SPACES.                     
           10  T1-VERBIAGE          PIC X(13).                                  
           10  FILLER               PIC X(15) VALUE SPACES.                     
           10  T1-TADJDTL-AMT       PIC ZZ,ZZZ,ZZ9.99-.                         
           10  T1-UNDERSCORE REDEFINES                                          
               T1-TADJDTL-AMT       PIC --------------.                         
                                                                                
       01  DL-UNDERSCORE-LINE.                                                  
           10  FILLER               PIC X(55) VALUE SPACES.                     
           10  FILLER               PIC X(14) VALUE '--------------'.           
                                                                                
       01  DL-NO-PROCESS-LINE.                                                  
           05  FILLER                  PIC X(49)    VALUE                       
               '*********** NOTHING TO PROCESS ***************'.                
                                                                                
      ******************************************************************        
      * COPYBOOK FOR REMAINING REPORT HEADING LINES, DETAIL LINE                
      ******************************************************************        
           COPY DPWS132O.                                                       
                                                                                
W34517******************************************************************        
W34517*  HEADER LINE                                                            
W34517******************************************************************        
W34517*01  DP085-GET-CURR-JOB-INFO-PGM.                                         
W34517*01  DP085-GET-CURR-JOB-INFO-PARMS.                                       
W34517     COPY DPWS085.                                                        
                                                                                
      ******************************************************************        
      *  ADJ-PHY-INV-FILE EXTRACT COPYBOOK - INPUT RECORD LAYOUT                
      ******************************************************************        
           COPY INRD109.                                                        
                                                                                
      ******************************************************************        
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
W26600* DB2 XMT LOCATION TABLE DCLGEN                                           
      *****************************************************************         
           EXEC SQL                                                             
W26600         INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
      /****************************************************************         
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
       0000-MAINLINE-SECTION.                                                   
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-LOOP                                            
                UNTIL PS-INP01-DONE-FILE.                                       
                                                                                
           PERFORM 9000-EOJ-TASKS.                                              
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      /****************************************************************         
      * 1000-INITIALIZATION.                                                    
      *****************************************************************         
       1000-INITIALIZATION.                                                     
                                                                                
           MOVE '*1000-INITIALIZATION.              *' TO PV-PARA.              
                                                                                
           DISPLAY '**********************************'.                        
W34517     DISPLAY '* START OF VAKRP287               '.                        
           DISPLAY '**********************************'.                        
                                                                                
           OPEN  INPUT PHY-INVENTORY-SUMMARY-FILE                               
                 INPUT DATE-CONTROL-CARD-FILE                                   
                OUTPUT ADJ-PHY-INVENTORY-REPORT.                                
                                                                                
W34517*  GET CURRENT JOB INFORMATION FOR USE IN THE REPORT                      
W34517     CALL DP085-GET-CURR-JOB-INFO-PGM                                     
W34517         USING DP085-GET-CURR-JOB-INFO-PARMS.                             
W34517     MOVE DP085-JOB-NAME     TO PV-JOB-NAME.                              
W34517                                                                          
W34517     MOVE PV-JOB-INFO                                                     
W34517         TO DP132O-STANDRD-HEADER-132-COLS(20:10).                        
                                                                                
      *** ACCEPT THE SYSTEM DATE & SYSTEM TIME.....                             
           ACCEPT CPU-DATE          FROM DATE.                                  
           MOVE CPU-YEAR            TO DP132O-RUN-YEAR.                         
           MOVE CPU-MONTH           TO DP132O-RUN-MONTH.                        
           MOVE CPU-DAY             TO DP132O-RUN-DAY.                          
                                                                                
           ACCEPT CPU-TIME          FROM TIME.                                  
           MOVE CPU-HOUR            TO DP132O-RUN-HOUR.                         
           MOVE CPU-MINUTE          TO DP132O-RUN-MINUTE.                       
                                                                                
W34517     MOVE 'VAKRP287'          TO DP132O-PROGRAM-NAME.                     
           MOVE 1                   TO DP132O-REPORT-NUMBER.                    
                                                                                
      *** INITIALIZATION...                                                     
           MOVE ZEROS               TO PC-PAGE-COUNT.                           
           MOVE ZEROS               TO PC-LINE-COUNT.                           
           MOVE ZEROS               TO PV-TADJDTL-AMT.                          
           MOVE ZEROS               TO PV-TOT-TADJDTL-AMT.                      
           MOVE ZEROS               TO PV-COMP-TADJDTL-AMT.                     
           MOVE ZEROS               TO PV-RECS-READ-ACCUM.                      
           MOVE ZEROS               TO PV-RECS-PROC-ACCUM.                      
           MOVE ZEROS               TO PV-STOR-RECS-ACCUM.                      
           MOVE 'N'                 TO PS-INP01-DONE-IND.                       
           MOVE 'N'                 TO PS-INP02-DONE-IND.                       
           MOVE 'N'                 TO PS-PRINT-HEADERS-SW.                     
           MOVE 'N'                 TO PS-FIRST-TIME-SW.                        
                                                                                
      *** DETERMINE WHAT THE PROCESS DATE WILL BE.....                          
           PERFORM 8100-READ-DATE-CONTROL.                                      
           IF PS-INP02-DONE-FILE OR                                             
              DCCR-CONTROL-DATE IS EQUAL TO SPACES                              
               PERFORM 3200-USE-SYSTEM-DATE                                     
           ELSE                                                                 
               PERFORM 3100-USE-CONTROL-CARD-DTE                                
           END-IF.                                                              
                                                                                
           DISPLAY '** SELECTED EXTRACT DATE: ' PV-PROCESS-DATE.                
                                                                                
      *** READ THE PHYSICAL INVENTORY ADJUSTMENT FILE....                       
           PERFORM 8000-READ-PHY-INV-FILE.                                      
                                                                                
      /****************************************************************         
      * 2000-PROCESS-LOOP.                                                      
      *****************************************************************         
       2000-PROCESS-LOOP.                                                       
                                                                                
           MOVE '*2000-PROCESS-LOOP.                *' TO PV-PARA.              
                                                                                
           IF FIRST-TIME-THRU                                                   
             IF IN109-CREATE-DATE IS EQUAL TO PV-PROCESS-DATE                   
               MOVE IN109-STORE-NBR  TO PV-SAVE-STORE                           
               MOVE 'Y'              TO PS-FIRST-TIME-SW                        
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF IN109-CREATE-DATE IS EQUAL TO PV-PROCESS-DATE                     
             IF IN109-STORE-NBR IS EQUAL TO PV-SAVE-STORE                       
               IF IN109-EXTD-AMT IS NOT EQUAL TO ZEROS                          
                 PERFORM 3300-PROCESS-DETAIL                                    
                 PERFORM 8000-READ-PHY-INV-FILE                                 
               ELSE                                                             
                 PERFORM 8000-READ-PHY-INV-FILE                                 
               END-IF                                                           
             ELSE                                                               
               PERFORM 3000-STORE-BREAK                                         
               IF IN109-EXTD-AMT IS NOT EQUAL TO ZEROS                          
                 PERFORM 3300-PROCESS-DETAIL                                    
               END-IF                                                           
               PERFORM 8000-READ-PHY-INV-FILE                                   
             END-IF                                                             
           ELSE                                                                 
             PERFORM 8000-READ-PHY-INV-FILE                                     
           END-IF.                                                              
                                                                                
      /****************************************************************         
      * 3000-STORE-BREAK.                                                       
      *   PRINT THE STORE TOTAL LINE.                                           
      *****************************************************************         
       3000-STORE-BREAK.                                                        
                                                                                
           MOVE '*3000-STORE-BREAK.                 *' TO PV-PARA.              
                                                                                
      *** PRINT STORE TOTALS IF WE HAVE PROCESSED INFORMATION...                
           IF PV-STOR-RECS-ACCUM IS GREATER THAN ZEROS                          
               IF PC-LINE-COUNT IS GREATER THAN 62                              
                   PERFORM 8400-PRINT-HEADINGS                                  
               END-IF                                                           
               WRITE REPORT-LINE FROM DL-UNDERSCORE-LINE                        
                                    AFTER ADVANCING 1 LINES                     
               MOVE 'STORE TOTAL'       TO T1-VERBIAGE                          
               MOVE PV-TOT-TADJDTL-AMT  TO T1-TADJDTL-AMT                       
               WRITE REPORT-LINE FROM T1-TOTAL-LINE                             
                                    AFTER ADVANCING 1 LINES                     
           END-IF.                                                              
                                                                                
      *** RESET ACCUMULATORS FOR THE NEXT STORE...                              
           MOVE IN109-STORE-NBR     TO PV-SAVE-STORE.                           
                                                                                
           MOVE 'N'                 TO PS-PRINT-HEADERS-SW.                     
           MOVE ZEROS               TO PV-STOR-RECS-ACCUM.                      
           MOVE ZEROS               TO PV-TOT-TADJDTL-AMT.                      
                                                                                
      ****************************************************************          
      * 3100-USE-CONTROL-CARD-DTE.                                              
      ****************************************************************          
       3100-USE-CONTROL-CARD-DTE.                                               
                                                                                
           MOVE '*3100-USE-CONTROL-CARD-DTE.        *' TO PV-PARA.              
                                                                                
           DISPLAY '**   3100-USE-CONTROL-CARD-DTE.'.                           
           DISPLAY '** USING CONTROL CARD DATE FOR PROCESS DATE.'.              
                                                                                
           MOVE DCCR-CONTROL-DATE   TO PV-PROCESS-DATE.                         
                                                                                
           MOVE PV-DB2-YEAR         TO H1-YEAR.                                 
           MOVE PV-DB2-MM           TO H1-MTH.                                  
           MOVE PV-DB2-DD           TO H1-DAY.                                  
                                                                                
      ****************************************************************          
      * 3200-USE-SYSTEM-DATE.                                                   
      ****************************************************************          
       3200-USE-SYSTEM-DATE.                                                    
                                                                                
           MOVE '*3200-USE-ML-CNTL-DTES.            *' TO PV-PARA.              
                                                                                
           DISPLAY '**   3200-USE-ML-CNTL-DTES.'.                               
           DISPLAY '** USING THE SYSTEM DATE FOR PROCESS DATE.'                 
                                                                                
           MOVE CPU-YEAR            TO PC-EDIT-YEAR.                            
           IF TWENTY-FIRST-CENTURY                                              
               MOVE 20              TO PV-DB2-CC                                
               MOVE CPU-YEAR        TO PV-DB2-YY                                
           ELSE                                                                 
               MOVE 21              TO PV-DB2-CC                                
               MOVE CPU-YEAR        TO PV-DB2-YY                                
           END-IF.                                                              
                                                                                
           MOVE PV-DB2-YEAR         TO H1-YEAR.                                 
           MOVE CPU-MONTH           TO H1-MTH, PV-DB2-MM.                       
           MOVE CPU-DAY             TO H1-DAY, PV-DB2-DD.                       
                                                                                
      *****************************************************************         
      * 3300-PROCESS-DETAIL.                                                    
      *   PRINT DETAIL INFORMATION.                                             
      *****************************************************************         
       3300-PROCESS-DETAIL.                                                     
                                                                                
      *** PRINT HEADER LINES IF WE HAVE ADJUSTMENTS & THE HEADERS HAVE          
      *** NOT BEEN PRINTED...                                                   
           IF PS-PRINT-HEADERS                                                  
               PERFORM 8400-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
      *** BUILD DETAIL INFORMATION...                                           
           MOVE IN109-AREA-NBR      TO D1-AREA-NBR.                             
           MOVE IN109-EXTD-AMT      TO D1-TADJDTL-AMT.                          
                                                                                
           IF PC-LINE-COUNT IS GREATER THAN 64                                  
               PERFORM 8400-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           WRITE REPORT-LINE FROM D1-DETAIL-LINE                                
                                    AFTER ADVANCING 1 LINE.                     
                                                                                
           ADD 1                    TO PC-LINE-COUNT.                           
                                                                                
      *** ACCUMULATE STORE & COMPANY TOTALS...                                  
           ADD IN109-EXTD-AMT       TO PV-TOT-TADJDTL-AMT                       
                                       PV-COMP-TADJDTL-AMT.                     
                                                                                
           ADD 1                    TO PV-RECS-PROC-ACCUM.                      
           ADD 1                    TO PV-STOR-RECS-ACCUM.                      
                                                                                
      /****************************************************************         
      * 8000-READ-PHY-INV-FILE.                                                 
      *****************************************************************         
       8000-READ-PHY-INV-FILE.                                                  
                                                                                
           READ PHY-INVENTORY-SUMMARY-FILE NEXT                                 
             INTO IN109-PHY-IN-SUMMARY-RECORD                                   
               AT END SET PS-INP01-DONE-FILE TO TRUE                            
           END-READ.                                                            
                                                                                
           IF (NOT PS-INP01-DONE-FILE)                                          
               ADD 1                TO PV-RECS-READ-ACCUM                       
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 8100-READ-DATE-CONTROL.                                                 
      *****************************************************************         
       8100-READ-DATE-CONTROL.                                                  
                                                                                
           READ DATE-CONTROL-CARD-FILE NEXT                                     
               AT END SET PS-INP02-DONE-FILE TO TRUE                            
           END-READ.                                                            
                                                                                
      *****************************************************************         
      * 8400-PRINT-HEADINGS.                                                    
      *****************************************************************         
       8400-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1                    TO PC-PAGE-COUNT.                           
           MOVE PC-PAGE-COUNT       TO DP132O-PAGE-NUMBER.                      
           WRITE REPORT-LINE FROM DP132O-STANDRD-HEADER-132-COLS                
                                    AFTER ADVANCING PAGE.                       
                                                                                
           WRITE REPORT-LINE FROM H1-HEADING AFTER ADVANCING 1 LINE.            
                                                                                
           MOVE PV-SAVE-STORE       TO H2-STORE.                                
W26600     PERFORM 8500-SELECT-XMTLOC-TABLE.                                    
           WRITE REPORT-LINE FROM H2-HEADING AFTER ADVANCING 2 LINE.            
                                                                                
           WRITE REPORT-LINE FROM H3-HEADING AFTER ADVANCING 2 LINE.            
           WRITE REPORT-LINE FROM H4-HEADING AFTER ADVANCING 1 LINE.            
           WRITE REPORT-LINE FROM H5-HEADING AFTER ADVANCING 1 LINE.            
                                                                                
           MOVE SPACES              TO REPORT-LINE.                             
           WRITE REPORT-LINE        AFTER ADVANCING 1 LINE.                     
                                                                                
           MOVE 8                   TO PC-LINE-COUNT.                           
                                                                                
           MOVE 'Y'                 TO PS-PRINT-HEADERS-SW.                     
                                                                                
      *****************************************************************         
W26600* 8500-SELECT-XMTLOC-TABLE.                                               
      *****************************************************************         
W26600 8500-SELECT-XMTLOC-TABLE.                                                
                                                                                
W26600     MOVE PV-SAVE-STORE       TO PV-STORE-NUM.                            
W26600     MOVE PV-STORE-NUM        TO XMT00001-LOC-NBR.                        
                                                                                
           EXEC SQL                                                             
W26600         SELECT LOC_NM                                                    
W26600          INTO :XMT00001-LOC-NM                                           
W26600          FROM XMT_LOC                                                    
W26600         WHERE LOC_NBR = :XMT00001-LOC-NBR                                
W27710         WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
W26600         MOVE XMT00001-LOC-NM TO H2-DESCRIP                               
             WHEN SQLCODE IS EQUAL TO +100                                      
               MOVE '****'          TO H2-DESCRIP                               
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
W26600         MOVE '8500-SELECT-XMTLOC-TABLE' TO PV-PARA                       
               MOVE 'LOAD TABLE'        TO PV-DB2-OPER-ATTEMPTED                
               PERFORM 9500-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      /****************************************************************         
      * 9000-EOJ-TASKS.                                                         
      *****************************************************************         
       9000-EOJ-TASKS.                                                          
                                                                                
      *** PRINT THE FINAL STORE TOTALS & COMPANY TOTALS....                     
           IF PV-RECS-PROC-ACCUM IS GREATER THAN ZERO                           
               PERFORM 3000-STORE-BREAK                                         
                                                                                
               ADD 1              TO PC-PAGE-COUNT                              
               MOVE PC-PAGE-COUNT TO DP132O-PAGE-NUMBER                         
               WRITE REPORT-LINE                                                
                 FROM DP132O-STANDRD-HEADER-132-COLS                            
                                  AFTER ADVANCING PAGE                          
               WRITE REPORT-LINE FROM H1-HEADING                                
                                  AFTER ADVANCING 1 LINE                        
               MOVE ZEROS         TO H2-STORE                                   
               MOVE SPACES        TO H2-DESCRIP                                 
               WRITE REPORT-LINE FROM H2-HEADING                                
                                  AFTER ADVANCING 2 LINES                       
               WRITE REPORT-LINE FROM H3-HEADING                                
                                  AFTER ADVANCING 2 LINES                       
               WRITE REPORT-LINE FROM H4-HEADING                                
                                  AFTER ADVANCING 1 LINE                        
               WRITE REPORT-LINE FROM H5-HEADING                                
                                  AFTER ADVANCING 1 LINE                        
                                                                                
               MOVE 'COMPANY TOTAL'        TO T1-VERBIAGE                       
               MOVE PV-COMP-TADJDTL-AMT    TO T1-TADJDTL-AMT                    
                                                                                
               WRITE REPORT-LINE FROM T1-TOTAL-LINE                             
                                    AFTER ADVANCING 2 LINES                     
           ELSE                                                                 
      *** PRINT A NO RECORDS PROCESSED PAGE FOR ZERO ACTIVITY...                
               ADD 1              TO PC-PAGE-COUNT                              
               MOVE PC-PAGE-COUNT TO DP132O-PAGE-NUMBER                         
               WRITE REPORT-LINE FROM DP132O-STANDRD-HEADER-132-COLS            
                                    AFTER ADVANCING PAGE                        
               WRITE REPORT-LINE FROM H1-HEADING                                
                                    AFTER ADVANCING 1 LINE                      
               MOVE ZEROS         TO H2-STORE                                   
               MOVE SPACES        TO H2-DESCRIP                                 
               WRITE REPORT-LINE FROM H2-HEADING                                
                                  AFTER ADVANCING 2 LINES                       
               WRITE REPORT-LINE FROM H3-HEADING                                
                                    AFTER ADVANCING 2 LINES                     
               WRITE REPORT-LINE FROM H4-HEADING                                
                                    AFTER ADVANCING 1 LINE                      
               WRITE REPORT-LINE FROM H5-HEADING                                
                                    AFTER ADVANCING 1 LINE                      
               WRITE REPORT-LINE FROM DL-NO-PROCESS-LINE                        
                                    AFTER ADVANCING 2 LINES                     
           END-IF.                                                              
                                                                                
      *** CLOSE FILES...                                                        
           CLOSE PHY-INVENTORY-SUMMARY-FILE                                     
                 DATE-CONTROL-CARD-FILE                                         
                 ADJ-PHY-INVENTORY-REPORT.                                      
                                                                                
           DISPLAY '**********************************'.                        
W34517     DISPLAY '* END OF VAKRP287               '.                          
           DISPLAY '**********************************'.                        
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9500-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
W34517     DISPLAY '**  PROGRAM   **  =  VAKRP287'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARA.                              
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9750-ABEND.                                                  
                                                                                
       9750-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
