000100*-----------------------*                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*-----------------------*                                                 
000400 PROGRAM-ID.    SAKCS429.                                                 
000500 AUTHOR.        GAYLE HAUPT.                                              
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000700 DATE-WRITTEN.  10/12/2000.                                               
000800 DATE-COMPILED.                                                           
000900                                                                          
001000*----------------------------------------------------------------*        
001100*    S429 - RETURN TRANSACTION LIST                              *        
001200*                                                                *        
001300*    THIS PROGRAM GENERATES A LIST OF TRANSACTIONS RETURNED      *        
001400*    AGAINST AN ORIGINAL TRANSACTION.  ONE TRANSACTION FROM      *        
001500*    THE LIST MAY BE SELECTED FOR FURTHER INFORMATION (F14 -     *        
001600*    ITEM INQUIRY OR F20 - TENDER INQUIRY).  THE ORIGINAL        *        
001700*    TRANSACTION INFORMATION IS OBTAINED FROM SAKCS426 (CORP.    *        
001800*    EJ TRAN. LIST).  ONLY ONE TRANSACTION MAY BE SELECTED FROM  *        
001900*    SAKCS426 AS INPUT TO SAKCS429.                              *        
002000*                                                                *        
002100*----------------------------------------------------------------*        
002200*                         CHANGES                                *        
002300******************************************************************        
002400* CHANGED: 07/16/2001                                            *        
002500* BY:      MATT STIFF                                            *        
002600* WHY:     ADDED LOGIC SO PROGRAM COULD RETRIEVE RETURN          *        
002700*          TRANSACTIONS WHEN ORIGINAL HAS ROLLED TO HISTORICAL   *        
002800*          TABLES.                                               *        
002910******************************************************************        
002920* CHANGED: 07/18/2001                                            *        
002930* BY:      DOUG JONES                                            *        
002940* WHY:     ADDED LOGIC SO PROGRAM COULD RETRIEVE RETURN          *        
002950*          TRANSACTIONS PERFORMED ON THE SAME DAY AS THE INQUIRY *        
002970******************************************************************        
NEMCHG* CHANGED: 12/11/2002                                            *        
NEMCHG* BY:      NANCY MARTIN                        WR24311           *        
NEMCVG* WHY:     CHANGED DISPLAY OF OPERATOR ID FROM 6 TO 7 DIGITS.    *        
002970******************************************************************        
006101* CHANGED: 01/18/2012                                            *        
006102* BY:      ROB SWAGER                                            *        
006103* FOR:     WR24719                                               *        
006104* WHY:     EXPANDED SIZE OF DL-TRAN-AMOUNT-LEFT FIELD            *        
006105*          FOR LARGE RETAIL AMOUNT.                              *        
006106******************************************************************        
C62728* CHANGED  10/04/2013                                            *        
C62728* BY:      COGNIZANT                                             *        
C62728* FOR:     CHG0062728                                            *        
C62728* WHY:     REMOVED LOGIC THAT REFERENCING THE FOLLOWING          *        
C62728*          COSA HISTORY TABLES FOR 9607 - FSH 2013-CFMC PROJECT  *        
C62728*                                                                *        
C62728*          TEPRKYH,TEPTRNH & TEPSTGP - STORE GROUP TABLE         *        
006106******************************************************************        
C00000*  CHANGED 09/02/2021    BY GERMAN BANDA    WR: CHG0261091                
C00000*                                                                         
C00000*  CHANGE CALL OF DPKUT100 TO                                             
C00000*  CALL DP010I-NUMERIC-EDIT-ROUTINE                                       
C00000*  MAKING THE CALL DYNAMIC VS STATIC                                      
C00000*-----------------------------------------------------------------        
CICS  *  CHANGED 10/12/2022    BY JIM HUNTER      WR: CHG0276153                
CICS  *                                                                         
CICS  *  ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL               
CICS  *  DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.              
CICS  *-----------------------------------------------------------------        
C62728******************************************************************        
003000                                                                          
003100 DATA DIVISION.                                                           
003200                                                                          
003300 WORKING-STORAGE SECTION.                                                 
003400                                                                          
003500 01  DK-DB2-KEYS.                                                         
003600     15  DK-SALES-DATE                  PIC  X(10) VALUE SPACE.           
003700     15  DK-STORE                       PIC S9(04) VALUE ZERO             
003800                                                   COMP.                  
003900     15  DK-REGISTER                    PIC S9(04) VALUE ZERO             
004000                                                   COMP.                  
004100     15  DK-TRANS                       PIC S9(04) VALUE ZERO             
004200                                                   COMP.                  
004300     15  DK-PARTITION                   PIC S9(04) VALUE ZERO             
004400                                                   COMP.                  
004500     15  DK-SALES-CORR-SEQ-NBR          PIC S9(04) VALUE ZERO             
004600                                                   COMP.                  
004700     15  DK-START-TIME                  PIC  X(08) VALUE SPACE.           
004800                                                                          
004900 01  PC-PROGRAM-CONSTANTS.                                                
005000     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
005100                                                    'SA429A  '.           
005200     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
005300                                                    'SAKM429 '.           
005400     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
005500                                                    'SAKCS429'.           
005600     05  PC-DTL-QUEUE-SEQ               PIC  9(01)  VALUE 3.              
005700**  OUTPUT LIST FROM SAKCS429                                             
005800     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
005900**  SELECTED ITEM PASSED TO SAKCS429                                      
006000     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
006100**  OUTPUT SELECTED ITEM FROM SAKCS429 LIST                               
006200     05  PC-SEL2-QUEUE-SEQ              PIC  9(01)  VALUE 3.              
006300     05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.            
006400     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +12             
006500                                                    COMP SYNC.            
006600     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
006700                                                    COMP SYNC.            
006800     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
006900                                                    +1000000              
007000                                                    COMP-3.               
007100     05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
007200                                                    COMP-3.               
007300     05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.          
007400     05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.          
007500     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
007600                                                    COMP SYNC.            
007700     05  PC-QUOTE                       PIC  X(01)  VALUE QUOTE.          
007800                                                                          
007900     05  PC-TSYMSG-NUMBERS.                                               
008000         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
008100         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
008200         10  PC-TSYMSG-00047            PIC  9(05)  VALUE 00047.          
008300         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
008400         10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
008500         10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
008600         10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
008700         10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
008800         10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
008900         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
009000         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
009100         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
009200         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
009300         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
009400         10  PC-TSYMSG-02416            PIC  9(05)  VALUE 02416.          
009500                                                                          
009600 01  PS-PROGRAM-SWITCHES.                                                 
009700     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
009800         88  PS-NO-ERROR                            VALUE 'Y'.            
009900         88  PS-ERROR                               VALUE 'N'.            
010000     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
010100         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
010200         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
010300     05  PS-LIST-END-SW                 PIC  X(01)  VALUE 'N'.            
010400         88  PS-LIST-END                            VALUE 'Y'.            
010500         88  PS-NO-LIST-END                         VALUE 'N'.            
010600     05  PS-END-OF-RETURNS-CSR-SW       PIC  X(01)  VALUE 'N'.            
010700         88  PS-END-OF-RETURNS-CSR                  VALUE 'Y'.            
010800     05  PS-END-OF-OPER-CSR-SW          PIC  X(01)  VALUE 'N'.            
010900         88  PS-END-OF-OPER-CSR                     VALUE 'Y'.            
011500                                                                          
011600 01  RF-PROCESSING-DATE.                                                  
011700     05  RF-PRCG-DTE-CC                 PIC  XX.                          
011800     05  RF-PRCG-DTE-YY                 PIC  XX.                          
011900     05  FILLER                         PIC  X.                           
012000     05  RF-PRCG-DTE-MM                 PIC  XX.                          
012100     05  FILLER                         PIC  X.                           
012200     05  RF-PRCG-DTE-DD                 PIC  XX.                          
012300                                                                          
012400 01  RF-DISPLAY-DATE.                                                     
012500     05  RF-DISPLAY-DTE-MM              PIC  XX.                          
012600     05  RF-DISPLAY-DTE-DD              PIC  XX.                          
012700     05  RF-DISPLAY-DTE-YY              PIC  XX.                          
012800                                                                          
012900 01  RF-STATUS-TEXT.                                                      
013000     05  RF-STATUS-TEXT-MSG             PIC  X(06).                       
013100     05  RF-STATUS-TEXT-DATE            PIC  X(06).                       
013200                                                                          
013300 01  PV-PROGRAM-VARIABLES.                                                
013400     05  PV-RET-STR-NBR                 PIC S9(04)  VALUE +0              
013500                                                    COMP.                 
013600     05  PV-RET-SLS-DTE-ISO             PIC  X(10)  VALUE SPACE.          
013700     05  PV-RET-PARTN-NBR               PIC S9(04)  VALUE +0              
013800                                                    COMP.                 
013900     05  PV-RET-RGST-ID                 PIC S9(04)  VALUE +0              
014000                                                    COMP.                 
014100     05  PV-RET-TRN-NBR                 PIC S9(04)  VALUE +0              
014200                                                    COMP.                 
014300     05  PV-RET-ASSOC-ID                PIC  9(09)  VALUE  0.             
014400     05  PV-RET-SLS-CORR-SEQ-NBR        PIC S9(04)  VALUE +0              
014500                                                    COMP.                 
014600     05  PV-RET-STRT-TM                 PIC  X(08)  VALUE SPACE.          
014800     05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
014900                                                    COMP SYNC.            
015000     05  PV-SUB                         PIC S9(04)  VALUE +0              
015100                                                    COMP SYNC.            
015200     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
015300                                                    COMP SYNC.            
015400     05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
015500                                                    COMP-3.               
015600     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
015700                                                    COMP-3.               
015800     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
015900                                                    COMP-3.               
016000     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
016100                                                    COMP-3.               
016200     05  PV-ITEM-IN-USE                 PIC S9(04)  VALUE +0              
016300                                                    COMP SYNC.            
016400     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
016500                                                    COMP-3.               
016600     05  PV-ITEM                        PIC S9(04)  VALUE +0              
016700                                                    COMP SYNC.            
016800     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
016900                                                    COMP SYNC.            
017000     05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
017100                                                    COMP SYNC.            
017200     05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
017300                                                    COMP SYNC.            
017400     05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
017500                                                    COMP SYNC.            
017600     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
017700                                                    COMP SYNC.            
017800     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
017900                                                    COMP SYNC.            
018000     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
018100                                                    COMP SYNC.            
018200     05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
018300                                                    COMP SYNC.            
018301                                                                          
018310     05  PV-CURR-TIMESTAMP              PIC  X(26)  VALUE SPACES.         
018311     05  PV-STR-GP-CDE                  PIC  X(01)  VALUE SPACES.         
018320                                                                          
018400     05  PV-DISP-ERR-INFO.                                                
018500         10  PV-DISP-SLS-DTE            PIC  X(10)  VALUE SPACE.          
018600         10  PV-DISP-STR                PIC  X(04)  VALUE SPACE.          
018700         10  PV-DISP-RGST-ID            PIC  X(04)  VALUE SPACE.          
018800         10  PV-DISP-TRN-NBR            PIC  X(04)  VALUE SPACE.          
018900                                                                          
019000     05  PV-SLS-DTE-FORMATTED.                                            
019100         10  PV-SLS-DTE-FORMATTED-MM    PIC X(02)  VALUE SPACES.          
019200         10  FILLER                     PIC X(01)  VALUE '/'.             
019300         10  PV-SLS-DTE-FORMATTED-DD    PIC X(02)  VALUE SPACES.          
019400         10  FILLER                     PIC X(01)  VALUE '/'.             
019500         10  PV-SLS-DTE-FORMATTED-YYYY  PIC X(04)  VALUE SPACES.          
019600     05  PV-SLS-DTE-ISO.                                                  
019700         10  PV-SLS-DTE-ISO-YYYY        PIC X(04)  VALUE SPACES.          
019800         10  FILLER                     PIC X(01)  VALUE '-'.             
019900         10  PV-SLS-DTE-ISO-MM          PIC X(02)  VALUE SPACES.          
020000         10  FILLER                     PIC X(01)  VALUE '-'.             
020100         10  PV-SLS-DTE-ISO-DD          PIC X(02)  VALUE SPACES.          
020200                                                                          
020300*----------------------------------------------------------------*        
020400*   THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.    *        
020500*   THIS MUST BE THE EXACT SAME FORMAT AS A BLOCK IN THE         *        
020600*   APPLICATION-SPECIFIC COMMAREA.                               *        
020700*----------------------------------------------------------------*        
020800 01  LQW-LIST-QUEUE-WORK-AREA.                                            
020900     05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
021000     05  LQW-ITEM-ENTRIES               OCCURS 12 TIMES                   
021100                                        INDEXED BY LQW-IDX.               
021200         10  LQW-LIST-ITEM-NUMBER       PIC S9(07)  COMP-3.               
021300         10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
021400         10  LQW-SELECTED-SW            PIC  X(01).                       
021500             88  LQW-SELECT                         VALUE 'S'.            
021600             88  LQW-DESELECT                       VALUE ' '.            
021700         10  LQW-ACTION                 PIC  X(03).                       
021800         10  LQW-PARTITION-NBR          PIC S9(04)  COMP.                 
021900         10  LQW-SALES-DATE             PIC  X(10).                       
022000         10  LQW-STORE                  PIC S9(04)  COMP.                 
022100         10  LQW-REGISTER               PIC S9(04)  COMP.                 
022200         10  LQW-TRANSACTION            PIC S9(04)  COMP.                 
022300         10  LQW-START-TIME             PIC  X(08).                       
022400         10  LQW-REGISTER-OPERATOR      PIC  9(06)  COMP-3.               
022500         10  LQW-TYPE                   PIC  X(02).                       
022600         10  LQW-ONLINE-IND             PIC  X(01).                       
022700         10  LQW-AMOUNT                 PIC S9(07)V9(02)                  
022800                                                    COMP-3.               
022900         10  LQW-SALES-CORR-SEQ-NBR     PIC S9(04)  COMP.                 
023000         10  LQW-SOURCE-TEXT            PIC  X(03).                       
023100         10  LQW-STATUS                 PIC  X(02).                       
023200         10  LQW-STATUS-TEXT            PIC  X(12).                       
023300         10  LQW-VOID-CODE              PIC  X(01).                       
023400         10  LQW-VOID-CODE-TEXT         PIC  X(12).                       
023500                                                                          
023600                                                                          
023700*---------------------------------------------------------------*         
023800*  RECORD LAYOUT FOR THE TRANSACTION SELECTION TEMP STORAGE Q.  *         
023900*---------------------------------------------------------------*         
024000                                                                          
024100     COPY SAWS001.                                                        
024200                                                                          
024300*---------------------------------------------------------------*         
024400*    MAP LAYOUT                                                 *         
024500*---------------------------------------------------------------*         
024600                                                                          
024700     COPY SAKM429.                                                        
024800                                                                          
024900 01  MR-OCCURS-LAYOUT                   REDEFINES  SA429AO.               
025000     05  FILLER                         PIC X(132).                       
025100     05  MR-SCROLL-AREA                 OCCURS 12 TIMES.                  
025200         10  MR-SELECTL                 PIC S9(04) COMP.                  
025300         10  MR-SELECTA                 PIC  X(01).                       
025400         10  MR-SELECTC                 PIC  X(01).                       
025500         10  MR-SELECTH                 PIC  X(01).                       
025600         10  MR-SELECTI                 PIC  X(01).                       
025700                                                                          
025800         10  MR-ACTIONL                 PIC S9(04) COMP.                  
025900         10  MR-ACTIONA                 PIC  X(01).                       
026000         10  MR-ACTIONC                 PIC  X(01).                       
026100         10  MR-ACTIONH                 PIC  X(01).                       
026200         10  MR-ACTION                  PIC  X(03).                       
026300                                                                          
026400         10  MR-DATEL                   PIC S9(04) COMP.                  
026500         10  MR-DATEA                   PIC  X(01).                       
026600         10  MR-DATEC                   PIC  X(01).                       
026700         10  MR-DATEH                   PIC  X(01).                       
026800         10  MR-DATE                    PIC  X(10).                       
026900                                                                          
027000         10  MR-STOREL                  PIC S9(04) COMP.                  
027100         10  MR-STOREA                  PIC  X(01).                       
027200         10  MR-STOREC                  PIC  X(01).                       
027300         10  MR-STOREH                  PIC  X(01).                       
027400         10  MR-STORE-X.                                                  
027500             15  MR-STORE               PIC  9(04).                       
027600                                                                          
027700         10  MR-REGL                    PIC S9(04) COMP.                  
027800         10  MR-REGA                    PIC  X(01).                       
027900         10  MR-REGC                    PIC  X(01).                       
028000         10  MR-REGH                    PIC  X(01).                       
028100         10  MR-REG-X.                                                    
028200             15  MR-REG                 PIC  9(04).                       
028300                                                                          
028400         10  MR-TRANSL                  PIC S9(04) COMP.                  
028500         10  MR-TRANSA                  PIC  X(01).                       
028600         10  MR-TRNASC                  PIC  X(01).                       
028700         10  MR-TRANSH                  PIC  X(01).                       
028800         10  MR-TRANS-X.                                                  
028900             15  MR-TRANS               PIC  9(04).                       
029000                                                                          
029100         10  MR-VAR-AREAL               PIC S9(04) COMP.                  
029200         10  MR-VAR-AREAA               PIC  X(01).                       
029300         10  MR-VAR-AREAC               PIC  X(01).                       
029400         10  MR-VAR-AREAH               PIC  X(01).                       
029500         10  MR-VAR-AREA                PIC  X(36).                       
029600                                                                          
029700*---------------------------------------------------------------*         
029800*  SCROLLABLE AREA OF SCREEN                                    *         
029900*---------------------------------------------------------------*         
030000 01  DL-DETAIL-LINE-LEFT.                                                 
NEMCHG     05  DL-REG-OP-LEFT                 PIC  9(07) VALUE ZERO.            
NEMCHG     05  FILLER                         PIC  X(02) VALUE SPACES.          
030300     05  DL-TYPE-LEFT                   PIC  X(02) VALUE SPACES.          
030400     05  FILLER                         PIC  X(05) VALUE SPACES.          
030500     05  DL-ONLINE-IND-LEFT             PIC  X(01) VALUE SPACES.          
030600     05  FILLER                         PIC  X(04) VALUE SPACES.          
030700     05  DL-VOID-IND-LEFT               PIC  X(01) VALUE SPACES.          
030800     05  FILLER                         PIC  X(01) VALUE SPACES.          
030900     05  DL-TRAN-AMOUNT-LEFT            PIC  Z,ZZZ,ZZ9.99-.               
031000                                                                          
031100 01  DL-DETAIL-LINE-RIGHT REDEFINES DL-DETAIL-LINE-LEFT.                  
031200     05  DL-TRAN-SRC-RIGHT              PIC  X(03).                       
031300     05  FILLER                         PIC  X(02).                       
031400     05  DL-STATUS-RIGHT                PIC  X(13).                       
031500     05  FILLER                         PIC  X(02).                       
031600     05  DL-VOID-INFO-RIGHT             PIC  X(13).                       
031700     05  FILLER                         PIC  X(03).                       
031800                                                                          
031900 01  HL-HEADING-LINES.                                                    
032000     05  HL-HEADING-LINE-LEFT           PIC  X(36)  VALUE                 
032100       'REG OP  TYPE  LINE VOID   AMOUNT    '.                            
032200     05  HL-HEADING-LINE-RIGHT          PIC  X(36)  VALUE                 
032300       'SRC  STATUS         VOID INFO       '.                            
032400                                                                          
032500*---------------------------------------------------------------*         
032600*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
032700*---------------------------------------------------------------*         
032800                                                                          
032900     COPY DPWS015.                                                        
033000                                                                          
033100*---------------------------------------------------------------*         
033200*    FUNCTION KEYS COPYBOOK.                                    *         
033300*---------------------------------------------------------------*         
033400                                                                          
033500     COPY DPWS016.                                                        
033600                                                                          
033700*---------------------------------------------------------------*         
033800*    NUMERIC EDIT LAYOUT.                                       *         
033900*---------------------------------------------------------------*         
034000                                                                          
034100     COPY DPWS010I.                                                       
034200                                                                          
034300*---------------------------------------------------------------*         
CICS  *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
034500*---------------------------------------------------------------*         
034600                                                                          
034700     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
034800                                                                          
034900*---------------------------------------------------------------*         
035000*    STANDARD COMMAREA.                                         *         
035100*---------------------------------------------------------------*         
035200                                                                          
035300     COPY DPWS020.                                                        
035400     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
035500                                                                          
035600*---------------------------------------------------------------*         
035700* INTER-APPLICATION COMMAREA.                                   *         
035800*   SAWS004 - IS THE INPUT LAYOUT COMING FROM EJ TRANSACTION    *         
035900*             LIST SCREEN (SAKCS426).                           *         
036000*   SAWS004 - IS ALSO THE OUTPUT LAYOUT THAT WILL BE PASSED     *         
036100*             FROM THE LIST GENERATED BY THIS PROGRAM FOR A     *         
036200*             LOCAL-GO FUNCTION.                                *         
036300*---------------------------------------------------------------*         
036400                                                                          
036500         10  INTER-APPL-COMM-AREA       PIC X(200).                       
036600     COPY SAWS004.                                                        
036700                                                                          
036800*---------------------------------------------------------------*         
036900*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
037000*---------------------------------------------------------------*         
037100         10  ASC-SPECIFIC-COMMAREA.                                       
037200             15  ASC-LIST-KEYS.                                           
037300                 20  ASC-KEY-SALES-DATE-ISO      PIC  X(10).              
037400                 20  ASC-KEY-SALES-DATE-DISPLAY  PIC  X(10).              
037500                 20  ASC-KEY-STORE               PIC S9(04)               
037600                                                 COMP.                    
037700                 20  ASC-KEY-REGISTER            PIC S9(04)               
037800                                                 COMP.                    
037900                 20  ASC-KEY-TRANSACTION         PIC S9(04)               
038000                                                 COMP.                    
038100                 20  ASC-KEY-TRANSACTION-AMOUNT  PIC S9(07)V9(02)         
038200                                                 COMP-3.                  
038300                 20  ASC-KEY-OPERATOR-ID         PIC S9(09)               
038400                                                 COMP.                    
038500                 20  ASC-KEY-TENDER-ID           PIC  X(33).              
038600                 20  ASC-KEY-TENDER-TYPE         PIC  X(02).              
038700                 20  ASC-KEY-KEYED-TENDER-IND    PIC  X(01).              
038800                 20  ASC-KEY-TRANSACTION-TYPE    PIC  X(02).              
038900                 20  ASC-KEY-VOID-TRANSACTION-IND                         
039000                                                 PIC  X(01).              
039100                 20  ASC-KEY-PARTN-NBR           PIC S9(04) COMP.         
039200                 20  ASC-KEY-STRUC-CDE           PIC  X(01).              
039300                 20  ASC-SAME-DATE-SW            PIC  X(01).              
039400                 20  ASC-KEY-SLS-CORR-SEQ-NBR    PIC S9(04) COMP.         
039500                 20  ASC-KEY-START-TIME          PIC  X(08).              
039600             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
039700                 20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
039800                     88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
039900                     88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
040000                                                                          
040100                 20  ASC-ITEM-ARRAY.                                      
040200                     25  ASC-ITEM-ENTRIES OCCURS 12 TIMES.                
040300                         30  ASC-LIST-ITEM-NUMBER                         
040400                                                     PIC S9(07)           
040500                                                     COMP-3.              
040600                         30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
040700                             88  ASC-VALID-SELECT    VALUE ' '            
040800                                                           'S'            
040900                                                           'B'            
041000                                                           'E'.           
041100                             88  ASC-TEMP-NO-SELECT  VALUE ' '.           
041200                             88  ASC-TEMP-SELECT     VALUE 'S'.           
041300                             88  ASC-TEMP-BEGIN      VALUE 'B'.           
041400                             88  ASC-TEMP-END        VALUE 'E'.           
041500                         30  ASC-SELECTED-SW         PIC  X(01).          
041600                             88  ASC-NO-SELECT       VALUE ' '.           
041700                             88  ASC-SELECT          VALUE 'S'.           
041800                             88  ASC-BEGIN           VALUE 'B'.           
041900                             88  ASC-END             VALUE 'E'.           
042000                         30  ASC-ACTION              PIC  X(03).          
042100                         30  ASC-PARTITION-NBR       PIC S9(04)           
042200                                                         COMP.            
042300                         30  ASC-SALES-DATE          PIC  X(10).          
042400                         30  ASC-STORE               PIC S9(04)           
042500                                                          COMP.           
042600                         30  ASC-REGISTER            PIC S9(04)           
042700                                                          COMP.           
042800                         30  ASC-TRANSACTION         PIC S9(04)           
042900                                                          COMP.           
043000                         30  ASC-START-TIME          PIC X(08).           
NEMCHG                         30  ASC-REGISTER-OPERATOR   PIC 9(07)            
043200                                                          COMP-3.         
043300                         30  ASC-TYPE                PIC  X(02).          
043400                         30  ASC-ONLINE-IND          PIC  X(01).          
043500                         30  ASC-AMOUNT          PIC S9(07)V9(02)         
043600                                                          COMP-3.         
043700                         30  ASC-SALES-CORR-SEQ-NBR  PIC S9(04)           
043800                                                           COMP.          
043900                         30  ASC-SOURCE-TEXT         PIC  X(03).          
044000                         30  ASC-STATUS              PIC  X(02).          
044100                             88 ASC-STAT-VALID-CORR  VALUE 'V'.           
044200                             88 ASC-STAT-VALID-POS   VALUE 'Z'.           
044300                             88 ASC-STAT-MIS-TOT     VALUE 'S'.           
044400                             88 ASC-STAT-MIS-TND     VALUE 'T'.           
044500                             88 ASC-STAT-OUT-BAL-TND VALUE 'B'.           
044600                             88 ASC-STAT-OUT-BAL-ITM VALUE 'O'.           
044700                             88 ASC-STAT-NON-NUM-ACT VALUE 'N'.           
044800                             88 ASC-STAT-MIS-ACT     VALUE 'M'.           
044900                             88 ASC-STAT-INVAL-COR   VALUE 'W'.           
045000                             88 ASC-STAT-INVAL-PRE   VALUE 'P'.           
045100                             88 ASC-STAT-INVAL-C-D   VALUE 'C'.           
045200                             88 ASC-STAT-INVAL-STR   VALUE 'I'.           
045300                             88 ASC-STAT-REVERSAL    VALUE 'R'.           
045400                             88 ASC-STAT-LATE-DC     VALUE 'L'.           
045500                             88 ASC-STAT-INVAL-HD-TOT                     
045600                                                     VALUE 'H'.           
045700                             88 ASC-STAT-ERROR-GEN   VALUE 'E'.           
045800                             88 ASC-STAT-INCOMP-DATA VALUE 'D'.           
045900                         30  ASC-STATUS-TEXT         PIC  X(12).          
046000                         30  ASC-VOID-CODE           PIC  X(01).          
046100                             88 ASC-VOID-AFTER       VALUE 'V'.           
046200                             88 ASC-VOID-DURING      VALUE 'A'.           
046300                             88 ASC-VOID-AFTER-DURING                     
046400                                                     VALUE 'I'.           
046500                             88 ASC-VOID-DURING-AFTER                     
046600                                                     VALUE 'B'.           
046700                             88 ASC-VOID-REVERSED    VALUE 'R'.           
046800                             88 ASC-VOID-UNCORRECTED VALUE 'U'.           
046900                             88 ASC-VOID-NORMAL      VALUE 'N'.           
047000                         30  ASC-VOID-CODE-TEXT      PIC  X(12).          
047100             15  ASC-BLK-SUB                         PIC S9(04)           
047200                                                          COMP            
047300                                                          SYNC.           
047400             15  ASC-ITEM-SUB                        PIC S9(04)           
047500                                                          COMP            
047600                                                          SYNC.           
047700             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
047800                                                          COMP-3.         
047900             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
048000                                                          COMP-3.         
048100             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
048200                                                          COMP-3.         
048300             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
048400                                                          COMP-3.         
048500             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
048600                                                          COMP-3.         
048700             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
048800                                                          COMP            
048900                                                          SYNC.           
049000             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
049100                                                          COMP-3.         
049200             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
049300                                                          COMP-3.         
049400             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
049500                 88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
049600                 88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
049700             15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
049800                 88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
049900                 88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
050000             15  ASC-LIST-QUEUE-NAME.                                     
050100                 20  ASC-LIST-TERM-ID                PIC  X(04).          
050200                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
050300                                                          COMP-3.         
050400                 20  ASC-LIST-SEQ                    PIC  9(01).          
050500             15  ASC-SEL-QUEUE-NAME.                                      
050600                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
050700                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
050800                                                          COMP-3.         
050900                 20  ASC-SEL-SEQ                     PIC  9(01).          
051000             15  ASC-SEL2-QUEUE-NAME.                                     
051100                 20  ASC-SEL2-TERM-ID                PIC  X(04).          
051200                 20  ASC-SEL2-TASK-NUMBER            PIC S9(05)           
051300                                                          COMP-3.         
051400                 20  ASC-SEL2-SEQ                    PIC  9(01).          
051500                                                                          
051600             15  ASC-START-ITEM-REQUEST.                                  
051700                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
051800             15  ASC-VIEW-IND                        PIC  X(01).          
051900                 88  ASC-LEFT-VIEW                   VALUE 'L'.           
052000                 88  ASC-RIGHT-VIEW                  VALUE 'R'.           
052100             15  ASC-TRAN-SEL-QUEUE-NAME             PIC  X(08).          
052200             15  ASC-TRAN-SEL-ITEM                   PIC S9(04)           
052300                                                     COMP SYNC.           
052400             15  ASC-TRAN-SEL-SUB                    PIC  9(02).          
052500             15  ASC-NBR-OF-TRAN-TS-ITEMS            PIC S9(05)           
052600                                                     COMP-3.              
052700         10  FILLER                                  PIC  X(793).         
052800                                                                          
052900*---------------------------------------------------------------*         
053000*    ABEND PROCESSING COPYBOOK.                                 *         
053100*---------------------------------------------------------------*         
053200                                                                          
053300     COPY DPWS013.                                                        
053400                                                                          
053500*---------------------------------------------------------------*         
053600*    DB2 AREA FOR TEPTRN (TRANSACTION TABLE)                    *         
053700*---------------------------------------------------------------*         
053800     EXEC SQL                                                             
053900          INCLUDE TEPTRN                                                  
054000     END-EXEC.                                                            
054100                                                                          
054900*---------------------------------------------------------------*         
055000*    DB2 AREA FOR TEPPART (PARTITION TABLE)                     *         
055100*---------------------------------------------------------------*         
055200     EXEC SQL                                                             
055300          INCLUDE TEPPART                                                 
055400     END-EXEC.                                                            
055500                                                                          
055600*---------------------------------------------------------------*         
055700*    DB2 AREA FOR TEPRKY  (RETURN KEY INFORMATION TABLE)        *         
055800*---------------------------------------------------------------*         
055900     EXEC SQL                                                             
056000          INCLUDE TEPRKY                                                  
056100     END-EXEC.                                                            
056200                                                                          
057000*---------------------------------------------------------------*         
057100*    DB2 AREA FOR COMMUNICATIONS                                *         
057200*---------------------------------------------------------------*         
057300     EXEC SQL                                                             
057400          INCLUDE SQLCA                                                   
057500     END-EXEC.                                                            
057600                                                                          
057700*---------------------------------------------------------------*         
057800*    RETURNS-CSR CURSOR -- THIS CURSOR WILL RETRIEVE ALL RETURN *         
057900*    TRANSACTIONS AGAINST THE ORIGINAL TRANSACTION.             *         
058000*---------------------------------------------------------------*         
058100                                                                          
058200     EXEC SQL                                                             
058300          DECLARE RETURNS-CSR CURSOR                                      
058400          FOR SELECT                                                      
058500                RET_SLS_DTE                                               
058600              , RET_STR_NBR                                               
058700              , RET_RGST_ID                                               
058800              , RET_TRN_NBR                                               
058900              , RET_STRT_TM                                               
059000              , RET_SEQ_NBR                                               
059100          FROM  TEPRKY                                                    
059200         WHERE  PARTN_NBR              = :DK-PARTITION                    
059300          AND   STR_NBR                = :DK-STORE                        
059400          AND   RGST_ID                = :DK-REGISTER                     
059500          AND   TRN_NBR                = :DK-TRANS                        
059600          AND   STRT_TM                = :DK-START-TIME                   
059700          AND   SLS_CORR_SEQ_NBR       = :DK-SALES-CORR-SEQ-NBR           
059800       ORDER BY RET_SEQ_NBR                                               
059900        WITH UR                                                           
060000     END-EXEC.                                                            
060100                                                                          
062900 LINKAGE SECTION.                                                         
063000                                                                          
063100 01  DFHCOMMAREA.                                                         
063200     05  FILLER                         OCCURS  1 TO 4072 TIMES           
063300                                        DEPENDING ON EIBCALEN.            
063400         10  FILLER                     PIC X.                            
063500                                                                          
063600 PROCEDURE DIVISION.                                                      
063700*----------------------------------------------------------------*        
063800*   THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM.  *        
063900*   FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM   *        
064000*   PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT.  *        
064100*----------------------------------------------------------------*        
064200 0000-MAIN-MODULE.                                                        
064300     INITIALIZE DP030-CICS-API-FIELDS.                                    
064400     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
064500     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
064600     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
064700     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
064800     MOVE LENGTH OF SA429AI        TO DP030-MAP-LENGTH (1).               
064900     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
065000     PERFORM 0001-CALL-CICS-ARCH-API.                                     
065100                                                                          
065200     PERFORM 1000-CONTROL-PROCESSING                                      
065300                                                                          
065400     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
065500     PERFORM 0001-CALL-CICS-ARCH-API.                                     
065600                                                                          
065700*----------------------------------------------------------------*        
065800*   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *        
065900*   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *        
066000*----------------------------------------------------------------*        
066100 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
066300                                    DFHCOMMAREA                           
066400                                    DP030-CICS-API-FIELDS                 
066500                                    DP020-STANDARD-COMMAREA               
066600                                    SA429AI.                              
066700                                                                          
066800     IF  DP030-RC-CALL-SUCCESSFUL                                         
066900         CONTINUE                                                         
067000     ELSE                                                                 
067100         SET DP013-NO-ROLLBACK                                            
067200             DP013-XCTL-DISPLAY-RESTART                                   
067300             DP013-CICS-ABEND      TO TRUE                                
067400         MOVE '0001-CALL-CICS-ARCH-API'                                   
067500                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
067800         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
067900         PERFORM DP013-0000-PROCESS-ABEND                                 
068000     END-IF.                                                              
068100*----------------------------------------------------------------*        
068200* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
068300* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
068400*                                                                *        
068500* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
068600*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
068700*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
068800*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
068900*----------------------------------------------------------------*        
069000                                                                          
069100 1000-CONTROL-PROCESSING.                                                 
069200                                                                          
069300     EVALUATE TRUE                                                        
069400         WHEN DP020-NEXT-ACT-INITIAL                                      
069500             INITIALIZE ASC-SPECIFIC-COMMAREA                             
069600             SET ASC-LEFT-VIEW     TO TRUE                                
069700             MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
069800                                      ASC-SEL-TERM-ID                     
069900                                      ASC-SEL2-TERM-ID                    
070000             MOVE DP020-SRC-TASK-NUMBER                                   
070100                                   TO ASC-LIST-TASK-NUMBER                
070200                                      ASC-SEL-TASK-NUMBER                 
070300                                      ASC-SEL2-TASK-NUMBER                
070400             MOVE PC-LIST-QUEUE-SEQ                                       
070500                                   TO ASC-LIST-SEQ                        
070600             MOVE PC-SEL-QUEUE-SEQ                                        
070700                                   TO ASC-SEL-SEQ                         
070800             MOVE PC-SEL2-QUEUE-SEQ                                       
070900                                   TO ASC-SEL2-SEQ                        
071000             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
071100***-----$TEMPLATE NOTE$-------------------------------------------        
071200*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
071300*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
071400*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
071500***---------------------------------------------------------------        
071600             IF  PS-COMMAREA-NOT-VALID                                    
071700                 SET DP020-NEXT-ACT-APPL-ERROR                            
071800                                   TO TRUE                                
071900             ELSE                                                         
072000                 SET SA001-INQ (ASC-TRAN-SEL-SUB)                         
072100                                   TO TRUE                                
072200                 PERFORM 9200-REWRITE-SEL-QUEUE                           
072300                 PERFORM 4000-BUILD-INITIAL-PANEL                         
072400             END-IF                                                       
072500                                                                          
072501         WHEN DP020-NEXT-ACT-READ-MAP                                     
072502             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
072503                                   TO PV-TOP-ITEM                         
072504             PERFORM 2000-PROCESS-PANEL                                   
072505                                                                          
072506         WHEN DP020-NEXT-ACT-RETURN                                       
072507             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
072508                                   TO PV-TOP-ITEM                         
072509             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
072510             PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
072520             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
072530             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
072540                                                                          
072550         WHEN OTHER                                                       
072560             SET DP013-LOGIC-ABEND TO TRUE                                
072570             SET DP013-NO-ROLLBACK TO TRUE                                
072580             MOVE '1000-CONTROL-PROCESSING'                               
072590                                   TO DP013-PARAGRAPH                     
072600             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
072700                                   TO DP013-MESSAGE-TEXT(1)               
072800             PERFORM DP013-0000-PROCESS-ABEND                             
072900     END-EVALUATE.                                                        
072901                                                                          
072902*----------------------------------------------------------------*        
072903* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
072904* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
072905*----------------------------------------------------------------*        
072906                                                                          
072907 1100-PROCESS-INTER-APPL-COMM.                                            
072908                                                                          
072909     SET PS-COMMAREA-VALID     TO TRUE.                                   
072910     MOVE SPACE                TO ASC-START-ITEM-REQUEST.                 
072911     EVALUATE TRUE                                                        
072912         WHEN DP020-INT-APPL-FORMAT-IND                                   
072913                                   = PC-LIST-QUEUE-SEQ                    
072914             MOVE SA004-NUMBER-OF-SELECTED-ITEMS                          
072915                                   TO ASC-NUMBER-OF-SELECTED-ITEMS        
072916             MOVE SA004-NUMBER-OF-TS-ITEMS                                
072917                                   TO ASC-NBR-OF-TRAN-TS-ITEMS            
072918             MOVE SA004-SEL-QUEUE-NAME                                    
072919                                   TO ASC-TRAN-SEL-QUEUE-NAME             
072920             MOVE SA004-SALES-DATE-ISO                                    
072930                                   TO ASC-KEY-SALES-DATE-ISO              
072931                                      DK-SALES-DATE                       
072932             MOVE SA004-SALES-DATE-DISPLAY                                
072933                                   TO ASC-KEY-SALES-DATE-DISPLAY          
072934             PERFORM 1120-PREPARE-LIST-ENTRY                              
072935                                                                          
072936         WHEN OTHER                                                       
072937             SET DP013-LOGIC-ABEND TO TRUE                                
072938             SET DP013-NO-ROLLBACK TO TRUE                                
072939             MOVE '1100-PROCESS-INTER-APPL-COMM'                          
072940                                   TO DP013-PARAGRAPH                     
072941             MOVE 'INVALID INTER APPL FORMAT INDICATOR'                   
072942                                   TO DP013-MESSAGE-TEXT(1)               
072943             PERFORM DP013-0000-PROCESS-ABEND                             
072944                                                                          
072945     END-EVALUATE.                                                        
072946                                                                          
072947*----------------------------------------------------------------*        
072948*    GET READY TO START AT THE FIRST ITEM ON THE LIST.           *        
072949*----------------------------------------------------------------*        
072950                                                                          
072951 1120-PREPARE-LIST-ENTRY.                                                 
072952                                                                          
072953     MOVE SA001-MAX-ITEMS TO ASC-TRAN-SEL-SUB.                            
072954     MOVE ZERO            TO ASC-TRAN-SEL-ITEM.                           
072955     PERFORM 9100-READ-TRAN-SEL-QUEUE.                                    
072956     MOVE SA001-PARTITION (ASC-TRAN-SEL-SUB)                              
072957                          TO DK-PARTITION                                 
072958                             ASC-KEY-PARTN-NBR.                           
072959     MOVE SA001-STORE (ASC-TRAN-SEL-SUB)                                  
072960                          TO DK-STORE                                     
072970                             ASC-KEY-STORE.                               
072980     MOVE SA001-REGISTER (ASC-TRAN-SEL-SUB)                               
072990                          TO DK-REGISTER                                  
073000                             ASC-KEY-REGISTER.                            
073100     MOVE SA001-TRANS (ASC-TRAN-SEL-SUB)                                  
073200                          TO DK-TRANS                                     
073300                             ASC-KEY-TRANSACTION.                         
073400     MOVE SA001-SALES-CORR-SEQ-NBR (ASC-TRAN-SEL-SUB)                     
073500                          TO DK-SALES-CORR-SEQ-NBR                        
073600                             ASC-KEY-SLS-CORR-SEQ-NBR.                    
073700     MOVE SA001-START-TIME (ASC-TRAN-SEL-SUB)                             
073800                          TO DK-START-TIME                                
073900                             ASC-KEY-START-TIME.                          
074000                                                                          
074100*----------------------------------------------------------------*        
074200* DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITING OF THE DATA    *        
074300* ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
074400* USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
074500* OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
074600* FUNCTION KEYS CAN GET THIS FAR.                                *        
074700*----------------------------------------------------------------*        
074800                                                                          
074900 2000-PROCESS-PANEL.                                                      
075000     EVALUATE TRUE                                                        
075100         WHEN DP020-SRC-AID = DP016-CLEAR                                 
075200             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
075300             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
075400             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
075500                                                                          
075600         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
075700                                                                          
075800*** THE REFRESH KEY JUST DOES A MOVE IN THIS CASE.  THIS IS OK            
075900*** BECAUSE THE DATA THAT THE SCREEN DISPLAYS DOES NOT CHANGE.            
076000*** THERE IS NO NEED TO HAVE DB2 DO ALL THE WORK WHEN IT IS NOT           
076100*** NECESSARY.                                                            
076200                                                                          
076300             SET ASC-LEFT-VIEW     TO TRUE                                
076400             MOVE +1               TO PV-TOP-ITEM                         
076500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
076600                                                                          
076700         WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
076800             PERFORM 2140-DESELECT-ALL-ITEMS                              
076900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
077000                                                                          
077100         WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
077200             PERFORM 2150-SELECT-ALL-ITEMS                                
077300             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
077400                                                                          
077500         WHEN OTHER                                                       
077600             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
077700             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
077800             IF  PS-NO-ERROR                                              
077900                 SET DP030-CONTINUE-GO                                    
078000                                 TO TRUE                                  
078100                 PERFORM 2100-CHECK-FUNCTION-KEY                          
078200             ELSE                                                         
078300                 SET DP030-OVERRIDE-GO                                    
078400                                 TO TRUE                                  
078500             END-IF                                                       
078600     END-EVALUATE.                                                        
078700                                                                          
078800*----------------------------------------------------------------*        
078900*  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
079000*----------------------------------------------------------------*        
079100 2050-DELETE-LIST-QUEUE.                                                  
079200                                                                          
079300     EXEC CICS                                                            
079400         DELETEQ TS                                                       
079500             QUEUE (ASC-LIST-QUEUE-NAME)                                  
079600             RESP  (PV-CICS-RESP)                                         
079700     END-EXEC.                                                            
079800                                                                          
079900     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
080000        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
080100         CONTINUE                                                         
080200     ELSE                                                                 
080300         SET DP013-CICS-ABEND      TO TRUE                                
080400         SET DP013-NO-ROLLBACK     TO TRUE                                
080500                                                                          
080600         MOVE '2050-DELETE-LIST-QUEUE'                                    
080700                                   TO DP013-PARAGRAPH                     
080800         MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
080900                                   TO DP013-MESSAGE-TEXT (1)              
081000         PERFORM DP013-0000-PROCESS-ABEND                                 
081100     END-IF.                                                              
081200                                                                          
081300     MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
081400                                                                          
081500*----------------------------------------------------------------*        
081600* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
081700* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
081800* CICS ARCHITECTURE API.                                         *        
081900*----------------------------------------------------------------*        
082000 2100-CHECK-FUNCTION-KEY.                                                 
082100     EVALUATE TRUE                                                        
082200         WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
082300         WHEN DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID)                       
082300         WHEN DP020-FK-LOCAL-FUNC-03(DP020-SRC-AID)                       
082400                                                                          
082500             IF  ASC-NUMBER-OF-SELECTED-ITEMS = +1                        
082600                 INITIALIZE SA004-INTER-APPL-COMMAREA                     
082700                 MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
082800                                TO SA004-NUMBER-OF-SELECTED-ITEMS         
082900                 PERFORM 5000-PREPARE-SEL-QUEUE                           
083000             ELSE                                                         
083100                 IF ASC-NUMBER-OF-SELECTED-ITEMS > +1                     
083200                     SET DP030-OVERRIDE-GO                                
083300                                   TO TRUE                                
083400                     MOVE PC-TSYMSG-00047                                 
083500                                   TO DP020-MSG-NUMBER                    
083600                     SET DP020-MSG-FATAL                                  
083700                                   TO TRUE                                
083800                 ELSE                                                     
083900                     SET DP030-OVERRIDE-GO                                
084000                                   TO TRUE                                
084100                     MOVE PC-TSYMSG-00057                                 
084200                                   TO DP020-MSG-NUMBER                    
084300                     SET DP020-MSG-WARNING                                
084400                                   TO TRUE                                
084500                 END-IF                                                   
084600             END-IF                                                       
084700                                                                          
084800         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
084900             MOVE +1               TO PV-TOP-ITEM                         
085000             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
085100                                                                          
085200         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
085300             PERFORM 2110-BROWSE-BACKWARD                                 
085400             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
085500                                                                          
085600         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
085700             PERFORM 2120-BROWSE-FORWARD                                  
085800             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
085900                                                                          
086000         WHEN DP020-FK-LEFT (DP020-SRC-AID)                               
086100             SET ASC-LEFT-VIEW  TO TRUE                                   
086200             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
086300                                                                          
086400         WHEN DP020-FK-RIGHT (DP020-SRC-AID)                              
086500             SET ASC-RIGHT-VIEW TO TRUE                                   
086600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
086700                                                                          
086800         WHEN DP020-SRC-AID = DP016-ENTER                                 
086900             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
087000             IF  PS-ERROR                                                 
087100                 CONTINUE                                                 
087200             ELSE                                                         
087300                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
087400             END-IF                                                       
087500                                                                          
087600         WHEN OTHER                                                       
087700             SET DP013-NO-ROLLBACK                                        
087800                 DP013-XCTL-DISPLAY-RESTART                               
087900                 DP013-CICS-ABEND  TO TRUE                                
088000             MOVE '2100-CHECK-FUNCTION-KEY'                               
088100                                   TO DP013-PARAGRAPH                     
088200             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
088300                                   TO DP013-MESSAGE-TEXT (1)              
088400             PERFORM DP013-0000-PROCESS-ABEND                             
088500     END-EVALUATE.                                                        
088600                                                                          
088700*----------------------------------------------------------------*        
088800*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
088900*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
089000*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
089100*    MESSAGE.                                                    *        
089200*----------------------------------------------------------------*        
089300                                                                          
089400 2110-BROWSE-BACKWARD.                                                    
089500     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
089600         GIVING PV-TOP-ITEM.                                              
089700                                                                          
089800     IF  ((PV-TOP-ITEM < +1)                                              
089900       OR (PV-TOP-ITEM = +1))                                             
090000         MOVE +1                   TO PV-TOP-ITEM                         
090100*        -- TOP OF LIST --                                                
090200         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
090300         SET DP020-MSG-INFORMATIONAL TO TRUE                              
090400     END-IF.                                                              
090500                                                                          
090600*----------------------------------------------------------------*        
090700*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
090800*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
090900*    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
091000*----------------------------------------------------------------*        
091100 2120-BROWSE-FORWARD.                                                     
091200                                                                          
091300     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
091400         GIVING PV-TOP-ITEM.                                              
091500     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
091600         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
091700                                   TO PV-TOP-ITEM                         
091800*        -- BOTTOM OF LIST --                                             
091900         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
092000         SET DP020-MSG-INFORMATIONAL                                      
092100                                   TO TRUE                                
092200     ELSE                                                                 
092300         COMPUTE PV-BOTTOM-ITEM =                                         
092400             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
092500         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
092600*            -- BOTTOM OF LIST --                                         
092700             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
092800             SET DP020-MSG-INFORMATIONAL                                  
092900                                   TO TRUE                                
093000         END-IF                                                           
093100     END-IF.                                                              
093200                                                                          
093300*----------------------------------------------------------------*        
093400*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
093500*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
093600*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
093700*    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
093800*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
093900*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
094000*----------------------------------------------------------------*        
094100 2130-CHECK-FOR-JUMP-BROWSE.                                              
094200                                                                          
094300     SET PS-NO-ERROR               TO TRUE.                               
094400     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
094500         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
094600                                   TO PV-TOP-ITEM                         
094700     ELSE                                                                 
094800         MOVE ASC-START-ITEM-REQUEST                                      
094900                                   TO DP010I-UNEDITED-FIELD               
095000         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
095100         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
095200         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
095300                                   TO TRUE                                
139218         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
139218                         USING DP010I-NUMERIC-EDIT-AREA                   
095500                                                                          
095600         IF  NOT DP010I-ERROR-DETECTED                                    
095700             MOVE DP010I-NUMERIC-FIELD                                    
095800                                   TO  ASC-START-ITEM-REQUEST-N           
095900             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
096000                 IF  ASC-START-ITEM-REQUEST-N >                           
096100                                          ASC-NUMBER-OF-ITEMS-READ        
096200                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
096300                     COMPUTE PV-TOP-ITEM =                                
096400                             ASC-NUMBER-OF-ITEMS-READ                     
096500                             - PC-ITEMS-PER-PANEL + 1                     
096600                     IF  PV-TOP-ITEM < +1                                 
096700                         MOVE +1   TO PV-TOP-ITEM                         
096800                         MOVE PC-TSYMSG-00070                             
096900                                   TO DP020-MSG-NUMBER                    
097000                         SET DP020-MSG-INFORMATIONAL                      
097100                                   TO TRUE                                
097200                     END-IF                                               
097300                 ELSE                                                     
097400                     MOVE ASC-START-ITEM-REQUEST-N                        
097500                                   TO PV-TOP-ITEM                         
097600                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
097700                     COMPUTE PV-BOTTOM-ITEM =                             
097800                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
097900                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
098000*                        -- BOTTOM OF LIST --                             
098100                         MOVE PC-TSYMSG-00070                             
098200                                   TO DP020-MSG-NUMBER                    
098300                         SET DP020-MSG-INFORMATIONAL                      
098400                                   TO TRUE                                
098500                     END-IF                                               
098600                 END-IF                                                   
098700             ELSE                                                         
098800*                --- VALUE OUT OF RANGE ----                              
098900                 MOVE PC-TSYMSG-00101                                     
099000                                   TO DP020-MSG-NUMBER                    
099100                 MOVE -1           TO ASTRTITL                            
099200                 SET DP030-SET-CURSOR-APPL-1                              
099300                                   TO TRUE                                
099400                 MOVE DP015-REVERSE                                       
099500                                   TO ASTRTITH                            
099600                 MOVE DP015-RED    TO ASTRTITC                            
099700                 MOVE DP015-UNP-NUM-BRT-MDT                               
099800                                   TO ASTRTITA                            
099900                 SET DP020-MSG-FATAL                                      
100000                                   TO TRUE                                
100100                 SET PS-ERROR      TO TRUE                                
100200             END-IF                                                       
100300                                                                          
100400         ELSE                                                             
100500             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
100600             MOVE -1               TO ASTRTITL                            
100700             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
100800             MOVE DP015-REVERSE    TO ASTRTITH                            
100900             MOVE DP015-RED        TO ASTRTITC                            
101000             MOVE DP015-UNP-NUM-BRT-MDT                                   
101100                                   TO ASTRTITA                            
101200             SET DP020-MSG-FATAL   TO TRUE                                
101300             SET PS-ERROR          TO TRUE                                
101400         END-IF                                                           
101500     END-IF.                                                              
101600                                                                          
101700*----------------------------------------------------------------*        
101800*    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
101900*    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
102000*----------------------------------------------------------------*        
102100 2140-DESELECT-ALL-ITEMS.                                                 
102200                                                                          
102300     MOVE +1                       TO ASC-BLK-SUB.                        
102400                                                                          
102500     PERFORM 2141-CLEAR-SELECTED-INDS                                     
102600         VARYING ASC-ITEM-SUB                                             
102700         FROM 1 BY 1                                                      
102800         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
102900                                                                          
103000     ADD +1                        TO ASC-BLK-SUB.                        
103100                                                                          
103200     PERFORM 2141-CLEAR-SELECTED-INDS                                     
103300         VARYING ASC-ITEM-SUB                                             
103400         FROM 1 BY 1                                                      
103500         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
103600                                                                          
103700     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
103800                                   TO PV-LIST-ITEM-NUMBER.                
103900                                                                          
104000     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
104100         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
104200                                                                          
104300     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
104400        (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
104500         MOVE +1                   TO ASC-BLK-SUB                         
104600                                                                          
104700         PERFORM 5100-WRITE-TEMP-STORAGE                                  
104800                                                                          
104900         MOVE +1                   TO PV-BLOCK-NUMBER                     
105000                                                                          
105100         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
105200             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
105300                                                                          
105400         PERFORM                                                          
105500             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
105600                                                                          
105700             PERFORM 4410-READ-TEMP-STORAGE                               
105800                                                                          
105900             PERFORM 2141-CLEAR-SELECTED-INDS                             
106000                 VARYING ASC-ITEM-SUB FROM 1 BY 1                         
106100                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
106200                                                                          
106300             PERFORM 5100-WRITE-TEMP-STORAGE                              
106400                                                                          
106500             ADD +1                TO PV-BLOCK-NUMBER                     
106600                                                                          
106700         END-PERFORM                                                      
106800                                                                          
106900         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
107000             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
107100                                                                          
107200         PERFORM                                                          
107300             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
107400                   ASC-HIGHEST-BLOCK-WRITTEN                              
107500                                                                          
107600             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
107700                                   TO PV-BLOCK-NUMBER                     
107800                                                                          
107900             PERFORM 4410-READ-TEMP-STORAGE                               
108000                                                                          
108100             PERFORM 2141-CLEAR-SELECTED-INDS                             
108200                 VARYING ASC-ITEM-SUB                                     
108300                 FROM 1 BY 1                                              
108400                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
108500                                                                          
108600             PERFORM 5100-WRITE-TEMP-STORAGE                              
108700                                                                          
108800             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
108900                                                                          
109000         END-PERFORM                                                      
109100                                                                          
109200         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
109300                                   TO PV-BLOCK-NUMBER                     
109400                                                                          
109500         PERFORM 4410-READ-TEMP-STORAGE                                   
109600     END-IF.                                                              
109700                                                                          
109800                                                                          
109900     MOVE +0                      TO ASC-NUMBER-OF-SELECTED-ITEMS.        
110000                                                                          
110100     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
110200                                                                          
110300*----------------------------------------------------------------*        
110400*    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
110500*----------------------------------------------------------------*        
110600 2141-CLEAR-SELECTED-INDS.                                                
110700                                                                          
110800     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
110900             NOT EQUAL ZERO                                               
111000         IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
111100             SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
111200                 ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
111300                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
111400                                   TO TRUE                                
111500         END-IF                                                           
111600     END-IF.                                                              
111700                                                                          
111800*----------------------------------------------------------------*        
111900*  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTION        *        
112000*  SWITCH FOR ALL ITEMS.                                         *        
112100*----------------------------------------------------------------*        
112200 2150-SELECT-ALL-ITEMS.                                                   
112300                                                                          
112400     MOVE +1                       TO ASC-BLK-SUB.                        
112500                                                                          
112600     PERFORM 2151-SET-SELECTED-INDS                                       
112700         VARYING ASC-ITEM-SUB                                             
112800         FROM 1 BY 1                                                      
112900         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
113000                                                                          
113100     ADD +1                        TO ASC-BLK-SUB.                        
113200                                                                          
113300     PERFORM 2151-SET-SELECTED-INDS                                       
113400         VARYING ASC-ITEM-SUB                                             
113500         FROM 1 BY 1                                                      
113600         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
113700                                                                          
113800     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
113900                                   TO PV-LIST-ITEM-NUMBER.                
114000                                                                          
114100     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
114200         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
114300                                                                          
114400     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
114500        (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
114600         MOVE +1                   TO ASC-BLK-SUB                         
114700                                                                          
114800         PERFORM 5100-WRITE-TEMP-STORAGE                                  
114900                                                                          
115000         MOVE +1                   TO PV-BLOCK-NUMBER                     
115100                                                                          
115200         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
115300             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
115400                                                                          
115500         PERFORM                                                          
115600             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
115700                                                                          
115800             PERFORM 4410-READ-TEMP-STORAGE                               
115900                                                                          
116000             PERFORM 2151-SET-SELECTED-INDS                               
116100                 VARYING ASC-ITEM-SUB                                     
116200                 FROM 1 BY 1                                              
116300                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
116400                                                                          
116500             PERFORM 5100-WRITE-TEMP-STORAGE                              
116600                                                                          
116700             ADD +1                TO PV-BLOCK-NUMBER                     
116800                                                                          
116900         END-PERFORM                                                      
117000                                                                          
117100         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
117200             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
117300                                                                          
117400         PERFORM                                                          
117500             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
117600                   ASC-HIGHEST-BLOCK-WRITTEN                              
117700                                                                          
117800             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
117900                                   TO PV-BLOCK-NUMBER                     
118000                                                                          
118100             PERFORM 4410-READ-TEMP-STORAGE                               
118200                                                                          
118300             PERFORM 2151-SET-SELECTED-INDS                               
118400                 VARYING ASC-ITEM-SUB                                     
118500                 FROM 1 BY 1                                              
118600                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
118700                                                                          
118800             PERFORM 5100-WRITE-TEMP-STORAGE                              
118900                                                                          
119000             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
119100                                                                          
119200         END-PERFORM                                                      
119300                                                                          
119400         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
119500                                   TO PV-BLOCK-NUMBER                     
119600                                                                          
119700         PERFORM 4410-READ-TEMP-STORAGE                                   
119800     END-IF.                                                              
119900                                                                          
120000                                                                          
120100     MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
120200                       ASC-NUMBER-OF-SELECTED-ITEMS.                      
120300                                                                          
120400     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
120500                                                                          
120600*----------------------------------------------------------------*        
120700*    MARK EACH ITEM AS BEING SELECTED.                           *        
120800*----------------------------------------------------------------*        
120900 2151-SET-SELECTED-INDS.                                                  
121000                                                                          
121100     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
121200             NOT EQUAL ZERO                                               
121300         IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
121400             SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
121500                 ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
121600                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
121700                               TO  TRUE                                   
121800         END-IF                                                           
121900     END-IF.                                                              
122000*----------------------------------------------------------------*        
122100*  MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE     *        
122200*  SCREEN INTO THE COMMAREA.  ALL EDITING IS DONE IN THE COMM.   *        
122300*----------------------------------------------------------------*        
122400 2200-MOVE-SCREEN-TO-COMMAREA.                                            
122500                                                                          
122600     MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
122700     MOVE +1                       TO PV-SUB.                             
122800                                                                          
122900     PERFORM 3100-SET-SUBSCRIPTS.                                         
123000     IF  ASTRTITL > ZERO                                                  
123100         MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
123200     ELSE                                                                 
123300         IF  ASTRTITF = DP015-ERASE-EOF                                   
123400             INITIALIZE ASC-START-ITEM-REQUEST                            
123500         END-IF                                                           
123600     END-IF.                                                              
123700     PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
123800         VARYING PV-SUB                                                   
123900         FROM 1 BY 1                                                      
124000         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
124100*----------------------------------------------------------------*        
124200*  MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA *        
124300*----------------------------------------------------------------*        
124400 2210-MOVE-LINES-TO-COMMAREA.                                             
124500     IF  MR-SELECTL (PV-SUB) > ZERO                                       
124600         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
124700         MOVE MR-SELECTI (PV-SUB)                                         
124800                                   TO ASC-TEMP-SELECTED-SW                
124900                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
125000     ELSE                                                                 
125100         IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
125200                 SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
125300                 MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
125400                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
125500         END-IF                                                           
125600     END-IF.                                                              
125700     ADD +1                        TO ASC-ITEM-SUB.                       
125800     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
125900         ADD +1                    TO ASC-BLK-SUB                         
126000         MOVE +1                   TO ASC-ITEM-SUB                        
126100     END-IF.                                                              
126200*----------------------------------------------------------------*        
126300*  EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE       *        
126400*  COMMAREA (EDITING IS NOT DONE AGAINST THE SCREEN DIRECTLY).   *        
126500*----------------------------------------------------------------*        
126600 3000-EDIT-DATA-IN-COMMAREA.                                              
126700     SET PS-NO-ERROR               TO TRUE.                               
126800     PERFORM 3200-RESET-ATTRIBUTES.                                       
126900     MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
127000**                                                                        
127100* CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
127200* EDITING IS DONE FROM THE BOTTOM UP.                                     
127300**                                                                        
127400     COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
127500         ASC-ITEMS-DISPLAYED - 1.                                         
127600     PERFORM 3100-SET-SUBSCRIPTS.                                         
127700     PERFORM 3300-EDIT-SELECTIONS                                         
127800         VARYING PV-SUB                                                   
127900         FROM PV-SUB BY -1                                                
128000         UNTIL PV-SUB < +1.                                               
128100                                                                          
128200                                                                          
128300*----------------------------------------------------------------*        
128400*    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
128500*    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
128600*    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
128700*                                                                *        
128800*    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
128900*    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
129000*    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
129100*    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
129200*----------------------------------------------------------------*        
129300                                                                          
129400 3100-SET-SUBSCRIPTS.                                                     
129500                                                                          
129600     COMPUTE ASC-ITEM-SUB =                                               
129700         (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
129800                                                                          
129900     IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
130000         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
130100                                                                          
130200         MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
130300     ELSE                                                                 
130400         MOVE +1                     TO ASC-BLK-SUB                       
130500     END-IF.                                                              
130600*----------------------------------------------------------------*        
130700*  RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.      *        
130800*----------------------------------------------------------------*        
130900                                                                          
131000 3200-RESET-ATTRIBUTES.                                                   
131100     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
131200     MOVE DP015-GREEN              TO ASTRTITC.                           
131300     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
131400                                                                          
131500     PERFORM                                                              
131600         VARYING PV-SUB                                                   
131700         FROM 1 BY 1                                                      
131800         UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
131900         MOVE DP015-UNP-ALP-NOR-OFF                                       
132000                                   TO MR-SELECTA (PV-SUB)                 
132100         MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
132200         MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
132300     END-PERFORM.                                                         
132400*----------------------------------------------------------------*        
132500*  EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS     *        
132600*  WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'    *        
132700*  IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR     *        
132800*  WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.              *        
132900*----------------------------------------------------------------*        
133000 3300-EDIT-SELECTIONS.                                                    
133100     EVALUATE TRUE                                                        
133200         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
133300             = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
133400                 CONTINUE                                                 
133500         WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
133600             IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
133700                 ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
133800                 MOVE ASC-TEMP-SELECTED-SW                                
133900                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
134000                                TO ASC-SELECTED-SW                        
134100                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
134200             END-IF                                                       
134300         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
134400                 = SPACE                                                  
134500             EVALUATE TRUE                                                
134600                 WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
134700                     SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
134800                     MOVE SPACE TO ASC-SELECTED-SW                        
134900                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
135000                 WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
135100                     MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
135200                     MOVE SPACE TO ASC-SELECTED-SW                        
135300                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
135400                 WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
135500                     MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
135600                     MOVE SPACE TO ASC-SELECTED-SW                        
135700                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
135800             END-EVALUATE                                                 
135900         WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
136000             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
136100                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
136200             END-IF                                                       
136300         WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
136400             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
136500                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
136600             END-IF                                                       
136700         WHEN OTHER                                                       
136800             MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
136900             SET DP020-MSG-FATAL    TO TRUE                               
137000             MOVE -1                TO MR-SELECTL (PV-SUB)                
137100             SET DP030-SET-CURSOR-APPL-1                                  
137200                                    TO TRUE                               
137300             MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
137400             MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
137500             SET PS-ERROR TO TRUE                                         
137600             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
137700                 SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
137800                 MOVE SPACE         TO ASC-SELECTED-SW                    
137900                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
138000             END-IF                                                       
138100     END-EVALUATE.                                                        
138200                                                                          
138300     SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
138400     SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
138500     IF  ASC-ITEM-SUB < 1                                                 
138600         SUBTRACT +1 FROM ASC-BLK-SUB                                     
138700         MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
138800     END-IF.                                                              
138900*----------------------------------------------------------------*        
139000*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
139100*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
139200*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
139300*    DISPLAY THE PANEL TO USER.                                  *        
139400*----------------------------------------------------------------*        
139500                                                                          
139600 4000-BUILD-INITIAL-PANEL.                                                
139700                                                                          
139800     INITIALIZE                 ASC-BLOCK-ENTRIES (1)                     
139900                                ASC-BLOCK-ENTRIES (2).                    
140000     MOVE +1                 TO ASC-BLK-SUB.                              
140100                                                                          
140200     SET ASC-ITEMS-LEFT-ON-DB                                             
140300                             TO TRUE.                                     
140400                                                                          
140500     MOVE +0                 TO ASC-ITEM-SUB                              
140600                                ASC-NUMBER-OF-ITEMS-READ                  
140700                                ASC-NUMBER-OF-SELECTED-ITEMS              
140800                                ASC-HIGHEST-BLOCK-WRITTEN                 
140900                                ASC-ITEM-AT-TOP-OF-PANEL.                 
141000                                                                          
141200     PERFORM 4010-OPEN-RETURNS-CURSOR.                                    
141300     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
141400     PERFORM 4030-CLOSE-RETURNS-CURSOR.                                   
141500                                                                          
141600                                                                          
141700     MOVE +1                   TO  PV-TOP-ITEM.                           
141800     IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
141900         SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
142000         SET DP020-MSG-FATAL   TO  TRUE                                   
142100         MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
142200     ELSE                                                                 
142300         MOVE -1               TO MR-SELECTL (1)                          
142400         SET DP030-SET-CURSOR-APPL-1                                      
142500                               TO TRUE                                    
142600         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
142700     END-IF.                                                              
142800                                                                          
142900*----------------------------------------------------------------*        
143000*  OPEN THE RETURNS CURSOR                                       *        
143100*----------------------------------------------------------------*        
143200 4010-OPEN-RETURNS-CURSOR.                                                
143300                                                                          
143500     EXEC SQL                                                             
143600         OPEN RETURNS-CSR                                                 
143700     END-EXEC.                                                            
144300                                                                          
144400     EVALUATE TRUE                                                        
144500         WHEN SQLCODE = ZERO                                              
144600              CONTINUE                                                    
144700         WHEN SQLWARN0 NOT = SPACE                                        
144800         WHEN SQLCODE NOT = ZERO                                          
144900              MOVE '4010-RETURNS-CURSOR'                                  
145000                               TO DP013-PARAGRAPH                         
145100              MOVE 'OPEN RETURNS-CSR CURSOR'                              
145200                               TO DP013-MESSAGE-TEXT (1)                  
145300              MOVE SQLCA       TO DP013-SQLCA                             
145400              SET DP013-DB2-ABEND                                         
145500                               TO TRUE                                    
145600              PERFORM DP013-0000-PROCESS-ABEND                            
145700     END-EVALUATE.                                                        
145800                                                                          
145900                                                                          
146000*----------------------------------------------------------------*        
146100*   CLOSE THE RETURNS CURSOR                                     *        
146200*----------------------------------------------------------------*        
146300 4030-CLOSE-RETURNS-CURSOR.                                               
146400                                                                          
146600     EXEC SQL                                                             
146700         CLOSE RETURNS-CSR                                                
146800     END-EXEC.                                                            
147400                                                                          
147500     EVALUATE TRUE                                                        
147600         WHEN SQLCODE = ZERO                                              
147700              CONTINUE                                                    
147800         WHEN SQLWARN0 NOT = SPACE                                        
147900         WHEN SQLCODE NOT = ZERO                                          
148000              MOVE '4030-CLOSE-RETURNS-CURSOR'                            
148100                               TO DP013-PARAGRAPH                         
148200              MOVE 'CLOSE RETURNS-CSR CURSOR'                             
148300                               TO DP013-MESSAGE-TEXT (1)                  
148400              MOVE SQLCA       TO DP013-SQLCA                             
148500              SET DP013-DB2-ABEND                                         
148600                               TO TRUE                                    
148700              PERFORM DP013-0000-PROCESS-ABEND                            
148800     END-EVALUATE.                                                        
148900                                                                          
149000*----------------------------------------------------------------*        
149100*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
149200*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
149300*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
149400*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
149500*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
149600*                                                                *        
149700*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
149800*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
149900*    UPON INITIALIZATION.                                        *        
150000*----------------------------------------------------------------*        
150100                                                                          
150200 4300-READ-ITEMS-FROM-DB.                                                 
150300                                                                          
150400     PERFORM                                                              
150500         VARYING PV-READ-COUNT                                            
150600         FROM 1 BY 1                                                      
150700         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
150800            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
150900                                                                          
151000         PERFORM 4310-FETCH                                               
151100                                                                          
151200         IF  ASC-ITEMS-LEFT-ON-DB                                         
151300             IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
151400                 ADD +1            TO ASC-ITEM-SUB                        
151500             ELSE                                                         
151600                 ADD +1            TO ASC-BLK-SUB                         
151700                 MOVE +1           TO ASC-ITEM-SUB                        
151800                                                                          
151900                 IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
152000                     MOVE +1       TO ASC-BLK-SUB                         
152100                     SET ASC-BLOCK-MODIFIED (1)                           
152200                                   TO TRUE                                
152300                     PERFORM 5100-WRITE-TEMP-STORAGE                      
152400                     MOVE ASC-BLOCK-ENTRIES                               
152500                               (PC-MAX-BLOCKS-IN-ARRAY)                   
152600                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
152700                     ADD +1        TO ASC-BLK-SUB                         
152800                     INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
152900                     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
153000                                   TO TRUE                                
153100                 END-IF                                                   
153200             END-IF                                                       
153300             ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
153400             MOVE ASC-NUMBER-OF-ITEMS-READ                                
153500                                   TO ASC-LIST-ITEM-NUMBER                
153600                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
153700             MOVE PV-RET-PARTN-NBR TO ASC-PARTITION-NBR                   
153800                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
153900             MOVE EPTRN-SLS-DTE (1:4)                                     
154000                                   TO PV-SLS-DTE-FORMATTED-YYYY           
154100             MOVE EPTRN-SLS-DTE (6:2)                                     
154200                                   TO PV-SLS-DTE-FORMATTED-MM             
154300             MOVE EPTRN-SLS-DTE (9:2)                                     
154400                                   TO PV-SLS-DTE-FORMATTED-DD             
154500             MOVE PV-SLS-DTE-FORMATTED                                    
154600                                   TO ASC-SALES-DATE                      
154700                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
154800             MOVE EPTRN-STR-NBR    TO ASC-STORE                           
154900                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
155000             MOVE EPTRN-RGST-ID    TO ASC-REGISTER                        
155100                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
155200             MOVE EPTRN-TRN-NBR    TO ASC-TRANSACTION                     
155300                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
155400             MOVE EPTRN-STRT-TM    TO ASC-START-TIME                      
155500                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
155600             MOVE EPTRN-ASSOC-ID   TO PV-RET-ASSOC-ID                     
155700             MOVE PV-RET-ASSOC-ID  TO ASC-REGISTER-OPERATOR               
155800                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
155900             MOVE EPTRN-TRN-TYP-CDE                                       
156000                                   TO  ASC-TYPE                           
156100                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
156200             MOVE EPTRN-SLS-CORR-SEQ-NBR                                  
156300                                   TO ASC-SALES-CORR-SEQ-NBR              
156400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
156500             EVALUATE TRUE                                                
156600                 WHEN EPTRN-SLS-CORR-SEQ-NBR = ZERO                       
156700                     MOVE 'POS'    TO ASC-SOURCE-TEXT                     
156800                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
156900                 WHEN EPTRN-SLS-CORR-SEQ-NBR > ZERO                       
157000                     MOVE 'SA'     TO ASC-SOURCE-TEXT                     
157100                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
157200                 WHEN EPTRN-SLS-CORR-SEQ-NBR < ZERO                       
157300                     MOVE 'SYS'    TO ASC-SOURCE-TEXT                     
157400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
157500             END-EVALUATE                                                 
157600                                                                          
157700             MOVE EPTRN-TRN-STAT-CDE                                      
157800                                   TO ASC-STATUS                          
157900                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
158000             EVALUATE TRUE                                                
158100                 WHEN ASC-STAT-VALID-CORR                                 
158200                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
158300                     MOVE 'CORR'       TO RF-STATUS-TEXT-MSG              
158400                     MOVE EPTRN-PRCG-DTE TO RF-PROCESSING-DATE            
158500                     MOVE RF-PRCG-DTE-MM TO RF-DISPLAY-DTE-MM             
158600                     MOVE RF-PRCG-DTE-DD TO RF-DISPLAY-DTE-DD             
158700                     MOVE RF-PRCG-DTE-YY TO RF-DISPLAY-DTE-YY             
158800                     MOVE RF-DISPLAY-DATE TO RF-STATUS-TEXT-DATE          
158900                     MOVE RF-STATUS-TEXT TO ASC-STATUS-TEXT               
159000                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
159100                 WHEN ASC-STAT-VALID-POS                                  
159200                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
159300                     MOVE 'VALID POS' TO ASC-STATUS-TEXT                  
159400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
159500                 WHEN ASC-STAT-MIS-TOT                                    
159600                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
159700                     MOVE 'MISSNG TOTAL' TO ASC-STATUS-TEXT               
159800                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
159900                 WHEN ASC-STAT-MIS-TND                                    
160000                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
160100                     MOVE 'MISSNG TENDR' TO ASC-STATUS-TEXT               
160200                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
160300                 WHEN ASC-STAT-OUT-BAL-ITM                                
160400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
160500                     MOVE 'ITMS NOT BAL' TO ASC-STATUS-TEXT               
160600                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
160700                 WHEN ASC-STAT-OUT-BAL-TND                                
160800                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
160900                     MOVE 'TNDR NOT BAL' TO ASC-STATUS-TEXT               
161000                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
161100                 WHEN ASC-STAT-NON-NUM-ACT                                
161200                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
161300                     MOVE 'ACCT NOT NUM' TO ASC-STATUS-TEXT               
161400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
161500                 WHEN ASC-STAT-MIS-ACT                                    
161600                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
161700                     MOVE 'MISSNG ACT #' TO ASC-STATUS-TEXT               
161800                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
161900                 WHEN ASC-STAT-INVAL-COR                                  
162000                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
162100                     MOVE 'INVALID CORR' TO ASC-STATUS-TEXT               
162200                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
162300                 WHEN ASC-STAT-INVAL-PRE                                  
162400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
162500                     MOVE 'INVALD PREFX' TO ASC-STATUS-TEXT               
162600                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
162700                 WHEN ASC-STAT-INVAL-C-D                                  
162800                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
162900                     MOVE 'INVLD CK DGT' TO ASC-STATUS-TEXT               
163000                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
163100                 WHEN ASC-STAT-INVAL-STR                                  
163200                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
163300                     MOVE 'INVALD STORE' TO ASC-STATUS-TEXT               
163400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
163500                 WHEN ASC-STAT-REVERSAL                                   
163600                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
163700                     MOVE 'REVRS'      TO RF-STATUS-TEXT-MSG              
163800                     MOVE EPTRN-PRCG-DTE TO RF-PROCESSING-DATE            
163900                     MOVE RF-PRCG-DTE-MM TO RF-DISPLAY-DTE-MM             
164000                     MOVE RF-PRCG-DTE-DD TO RF-DISPLAY-DTE-DD             
164100                     MOVE RF-PRCG-DTE-YY TO RF-DISPLAY-DTE-YY             
164200                     MOVE RF-DISPLAY-DATE TO RF-STATUS-TEXT-DATE          
164300                     MOVE RF-STATUS-TEXT TO ASC-STATUS-TEXT               
164400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
164500                 WHEN ASC-STAT-LATE-DC                                    
164600                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
164700                     MOVE 'LATE '      TO RF-STATUS-TEXT-MSG              
164800                     MOVE EPTRN-PRCG-DTE TO RF-PROCESSING-DATE            
164900                     MOVE RF-PRCG-DTE-MM TO RF-DISPLAY-DTE-MM             
165000                     MOVE RF-PRCG-DTE-DD TO RF-DISPLAY-DTE-DD             
165100                     MOVE RF-PRCG-DTE-YY TO RF-DISPLAY-DTE-YY             
165200                     MOVE RF-DISPLAY-DATE TO RF-STATUS-TEXT-DATE          
165300                     MOVE RF-STATUS-TEXT TO ASC-STATUS-TEXT               
165400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
165500                 WHEN ASC-STAT-INVAL-HD-TOT                               
165600                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
165700                     MOVE 'HD TOTAL INC' TO ASC-STATUS-TEXT               
165800                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
165900                 WHEN ASC-STAT-ERROR-GEN                                  
166000                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
166100                     MOVE 'ERROR IN TRN' TO ASC-STATUS-TEXT               
166200                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
166300                 WHEN ASC-STAT-INCOMP-DATA                                
166400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
166500                     MOVE 'INCOMPL DATA' TO ASC-STATUS-TEXT               
166600                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
166700                 WHEN OTHER                                               
166800                     MOVE 'UNKNOWN     ' TO ASC-STATUS-TEXT               
166900                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
167000             END-EVALUATE                                                 
167100                                                                          
167200             MOVE EPTRN-VOID-ABRT-CDE                                     
167300                                   TO ASC-VOID-CODE                       
167400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
167500                                                                          
167600             EVALUATE TRUE                                                
167700                 WHEN ASC-VOID-NORMAL                                     
167800                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
167900                     MOVE 'NORMAL TRAN' TO ASC-VOID-CODE-TEXT             
168000                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
168100                 WHEN ASC-VOID-AFTER                                      
168200                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
168300                     MOVE 'VOID AFTER' TO ASC-VOID-CODE-TEXT              
168400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
168500                 WHEN ASC-VOID-DURING                                     
168600                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
168700                     MOVE 'VOID DURING' TO ASC-VOID-CODE-TEXT             
168800                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
168900                 WHEN ASC-VOID-AFTER-DURING                               
169000                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
169100                     MOVE 'VOID AFT/DUR' TO ASC-VOID-CODE-TEXT            
169200                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
169300                 WHEN ASC-VOID-DURING-AFTER                               
169400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
169500                     MOVE 'VOID DUR/AFT' TO ASC-VOID-CODE-TEXT            
169600                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
169700                 WHEN ASC-VOID-REVERSED                                   
169800                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
169900                     MOVE 'REVERSD CORR' TO ASC-VOID-CODE-TEXT            
170000                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
170100                 WHEN ASC-VOID-UNCORRECTED                                
170200                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
170300                     MOVE 'UNCORR VOID' TO ASC-VOID-CODE-TEXT             
170400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
170500                 WHEN OTHER                                               
170600                     MOVE 'UNKNOWN    ' TO ASC-VOID-CODE-TEXT             
170700                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
170800             END-EVALUATE                                                 
170900                                                                          
171000             MOVE EPTRN-TRN-TOT-AMT     TO ASC-AMOUNT                     
171100                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
171200             MOVE EPTRN-SRVR-ONLN-IND   TO ASC-ONLINE-IND                 
171300                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
171400         END-IF                                                           
171500     END-PERFORM.                                                         
171600                                                                          
171700     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
171800         MOVE +1               TO ASC-BLK-SUB                             
171900         PERFORM 5100-WRITE-TEMP-STORAGE                                  
172000         ADD +1                TO ASC-BLK-SUB                             
172100         PERFORM 5100-WRITE-TEMP-STORAGE                                  
172200                                                                          
172300         MOVE 1                TO ASC-BLK-SUB                             
172400                                  PV-BLOCK-NUMBER                         
172500         PERFORM 4410-READ-TEMP-STORAGE                                   
172600         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
172700             MOVE 2            TO ASC-BLK-SUB                             
172800                                  PV-BLOCK-NUMBER                         
172900             PERFORM 4410-READ-TEMP-STORAGE                               
173000         END-IF                                                           
173100     END-IF.                                                              
173200                                                                          
173300*----------------------------------------------------------------*        
173400*    FETCH THE RETURNS-CSR CURSOR.                               *        
173500*----------------------------------------------------------------*        
173600                                                                          
173700 4310-FETCH.                                                              
173800                                                                          
174000     EXEC SQL                                                             
174100         FETCH RETURNS-CSR                                                
174200         INTO      :EPRKY-RET-SLS-DTE                                     
174300              ,:EPRKY-RET-STR-NBR                                         
174400              ,:EPRKY-RET-RGST-ID                                         
174500              ,:EPRKY-RET-TRN-NBR                                         
174600              ,:EPRKY-RET-STRT-TM                                         
174700              ,:EPRKY-RET-SEQ-NBR                                         
174800     END-EXEC.                                                            
175900                                                                          
176000     EVALUATE TRUE                                                        
176100         WHEN SQLCODE = ZERO                                              
176200         WHEN SQLCODE = +100                                              
176300              CONTINUE                                                    
176400         WHEN SQLWARN0 NOT = SPACE                                        
176500         WHEN SQLCODE NOT = ZERO                                          
176600              MOVE '4310-FETCH-CURSOR'                                    
176700                               TO DP013-PARAGRAPH                         
176800              MOVE 'FETCH CURSOR '                                        
176900                               TO DP013-MESSAGE-TEXT (1)                  
177000              MOVE SQLCA       TO DP013-SQLCA                             
177100              SET DP013-DB2-ABEND                                         
177200                               TO TRUE                                    
177300              PERFORM DP013-0000-PROCESS-ABEND                            
177400     END-EVALUATE.                                                        
177500                                                                          
177600     IF  SQLCODE EQUAL +100                                               
177700         SET ASC-NO-ITEMS-LEFT-ON-DB                                      
177800                                   TO TRUE                                
177900     END-IF.                                                              
178000                                                                          
178100     IF SQLCODE = ZERO                                                    
178200         MOVE EPRKY-RET-SLS-DTE    TO PV-RET-SLS-DTE-ISO                  
178300         MOVE EPRKY-RET-STR-NBR    TO PV-RET-STR-NBR                      
178400         MOVE EPRKY-RET-RGST-ID    TO PV-RET-RGST-ID                      
178500         MOVE EPRKY-RET-TRN-NBR    TO PV-RET-TRN-NBR                      
178600         MOVE EPRKY-RET-STRT-TM    TO PV-RET-STRT-TM                      
178700         PERFORM 4320-GET-PART-NUM                                        
178800         PERFORM 4330-GET-RET-TRAN-INFO                                   
178900     END-IF.                                                              
179000                                                                          
179100*----------------------------------------------------------------*        
179200*    GET THE PARTITION NUMBER                                    *        
179500*----------------------------------------------------------------*        
179600                                                                          
179700 4320-GET-PART-NUM.                                                       
179800                                                                          
179900     EXEC SQL                                                             
180000         SELECT  PARTN_NBR                                                
180200           INTO :EPPART-PARTN-NBR                                         
180400           FROM  TEPPART                                                  
180500          WHERE  SLS_DTE            = :PV-RET-SLS-DTE-ISO                 
180600     END-EXEC.                                                            
180700                                                                          
180800     EVALUATE TRUE                                                        
180900         WHEN SQLCODE = ZERO                                              
181000             CONTINUE                                                     
181010         WHEN SQLCODE = -811                                              
181011** INDICATES WE HAVE A SAME DAY RETURN                                    
181013             EXEC SQL                                                     
181014                SET :PV-CURR-TIMESTAMP = CURRENT TIMESTAMP                
181015             END-EXEC                                                     
181050             PERFORM 4327-SLCT-SAME-DAY-RTRN-PART                         
181100         WHEN SQLWARN0 NOT = SPACE                                        
181200         WHEN SQLCODE NOT = ZERO                                          
181300             MOVE '4320-GET-PART-NUM'                                     
181400                                   TO DP013-PARAGRAPH                     
181500             MOVE 'GET PART NUMBER'                                       
181600                                   TO DP013-MESSAGE-TEXT (1)              
181700             MOVE SQLCA            TO DP013-SQLCA                         
181800             SET DP013-DB2-ABEND   TO TRUE                                
181900             PERFORM DP013-0000-PROCESS-ABEND                             
182000     END-EVALUATE.                                                        
182100                                                                          
182300     MOVE EPPART-PARTN-NBR     TO PV-RET-PARTN-NBR.                       
182600                                                                          
182733*----------------------------------------------------------------*        
182734*   SELECTS PART NUMBER FOR SAME DAY RETURNS                              
182735*                                                                         
182736*                                                                         
182737*----------------------------------------------------------------*        
182738                                                                          
182739 4327-SLCT-SAME-DAY-RTRN-PART.                                            
182740                                                                          
182741     EXEC SQL                                                             
182742         SELECT  PARTN_NBR                                                
182744           INTO :EPPART-PARTN-NBR                                         
182746           FROM  TEPPART                                                  
182747          WHERE  SLS_DTE            = :PV-RET-SLS-DTE-ISO                 
182749     END-EXEC.                                                            
182750                                                                          
182751     EVALUATE TRUE                                                        
182752         WHEN SQLCODE = ZERO                                              
182753             CONTINUE                                                     
182764         WHEN SQLCODE NOT = ZERO                                          
182765             MOVE '4327-SLCT-SAME-DAY-RTRN-PART'                          
182766                                   TO DP013-PARAGRAPH                     
182767             MOVE 'ERROR SELECTING PART'                                  
182768                                   TO DP013-MESSAGE-TEXT (1)              
182769             MOVE SQLCA            TO DP013-SQLCA                         
182770             SET DP013-DB2-ABEND   TO TRUE                                
182771             PERFORM DP013-0000-PROCESS-ABEND                             
182772     END-EVALUATE.                                                        
182773                                                                          
182780*----------------------------------------------------------------*        
182800*  RETRIEVE TRANSACTION LEVEL INFORMATION ON RETURN TRANSACTION, *        
182900*  THROUGH OPERATIONAL TABLES, TO LIST ON SAKCS429               *        
183000*  SCREEN AND TO PASS ONTO F14 (ITEM INQ) AND F20 (TENDER INQ)   *        
183100*  SCREENS.                                                      *        
183200*----------------------------------------------------------------*        
183300                                                                          
183400 4330-GET-RET-TRAN-INFO.                                                  
183500                                                                          
183700     EXEC SQL                                                             
183800         SELECT       TRN.SLS_DTE                                         
183900                , STR_NBR                                                 
184000                , RGST_ID                                                 
184100                , TRN_NBR                                                 
184200                , STRT_TM                                                 
184300                , SLS_CORR_SEQ_NBR                                        
184400                , TRN_TYP_CDE                                             
184500                , ASSOC_ID                                                
184600                , TRN_TOT_AMT                                             
184700                , SRVR_ONLN_IND                                           
184800                , VOID_ABRT_CDE                                           
184900                , TRN_STAT_CDE                                            
185000                , PRCG_DTE                                                
185100           INTO :EPTRN-SLS-DTE                                            
185200              , :EPTRN-STR-NBR                                            
185300              , :EPTRN-RGST-ID                                            
185400              , :EPTRN-TRN-NBR                                            
185500              , :EPTRN-STRT-TM                                            
185600              , :EPTRN-SLS-CORR-SEQ-NBR                                   
185700              , :EPTRN-TRN-TYP-CDE                                        
185800              , :EPTRN-ASSOC-ID                                           
185900              , :EPTRN-TRN-TOT-AMT                                        
186000              , :EPTRN-SRVR-ONLN-IND                                      
186100              , :EPTRN-VOID-ABRT-CDE                                      
186200              , :EPTRN-TRN-STAT-CDE                                       
186300              , :EPTRN-PRCG-DTE                                           
186400           FROM       TEPTRN  TRN                                         
186500                , TEPPART PRT                                             
186600          WHERE       PRT.PARTN_NBR      = :PV-RET-PARTN-NBR              
186700           AND        TRN.PARTN_NBR      =  PRT.PARTN_NBR                 
186800           AND        TRN.STR_NBR        = :PV-RET-STR-NBR                
186900           AND        TRN.SLS_DTE        = :PV-RET-SLS-DTE-ISO            
187000           AND        TRN.RGST_ID        = :PV-RET-RGST-ID                
187100           AND        TRN.TRN_NBR        = :PV-RET-TRN-NBR                
187200           AND        TRN.STRT_TM        = :PV-RET-STRT-TM                
187400           AND        TRN.SLS_CORR_SEQ_NBR                                
187500                                    IN (0, -1)                            
187600        WITH UR                                                           
187700     END-EXEC.                                                            
192200                                                                          
192300     EVALUATE TRUE                                                        
192400         WHEN SQLCODE = ZERO                                              
192500         WHEN SQLCODE = +100                                              
192600         WHEN SQLCODE = -811                                              
192700             CONTINUE                                                     
192800         WHEN SQLWARN0 NOT = SPACE                                        
192900         WHEN SQLCODE NOT = ZERO                                          
193000             MOVE '4330-GET-RET-TRAN-INFO'                                
193100                                        TO DP013-PARAGRAPH                
193200             MOVE 'GET RET TRAN INFO'   TO DP013-MESSAGE-TEXT (1)         
193300             MOVE SQLCA                 TO DP013-SQLCA                    
193400             SET DP013-DB2-ABEND        TO TRUE                           
193500             PERFORM DP013-0000-PROCESS-ABEND                             
193600     END-EVALUATE.                                                        
193700                                                                          
193800     EVALUATE TRUE                                                        
193900         WHEN SQLCODE = +100                                              
194000         WHEN SQLCODE = -811                                              
194100             MOVE EPTRN-SLS-DTE         TO PV-DISP-SLS-DTE                
194200             MOVE EPTRN-STR-NBR         TO PV-DISP-STR                    
194300             MOVE EPTRN-RGST-ID         TO PV-DISP-RGST-ID                
194400             MOVE EPTRN-TRN-NBR         TO PV-DISP-TRN-NBR                
194500             MOVE '4330-GET-RET-TRAN-INFO'                                
194600                                        TO DP013-PARAGRAPH                
194700             MOVE PV-DISP-ERR-INFO      TO DP013-MESSAGE-TEXT (1)         
194800             MOVE SQLCA                 TO DP013-SQLCA                    
194900             SET DP013-DB2-ABEND        TO TRUE                           
195000             PERFORM DP013-0000-PROCESS-ABEND                             
195100     END-EVALUATE.                                                        
195200                                                                          
195300                                                                          
199200*----------------------------------------------------------------*        
199300* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
199400*----------------------------------------------------------------*        
199500                                                                          
199600 4400-MOVE-COMMAREA-TO-SCREEN.                                            
199700     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
199800         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
199900         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
200000             MOVE +1    TO ASC-BLK-SUB                                    
200100             PERFORM 5100-WRITE-TEMP-STORAGE                              
200200             ADD +1     TO ASC-BLK-SUB                                    
200300             PERFORM 5100-WRITE-TEMP-STORAGE                              
200400             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
200500                 GIVING PV-BLOCK-NUMBER                                   
200600                 REMAINDER PV-REMAINDER                                   
200700             IF  PV-REMAINDER > ZERO                                      
200800                 ADD +1 TO PV-BLOCK-NUMBER                                
200900             END-IF                                                       
201000             MOVE +1    TO ASC-BLK-SUB                                    
201100             PERFORM 4410-READ-TEMP-STORAGE                               
201200             ADD +1     TO ASC-BLK-SUB                                    
201300                           PV-BLOCK-NUMBER                                
201400             PERFORM 4410-READ-TEMP-STORAGE                               
201500     END-IF.                                                              
201600     MOVE +1            TO ASC-BLK-SUB.                                   
201700     COMPUTE ASC-ITEM-SUB =                                               
201800         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
201900         + PC-ITEMS-PER-PANEL).                                           
202000                                                                          
202100     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
202200         ADD +1         TO ASC-BLK-SUB                                    
202300         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
202400     END-IF.                                                              
202500                                                                          
202600     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
202700         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
202800     PERFORM 4420-FORMAT-LIST-LINES                                       
202900         VARYING PV-SUB                                                   
203000         FROM PC-ITEMS-PER-PANEL BY -1                                    
203100         UNTIL PV-SUB < +1.                                               
203200                                                                          
203300     COMPUTE ASC-ITEMS-DISPLAYED =                                        
203400         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
203500     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
203600                                      ASTRTITO.                           
203700     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
203800     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
203900     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
204000                                   TO ASELITO.                            
204100     MOVE ASC-KEY-SALES-DATE-DISPLAY                                      
204200                                   TO AORSLDTO.                           
204300     MOVE ASC-KEY-STORE            TO ASTOREO.                            
204400     MOVE ASC-KEY-REGISTER         TO ARGSTIDO.                           
204500     MOVE ASC-KEY-TRANSACTION      TO ATRNNBRO.                           
204600                                                                          
204700     IF  DP020-MSG-NUMBER-X = SPACE                                       
204800         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
204900*            --- MORE ITEMS TO DISPLAY ----                               
205000             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
205100             SET DP020-MSG-INFORMATIONAL TO TRUE                          
205200         END-IF                                                           
205300     END-IF.                                                              
205400*----------------------------------------------------------------*        
205500*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
205600*----------------------------------------------------------------*        
205700                                                                          
205800 4410-READ-TEMP-STORAGE.                                                  
205900                                                                          
206000     EXEC CICS                                                            
206100         READQ TS                                                         
206200             QUEUE (ASC-LIST-QUEUE-NAME)                                  
206300             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
206400             ITEM  (PV-BLOCK-NUMBER)                                      
206500             RESP  (PV-CICS-RESP)                                         
206600     END-EXEC.                                                            
206700                                                                          
206800     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
206900       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
207000         CONTINUE                                                         
207100     ELSE                                                                 
207200         SET DP013-CICS-ABEND     TO TRUE                                 
207300         SET DP013-NO-ROLLBACK    TO TRUE                                 
207400         MOVE '4410-READ-TEMP-STORAGE'                                    
207500                                  TO DP013-PARAGRAPH                      
207600         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
207700                                  TO DP013-MESSAGE-TEXT(1)                
207800         PERFORM DP013-0000-PROCESS-ABEND                                 
207900     END-IF.                                                              
208000                                                                          
208100     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
208200                                                                          
208300     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
208400         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
208500     END-IF.                                                              
208600                                                                          
208700*----------------------------------------------------------------*        
208800*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
208900*----------------------------------------------------------------*        
209000                                                                          
209100 4420-FORMAT-LIST-LINES.                                                  
209200     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
209300         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
209400         MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
209500         MOVE DP015-HL-OFF       TO MR-SELECTH (PV-SUB)                   
209600         MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
209700                                    MR-ACTION (PV-SUB)                    
209800                                    MR-DATE (PV-SUB)                      
209900                                    MR-STORE-X (PV-SUB)                   
210000                                    MR-REG-X (PV-SUB)                     
210100                                    MR-TRANS-X (PV-SUB)                   
210200                                    MR-VAR-AREA (PV-SUB)                  
210300     ELSE                                                                 
210400         IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
210500                                                   > SPACE                
210600             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
210700                                 TO MR-SELECTI (PV-SUB)                   
210800         ELSE                                                             
210900             MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
211000                                 TO MR-SELECTI (PV-SUB)                   
211100         END-IF                                                           
211200                                                                          
211300         IF  DP030-SET-CURSOR-TRAILER                                     
211400             IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
211500                 > SPACE)                                                 
211600               OR (PV-SUB = 1))                                           
211700                SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
211800                MOVE -1          TO MR-SELECTL (PV-SUB)                   
211900             END-IF                                                       
212000         END-IF                                                           
212100                                                                          
212200*** THE IF STATEMENT BELOW IS COMMENTED OUT SO THAT THE SCREEN            
212300*** ALWAYS GETS REBUILT.  THIS ALLOWS THE LEFT AND RIGHT                  
212400*** FUNCTION KEYS TO OPERATE CORRECTLY.                                   
212500                                                                          
212600*        IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
212700             MOVE ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB)                   
212800                                   TO MR-ACTION (PV-SUB)                  
212900             MOVE ASC-SALES-DATE (ASC-BLK-SUB ASC-ITEM-SUB)               
213000                                   TO MR-DATE (PV-SUB)                    
213100             MOVE ASC-STORE (ASC-BLK-SUB ASC-ITEM-SUB)                    
213200                                   TO MR-STORE (PV-SUB)                   
213300             MOVE ASC-REGISTER (ASC-BLK-SUB ASC-ITEM-SUB)                 
213400                                   TO MR-REG (PV-SUB)                     
213500             MOVE ASC-TRANSACTION (ASC-BLK-SUB ASC-ITEM-SUB)              
213600                                   TO MR-TRANS (PV-SUB)                   
213700             MOVE SPACES           TO DL-DETAIL-LINE-LEFT                 
213800             IF ASC-LEFT-VIEW                                             
213900                 MOVE DP015-ASK-NOR-OFF                                   
214000                                   TO AVARONA                             
214100                 MOVE DP015-WHITE  TO AVARONC                             
214200                 MOVE 'ON'         TO AVARONO                             
214300                 MOVE ASC-REGISTER-OPERATOR                               
214400                       (ASC-BLK-SUB ASC-ITEM-SUB)                         
214500                                   TO DL-REG-OP-LEFT                      
214600                 MOVE ASC-TYPE (ASC-BLK-SUB ASC-ITEM-SUB)                 
214700                                   TO DL-TYPE-LEFT                        
214800                 MOVE ASC-ONLINE-IND (ASC-BLK-SUB ASC-ITEM-SUB)           
214900                                   TO DL-ONLINE-IND-LEFT                  
215000                 IF ASC-VOID-CODE (ASC-BLK-SUB ASC-ITEM-SUB)              
215100                                   = 'N'                                  
215200                     MOVE 'N'      TO DL-VOID-IND-LEFT                    
215300                 ELSE                                                     
215400                     MOVE 'Y'      TO DL-VOID-IND-LEFT                    
215500                 END-IF                                                   
215600                 MOVE ASC-AMOUNT (ASC-BLK-SUB ASC-ITEM-SUB)               
215700                                   TO DL-TRAN-AMOUNT-LEFT                 
215800                 MOVE DL-DETAIL-LINE-LEFT                                 
215900                                   TO MR-VAR-AREA (PV-SUB)                
216000                 MOVE HL-HEADING-LINE-LEFT                                
216100                                   TO AVARHDRO                            
216200             ELSE                                                         
216300                 MOVE ASC-SOURCE-TEXT (ASC-BLK-SUB ASC-ITEM-SUB)          
216400                                   TO DL-TRAN-SRC-RIGHT                   
216500                 MOVE ASC-STATUS-TEXT (ASC-BLK-SUB ASC-ITEM-SUB)          
216600                                   TO DL-STATUS-RIGHT                     
216700                 MOVE ASC-VOID-CODE-TEXT                                  
216800                        (ASC-BLK-SUB ASC-ITEM-SUB)                        
216900                                   TO DL-VOID-INFO-RIGHT                  
217000                 MOVE DL-DETAIL-LINE-RIGHT                                
217100                                   TO MR-VAR-AREA (PV-SUB)                
217200                 MOVE DP015-ASK-DRK-OFF                                   
217300                                   TO AVARONA                             
217400                 MOVE HL-HEADING-LINE-RIGHT                               
217500                                   TO AVARHDRO                            
217600             END-IF                                                       
217700*        END-IF                                                           
217800     END-IF.                                                              
217900     SUBTRACT +1 FROM ASC-ITEM-SUB                                        
218000     IF  ASC-ITEM-SUB < + 1                                               
218100         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
218200         MOVE +1                 TO ASC-BLK-SUB                           
218300     END-IF.                                                              
218400                                                                          
218500*----------------------------------------------------------------*        
218600* PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
218700* ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
218800* FOR THE SELECTED ITEMS QUEUE.                                  *        
218900*                                                                *        
219000* FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
219100* QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
219200* THE ARRAY IS FULL.                                             *        
219300*----------------------------------------------------------------*        
219400                                                                          
219500 5000-PREPARE-SEL-QUEUE.                                                  
219600                                                                          
219700     MOVE +1                   TO  ASC-BLK-SUB.                           
219800     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
219900     ADD +1                    TO  ASC-BLK-SUB.                           
220000     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
220100                                                                          
220200     MOVE ASC-SEL2-QUEUE-NAME   TO  SA004-SEL-QUEUE-NAME.                 
220300     PERFORM 6010-DELETE-SEL-QUEUE.                                       
220400                                                                          
220500     SET ASC-UPDATE-TSQ        TO  TRUE.                                  
220600                                                                          
220700     SET SA001-IDX             TO  1.                                     
220800     MOVE ZERO                 TO SA004-NUMBER-OF-TS-ITEMS.               
220900     INITIALIZE SA001-SELECTED-ITEM-ARRAY.                                
221000                                                                          
221100     PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
221200             VARYING PV-ITEM                                              
221300             FROM 1 BY 1                                                  
221400             UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
221500                                                                          
221600     IF  SA001-IDX > 1                                                    
221700         PERFORM 5205-WRITE-SEL-QUEUE                                     
221800         ADD +1                TO SA004-NUMBER-OF-TS-ITEMS                
221900     END-IF.                                                              
222000                                                                          
222100*----------------------------------------------------------------*        
222200*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
222300*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
222400*----------------------------------------------------------------*        
222500                                                                          
222600 5100-WRITE-TEMP-STORAGE.                                                 
222700     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
222800                                   TO PV-LIST-ITEM-NUMBER.                
222900     COMPUTE PV-BLOCK-NUMBER =                                            
223000         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
223100                                                                          
223200     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
223300     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
223400         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
223500             PERFORM 5105-REWRITE-ASC-LISTQ                               
223600         END-IF                                                           
223700     ELSE                                                                 
223800         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
223900         ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
224000         PERFORM 5110-WRITE-ASC-LISTQ                                     
224100         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
224200     END-IF.                                                              
224300                                                                          
224400     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
224500*----------------------------------------------------------------*        
224600*  REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-     *        
224700*  SPECIFIC COMMAREA.                                            *        
224800*----------------------------------------------------------------*        
224900 5105-REWRITE-ASC-LISTQ.                                                  
225000     EXEC CICS                                                            
225100         WRITEQ TS                                                        
225200             QUEUE (ASC-LIST-QUEUE-NAME)                                  
225300             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
225400             ITEM  (PV-BLOCK-NUMBER)                                      
225500             REWRITE                                                      
225600             RESP  (PV-CICS-RESP)                                         
225700     END-EXEC.                                                            
225800                                                                          
225900     IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
226000         CONTINUE                                                         
226100     ELSE                                                                 
226200         SET DP013-CICS-ABEND            TO TRUE                          
226300         SET DP013-NO-ROLLBACK           TO TRUE                          
226400         MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
226500         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
226600                                         TO DP013-MESSAGE-TEXT(1)         
226700         PERFORM DP013-0000-PROCESS-ABEND                                 
226800     END-IF.                                                              
226900                                                                          
227000*----------------------------------------------------------------*        
227100*  WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-       *        
227200*  SPECIFIC COMMAREA.                                            *        
227300*----------------------------------------------------------------*        
227400 5110-WRITE-ASC-LISTQ.                                                    
227500     EXEC CICS                                                            
227600         WRITEQ TS                                                        
227700             QUEUE (ASC-LIST-QUEUE-NAME)                                  
227800             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
227900             ITEM  (PV-BLOCK-NUMBER)                                      
228000             RESP  (PV-CICS-RESP)                                         
228100     END-EXEC.                                                            
228200     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
228300         SET DP013-CICS-ABEND      TO TRUE                                
228400         SET DP013-NO-ROLLBACK     TO TRUE                                
228500         MOVE '5110-WRITE-ASC-LISTQ'                                      
228600                                   TO DP013-PARAGRAPH                     
228700         MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
228800                                   TO DP013-MESSAGE-TEXT(1)               
228900         PERFORM DP013-0000-PROCESS-ABEND                                 
229000     END-IF.                                                              
229100                                                                          
229200*----------------------------------------------------------------*        
229300* READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
229400* TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
229500*----------------------------------------------------------------*        
229600                                                                          
229700 5200-FILL-SEL-QUEUE-RECORD.                                              
229800                                                                          
229900     PERFORM 6112-READ-LIST-QUEUE.                                        
230000                                                                          
230100     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
230200         PERFORM                                                          
230300         VARYING LQW-IDX                                                  
230400         FROM 1 BY 1                                                      
230500         UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
230600           OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
230700                                                                          
230800             IF  LQW-SELECT (LQW-IDX)                                     
230900                                                                          
231000***  THE MOVE TO THE SALES DATE VARIABLE IS OK HERE BECAUSE               
231100***  ONLY ONE ITEM CAN BE SELECTED                                        
231200                                                                          
231300                 MOVE LQW-SALES-DATE (LQW-IDX)                            
231400                                   TO SA004-SALES-DATE-DISPLAY            
231500                                      PV-SLS-DTE-FORMATTED                
231600                 MOVE PV-SLS-DTE-FORMATTED-YYYY                           
231700                                   TO PV-SLS-DTE-ISO-YYYY                 
231800                 MOVE PV-SLS-DTE-FORMATTED-MM                             
231900                                   TO PV-SLS-DTE-ISO-MM                   
232000                 MOVE PV-SLS-DTE-FORMATTED-DD                             
232100                                   TO PV-SLS-DTE-ISO-DD                   
232200                 MOVE PV-SLS-DTE-ISO                                      
232300                                   TO SA004-SALES-DATE-ISO                
232400                 MOVE LQW-PARTITION-NBR (LQW-IDX)                         
232500                                   TO SA001-PARTITION (SA001-IDX)         
232600                 MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
232700                                   TO SA001-ITEM (SA001-IDX)              
232800                 MOVE LQW-STORE (LQW-IDX)                                 
232900                                   TO SA001-STORE (SA001-IDX)             
233000                 MOVE LQW-REGISTER (LQW-IDX)                              
233100                                   TO SA001-REGISTER (SA001-IDX)          
233200                 MOVE LQW-TRANSACTION (LQW-IDX)                           
233300                                   TO SA001-TRANS (SA001-IDX)             
233400                 MOVE LQW-START-TIME (LQW-IDX)                            
233500                                   TO SA001-START-TIME                    
233600                                      (SA001-IDX)                         
233700                 MOVE LQW-REGISTER-OPERATOR (LQW-IDX)                     
233800                                   TO SA001-ID (SA001-IDX)                
233900                 MOVE LQW-TYPE (LQW-IDX)                                  
234000                                   TO SA001-TYPE (SA001-IDX)              
234100                 MOVE LQW-SALES-CORR-SEQ-NBR (LQW-IDX)                    
234200                                   TO SA001-SALES-CORR-SEQ-NBR            
234300                                       (SA001-IDX)                        
234400                 MOVE LQW-VOID-CODE (LQW-IDX)                             
234500                                   TO SA001-VOID-ABRT-CDE                 
234600                                       (SA001-IDX)                        
234700                 MOVE LQW-STATUS (LQW-IDX)                                
234800                                   TO SA001-TRAN-STAT-CDE                 
234900                                       (SA001-IDX)                        
235000                 MOVE SPACE        TO SA001-ACTION-SW (SA001-IDX)         
235100                 SET SA001-UPD-TRAN (SA001-IDX)                           
235200                                   TO TRUE                                
235300                 SET SA001-IDX UP BY 1                                    
235400                 IF  SA001-IDX > SA001-MAX-ITEMS                          
235500                     PERFORM 5205-WRITE-SEL-QUEUE                         
235600                     ADD +1        TO SA004-NUMBER-OF-TS-ITEMS            
235700                     INITIALIZE SA001-SELECTED-ITEM-ARRAY                 
235800                     SET SA001-IDX TO  1                                  
235900                 END-IF                                                   
236000             END-IF                                                       
236100         END-PERFORM                                                      
236200     END-IF.                                                              
236300                                                                          
236400*----------------------------------------------------------------*        
236500*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
236600*----------------------------------------------------------------*        
236700                                                                          
236800 5205-WRITE-SEL-QUEUE.                                                    
236900                                                                          
237000     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
237100                                   TO SA001-SELECTED-ITEMS.               
237200                                                                          
237300     EXEC CICS                                                            
237400          WRITEQ TS                                                       
237500              QUEUE (ASC-SEL2-QUEUE-NAME)                                 
237600              FROM  (SA001-TRANSACTION-SEL-REC)                           
237700              ITEM  (PV-SEL-QUEUE-ITEM)                                   
237800              RESP  (PV-CICS-RESP)                                        
237900     END-EXEC.                                                            
238000                                                                          
238100     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
238200         CONTINUE                                                         
238300     ELSE                                                                 
238400         SET DP013-CICS-ABEND      TO TRUE                                
238500         SET DP013-NO-ROLLBACK     TO TRUE                                
238600         MOVE '5205-WRITE-SEL-QUEUE'                                      
238700                                   TO DP013-PARAGRAPH                     
238800         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
238900                                   TO DP013-MESSAGE-TEXT(1)               
239000         PERFORM DP013-0000-PROCESS-ABEND                                 
239100     END-IF.                                                              
239200                                                                          
239300*----------------------------------------------------------------*        
239400*  WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION,  *        
239500*  SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT  *        
239600*  THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO     *        
239700*  THE USER.                                                     *        
239800*----------------------------------------------------------------*        
239900 6000-REFRESH-PANEL-FROM-TEMP.                                            
240000     IF  ASC-UPDATE-TSQ                                                   
240100         SET ASC-NO-TSQ-UPDATE TO TRUE                                    
240200         MOVE ZERO                 TO PV-SAVE-ITEM                        
240300                                      PV-SEL-QUEUE-ITEM                   
240400         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
240500                                   TO PV-HOLD-SELECTED-ITEMS              
240600         MOVE +1                   TO PV-ITEMS-PROCESSED                  
240700         PERFORM 6100-UPDATE-TSQ                                          
240800             UNTIL PV-ITEMS-PROCESSED >                                   
240900                                     PV-HOLD-SELECTED-ITEMS               
241000         PERFORM 6010-DELETE-SEL-QUEUE                                    
241100         INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
241200                    ASC-BLOCK-ENTRIES (2)                                 
241300     END-IF.                                                              
241400                                                                          
241500*----------------------------------------------------------------*        
241600*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
241700*----------------------------------------------------------------*        
241800 6010-DELETE-SEL-QUEUE.                                                   
241900                                                                          
242000     EXEC CICS                                                            
242100         DELETEQ TS                                                       
242200             QUEUE (ASC-SEL2-QUEUE-NAME)                                  
242300             RESP  (PV-CICS-RESP)                                         
242400     END-EXEC.                                                            
242500                                                                          
242600     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
242700        (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
242800         CONTINUE                                                         
242900     ELSE                                                                 
243000         SET DP013-CICS-ABEND TO TRUE                                     
243100         SET DP013-NO-ROLLBACK TO TRUE                                    
243200         MOVE '6010-DELETE-SEL-QUEUE'                                     
243300                        TO DP013-PARAGRAPH                                
243400         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
243500                        TO DP013-MESSAGE-TEXT (1)                         
243600         PERFORM DP013-0000-PROCESS-ABEND                                 
243700     END-IF.                                                              
243800                                                                          
243900*----------------------------------------------------------------*        
244000*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
244100*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
244200*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
244300*    THE TEMP QUEUE.                                             *        
244400*----------------------------------------------------------------*        
244500                                                                          
244600 6100-UPDATE-TSQ.                                                         
244700                                                                          
244800     ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
244900     PERFORM 6105-READ-SEL-QUEUE.                                         
245000     PERFORM                                                              
245100         VARYING SA001-IDX                                                
245200         FROM 1 BY 1                                                      
245300         UNTIL ((PV-ITEMS-PROCESSED >                                     
245400                              PV-HOLD-SELECTED-ITEMS)                     
245500         OR (SA001-IDX GREATER THAN SA001-MAX-ITEMS))                     
245600         IF  SA001-ACTION-SW (SA001-IDX) > SPACE                          
245700             PERFORM 6110-PROCESS-ITEMS                                   
245800         END-IF                                                           
245900         ADD +1                TO PV-ITEMS-PROCESSED                      
246000     END-PERFORM.                                                         
246100     PERFORM 6111-WRITE-LIST-QUEUE.                                       
246200*----------------------------------------------------------------*        
246300*    READ THE LIST QUEUE RECORD.                                 *        
246400*----------------------------------------------------------------*        
246500                                                                          
246600 6105-READ-SEL-QUEUE.                                                     
246700                                                                          
246800     EXEC CICS                                                            
246900          READQ TS                                                        
247000              QUEUE (ASC-SEL2-QUEUE-NAME)                                 
247100              INTO  (SA001-TRANSACTION-SEL-REC)                           
247200              ITEM  (PV-SEL-QUEUE-ITEM)                                   
247300              RESP  (PV-CICS-RESP)                                        
247400     END-EXEC.                                                            
247500                                                                          
247600     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
247700         CONTINUE                                                         
247800     ELSE                                                                 
247900         SET DP013-CICS-ABEND      TO  TRUE                               
248000         SET DP013-NO-ROLLBACK     TO  TRUE                               
248100         MOVE '6105-READ-SEL-QUEUE'                                       
248200                                   TO  DP013-PARAGRAPH                    
248300         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
248400                                   TO  DP013-MESSAGE-TEXT(1)              
248500         PERFORM DP013-0000-PROCESS-ABEND                                 
248600     END-IF.                                                              
248700                                                                          
248800*----------------------------------------------------------------*        
248900*    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
249000*    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
249100*    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
249200*----------------------------------------------------------------*        
249300                                                                          
249400 6110-PROCESS-ITEMS.                                                      
249500                                                                          
249600     COMPUTE PV-ITEM = 1 + ((SA001-ITEM (SA001-IDX) - 1)                  
249700                     / PC-ITEMS-PER-PANEL).                               
249800                                                                          
249900     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
250000         IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
250100             PERFORM 6111-WRITE-LIST-QUEUE                                
250200         END-IF                                                           
250300         MOVE PV-ITEM          TO PV-SAVE-ITEM                            
250400                                                                          
250500         PERFORM 6112-READ-LIST-QUEUE                                     
250600         IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
250700             CONTINUE                                                     
250800         END-IF                                                           
250900     END-IF.                                                              
251000                                                                          
251100     COMPUTE PV-SUB = (SA001-ITEM (SA001-IDX)                             
251200                    - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
251300     SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
251400     MOVE LQW-SELECTED-SW (PV-SUB)                                        
251500                               TO LQW-TEMP-SELECTED-SW (PV-SUB).          
251600     EVALUATE TRUE                                                        
251700         WHEN SA001-INQ (SA001-IDX)                                       
251800              MOVE PC-INQ      TO LQW-ACTION (PV-SUB)                     
251900                                                                          
252000         WHEN SA001-ERR (SA001-IDX)                                       
252100              MOVE PC-ERR      TO LQW-ACTION (PV-SUB)                     
252200     END-EVALUATE.                                                        
252300                                                                          
252400     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
252500                                                                          
252600*----------------------------------------------------------------*        
252700*    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *        
252800*----------------------------------------------------------------*        
252900                                                                          
253000 6111-WRITE-LIST-QUEUE.                                                   
253100                                                                          
253200     EXEC CICS                                                            
253300         WRITEQ TS                                                        
253400             QUEUE (ASC-LIST-QUEUE-NAME)                                  
253500             FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
253600             ITEM  (PV-SAVE-ITEM)                                         
253700             REWRITE                                                      
253800             RESP  (PV-CICS-RESP)                                         
253900     END-EXEC.                                                            
254000                                                                          
254100     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
254200         CONTINUE                                                         
254300     ELSE                                                                 
254400         SET DP013-CICS-ABEND  TO  TRUE                                   
254500         SET DP013-NO-ROLLBACK TO  TRUE                                   
254600         MOVE '6111-WRITE-LIST-QUEUE'                                     
254700                               TO  DP013-PARAGRAPH                        
254800         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
254900                               TO  DP013-MESSAGE-TEXT(1)                  
255000         PERFORM DP013-0000-PROCESS-ABEND                                 
255100     END-IF.                                                              
255200                                                                          
255300                                                                          
255400*----------------------------------------------------------------*        
255500*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
255600*----------------------------------------------------------------*        
255700                                                                          
255800 6112-READ-LIST-QUEUE.                                                    
255900                                                                          
256000     EXEC CICS                                                            
256100         READQ TS                                                         
256200             QUEUE (ASC-LIST-QUEUE-NAME)                                  
256300             INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
256400             ITEM  (PV-ITEM)                                              
256500             RESP  (PV-CICS-RESP)                                         
256600     END-EXEC.                                                            
256700                                                                          
256800     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
256900         CONTINUE                                                         
257000     ELSE                                                                 
257100         SET DP013-CICS-ABEND        TO TRUE                              
257200         SET DP013-NO-ROLLBACK       TO TRUE                              
257300         MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
257400         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
257500                                     TO DP013-MESSAGE-TEXT(1)             
257600         PERFORM DP013-0000-PROCESS-ABEND                                 
257700     END-IF.                                                              
257800                                                                          
257900                                                                          
258000*----------------------------------------------------------------*        
258100*    READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.    *        
258200*----------------------------------------------------------------*        
258300                                                                          
258400 9100-READ-TRAN-SEL-QUEUE.                                                
258500                                                                          
258600     MOVE +1                         TO ASC-TRAN-SEL-ITEM.                
258700     MOVE +1                         TO ASC-TRAN-SEL-SUB.                 
258800                                                                          
258900     EXEC CICS                                                            
259000         READQ TS                                                         
259100             QUEUE (ASC-TRAN-SEL-QUEUE-NAME)                              
259200             INTO  (SA001-TRANSACTION-SEL-REC)                            
259300             ITEM  (ASC-TRAN-SEL-ITEM)                                    
259400             RESP  (PV-CICS-RESP)                                         
259500     END-EXEC.                                                            
259600                                                                          
259700     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
259800         CONTINUE                                                         
259900     ELSE                                                                 
260000         SET DP013-LOGIC-ABEND                                            
260100             DP013-NO-ROLLBACK       TO TRUE                              
260200         MOVE '9100-READ-SEL-QUEUE'  TO DP013-PARAGRAPH                   
260300         MOVE 'ATTEMPTING TO READ THE LIST SELECTION TS QUEUE'            
260400                                     TO DP013-MESSAGE-TEXT (1)            
260500         PERFORM DP013-0000-PROCESS-ABEND                                 
260600     END-IF.                                                              
260700                                                                          
260800                                                                          
260900*----------------------------------------------------------------*        
261000*    WRITE SELECTED ITEM TO TEMP STORAGE QUEUE.                  *        
261100*----------------------------------------------------------------*        
261200                                                                          
261300 9200-REWRITE-SEL-QUEUE.                                                  
261400     EXEC CICS                                                            
261500          WRITEQ TS                                                       
261600              QUEUE   (ASC-TRAN-SEL-QUEUE-NAME)                           
261700              FROM    (SA001-TRANSACTION-SEL-REC)                         
261800              ITEM    (ASC-TRAN-SEL-ITEM)                                 
261900              RESP    (PV-CICS-RESP)                                      
262000              REWRITE                                                     
262100     END-EXEC.                                                            
262200                                                                          
262300     IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                           
262400         SET DP013-CICS-ABEND                                             
262500             DP013-NO-ROLLBACK     TO TRUE                                
262600         MOVE '9200-REWRITE-SEL-QUEUE'                                    
262700                                   TO DP013-PARAGRAPH                     
262800         MOVE 'ATTEMPTING TO WRITE THE LIST SELECTIONS QUEUE'             
262900                                   TO DP013-MESSAGE-TEXT (1)              
263000         PERFORM DP013-0000-PROCESS-ABEND                                 
263100     END-IF.                                                              
263200                                                                          
263300                                                                          
263400*----------------------------------------------------------------*        
263500*    ABEND PROCESSOR MODULE                                      *        
263600*----------------------------------------------------------------*        
263700                                                                          
263800     COPY DPPD013.                                                        
