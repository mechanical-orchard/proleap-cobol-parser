       ID DIVISION.                                                             
       PROGRAM-ID.      INKUP130.                                               
       AUTHOR.          JEFF HARTIG/COMPUWARE.                                  
       DATE-WRITTEN     06/24/98.                                               
       DATE-COMPILED.                                                           
      ******************************************************************        
      * INKUP130 - UPDATE INVENTORY PARAMETER TABLE (TINVPAR) WITH     *        
      * ACUTAL INVENTORY DATES FROM INPUT FILE.                        *        
      ******************************************************************        
20246 *  CHANGED: 11/04/99     ROLLIN KRING    WR# 20246               *        
20246 *                                                                *        
20246 *  REMOVED THE STORE CONTROL CARD AND REPLACE WITH TINVPAR READ. *        
20246 *  REVISED TO USE THE TINVPAR TABLE FOR CHECKING WHICH STORES TO *        
20246 *  PROCESS.  ONLY STORES WITH 'I' IN THE UNT-BKG-STAT-CDE WILL BE         
20246 *  PROCESSED. VALID VALUES FOR THE UNIT BOOKING STATUS CODE ARE: *        
20246 *  S - SELECTED AS CANDIDATE                                     *        
20246 *  A - SELECTED BY USER TO BOOK(APPROVED)                        *        
20246 *  I - BOOKING IN PROCESS                                        *        
20246 *  C - BOOKING COMPLETE                                          *        
20246 *  ADDED TO UPDATE THE UNT-BKG-STAT-CDE TO 'C' FOR THE STORES    *        
20246 *  THAT ARE PROCESSED.  THIS WILL BE USED IDENTIFY STORES THAT   *        
20246 *  THE UNIT BOOKING PROCESS IS COMPLETE.                         *        
W26600******************************************************************        
W26600*  W26600  08/  /2004  PHYSICAL INVENTORY - STORE EXPANSION               
W26600*                      M JOHNSON                                          
W26600*                                                                         
W28545*  W28545  06-30-2006  J SELWITSCHKA                                      
W28545*                      DEPARTMENT LEVEL INVENTORY PROJECT                 
W33304*  W33304  06-04-2008  JILL LIN                                  *        
W33304*                      HOLDING INVENTORY LEDGER OPEN ACROSS      *        
W33304*                      PERIODS INVENTORY SHORTAGE REPORTING      *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-STORE-RECS-READ    PIC S9(9)     COMP-3 VALUE +0.             
           05  PA-TINVPAR-ROWS-UPDATED                                          
                                     PIC S9(09)    COMP-3 VALUE +0.             
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-ID         PIC X(08)     VALUE 'INKUP130'.            
           05  PC-EMPTY-DATE         PIC X(10)     VALUE '9999-09-09'.          
W33304     05  PC-UNIT-BK-STAT-CDE   PIC X(02)     VALUE 'TC'.                  
20246      05  PC-MAX-RETRIES        PIC 9(03)     VALUE 3.                     
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-CLASS              PIC X(02)     VALUE SPACES.                
           05  PV-DB2-OPER-ATTEMPTED PIC X(30)     VALUE SPACES.                
           05  PV-DEPT               PIC X(03)     VALUE SPACES.                
           05  PV-MAX-RETRIES        PIC S9(05)    COMP-3 VALUE +3.             
           05  PV-PARAGRAPH-NAME     PIC X(30)     VALUE SPACES.                
           05  PV-SQL-SUB            PIC S9(05)    COMP-3 VALUE +0.             
W26600     05  PV-STORE-NMBR         PIC 9(04)     VALUE ZEROS.                 
           05  PV-CURRENT-INV-DATE   PIC X(10)     VALUE SPACES.                
           05  PV-TODAYS-TIME        PIC 9(08)     VALUE ZERO.                  
           05  FILLER REDEFINES PV-TODAYS-TIME.                                 
               10  PV-HH             PIC 9(02).                                 
               10  PV-MIN            PIC 9(02).                                 
               10  PV-SEC            PIC 9(02).                                 
               10  PV-HUND-SEC       PIC 9(02).                                 
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-MORE-TINVPAR-ROWS-SW   PIC X  VALUE 'Y'.                      
               88  PS-MORE-TINVPAR-ROWS         VALUE 'Y'.                      
               88  PS-NO-MORE-TINVPAR-ROWS      VALUE 'N'.                      
                                                                                
       01  ABEND-CODE                PIC S9(04)    VALUE +0 COMP SYNC.          
           88  ABEND-4013-DB2-ERROR                VALUE +4013.                 
                                                                                
W28545*************************************************************             
W28545*    DCLGEN FOR TINCNTL TABLE                               *             
W28545*************************************************************             
W28545     EXEC SQL                                                             
W28545         INCLUDE TINCNTL                                                  
W28545     END-EXEC.                                                            
W28545                                                                          
      *************************************************************             
      *    DCLGEN FOR TINVPAR TABLE                               *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *    DB2 ERROR MESSAGE AREA                                 *             
      *************************************************************             
           COPY DPWS004.                                                        
                                                                                
                                                                                
      *************************************************************             
      *    DB2 COMMUNICATION AREA                                 *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
20246 *****************************************************************         
20246 * DB2 CURSOR FOR TINVPAR                                                  
20246 *****************************************************************         
20246      EXEC SQL   DECLARE TINVPAR_CURSOR CURSOR FOR                         
20246                                                                           
W26600         SELECT LOC_NBR                                                   
W28545               ,A.INV_ID                                                  
W28545           FROM TINVPAR A                                                 
W28545               ,TINCNTL B                                                 
20246           WHERE ACTL_UNIT_BK_DTE = '9999-09-09'                           
W33304            AND UNT_BKG_STAT_CDE = 'IP'                                   
W28545            AND B.ACTV_IND = 'Y'                                          
W28545            AND A.INV_ID = B.INV_ID                                       
W33304            AND A.LOC_INV_STAT_CDE = 'IN'                                 
W26600         ORDER BY LOC_NBR                                                 
W26600         WITH UR                                                          
20246                                                                           
20246      END-EXEC.                                                            
       PROCEDURE DIVISION.                                                      
                                                                                
       A000-MAINLINE.                                                           
                                                                                
      *************************************************************             
      *     MAIN PROCESSING PARAGRAPH                             *             
      *************************************************************             
                                                                                
            PERFORM B100-INITIALIZATION.                                        
                                                                                
20246       PERFORM Z600-READ-TINVPAR                                           
20246          UNTIL PS-NO-MORE-TINVPAR-ROWS.                                   
                                                                                
            PERFORM B300-END-PROCESSING.                                        
                                                                                
            STOP RUN.                                                           
                                                                                
      ******************************************************************        
      * OPEN TINVPAR CURSOR                                                     
      ******************************************************************        
       B100-INITIALIZATION.                                                     
                                                                                
           EXEC SQL                                                             
               OPEN TINVPAR_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'OPEN TINVPAR_CURSOR' TO PV-DB2-OPER-ATTEMPTED              
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ***************************************************************           
      *     - CLOSE ALL FILES                                       *           
      ***************************************************************           
       B300-END-PROCESSING.                                                     
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                   CLOSE TINVPAR_CURSOR                                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'B300-END-PROCESSING'                               
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE ON TINVPAR CURSOR'                           
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
            DISPLAY 'INPUT RECORDS READ   - ', PA-STORE-RECS-READ.              
            DISPLAY 'TINVPAR ROWS UPDATED - ', PA-TINVPAR-ROWS-UPDATED.         
                                                                                
      ******************************************************************        
      * READ INPUT FILE                                                *        
      ******************************************************************        
       Z600-READ-TINVPAR.                                                       
                                                                                
20246      EXEC SQL FETCH TINVPAR_CURSOR                                        
W26600        INTO :INVPAR-LOC-NBR                                              
W28545            ,:INVPAR-INV-ID                                               
20246      END-EXEC.                                                            
20246                                                                           
20246      EVALUATE SQLCODE                                                     
20246          WHEN +0                                                          
20246             ADD 1 TO PA-STORE-RECS-READ                                   
W26600            MOVE INVPAR-LOC-NBR  TO PV-STORE-NMBR                         
20246             PERFORM Z700-UPDATE-INVPAR-ROW                                
20246          WHEN +100                                                        
20246              SET PS-NO-MORE-TINVPAR-ROWS TO TRUE                          
20246          WHEN OTHER                                                       
20246              MOVE 'FETCH TINVPAR_CURSOR'                                  
20246                                 TO  PV-DB2-OPER-ATTEMPTED                 
20246              PERFORM Z999-SQL-ABEND                                       
20246      END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * UPDATE TINVPAR TABLE WITH INVENTORY DATE                       *        
      ******************************************************************        
       Z700-UPDATE-INVPAR-ROW.                                                  
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PV-MAX-RETRIES) OR                           
                   (SQLCODE = 0)                                                
                                                                                
                       EXEC SQL                                                 
                           UPDATE TINVPAR                                       
                           SET ACTL_UNIT_BK_DTE = CURRENT DATE,                 
                               UNT_BKG_STAT_CDE = :PC-UNIT-BK-STAT-CDE,         
                               CHG_ID_NBR       = :PC-PROGRAM-ID,               
                               CHG_TMST         = CURRENT TIMESTAMP             
W26600                     WHERE LOC_NBR        = :INVPAR-LOC-NBR               
                           AND ACTL_UNIT_BK_DTE = :PC-EMPTY-DATE                
W28545                     AND INV_ID           = :INVPAR-INV-ID                
                       END-EXEC                                                 
                                                                                
                       EVALUATE TRUE                                            
                           WHEN SQLCODE = ZERO                                  
                               ADD +1 TO PA-TINVPAR-ROWS-UPDATED                
                           WHEN SQLWARN0 NOT = SPACES                           
                           WHEN SQLCODE LESS ZERO                               
                           WHEN SQLCODE GREATER ZERO                            
                               MOVE 'Z700-UPDATE-TINVPAR-ROW'                   
                                   TO PV-PARAGRAPH-NAME                         
                               MOVE 'UPDATE TINVPAR ROW'                        
                                   TO PV-DB2-OPER-ATTEMPTED                     
                               PERFORM Z999-SQL-ABEND                           
                       END-EVALUATE                                             
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB GREATER PV-MAX-RETRIES                                 
               MOVE 'Z700-INSERT-TADJADD-ROW' TO PV-PARAGRAPH-NAME              
               MOVE 'EXCEEDED MAX RETRIES' TO PV-DB2-OPER-ATTEMPTED             
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
       Z999-SQL-ABEND.                                                          
                                                                                
      ***************************************************************           
      *    SQL ABEND PROCESSING                                     *           
      ***************************************************************           
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   = ' PC-PROGRAM-ID.                             
           DISPLAY '** PARAGRAPH = ' PV-PARAGRAPH-NAME.                         
           DISPLAY '** OPERATION = ' PV-DB2-OPER-ATTEMPTED.                     
           COPY DPPD004.                                                        
