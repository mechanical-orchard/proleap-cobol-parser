       IDENTIFICATION DIVISION.                                         00010020
       PROGRAM-ID.           PXKUT101.                                  00020020
       AUTHOR.               RICH KELLEN.                               00030020
       INSTALLATION.         KOHLS DEPARTMENT STORES.                   00040020
       DATE-WRITTEN          06/13/2010.                                00050020
       DATE-COMPILED.                                                   00060020
                                                                        00070020
      ******************************************************************00080020
      *   PXKUT101 - ADJUST STR-EXPTED-QTY ON EXTRACT FILE FROM DATA    00090020
      *              FROM THE ORACLE PXT_STR_MDSE_OH_TRKG TABLE.        00091020
      *              THIS IS DONE BY MATCHING AN EXTRACT FILE WITH THE  00091123
      *              ORACLE FILE. ALL MATCHES ARE WRITTEN OUT, WHETHER  00091223
      *              THE OH QTY IS ZERO. RECORDS THAT DID NOT HAVE A    00091323
      *              MATCH AND WHOSE OH QTY IS ZERO OR NEGATIVE WILL    00091423
      *              NOT BE WRITTEN OUT. WHITE TICKET AND NEW MARKS WILL00091523
      *              NEVER HAVE A MATCH AS WE ONLY ADJUST YELLOW TICKET 00091623
      *              ADDITIONAL MARK EVENTS.                            00091723
      *                                                                 00091823
      ******************** HISTORY OF CHANGES **************************00092020
      * CHG ID/                                                        *00093020
      * WR/PROJ          DATE        DESCRIPTION OF CHANGES            *00094020
      * ---------------  ----------  --------------------------------  *00095020
      * WR36056          06/24/2010  INITIAL IMPLEMENTATION             00096020
      *                                                                 00097020
      ******************************************************************00098020
                                                                        00099020
       ENVIRONMENT DIVISION.                                            00100020
                                                                        00110020
       CONFIGURATION SECTION.                                           00120020
                                                                        00130020
       SOURCE-COMPUTER. IBM-3090.                                       00140020
       OBJECT-COMPUTER. IBM-3090.                                       00150020
                                                                        00160020
       INPUT-OUTPUT SECTION.                                            00170020
       FILE-CONTROL.                                                    00180020
           SELECT YLLW-TCK-EXTR-FILE                                    00190020
               ASSIGN TO INP01.                                         00200020
                                                                        00210020
           SELECT YLLW-TCK-ADJ-FILE                                     00220020
               ASSIGN TO INP02.                                         00230020
                                                                        00240020
           SELECT OH-QTY-ADJ-FILE                                       00250020
               ASSIGN TO OUT01.                                         00260020
                                                                        00270020
       DATA DIVISION.                                                   00280020
                                                                        00290020
       FILE SECTION.                                                    00300020
       FD  YLLW-TCK-EXTR-FILE                                           00310020
           LABEL RECORDS ARE STANDARD                                   00320020
           RECORDING MODE IS F                                          00330020
           BLOCK CONTAINS 0 RECORDS.                                    00340020
       01  YLLW-TCK-EXTR-REC               PIC  X(222).                 00350020
                                                                        00360020
       FD  YLLW-TCK-ADJ-FILE                                            00370020
           LABEL RECORDS ARE STANDARD                                   00371020
           RECORDING MODE IS F                                          00372020
           BLOCK CONTAINS 0 RECORDS.                                    00373020
       01  YLLW-TCK-ADJ-REC                PIC  X(026).                 00374020
                                                                        00375020
       FD  OH-QTY-ADJ-FILE                                              00376020
           LABEL RECORDS ARE STANDARD                                   00377020
           RECORDING MODE IS F                                          00378020
           BLOCK CONTAINS 0 RECORDS.                                    00379020
       01  OH-QTY-ADJ-REC                  PIC X(213).                  00380020
                                                                        00390020
       WORKING-STORAGE SECTION.                                         00400020
                                                                        00410020
                                                                        00420020
                                                                        00430020
      ***************************************************************** 00440020
      *  PROGRAM'S ACCUMULATORS                                       * 00450020
      ***************************************************************** 00460020
       01  PA-PROGRAM-ACCUMULATORS.                                     00470020
           05 PA-RECORDS-READ            PIC 9(10)       VALUE ZEROES.  00480020
           05 PA-RECORDS-UPDATED         PIC 9(10)       VALUE ZEROES.  00490020
           05 PA-RECORDS-WRITTEN         PIC 9(10)       VALUE ZEROES.  00500023
           05 PA-RECORDS-REJECTED        PIC 9(10)       VALUE ZEROES.  00510023
                                                                        00520020
      ***************************************************************** 00530020
      *  PROGRAM'S CONSTANTS                                          * 00540020
      ***************************************************************** 00550020
       01  PC-PROGRAM-CONSTANTS.                                        00560020
           05  PC-PROGRAM-NAME             PIC X(08)       VALUE        00580020
               'PXKUT101'.                                              00590020
                                                                        00600020
      ***************************************************************** 00610020
      *  PROGRAM'S SWITCHES                                           * 00620020
      ***************************************************************** 00630020
       01  PS-PROGRAM-SWITCHES.                                         00640020
           05  PS-END-OF-EXTR-INP-FILE-SW PIC X(01)        VALUE 'N'.   00650020
               88  PS-END-OF-EXTR-INP-FILE                 VALUE 'Y'.   00660020
               88  PS-NOT-END-OF-EXTR-INP-FILE             VALUE 'N'.   00670020
           05  PS-END-OF-ADJ-INP-FILE-SW PIC X(01)         VALUE 'N'.   00680020
               88  PS-END-OF-ADJ-INP-FILE                  VALUE 'Y'.   00690020
               88  PS-NOT-END-OF-ADJ-INP-FILE              VALUE 'N'.   00700020
                                                                        00740020
      ***************************************************************** 00750020
      *  PROGRAM'S VARIABLES                                          * 00760020
      ***************************************************************** 00770020
       01  PV-PROGRAM-VARIABLES.                                        00780020
           05  PV-GP-RTL-QTY-X             PIC  9(10)  VALUE ZERO.      00790020
           05  PV-ADJ-QTY                  PIC S9(6)   VALUE ZEROES.    00870020
           05  PV-HOLD-ADJ-QTY             PIC S9(6)   VALUE ZEROES.    00880020
                                                                        00910020
                                                                        00920020
      ***************************************************************** 00930020
      *  ABEND CODE AREA                                              * 00940020
      ***************************************************************** 00950020
       01  ABEND-CODE                      PIC S9(04)  VALUE +0  COMP.  00960020
           88  ABEND-4013-DB-ERROR                     VALUE +4013.     01060020
                                                                        01100020
                                                                        01160020
      ******************************************************************01170020
      **COPYBOOKS                                                       01180020
      ******************************************************************01190020
      ****ON HAND QTY EXTRACT INPUT RECORD LAYOUT                       01200020
           COPY PXRD098.                                                01210020
                                                                        01220020
      ****ON HAND QTY ADJUSTMENT INPUT RECORD LAYOUT                    01230020
           COPY PXRD124.                                                01240020
                                                                        01250020
      ****YELLOW/WHITE TICKET EVENT EXTRACT OUTPUT RECORD LAYOUT        01260023
           COPY PXRD040.                                                01270020
                                                                        01280020
      *---------------------------------------------                    01290020
      *  DB2 COMMUNICATION AREA.                                        01300020
      *---------------------------------------------                    01310020
           EXEC SQL                                                     01320020
                INCLUDE SQLCA                                           01330020
           END-EXEC.                                                    01340020
                                                                        01350020
           COPY SQLCA2.                                                 01360020
      ******************************************************************01370020
      * DB2 ERROR MESSAGES FORMAT AREA.                                 01380020
      ******************************************************************01390020
           COPY DPWS004.                                                01400020
                                                                        01410020
      *                                                                 01420020
      ******************************************************************01430020
      *  DB2 TABLE PXT00000 (PXT_PRCHG_PRCG)                            01440020
      ******************************************************************01450020
           EXEC SQL                                                     01460020
               INCLUDE PXT00000                                         01470020
           END-EXEC.                                                    01480020
                                                                        01490020
       PROCEDURE DIVISION.                                              01500020
                                                                        01510020
      ******************************************************************01520020
      *    MAINLINE LOGIC                                              *01530020
      ******************************************************************01540020
                                                                        01550020
       0000-MAINLINE.                                                   01560020
                                                                        01570020
           PERFORM 1000-INITIALIZATION.                                 01580020
                                                                        01590020
           PERFORM 2000-PRCSS-YLLW-TCK-EXTR-FL                          01600020
               UNTIL PS-END-OF-EXTR-INP-FILE.                           01610020
                                                                        01620020
           PERFORM 9900-EOJ-LOGIC.                                      01630020
                                                                        01640020
           GOBACK.                                                      01650020
                                                                        01660020
      ******************************************************************01670020
      *    OPEN FILES AND READ FIRST RECORDS FROM INPUT FILES.         *01680020
      ******************************************************************01690020
       1000-INITIALIZATION.                                             01700020
                                                                        01710020
           OPEN INPUT  YLLW-TCK-EXTR-FILE                               01720020
                       YLLW-TCK-ADJ-FILE                                01730020
               OUTPUT  OH-QTY-ADJ-FILE.                                 01740020
                                                                        01750020
           PERFORM 6100-READ-YLLW-TCK-EXTR-FL.                          01760020
           PERFORM 6200-READ-YLLW-TCK-ADJ-FL.                           01770020
           IF PS-END-OF-EXTR-INP-FILE                                   01780020
               DISPLAY ' *** NOTHING TO PROCESS ***'                    01790020
               DISPLAY ' '                                              01800020
           END-IF.                                                      01810020
                                                                        01820020
      ******************************************************************01830020
      *    2000-PRCSS-YLLW-TCK-EXTR-FL                                 *01840020
      *    THE INPUT FILES ARE IN SKU, STORE SEQUENCE.                 *01850020
      ******************************************************************01860020
       2000-PRCSS-YLLW-TCK-EXTR-FL.                                     01870020
                                                                        01880020
           MOVE PXRD098-INTR-UPC-ID TO PXT00000-INTR-UPC-ID.            01890020
           MOVE PXRD098-STR-NBR     TO PXT00000-LOC-NBR.                01900020
           PERFORM 8000-SKU-OH-LOOKUP-SELECT.                           01910020
           IF PXRD124-SKU-NBR = PXRD098-SKU-NBR                         01920020
             IF PXRD124-STR-NBR = PXRD098-STR-NBR                       01930020
               MOVE PXRD124-OH-ADJ-QTY TO PV-HOLD-ADJ-QTY               01940020
               COMPUTE PV-ADJ-QTY = PXT00000-STR-EXPTED-QTY -           01950020
                 PV-HOLD-ADJ-QTY                                        01960020
               MOVE PV-ADJ-QTY TO PXT00000-STR-EXPTED-QTY               01970020
               MOVE PXT00000-STR-EXPTED-QTY TO PXRD040-STR-EXPTED-QTY   01980021
               ADD 1 TO PA-RECORDS-UPDATED                              01990020
               PERFORM 2100-MOVE-TO-PXRD040                             02000020
               PERFORM 6300-WRITE-OH-QTY-ADJ-FL                         02010020
               PERFORM 6200-READ-YLLW-TCK-ADJ-FL                        02020020
               PERFORM 6100-READ-YLLW-TCK-EXTR-FL                       02030020
             ELSE                                                       02040020
               IF PXRD124-STR-NBR < PXRD098-STR-NBR                     02050020
                 ADD 1 TO PA-RECORDS-REJECTED                           02060020
                 IF PA-RECORDS-REJECTED < 1001                          02070020
                   DISPLAY ' REJ - ' PA-RECORDS-READ ' '                02080020
                                     PXRD124-OH-ADJ-RECORD              02090020
                 END-IF                                                 02100020
                 PERFORM 6200-READ-YLLW-TCK-ADJ-FL                      02110020
               ELSE                                                     02120020
                 IF PXT00000-STR-EXPTED-QTY > 0                         02130020
                   MOVE PXT00000-STR-EXPTED-QTY TO                      02140021
                        PXRD040-STR-EXPTED-QTY                          02141021
                   PERFORM 2100-MOVE-TO-PXRD040                         02150020
                   PERFORM 6300-WRITE-OH-QTY-ADJ-FL                     02160020
                 END-IF                                                 02170020
                 PERFORM 6100-READ-YLLW-TCK-EXTR-FL                     02180020
           ELSE                                                         02190020
           IF PXRD124-SKU-NBR < PXRD098-SKU-NBR                         02200020
             IF PXT00000-STR-EXPTED-QTY > 0                             02210020
               MOVE PXT00000-STR-EXPTED-QTY TO PXRD040-STR-EXPTED-QTY   02220021
               PERFORM 2100-MOVE-TO-PXRD040                             02230020
               PERFORM 6300-WRITE-OH-QTY-ADJ-FL                         02240020
             END-IF                                                     02250020
             PERFORM 6200-READ-YLLW-TCK-ADJ-FL                          02260020
           ELSE                                                         02270020
             IF PXT00000-STR-EXPTED-QTY > 0                             02280020
               MOVE PXT00000-STR-EXPTED-QTY TO PXRD040-STR-EXPTED-QTY   02290021
               PERFORM 2100-MOVE-TO-PXRD040                             02300020
               PERFORM 6300-WRITE-OH-QTY-ADJ-FL                         02310020
             END-IF                                                     02320020
             PERFORM 6100-READ-YLLW-TCK-EXTR-FL.                        02330020
                                                                        02360020
      ******************************************************************02370020
      *  MOVE INPUT FORMAT TO OUTPUT FORMAT.                            02380020
      ******************************************************************02390020
       2100-MOVE-TO-PXRD040.                                            02400020
           MOVE PXT00000-IN-TRNST-QTY      TO PXRD040-IN-TRNST-QTY.     02410021
           MOVE PXRD098-UNT-RTL-AMT        TO PXRD040-UNT-RTL-AMT.      02411021
           MOVE PXRD098-PRCHG-ID           TO PXRD040-PRCHG-ID.         02420020
           MOVE PXRD098-STR-GP-TYP-CDE     TO PXRD040-STR-GP-TYP-CDE.   02430020
           MOVE PXRD098-STR-GP-ID          TO PXRD040-STR-GP-ID.        02440020
           MOVE PXRD098-EFF-DATE           TO PXRD040-EFF-DATE.         02450020
           MOVE PXRD098-STR-NBR            TO PXRD040-STR-NBR.          02460020
           MOVE PXRD098-LOC-NM             TO PXRD040-LOC-NM.           02470020
           MOVE PXRD098-BUS-AREA-ID        TO PXRD040-BUS-AREA-ID.      02480020
           MOVE PXRD098-BUS-AREA-DESC      TO PXRD040-BUS-AREA-DESC.    02490020
           MOVE PXRD098-DEPT-NBR           TO PXRD040-DEPT-NBR.         02500020
           MOVE PXRD098-VS-NBR             TO PXRD040-VS-NBR.           02510020
           MOVE PXRD098-LBL-NM             TO PXRD040-LBL-NM.           02520020
           MOVE PXRD098-VS-DESC            TO PXRD040-VS-DESC.          02530020
           MOVE PXRD098-STKR-NBR           TO PXRD040-STKR-NBR.         02540020
           MOVE PXRD098-SUB-CL-NBR         TO PXRD040-SUB-CL-NBR.       02550020
           MOVE PXRD098-LAST-UPD-TMST      TO PXRD040-LAST-UPD-TMST.    02560020
           MOVE PXRD098-LAST-NCLR-RTL-AMT  TO PXRD040-LAST-NCLR-RTL-AMT.02570020
           MOVE PXRD098-EVNT-CTG-CDE       TO PXRD040-EVNT-CTG-CDE.     02580020
           MOVE PXRD098-PRCHG-TYP-CDE      TO PXRD040-PRCHG-TYP-CDE.    02590020
           MOVE PXRD098-SKU-NBR            TO PXRD040-SKU-NBR.          02600020
           MOVE PXRD098-GP-RTL-QTY         TO PV-GP-RTL-QTY-X.          02610020
           IF PV-GP-RTL-QTY-X NUMERIC                                   02620020
             MOVE PXRD098-GP-RTL-QTY       TO PXRD040-GP-RTL-QTY        02640020
           ELSE                                                         02650020
             MOVE ZERO                     TO PXRD040-GP-RTL-QTY.       02660020
           MOVE PXRD098-GP-RTL-QTY-NULL-IND TO                          02670020
                PXRD040-GP-RTL-QTY-NULL-IND.                            02671020
           IF PXRD098-GP-RTL-AMT NUMERIC                                02671120
             MOVE PXRD098-GP-RTL-AMT       TO PXRD040-GP-RTL-AMT        02671220
           ELSE                                                         02671320
             MOVE ZERO                     TO PXRD040-GP-RTL-AMT.       02671420
           MOVE PXRD098-GP-RTL-AMT-NULL-IND TO                          02671520
                PXRD040-GP-RTL-AMT-NULL-IND.                            02671620
                                                                        02671720
      ******************************************************************02671820
      *  READ EXTRACT INPUT FILE                                        02671920
      ******************************************************************02672020
       6100-READ-YLLW-TCK-EXTR-FL.                                      02673020
                                                                        02674020
           READ YLLW-TCK-EXTR-FILE INTO PXRD098-EXTRACT-RECORD          02675020
               AT END SET PS-END-OF-EXTR-INP-FILE TO TRUE.              02676020
                                                                        02677020
           IF PS-NOT-END-OF-EXTR-INP-FILE                               02678020
               ADD 1 TO PA-RECORDS-READ                                 02679020
           END-IF.                                                      02680020
                                                                        02690020
      ******************************************************************02700020
      *  READ ADJUSTMENT INPUT FILE                                     02710020
      ******************************************************************02720020
       6200-READ-YLLW-TCK-ADJ-FL.                                       02730020
                                                                        02740020
           READ YLLW-TCK-ADJ-FILE INTO PXRD124-OH-ADJ-RECORD            02750020
               AT END SET PS-END-OF-ADJ-INP-FILE TO TRUE.               02751020
                                                                        02752020
           IF PS-END-OF-ADJ-INP-FILE                                    02753020
               MOVE ALL '9' TO PXRD124-SKU-NBR                          02754020
                               PXRD124-STR-NBR                          02755020
           END-IF.                                                      02756020
                                                                        02757020
      ******************************************************************02758020
      *  WRITE OH QTY ADJ FILE                                          02759020
      ******************************************************************02760020
       6300-WRITE-OH-QTY-ADJ-FL.                                        02770020
           ADD 1 TO PA-RECORDS-WRITTEN.                                 02780020
           WRITE OH-QTY-ADJ-REC                                         02790020
             FROM PXRD040-EXTRACT-RECORD.                               02800021
                                                                        02810020
      *------------------------------------------------------           02820020
      * 8000-SKU-OH-LOOKUP-SELECT.                                      02830020
      *     LOOKS UP THE OH AND IN TRANSIT QTY FROM THE                 02840020
      *     THE PXT_PRCHG_PRCHG TABLE.                                  02850020
      *------------------------------------------------------           02860020
                                                                        02870020
       8000-SKU-OH-LOOKUP-SELECT.                                       02880020
                                                                        02890020
           EXEC SQL                                                     02900020
              SELECT A.STR_EXPTED_QTY                                   02910020
                    ,A.IN_TRNST_QTY                                     02920020
              INTO  :PXT00000-STR-EXPTED-QTY                            02930020
                   ,:PXT00000-IN-TRNST-QTY                              02940020
              FROM   PXT_PRCHG_PRCG    A                                02950020
              WHERE A.INTR_UPC_ID    = :PXT00000-INTR-UPC-ID            02960020
                AND A.LOC_NBR        = :PXT00000-LOC-NBR                02970020
              WITH UR                                                   02980020
           END-EXEC.                                                    02990020
                                                                        03000020
           EVALUATE TRUE                                                03010020
              WHEN SQLCODE = +0                                         03020020
                CONTINUE                                                03030020
              WHEN SQLCODE = +100                                       03040020
                  MOVE 0 TO PXT00000-STR-EXPTED-QTY                     03070020
                            PXT00000-IN-TRNST-QTY                       03080020
              WHEN OTHER                                                03090020
                DISPLAY 'SELECT FAILED IN PARA 8000'                    03100020
                DISPLAY 'LOC-NBR:     ' PXT00000-LOC-NBR                03110020
                DISPLAY 'INTR-UPC:    ' PXT00000-INTR-UPC-ID            03120020
                PERFORM 9980-SQL-ERROR                                  03130020
           END-EVALUATE.                                                03140020
                                                                        03150020
      ******************************************************************03160020
      *    9900-EOJ-LOGIC.                                              03170020
      *      THIS IS PERFORMED AT END OF INPUT FILE.                    03180020
      *      PURPOSE: CLOSE INPUT FILE,                                 03190020
      *      DISPLAY RECORD COUNTS.                                     03200020
      ******************************************************************03210020
       9900-EOJ-LOGIC.                                                  03220020
                                                                        03230020
           CLOSE YLLW-TCK-EXTR-FILE                                     03240020
                 YLLW-TCK-ADJ-FILE                                      03250020
                 OH-QTY-ADJ-FILE.                                       03260020
                                                                        03270020
           DISPLAY ' '.                                                 03280020
           DISPLAY 'RECORDS READ         ' PA-RECORDS-READ.             03290020
           DISPLAY 'RECORDS UPDATED      ' PA-RECORDS-UPDATED.          03300020
           DISPLAY 'UPDATE RECS NOT FOUND' PA-RECORDS-REJECTED.         03310020
           DISPLAY 'RECORDS WRITTEN      ' PA-RECORDS-WRITTEN.          03320020
           DISPLAY '**------ END ' PC-PROGRAM-NAME ' ------**'.         03330020
                                                                        03340020
      ******************************************************************03350020
      *    END OF SOURCE CODE FOR PXKUT101                             *03360020
      ******************************************************************03370020
      *----------------------------------------------------------       03380020
       9980-SQL-ERROR.                                                  03390020
      *----------------------------------------------------------       03400020
                                                                        03410020
           MOVE SQLCODE    TO SQLCODE2.                                 03420020
           MOVE SQLERRD(3) TO SQLERRD3.                                 03430020
           DISPLAY '** PROGRAM        ** = PXKUT100'.                   03440020
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  03450020
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  03460020
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                   03470020
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  03480020
                                                                        03490020
           SET ABEND-4013-DB-ERROR  TO TRUE.                            03500020
           CALL 'ILBOABN0' USING ABEND-CODE.                            03510020
                                                                        03520020
      *                                                                 03530020
