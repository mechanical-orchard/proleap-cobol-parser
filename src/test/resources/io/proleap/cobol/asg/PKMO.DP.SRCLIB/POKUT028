000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    POKUT028.                                                 
000600 AUTHOR.        PATI GREEN.                                               
000700 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000800 DATE-WRITTEN.  08/12/98.                                                 
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM WILL PRODUCE AN ON-ORDER EXTRACT FILE CONSISTING          
001300*  OF ALL ACTIVE/APPROVED (AC/AP) PURCHASE ORDERS WHICH HAVE NOT          
001400*  BEEN COMPLETELY RECEIVED.  THE EXTRACT FILE WILL CONTAIN 1             
001500*  RECORD FOR EACH DATE/PO-NBR/SKU/STORE.                                 
001600*                                                                         
001700*  A MASTER/SPECIFIC PLAN WILL BE CONSIDERED COMPLETE AND BYPASSED        
001800*  FROM PROCESSING IF THE PLAN'S TOTAL RECEIPT QTY >= DC SPLIT QTY        
001900*                                                                         
002000*  PC-NBR-OF-SHIP-DAYS IS THE NUMBER OF DAYS TO BE ADDED TO THE           
002100*  PLNSHP-DONT-SHIP-BEF-DTE TO GIVE THE IN-DC-DATE.  THE SUNDAY           
002200*  DATE (FISCAL WEEK DATE) FOR THE IN-DC-DATE BECOMES THE                 
002300*  WEEK-DATE FOR THE ORDER.                                               
002400*                                                                         
002500*  ORDERS WITH A WEEK DATE PRIOR TO THE CURRENT SUNDAY DATE WILL          
002600*  BE ASSIGNED THE CURRENT SUNDAY WEEK-DATE, SO THESE "OLD" OPEN          
002700*  ORDERS CAN EVENTUALLY GET ROLLED INTO THE CURRENT WEEK IN              
002800*  SUBSEQUENT PROGRAMS.                                                   
002900*                                                                         
003000*  A DATE CONTROL CARD IS READ WHICH CONTAINS THE CURRENT PROCESS-        
003100*  ING DATE (FOR USE IN RETRIEVING RECEIPT INFO) AND THE CURRENT          
003200*  SUNDAY DATE (WEEK DATE FOR SOME OUTPUT RECORDS).                       
003300*                                                                         
003400******************************************************************        
003500* DATE  12/22/99                                                 *        
003600*   CHANGES MADE FOR REGIONAL CLEARANCE PRICING:                 *        
003700*     FOR ITEMS THAT HAVE A TSK-REGN-PRIC IND OF 'Y'             *        
003800*     PASS TUPCPLS UPC-STAT-CDE TO DATA WAREHOUSE ON ORDER       *        
003900*     FILE'S STATUS CODE IF A ROW EXISTS. DEFAULT THE STATUS     *        
004000*     CODE TO 20 IF ROW DOES NOT EXIST.                          *        
004100*     FOR ITEMS THAT HAVE A TSK-REGN-PRIC-IND OF 'N'             *        
004200*     CONTINUE TO USE TSK STATUS CODE.                           *        
004300*                                                                *        
004400*   MADE BY: K. SCHOLEY                    WR #: 20195           *        
004500******************************************************************        
004600* DATE  03/13/00                                                 *        
004700*   CORRECTION TO CURSOR:                                        *        
004800*     THE PO CURSOR HAS 2 IDENTICAL STATEMENTS IN THE WHERE      *        
004900*     CLAUSE. REMOVED 1 OF THEM.                                 *        
005000*                                                                *        
005100*   MADE BY: K. SCHOLEY                    WR #: 20195           *        
005200******************************************************************        
005300* DATE  03/21/00                                                 *        
005400*   CHANGE TO CONDITION WHICH EXECUTES 8700-SELECT-TUPCPLS       *        
005500*     FOR RCP, PER BETTY DORST.                                  *        
005600*                                                                *        
005700*   MADE BY: K. SCHOLEY                    WR #: 20195           *        
005800******************************************************************        
005900* DATE  10/30/01                                                 *        
006000*   MADE ALL READS OF TRECDIS DIRTY READS.                       *        
006100*   MADE BY: R. STAUFFACHER                PITS#: 429375         *        
006200******************************************************************        
006300* 11/23/01 CHANGES MADE BY MIKE FRIESS   WR# 6349                         
006400* CHANGES: REPLACE OLD CALENDAR CALL WITH NEW OTB DATE ROUTINE            
006500*          WHEN DERIVING OTB DATES                                        
006600******************************************************************        
006700* 04/25/02 CHANGES MADE BY RYAN STAUFFACHER WR 23597                      
006800* CHANGES: CHANGE PO ITEM CURSOR TO INCLUDE COMPLETED LINES               
006900******************************************************************        
007000* 05/13/03 - RAMAKRISHNA PRASAD V, IGSI - WR NO. 24803           *        
007100*                                                                *        
007200*       - COPY BOOK PORD018 CHANGED FOR NON-INTELLIGENT SKU      *        
007300*         IMPLEMENTATION                                         *        
007400*       - REMOVED THE REFERENCES TO TSK TABLE                    *        
007500*         AND REPLACED WITH XLT_SKU, XLT_VND_STYL AND TUPCPLS    *        
007600*         TABLES  FOR NI SKU CONVERSION                          *        
007700*                                                                *        
007800******************************************************************        
007900* 08/08/03 CHANGES MADE BY VISWA KARUTURI WR# P000573763                  
008000* CHANGES: FIXED THE PROBLEM 'WRITING STATUS 25 RECORDS'                  
008100******************************************************************        
008200* 08/08/07 - JACK KRAMER    WR 31476                                      
008300*       - ADDED MQ PROCESSING TO SEND AN 'END RUN OF PROGRAM'             
008400*         MESSAGE TO THE MA QUEUE FOR SAME DAY ORDER QUANTITY             
008500*         CHANGES FOR KMA                                                 
008600******************************************************************        
008610******************************************************************        
008620* 29/06/09 - TCS                                                          
008630*       - THE SELECT QUERY IN SECTION 7700-GET-RECDIS CAN BE              
008640*         ELIMINATED BY CHANGING THE QUERY IN 7200-SUM-RECDIS-DC          
008650*         INTO A CURSOR WITH A GROUP BY ON A.STR_NBR.                     
008651*       - TO FIND CHANGES SEARCH 'TCS-MIPS-RDN'.                          
008660******************************************************************        
008620* 03/11/10 - COGNIZANT                                                    
008630*       - EFC2 TO EFC1 CONVERSION                                         
008651*       - TO FIND CHANGES SEARCH 'EFC2 TO EFC1                            
008660******************************************************************        
PK4679* 08/06/10 - COGNIZANT                                                    
PK4679*       - PO_CSR CURSOR TUNED FOR IMPROVING PERFORMANCE                   
008660******************************************************************        
      * 08/18/10 - COGNIZANT (SANDEEP) - WR3119D                                
      *       - MLOF PROJECT CHANGES                                            
      *       - REMOVED THE EFC2 CONVERSION CHANGES.                            
      ******************************************************************        
      * 10/15/15 - COGNIZANT  - CHG0125633                                      
      *       - CHANGES MADE IN SUM-RECDIS-CSR CURSOR TO FETCH RECEIPT          
      *         USING STORE NUMBER ASSOCIATED WITH DC INSTEAD                   
      *         OF DC NUMBER.                                                   
C98147*****************************************************************         
      * 12/15/15 MADE BY: COGNIZANT               PRB0056606                    
      * CHANGES: HANDLED THE INVALID DO-NOT-SHIP-BEF-DATE ISSUE IN              
      *          IN PROGRAM POKUT028 BY SKIPPING THE POS WITH                   
      *          INVALID DO-NOT-SHIP-BEF-DATE                                   
      ******************************************************************        
      *****************************************************************         
      * 02/10/16 MADE BY: COGNIZANT               CHG00XXXXX                    
      * CHANGES: MQPUT LOGIC TO PUT 'END RUN PROGRAM' TO THE SAME ORDER         
      *          QUANTITY MQ FOR MTK-II IS ADDED.                               
      ******************************************************************        
      *****************************************************************         
      * 05/03/19 MADE BY: COGNIZANT               CHG00XXXXX                    
      * CHANGES: MQPUT LOGIC TO PUT 'END RUN PROGRAM' TO THE SAME ORDER         
      *          QUANTITY MQ IS REMOVED DUE tO MTK 2 AP DECOMMISSION            
      ******************************************************************        
008800******************************************************************        
008700 ENVIRONMENT DIVISION.                                                    
008900                                                                          
009000 CONFIGURATION SECTION.                                                   
009100                                                                          
009200 SOURCE-COMPUTER. IBM-3090.                                               
009300 OBJECT-COMPUTER. IBM-3090.                                               
009400                                                                          
009500 INPUT-OUTPUT SECTION.                                                    
009600                                                                          
009700 FILE-CONTROL.                                                            
009800     SELECT DATE-CONTROL-CARD                                             
009900         ASSIGN TO INP01.                                                 
010000     SELECT MQ-CONTROL-CARD-1                                             
010100         ASSIGN TO INP02.                                                 
010200     SELECT MQ-CONTROL-CARD-2                                             
010300         ASSIGN TO INP03.                                                 
010400     SELECT MQ-CONTROL-CARD-3                                             
010500         ASSIGN TO INP04.                                                 
010600     SELECT MQ-CONTROL-CARD-4                                             
010700         ASSIGN TO INP05.                                                 
010800     SELECT PO-ONORD-EXT-FILE                                             
010900         ASSIGN TO OUT01.                                                 
011000                                                                          
011100     SELECT PO-ONORD-PDUE-FILE                                            
011200         ASSIGN TO OUT02.                                                 
011300                                                                          
011400******************************************************************        
011500 DATA DIVISION.                                                           
011600******************************************************************        
011700                                                                          
011800 FILE SECTION.                                                            
011900                                                                          
012000 FD  DATE-CONTROL-CARD                                                    
012100     RECORDING MODE IS F                                                  
012200     LABEL RECORDS ARE STANDARD                                           
012300     BLOCK CONTAINS 0 RECORDS.                                            
012400                                                                          
012500 01  DATE-CONTROL-RECORD         PIC X(80).                               
012600                                                                          
012700 FD  MQ-CONTROL-CARD-1                                                    
012800     RECORDING MODE F                                                     
012900     BLOCK CONTAINS 0 RECORDS                                             
013000     LABEL RECORDS ARE OMITTED.                                           
013100 01  MQ-CONTROL-CARD-1-LINE           PIC  X(80).                         
013200                                                                          
013300 FD  MQ-CONTROL-CARD-2                                                    
013400     RECORDING MODE F                                                     
013500     BLOCK CONTAINS 0 RECORDS                                             
013600     LABEL RECORDS ARE OMITTED.                                           
013700 01  MQ-CONTROL-CARD-2-LINE           PIC  X(80).                         
013800                                                                          
013900 FD  MQ-CONTROL-CARD-3                                                    
014000     RECORDING MODE F                                                     
014100     BLOCK CONTAINS 0 RECORDS                                             
014200     LABEL RECORDS ARE OMITTED.                                           
014300 01  MQ-CONTROL-CARD-3-LINE           PIC  X(80).                         
014400                                                                          
014500 FD  MQ-CONTROL-CARD-4                                                    
014600     RECORDING MODE F                                                     
014700     BLOCK CONTAINS 0 RECORDS                                             
014800     LABEL RECORDS ARE OMITTED.                                           
014900 01  MQ-CONTROL-CARD-4-LINE           PIC  X(80).                         
015000                                                                          
015100 FD  PO-ONORD-EXT-FILE                                                    
015200     RECORDING MODE IS F                                                  
015300     LABEL RECORDS ARE STANDARD                                           
015400     RECORD CONTAINS 104 CHARACTERS                                       
015500     BLOCK CONTAINS 0 RECORDS                                             
015600     DATA RECORD IS PO-ONORD-EXT-REC.                                     
015700                                                                          
015800 01  PO-ONORD-EXT-REC               PIC X(104).                           
015900 FD  PO-ONORD-PDUE-FILE                                                   
016000     RECORDING MODE IS F                                                  
016100     LABEL RECORDS ARE STANDARD                                           
016200     RECORD CONTAINS 104 CHARACTERS                                       
016300     BLOCK CONTAINS 0 RECORDS                                             
016400     DATA RECORD IS PO-ONORD-PDUE-REC.                                    
016500                                                                          
016600 01  PO-ONORD-PDUE-REC               PIC X(104).                          
016700                                                                          
016800                                                                          
016900 WORKING-STORAGE SECTION.                                                 
017000                                                                          
017100 01  PC-PROGRAM-CONSTANTS.                                                
017200     05  PC-PROGRAM-NAME            PIC  X(08) VALUE 'POKUT028'.          
017300     05  PC-MAX-RETRIES             PIC S9(04) VALUE  +3 COMP.            
017400     05  PC-899                     PIC  9(03) VALUE  899.                
017500     05  PC-NBR-OF-SHIP-DAYS        PIC  9(02) VALUE  10.                 
017600     05  PC-MSG-TEXT                PIC X(100)                            
017700                                   VALUE 'END RUN OF PROGRAM'.            
017800                                                                          
017900 01  PS-PROGRAM-SWITCHES.                                                 
018000     05  PS-END-OF-DATE-CONTROL-CARD-SW PIC X  VALUE 'N'.                 
018100         88  PS-END-OF-DATE-CONTROL-CARD       VALUE 'Y'.                 
018200                                                                          
018300     05  PS-ITEMS-LEFT-ON-DB-SW     PIC X      VALUE 'Y'.                 
018400        88  PS-ITEMS-LEFT-ON-DB                VALUE 'Y'.                 
018500        88  PS-NO-ITEMS-LEFT-ON-DB             VALUE 'N'.                 
018600                                                                          
018700     05  PS-ITEMS-LEFT-DC-SW        PIC X      VALUE 'Y'.                 
018800        88  PS-DC-LEFT                         VALUE 'Y'.                 
018900        88  PS-NO-DC-LEFT                      VALUE 'N'.                 
019000     05  PS-PAST-DUE-SW             PIC X      VALUE 'Y'.                 
019100        88  PS-PAST-DUE                        VALUE 'Y'.                 
019200        88  PS-NOT-PAST-DUE                    VALUE 'N'.                 
019300                                                                          
019400     05  PS-ITEMS-LEFT-ON-TALLPLN-SW PIC X     VALUE 'Y'.                 
019500        88  PS-ITEMS-LEFT-ON-TALLPLN           VALUE 'Y'.                 
019600        88  PS-NO-ITEMS-LEFT-ON-TALLPLN        VALUE 'N'.                 
019700                                                                          
019800     05  PS-ITEMS-LEFT-ON-TMEATDS-SW PIC X     VALUE 'Y'.                 
019900        88  PS-ITEMS-LEFT-ON-TMESTDS           VALUE 'Y'.                 
020000        88  PS-NO-ITEMS-LEFT-ON-TMESTDS        VALUE 'N'.                 
020100                                                                          
020200     05  PS-STORE-FOUND-SW          PIC X      VALUE 'N'.                 
020300        88  PS-STORE-FOUND                     VALUE 'Y'.                 
020400        88  PS-NO-STORE-FOUND                  VALUE 'N'.                 
020500                                                                          
020600     05  PS-TALLPLN-EXISTS-SW       PIC X      VALUE 'N'.                 
020700        88  PS-TALLPLN-EXISTS                  VALUE 'Y'.                 
020800        88  PS-NO-TALLPLN-EXISTS               VALUE 'N'.                 
020900     05  PS-CONTROL-CARD-1-SW      PIC X(01) VALUE 'N'.                   
021000         88  PS-CONTROL-CARD-1-END           VALUE 'Y'.                   
021100         88  PS-CONTROL-CARD-1-START         VALUE 'N'.                   
021200     05  PS-CONTROL-CARD-2-SW      PIC X(01) VALUE 'N'.                   
021300         88  PS-CONTROL-CARD-2-END           VALUE 'Y'.                   
021400         88  PS-CONTROL-CARD-2-START         VALUE 'N'.                   
021500     05  PS-CONTROL-CARD-3-SW      PIC X(01) VALUE 'N'.                   
021600         88  PS-CONTROL-CARD-3-END           VALUE 'Y'.                   
021700         88  PS-CONTROL-CARD-3-START         VALUE 'N'.                   
021800     05  PS-CONTROL-CARD-4-SW      PIC X(01) VALUE 'N'.                   
021900         88  PS-CONTROL-CARD-4-END           VALUE 'Y'.                   
022000         88  PS-CONTROL-CARD-4-START         VALUE 'N'.                   
022010** TCS-MIPS-RDN-START                                                     
022020     05  PS-SUM-RECDIS-CSR-SW      PIC X(01) VALUE 'N'.                   
022030         88  PS-SUM-RECDIS-CSR-END           VALUE 'Y'.                   
022040         88  PS-SUM-RECDIS-CSR-START         VALUE 'N'.                   
022050     05  PS-STR-MATCH-SW           PIC X(01) VALUE 'N'.                   
022060         88  PS-STR-MATCH-FOUND              VALUE 'Y'.                   
022070         88  PS-STR-MATCH-NOT-FOUND          VALUE 'N'.                   
022080** TCS-MIPS-RDN-END                                                       
022100                                                                          
022200                                                                          
022300 01  PV-PROGRAM-VARIABLES.                                                
022400     05  PV-PO-CURSOR-COUNT         PIC S9(09) COMP-3                     
022500                                               VALUE ZEROES.              
022600     05  PV-OUTPUT-REC-COUNT        PIC S9(09) COMP-3                     
022700                                               VALUE ZEROES.              
022800     05  PV-RETRY-COUNTER           PIC S9(04) VALUE +0                   
022900                                               COMP SYNC.                 
023000     05  ABEND-CODE                 PIC S9(04) VALUE +0                   
023100                                               COMP SYNC.                 
023200         88  AC-MQ-ERROR                       VALUE +4020.               
023300     05  PV-PARAGRAPH-NAME          PIC  X(30) VALUE SPACES.              
023400                                                                          
023500     05  PV-DB2-OPER-ATTEMPTED      PIC  X(30) VALUE SPACES.              
023600                                                                          
023700     05  PV-VERIFY-QTY-NULL         PIC S9(04) VALUE +0                   
023800                                               COMP SYNC.                 
023900     05  PV-DC-VERIFY-QTY-NULL      PIC S9(04) VALUE +0                   
024000                                               COMP SYNC.                 
024010* TCS-MIPS-REDN-START                                                     
024020     05  PV-DC-TOTAL-QTY            PIC S9(09) VALUE +0                   
024030                                               COMP SYNC.                 
024040* TCS-MIPS-REDN-END                                                       
024100     05  PV-DC-MESTDS-QTY-NULL      PIC S9(04) VALUE +0                   
024200                                               COMP SYNC.                 
024300     05  PV-DC-QTY                  PIC S9(09) COMP-3                     
024400                                               VALUE ZEROES.              
024500     05  PV-DC-NBR                  PIC S9(04) COMP VALUE ZERO.           
024600     05  PV-DC-PLAN-NBR             PIC S9(04) COMP VALUE ZERO.           
024700     05  PV-DC-PLAN-STATUS          PIC X(02)  VALUE SPACES.              
024800     05  PV-DC-ALLPLN-QTY           PIC S9(09) COMP-3                     
024900                                               VALUE ZEROES.              
025000     05  PV-DC-MESTDS-QTY           PIC S9(09) COMP-3                     
025100                                               VALUE ZEROES.              
025200     05  PV-CURR-SPLIT-QTY          PIC S9(09) COMP                       
025300                                               VALUE ZEROES.              
025400                                                                          
025500     05  PV-STR-NBR-9               PIC S9(04) COMP                       
025600                                               VALUE ZERO.                
025700     05  PV-STR-QTY                 PIC S9(09) COMP-3                     
025800                                               VALUE ZERO.                
025900     05  PV-STR-PROMO-QTY           PIC S9(09) COMP-3                     
026000                                               VALUE ZERO.                
026100     05  PV-PO-TYP-CDE              PIC X(02)  VALUE SPACES.              
026100     05  PV-COUNT                   PIC 9(04)  VALUE ZERO.                
026100     05  PV-PREV-PO-NBR             PIC S9(09) COMP-3                     
026400                                               VALUE ZEROES.              
026100     05  PV-CUR-PO-NBR             PIC S9(09) COMP-3                      
026400                                               VALUE ZEROES.              
026200     05  PV-PO-ENTRY.                                                     
026300         10  PV-PO-NBR              PIC S9(09) COMP-3                     
026400                                               VALUE ZEROES.              
026500         10  PV-ITM-NBR             PIC S9(15) COMP-3                     
026600                                               VALUE ZEROES.              
026700         10  PV-DEPT-NBR            PIC X(03)  VALUE SPACES.              
026800         10  PV-MAJ-CL-NBR          PIC 9(02)  VALUE ZEROES.              
026900         10  PV-SUB-CL-NBR          PIC 9(02)  VALUE ZEROES.              
027000         10  PV-SKU-NBR             PIC 9(08)  VALUE ZEROES.              
027100         10  PV-GROUP-CODE          PIC X(03)  VALUE SPACES.              
027200         10  PV-STATUS-CODE         PIC X(02)  VALUE ZEROS.               
027300         10  PV-LONG-VENDOR-STYLE   PIC X(20)  VALUE SPACES.              
027400         10  PV-ENT-ID              PIC S9(09) COMP-3                     
027500                                               VALUE ZEROES.              
027600         10  PV-UNT-COST-AMT        PIC S9(04)V999 COMP-3                 
027700                                               VALUE ZEROES.              
027800         10  PV-UNT-RTL-PR-AMT      PIC S9(07)V99 COMP-3                  
027900                                               VALUE ZEROES.              
028000                                                                          
028100     05  PV-SYSTEM-DATE.                                                  
028200         10  PV-SYSTEM-YY           PIC 9(02) VALUE ZEROES.               
028300         10  PV-SYSTEM-MM           PIC 9(02) VALUE ZEROES.               
028400         10  PV-SYSTEM-DD           PIC 9(02) VALUE ZEROES.               
028500                                                                          
028600     05  PV-SYSTEM-DATE-ISO-FMT.                                          
028700         10  PV-SYSTEM-FMT-CC       PIC X(02) VALUE SPACES.               
028800         10  PV-SYSTEM-FMT-YY       PIC X(02) VALUE SPACES.               
028900         10  FILLER                 PIC X(01) VALUE '-'.                  
029000         10  PV-SYSTEM-FMT-MM       PIC X(02) VALUE SPACES.               
029100         10  FILLER                 PIC X(01) VALUE '-'.                  
029200         10  PV-SYSTEM-FMT-DD       PIC X(02) VALUE SPACES.               
029300                                                                          
029400     05  PV-FIRST-FP-DAY            PIC X(10) VALUE SPACES.               
029500     05  PV-HOLD-SHIP-DATE          PIC X(10) VALUE SPACES.               
029600     05  PV-HOLD-IN-DC-DATE         PIC X(10) VALUE SPACES.               
029700     05  PV-HOLD-WEEK-DATE          PIC X(10) VALUE SPACES.               
029800     05  PV-DEFAULT-STATUS-CODE     PIC X(2)  VALUE '20'.                 
029900     05  PV-UPCPLS-UPC-STAT-CDE     PIC X(2)  VALUE SPACES.               
030000     05  PV-QM-NAME               PIC X(48).                              
030100     05  PV-MSGBUFFER.                                                    
030200         10  PV-MSGBUFFER-ARRAY   PIC X(1) OCCURS 4096 TIMES.             
030210* TCS-MIPS-REDN-START                                                     
030220    03   PV-STR-NBR-ARRY-P.                                               
030230     05  PV-STR-NBR-ARRY OCCURS 9999 TIMES.                               
030240         10  PV-STR-NBR           PIC S9(4)  COMP.                        
030250         10  PV-DC-SUM            PIC S9(04) COMP SYNC.                   
030260     05  PV-STR-INDX              PIC 9(04) VALUE 0.                      
030270     05  PV-LAST-STR-INDX         PIC 9(04) VALUE 0.                      
030280* TCS-MIPS-REDN-END                                                       
030300     05  PV-MSGLENGTH             PIC S9(9) BINARY VALUE 50.              
030400     05  PV-NUMPUTS               PIC S9(9) BINARY VALUE 0.               
030400     05  PV-NUMPUTS-2             PIC S9(9) BINARY VALUE 0.               
030500     05  PV-ERROR-MESSAGE         PIC X(48) VALUE SPACES.                 
030600     05  PV-HCONN                 PIC S9(9) BINARY VALUE 0.               
030700     05  PV-PQ-HANDLE             PIC S9(9) BINARY VALUE 0.               
030800     05  PV-OPEN-OPTIONS          PIC S9(9) BINARY.                       
030900     05  PV-COMPLETION-CODE       PIC S9(9) BINARY.                       
031000     05  PV-REASON                PIC S9(9) BINARY.                       
031100     05  PV-CON-REASON            PIC S9(9) BINARY.                       
031200                                                                          
031300 01  WS-WORKING-STORAGE-VARIABLES.                                        
031400     05  WS-MQCONN                  PIC X(8)  VALUE 'CSQBCONN'.           
031500     05  WS-MQOPEN                  PIC X(8)  VALUE 'CSQBOPEN'.           
031600     05  WS-MQPUT                   PIC X(8)  VALUE 'CSQBPUT'.            
031700     05  WS-MQCLOSE                 PIC X(8)  VALUE 'CSQBCLOS'.           
031800     05  WS-MQDISC                  PIC X(8)  VALUE 'CSQBDISC'.           
031900                                                                          
032000*    API control blocks                                                   
032100                                                                          
032200 01  MQM-OBJECT-DESCRIPTOR.                                               
032300     COPY CMQODV.                                                         
032400 01  MQM-MESSAGE-DESCRIPTOR.                                              
032500     COPY CMQMDV.                                                         
032600 01  MQM-PUT-MESSAGE-OPTIONS.                                             
032700     COPY CMQPMOV.                                                        
032800*                                                                         
032900*    MQV contains constants (for filling in the control blocks)           
033000*    and return codes (for testing the result of a call)                  
033100*                                                                         
033200 01  MQM-CONSTANTS.                                                       
033300     COPY CMQV.                                                           
033400*                                                                         
033500*----------------------------------------------------------------*        
033600*    CONTROL CARD                                                         
033700*----------------------------------------------------------------*        
033800 01  CC-CONTROL-CARD-1.                                                   
033900     05  CC-CONN-QMGR              PIC X(48).                             
034000     05  FILLER                    PIC X(32).                             
034100                                                                          
034200 01  CC-CONTROL-CARD-2.                                                   
034300     05  CC-DEST-QMGR              PIC X(48).                             
034400     05  FILLER                    PIC X(32).                             
034500                                                                          
034600 01  CC-CONTROL-CARD-3.                                                   
034700     05  CC-QUEUE-NAME             PIC X(48).                             
034800     05  FILLER                    PIC X(32).                             
034900                                                                          
035000 01  CC-CONTROL-CARD-4.                                                   
035100     05  CC-PRIORITY               PIC X(01).                             
035200     05  FILLER                    PIC X(79).                             
035300                                                                          
035500*  DATE CONTROL CARD                                                      
035600 01  PV-CONTROL-RECORD.                                                   
035700     05  PV-CONTROL-PROCESS-DATE        PIC   X(10).                      
035800     05  FILLER                         PIC   X(01).                      
035900     05  PV-CONTROL-SUNDAY-DATE         PIC   X(10).                      
036000     05  FILLER                         PIC   X(59).                      
036100                                                                          
036200******************************************************************        
036300* PO ON-ORDER EXTRACT OUTPUT FILE LAYOUT                                  
036400******************************************************************        
036500     COPY PORD018.                                                        
036600                                                                          
036700******************************************************************        
036800*    CALENDAR ROUTINE                                                     
036900******************************************************************        
037000     COPY DPWS500.                                                        
037100                                                                          
037200*****************************************************************         
037300* WORK FIELDS FOR WEEKLY OTB DATE ROUTINE.                                
037400*****************************************************************         
037500     COPY POWS055.                                                        
037600                                                                          
037700*****************************************************************         
037800* DB2 FORMATTED ERROR MESSAGE                                             
037900*****************************************************************         
038000     COPY DPWS004.                                                        
038100                                                                          
038200*---------------------------------------------------------------*         
038300*    DB2 AREA FOR PURCHASE ORDER                                *         
038400*---------------------------------------------------------------*         
038500     EXEC SQL                                                             
038600          INCLUDE TPURCHS                                                 
038700     END-EXEC.                                                            
038800*---------------------------------------------------------------*         
038900*    DB2 AREA FOR PURCHASE ITEM                                 *         
039000*---------------------------------------------------------------*         
039100     EXEC SQL                                                             
039200          INCLUDE TPURITM                                                 
039300     END-EXEC.                                                            
039400*---------------------------------------------------------------*         
039500*    DB2 AREA FOR PURCHASE ORDER PLANNED SHIPMENT TABLE         *         
039600*---------------------------------------------------------------*         
039700     EXEC SQL                                                             
039800          INCLUDE TPLNSHP                                                 
039900     END-EXEC.                                                            
040000*---------------------------------------------------------------*         
040100*    DB2 AREA FOR MERCHANDISE STORE DISTRIBUTION                *         
040200*---------------------------------------------------------------*         
040300     EXEC SQL                                                             
040400          INCLUDE TMESTDS                                                 
040500     END-EXEC.                                                            
040600*---------------------------------------------------------------*         
040700*    DB2 AREA FOR TALLPLN TABLE                                 *         
040800*---------------------------------------------------------------*         
040900     EXEC SQL                                                             
041000          INCLUDE TALLPLN                                                 
041100     END-EXEC.                                                            
041200*---------------------------------------------------------------*         
041300*    DB2 AREA FOR TRECEIV TABLE                                 *         
041400*---------------------------------------------------------------*         
041500     EXEC SQL                                                             
041600          INCLUDE TRECEIV                                                 
041700     END-EXEC.                                                            
041800*---------------------------------------------------------------*         
041900*    DB2 AREA FOR TRECDIS TABLE                                 *         
042000*---------------------------------------------------------------*         
042100     EXEC SQL                                                             
042200          INCLUDE TRECDIS                                                 
042300     END-EXEC.                                                            
042400*---------------------------------------------------------------*         
042500*    DB2 AREA FOR TRECITM TABLE                                 *         
042600*---------------------------------------------------------------*         
042700     EXEC SQL                                                             
042800          INCLUDE TRECITM                                                 
042900     END-EXEC.                                                            
043000*---------------------------------------------------------------*         
043100*    DB2 AREA FOR TSKXREF                                       *         
043200*---------------------------------------------------------------*         
043300     EXEC SQL                                                             
043400          INCLUDE TSKXREF                                                 
043500     END-EXEC.                                                            
043600*---------------------------------------------------------------*         
043700*    DB2 AREA FOR TUPCPLS                                       *         
043800*---------------------------------------------------------------*         
043900     EXEC SQL                                                             
044000          INCLUDE TUPCPLS                                                 
044100     END-EXEC.                                                            
044200*---------------------------------------------------------------*         
044300*    DB2 AREA FOR SKU TABLE                                     *         
044400*---------------------------------------------------------------*         
044500     EXEC SQL                                                             
044600          INCLUDE XLT00003                                                
044700     END-EXEC.                                                            
044800*---------------------------------------------------------------*         
044900*    DB2 AREA FOR VENDOR STYLE                                  *         
045000*---------------------------------------------------------------*         
045100     EXEC SQL                                                             
045200          INCLUDE XLT00004                                                
045300     END-EXEC.                                                            
045400                                                                          
045500*---------------------------------------------------------------*         
045600*    DB2 AREA FOR COMMUNICATIONS                                *         
045700*---------------------------------------------------------------*         
045800     EXEC SQL                                                             
045900          INCLUDE SQLCA                                                   
046000     END-EXEC.                                                            
046100                                                                          
046200*---------------------------------------------------------------*         
046300*    PO ITEM CURSOR                                             *         
046400*---------------------------------------------------------------*         
046500                                                                          
046600     EXEC SQL                                                             
046700         DECLARE PO-CSR  CURSOR FOR                                       
046800         SELECT                                                           
046900                A.PO_NBR,                                                 
047000                A.DEPT_NBR,                                               
047100                A.OTB_PER_NBR,                                            
047200                A.PO_TYP_CDE,                                             
047300                B.DONT_SHIP_BEF_DTE,                                      
047400                C.PO_LNE_NBR,                                             
047500                C.APP_PO_LNE_NBR,                                         
047600                V.MAJ_CL_NBR,                                             
047700                V.SUB_CL_NBR,                                             
047800                S.SKU_NBR,                                                
047900                C.ITM_NBR,                                                
048000                C.UNT_QTY,                                                
048100                C.UNT_RTL_PR_AMT,                                         
048200                C.UNT_COST_AMT,                                           
048300                C.GP_QTY,                                                 
048400                C.GP_RTL_AMT,                                             
048500                C.AD_EVNT_DTE,                                            
048600                E.SKU_STAT_CDE,                                           
048700                S.GP_CDE,                                                 
048800                V.VS_NBR,                                                 
048900                V.ENT_ID                                                  
049000          FROM  TPURCHS A,                                                
049100                TPLNSHP B,                                                
049200                TPURITM C,                                                
049300                XLT_SKU S,                                                
049400                XLT_VND_STYL V,                                           
049500                TSKXREF E                                                 
049600            WHERE A.PO_NBR         = C.PO_NBR                             
049700            AND A.PO_NBR         = B.PO_NBR                               
PK4679            AND A.PO_NBR         = A.PO_NBR                               
049800            AND A.VER_STATUS_CDE = 'A'                                    
049900            AND A.VER_STATUS_CDE = B.VER_STATUS_CDE                       
050000            AND B.VER_STATUS_CDE = C.VER_STATUS_CDE                       
050100            AND C.ITM_NBR        = S.ITM_NBR                              
050200            AND S.STYL_ID        = V.STYL_ID                              
050300            AND C.ITM_NBR        = E.ITM_NBR                              
050400            AND A.STATUS_CDE IN ('AC','AP')                               
050500            AND C.STATUS_CDE IN ('AC','AP','CM')                          
PK4679*         AND V.STYL_ID        = V.STYL_ID                                
050600          ORDER BY                                                        
050700                A.PO_NBR,                                                 
050800                V.MAJ_CL_NBR,                                             
050900                V.SUB_CL_NBR,                                             
051000                S.SKU_NBR                                                 
051100          FOR FETCH ONLY                                                  
051200     END-EXEC.                                                            
051300                                                                          
051400*---------------------------------------------------------------*         
051500*    ALLPLN CURSOR                                              *         
051600*---------------------------------------------------------------*         
051700                                                                          
051800     EXEC SQL                                                             
051900         DECLARE ALLPLN-CSR CURSOR FOR                                    
052000           SELECT DEST_NBR,                                               
052100                  CURR_SPLIT_QTY,                                         
052200                  PLAN_NBR,                                               
052300                  STATUS_CDE                                              
052400           FROM TALLPLN                                                   
052500          WHERE  PO_NBR      = :PURCHS-PO-NBR                             
052600            AND  PO_LNE_NBR  = :PURITM-PO-LNE-NBR                         
052700          ORDER BY DEST_NBR                                               
052800     END-EXEC.                                                            
052900                                                                          
053000*---------------------------------------------------------------*         
053100*    MESTDS CURSOR                                              *         
053200*---------------------------------------------------------------*         
053300                                                                          
053400     EXEC SQL                                                             
053500         DECLARE MESTDS-CSR CURSOR FOR                                    
053600           SELECT STR_DISTRO_QTY,                                         
053700                  STR_NBR                                                 
053800           FROM TMESTDS                                                   
053900           WHERE   PO_NBR      = :PURCHS-PO-NBR                           
054000            AND    PO_LNE_NBR  = :PURITM-PO-LNE-NBR                       
054100            AND    PLAN_NBR    = :MESTDS-PLAN-NBR                         
054200           ORDER BY   STR_NBR                                             
054300     END-EXEC.                                                            
054400                                                                          
054410** TCS-MIPS-RDN-START                                                     
054420*---------------------------------------------------------------*         
054430*    SUM-RECDIS CURSOR                                          *         
054440*---------------------------------------------------------------*         
054450                                                                          
054460     EXEC SQL                                                             
054470         DECLARE SUM-RECDIS-CSR  CURSOR FOR                               
054480           SELECT A.STR_NBR, SUM(A.AP_VERIFY_ITM_QTY)                     
054490           FROM TRECDIS A,                                                
054491                TRECITM B,                                                
054492                TRECEIV C,                                                
                      TMESTDS D                                                 
054493           WHERE A.ORD_NBR = :PURCHS-PO-NBR                               
054494             AND B.ORD_NBR = A.ORD_NBR                                    
054495             AND C.ORD_NBR = B.ORD_NBR                                    
                   AND D.PO_NBR = A.ORD_NBR                                     
054496             AND A.ORD_LNE_NBR = :PURITM-APP-PO-LNE-NBR                   
054497             AND B.ORD_LNE_NBR = A.ORD_LNE_NBR                            
                   AND D.PO_LNE_NBR = A.ORD_LNE_NBR                             
054498*            AND A.OPER_UNT_ID = :PV-DC-NBR                               
                   AND D.PLAN_NBR    = :PV-DC-PLAN-NBR                          
                   AND D.STR_NBR     = A.STR_NBR                                
054499             AND B.OPER_UNT_ID = A.OPER_UNT_ID                            
054500             AND C.OPER_UNT_ID = B.OPER_UNT_ID                            
054501             AND A.REC_SEQ_NBR = B.REC_SEQ_NBR                            
054502             AND B.REC_SEQ_NBR = C.REC_SEQ_NBR                            
054503             AND A.FCLTY_ID    = B.FCLTY_ID                               
054504             AND B.FCLTY_ID    = C.FCLTY_ID                               
054505             AND A.VER_STATUS_CDE = 'A'                                   
054506             AND B.VER_STATUS_CDE = A.VER_STATUS_CDE                      
054507             AND C.VER_STATUS_CDE = B.VER_STATUS_CDE                      
054508             AND B.STATUS_CDE = 'VE'                                      
054509             AND C.STATUS_CDE = 'VE'                                      
054510             AND DATE(C.VERFYD_TMST) <= :PV-CONTROL-PROCESS-DATE          
054511             GROUP BY   A.STR_NBR                                         
054512           WITH UR                                                        
054513     END-EXEC.                                                            
054514** TCS-MIPS-RDN-END                                                       
054520                                                                          
054600******************************************************************        
054700 PROCEDURE DIVISION.                                                      
054800******************************************************************        
054900                                                                          
055000 0000-MAINLINE.                                                           
055100                                                                          
055200     OPEN INPUT  DATE-CONTROL-CARD                                        
055300          OUTPUT PO-ONORD-EXT-FILE                                        
055400                 PO-ONORD-PDUE-FILE.                                      
055500* ------------------------------------------------------------- *         
055600*  READ CONTROL CARD TO GET CONNECTION QUEUE MANAGER NAME       *         
055700* ------------------------------------------------------------- *         
055800      SET PS-CONTROL-CARD-1-START TO TRUE.                                
055900      OPEN INPUT MQ-CONTROL-CARD-1.                                       
056000      READ MQ-CONTROL-CARD-1 INTO CC-CONTROL-CARD-1                       
056100          AT END                                                          
056200              SET PS-CONTROL-CARD-1-END                                   
056300                                  TO TRUE.                                
056400                                                                          
056500      IF PS-CONTROL-CARD-1-END                                            
056600         DISPLAY '** ABEND **'                                            
056700         DISPLAY '** PGM = ' PC-PROGRAM-NAME ' **'                        
056800         DISPLAY '** PARAGRAPH = 0000-MAINLINE'                           
056900         DISPLAY                                                          
057000              'CONTROL CARD MISSING - FOR CONNECTION QMGR NAME'           
057100         MOVE +4008 TO ABEND-CODE                                         
057200         CALL 'ILBOABN0' USING ABEND-CODE                                 
057300      ELSE                                                                
057400          DISPLAY PC-PROGRAM-NAME                                         
057500                  '- CONTROL CARD: '                                      
057600                  CC-CONTROL-CARD-1                                       
057700      END-IF.                                                             
057800                                                                          
057900      CLOSE MQ-CONTROL-CARD-1.                                            
058000                                                                          
058100* ------------------------------------------------------------- *         
058200*  READ CONTROL CARD TO GET DESTINATION QUEUE MANAGER NAME      *         
058300* ------------------------------------------------------------- *         
058400      SET PS-CONTROL-CARD-2-START TO TRUE.                                
058500      OPEN INPUT MQ-CONTROL-CARD-2.                                       
058600      READ MQ-CONTROL-CARD-2 INTO CC-CONTROL-CARD-2                       
058700          AT END                                                          
058800              SET PS-CONTROL-CARD-2-END                                   
058900                                  TO TRUE.                                
059000                                                                          
059100      IF PS-CONTROL-CARD-2-END                                            
059200         DISPLAY '** ABEND **'                                            
059300         DISPLAY '** PGM = ' PC-PROGRAM-NAME ' **'                        
059400         DISPLAY '** PARAGRAPH = 0000-MAINLINE'                           
059500         DISPLAY                                                          
059600              'CONTROL CARD MISSING - FOR DESTINATION QMGR NAME'          
059700         MOVE +4008 TO ABEND-CODE                                         
059800         CALL 'ILBOABN0' USING ABEND-CODE                                 
059900      ELSE                                                                
060000          DISPLAY PC-PROGRAM-NAME                                         
060100                  '- CONTROL CARD: '                                      
060200                  CC-CONTROL-CARD-2                                       
060300      END-IF.                                                             
060400                                                                          
060500      CLOSE MQ-CONTROL-CARD-2.                                            
060600                                                                          
060700* ------------------------------------------------------------- *         
060800*  READ CONTROL CARD TO GET QUEUE NAME                          *         
060900* ------------------------------------------------------------- *         
061000                                                                          
061100      SET PS-CONTROL-CARD-3-START TO TRUE.                                
061200      OPEN INPUT MQ-CONTROL-CARD-3.                                       
061300      READ MQ-CONTROL-CARD-3 INTO CC-CONTROL-CARD-3                       
061400          AT END                                                          
061500              SET PS-CONTROL-CARD-3-END                                   
061600                                  TO TRUE.                                
061700                                                                          
061800      IF PS-CONTROL-CARD-3-END                                            
061900         DISPLAY '** ABEND **'                                            
062000         DISPLAY '** PGM = ' PC-PROGRAM-NAME ' **'                        
062100         DISPLAY '** PARAGRAPH = 0000-MAINLINE'                           
062200         DISPLAY                                                          
062300             'CONTROL CARD MISSING - FOR QUEUE NAME'                      
062400         MOVE +4008 TO ABEND-CODE                                         
062500         CALL 'ILBOABN0' USING ABEND-CODE                                 
062600      ELSE                                                                
062700          DISPLAY PC-PROGRAM-NAME                                         
062800                  '- CONTROL CARD: '                                      
062900                  CC-CONTROL-CARD-3                                       
063000      END-IF.                                                             
063100                                                                          
063200      CLOSE MQ-CONTROL-CARD-3.                                            
063300                                                                          
063400* ------------------------------------------------------------- *         
063500*  READ CONTROL CARD TO GET PRIORITY                            *         
063600* ------------------------------------------------------------- *         
063700                                                                          
063800      SET PS-CONTROL-CARD-4-START TO TRUE.                                
063900      OPEN INPUT MQ-CONTROL-CARD-4.                                       
064000      READ MQ-CONTROL-CARD-4 INTO CC-CONTROL-CARD-4                       
064100          AT END                                                          
064200              SET PS-CONTROL-CARD-4-END                                   
064300                                  TO TRUE.                                
064400                                                                          
064500     IF PS-CONTROL-CARD-4-END                                             
064600         DISPLAY '** ABEND **'                                            
064700         DISPLAY '** PGM = ' PC-PROGRAM-NAME ' **'                        
064800         DISPLAY '** PARAGRAPH = 0000-MAINLINE'                           
064900         DISPLAY                                                          
065000            'CONTROL CARD MISSING - FOR PRIORITY'                         
065100         MOVE +4008 TO ABEND-CODE                                         
065200         CALL 'ILBOABN0' USING ABEND-CODE                                 
065300     ELSE                                                                 
065400         DISPLAY PC-PROGRAM-NAME                                          
065500                 '- CONTROL CARD: '                                       
065600                 CC-CONTROL-CARD-4                                        
065700     END-IF.                                                              
065800                                                                          
065900     CLOSE MQ-CONTROL-CARD-4.                                             
063300                                                                          
066000                                                                          
066100     PERFORM 1000-INITIALIZE.                                             
066200                                                                          
066300     ACCEPT PV-SYSTEM-DATE FROM DATE.                                     
066400     MOVE PV-SYSTEM-YY        TO PV-SYSTEM-FMT-YY.                        
066500     MOVE PV-SYSTEM-MM        TO PV-SYSTEM-FMT-MM.                        
066600     MOVE PV-SYSTEM-DD        TO PV-SYSTEM-FMT-DD.                        
066700                                                                          
066800     IF PV-SYSTEM-YY > '80'                                               
066900        MOVE '19' TO PV-SYSTEM-FMT-CC                                     
067000     ELSE                                                                 
067100        MOVE '20' TO PV-SYSTEM-FMT-CC                                     
067200     END-IF.                                                              
067300                                                                          
067400     DISPLAY '** SYSTEM DATE: ' PV-SYSTEM-DATE-ISO-FMT.                   
067500                                                                          
067600     PERFORM 1100-READ-DATE-CONTROL-CARD.                                 
067700     PERFORM 1200-VALIDATE-PROCESS-DATE.                                  
067800     PERFORM 1300-VALIDATE-SUNDAY-DATE.                                   
067900                                                                          
068000     PERFORM 7000-OPEN-PO-CURSOR.                                         
068100                                                                          
068200     PERFORM 6000-CONNECT-QUEUE-MANAGER.                                  
068300                                                                          
068400     PERFORM 6100-OPEN-QUEUE.                                             
068500                                                                          
068600     PERFORM 6200-MQPUT-MSG.                                              
068700                                                                          
068800     PERFORM 6300-CLOSE-QUEUE.                                            
068900                                                                          
068900                                                                          
069000     PERFORM 6400-DISCONNECT-QMGR.                                        
069100                                                                          
069200     PERFORM 7010-FETCH-PO-CURSOR.                                        
069300     PERFORM 2000-PROCESS-ON-ORDER                                        
069400        UNTIL PS-NO-ITEMS-LEFT-ON-DB.                                     
069500     PERFORM 7020-CLOSE-PO-CURSOR.                                        
069600                                                                          
069700     PERFORM 9000-END-OF-JOB.                                             
069800                                                                          
069900     CLOSE DATE-CONTROL-CARD                                              
070000           PO-ONORD-EXT-FILE                                              
070100           PO-ONORD-PDUE-FILE.                                            
070200                                                                          
070300     GOBACK.                                                              
070400                                                                          
070500                                                                          
070600******************************************************************        
070700* INITIALIZE WORKING STORAGE AREAS AND THE OUTPUT FILE RECORD.            
070800******************************************************************        
070900 1000-INITIALIZE.                                                         
071000                                                                          
071100     MOVE ZERO               TO PV-PO-NBR                                 
071200                                PV-ITM-NBR                                
071300                                PV-ENT-ID                                 
071400                                PV-STR-QTY                                
071500                                PV-DC-QTY                                 
071600                                PV-UNT-COST-AMT                           
071700                                PV-UNT-RTL-PR-AMT                         
071800                                PV-STR-NBR-9                              
071900                                PV-DC-NBR                                 
072000                                PV-DC-PLAN-NBR                            
072100                                PV-DC-ALLPLN-QTY                          
072200                                PV-DC-MESTDS-QTY.                         
072300                                                                          
072400     MOVE SPACES             TO PV-DEPT-NBR                               
072500                                PV-GROUP-CODE                             
072600                                PV-PO-TYP-CDE                             
072700                                PV-LONG-VENDOR-STYLE                      
072800                                PV-DC-PLAN-STATUS.                        
072900     MOVE ZEROS              TO PV-MAJ-CL-NBR                             
073000                                PV-SUB-CL-NBR                             
073100                                PV-SKU-NBR.                               
073200                                                                          
073300                                                                          
073400     INITIALIZE PV-PO-ENTRY.                                              
073500                                                                          
073600     INITIALIZE PO018-ONORD-EXTRACT-RECORD.                               
073700                                                                          
073800                                                                          
073900******************************************************************        
074000*  READ THE DATE CONTROL CARD.                                   *        
074100******************************************************************        
074200 1100-READ-DATE-CONTROL-CARD.                                             
074300                                                                          
074400     READ DATE-CONTROL-CARD                                               
074500         INTO PV-CONTROL-RECORD                                           
074600         AT END                                                           
074700             SET PS-END-OF-DATE-CONTROL-CARD TO TRUE                      
074800     END-READ.                                                            
074900                                                                          
075000     IF PS-END-OF-DATE-CONTROL-CARD                                       
075100         DISPLAY '** ABEND **'                                            
075200         DISPLAY '** PGM = ' PC-PROGRAM-NAME ' **'                        
075300         DISPLAY '** PARAGRAPH = 1100-READ-DATE-CONTROL-CARD'             
075400         DISPLAY '** EMPTY DATE CONTROL CARD **'                          
075500         MOVE +4008 TO ABEND-CODE                                         
075600         CALL 'ILBOABN0' USING ABEND-CODE                                 
075700     END-IF.                                                              
075800                                                                          
075900                                                                          
076000******************************************************************        
076100* CALL THE CALENDAR ROUTINE USING THE CONTROL CARD PROCESS DATE           
076200* TO VALIDATE IT IS A VALID DATE.                                         
076300******************************************************************        
076400 1200-VALIDATE-PROCESS-DATE.                                              
076500                                                                          
076600     INITIALIZE DPG51                                                     
076700                DPG52                                                     
076800                DPG53                                                     
076900                DPG54                                                     
077000                DPG55                                                     
077100                DPG56.                                                    
077200                                                                          
077300     SET DPG51-FISCAL-454-CALENDAR                                        
077400         DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
077500                                                                          
077600     MOVE PV-SYSTEM-DATE           TO DPG51-SYSTEM-DATE.                  
077700     MOVE PV-CONTROL-PROCESS-DATE  TO DPG52-LK-DATE-INPUT.                
077800                                                                          
077900     CALL DP500-KOHLS-CALENDAR-ROUTINE  USING DPG51                       
078000                                              DPG52                       
078100                                              DPG53                       
078200                                              DPG54                       
078300                                              DPG55                       
078400                                              DPG56.                      
078500                                                                          
078600                                                                          
078700     IF DPG54-NO-ERROR                                                    
078800         DISPLAY '** PROCESSING DATE: ' PV-CONTROL-PROCESS-DATE           
078900     ELSE                                                                 
079000         DISPLAY '** ABEND **'                                            
079100         DISPLAY '** PGM = ' PC-PROGRAM-NAME ' **'                        
079200         DISPLAY '** PARAGRAPH = 1200-VALIDATE-PROCESS-DATE'              
079300         DISPLAY '** BAD CALL TO CALENDAR ROUTINE USING '                 
079400                     PV-CONTROL-PROCESS-DATE                              
079500         DISPLAY DPG54-ERROR-MESSAGE                                      
079600         MOVE +4019 TO ABEND-CODE                                         
079700         CALL 'ILBOABN0' USING ABEND-CODE                                 
079800     END-IF.                                                              
079900                                                                          
080000                                                                          
080100******************************************************************        
080200* CALL THE CALENDAR ROUTINE USING THE CONTROL CARD DATE IN ORDER          
080300* TO VALIDATE IT IS A SUNDAY DATE.                                        
080400******************************************************************        
080500 1300-VALIDATE-SUNDAY-DATE.                                               
080600                                                                          
080700     INITIALIZE DPG51                                                     
080800                DPG52                                                     
080900                DPG53                                                     
081000                DPG54                                                     
081100                DPG55                                                     
081200                DPG56.                                                    
081300                                                                          
081400     SET DPG51-FISCAL-454-CALENDAR                                        
081500         DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
081600                                                                          
081700     MOVE PV-SYSTEM-DATE           TO DPG51-SYSTEM-DATE.                  
081800     MOVE PV-CONTROL-SUNDAY-DATE   TO DPG52-LK-DATE-INPUT.                
081900                                                                          
082000     CALL DP500-KOHLS-CALENDAR-ROUTINE  USING DPG51                       
082100                                              DPG52                       
082200                                              DPG53                       
082300                                              DPG54                       
082400                                              DPG55                       
082500                                              DPG56.                      
082600                                                                          
082700                                                                          
082800     IF DPG54-NO-ERROR                                                    
082900         IF DPG55-NUMERIC-WEEK-DAY-SUN                                    
083000            DISPLAY '** CURR SUNDAY DATE: ' PV-CONTROL-SUNDAY-DATE        
083100         ELSE                                                             
083200            DISPLAY '** ABEND **'                                         
083300            DISPLAY '** PGM = ' PC-PROGRAM-NAME ' **'                     
083400            DISPLAY '** PARAGRAPH = 1300-VALIDATE-SUNDAY-DATE'            
083500            DISPLAY '** CONTROL CARD DATE NOT A SUNDAY DATE: '            
083600                        PV-CONTROL-SUNDAY-DATE                            
083700            MOVE +4003 TO ABEND-CODE                                      
083800            CALL 'ILBOABN0' USING ABEND-CODE                              
083900         END-IF                                                           
084000     ELSE                                                                 
084100         DISPLAY '** ABEND **'                                            
084200         DISPLAY '** PGM = ' PC-PROGRAM-NAME ' **'                        
084300         DISPLAY '** PARAGRAPH = 1300-VALIDATE-SUNDAY-DATE'               
084400         DISPLAY '** BAD CALL TO CALENDAR ROUTINE USING '                 
084500                     PV-CONTROL-SUNDAY-DATE                               
084600         DISPLAY DPG54-ERROR-MESSAGE                                      
084700         MOVE +4019 TO ABEND-CODE                                         
084800         CALL 'ILBOABN0' USING ABEND-CODE                                 
084900     END-IF.                                                              
085000                                                                          
085100                                                                          
085200******************************************************************        
085300* PROCESS THE PO CURSOR ROW.                                              
085400*   - MOVE FETCHED FIELDS TO PV FIELDS.                                   
085500*   - OPEN THE TALLPLN CURSOR.                                            
085600*   - FETCH THE FIRST TALLPLN ROW.                                        
085700*   - IF A TALLPLN WAS NOT FOUND (NO DC SPLIT), ASSIGN THE PO ITM         
085800*        QTY TO CORP STORE 899.                                           
085900*   - IF A TALLPLN WAS FOUND, PROCESS THRU EACH TALLPLN ROW.              
086000*        IF THE SPLIT QTY = 0, JUST FETCH THE NEXT TALLPLN ROW.           
086100*        IF THE SPLIT QTY <> 0, PROCESS THE DC/STORE, THEN FETCH          
086200*           THE NEXT TALLPLAN ROW.                                        
086300*   - IF NO TALLPLN ROW WAS FOUND WITH A SPLIT QTY <> 0, ASSIGN           
086400*        THE PO ITM QTY TO CORP STORE 899.                                
086500*   - CLOSE THE TALLPLN CURSOR.                                           
086600*   - FETCH THE NEXT PO CURSOR ROW.                                       
086700*   - INITIALIZE WORKING STORAGE AND OUTPUT FILE RECORD.                  
086800******************************************************************        
086900 2000-PROCESS-ON-ORDER.                                                   
087000                                                                          
087100     MOVE '2000-PROCESS-ON-ORDER'                                         
087200                                  TO PV-PARAGRAPH-NAME.                   
087300* TO CHECK IF THERE IS NULL PLAN SHIP BEFORE DATE FOR FETCHED PO *        
           IF PLNSHP-DONT-SHIP-BEF-DTE = '0001-01-01'                           
              COMPUTE PV-COUNT = PV-COUNT + 1                                   
              MOVE PURCHS-PO-NBR  TO PV-CUR-PO-NBR                              
              IF PV-CUR-PO-NBR NOT EQUAL  PV-PREV-PO-NBR                        
                DISPLAY 'INVALID PO = ' PURCHS-PO-NBR                           
              END-IF                                                            
              MOVE PV-CUR-PO-NBR TO PV-PREV-PO-NBR                              
095900        PERFORM 7010-FETCH-PO-CURSOR                                      
096100        PERFORM 1000-INITIALIZE                                           
           ELSE                                                                 
              PERFORM 2001-PROCESS-ON-ORDER                                     
089300     END-IF.                                                              
086800******************************************************************        
086900 2001-PROCESS-ON-ORDER.                                                   
           MOVE '2001-PROCESS-ON-ORDER'                                         
                                        TO PV-PARAGRAPH-NAME.                   
087300* SAVE THE PO-CSR FIELDS IN PV-PO-ENTRY *                                 
087400     MOVE PURCHS-PO-NBR      TO PV-PO-NBR.                                
087500     MOVE PURCHS-DEPT-NBR    TO PV-DEPT-NBR.                              
087600     MOVE PURCHS-PO-TYP-CDE  TO PV-PO-TYP-CDE.                            
087700     MOVE XLT00004-MAJ-CL-NBR TO PV-MAJ-CL-NBR.                           
087800     MOVE XLT00004-SUB-CL-NBR TO PV-SUB-CL-NBR.                           
087900     MOVE XLT00003-SKU-NBR   TO PV-SKU-NBR.                               
088000     MOVE PURITM-ITM-NBR     TO PV-ITM-NBR.                               
088100     MOVE SKXREF-SKU-STAT-CDE TO PV-STATUS-CODE.                          
088200     MOVE XLT00003-GP-CDE    TO PV-GROUP-CODE.                            
088300     MOVE XLT00004-VS-NBR    TO PV-LONG-VENDOR-STYLE.                     
088400     MOVE XLT00004-ENT-ID    TO PV-ENT-ID.                                
088500                                                                          
088500                                                                          
088600     MOVE PURITM-UNT-COST-AMT TO PV-UNT-COST-AMT.                         
088700                                                                          
088800     IF (PURITM-GP-QTY > 1) AND (PURITM-GP-RTL-AMT > 0)                   
088900         COMPUTE PV-UNT-RTL-PR-AMT =                                      
089000                 PURITM-GP-RTL-AMT / PURITM-GP-QTY                        
089100     ELSE                                                                 
089200         MOVE PURITM-UNT-RTL-PR-AMT TO PV-UNT-RTL-PR-AMT                  
089300     END-IF.                                                              
089400                                                                          
089500     SET  PS-DC-LEFT         TO TRUE.                                     
089600                                                                          
089700*-------- DETERMINE IF THERE IS A DC SPLIT -------------------*           
089800                                                                          
089900     PERFORM 7100-OPEN-ALLPLN-CURSOR.                                     
090000     PERFORM 7110-FETCH-ALLPLN-CURSOR.                                    
090100                                                                          
090200     IF SQLCODE = +100                                                    
090300                                                                          
090400*-----------------   NO DC SPLIT  ------------------*                     
090500                                                                          
090600         MOVE PC-899         TO PV-STR-NBR-9                              
090700         MOVE PURITM-UNT-QTY TO PV-STR-QTY                                
090800         PERFORM 8000-FORMAT-STORE-INFO                                   
090900         PERFORM 8500-WRITE-EXTRACT                                       
091000     ELSE                                                                 
091100*-----------------   DC SPLIT EXISTS ------------------*                  
091200         SET PS-NO-TALLPLN-EXISTS TO TRUE                                 
091300                                                                          
091400         MOVE 0 TO PV-DC-QTY                                              
091500         PERFORM 7250-SUM-RECDIS-LINE                                     
091600         IF RECDIS-AP-VERIFY-ITM-QTY = 0                                  
091700           PERFORM 7130-SUM-SPLITS                                        
091800           IF PV-CURR-SPLIT-QTY < PURITM-UNT-QTY                          
091900            IF SQLCODE = 0                                                
092000                SET PS-TALLPLN-EXISTS TO TRUE                             
092100            END-IF                                                        
092200* ---- SUM OF THE DC SPLIT DOESNT = LINE QTY AND NO RCPTS                 
092300* ---- SO ASSIGN MISSING SPLIT QTY TO STR 899 (CORP)                      
092400*           DISPLAY 'SPLIT DOESNT MATCH LINE '                            
092500*           PV-CURR-SPLIT-QTY ' VS ' PURITM-UNT-QTY                       
092600*           'PO ' PURCHS-PO-NBR ' LNE ' PURITM-APP-PO-LNE-NBR             
092700              COMPUTE PV-DC-QTY = PURITM-UNT-QTY -                        
092800                                                PV-CURR-SPLIT-QTY         
092900              MOVE PC-899          TO PV-STR-NBR-9                        
093000              MOVE PV-DC-QTY       TO PV-STR-QTY                          
093100              PERFORM 8000-FORMAT-STORE-INFO                              
093200              PERFORM 8500-WRITE-EXTRACT                                  
093300           END-IF                                                         
093400         END-IF                                                           
093500                                                                          
093600         PERFORM                                                          
093700           UNTIL PS-NO-DC-LEFT                                            
093800           IF ALLPLN-CURR-SPLIT-QTY = ZERO                                
093900               PERFORM 7110-FETCH-ALLPLN-CURSOR                           
094000           ELSE                                                           
094100               SET PS-TALLPLN-EXISTS TO TRUE                              
094200               PERFORM 2100-PROCESS-DC                                    
094300               PERFORM 7110-FETCH-ALLPLN-CURSOR                           
094400           END-IF                                                         
094500         END-PERFORM                                                      
094600                                                                          
094700         IF PS-TALLPLN-EXISTS                                             
094800             CONTINUE                                                     
094900         ELSE                                                             
095000             MOVE PC-899         TO PV-STR-NBR-9                          
095100             MOVE PURITM-UNT-QTY TO PV-STR-QTY                            
095200             PERFORM 8000-FORMAT-STORE-INFO                               
095300             PERFORM 8500-WRITE-EXTRACT                                   
095400         END-IF                                                           
095500     END-IF.                                                              
095600                                                                          
095700     PERFORM 7120-CLOSE-ALLPLN-CURSOR.                                    
095800                                                                          
095900     PERFORM 7010-FETCH-PO-CURSOR.                                        
096000                                                                          
096100     PERFORM 1000-INITIALIZE.                                             
096200                                                                          
096300                                                                          
096400******************************************************************        
096500* PROCESS THE DC/STORE ON THE TALLPLN ROW.                                
096600*   - MOVE TALLPLN FETCHED FIELDS TO PV FIELDS.                           
096700*   - SUM THE RECEIPTS FOR THE CURRENT PO-LINE/DC.                        
096800*   - IF THE RECEIPT QTY >= TALLPLN QTY, THE ORDER IS CONSIDERED          
096900*       COMPLETE, CONTINUE WITH THE NEXT TALLPLN ROW.                     
097000*   - OTHERWISE:                                                          
097100*       SUM THE STORE DISTRIBUTIONS FOR THE CURRENT PO-LINE.              
097200*       IF NO STORE DISTRIBUTIONS, ASSIGN THE ON-ORDER TO THE DC.         
097300*          (ON ORD = TALLPLN QTY - RECEIPTS).                             
097400*       IF STORE DISTRO QTY = TALLPLN QTY, PROCESS THE "MASTER"           
097500*          PLAN.  "MASTER" PLANS LOOK AT STORE DISTRO & STORE             
097600*          RECEIPTS.                                                      
097700*       IF STORE DISTRO QTY <> TALLPLN QTY, PROCESS THE "SPECIFIC"        
097800*          PLAN.  "SPECIFIC" PLANS DO NOT LOOK AT STORE RECEIPTS.         
097900*          IF THE TALLPLAN STATUS WAS ACTIVE (AC), RETRIEVE THE           
098000*            SPECIFIC STORE DISTRIBUTIONS AND ASSIGN ON-ORDER TO          
098100*            STORES.  THEN ASSIGN THE REMAINDER TO THE DC.                
098200*          IF THE TALLPLAN STATUS WAS INACTIVE (IN), ASSIGN               
098300*            ON-ORDER TO THE DC.                                          
098400******************************************************************        
098500 2100-PROCESS-DC.                                                         
098600                                                                          
098700* SAVE ALLPLN-CSR FIELDS *                                                
098800     MOVE ALLPLN-DEST-NBR    TO PV-DC-NBR.                                
098900     MOVE ALLPLN-PLAN-NBR    TO PV-DC-PLAN-NBR.                           
099000     MOVE ALLPLN-STATUS-CDE  TO PV-DC-PLAN-STATUS.                        
099100     MOVE ALLPLN-CURR-SPLIT-QTY TO PV-DC-ALLPLN-QTY.                      
099200     MOVE ZERO               TO PV-DC-QTY.                                
099300                                                                          
099310** TCS-MIPS-RDN-START                                                     
099400*    PERFORM 7200-SUM-RECDIS-DC.                                          
099410     SET PS-SUM-RECDIS-CSR-START TO TRUE                                  
099420     MOVE ZEROES TO PV-DC-TOTAL-QTY                                       
099430                    PV-LAST-STR-INDX                                      
099440                    PV-STR-INDX.                                          
099450     INITIALIZE PV-STR-NBR-ARRY-P.                                        
099460                                                                          
099470     PERFORM 7210-OPEN-SUM-RECDIS-CSR.                                    
099480     PERFORM 7220-FETCH-SUM-RECDIS-CSR VARYING PV-STR-INDX                
099490                 FROM 1 BY 1 UNTIL PS-SUM-RECDIS-CSR-END                  
099491     PERFORM 7230-CLOSE-SUM-RECDIS-CSR                                    
099494** TCS-MIPS-RDN-END                                                       
099600     IF RECDIS-AP-VERIFY-ITM-QTY >= PV-DC-ALLPLN-QTY                      
099700*------- DC ORDER IS CONSIDERED COMPLETE, CONTINUE WITH NEXT DC           
099800         CONTINUE                                                         
099900     ELSE                                                                 
100000         MOVE PV-DC-PLAN-NBR    TO MESTDS-PLAN-NBR                        
100100         PERFORM 7300-SUM-MESTDS-DC                                       
100200         MOVE MESTDS-STR-DISTRO-QTY TO PV-DC-MESTDS-QTY                   
100300         IF PV-DC-MESTDS-QTY = ZERO                                       
100400*----------- NO STORE DISTRO QTY, ASSIGN ON-ORD TO THE DC  ----*          
100500             COMPUTE PV-DC-QTY = PV-DC-ALLPLN-QTY -                       
100600                                     RECDIS-AP-VERIFY-ITM-QTY             
100700             IF PV-DC-QTY > ZERO                                          
100800                 MOVE PV-DC-NBR TO PV-STR-NBR-9                           
100900                 MOVE PV-DC-QTY TO PV-STR-QTY                             
101000                 PERFORM 8000-FORMAT-STORE-INFO                           
101100                 PERFORM 8500-WRITE-EXTRACT                               
101200             END-IF                                                       
101300         ELSE                                                             
101400             IF  PV-DC-MESTDS-QTY = PV-DC-ALLPLN-QTY                      
101500*--------------- MASTER PLAN: --------------------------------*           
101600*--------------- ASSIGN ON-ORD TO STORES ---------------------*           
101700                 PERFORM 2110-GET-MASTER-STR-ALLOC                        
101800             ELSE                                                         
101900*--------------- SPECIFIC PLAN: ------------------------------*           
102000                 IF PV-DC-PLAN-STATUS = 'AC'                              
102100*RASTEMP            DISPLAY 'SPECIFIC ACTIVE PLAN -DC ' PV-DC-NBR         
102200*RASTEMP           ' PO # ' PURCHS-PO-NBR ' LNE '                         
102300*RASTEMP                                    PURITM-APP-PO-LNE-NBR         
102400*------------------- ACTIVE SPECIFIC PLAN: -------------------*           
102500*------------------- ASSIGN ON-ORD TO STORES, DIFF TO DC -----*           
102600                     PERFORM 2120-GET-SPECIFIC-STR-ALLOC                  
102700                     COMPUTE PV-DC-QTY = PV-DC-ALLPLN-QTY -               
102800                    (PV-DC-MESTDS-QTY + RECDIS-AP-VERIFY-ITM-QTY)         
102900                     IF PV-DC-QTY > ZERO                                  
103000                         MOVE PV-DC-NBR TO PV-STR-NBR-9                   
103100                         MOVE PV-DC-QTY TO PV-STR-QTY                     
103200                         PERFORM 8000-FORMAT-STORE-INFO                   
103300                         PERFORM 8500-WRITE-EXTRACT                       
103400                     END-IF                                               
103500                 ELSE                                                     
103600*RASTEMP      DISPLAY 'SPECIFIC INACTIVE PLAN -DC ' PV-DC-NBR             
103700*RASTEMP           ' PO # ' PURCHS-PO-NBR ' LNE '                         
103800*RASTEMP                                    PURITM-APP-PO-LNE-NBR         
103900*------------------- IN-ACTIVE SPECIFIC PLAN: ----------------*           
104000*------------------- ASSIGN ON-ORD TO DC ---------------------*           
104100                     COMPUTE PV-DC-QTY = PV-DC-ALLPLN-QTY -               
104200                                     RECDIS-AP-VERIFY-ITM-QTY             
104300                     IF PV-DC-QTY > ZERO                                  
104400                         MOVE PV-DC-NBR TO PV-STR-NBR-9                   
104500                         MOVE PV-DC-QTY TO PV-STR-QTY                     
104600                         PERFORM 8000-FORMAT-STORE-INFO                   
104700                         PERFORM 8500-WRITE-EXTRACT                       
104800                     END-IF                                               
104900                 END-IF                                                   
105000             END-IF                                                       
105100         END-IF                                                           
105200     END-IF.                                                              
105300                                                                          
105400                                                                          
105500                                                                          
105600******************************************************************        
105700* OPEN THE STORE DISTRIBUTION (TMESTDS) CURSOR.                           
105800* PROCESS THRU EACH STORE DISTRIBUTION:                                   
105900*   FOR EACH STORE DISTRIBUTION FOUND:                                    
106000*     - RETRIEVE ANY RECEIPTS FOR THE STORE/PO-LINE.                      
106100*     - COMPUTE ON-ORDER QTY FOR THE STORE = STORE DISTRO - RCPTS.        
106200*     - FORMAT THE OUTPUT RECORD.                                         
106300*     - WRITE  THE OUTPUT RECORD.                                         
106400* CLOSE THE STORE DISTRO CURSOR AFTER ALL STORES ARE PROCESSED.           
106500******************************************************************        
106600 2110-GET-MASTER-STR-ALLOC.                                               
106700                                                                          
106800     PERFORM 7600-OPEN-MESTDS-CURSOR.                                     
106900     SET PS-ITEMS-LEFT-ON-TMESTDS  TO TRUE.                               
107000                                                                          
107100     PERFORM                                                              
107200        UNTIL PS-NO-ITEMS-LEFT-ON-TMESTDS                                 
107300           PERFORM 7610-FETCH-MESTDS                                      
107400           IF PS-ITEMS-LEFT-ON-TMESTDS                                    
107500              PERFORM  7700-GET-RECDIS                                    
107600              COMPUTE PV-STR-QTY =                                        
107700                      PV-STR-QTY - RECDIS-AP-VERIFY-ITM-QTY               
107800              IF  PV-STR-QTY > ZERO                                       
107900                 PERFORM 8000-FORMAT-STORE-INFO                           
108000                 PERFORM 8500-WRITE-EXTRACT                               
108100              END-IF                                                      
108200           END-IF                                                         
108300     END-PERFORM.                                                         
108400                                                                          
108500     PERFORM 7620-CLOSE-MESTDS-CURSOR.                                    
108600                                                                          
108700                                                                          
108800******************************************************************        
108900* OPEN THE STORE DISTRIBUTION (TMESTDS) CURSOR.                           
109000* PROCESS THRU EACH STORE DISTRIBUTION:                                   
109100*   FOR EACH STORE DISTRIBUTION FETCHED:                                  
109200*     - ON-ORDER QTY FOR THE STORE = STORE DISTRO QTY.                    
109300*     - FORMAT THE OUTPUT RECORD.                                         
109400*     - WRITE  THE OUTPUT RECORD.                                         
109500* CLOSE THE STORE DISTRO CURSOR AFTER ALL STORES ARE PROCESSED.           
109600******************************************************************        
109700 2120-GET-SPECIFIC-STR-ALLOC.                                             
109800                                                                          
109900     PERFORM 7600-OPEN-MESTDS-CURSOR.                                     
110000     SET PS-ITEMS-LEFT-ON-TMESTDS  TO TRUE.                               
110100                                                                          
110200     PERFORM                                                              
110300         UNTIL PS-NO-ITEMS-LEFT-ON-TMESTDS                                
110400            PERFORM 7610-FETCH-MESTDS                                     
110500            IF PS-ITEMS-LEFT-ON-TMESTDS                                   
110600                PERFORM 8000-FORMAT-STORE-INFO                            
110700                PERFORM 8500-WRITE-EXTRACT                                
110800            END-IF                                                        
110900     END-PERFORM.                                                         
111000                                                                          
111100     PERFORM 7620-CLOSE-MESTDS-CURSOR.                                    
111200                                                                          
111300                                                                          
111400* ------------------------------------------------------------- *         
111500*    Connect to the queue manager                                         
111600* ------------------------------------------------------------- *         
111700                                                                          
111800 6000-CONNECT-QUEUE-MANAGER.                                              
111900                                                                          
112000     MOVE CC-CONN-QMGR TO PV-QM-NAME.                                     
112100     CALL WS-MQCONN USING PV-QM-NAME                                      
112200                          PV-HCONN                                        
112300                          PV-COMPLETION-CODE                              
112400                          PV-REASON.                                      
112500                                                                          
112600*    If connection failed then display error message                      
112700*    and exit                                                             
112800                                                                          
112900     MOVE PV-REASON TO PV-CON-REASON.                                     
113000     IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
113100        MOVE 'MQCONN ERROR'               TO PV-ERROR-MESSAGE             
113200        MOVE '1000-CONNECT-QUEUE-MANAGER' TO PV-PARAGRAPH-NAME            
113300        PERFORM 6500-DISPLAY-ERROR-MESSAGE                                
113400     END-IF.                                                              
113500     DISPLAY 'MQCONN SUCCESSFUL TO ', PV-QM-NAME.                         
113600                                                                          
113700* ------------------------------------------------------------- *         
113800*    Open the queue for output                                            
113900* ------------------------------------------------------------- *         
114000                                                                          
114100 6100-OPEN-QUEUE.                                                         
114200                                                                          
114300     COMPUTE PV-OPEN-OPTIONS = MQOO-OUTPUT +                              
114400                            MQOO-FAIL-IF-QUIESCING +                      
114500                            MQOO-SET-IDENTITY-CONTEXT.                    
114600     MOVE CC-QUEUE-NAME     TO MQOD-OBJECTNAME.                           
114700     MOVE CC-DEST-QMGR      TO MQOD-OBJECTQMGRNAME.                       
114800*                                                                         
114900     CALL WS-MQOPEN USING PV-HCONN                                        
115000                         MQM-OBJECT-DESCRIPTOR                            
115100                         PV-OPEN-OPTIONS                                  
115200                         PV-PQ-HANDLE                                     
115300                         PV-COMPLETION-CODE                               
115400                         PV-REASON.                                       
115500*                                                                         
115600*    If open failed then display error message                            
115700*    and exit.                                                            
115800*                                                                         
115900     IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
116000        MOVE 'MQOPEN ERROR'    TO PV-ERROR-MESSAGE                        
116100        MOVE '1000-OPEN-QUEUE' TO PV-PARAGRAPH-NAME                       
116200        PERFORM 6500-DISPLAY-ERROR-MESSAGE                                
116300        PERFORM 6400-DISCONNECT-QMGR                                      
116400     END-IF.                                                              
116500     DISPLAY 'MQOPEN SUCCESSFUL'.                                         
116600                                                                          
116700* ------------------------------------------------------------- *         
116800* PUT MESSAGE ON LOCAL QUEUE TO END THE DATA COLLECT PROCESS              
116900* ------------------------------------------------------------- *         
117000 6200-MQPUT-MSG.                                                          
117100*                                                                         
117200*    Set persistence                                                      
117300*                                                                         
117400     MOVE MQMI-NONE         TO MQMD-MSGID.                                
117500     MOVE MQCI-NONE         TO MQMD-CORRELID.                             
117600*    MOVE PC-CORRELID-ASCII TO MQMD-CORRELID.                             
117700     MOVE PC-MSG-TEXT       TO PV-MSGBUFFER.                              
117800     MOVE MQPER-PERSISTENT  TO MQMD-PERSISTENCE.                          
117900     MOVE CC-PRIORITY       TO MQMD-PRIORITY.                             
118000     MOVE MQFMT-STRING      TO MQMD-FORMAT.                               
118100     COMPUTE MQPMO-OPTIONS  = MQPMO-SYNCPOINT +                           
118200                              MQPMO-SET-IDENTITY-CONTEXT.                 
118300*                                                                         
118400     CALL WS-MQPUT USING PV-HCONN                                         
118500                         PV-PQ-HANDLE                                     
118600                         MQM-MESSAGE-DESCRIPTOR                           
118700                         MQM-PUT-MESSAGE-OPTIONS                          
118800                         PV-MSGLENGTH                                     
118900                         PV-MSGBUFFER                                     
119000                         PV-COMPLETION-CODE                               
119100                         PV-REASON                                        
119200*                                                                         
119300*        If put failed then display error message                         
119400*        and break out of loop                                            
119500*                                                                         
119600     IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
119700        MOVE 'MQPUT ERROR'            TO PV-ERROR-MESSAGE                 
119800        MOVE '6200-MQPUT-MSG' TO PV-PARAGRAPH-NAME                        
119900        PERFORM 6500-DISPLAY-ERROR-MESSAGE                                
120000     ELSE                                                                 
120100        ADD 1 TO PV-NUMPUTS                                               
120200        DISPLAY 'MQPUT SUCCESSFUL'                                        
120300     END-IF.                                                              
120400                                                                          
120500* ------------------------------------------------------------- *         
120600* CLOSE THE QUEUE                                                         
120700* ------------------------------------------------------------- *         
120800                                                                          
120900 6300-CLOSE-QUEUE.                                                        
121000     CALL WS-MQCLOSE USING PV-HCONN                                       
121100                          PV-PQ-HANDLE                                    
121200                          MQCO-NONE                                       
121300                          PV-COMPLETION-CODE                              
121400                          PV-REASON.                                      
121500     IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
121600        MOVE 'MQCLOSE ERROR'     TO PV-ERROR-MESSAGE                      
121700        MOVE '6300-CLOSE-QUEUE ' TO PV-PARAGRAPH-NAME                     
121800        PERFORM 6500-DISPLAY-ERROR-MESSAGE                                
121900     ELSE                                                                 
122000        DISPLAY 'MQCLOSE SUCCESSFUL'                                      
122100     END-IF.                                                              
122200                                                                          
122300* ------------------------------------------------------------- *         
122400* DISCONNECT FROM THE MQ QUEUE MANAGER                                    
122500* ------------------------------------------------------------- *         
122600 6400-DISCONNECT-QMGR.                                                    
122700                                                                          
122800     IF PV-CON-REASON IS NOT EQUAL TO MQRC-ALREADY-CONNECTED              
122900        CALL WS-MQDISC USING PV-HCONN                                     
123000                             PV-COMPLETION-CODE                           
123100                             PV-REASON                                    
123200        IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                        
123300           MOVE 'MQDISC ERROR'         TO PV-ERROR-MESSAGE                
123400           MOVE '6400-DISCONNECT-QMGR' TO PV-PARAGRAPH-NAME               
123500           PERFORM 6500-DISPLAY-ERROR-MESSAGE                             
123600        ELSE                                                              
123700           DISPLAY 'MQDISC SUCCESSFUL'                                    
123800        END-IF                                                            
123900     END-IF.                                                              
124000* ------------------------------------------------------------- *         
124100* DISPLAY THE VALUES OF A FAILED MQ API COMMAND                           
124200* ------------------------------------------------------------- *         
124300 6500-DISPLAY-ERROR-MESSAGE.                                              
124400* ------------------------------------------------------------- *         
124500                                                                          
124600     DISPLAY '** ABEND ***************************************'.          
124700     DISPLAY '** PROGRAM:   ', PC-PROGRAM-NAME.                           
124800     DISPLAY '** PARAGRAPH: ', PV-PARAGRAPH-NAME.                         
124900     DISPLAY '** MESSAGE:   ', PV-ERROR-MESSAGE.                          
125000     DISPLAY '** COMP CODE: ', PV-COMPLETION-CODE.                        
125100     DISPLAY '** RSN CODE:  ', PV-REASON.                                 
125101     DISPLAY '************************************************'.          
125102     SET AC-MQ-ERROR TO TRUE.                                             
125103     CALL 'ILBOABN0' USING ABEND-CODE.                                    
125104                                                                          
125105******************************************************************        
125106* OPEN THE PURCHASE ORDER CURSOR.                                         
125107******************************************************************        
125108 7000-OPEN-PO-CURSOR.                                                     
125109                                                                          
125110     MOVE '7000-OPEN-PO-CURSOR'                                           
125120                                  TO PV-PARAGRAPH-NAME.                   
125130     INITIALIZE DCLXLT00003                                               
125140                DCLXLT00004.                                              
125150        EXEC SQL                                                          
125160             OPEN PO-CSR                                                  
125170        END-EXEC.                                                         
125180                                                                          
125190         EVALUATE TRUE                                                    
125200             WHEN SQLCODE = ZERO                                          
125300                 CONTINUE                                                 
125400             WHEN SQLWARN0 NOT = SPACES                                   
125500             WHEN SQLCODE < ZERO                                          
125600             WHEN SQLCODE > ZERO                                          
125700                 MOVE 'OPEN CURSOR' TO PV-DB2-OPER-ATTEMPTED              
125800                 PERFORM  9999-SQL-ABEND                                  
125900         END-EVALUATE.                                                    
126000                                                                          
126100                                                                          
126200******************************************************************        
126300* FETCH THE PURCHASE ORDER CURSOR.                                        
126400* FOR EACH SUCCESSFUL FETCH, ADD 1 TO THE CURSOR COUNTER.                 
126500******************************************************************        
126600 7010-FETCH-PO-CURSOR.                                                    
126700                                                                          
126800     MOVE '7010-FETCH-PO-CURSOR'                                          
126900                                  TO PV-PARAGRAPH-NAME.                   
127000                                                                          
127100        EXEC SQL                                                          
127200           FETCH PO-CSR                                                   
127300           INTO :PURCHS-PO-NBR,                                           
127400                :PURCHS-DEPT-NBR,                                         
127500                :PURCHS-OTB-PER-NBR,                                      
127600                :PURCHS-PO-TYP-CDE,                                       
127700                :PLNSHP-DONT-SHIP-BEF-DTE,                                
127800                :PURITM-PO-LNE-NBR,                                       
127900                :PURITM-APP-PO-LNE-NBR,                                   
128000                :XLT00004-MAJ-CL-NBR,                                     
128100                :XLT00004-SUB-CL-NBR,                                     
128200                :XLT00003-SKU-NBR,                                        
128300                :PURITM-ITM-NBR,                                          
128400                :PURITM-UNT-QTY,                                          
128500                :PURITM-UNT-RTL-PR-AMT,                                   
128600                :PURITM-UNT-COST-AMT,                                     
128700                :PURITM-GP-QTY,                                           
128800                :PURITM-GP-RTL-AMT,                                       
128900                :PURITM-AD-EVNT-DTE,                                      
129000                :SKXREF-SKU-STAT-CDE,                                     
129100                :XLT00003-GP-CDE,                                         
129200                :XLT00004-VS-NBR,                                         
129300                :XLT00004-ENT-ID                                          
129400        END-EXEC.                                                         
129500                                                                          
129600         EVALUATE TRUE                                                    
129700             WHEN SQLCODE = ZERO                                          
129800                 ADD 1 TO PV-PO-CURSOR-COUNT                              
129900             WHEN SQLCODE = +100                                          
130000                 SET PS-NO-ITEMS-LEFT-ON-DB TO TRUE                       
130100             WHEN SQLWARN0 NOT = SPACES                                   
130200             WHEN SQLCODE < ZERO                                          
130300             WHEN SQLCODE > ZERO                                          
130400                 MOVE 'FETCH CURSOR' TO PV-DB2-OPER-ATTEMPTED             
130500                 PERFORM  9999-SQL-ABEND                                  
130600         END-EVALUATE.                                                    
130700                                                                          
130800                                                                          
130900******************************************************************        
131000* CLOSE THE PURCHASE ORDER CURSOR.                                        
131100******************************************************************        
131200 7020-CLOSE-PO-CURSOR.                                                    
131300                                                                          
131400     MOVE '7020-CLOSE-PO-CURSOR'                                          
131500                                  TO PV-PARAGRAPH-NAME.                   
131600        EXEC SQL                                                          
131700            CLOSE PO-CSR                                                  
131800        END-EXEC.                                                         
131900                                                                          
132000         EVALUATE TRUE                                                    
132100             WHEN SQLCODE = ZERO                                          
132200                 CONTINUE                                                 
132300             WHEN SQLWARN0 NOT = SPACES                                   
132400             WHEN SQLCODE < ZERO                                          
132500             WHEN SQLCODE > ZERO                                          
132600                 MOVE 'CLOSE CURSOR' TO PV-DB2-OPER-ATTEMPTED             
132700                 PERFORM  9999-SQL-ABEND                                  
132800         END-EVALUATE.                                                    
132900                                                                          
133000                                                                          
133100******************************************************************        
133200* OPEN THE ALLPLN CURSOR.                                                 
133300******************************************************************        
133400 7100-OPEN-ALLPLN-CURSOR.                                                 
133500                                                                          
133600     MOVE '7100-OPEN-ALLPLN-CURSOR'                                       
133700                                  TO PV-PARAGRAPH-NAME.                   
133800        EXEC SQL                                                          
133900             OPEN ALLPLN-CSR                                              
134000        END-EXEC.                                                         
134100                                                                          
134200         EVALUATE TRUE                                                    
134300             WHEN SQLCODE = ZERO                                          
134400                 CONTINUE                                                 
134500             WHEN SQLWARN0 NOT = SPACES                                   
134600             WHEN SQLCODE < ZERO                                          
134700             WHEN SQLCODE > ZERO                                          
134800                 MOVE 'OPEN CURSOR' TO PV-DB2-OPER-ATTEMPTED              
134900                 PERFORM  9999-SQL-ABEND                                  
135000         END-EVALUATE.                                                    
135100                                                                          
135200                                                                          
135300******************************************************************        
135400* FETCH THE ALLPLAN CURSOR.                                               
135500******************************************************************        
135600 7110-FETCH-ALLPLN-CURSOR.                                                
135700                                                                          
135800     MOVE '7110-FETCH-ALLPLN-CURSOR'                                      
135900                                  TO PV-PARAGRAPH-NAME.                   
136000        EXEC SQL                                                          
136100           FETCH ALLPLN-CSR                                               
136200           INTO :ALLPLN-DEST-NBR                                          
136300               ,:ALLPLN-CURR-SPLIT-QTY                                    
136400               ,:ALLPLN-PLAN-NBR                                          
136500               ,:ALLPLN-STATUS-CDE                                        
136600        END-EXEC.                                                         
136700                                                                          
136800         EVALUATE TRUE                                                    
136900             WHEN SQLCODE = ZERO                                          
137000                 CONTINUE                                                 
137100             WHEN SQLCODE = +100                                          
137200                 SET PS-NO-DC-LEFT   TO TRUE                              
137300             WHEN SQLWARN0 NOT = SPACES                                   
137400             WHEN SQLCODE < ZERO                                          
137500             WHEN SQLCODE > ZERO                                          
137600                 MOVE 'FETCH CURSOR' TO PV-DB2-OPER-ATTEMPTED             
137700                 PERFORM  9999-SQL-ABEND                                  
137800         END-EVALUATE.                                                    
137900                                                                          
138000                                                                          
138100******************************************************************        
138200* CLOSE THE ALLPLN CURSOR                                                 
138300******************************************************************        
138400 7120-CLOSE-ALLPLN-CURSOR.                                                
138500                                                                          
138600     MOVE '7120-CLOSE-ALLPLN-CURSOR'                                      
138700                                  TO PV-PARAGRAPH-NAME.                   
138800        EXEC SQL                                                          
138900            CLOSE ALLPLN-CSR                                              
139000        END-EXEC.                                                         
139100                                                                          
139200         EVALUATE TRUE                                                    
139300             WHEN SQLCODE = ZERO                                          
139400                 CONTINUE                                                 
139500             WHEN SQLWARN0 NOT = SPACES                                   
139600             WHEN SQLCODE < ZERO                                          
139700             WHEN SQLCODE > ZERO                                          
139800                 MOVE 'CLOSE CURSOR' TO PV-DB2-OPER-ATTEMPTED             
139900                 PERFORM  9999-SQL-ABEND                                  
140000         END-EVALUATE.                                                    
140100                                                                          
140200                                                                          
140300******************************************************************        
140400* SUM THE TALLPLN SPLITS FOR COMPARISON TO LINE QTY                       
140500******************************************************************        
140600 7130-SUM-SPLITS.                                                         
140700                                                                          
140800     MOVE '7130-SUM-SPLITS'                                               
140900                                  TO PV-PARAGRAPH-NAME.                   
141000     PERFORM                                                              
141100         WITH TEST AFTER                                                  
141200         VARYING PV-RETRY-COUNTER                                         
141300         FROM 1 BY 1                                                      
141400         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
141500                   OR (SQLCODE = ZERO))                                   
141600                                                                          
141700        EXEC SQL                                                          
141800           SELECT SUM(CURR_SPLIT_QTY)                                     
141900           INTO :PV-CURR-SPLIT-QTY:PV-DC-VERIFY-QTY-NULL                  
142000           FROM TALLPLN                                                   
142100          WHERE  PO_NBR      = :PURCHS-PO-NBR                             
142200            AND  PO_LNE_NBR  = :PURITM-PO-LNE-NBR                         
142300        END-EXEC                                                          
142400                                                                          
142500         EVALUATE TRUE                                                    
142600             WHEN SQLCODE = ZERO                                          
142700                 IF PV-DC-VERIFY-QTY-NULL < ZERO                          
142800                    MOVE ZERO TO PV-CURR-SPLIT-QTY                        
142900                 END-IF                                                   
143000             WHEN SQLCODE = +100                                          
143100                    MOVE ZERO TO PV-CURR-SPLIT-QTY                        
143200                 CONTINUE                                                 
143300             WHEN SQLWARN0 NOT = SPACES                                   
143400             WHEN SQLCODE < ZERO                                          
143500             WHEN SQLCODE > ZERO                                          
143600                 MOVE 'SELECT SUM' TO PV-DB2-OPER-ATTEMPTED               
143700                 PERFORM  9999-SQL-ABEND                                  
143800         END-EVALUATE                                                     
143900      END-PERFORM.                                                        
144000                                                                          
144100     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
144200          MOVE 'MAX RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED            
144300          PERFORM  9999-SQL-ABEND                                         
144400     END-IF.                                                              
144500                                                                          
144510* TCS-MIPS-RDN-START                                                      
144520                                                                          
144530******************************************************************        
144540* OPEN THE STORE DISTRIBUTION (TMESTDS) CURSOR.                           
144550******************************************************************        
144560 7210-OPEN-SUM-RECDIS-CSR.                                                
144570                                                                          
144580     MOVE '7210-OPEN-SUM-RECDIS-CSR'                                      
144590                                  TO PV-PARAGRAPH-NAME.                   
144591     PERFORM                                                              
144592         WITH TEST AFTER                                                  
144593         VARYING PV-RETRY-COUNTER                                         
144594         FROM 1 BY 1                                                      
144595         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
144596                   OR (SQLCODE = ZERO))                                   
144597                                                                          
144598        EXEC SQL                                                          
144599             OPEN SUM-RECDIS-CSR                                          
144600        END-EXEC                                                          
144601                                                                          
144602         EVALUATE TRUE                                                    
144603             WHEN SQLCODE = ZERO                                          
144604                 CONTINUE                                                 
144605             WHEN SQLWARN0 NOT = SPACES                                   
144606             WHEN SQLCODE < ZERO                                          
144607             WHEN SQLCODE > ZERO                                          
144608                 MOVE 'OPEN CURSOR' TO PV-DB2-OPER-ATTEMPTED              
144609                 PERFORM  9999-SQL-ABEND                                  
144610         END-EVALUATE                                                     
144611     END-PERFORM.                                                         
144612                                                                          
144613                                                                          
144614******************************************************************        
144615* FETCH THE STORE DISTRIBUTION (TMESTDS) CURSOR.                          
144616* FOR THE FETCHED ROW, MOVE STORE NBR AND DISTRO QTY TO PV FIELDS.        
144617******************************************************************        
144618 7220-FETCH-SUM-RECDIS-CSR.                                               
144619                                                                          
144621     MOVE '7220-FETCH-SUM-RECDIS-CSR'                                     
144622                                  TO PV-PARAGRAPH-NAME.                   
144623        EXEC SQL                                                          
144624           FETCH SUM-RECDIS-CSR                                           
144625           INTO :RECDIS-STR-NBR                                           
144626               ,:RECDIS-AP-VERIFY-ITM-QTY:PV-DC-VERIFY-QTY-NULL           
144627        END-EXEC.                                                         
144628                                                                          
144629         EVALUATE TRUE                                                    
144630             WHEN SQLCODE = ZERO                                          
144631                 IF PV-DC-VERIFY-QTY-NULL < ZERO                          
144632                    MOVE RECDIS-STR-NBR                                   
144633                                      TO PV-STR-NBR(PV-STR-INDX)          
144634                    MOVE ZEROES                                           
144635                                      TO PV-DC-SUM(PV-STR-INDX)           
144636                 ELSE                                                     
144637                    COMPUTE PV-DC-TOTAL-QTY =                             
144638                             PV-DC-TOTAL-QTY  +                           
144639                             RECDIS-AP-VERIFY-ITM-QTY                     
144640                    MOVE RECDIS-STR-NBR       TO                          
144641                                         PV-STR-NBR(PV-STR-INDX)          
144642                    MOVE RECDIS-AP-VERIFY-ITM-QTY TO                      
144643                                         PV-DC-SUM(PV-STR-INDX)           
144644                 END-IF                                                   
144645             WHEN SQLCODE = +100                                          
144647                 MOVE PV-DC-TOTAL-QTY TO RECDIS-AP-VERIFY-ITM-QTY         
144648                 COMPUTE PV-LAST-STR-INDX = PV-STR-INDX - 1               
144650                 SET PS-SUM-RECDIS-CSR-END  TO TRUE                       
144651             WHEN SQLWARN0 NOT = SPACES                                   
144652             WHEN SQLCODE < ZERO                                          
144653             WHEN SQLCODE > ZERO                                          
144654                 MOVE 'SELECT SUM' TO PV-DB2-OPER-ATTEMPTED               
144655                 PERFORM  9999-SQL-ABEND                                  
144656         END-EVALUATE.                                                    
144657                                                                          
144658                                                                          
144659******************************************************************        
144660* CLOSE THE STORE DISTRIBUTION (TMESTDS) CURSOR.                          
144661******************************************************************        
144662 7230-CLOSE-SUM-RECDIS-CSR.                                               
144663                                                                          
144665     MOVE '7230-CLOSE-SUM-RECDIS-CSR'                                     
144666                                  TO PV-PARAGRAPH-NAME.                   
144667        EXEC SQL                                                          
144668            CLOSE SUM-RECDIS-CSR                                          
144669        END-EXEC.                                                         
144670                                                                          
144671         EVALUATE TRUE                                                    
144672             WHEN SQLCODE = ZERO                                          
144673                 CONTINUE                                                 
144674             WHEN SQLWARN0 NOT = SPACES                                   
144675             WHEN SQLCODE < ZERO                                          
144676             WHEN SQLCODE > ZERO                                          
144677                 MOVE 'CLOSE CURSOR' TO PV-DB2-OPER-ATTEMPTED             
144678                 PERFORM  9999-SQL-ABEND                                  
144679         END-EVALUATE.                                                    
144680                                                                          
144681* TCS-MIPS-RDN-END                                                        
144690                                                                          
144700******************************************************************        
144800* SUM THE ACTIVE/VERIFIED RECEIPTS QTY FOR THE CURRENT PO LINE NBR        
144900*     & DC.                                                               
145000******************************************************************        
145100 7200-SUM-RECDIS-DC.                                                      
145200                                                                          
145300     MOVE '7200-SUM-RECDIS-DC'                                            
145400                                  TO PV-PARAGRAPH-NAME.                   
145500     PERFORM                                                              
145600         WITH TEST AFTER                                                  
145700         VARYING PV-RETRY-COUNTER                                         
145800         FROM 1 BY 1                                                      
145900         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
146000                   OR (SQLCODE = ZERO))                                   
146100                                                                          
146200        EXEC SQL                                                          
146300           SELECT SUM(A.AP_VERIFY_ITM_QTY)                                
146400           INTO :RECDIS-AP-VERIFY-ITM-QTY:PV-DC-VERIFY-QTY-NULL           
146500           FROM TRECDIS A,                                                
146600                TRECITM B,                                                
146700                TRECEIV C                                                 
146800           WHERE A.ORD_NBR = :PURCHS-PO-NBR                               
146900             AND B.ORD_NBR = A.ORD_NBR                                    
147000             AND C.ORD_NBR = B.ORD_NBR                                    
147100             AND A.ORD_LNE_NBR = :PURITM-APP-PO-LNE-NBR                   
147200             AND B.ORD_LNE_NBR = A.ORD_LNE_NBR                            
147300             AND A.OPER_UNT_ID = :PV-DC-NBR                               
147400             AND B.OPER_UNT_ID = A.OPER_UNT_ID                            
147500             AND C.OPER_UNT_ID = B.OPER_UNT_ID                            
147600             AND A.REC_SEQ_NBR = B.REC_SEQ_NBR                            
147700             AND B.REC_SEQ_NBR = C.REC_SEQ_NBR                            
147800             AND A.FCLTY_ID    = B.FCLTY_ID                               
147900             AND B.FCLTY_ID    = C.FCLTY_ID                               
148000             AND A.VER_STATUS_CDE = 'A'                                   
148100             AND B.VER_STATUS_CDE = A.VER_STATUS_CDE                      
148200             AND C.VER_STATUS_CDE = B.VER_STATUS_CDE                      
148300             AND B.STATUS_CDE = 'VE'                                      
148400             AND C.STATUS_CDE = 'VE'                                      
148500             AND DATE(C.VERFYD_TMST) <= :PV-CONTROL-PROCESS-DATE          
148600           WITH UR                                                        
148700        END-EXEC                                                          
148800                                                                          
148900         EVALUATE TRUE                                                    
149000             WHEN SQLCODE = ZERO                                          
149100                 IF PV-DC-VERIFY-QTY-NULL < ZERO                          
149200                    MOVE ZERO TO RECDIS-AP-VERIFY-ITM-QTY                 
149300                 END-IF                                                   
149400             WHEN SQLCODE = +100                                          
149500                    MOVE ZERO TO RECDIS-AP-VERIFY-ITM-QTY                 
149600                 CONTINUE                                                 
149700             WHEN SQLWARN0 NOT = SPACES                                   
149800             WHEN SQLCODE < ZERO                                          
149900             WHEN SQLCODE > ZERO                                          
150000                 MOVE 'SELECT SUM' TO PV-DB2-OPER-ATTEMPTED               
150100                 PERFORM  9999-SQL-ABEND                                  
150200         END-EVALUATE                                                     
150300      END-PERFORM.                                                        
150400                                                                          
150500     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
150600          MOVE 'MAX RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED            
150700          PERFORM  9999-SQL-ABEND                                         
150800     END-IF.                                                              
150900                                                                          
151000                                                                          
151100******************************************************************        
151200* SUM THE ACTIVE/VERIFIED RECEIPTS QTY FOR THE CURRENT PO LINE NBR        
151300******************************************************************        
151400 7250-SUM-RECDIS-LINE.                                                    
151500                                                                          
151600     MOVE '7250-SUM-RECDIS-LINE'                                          
151700                                  TO PV-PARAGRAPH-NAME.                   
151800     PERFORM                                                              
151900         WITH TEST AFTER                                                  
152000         VARYING PV-RETRY-COUNTER                                         
152100         FROM 1 BY 1                                                      
152200         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
152300                   OR (SQLCODE = ZERO))                                   
152400                                                                          
152500        EXEC SQL                                                          
152600           SELECT SUM(A.AP_VERIFY_ITM_QTY)                                
152700           INTO :RECDIS-AP-VERIFY-ITM-QTY:PV-DC-VERIFY-QTY-NULL           
152800           FROM TRECDIS A,                                                
152900                TRECITM B,                                                
153000                TRECEIV C                                                 
153100           WHERE A.ORD_NBR = :PURCHS-PO-NBR                               
153200             AND B.ORD_NBR = A.ORD_NBR                                    
153300             AND C.ORD_NBR = B.ORD_NBR                                    
153400             AND A.ORD_LNE_NBR = :PURITM-APP-PO-LNE-NBR                   
153500             AND B.ORD_LNE_NBR = A.ORD_LNE_NBR                            
153600             AND B.OPER_UNT_ID = A.OPER_UNT_ID                            
153700             AND C.OPER_UNT_ID = B.OPER_UNT_ID                            
153800             AND A.REC_SEQ_NBR = B.REC_SEQ_NBR                            
153900             AND B.REC_SEQ_NBR = C.REC_SEQ_NBR                            
154000             AND A.FCLTY_ID    = B.FCLTY_ID                               
154100             AND B.FCLTY_ID    = C.FCLTY_ID                               
154200             AND A.VER_STATUS_CDE = 'A'                                   
154300             AND B.VER_STATUS_CDE = A.VER_STATUS_CDE                      
154400             AND C.VER_STATUS_CDE = B.VER_STATUS_CDE                      
154500             AND B.STATUS_CDE = 'VE'                                      
154600             AND C.STATUS_CDE = 'VE'                                      
154700             AND DATE(C.VERFYD_TMST) <= :PV-CONTROL-PROCESS-DATE          
154800           WITH UR                                                        
154900        END-EXEC                                                          
155000                                                                          
155100         EVALUATE TRUE                                                    
155200             WHEN SQLCODE = ZERO                                          
155300                 IF PV-DC-VERIFY-QTY-NULL < ZERO                          
155400                    MOVE ZERO TO RECDIS-AP-VERIFY-ITM-QTY                 
155500                 END-IF                                                   
155600             WHEN SQLCODE = +100                                          
155700                    MOVE ZERO TO RECDIS-AP-VERIFY-ITM-QTY                 
155800                 CONTINUE                                                 
155900             WHEN SQLWARN0 NOT = SPACES                                   
156000             WHEN SQLCODE < ZERO                                          
156100             WHEN SQLCODE > ZERO                                          
156200                 MOVE 'SELECT SUM' TO PV-DB2-OPER-ATTEMPTED               
156300                 PERFORM  9999-SQL-ABEND                                  
156400         END-EVALUATE                                                     
156500      END-PERFORM.                                                        
156600                                                                          
156700     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
156800          MOVE 'MAX RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED            
156900          PERFORM  9999-SQL-ABEND                                         
157000     END-IF.                                                              
157100                                                                          
157200                                                                          
157300******************************************************************        
157400* SUM ALL STORE DISTRIBUTION QTY'S FOR CURRENT PO LINE NBR & DC.          
157500******************************************************************        
157600 7300-SUM-MESTDS-DC.                                                      
157700                                                                          
157800     MOVE '7300-SUM-MESTDS-DC'                                            
157900                                  TO PV-PARAGRAPH-NAME.                   
158000     PERFORM                                                              
158100         WITH TEST AFTER                                                  
158200         VARYING PV-RETRY-COUNTER                                         
158300         FROM 1 BY 1                                                      
158400         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
158500                   OR (SQLCODE = ZERO))                                   
158600                                                                          
158700        EXEC SQL                                                          
158800           SELECT SUM(STR_DISTRO_QTY)                                     
158900           INTO :MESTDS-STR-DISTRO-QTY:PV-DC-MESTDS-QTY-NULL              
159000           FROM TMESTDS                                                   
159100           WHERE PO_NBR = :PURCHS-PO-NBR                                  
159200            AND  PO_LNE_NBR  = :PURITM-PO-LNE-NBR                         
159300            AND  PLAN_NBR    = :MESTDS-PLAN-NBR                           
159400        END-EXEC                                                          
159500                                                                          
159600         EVALUATE TRUE                                                    
159700             WHEN SQLCODE = ZERO                                          
159800                 IF PV-DC-MESTDS-QTY-NULL < ZERO                          
159900                    MOVE ZERO TO MESTDS-STR-DISTRO-QTY                    
160000                 END-IF                                                   
160100             WHEN SQLCODE = +100                                          
160200                    MOVE ZERO TO MESTDS-STR-DISTRO-QTY                    
160300                 CONTINUE                                                 
160400             WHEN SQLWARN0 NOT = SPACES                                   
160500             WHEN SQLCODE < ZERO                                          
160600             WHEN SQLCODE > ZERO                                          
160700                 MOVE 'SELECT SUM' TO PV-DB2-OPER-ATTEMPTED               
160800                 PERFORM  9999-SQL-ABEND                                  
160900         END-EVALUATE                                                     
161000      END-PERFORM.                                                        
161100                                                                          
161200     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
161300          MOVE 'MAX RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED            
161400          PERFORM  9999-SQL-ABEND                                         
161500     END-IF.                                                              
161600                                                                          
161700                                                                          
161800******************************************************************        
161900* OPEN THE STORE DISTRIBUTION (TMESTDS) CURSOR.                           
162000******************************************************************        
162100 7600-OPEN-MESTDS-CURSOR.                                                 
162200                                                                          
162300     MOVE '7600-OPEN-MESTDS-CURSOR'                                       
162400                                  TO PV-PARAGRAPH-NAME.                   
162500        EXEC SQL                                                          
162600             OPEN MESTDS-CSR                                              
162700        END-EXEC.                                                         
162800                                                                          
162900         EVALUATE TRUE                                                    
163000             WHEN SQLCODE = ZERO                                          
163100                 CONTINUE                                                 
163200             WHEN SQLWARN0 NOT = SPACES                                   
163300             WHEN SQLCODE < ZERO                                          
163400             WHEN SQLCODE > ZERO                                          
163500                 MOVE 'OPEN CURSOR' TO PV-DB2-OPER-ATTEMPTED              
163600                 PERFORM  9999-SQL-ABEND                                  
163700         END-EVALUATE.                                                    
163800                                                                          
163900                                                                          
164000******************************************************************        
164100* FETCH THE STORE DISTRIBUTION (TMESTDS) CURSOR.                          
164200* FOR THE FETCHED ROW, MOVE STORE NBR AND DISTRO QTY TO PV FIELDS.        
164300******************************************************************        
164400 7610-FETCH-MESTDS.                                                       
164500                                                                          
164600     MOVE '7610-FETCH-MESTDS'                                             
164700                                  TO PV-PARAGRAPH-NAME.                   
164800        EXEC SQL                                                          
164900           FETCH MESTDS-CSR                                               
165000           INTO :MESTDS-STR-DISTRO-QTY                                    
165100               ,:MESTDS-STR-NBR                                           
165200        END-EXEC.                                                         
165300                                                                          
165400        EVALUATE TRUE                                                     
165500            WHEN SQLCODE = ZERO                                           
165600                MOVE MESTDS-STR-NBR        TO PV-STR-NBR-9                
165700                MOVE MESTDS-STR-DISTRO-QTY TO PV-STR-QTY                  
165800            WHEN SQLCODE = +100                                           
165900                SET PS-NO-ITEMS-LEFT-ON-TMESTDS  TO TRUE                  
166000            WHEN SQLWARN0 NOT = SPACES                                    
166100            WHEN SQLCODE < ZERO                                           
166200            WHEN SQLCODE > ZERO                                           
166300                MOVE 'FETCH CURSOR' TO PV-DB2-OPER-ATTEMPTED              
166400                PERFORM  9999-SQL-ABEND                                   
166500        END-EVALUATE.                                                     
166600                                                                          
166700                                                                          
166800******************************************************************        
166900* CLOSE THE STORE DISTRIBUTION (TMESTDS) CURSOR.                          
167000******************************************************************        
167100 7620-CLOSE-MESTDS-CURSOR.                                                
167200                                                                          
167300     MOVE '7620-CLOSE-MESTDS-CURSOR'                                      
167400                                  TO PV-PARAGRAPH-NAME.                   
167500        EXEC SQL                                                          
167600            CLOSE MESTDS-CSR                                              
167700        END-EXEC.                                                         
167800                                                                          
167900         EVALUATE TRUE                                                    
168000             WHEN SQLCODE = ZERO                                          
168100                 CONTINUE                                                 
168200             WHEN SQLWARN0 NOT = SPACES                                   
168300             WHEN SQLCODE < ZERO                                          
168400             WHEN SQLCODE > ZERO                                          
168500                 MOVE 'CLOSE CURSOR' TO PV-DB2-OPER-ATTEMPTED             
168600                 PERFORM  9999-SQL-ABEND                                  
168700         END-EVALUATE.                                                    
168800                                                                          
168900                                                                          
169000******************************************************************        
169100* SUM THE ACTIVE/VERIFIED RECEIPTS QTY FOR THE CURRENT PO LINE NBR        
169200*     & PARTICULAR STORE.                                                 
169300******************************************************************        
169400 7700-GET-RECDIS.                                                         
169500                                                                          
169600     MOVE '7700-GET-RECDIS'                                               
169700                                  TO PV-PARAGRAPH-NAME.                   
169710* TCS-MIPS-REDN-START                                                     
169800*    PERFORM                                                              
169900*        WITH TEST AFTER                                                  
170000*        VARYING PV-RETRY-COUNTER                                         
170100*        FROM 1 BY 1                                                      
170200*        UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
170300*                  OR (SQLCODE = ZERO))                                   
170400*                                                                         
170500*       EXEC SQL                                                          
170600*          SELECT SUM(A.AP_VERIFY_ITM_QTY)                                
170700*          INTO :RECDIS-AP-VERIFY-ITM-QTY:PV-VERIFY-QTY-NULL              
170800*          FROM  TRECDIS A,                                               
170900*                TRECITM B,                                               
171000*                TRECEIV C                                                
171100*          WHERE A.ORD_NBR = :PURCHS-PO-NBR                               
171200*            AND B.ORD_NBR = A.ORD_NBR                                    
171300*            AND B.CRE_TMST = B.CRE_TMST                                  
171400*            AND C.ORD_NBR = B.ORD_NBR                                    
171500*            AND A.ORD_LNE_NBR = :PURITM-APP-PO-LNE-NBR                   
171600*            AND B.ORD_LNE_NBR = A.ORD_LNE_NBR                            
171700*            AND A.STR_NBR     = :MESTDS-STR-NBR                          
171800*            AND A.OPER_UNT_ID = :PV-DC-NBR                               
171900*            AND B.OPER_UNT_ID = A.OPER_UNT_ID                            
172000*            AND C.OPER_UNT_ID = B.OPER_UNT_ID                            
172100*            AND A.REC_SEQ_NBR = B.REC_SEQ_NBR                            
172200*            AND B.REC_SEQ_NBR = C.REC_SEQ_NBR                            
172300*            AND A.FCLTY_ID    = B.FCLTY_ID                               
172400*            AND B.FCLTY_ID    = C.FCLTY_ID                               
172500*            AND A.VER_STATUS_CDE = 'A'                                   
172600*            AND B.VER_STATUS_CDE = A.VER_STATUS_CDE                      
172700*            AND C.VER_STATUS_CDE = B.VER_STATUS_CDE                      
172800*            AND B.STATUS_CDE = 'VE'                                      
172900*            AND C.STATUS_CDE = 'VE'                                      
173000*            AND DATE(C.VERFYD_TMST) <= :PV-CONTROL-PROCESS-DATE          
173100*          WITH UR                                                        
173200*       END-EXEC                                                          
173300*                                                                         
173400*        EVALUATE TRUE                                                    
173500*            WHEN SQLCODE = ZERO                                          
173600*                IF PV-VERIFY-QTY-NULL < ZERO                             
173700*                   MOVE ZERO TO RECDIS-AP-VERIFY-ITM-QTY                 
173800*                END-IF                                                   
173900*            WHEN SQLCODE = +100                                          
174000*                   MOVE ZERO TO RECDIS-AP-VERIFY-ITM-QTY                 
174100*                CONTINUE                                                 
174200*            WHEN SQLWARN0 NOT = SPACES                                   
174300*            WHEN SQLCODE < ZERO                                          
174400*            WHEN SQLCODE > ZERO                                          
174500*                MOVE 'SELECT SUM' TO PV-DB2-OPER-ATTEMPTED               
174600*                PERFORM  9999-SQL-ABEND                                  
174700*        END-EVALUATE                                                     
174800*     END-PERFORM.                                                        
174900*                                                                         
175000*    IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
175100*         MOVE 'MAX RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED            
175200*         PERFORM  9999-SQL-ABEND                                         
175300*    END-IF.                                                              
175310     SET PS-STR-MATCH-NOT-FOUND TO TRUE                                   
175320     PERFORM VARYING PV-STR-INDX FROM 1 BY 1 UNTIL                        
175330                     ( PS-STR-MATCH-FOUND OR                              
175340                     ( PV-STR-INDX > PV-LAST-STR-INDX ))                  
175350             IF PV-STR-NBR(PV-STR-INDX) = MESTDS-STR-NBR                  
175360                SET PS-STR-MATCH-FOUND      TO TRUE                       
175370                MOVE PV-DC-SUM(PV-STR-INDX) TO                            
175380                                        RECDIS-AP-VERIFY-ITM-QTY          
175390             END-IF                                                       
175391     END-PERFORM.                                                         
175392                                                                          
175393     IF PS-STR-MATCH-NOT-FOUND                                            
175394        MOVE ZEROES  TO  RECDIS-AP-VERIFY-ITM-QTY                         
175395     END-IF.                                                              
175396* TCS-MIPS-RDN-END                                                        
175400                                                                          
175500                                                                          
175600******************************************************************        
175700* FORMAT THE OUTPUT ON-ORDER RECORD.                                      
175800*   - MOVE THE PV FIELDS TO THE OUTPUT RECORD.                            
175900*   - CALCULATE TOTAL COST & TOTAL RETAIL DOLLARS.                        
176000*   - IF AN AD-EVENT-DATE IS NOT = 0001-01-01, SET THE PROMO IND          
176100*     AND QTY.                                                            
176200*   - DETERMINE THE IN-DC-DATE AND THE WEEK-DATE:                         
176300*        IN-DC-DATE = SHIP-DATE + PC-NBR-SHIP-DAYS.                       
176400*        WEEK-DATE = FISCAL BOW DATE/SUNDAY DATE FOR IN-DC-DATE.          
176500*     IF THE CURRENT SHIP-DATE IS THE SAME AS THE HOLD-SHIP-DATE,         
176600*        JUST MOVE THE HOLD-IN-DC-DATE AND HOLD-WEEK-DATE TO THE          
176700*        OUTPUT RECORD FIELDS.                                            
176800*     OTHERWISE, CALL THE CALENDAR ROUTINE USING THE CURRENT SHIP-        
176900*        DATE TO RETRIEVE THE IN-DC-DATE AND WEEK-DATE.                   
177000*        MOVE SHIP-DATE AND IN-DC-DATE TO THE HOLD FIELDS AND             
177100*             MOVE IN-DC-DATE TO THE OUTPUT RECORD.                       
177200*        IF THE WEEK-DATE > CURR-SUNDAY-DATE (FOUND IN 1100),             
177300*           MOVE THE WEEK-DATE TO THE OUTPUT REC & THE HOLD FIELD         
177400*        ELSE                                                             
177500*           MOVE CURR-SUNDAY-DATE TO THE OUTPUT REC & THE HOLD.           
177600*           (THIS WILL FORCE ORDERS FOR PRIOR WEEKS TO HAVE THE           
177700*            CURRENT SUNDAY WEEK-DATE, SO THESE "OLD" OPEN ORDERS         
177800*            CAN EVENTUALLY GET ROLLED INTO THE CURRENT WEEK IN           
177900*            SUBSEQUENT PROGRAMS.)                                        
178000******************************************************************        
178100 8000-FORMAT-STORE-INFO.                                                  
178200                                                                          
178300     MOVE PV-ITM-NBR        TO PO018-ONORD-ITEM-NBR.                      
178400     MOVE PV-DEPT-NBR       TO PO018-ONORD-DEPT-NBR.                      
178500     MOVE PV-SKU-NBR        TO PO018-ONORD-SKU.                           
178600     MOVE PV-ENT-ID         TO PO018-ONORD-ENT-ID.                        
178700     MOVE PV-GROUP-CODE     TO PO018-ONORD-GROUP-CODE.                    
178800                                                                          
178900     MOVE PV-LONG-VENDOR-STYLE                                            
179000                            TO PO018-ONORD-LONG-VENDOR-STYLE.             
179100                                                                          
179200     MOVE PV-STR-NBR-9      TO PO018-ONORD-STORE-NBR                      
179300     MOVE PV-STR-QTY        TO PO018-ONORD-UNIT-QTY.                      
179400                                                                          
179500     COMPUTE PO018-ONORD-COST-AMT =                                       
179600             PV-STR-QTY * PV-UNT-COST-AMT.                                
179700                                                                          
179800     COMPUTE PO018-ONORD-RTL-AMT =                                        
179900             PV-STR-QTY * PV-UNT-RTL-PR-AMT.                              
180000                                                                          
180100     MOVE PV-PO-NBR                TO PO018-ONORD-PO-NBR.                 
180200                                                                          
180300     MOVE PURCHS-OTB-PER-NBR       TO PO018-ONORD-BOUGHT-DATE.            
180400     MOVE PURCHS-PO-TYP-CDE        TO PO018-ONORD-PO-TYP-CDE.             
180500                                                                          
180600     IF PURITM-AD-EVNT-DTE  = '0001-01-01'                                
180700        MOVE 'N'        TO PO018-ONORD-PROMO-IND                          
180800        MOVE +0         TO PO018-ONORD-PROMO-QTY                          
180900     ELSE                                                                 
181000        MOVE 'Y'        TO PO018-ONORD-PROMO-IND                          
181100        MOVE PV-STR-QTY TO PO018-ONORD-PROMO-QTY                          
181200     END-IF.                                                              
181300                                                                          
181400     IF PLNSHP-DONT-SHIP-BEF-DTE = PV-HOLD-SHIP-DATE                      
181500         CONTINUE                                                         
181600     ELSE                                                                 
181700         PERFORM 8200-GET-FIRST-FISCAL-DAY                                
181800         MOVE PLNSHP-DONT-SHIP-BEF-DTE   TO POWS055-INPUT-DATE            
181900         PERFORM POPD055-CALC-WEEKLY-OTB                                  
182000         IF POWS055-NO-ERROR                                              
182100            MOVE PLNSHP-DONT-SHIP-BEF-DTE TO PV-HOLD-SHIP-DATE            
182200            MOVE POWS055-CALCED-OTB-DATE  TO PV-HOLD-IN-DC-DATE           
182300            IF POWS055-OTB-FSCL-WK-BEG-DTE >                              
182400                                      PV-CONTROL-SUNDAY-DATE              
182500               MOVE POWS055-OTB-FSCL-WK-BEG-DTE                           
182600                                        TO PV-HOLD-WEEK-DATE              
182700            ELSE                                                          
182800               MOVE PV-CONTROL-SUNDAY-DATE                                
182900                                        TO PV-HOLD-WEEK-DATE              
183000            END-IF                                                        
183100            IF PV-HOLD-IN-DC-DATE    <  PV-FIRST-FP-DAY                   
183200                SET PS-PAST-DUE TO TRUE                                   
183300              ELSE                                                        
183400                SET PS-NOT-PAST-DUE TO TRUE                               
183500            END-IF                                                        
183600         ELSE                                                             
183700             DISPLAY '** ABEND **'                                        
183800             DISPLAY '** PGM = ' PC-PROGRAM-NAME ' **'                    
183900             DISPLAY '** PARAGRAPH = 8000-FORMAT-STORE-INFO'              
184000             DISPLAY '** BAD PERFORM DATE ROUTINE USING '                 
184100                         PLNSHP-DONT-SHIP-BEF-DTE                         
184200             DISPLAY '** ' POWS055-ERROR-TYPE                             
184300             MOVE POWS055-SQLCODE       TO ABEND-CODE                     
184400             CALL 'ILBOABN0' USING ABEND-CODE                             
184500         END-IF                                                           
184600     END-IF.                                                              
184700                                                                          
184800     MOVE PV-HOLD-IN-DC-DATE  TO PO018-ONORD-IN-DC-DATE.                  
184900     MOVE PV-HOLD-WEEK-DATE   TO PO018-ONORD-WEEK-DATE.                   
185000                                                                          
185100*IF REGIONAL INDICATOR IS YES, SELECT TUPCPLS FOR STATUS CODE             
185200     IF SKXREF-SKU-STAT-CDE = '25'                                        
185300       PERFORM 8700-SELECT-TUPCPLS                                        
185400     ELSE                                                                 
185500       MOVE PV-STATUS-CODE        TO PO018-ONORD-STATUS-CODE              
185600     END-IF.                                                              
185700                                                                          
185800******************************************************************        
185900* CALL CALENDAR ROUTINE TO DETERMINE 1ST DAY OF FIS PERIOD BASED          
186000* ON THE SYSTEM DATE.                                                     
186100******************************************************************        
186200 8200-GET-FIRST-FISCAL-DAY.                                               
186300                                                                          
186400     INITIALIZE DPG51                                                     
186500                DPG52                                                     
186600                DPG53                                                     
186700                DPG54                                                     
186800                DPG55                                                     
186900                DPG56.                                                    
187000                                                                          
187100     MOVE 'F2' TO DPG51-CALENDAR-TYPE.                                    
187200     SET DPG51-FISCAL-454-CALENDAR                                        
187300         DPG51-DO-NOT-INCR-DECR-DATE                                      
187400         DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
187500                                                                          
187600     MOVE PV-SYSTEM-DATE-ISO-FMT   TO DPG51-SYSTEM-DATE.                  
187700     MOVE PV-SYSTEM-DATE-ISO-FMT   TO DPG52-LK-DATE-INPUT.                
187800                                                                          
187900     CALL DP500-KOHLS-CALENDAR-ROUTINE  USING DPG51                       
188000                                              DPG52                       
188100                                              DPG53                       
188200                                              DPG54                       
188300                                              DPG55                       
188400                                              DPG56.                      
188500                                                                          
188600     IF   (DPG54-NO-ERROR                                                 
188700       OR  DPG54-WARNING-ERROR)                                           
188800       AND DPG54-DATE-VALID                                               
188900           MOVE DPG56-FIRST-FP-DB2-ISO-FMT TO PV-FIRST-FP-DAY             
189000     ELSE                                                                 
189100             DISPLAY '** ABEND **'                                        
189200             DISPLAY '** PGM = ' PC-PROGRAM-NAME ' **'                    
189300             DISPLAY '** PARAGRAPH = 8200-GET-FIRST-FISCAL-DAY'           
189400             DISPLAY '** BAD CALL TO CALENDAR ROUTINE USING '             
189500                         PV-SYSTEM-DATE-ISO-FMT                           
189600             DISPLAY DPG54-ERROR-MESSAGE                                  
189700             MOVE +4019 TO ABEND-CODE                                     
189800             CALL 'ILBOABN0' USING ABEND-CODE                             
189900     END-IF.                                                              
190000                                                                          
190100******************************************************************        
190200* WRITE THE OUTPUT RECORD TO THE EXTRACT FILE.                            
190300* ADD 1 TO THE OUTPUT RECORD COUNT.                                       
190400******************************************************************        
190500 8500-WRITE-EXTRACT.                                                      
190600                                                                          
190700     WRITE PO-ONORD-EXT-REC FROM PO018-ONORD-EXTRACT-RECORD.              
190800     ADD 1 TO PV-OUTPUT-REC-COUNT.                                        
190900     IF PS-PAST-DUE                                                       
191000         PERFORM 8600-WRITE-EXTRACT                                       
191100     END-IF.                                                              
191200                                                                          
191300******************************************************************        
191400* WRITE THE OUTPUT RECORD TO THE EXTRACT FILE.                            
191500* ADD 1 TO THE OUTPUT RECORD COUNT.                                       
191600******************************************************************        
191700 8600-WRITE-EXTRACT.                                                      
191800                                                                          
191900     WRITE PO-ONORD-PDUE-REC FROM PO018-ONORD-EXTRACT-RECORD.             
192000                                                                          
192100******************************************************************        
192200* SELECT TUPCPLS                                                          
192300******************************************************************        
192400 8700-SELECT-TUPCPLS.                                                     
192500     PERFORM                                                              
192600       WITH TEST AFTER                                                    
192700       VARYING PV-RETRY-COUNTER                                           
192800       FROM 1 BY 1                                                        
192900       UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                         
193000             OR (SQLCODE = ZERO)                                          
193100             OR (SQLCODE = +100))                                         
193200                                                                          
193300     EXEC SQL                                                             
193400       SELECT UPC_STAT_CDE                                                
193500         INTO :PV-UPCPLS-UPC-STAT-CDE                                     
193600       FROM TUPCPLS                                                       
193700         WHERE ITM_NBR = :PV-ITM-NBR                                      
193800          AND ((CURRENT TIMESTAMP BETWEEN                                 
193900                EFF_BEG_TMST AND EFF_END_TMST)                            
194000           OR (EFF_BEG_TMST <= CURRENT TIMESTAMP AND                      
194100                EFF_END_TMST IS NULL))                                    
194200          AND STR_NBR = :PV-STR-NBR-9                                     
194300        END-EXEC                                                          
194400                                                                          
194500     EVALUATE TRUE                                                        
194600*GET STATUS FROM TUPCPLS IF AVAILABLE                                     
194700       WHEN SQLCODE = +0                                                  
194800         MOVE PV-UPCPLS-UPC-STAT-CDE                                      
194900                               TO PO018-ONORD-STATUS-CODE                 
195000       WHEN SQLCODE = -904                                                
195100       WHEN SQLCODE = -913                                                
195200         CONTINUE                                                         
195300*DEFAULT TO STATUS 20, CANNOT DEFAULT TO TSK STATUS CODE                  
195400*TSK STATUS CODE COULD BE 25, 25 NOT VALID STORE STATUS                   
195500*ON DATA WAREHOUSE                                                        
195600       WHEN SQLCODE = +100                                                
195700         MOVE PV-DEFAULT-STATUS-CODE                                      
195800                               TO PO018-ONORD-STATUS-CODE                 
195900       WHEN SQLWARN0 NOT = SPACES                                         
196000       WHEN SQLCODE < ZERO                                                
196100       WHEN SQLCODE > 0                                                   
196200         MOVE '8700-SELECT-TUPCPLS' TO PV-DB2-OPER-ATTEMPTED              
196300         PERFORM 9999-SQL-ABEND                                           
196400       END-EVALUATE                                                       
196500     END-PERFORM                                                          
196600                                                                          
196700     IF PV-RETRY-COUNTER > PC-MAX-RETRIES                                 
196800      MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'                
196900          TO PV-DB2-OPER-ATTEMPTED                                        
197000      PERFORM 9999-SQL-ABEND                                              
197100     END-IF.                                                              
197200                                                                          
197300*****************************************************************         
197400* EXECUTABLE CODE FOR WEEKLY OTB CALC ROUTINE.                            
197500*****************************************************************         
197600     COPY POPD055.                                                        
197700                                                                          
197800******************************************************************        
197900* DISPLAY PROGRAM COUNTS.                                                 
198000******************************************************************        
198100 9000-END-OF-JOB.                                                         
198200                                                                          
198300     DISPLAY '** PROGRAM ' PC-PROGRAM-NAME ' COUNTS **'                   
198400     DISPLAY '** PO-CSR COUNT = ' PV-PO-CURSOR-COUNT.                     
198500     DISPLAY '** OUTPUT COUNT = ' PV-OUTPUT-REC-COUNT.                    
           DISPLAY '**SKIPPED COUNT = ' PV-COUNT.                               
198600                                                                          
198700                                                                          
198800******************************************************************        
198900* ABEND THE PROGRAM IN THE EVENT THAT AN SQL ERROR OR WARNING             
199000* CODE OCCURS.                                                            
199100******************************************************************        
199200 9999-SQL-ABEND.                                                          
199300                                                                          
199400     DISPLAY '** ABEND     **'.                                           
199500     DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
199600     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
199700     DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.                  
199800                                                                          
199900*-----------------------------------------------------------------        
200000* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
200100* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
200200* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
200300* FORMAT.                                                                 
200400*-----------------------------------------------------------------        
200500                                                                          
200600     COPY DPPD004.                                                        
200700                                                                          
200800     MOVE +4013 TO ABEND-CODE.                                            
200900     CALL 'ILBOABN0' USING ABEND-CODE.                                    
