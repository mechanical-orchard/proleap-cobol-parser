       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT154.                                       
       AUTHOR.                  DAN GRUBER.                                     
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            11/03/97.                                       
       DATE-COMPILED.                                                           
      *                                                                         
      ******************************************************************        
      *  THIS PROGRAM CREATES ADJUSTMENT RECORDS WHICH WILL CORRECT             
      *  THE PROBLEMS WITH BOOKING OF UNITS DURING PHYSICAL INVENTORY           
      *  TIME FOR DIRECT STORE SHIPMENTS.                                       
      ******************************************************************        
      *  CHANGED: 05/99    JULIE SEIDLER                                        
      *  ADDED LOGIC TO SET THE A070-UNIT-BOOK-IND. THIS WAS PART OF            
      *  ENHANCEMENTS MADE FOR VARIANCE REPORTING.                              
      ******************************************************************        
RB0401*  CHANGED: 04/01    RONNA BUTZ                                           
RB0401*  MANIFESTING - USE THE PLANNED INVENTORY DATE INSTEAD OF THE            
RB0401*  DC CUTOFF DATE FOR STORES THAT ARE MANIFESTED                          
RB0401******************************************************************        
W21732*  CHANGED: 12/02    SUDHARSHAN ADDURI/IGSI    WR # 21732        *        
W21732*  REPLACED KDC1802 COPYBOOK WITH FSRD035 COPYBOOK. MOVED ITEM   *        
W21732*  NBR TO FSRD035 COPYBOOK INSTEAD OF DEPT, CLASS AND 3 DIGIT    *        
W21732*  SEQ NBR TO KDC1802 COPYBOOK.                                  *        
W26602******************************************************************        
W26602*  CHANGED: 04/27/2004    SCOTT JACKSON        WR # 26602        *        
W26602*  STORE EXPANSION CHANGES. MODIFY REFERENCES TO STORE-NBR DEFINED        
W26602*  AS PIC X(03)/9(04) TO USE PIC S9(04) COMP DEFINITION. COPYBOOK         
W26602*  'STOREEXT' INCORPORATES SE-LOC-NBR AS S9(04). USE THIS FIELD           
W26602*  GOING FORWARD. DB2 TABLE TINVPAR CHANGED ALSO-USE LOC_NBR FIELD        
W26602******************************************************************        
W26600*  CHANGED: 08-17-2004    PAUL KEBER           WR # 26600                 
W26600*  INVENTORY CUTOFF CHANGES. ADD A NEW OUTPUT FILE TO CREATE              
W26600*  CUTOFF DATABASE ADJUSTMENT EXTRACT RECORDS IN THE FORMAT OF            
W26600*  COPYBOOK INRD051 FOR INV1 AND INV2 TRANSACTIONS.  REMOVE FS            
W26600*  ADJUSTMENT FILE WRITES FOR INV1 AND INV2 TRANSACTIONS.                 
W26600******************************************************************        
W28545*  CHANGED: 07-21-2006    J SELWITSCHKA / STRATAGEM                       
W28545*                                              WR # 28545                 
W28545*  DEPARTMENT LEVEL INVENTORY. INCORPORATE INVENTORY ID.                  
W28545******************************************************************        
W33304*  CHANGED 04-03-2008     CHUCK EBERT / STRATAGEM   WR # 33304            
W33304*  HOLDING INVENTORY LEDGER OPEN (HILO) PROJECT                           
W33304******************************************************************        
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT IN-DAILY-EXTRACT-FILE                                         
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT FS-ADJUSTMENT-FILE                                            
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT IN-DSS-REPORT-FILE                                            
               ASSIGN TO OUT02.                                                 
                                                                                
W26600     SELECT IN-DSS-ADJUSTMENT-EXTRACT-FILE                                
W26600         ASSIGN TO OUT03.                                                 
                                                                                
      *                                                                         
       DATA DIVISION.                                                           
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  IN-DAILY-EXTRACT-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IN-DAILY-EXTRACT-REC           PIC X(300).                           
      *                                                                         
      *                                                                         
       FD  FS-ADJUSTMENT-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
W21732 01  FS-ADJUSTMENT-REC              PIC X(200).                           
      *                                                                         
      *                                                                         
       FD  IN-DSS-REPORT-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IN-DSS-REPORT-REC              PIC X(300).                           
      *                                                                         
      *                                                                         
W26600 FD  IN-DSS-ADJUSTMENT-EXTRACT-FILE                                       
W26600     RECORDING MODE IS F                                                  
W26600     LABEL RECORDS ARE STANDARD                                           
W26600     BLOCK CONTAINS 0 RECORDS.                                            
W26600                                                                          
W26600 01  IN-DSS-ADJUSTMENT-EXTRACT-REC  PIC X(48).                            
      *                                                                         
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES             PIC S9(03)  VALUE +3                  
                                                      COMP SYNC.                
           05  PC-NINES-DATE              PIC X(10)   VALUE                     
                                                      '9999-09-09'.             
RB0401     05  PC-YES                     PIC X(01)   VALUE 'Y'.                
RB0401     05  PC-NO                      PIC X(01)   VALUE 'N'.                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-DAILY-FILE-SW         PIC X(01)   VALUE 'N'.           
               88  PS-END-OF-DAILY-FILE                    VALUE 'Y'.           
               88  PS-NOT-END-OF-DAILY-FILE                VALUE 'N'.           
           05  PS-STORE-ON-TINVPAR-SW          PIC X(01)   VALUE 'N'.           
               88  PS-STORE-ON-TINVPAR                     VALUE 'N'.           
               88  PS-STORE-NOT-ON-TINVPAR                 VALUE 'Y'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PROGRAM-NAME            PIC X(08)   VALUE 'INKUT154'.         
           05  PV-PARAGRAPH-NAME          PIC X(30)   VALUE SPACE.              
           05  PV-DB2-OPERATION-ATTEMPTED PIC X(30)   VALUE SPACE.              
           05  PV-RETRY-COUNT             PIC S9(04)  VALUE +0                  
                                                      COMP.                     
                                                                                
           05  PV-TOTAL-RECS-IN           PIC 9(11)   VALUE ZEROES.             
           05  PV-TOTAL-RECS-OUT          PIC 9(11)   VALUE ZEROES.             
W26600     05  PV-TOTAL-DSS-ADJ-EXT-INV1  PIC 9(11)   VALUE ZEROES.             
W26600     05  PV-TOTAL-DSS-ADJ-EXT-INV2  PIC 9(11)   VALUE ZEROES.             
                                                                                
           05  PV-TOTAL-RECS-IN-FMT       PIC ZZZZZZZZZZ9  VALUE SPACE.         
           05  PV-TOTAL-RECS-OUT-FMT      PIC ZZZZZZZZZZ9  VALUE SPACE.         
W26600     05  PV-TOTAL-DSS-ADJ-EXT-OUT-FMT                                     
W26600                                    PIC ZZZZZZZZZZ9  VALUE ZERO.          
                                                                                
W26602*    05  PV-PREV-STORE-NBR          PIC 9(03)   VALUE ZEROES.             
W26602     05  PV-PREV-STORE-NBR          PIC S9(04)  COMP.                     
                                                                                
           05  PV-STORE-DC-FLUSH-DATE     PIC X(10)   VALUE SPACE.              
                                                                                
           05  PV-PLND-INV-DATE           PIC X(10)   VALUE SPACE.              
                                                                                
           05  PV-ACTL-UNIT-BK-DATE       PIC X(10)   VALUE SPACE.              
                                                                                
           05  PV-VERIFIED-DATE.                                                
               10  PV-VERIFIED-CC         PIC X(02)   VALUE SPACE.              
               10  PV-VERIFIED-YY         PIC X(02)   VALUE SPACE.              
               10  FILLER                 PIC X       VALUE '-'.                
               10  PV-VERIFIED-MM         PIC X(02)   VALUE SPACE.              
               10  FILLER                 PIC X       VALUE '-'.                
               10  PV-VERIFIED-DD         PIC X(02)   VALUE SPACE.              
                                                                                
           05  PV-RECEIVED-DATE.                                                
               10  PV-RECEIVED-CC         PIC X(02)   VALUE SPACE.              
               10  PV-RECEIVED-YY         PIC X(02)   VALUE SPACE.              
               10  FILLER                 PIC X       VALUE '-'.                
               10  PV-RECEIVED-MM         PIC X(02)   VALUE SPACE.              
               10  FILLER                 PIC X       VALUE '-'.                
               10  PV-RECEIVED-DD         PIC X(02)   VALUE SPACE.              
                                                                                
RB0401     05  PV-MFSTNG-IND              PIC X(01)   VALUE SPACE.              
W21732                                                                          
W21732     05  PV-SKU-NBR                 PIC 9(08).                            
W21732                                                                          
W21732     05  PV-ITM-NBR                 PIC S9(15)V COMP-3.                   
                                                                                
       01  DK-DB2-KEYS.                                                         
W26602*    05  DK-STORE-NBR               PIC X(03)   VALUE SPACE.              
W26602     05  DK-STORE-NBR-X             PIC 9(04)   VALUE ZERO.               
W26602     05  DK-STORE-NBR               PIC S9(04)  COMP.                     
                                                                                
       01  ABEND-CODE                     PIC S9(04)  COMP SYNC                 
                                                      VALUE ZERO.               
                                                                                
      ******************************************************************        
      * WORKING STORAGE AREA FOR THE DAILY STORE EXTRACT FILE                   
      ******************************************************************        
                                                                                
           COPY STOREEXT.                                                       
                                                                                
      ******************************************************************        
      * WORKING STORAGE AREA FOR THE ADJUSTMENT OUTPUT FILE                     
      ******************************************************************        
                                                                                
W21732     COPY FSRD035.                                                        
                                                                                
      ******************************************************************01090000
      * CUTOFF DATABASE ADJUSTMENT EXTRACT OUTPUT FILE COPYBOOK         01100000
      ******************************************************************01110000
W26600     COPY INRD051.                                                01120000
                                                                        01130000
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * DB2 TABLE DCLGENS NEEDED FOR THIS PROGRAM.                              
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
W28545     EXEC SQL                                                             
W28545         INCLUDE TINCNTL                                                  
W28545     END-EXEC.                                                            
W28545                                                                          
W21732     EXEC SQL                                                             
W21732         INCLUDE TSKXREF                                                  
W21732     END-EXEC.                                                            
W21732                                                                          
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
            EJECT.                                                              
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
      *                                                                         
      ******************************************************************        
      * MAIN MODULE                                                             
      ******************************************************************        
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS-DAILY-FILE                                      
               UNTIL PS-END-OF-DAILY-FILE.                                      
                                                                                
           PERFORM 9000-END-OF-JOB.                                             
                                                                                
           GOBACK.                                                              
      *                                                                         
      ******************************************************************        
      * OPEN THE FILES USED IN THE PROGRAM, SET THE PREVIOUS STORE              
      * NUMBER TO '000' INITIALLY, AND READ THE FIRST RECORD OF THE             
      * DAILY EXTRACT FILE.                                                     
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  IN-DAILY-EXTRACT-FILE                                    
                OUTPUT FS-ADJUSTMENT-FILE                                       
                       IN-DSS-REPORT-FILE                                       
W26600                 IN-DSS-ADJUSTMENT-EXTRACT-FILE.                          
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'STATISTICS FOR PROGRAM ' PV-PROGRAM-NAME.                   
           DISPLAY ' '.                                                         
                                                                                
           MOVE ZEROES                      TO PV-PREV-STORE-NBR.               
                                                                                
           PERFORM 7000-READ-DAILY-EXTRACT-FILE.                                
                                                                                
      *                                                                         
      ******************************************************************        
      * PROCESS THE DAILY EXTRACT FILE                                          
      ******************************************************************        
       2000-PROCESS-DAILY-FILE.                                                 
                                                                                
W26602*    IF SE-STORE NOT EQUAL PV-PREV-STORE-NBR                              
W26602     IF SE-LOC-NBR NOT EQUAL PV-PREV-STORE-NBR                            
               PERFORM 6000-GET-TINVPAR-INFO                                    
           END-IF.                                                              
                                                                                
           MOVE SE-DV-MM               TO PV-VERIFIED-MM.                       
           MOVE SE-DV-DD               TO PV-VERIFIED-DD.                       
           MOVE SE-DV-YY               TO PV-VERIFIED-YY.                       
           IF SE-DV-YY > 80                                                     
               MOVE '19'               TO PV-VERIFIED-CC                        
           ELSE                                                                 
               MOVE '20'               TO PV-VERIFIED-CC                        
           END-IF.                                                              
                                                                                
           MOVE SE-DR-MM               TO PV-RECEIVED-MM.                       
           MOVE SE-DR-DD               TO PV-RECEIVED-DD.                       
           MOVE SE-DR-YY               TO PV-RECEIVED-YY.                       
           IF SE-DR-YY > 80                                                     
               MOVE '19'               TO PV-RECEIVED-CC                        
           ELSE                                                                 
               MOVE '20'               TO PV-RECEIVED-CC                        
           END-IF.                                                              
W21732                                                                          
W21732     IF SE-SKU-NBR (6:3) = '000' OR '999'                                 
W21732         MOVE ZEROES             TO PV-ITM-NBR                            
W21732     ELSE                                                                 
W21732         PERFORM 3000-GET-ITEM-NBR                                        
W21732     END-IF.                                                              
W21732                                                                          
RB0401     IF PS-STORE-ON-TINVPAR                                               
RB0401                                                                          
RB0401         IF PV-MFSTNG-IND = PC-YES                                        
RB0401                                                                          
RB0401             IF ( PV-VERIFIED-DATE > PV-PLND-INV-DATE ) AND               
RB0401                ( PV-RECEIVED-DATE <= PV-PLND-INV-DATE )                  
RB0401                                                                          
RB0401                 IF ( PV-VERIFIED-DATE > PV-PLND-INV-DATE ) AND           
RB0401                   (( PV-VERIFIED-DATE < PV-ACTL-UNIT-BK-DATE ) OR        
RB0401                    ( PV-ACTL-UNIT-BK-DATE = PC-NINES-DATE ))             
RB0401                     PERFORM 8100-WRITE-DSS-ADJUST2-RECORD                
RB0401                     PERFORM 8600-WRITE-DSS-REPORT-RECORD                 
RB0401                     ADD 1               TO PV-TOTAL-RECS-OUT             
RB0401                 END-IF                                                   
RB0401                                                                          
RB0401                 IF ( PV-VERIFIED-DATE >= PV-ACTL-UNIT-BK-DATE )          
RB0401                     PERFORM 8200-WRITE-DSS-ADJUST3-RECORD                
RB0401                     PERFORM 8600-WRITE-DSS-REPORT-RECORD                 
RB0401                     ADD 1               TO PV-TOTAL-RECS-OUT             
RB0401                 END-IF                                                   
RB0401                                                                          
RB0401             END-IF                                                       
RB0401                                                                          
RB0401         ELSE                                                             
RB0401                                                                          
W33304           IF ( INVPAR-SCHD-PHY-CNT-CDE = ('PI' OR 'CL') AND              
W27808                INVPAR-SCAN-INV-IND = 'Y')                                
W27808                                                                          
                   IF ( PV-VERIFIED-DATE > PV-STORE-DC-FLUSH-DATE ) AND         
                      ( PV-RECEIVED-DATE <= PV-STORE-DC-FLUSH-DATE )            
                                                                                
                       IF ( PV-VERIFIED-DATE <= PV-PLND-INV-DATE ) AND          
                         (( PV-VERIFIED-DATE < PV-ACTL-UNIT-BK-DATE ) OR        
                          ( PV-ACTL-UNIT-BK-DATE = PC-NINES-DATE ))             
                           PERFORM 8000-WRITE-DSS-ADJUST1-RECORD                
                           PERFORM 8600-WRITE-DSS-REPORT-RECORD                 
                           ADD 1               TO PV-TOTAL-RECS-OUT             
                       END-IF                                                   
                                                                                
                       IF ( PV-VERIFIED-DATE > PV-PLND-INV-DATE ) AND           
                         (( PV-VERIFIED-DATE < PV-ACTL-UNIT-BK-DATE ) OR        
                          ( PV-ACTL-UNIT-BK-DATE = PC-NINES-DATE ))             
                           PERFORM 8100-WRITE-DSS-ADJUST2-RECORD                
                           PERFORM 8600-WRITE-DSS-REPORT-RECORD                 
                           ADD 1               TO PV-TOTAL-RECS-OUT             
                       END-IF                                                   
                                                                                
                       IF ( PV-VERIFIED-DATE >= PV-ACTL-UNIT-BK-DATE )          
                           PERFORM 8200-WRITE-DSS-ADJUST3-RECORD                
                           PERFORM 8600-WRITE-DSS-REPORT-RECORD                 
                           ADD 1               TO PV-TOTAL-RECS-OUT             
                       END-IF                                                   
                                                                                
                   END-IF                                                       
                                                                                
W27808           END-IF                                                         
                                                                                
RB0401         END-IF                                                           
RB0401                                                                          
RB0401     END-IF.                                                              
                                                                                
           ADD 1                       TO PV-TOTAL-RECS-IN.                     
                                                                                
W26602     MOVE SE-LOC-NBR             TO PV-PREV-STORE-NBR.                    
                                                                                
           PERFORM 7000-READ-DAILY-EXTRACT-FILE.                                
                                                                                
      *                                                                         
W21732******************************************************************        
W21732*  GET THE ITEM NUMBER FROM TSKXREF TABLE.                       *        
W21732******************************************************************        
W21732 3000-GET-ITEM-NBR.                                                       
W21732                                                                          
W21732         MOVE SE-SKU-NBR           TO PV-SKU-NBR.                         
W21732         MOVE PV-SKU-NBR           TO SKXREF-SKU.                         
W21732                                                                          
W21732     PERFORM                                                              
W21732         WITH TEST AFTER                                                  
W21732         VARYING PV-RETRY-COUNT                                           
W21732         FROM 1 BY 1                                                      
W21732         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
W21732                   OR (SQLCODE = ZERO)                                    
W21732                   OR (SQLCODE = +100))                                   
W21732         EXEC SQL                                                         
W21732           SELECT ITM_NBR                                                 
W21732             INTO :SKXREF-ITM-NBR                                         
W21732           FROM TSKXREF                                                   
W21732           WHERE SKU    = :SKXREF-SKU                                     
W21732         END-EXEC                                                         
W21732                                                                          
W21732         EVALUATE TRUE                                                    
W21732             WHEN SQLCODE = ZERO                                          
W21732                MOVE SKXREF-ITM-NBR  TO PV-ITM-NBR                        
W21732             WHEN SQLCODE = +100                                          
W21732                MOVE ZEROES          TO PV-ITM-NBR                        
W21732             WHEN SQLCODE = -911                                          
W21732             WHEN SQLCODE = -913                                          
W21732                CONTINUE                                                  
W21732             WHEN SQLWARN0 NOT = SPACES                                   
W21732             WHEN SQLCODE < ZERO                                          
W21732             WHEN SQLCODE > ZERO                                          
W21732                MOVE '3000-GET-ITEM-NBR'                                  
W21732                                   TO PV-PARAGRAPH-NAME                   
W21732                MOVE 'SELECT A ROW ON THE TSKXREF TABLE'                  
W21732                                   TO PV-DB2-OPERATION-ATTEMPTED          
W21732                PERFORM 9999-SQL-ABEND                                    
W21732          END-EVALUATE                                                    
W21732      END-PERFORM.                                                        
W21732                                                                          
W21732      IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                 
W21732          MOVE '3000-GET-ITEM-NBR' TO PV-PARAGRAPH-NAME                   
W21732                                                                          
W21732          MOVE 'MAX RETRIES EXCEEDED ON TSKXREF TABLE'                    
W21732                                   TO PV-DB2-OPERATION-ATTEMPTED          
W21732          PERFORM 9999-SQL-ABEND                                          
W21732      END-IF.                                                             
W21732                                                                          
      ******************************************************************        
      *  GET THE STORE'S DC FLUSH DATE FROM THE TINVPAR TABLE.         *        
      *  THIS DATE WILL BE COMPARED AGAINST THE RECEIVED AND           *        
      *  VERIFIED DATES ON THE RECORD FOR THAT STORE.                  *        
      ******************************************************************        
       6000-GET-TINVPAR-INFO.                                                   
                                                                                
W26602     MOVE SE-LOC-NBR         TO DK-STORE-NBR.                             
                                                                                
           MOVE SPACES             TO INVPAR-PLND-INV-DTE                       
                                      INVPAR-ACTL-UNIT-BK-DTE                   
                                      INVPAR-ACTL-DC-CTOFF-DTE                  
W27808                                INVPAR-SCHD-PHY-CNT-CDE                   
W27808                                INVPAR-SCAN-INV-IND                       
                                      PV-PLND-INV-DATE                          
                                      PV-ACTL-UNIT-BK-DATE                      
RB0401                                PV-STORE-DC-FLUSH-DATE                    
RB0401                                PV-MFSTNG-IND.                            
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT                                           
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
                         OR (SQLCODE = ZERO)                                    
                         OR (SQLCODE = +100))                                   
               EXEC SQL                                                         
                   SELECT PLND_INV_DTE                                          
                         ,ACTL_UNIT_BK_DTE                                      
                         ,ACTL_DC_CTOFF_DTE                                     
RB0401                   ,MFSTNG_IND                                            
W27808                   ,SCHD_PHY_CNT_CDE                                      
W27808                   ,SCAN_INV_IND                                          
                   INTO  :INVPAR-PLND-INV-DTE                                   
                        ,:INVPAR-ACTL-UNIT-BK-DTE                               
                        ,:INVPAR-ACTL-DC-CTOFF-DTE                              
RB0401                  ,:INVPAR-MFSTNG-IND                                     
W27808                  ,:INVPAR-SCHD-PHY-CNT-CDE                               
W27808                  ,:INVPAR-SCAN-INV-IND                                   
                   FROM                                                         
W28545                    TINVPAR A                                             
W28545                   ,TINCNTL B                                             
                   WHERE                                                        
W26602                    LOC_NBR           = :DK-STORE-NBR                     
W33304               AND  A.LOC_INV_STAT_CDE = 'IN'                             
W28545               AND  A.INV_ID          = B.INV_ID                          
W28545               AND  B.ACTV_IND        = 'Y'                               
                     AND  PLND_INV_DTE      =                                   
                          ( SELECT MAX (PLND_INV_DTE)                           
W28545                      FROM   TINVPAR C                                    
W28545                            ,TINCNTL D                                    
W26602                        WHERE  LOC_NBR   = :DK-STORE-NBR                  
W33304                        AND    C.LOC_INV_STAT_CDE = 'IN'                  
W28545                        AND    C.INV_ID  = D.INV_ID                       
W28545                        AND    D.ACTV_IND = 'Y'                           
                              AND ACTL_FIN_BK_DTE = :PC-NINES-DATE)             
               END-EXEC                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       SET PS-STORE-ON-TINVPAR                                  
                                         TO TRUE                                
                       MOVE INVPAR-PLND-INV-DTE                                 
                                         TO PV-PLND-INV-DATE                    
                       MOVE INVPAR-ACTL-UNIT-BK-DTE                             
                                         TO PV-ACTL-UNIT-BK-DATE                
                       MOVE INVPAR-ACTL-DC-CTOFF-DTE                            
                                         TO PV-STORE-DC-FLUSH-DATE              
RB0401                 MOVE INVPAR-MFSTNG-IND                                   
RB0401                                   TO PV-MFSTNG-IND                       
                   WHEN SQLCODE = +100                                          
                       SET PS-STORE-NOT-ON-TINVPAR                              
                                         TO TRUE                                
                       MOVE DK-STORE-NBR TO DK-STORE-NBR-X                      
                       DISPLAY '**************************************'         
                       DISPLAY '*         ' DK-STORE-NBR-X                      
                       DISPLAY '* STORE NOT FOUND ON TINVPAR '                  
                       DISPLAY '*    OR    IS NOT DOING INVENTORY  '            
                       DISPLAY '**************************************'         
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '6000-GET-TINVPAR-INFO'                             
                                         TO PV-PARAGRAPH-NAME                   
                       MOVE 'SELECT A ROW ON THE TINVPAR TABLE'                 
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE '6000-GET-TINVPAR-INFO'                                     
                                         TO PV-PARAGRAPH-NAME                   
               MOVE 'MAX RETRIES EXCEEDED ON TINVPAR TABLE'                     
                                         TO PV-DB2-OPERATION-ATTEMPTED          
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *                                                                         
      ******************************************************************        
      *  READ THE DAILY EXTRACT FILE.                                  *        
      ******************************************************************        
       7000-READ-DAILY-EXTRACT-FILE.                                            
                                                                                
           READ IN-DAILY-EXTRACT-FILE                                           
               INTO STORE-EXTRACT-LINE                                          
               AT END                                                           
                   SET PS-END-OF-DAILY-FILE TO TRUE                             
           END-READ.                                                            
                                                                                
      *                                                                         
      ******************************************************************        
W26600*  WRITE AN 'INV1' INVENTORY CUTOFF ADJUSTMENT RECORD.           *        
      ******************************************************************        
       8000-WRITE-DSS-ADJUST1-RECORD.                                           
                                                                                
W26600     MOVE SE-LOC-NBR             TO  IN051-LOC-NBR.                  CL*12
W26600     MOVE 'INV1'                 TO  IN051-TRANS-TYPE.               CL*12
W26600     MOVE PV-VERIFIED-DATE       TO  IN051-VERIFIED-DATE.                 
W26600     MOVE SE-RETAIL-UNITS-MARK   TO  IN051-QUANTITY.                 CL*17
W26600     MOVE SE-SKU-NBR             TO  IN051-SKU-NBR.                       
W26600     ADD +1 TO PV-TOTAL-DSS-ADJ-EXT-INV1.                                 
                                                                                
W26600     PERFORM 8700-WRITE-DSS-ADJ-EXTR-RECORD.                              
                                                                                
      *                                                                         
      ******************************************************************        
W26600*  WRITE AN 'INV2' INVENTORY CUTOFF ADJUSTMENT RECORD.           *        
      ******************************************************************        
       8100-WRITE-DSS-ADJUST2-RECORD.                                           
                                                                                
W26600     MOVE SE-LOC-NBR             TO  IN051-LOC-NBR.                  CL*12
W26600     MOVE 'INV2'                 TO  IN051-TRANS-TYPE.               CL*12
W26600     MOVE PV-VERIFIED-DATE       TO  IN051-VERIFIED-DATE.                 
W26600     MOVE SE-RETAIL-UNITS-MARK   TO  IN051-QUANTITY.                 CL*17
W26600     MOVE SE-SKU-NBR             TO  IN051-SKU-NBR.                       
W26600     ADD +1 TO PV-TOTAL-DSS-ADJ-EXT-INV2.                                 
                                                                                
W26600     PERFORM 8700-WRITE-DSS-ADJ-EXTR-RECORD.                              
                                                                                
      *                                                                         
      ******************************************************************        
      *  WRITE AN ADJUSTMENT RECORD WHICH WILL BE PULLED INTO THE      *        
      *  NEXT RUN OF THE RECEIPTS/AJDUSTMENT POSTING PROCESS.          *        
      ******************************************************************        
       8200-WRITE-DSS-ADJUST3-RECORD.                                           
                                                                                
W21732     MOVE SPACES               TO FS035-ADJUSTMENT-RECORD.                
                                                                                
W21732     MOVE 'INKUT154'             TO  FS035-ADJ-SRC-PGM-NM.           CL*12
W21732     MOVE PV-ITM-NBR             TO  FS035-ITM-NBR.                  CL*12
W26602     MOVE SE-LOC-NBR             TO  FS035-LOC-NBR.                  CL*12
W21732     SET  FS035-OH-ADJ-ONLY      TO  TRUE.                           CL*12
W21732     SET  FS035-ACTN-NEG         TO  TRUE.                           CL*12
W21732     MOVE SE-RETAIL-UNITS-MARK   TO  FS035-ADJ-QTY.                  CL*17
W21732     MOVE 'INV3'                 TO  FS035-BTCH-NBR.                 CL*12
W21732     MOVE 'Y'                    TO  FS035-UNT-BKG-IND.              CL*17
                                                                                
           PERFORM 8500-WRITE-ADJ-REC-TO-FILE.                                  
                                                                                
      *                                                                         
      ******************************************************************        
      *  WRITE A RECORD TO THE ADJUSTMENT FILE.                        *        
      ******************************************************************        
       8500-WRITE-ADJ-REC-TO-FILE.                                              
                                                                                
           WRITE FS-ADJUSTMENT-REC                                              
W21732         FROM FS035-ADJUSTMENT-RECORD.                                    
                                                                                
      *                                                                         
      ******************************************************************        
      *  WRITE A RECORD TO THE DSS REPORT FILE.                        *        
      ******************************************************************        
       8600-WRITE-DSS-REPORT-RECORD.                                            
                                                                                
           MOVE PV-ACTL-UNIT-BK-DATE TO SE-ACTL-UNIT-BK-DATE.                   
                                                                                
           WRITE IN-DSS-REPORT-REC                                              
               FROM STORE-EXTRACT-LINE.                                         
                                                                                
      *                                                                         
      ******************************************************************        
      *  WRITE A RECORD TO THE DSS ADJUSTMENT EXTRACT FILE.            *        
      ******************************************************************        
W26600 8700-WRITE-DSS-ADJ-EXTR-RECORD.                                          
                                                                                
W26600     WRITE IN-DSS-ADJUSTMENT-EXTRACT-REC                                  
W26600         FROM IN051-CTOFF-ADJ-EXTR-RECORD.                                
                                                                                
      *                                                                         
      ******************************************************************        
      * CLOSE THE FILES USED IN THE PROGRAM.                                    
      ******************************************************************        
       9000-END-OF-JOB.                                                         
                                                                                
           MOVE PV-TOTAL-RECS-IN     TO PV-TOTAL-RECS-IN-FMT.                   
           MOVE PV-TOTAL-RECS-OUT    TO PV-TOTAL-RECS-OUT-FMT.                  
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '***********************************************'            
           DISPLAY '* TOTAL NUMBER OF INPUT RECS ---> '                         
                PV-TOTAL-RECS-IN-FMT ' *'.                                      
           DISPLAY '* TOTAL NUMBER OF OUTPUT RECS --> '                         
                PV-TOTAL-RECS-OUT-FMT ' *'.                                     
W26600     MOVE PV-TOTAL-DSS-ADJ-EXT-INV1                                       
W26600                               TO PV-TOTAL-DSS-ADJ-EXT-OUT-FMT.           
W26600     DISPLAY '* TOTAL DSS ADJ EXTR INV1 RECS -> '                         
W26600          PV-TOTAL-DSS-ADJ-EXT-OUT-FMT ' *'.                              
W26600     MOVE PV-TOTAL-DSS-ADJ-EXT-INV2                                       
W26600                               TO PV-TOTAL-DSS-ADJ-EXT-OUT-FMT.           
W26600     DISPLAY '* TOTAL DSS ADJ EXTR INV2 RECS -> '                         
W26600          PV-TOTAL-DSS-ADJ-EXT-OUT-FMT ' *'.                              
           DISPLAY '***********************************************'            
           DISPLAY ' '.                                                         
                                                                                
           CLOSE IN-DAILY-EXTRACT-FILE                                          
                 FS-ADJUSTMENT-FILE                                             
                 IN-DSS-REPORT-FILE                                             
W26600           IN-DSS-ADJUSTMENT-EXTRACT-FILE.                                
                                                                                
      *                                                                         
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                  TO ABEND-CODE.                           
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPERATION-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
