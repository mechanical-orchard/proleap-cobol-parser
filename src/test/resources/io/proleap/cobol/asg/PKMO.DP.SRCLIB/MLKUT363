       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.              MLKUT363.                               00020000
       AUTHOR.                  THERESE.RAMSEYER.                       00030000
       INSTALLATION.            KOHLS DEPARTMENT STORES.                00040000
       DATE-WRITTEN.            10-23-2009.                             00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      *                 EXTRACT EXPENSE VENDORS                        *00080000
      ******************************************************************00090000
      *  THIS PROGRAM WILL EXTRACT EXPENSE VENDORS FROM THE DB2 TABLE  *00100000
      *  MLT_VND_ALLOC TO A FLAT FILE. THERE ARE 2 INPUT FILES, ONE IS *00110000
      *  MANDATORY AND REQUIRES A VALID FISCAL WEEK END DATE, AND THE  *00120000
      *  OTHER ALLOWS USER DEFINED EXPENSE VENDOR TYPES FOR A FILTER.  *00130000
      *  =================                                             *00144000
      *  I M P O R T A N T !!!                                         *00145000
      *  =================                                             *00146000
      *  THIS PROGRAM CURRENTLY HANDLES THESE 3 USER DEFINED SCENARIOS:*00147000
      *  _____________________________________________________________ *00147100
      *  01,02      FOR FREIGHT, SALVAGE                               *00148000
      *  03         FOR LICENSE FEE                                    *00149000
      *  01,02,03   FOR FREIGHT, SALVAGE & LICENSE FEE                 *00149100
      *                                                                *00149200
      *  IF INPUT 2 IS EMPTY, THEN THE DEFAULT WILL BE TO FILTER       *00149300
      *  EXP_TYP_NBRS 01 THRU 10 FROM MLT_VND_ALLOC AS THESE ARE       *00149400
      *  VARIABLES INITIALIZED IN WORKING STORAGE.                    * 00149500
      *                                                                *00149600
      *  FOR FUTURE EXPANSION,                                         *00149700
      *  UP TO 10 EXP_TYP_NBR 'S MAY BE ADDED ACCORDING TO THE         *00151000
      *  FOLLOWING FORMAT:                                             *00152000
      *  01,02,03,04,05,06,07,08,09,10                                 *00153000
      *  UP TO FIRST 29 BYTES USER DEFINED,THEN REST FILLER SPACE TO 80*00154000
      *  CODE WILL NEED TO BE ADDED TO THIS PROGRAM TO ACCOMMODATE     *00155000
      *  NEW EXP_TYP_NBR SCENARIOS.                                    *00156000
      *  ANY NEW SCENARIO CONTAINING EXP_TYP_NBR = 03 MUST SET THE     *00159000
      *  PS-LICENSE-FEE SWITCH IN ORDER TO PROPERLY INCREMENT THE      *00159100
      *  ML363-GRP-ID.                                                  00159200
      *  WHEN EXP_TYP_NBR = +3, THE OUTPUT ML363-GRP-ID SHOULD BE      *00159500
      *  INCREMENTED BY +1. IT IS INITIALIZED TO +0 IN WORKING STORAGE.*00159600
      ******************** HISTORY OF CHANGES **************************00160000
      * CHG ID/                                                        *00170000
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *00180000
      * -------  ----------  ----------------------------------------  *00190000
      * WR37965  2009-10-23  INITIAL IMPLEMENTATION.                   *00200000
      *                      THERESE RAMSEYER.                         *00210000
      ******************************************************************00220000
                                                                        00230000
       ENVIRONMENT DIVISION.                                            00240000
       CONFIGURATION SECTION.                                           00250000
       INPUT-OUTPUT SECTION.                                            00260000
       FILE-CONTROL.                                                    00270000
                                                                        00280000
           SELECT DATE-FILE                                             00290000
               ASSIGN TO INP01.                                         00300000
                                                                        00310000
           SELECT ENT-TYPE-FILE                                         00320000
               ASSIGN TO INP02.                                         00330000
                                                                        00340000
           SELECT EXPENSE-VENDOR-FILE                                   00350000
               ASSIGN TO OUT01.                                         00360000
                                                                        00370000
       DATA DIVISION.                                                   00380000
       FILE SECTION.                                                    00390000
                                                                        00400000
       FD  DATE-FILE                                                    00410000
           RECORDING MODE IS F                                          00420000
           LABEL RECORDS ARE STANDARD                                   00430000
           BLOCK CONTAINS 0 RECORDS.                                    00440000
       01  DATE-REC                        PIC X(80).                   00450000
                                                                        00460000
       FD  ENT-TYPE-FILE                                                00470000
           RECORDING MODE IS F                                          00480000
           LABEL RECORDS ARE STANDARD                                   00490000
           BLOCK CONTAINS 0 RECORDS.                                    00500000
       01  ENT-TYPE-REC                    PIC X(80).                   00510000
                                                                        00520000
       FD  EXPENSE-VENDOR-FILE                                          00530000
           RECORDING MODE IS F                                          00540000
           LABEL RECORDS ARE STANDARD                                   00550000
           BLOCK CONTAINS 0 RECORDS.                                    00560000
       01  EXPENSE-VENDOR-REC              PIC X(80).                   00570000
                                                                        00580000
       WORKING-STORAGE SECTION.                                         00590000
                                                                        00600000
       01  FILLER                          PIC X(40)       VALUE        00610000
           '*** WORKING STORAGE AREA BEGIN ***'.                        00620000
                                                                        00630000
      ***************************************************************** 00640000
      *    DATE CONTROL CARD                                            00650000
      ***************************************************************** 00660000
       01  CC-DATE-CONTROL-CARD.                                        00670000
           05  FILLER                    PIC  X(03).                    00680000
           05  CC-FSCL-WK-END-DTE        PIC  X(10).                    00690000
           05  FILLER                    PIC  X(67).                    00700000
                                                                        00710000
      ***************************************************************** 00720000
      *    USER DEFINED ENT TYP NBR FOR FILTER                          00730000
      ***************************************************************** 00740000
       01  CC-ENT-TYP-CONTROL-CARD.                                     00750000
           05  CC-ENT-TYP                PIC  X(29).                    00770000
           05  FILLER                    PIC  X(51).                    00780000
                                                                        00790000
       01  FILLER                          PIC X(40)       VALUE        00800000
           '*** PROGRAMMING CONSTANT BEGIN ***'.                        00810000
                                                                        00820000
       01  PC-PROGRAM-CONSTANTS-AREA.                                   00830000
           05  PC-MAX-RETRIES             PIC S9(03) VALUE +3 COMP-3.   00840000
           05  PC-PROGRAM-NAME            PIC X(08)  VALUE 'MLKUT363'.  00850000
           05  PC-DEFAULT-NINES           PIC X(10)  VALUE '9999-12-31'.00851000
           05  PC-FRGT-SALV               PIC X(05)  VALUE '01,02'.     00860000
           05  PC-LICENSE-FEE             PIC X(03)  VALUE '03'.        00870000
           05  PC-FRGT-SALV-LICENSE-FEE   PIC X(08)  VALUE '01,02,03'.  00880000
                                                                        00890000
       01  FILLER                          PIC X(40)       VALUE        00900000
           '*** PROGRAMMING SWITCHES BEGIN ***'.                        00910000
                                                                        00920000
       01  PS-SWITCHES-AREA.                                            00930000
           05  PS-END-OF-DATE-FILE-SW      PIC X           VALUE 'N'.   00940000
               88  PS-END-OF-DATE-FILE                     VALUE 'Y'.   00950000
               88  PS-NOT-END-OF-DATE-FILE                 VALUE 'N'.   00960000
           05  PS-ENT-TYPE-FILE-SW         PIC X           VALUE 'N'.   00970000
               88  PS-END-OF-ENT-TYPE-FILE                 VALUE 'Y'.   00980000
               88  PS-NOT-END-OF-ENT-TYPE-FILE             VALUE 'N'.   00990000
           05  PS-END-OF-VENDOR-CSR-SW     PIC X           VALUE 'N'.   01020000
               88  PS-NOT-END-OF-VENDOR-CSR                VALUE 'N'.   01030000
               88  PS-END-OF-VENDOR-CSR                    VALUE 'Y'.   01040000
           05  PS-VENDOR-OPTIONS-SW        PIC  S9(4) COMP VALUE +0.    01041000
               88  PS-LICENSE-FEE                          VALUE +3.    01042000
           05  PS-LF-FOUND-SW              PIC X           VALUE 'N'.   01043009
               88  PS-LF-FOUND                             VALUE 'Y'.   01044009
               88  PS-LF-NOT-FOUND                         VALUE 'N'.   01045009
                                                                        01050000
       01  FILLER                          PIC X(40)       VALUE        01060000
           '*** REPORT REQUEST CONTROL REC ***'.                        01070000
                                                                        01080000
       01  FILLER                          PIC X(40)       VALUE        01090000
           '*** PROGRAMMING VARIABLE BEGIN ***'.                        01100000
                                                                        01110000
       01  PV-PROGRAM-VARIABLES-AREA.                                   01120000
           05  PV-RETRY-COUNTER            PIC S9(04) COMP VALUE +0.    01130000
           05  PV-GRP-ID-COUNTER           PIC S9(04) COMP VALUE +0.    01140000
           05  PV-DTE-IS-VALID             PIC X(01)    VALUE SPACES.   01141000
           05  PV-OPERATION-MSG-1          PIC X(30)    VALUE SPACES.   01220000
           05  PV-OPERATION-MSG-2          PIC X(30)    VALUE SPACES.   01230000
           05  PV-PARAGRAPH-NAME           PIC X(30)    VALUE SPACES.   01240000
           05  PV-PREV-ENT-ID              PIC S9(09) COMP VALUE +0.    01241021
           05  PV-EXP-TYPE1                PIC S9(4) COMP VALUE +1.     01250000
           05  PV-EXP-TYPE2                PIC S9(4) COMP VALUE +2.     01260000
           05  PV-EXP-TYPE3                PIC S9(4) COMP VALUE +3.     01270000
           05  PV-EXP-TYPE4                PIC S9(4) COMP VALUE +4.     01280000
           05  PV-EXP-TYPE5                PIC S9(4) COMP VALUE +5.     01290000
           05  PV-EXP-TYPE6                PIC S9(4) COMP VALUE +6.     01300000
           05  PV-EXP-TYPE7                PIC S9(4) COMP VALUE +7.     01310000
           05  PV-EXP-TYPE8                PIC S9(4) COMP VALUE +8.     01320000
           05  PV-EXP-TYPE9                PIC S9(4) COMP VALUE +9.     01330000
           05  PV-EXP-TYPE10               PIC S9(4) COMP VALUE +10.    01340000
                                                                        01350000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   01360000
           05  PE-MSG-01                       PIC X(30) VALUE          01370000
                'FISCAL WEEK END DTE REQUIRED  '.                       01380000
           05  PE-MSG-02                       PIC X(30) VALUE          01390000
                'INPUT FILE IS EMPTY           '.                       01400000
           05  PE-MSG-03                       PIC X(30) VALUE          01401010
                'LICENSE FEES INTERNAL TABLE   '.                       01402010
           05  PE-MSG-04                       PIC X(30) VALUE          01403010
                'LIMIT EXCEEDED                '.                       01404010
                                                                        01410000
       01  FILLER                          PIC X(40)       VALUE        01420000
           '*** PROGRAMMING ACCUMULATORS BEGIN ***'.                    01430000
                                                                        01440000
       01  PA-PROGRAM-ACCUMULATORS.                                     01450000
           05  PA-TOTALS.                                               01460000
               10 PA-TOT-CSR-FETCHES        PIC S9(09) VALUE +0 COMP-3. 01470000
               10 PA-TOT-RECS-WRITTEN       PIC S9(09) VALUE +0 COMP-3. 01480000
                                                                        01490000
       01  PT-LIC-FEES-LBL-TBL.                                         01491010
           05  PT-LF-INDX-MAX              PIC S9(05)      VALUE +1000. 01492009
           05  PT-LIC-FEES OCCURS 1000 TIMES                            01493009
                                           INDEXED BY PT-LF-INDX        01494009
                                                      PT-LF-INDX-TOT.   01496009
               10  PT-LF-LBL-SEQ-NBR       PIC S9(04) COMP   VALUE +0.  01499110
               10  PT-LF-GROUP-ID          PIC  9(04)        VALUE  0.  01499210
                                                                        01499609
       01  FILLER                          PIC X(40)       VALUE        01500000
           '*** SQL WORK AREA BEGIN ***'.                               01510000
                                                                        01520000
      ***************************************************************** 01530000
      *  SQL ABEND ROUTINE WORKING STORAGE                              01540000
      ***************************************************************** 01550000
      *01  DP004-DB2-ERROR-FIELDS.                                      01560000
           COPY DPWS004.                                                01570000
                                                                        01580000
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES 01590000
                               COMP SYNC.                               01600000
           88  ABEND-4003                                  VALUE +4003. 01610000
                                                                        01620000
           EXEC SQL                                                     01630000
               INCLUDE SQLCA                                            01640000
           END-EXEC.                                                    01650000
                                                                        01660000
      ***************************************************************** 01670000
      *    MLT_VND_ALLOC  - ML'S VENDOR ALLOCATION TABLE              * 01680000
      ***************************************************************** 01690000
           EXEC SQL                                                     01700000
               INCLUDE MLT00025                                         01710000
           END-EXEC.                                                    01720000
                                                                        01730000
      ***************************************************************** 01731000
      *    TDTATTR - DATE ATTRIBUTE TABLE                             * 01732000
      ***************************************************************** 01733000
           EXEC SQL                                                     01734000
               INCLUDE TDTATTR                                          01735000
           END-EXEC.                                                    01736000
                                                                        01737000
      ***************************************************************** 01740000
      *    VENDOR_CSR CURSOR                                          * 01750000
      ***************************************************************** 01760000
                                                                        01770000
           EXEC SQL   DECLARE VENDOR_CSR CURSOR FOR                     01780000
               SELECT ENT_ID                                            01790000
                     ,EXP_TYP_NBR                                       01800000
                     ,LBL_SEQ_NBR                                       01801014
                 FROM MLT_VND_ALLOC                                     01810000
                WHERE EXP_TYP_NBR IN (:PV-EXP-TYPE1, :PV-EXP-TYPE2,     01820000
                                      :PV-EXP-TYPE3, :PV-EXP-TYPE4,     01830000
                                      :PV-EXP-TYPE5, :PV-EXP-TYPE6,     01840000
                                      :PV-EXP-TYPE7, :PV-EXP-TYPE8,     01850000
                                      :PV-EXP-TYPE9, :PV-EXP-TYPE10)    01860000
                  AND (   (:CC-FSCL-WK-END-DTE BETWEEN EFF_BEG_DTE AND  01870000
                                                  EFF_END_DTE)          01880000
                    OR    (EFF_BEG_DTE  <= :CC-FSCL-WK-END-DTE          01881000
                           AND EFF_END_DTE = :PC-DEFAULT-NINES)  )      01882000
             GROUP BY ENT_ID                                            01890000
                     ,EXP_TYP_NBR                                       01900000
                     ,LBL_SEQ_NBR                                       01901015
            ORDER BY  ENT_ID                                            01910000
                     ,EXP_TYP_NBR                                       01920000
                     ,LBL_SEQ_NBR                                       01921015
                 WITH UR                                                01930000
           END-EXEC.                                                    01940000
                                                                        01950000
      ******************************************************************01960000
      * OUTPUT LATOUT -  EXPENSE VENDOR FILE                            01970000
      ******************************************************************01980000
           COPY MLRD363.                                                01990000
      ******************************************************************02000000
                                                                        02010000
       01  FILLER                          PIC X(40)       VALUE        02020000
           '*** WORKING STORAGE END ***'.                               02030000
                                                                        02040000
       PROCEDURE DIVISION.                                              02060000
                                                                        02070000
       0000-MAIN-MODULE.                                                02080000
      ******************************************************************02090000
      *  MAIN PROCESSING.                                               02100000
      ******************************************************************02110000
                                                                        02120000
           PERFORM 1000-INITIALIZATION.                                 02130000
                                                                        02140000
           PERFORM 7100-FETCH-VENDOR-CSR                                02150000
               UNTIL PS-END-OF-VENDOR-CSR.                              02160000
                                                                        02170000
           PERFORM 7200-CLOSE-VENDOR-CSR.                               02171000
                                                                        02172000
           PERFORM 8000-EOJ-ROUTINE.                                    02180000
           PERFORM 8100-DISPLAY-SUMMARY-STATS.                          02190000
                                                                        02200000
           STOP RUN.                                                    02210000
                                                                        02220000
      ******************************************************************02230000
      * OPEN FILES, INITIALIZE VARIABLES, READ INPUTS, OPEN CURSOR      02240000
      ******************************************************************02260000
       1000-INITIALIZATION.                                             02270000
                                                                        02280000
           OPEN INPUT  DATE-FILE                                        02290000
                       ENT-TYPE-FILE.                                   02300000
           OPEN OUTPUT EXPENSE-VENDOR-FILE.                             02310000
                                                                        02320000
           INITIALIZE ML363-VNDR-REC.                                   02321000
           MOVE 0 TO ML363-GRP-ID.                                      02321100
           SET PT-LF-INDX-TOT TO 1.                                     02321313
                                                                        02322000
           PERFORM 5000-READ-DATE-FILE.                                 02330000
           PERFORM 5100-READ-ENT-TYPE-FILE.                             02340000
           PERFORM 7000-OPEN-VENDOR-CSR.                                02350000
                                                                        02360000
      ******************************************************************02380000
      *    READ INPUT 1 FISCAL WEEK END DATE FROM  CONTROL CARD         02390000
      *    A VALID FISCAL WEEK END DATE ID REQUIRED                     02391000
      ******************************************************************02400000
       5000-READ-DATE-FILE.                                             02410000
                                                                        02420000
           READ DATE-FILE                                               02430000
               INTO CC-DATE-CONTROL-CARD                                02440000
                 AT END                                                 02441000
                    MOVE '5000-READ-DATE-FILE' TO PV-PARAGRAPH-NAME     02450000
                    MOVE  PE-MSG-01            TO PV-OPERATION-MSG-1    02460000
                    MOVE  PE-MSG-02            TO PV-OPERATION-MSG-2    02470000
                    PERFORM 9997-NON-SQL-ABEND                          02480000
                    SET PS-END-OF-DATE-FILE TO TRUE                     02482000
                 NOT AT END                                             02490000
                    PERFORM 6000-VALIDATE-DATE                          02500000
           END-READ.                                                    02520000
                                                                        02521000
      ******************************************************************02530000
      *    READ ENT-TYP-NBR WHICH ARE USER-DEFINED AND OPTIONAL - IF THE02540000
      *    FILE IS EMPTY - THE DEFAULT WILL BE 01 THRU 10               02541000
      *    01,02,03,04,05,06,07,08,09,10 TO FILTER EXP_TYP_NBRS FROM THE02542000
      *    MLT_VND_ALLOC TABLE.                                         02543000
      ******************************************************************02550000
       5100-READ-ENT-TYPE-FILE.                                         02560000
                                                                        02570000
           READ ENT-TYPE-FILE                                           02580000
               INTO CC-ENT-TYP-CONTROL-CARD                             02590000
               AT END SET PS-END-OF-ENT-TYPE-FILE TO TRUE               02591000
               DISPLAY 'NO USER DEFINED ENT TYPES - DEFAULTS USED'      02592000
               NOT AT END                                               02600000
                    PERFORM 6100-SET-UP-VARIABLES                       02610000
           END-READ.                                                    02620000
                                                                        02630000
      ****************************************************************  02640000
      * MAKE SURE THE DATE SUPPLIED IS A VALID FISCAL WEEK END DATE     02650000
      * FOUND ON TDTATTR.                                               02660000
      ****************************************************************  02670000
       6000-VALIDATE-DATE.                                              02680000
                                                                        02690000
           PERFORM                                                      02700000
               WITH TEST AFTER                                          02710000
               VARYING PV-RETRY-COUNTER                                 02720000
               FROM 1 BY 1                                              02730000
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               02740000
                         OR (SQLCODE = ZERO))                           02750000
                  EXEC SQL                                              02760000
                      SELECT 'Y'                                        02770000
                        INTO :PV-DTE-IS-VALID                           02780000
                        FROM TDTATTR                                    02790000
                       WHERE KY_DTE          = :CC-FSCL-WK-END-DTE      02800000
                         AND FSCL_WK_END_DTE = :CC-FSCL-WK-END-DTE      02801000
                  END-EXEC                                              02810000
                                                                        02820000
                  EVALUATE TRUE                                         02830000
                      WHEN SQLCODE = 0                                  02840000
                           DISPLAY 'INPUT FSCL WK END DTE: '            02850000
                                                   CC-FSCL-WK-END-DTE   02851000
                                                                        02852000
                      WHEN SQLCODE = -904                               02860000
                      WHEN SQLCODE = -913                               02870000
                           CONTINUE                                     02880000
                      WHEN SQLWARN0 NOT = SPACES                        02890000
                      WHEN SQLCODE < +0                                 02900000
                      WHEN SQLCODE > +0                                 02910000
                        DISPLAY '**************************************'02920000
                        DISPLAY '** ABEND                            **'02930000
                        DISPLAY '** PROGRAM = MLKUT363               **'02940000
                        DISPLAY '** PARA = 6000-VALIDATE-DATE        **'02950000
                        DISPLAY '** SELECT TDTATTR                   **'02960000
                        DISPLAY '** INPUT DATE IS NOT FISCAL WEEK END**'02961000
                        DISPLAY '** CC-FSCL-WK-END-DTE  = '             02970000
                                                   CC-FSCL-WK-END-DTE   02980000
                        DISPLAY '**************************************'02990000
                        PERFORM 9998-SQL-ABEND                          03000000
                  END-EVALUATE                                          03010000
           END-PERFORM.                                                 03020000
                                                                        03030000
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                         03040000
               DISPLAY '****************************************'       03050000
               DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'       03060000
               DISPLAY '** SELECT TDTATTR                     **'       03070000
               DISPLAY '** PARA = 6000-VALIDATE-DATE          **'       03080000
               DISPLAY '** CC-FSCL-WK-END-DTE  = '                      03090000
                                              CC-FSCL-WK-END-DTE        03100000
               DISPLAY '****************************************'       03110000
               PERFORM 9998-SQL-ABEND                                   03120000
           END-IF.                                                      03130000
                                                                        03140000
      ******************************************************************03150000
      *    PROCESS VENDORS                                              03160000
      *    ANY TIME THE CC_ENT_TYP CONTAINS 3, SET THE PS-LICENSE-FEE   03161000
      *    SWITCH TO INCREMENT THE ML363-GRP-ID. THAT IS THE ONLY TIME  03162000
      *    THE ML363-GRP-ID IS INCREMENTED.                             03163000
      ******************************************************************03170000
       6100-SET-UP-VARIABLES.                                           03180000
                                                                        03190000
           DISPLAY 'CC-ENT-TYP: ' CC-ENT-TYP.                           03191000
                                                                        03192000
           EVALUATE TRUE                                                03200000
      * * * 01,02,03                                                    03210000
                 WHEN CC-ENT-TYP = PC-FRGT-SALV-LICENSE-FEE             03220000
                      SET PS-LICENSE-FEE TO TRUE                        03221000
                      MOVE +1  TO PV-EXP-TYPE1                          03230000
                      MOVE +2  TO PV-EXP-TYPE2                          03240000
                      MOVE +3  TO PV-EXP-TYPE3                          03250000
                                  PV-EXP-TYPE4                          03260000
                                  PV-EXP-TYPE5                          03270000
                                  PV-EXP-TYPE6                          03280000
                                  PV-EXP-TYPE7                          03290000
                                  PV-EXP-TYPE8                          03300000
                                  PV-EXP-TYPE9                          03310000
                                  PV-EXP-TYPE10                         03320000
                                                                        03330000
      * * * 01,02                                                       03340000
                 WHEN CC-ENT-TYP = PC-FRGT-SALV                         03350000
                      MOVE +1  TO PV-EXP-TYPE1                          03360000
                      MOVE +2  TO PV-EXP-TYPE2                          03370000
                                  PV-EXP-TYPE3                          03380000
                                  PV-EXP-TYPE4                          03390000
                                  PV-EXP-TYPE5                          03400000
                                  PV-EXP-TYPE6                          03410000
                                  PV-EXP-TYPE7                          03420000
                                  PV-EXP-TYPE8                          03430000
                                  PV-EXP-TYPE9                          03440000
                                  PV-EXP-TYPE10                         03450000
                                                                        03460000
      * * * 03                                                          03470000
                 WHEN CC-ENT-TYP = PC-LICENSE-FEE                       03480000
                      SET PS-LICENSE-FEE TO TRUE                        03481000
                      MOVE +3  TO PV-EXP-TYPE1                          03490000
                                  PV-EXP-TYPE2                          03500000
                                  PV-EXP-TYPE3                          03510000
                                  PV-EXP-TYPE4                          03520000
                                  PV-EXP-TYPE5                          03530000
                                  PV-EXP-TYPE6                          03540000
                                  PV-EXP-TYPE7                          03550000
                                  PV-EXP-TYPE8                          03560000
                                  PV-EXP-TYPE9                          03570000
                                  PV-EXP-TYPE10                         03580000
                                                                        03590000
                 WHEN OTHER                                             03591000
                      DISPLAY 'USER DEFINED ENT TYPES NOT RECOGNIZED'   03592000
                      DISPLAY 'DEFAULTS WERE USED'                      03593000
           END-EVALUATE.                                                03600000
                                                                        03610000
      ******************************************************************03620000
      *  OPEN THE VENDOR CURSOR                                         03630000
      ******************************************************************03640000
       7000-OPEN-VENDOR-CSR.                                            03650000
                                                                        03660000
           PERFORM                                                      03670000
               WITH TEST AFTER                                          03680000
               VARYING PV-RETRY-COUNTER                                 03690000
               FROM 1 BY 1                                              03700000
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               03710000
                         OR (SQLCODE = ZERO))                           03720000
                                                                        03730000
               EXEC SQL                                                 03740000
                   OPEN VENDOR_CSR                                      03750000
               END-EXEC                                                 03760000
                                                                        03770000
               EVALUATE TRUE                                            03780000
                 WHEN SQLCODE = ZERO                                    03790000
                     CONTINUE                                           03800000
                 WHEN SQLWARN0 NOT EQUAL SPACE                          03810000
                 WHEN SQLCODE NOT EQUAL ZERO                            03820000
                     DISPLAY '**************************************'   03830000
                     DISPLAY '** ABEND                            **'   03840000
                     DISPLAY '** PROGRAM = MLKUT363               **'   03850000
                     DISPLAY '** PARA = 7000-OPEN-VENDOR-CSR      **'   03860000
                     DISPLAY '** OPEN VENDOR_CSR                  **'   03870000
                     DISPLAY '**************************************'   03880000
                     PERFORM 9998-SQL-ABEND                             03890000
             END-EVALUATE                                               03900000
           END-PERFORM.                                                 03910000
                                                                        03920000
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                    03930000
               (SQLCODE NOT = ZERO)                                     03940000
               DISPLAY '****************************************'       03950000
               DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'       03960000
               DISPLAY '** OPEN VENDOR_CSR                    **'       03970000
               DISPLAY '** PARA = 7000-OPEN-VENDOR-CSR        **'       03980000
               DISPLAY '****************************************'       03990000
               PERFORM 9998-SQL-ABEND                                   04000000
           END-IF.                                                      04010000
                                                                        04020000
      ******************************************************************04030000
      *  FETCH ROWS FOR VENDOR_CSR - FOR EACH HIT,                      04040000
      *  WRITE A RECORD TO THE EXPENSE VENDOR FILE                      04050000
      ******************************************************************04060000
       7100-FETCH-VENDOR-CSR.                                           04070000
                                                                        04080000
           PERFORM                                                      04090000
               WITH TEST AFTER                                          04100000
               VARYING PV-RETRY-COUNTER                                 04110000
               FROM 1 BY 1                                              04120000
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               04130000
                         OR (SQLCODE = ZERO)                            04140000
                         OR (SQLCODE = +100))                           04150000
                                                                        04160000
              EXEC SQL                                                  04170000
                FETCH                                                   04180000
                  FROM VENDOR_CSR                                       04190000
                  INTO                                                  04200000
                    :MLT00025-ENT-ID                                    04210000
                   ,:MLT00025-EXP-TYP-NBR                               04220000
                   ,:MLT00025-LBL-SEQ-NBR                               04221014
              END-EXEC                                                  04230000
                                                                        04240000
              EVALUATE TRUE                                             04250000
                  WHEN SQLCODE = ZERO                                   04260000
                    ADD +1                    TO PA-TOT-CSR-FETCHES     04260300
                    EVALUATE TRUE                                       04261000
                      WHEN PS-LICENSE-FEE                               04262021
                         AND MLT00025-EXP-TYP-NBR = +3                  04263000
                       IF MLT00025-ENT-ID NOT = PV-PREV-ENT-ID          04264021
                          SET PS-LF-NOT-FOUND       TO TRUE             04264121
                          PERFORM WITH TEST BEFORE                      04265021
                            VARYING PT-LF-INDX FROM 1 BY 1              04265118
                            UNTIL PT-LF-INDX > PT-LF-INDX-TOT           04266118
                               OR PS-LF-FOUND                           04266220
                               IF PT-LF-LBL-SEQ-NBR (PT-LF-INDX) =      04267020
                                                  MLT00025-LBL-SEQ-NBR  04268010
                                   SET PS-LF-FOUND      TO TRUE         04268120
                                   MOVE PT-LF-GROUP-ID (PT-LF-INDX)     04268223
                                                        TO ML363-GRP-ID 04268323
                               END-IF                                   04268920
                          END-PERFORM                                   04269012
                          IF PS-LF-FOUND                                04270010
                               CONTINUE                                 04270523
                          ELSE                                          04270610
                              ADD 1                 TO PV-GRP-ID-COUNTER04271009
                              MOVE PV-GRP-ID-COUNTER  TO ML363-GRP-ID   04272009
                              IF PT-LF-INDX-TOT > PT-LF-INDX-MAX        04274010
                                  MOVE '7100-FETCH-VENDOR-CSR'          04274110
                                               TO PV-PARAGRAPH-NAME     04274210
                                  MOVE  PE-MSG-03 TO PV-OPERATION-MSG-1 04274310
                                  MOVE  PE-MSG-04 TO PV-OPERATION-MSG-2 04274410
                                  PERFORM 9997-NON-SQL-ABEND            04274510
                              END-IF                                    04275010
                              MOVE MLT00025-LBL-SEQ-NBR TO              04276010
                                      PT-LF-LBL-SEQ-NBR (PT-LF-INDX-TOT)04277010
                              MOVE ML363-GRP-ID         TO              04277121
                                      PT-LF-GROUP-ID    (PT-LF-INDX-TOT)04277221
                              SET PT-LF-INDX-TOT UP BY 1                04278013
                          END-IF                                        04300109
                          MOVE MLT00025-ENT-ID      TO ML363-ENT-ID     04300210
                          MOVE MLT00025-EXP-TYP-NBR TO ML363-EXP-TYP    04300310
                          PERFORM 7300-WRITE-EXP-VENDOR-REC             04300410
                       END-IF                                           04300522
                       MOVE MLT00025-ENT-ID      TO PV-PREV-ENT-ID      04300622
                      WHEN OTHER                                        04301021
                          MOVE 0                    TO ML363-GRP-ID     04302000
                          MOVE MLT00025-ENT-ID      TO ML363-ENT-ID     04303000
                          MOVE MLT00025-EXP-TYP-NBR TO ML363-EXP-TYP    04304000
                          PERFORM 7300-WRITE-EXP-VENDOR-REC             04305000
                    END-EVALUATE                                        04306000
                                                                        04310000
                  WHEN SQLCODE = +100                                   04320000
                      SET PS-END-OF-VENDOR-CSR TO TRUE                  04330000
                                                                        04340000
                  WHEN SQLWARN0 NOT EQUAL SPACE                         04350000
                  WHEN SQLCODE NOT EQUAL ZERO                           04360000
                      DISPLAY '***************************************' 04370000
                      DISPLAY '** ABEND                             **' 04380000
                      DISPLAY '** PROGRAM = MLKUT363                **' 04390000
                      DISPLAY '** PARA =  7100-FETCH-VENDOR-CSR     **' 04400000
                      DISPLAY '** FETCH VENDOR_CSR                  **' 04410000
                      DISPLAY '** LAST ENT ID FETCHED:'                 04420000
                                                   MLT00025-ENT-ID      04430000
                      DISPLAY '** TOT CSR FETCHES: '                    04440000
                                            PA-TOT-CSR-FETCHES          04450000
                      DISPLAY '**************************************'  04460000
                      PERFORM 9998-SQL-ABEND                            04470000
              END-EVALUATE                                              04480000
           END-PERFORM.                                                 04490000
                                                                        04500000
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                    04510000
               (SQLCODE NOT = ZERO) AND                                 04520000
               (SQLCODE NOT = +100)                                     04530000
               DISPLAY '****************************************'       04540000
               DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'       04550000
               DISPLAY '** FETCH VENDOR_CSR                   **'       04560000
               DISPLAY '** PARA = 7100-FETCH-VENDOR-CSR       **'       04570000
               DISPLAY '** LAST ENT ID FETCHED:' MLT00025-ENT-ID        04580000
               DISPLAY '** TOT CSR FETCHES: '                           04590000
                                            PA-TOT-CSR-FETCHES          04600000
               DISPLAY '****************************************'       04610000
               PERFORM 9998-SQL-ABEND                                   04620000
           END-IF.                                                      04630000
                                                                        04640000
      ******************************************************************04650000
      *  CLOSE VENDOR_CSR CURSOR                                        04660000
      ******************************************************************04670000
       7200-CLOSE-VENDOR-CSR.                                           04680000
                                                                        04690000
           PERFORM                                                      04700000
               WITH TEST AFTER                                          04710000
               VARYING PV-RETRY-COUNTER                                 04720000
               FROM 1 BY 1                                              04730000
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               04740000
                         OR (SQLCODE = ZERO))                           04750000
                                                                        04760000
               EXEC SQL                                                 04770000
                   CLOSE VENDOR_CSR                                     04780000
               END-EXEC                                                 04790000
                                                                        04800000
               EVALUATE TRUE                                            04810000
                 WHEN SQLCODE = ZERO                                    04820000
                     CONTINUE                                           04830000
                 WHEN SQLWARN0 NOT EQUAL SPACE                          04840000
                 WHEN SQLCODE NOT EQUAL ZERO                            04850000
                     DISPLAY '**************************************'   04860000
                     DISPLAY '** ABEND                            **'   04870000
                     DISPLAY '** PROGRAM = MLKUT363               **'   04880000
                     DISPLAY '** PARA = 7200-CLOSE-VENDOR-CSR     **'   04890000
                     DISPLAY '** CLOSE VENDOR_CSR                 **'   04900000
                     DISPLAY '**************************************'   04910000
                     PERFORM 9998-SQL-ABEND                             04920000
             END-EVALUATE                                               04930000
           END-PERFORM.                                                 04940000
                                                                        04950000
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                    04960000
               (SQLCODE NOT = ZERO)                                     04970000
               DISPLAY '****************************************'       04980000
               DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'       04990000
               DISPLAY '** CLOSE VENDOR_CSR                   **'       05000000
               DISPLAY '** PARA = 7200-CLOSE-VENDOR-CSR       **'       05010000
               DISPLAY '****************************************'       05020000
               PERFORM 9998-SQL-ABEND                                   05030000
           END-IF.                                                      05040000
                                                                        05050000
      ******************************************************************05051000
      * WRITE ADJUSTMENT TRANSACTION (MLRD254 FORMAT)                   05052000
      ******************************************************************05053000
       7300-WRITE-EXP-VENDOR-REC.                                       05054000
                                                                        05055000
           WRITE EXPENSE-VENDOR-REC                                     05056000
            FROM ML363-VNDR-REC.                                        05056100
           ADD 1 TO PA-TOT-RECS-WRITTEN.                                05057000
                                                                        05058000
           INITIALIZE ML363-ENT-ID                                      05059000
                      ML363-EXP-TYP.                                    05059100
                                                                        05059400
      ******************************************************************05060000
      *  CLOSE FILES                                                    05070000
      ******************************************************************05090000
       8000-EOJ-ROUTINE.                                                05100000
                                                                        05110000
           CLOSE DATE-FILE                                              05111000
                 ENT-TYPE-FILE                                          05112000
                 EXPENSE-VENDOR-FILE.                                   05112100
                                                                        05113000
      ******************************************************************05210000
      * DISPLAY CURSOR FETCH AND OUTREC WRITTEN COUNTS                  05230000
      ******************************************************************05240000
       8100-DISPLAY-SUMMARY-STATS.                                      05250000
                                                                        05250100
           DISPLAY ' ********************************************'      05251000
           DISPLAY '     * MLKUT363 SUMMARY STATISTICS *         '      05252000
           DISPLAY ' ********************************************'      05253000
           DISPLAY ' '                                                  05254000
           DISPLAY ' TOTAL VENDOR CSR FETCHES....:'                     05255000
                                          PA-TOT-CSR-FETCHES.           05256000
           DISPLAY ' TOTAL OUTPUT RECS WRITTEN...:'                     05257000
                                          PA-TOT-RECS-WRITTEN.          05258000
                                                                        05259000
      ******************************************************************05290000
      *   9997-NON-SQL-ABEND   FOR NON SQL ERROR                        05300000
      ******************************************************************05310000
       9997-NON-SQL-ABEND.                                              05320000
                                                                        05330000
           DISPLAY '*********************'                              05340000
           DISPLAY '** ABEND           **'.                             05350000
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          05360000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        05370000
           DISPLAY '** OPER MSG 1      ** = ' PV-OPERATION-MSG-1.       05380000
           DISPLAY '** OPER MSG 2      ** = ' PV-OPERATION-MSG-2.       05390000
           DISPLAY '*********************   '.                          05400000
           CALL 'ILBOABN0' USING ABEND-CODE.                            05410000
                                                                        05420000
           PERFORM 9999-ABEND.                                          05430000
                                                                        05440000
      ******************************************************************05450000
      *    SQL ERROR - ABEND                                            05460000
      ******************************************************************05470000
       9998-SQL-ABEND.                                                  05480000
                                                                        05490000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         05580000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        05590000
                                                                        05600000
           COPY DPPD004.                                                05610000
                                                                        05620000
           PERFORM 9999-ABEND.                                          05630000
                                                                        05640000
      ******************************************************************05650000
      *    ABEND CALL                                                   05660000
      ******************************************************************05670000
       9999-ABEND.                                                      05680000
                                                                        05690000
           MOVE +4013          TO ABEND-CODE.                           05700000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           05710000
                                                                        05720000
