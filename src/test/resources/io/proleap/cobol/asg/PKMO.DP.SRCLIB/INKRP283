       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         INKRP283.                                            
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       01/23/2003.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *PURPOSE:                                                                 
      *                                                                         
      *    THIS PROGRAM WILL CREATE A SUMMARIZED INVENTORY AUDIT TRAIL          
      * REPORT FROM THE SORTED INPUT BOOKING SUMMARY FILE.                      
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
W26600* W26600  08-01-2004 ROD JANSSEN INVENTORY STORE EXPANSION                
W26006* W26006  02-24-2005 ERIN SEARL  REMOVE REFERENCES TO OBSOLETE            
W26006*                                STORE TABLE.                             
PK1383* PKE1383 07-30-2009  PRITHIVIRAJ S -CORRECTED THE CODE TO DISPLAY        
PK1383*                       THE FOUR DIGIT STORE NUMBER IN THE REPORT.        
P40463*PRB40463 22-03-2013  INCLUDED 'WITH UR' IN THE QUERIES                   
P40463******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BOOK-INPUT-SUMMARY-FILE                                       
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT ADJ-PHY-INVENTORY-REPORT                                      
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BOOK-INPUT-SUMMARY-FILE                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS BOOK-INPUT-SUMMARY-RECORD.                            
                                                                                
       01  BOOK-INPUT-SUMMARY-RECORD       PIC X(80).                           
                                                                                
       FD  ADJ-PHY-INVENTORY-REPORT                                             
W26600     RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS REPORT-LINE.                                          
                                                                                
       01  REPORT-LINE                   PIC X(132).                            
                                                                                
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
       01  PC-PRINT-CONTROL.                                                    
           05  PC-PAGE-COUNT             PIC S9(05) VALUE ZERO.                 
           05  PC-LINE-COUNT             PIC S9(03) VALUE ZERO COMP-3.          
           05  PC-LINE-SPACE             PIC S9(05) VALUE ZERO.                 
           05  PC-PAGE-COUNT2            PIC S9(05) VALUE ZERO.                 
           05  PC-LINE-COUNT2            PIC S9(03) VALUE ZERO COMP-3.          
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARA                   PIC X(30)  VALUE SPACES.               
           05  PV-DB2-OPER-ATTEMPTED     PIC X(30)  VALUE SPACES.               
           05  PV-TAB-MAX                PIC 9(04)  VALUE 9999.                 
W26600     05  MAX-STORES                PIC 9(05)  VALUE 10000.                
W26600     05  PV-STR-NDX                PIC S9(04) COMP VALUE ZEROS.           
           05  PV-DEPT-SUB               PIC 9(05)  VALUE ZEROES.               
           05  PV-COMPUTED-FIELDS.                                              
               10  PV-ADJUSTMENT-QTY     PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-OLD-ONHAND-QTY     PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-QTY                PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-AFTER-RCPT         PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-STR-EXPTED         PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-CUTOFF-INTRANS     PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-CUTOFF-STR-EXPTED  PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-TOT-ADJUSTMENT-QTY PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-TOT-OLD-ONHAND-QTY PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-TOT-QTY            PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-TOT-AFTER-RCPT     PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-TOT-CUTOFF-INTRANS PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-TOT-CUTOFF-STR-EXPTED                                     
                                         PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-WRK-ADJUSTMENT-QTY PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-DTL-ADJUSTMENT-QTY PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-DTL-OLD-ONHAND-QTY PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-DTL-QTY            PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-DTL-AFTER-RCPT     PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-DTL-CUTOFF-INTRANS PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-DTL-CUTOFF-STR-EXPTED                                     
                                         PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-TB-STR-EXPTED      PIC S9(15) COMP-3 VALUE ZEROS.         
               10  PV-STR-TB-STR-EXPTED  PIC S9(15) COMP-3 VALUE ZEROS.         
                                                                                
           05  PV-DEPT-NMBR              PIC X(04).                             
           05  PV-DEPT-NMBR-NUM REDEFINES                                       
               PV-DEPT-NMBR              PIC 9(04).                             
W26600     05  PV-STORE-NMBR             PIC X(04).                             
           05  PV-STORE-NMBR-NUM REDEFINES                                      
W26600         PV-STORE-NMBR             PIC 9(04).                             
W26600     05  PV-STORE-NMBR-SAVE        PIC X(04).                             
W26600     05  PV-LOC-NBR                PIC 9(04).                             
W26600     05  PV-LOC-NBR-X REDEFINES                                           
W26600         PV-LOC-NBR                PIC X(04).                             
W26600     05  PV-HDR-LOC-NBR            PIC 9(04).                             
                                                                                
       01  SWITCHES.                                                            
           05  WS-PHYINV-DONE-SW         PIC X(01) VALUE 'N'.                   
               88  PHYINV-PROCESSING-COMPLETE      VALUE 'Y'.                   
           05  WS-FIRST-TIME-SW          PIC X(01) VALUE 'N'.                   
               88  FIRST-TIME-THRU                 VALUE 'N'.                   
               88  CONTINUED-PROCESSING            VALUE 'Y'.                   
                                                                                
       01  TB-DEPT-TABLE.                                                       
           05 TB-DEPT-TAB                OCCURS 10000 TIMES.                    
              10  TB-ADJUSTMENT-QTY      PIC S9(15) COMP-3 VALUE ZEROS.         
              10  TB-QTY                 PIC S9(15) COMP-3 VALUE ZEROS.         
              10  TB-OLD-ONHAND-QTY      PIC S9(15) COMP-3 VALUE ZEROS.         
              10  TB-AFTER-RCPT          PIC S9(15) COMP-3 VALUE ZEROS.         
              10  TB-CUTOFF-INTRANS      PIC S9(15) COMP-3 VALUE ZEROS.         
              10  TB-CUTOFF-STR-EXPTED   PIC S9(15) COMP-3 VALUE ZEROS.         
                                                                                
       01  TB-STORE-ACCUM-TABLE.                                                
W26600     05  TB-STR-ELEMENTS           OCCURS 10000 TIMES.                    
               10  TB-STR-ADJUSTMENT-QTY PIC S9(15) COMP-3 VALUE ZEROS.         
               10  TB-STR-QTY            PIC S9(15) COMP-3 VALUE ZEROS.         
               10  TB-STR-OLD-ONHAND     PIC S9(15) COMP-3 VALUE ZEROS.         
               10  TB-STR-AFTER-RCPT     PIC S9(15) COMP-3 VALUE ZEROS.         
               10  TB-STR-CUTOFF-INTRANS PIC S9(15) COMP-3 VALUE ZEROS.         
               10  TB-STR-CUTOFF-STR-EXPTED                                     
                                         PIC S9(15) COMP-3 VALUE ZEROS.         
                                                                                
       01  CPU-DATE.                                                            
           05  CPU-YEAR                  PIC X(02)  VALUE SPACES.               
           05  CPU-MONTH                 PIC X(02)  VALUE SPACES.               
           05  CPU-DAY                   PIC X(02)  VALUE SPACES.               
                                                                                
       01  CPU-TIME.                                                            
           05  CPU-HOUR                  PIC X(02)  VALUE SPACES.               
           05  CPU-MINUTE                PIC X(02)  VALUE SPACES.               
           05  FILLER                    PIC X(02)  VALUE SPACES.               
                                                                                
       01  H1-HEADING.                                                          
           05  FILLER              PIC X(40)   VALUE SPACES.                    
           05  FILLER              PIC X(97)   VALUE                            
              'SUMMARIZED INVENTORY ADJUSTMENT AUDIT REPORT - UNITS'.           
                                                                                
       01  H1B-HEADING.                                                         
           05  FILLER              PIC X(56)   VALUE SPACES.                    
           05  FILLER              PIC X(06)   VALUE 'STORE '.                  
           05  FILLER              PIC X(01)   VALUE SPACES.                    
W26600     05  H1B-STORE           PIC X(04)   VALUE SPACES.                    
W26600     05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  H1B-STORE-NAME      PIC X(13)   VALUE SPACES.                    
                                                                                
       01  H2-HEADING.                                                          
           05  FILLER               PIC X(04)     VALUE 'DEPT'.                 
           05  FILLER               PIC X(14)     VALUE SPACES.                 
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(08)     VALUE 'PHYSICAL'.             
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(09)     VALUE SPACES.                 
           05  FILLER               PIC X(06)     VALUE 'CUTOFF'.               
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(05)     VALUE SPACES.                 
           05  FILLER               PIC X(10)     VALUE 'POST AFTER'.           
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(05)     VALUE SPACES.                 
           05  FILLER               PIC X(10)     VALUE 'STR EXPECT'.           
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  FILLER               PIC X(13)     VALUE 'FASHION SALES'.        
           05  FILLER               PIC X(11)     VALUE SPACES.                 
                                                                                
       01  H2B-HEADING.                                                         
           05  FILLER               PIC X(05)     VALUE 'STORE'.                
           05  FILLER               PIC X(13)     VALUE SPACES.                 
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(08)     VALUE 'PHYSICAL'.             
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(09)     VALUE SPACES.                 
           05  FILLER               PIC X(06)     VALUE 'CUTOFF'.               
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(05)     VALUE SPACES.                 
           05  FILLER               PIC X(10)     VALUE 'POST AFTER'.           
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(05)     VALUE SPACES.                 
           05  FILLER               PIC X(10)     VALUE 'STR EXPECT'.           
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  FILLER               PIC X(13)     VALUE 'FASHION SALES'.        
           05  FILLER               PIC X(10)     VALUE SPACES.                 
                                                                                
       01  H3-HEADING.                                                          
           05  FILLER               PIC X(04)     VALUE SPACES.                 
           05  FILLER               PIC X(14)     VALUE SPACES.                 
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(08)     VALUE 'QUANTITY'.             
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(09)     VALUE SPACES.                 
           05  FILLER               PIC X(06)     VALUE 'ONHAND'.               
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(05)     VALUE SPACES.                 
           05  FILLER               PIC X(10)     VALUE 'FLUSH RCPT'.           
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(09)     VALUE 'QUANTITY'.             
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(04)     VALUE SPACES.                 
           05  FILLER               PIC X(11)     VALUE 'ADJUSTMENT'.           
           05  FILLER               PIC X(10)     VALUE SPACES.                 
                                                                                
       01  C1-COMMENT.                                                          
W26600     05  C1-STORE-NBR        PIC X(04)   VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(10)   VALUE                            
               'STR TTL **'.                                                    
                                                                                
       01  D1-DETAIL-LINE.                                                      
           05  D1-DEPT-NBR          PIC 9999.                                   
           05  D1-DEPT-NBR-1 REDEFINES D1-DEPT-NBR.                             
               10 D1-DEPT-1         PIC X(01).                                  
               10 D1-DEPT-2         PIC X(03).                                  
           05  FILLER               PIC X(01)        VALUE SPACES.              
           05  D1-COMMENT           PIC X(14)        VALUE SPACES.              
           05  FILLER               PIC X(05)        VALUE SPACES.              
           05  D1-QTY               PIC ZZ,ZZZ,ZZZ,ZZ9-.                        
           05  FILLER               PIC X(06)        VALUE SPACES.              
           05  D1-ONHAND-QTY        PIC ZZ,ZZZ,ZZZ,ZZ9-.                        
           05  FILLER               PIC X(06)        VALUE SPACES.              
           05  D1-AFTER-RCPT        PIC ZZ,ZZZ,ZZZ,ZZ9-.                        
           05  FILLER               PIC X(06)        VALUE SPACES.              
           05  D1-STR-EXPTED        PIC ZZ,ZZZ,ZZZ,ZZ9-.                        
           05  FILLER               PIC X(06)        VALUE SPACES.              
           05  D1-ADJUSTMENT-QTY    PIC ZZ,ZZZ,ZZZ,ZZ9-.                        
                                                                                
      /****************************************************************         
      * COPY FOR STANDARD HEADING                                               
      *****************************************************************         
                                                                                
           COPY DPWS133O.                                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * 01  IN016-PHYSICAL-COUNT-RECORD                                         
      *****************************************************************         
W26600     COPY INWS016.                                                        
                                                                                
      *****************************************************************         
      * DB2 STORE TABLE DCLGEN.                                                 
      *****************************************************************         
           EXEC SQL                                                             
W26600         INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 MERCHANDISE DEPARTMENT                                              
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      *FLOW:    SEQUENTIALLY READ THE SORTED INPUT BOOKING SUMMARY FILE.        
      *      ACCUMULATE TOTALS FOR PHYSICAL INVENTORY, CUTOFF ONHAND,           
      *      POST AFTER FLUSH RECEIPTS, AND FASHION SALES ADJUSTMENTS.          
      *      FASHION SALES ADJUSTMENTS ARE CALCULATED FROM THE LATER.           
      *      NUMBERS ARE ACCUMULATED BY DEPARTMENT FOR A GIVEN STORE.           
      *                                                                         
      *         WHEN A STORE BREAK OCCURS, PRINT A DETAIL LINE FOR EACH         
      *      DEPARTMENT BY DUMPING THE CONTENTS OF THE DEPARTMENT TABLE         
      *      FOR THE STORE BEING PROCESSED. TOTAL UP FOR THE STORE.             
      *                                                                         
      *         THE MODULE GENERATES TWO REPORTS. A SUMMARIZED AUDIT            
      *      REPORT AND A DETAIL AUDIT REPORT. AS WE ACCUMULATE FOR THE         
      *      SUMMARY, WRITE A DETAIL LINE FOR EACH SKU BEING PROCESSED.         
      *                                                                         
      *         AT END OF FILE, PROVIDE COMPANY TOTALS; AS WELL, AS A           
      *      STORE LEVEL SUMMARY REPORT FOR REPORT 1.                           
      *                                                                         
      *         TABLES USED IN THE RPEORT.                                      
W26600*      XMT_LOC - LOCATION TABLE.                                          
      *                                                                         
      *****************************************************************         
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
       A100-MAIN-MODULE.                                                        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM B200-PRINT-REPORT                                            
               UNTIL PHYINV-PROCESSING-COMPLETE.                                
                                                                                
           PERFORM B600-EOJ.                                                    
                                                                                
       A999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      /****************************************************************         
      * B100-HOUSEKEEPING.......                                                
      *****************************************************************         
       B100-HOUSEKEEPING.                                                       
           MOVE '*B100-HOUSEKEEPING.              *' TO PV-PARA                 
                                                                                
           OPEN  INPUT BOOK-INPUT-SUMMARY-FILE                                  
                OUTPUT ADJ-PHY-INVENTORY-REPORT.                                
                                                                                
           ACCEPT CPU-DATE FROM DATE.                                           
           MOVE CPU-YEAR   TO DP133O-RUN-YEAR.                                  
           MOVE CPU-MONTH  TO DP133O-RUN-MONTH.                                 
           MOVE CPU-DAY    TO DP133O-RUN-DAY.                                   
                                                                                
           ACCEPT CPU-TIME FROM TIME.                                           
           MOVE CPU-HOUR   TO DP133O-RUN-HOUR.                                  
           MOVE CPU-MINUTE TO DP133O-RUN-MINUTE.                                
                                                                                
           MOVE 'INKRP283' TO DP133O-PROGRAM-NAME.                              
           MOVE ZEROS      TO PC-PAGE-COUNT.                                    
           MOVE ZEROS      TO PC-PAGE-COUNT2.                                   
                                                                                
           MOVE 'N'        TO WS-PHYINV-DONE-SW.                                
           MOVE 'N'        TO WS-FIRST-TIME-SW.                                 
                                                                                
           INITIALIZE TB-DEPT-TABLE.                                            
           INITIALIZE TB-STORE-ACCUM-TABLE.                                     
           INITIALIZE IN016-PHYSICAL-COUNT-RECORD.                              
                                                                                
           PERFORM Z200-READ-BOOKING-INPUT-FILE.                                
                                                                                
W26600     MOVE IN016-STORE-9 TO XMT00001-LOC-NBR.                              
           PERFORM B500-GET-STR-NAME.                                           
                                                                                
      /****************************************************************         
      * B200-PRINT-REPORT.                                                      
      *     READ THE SORTED BOOKING INPUT SUMMARY UNTIL END OF FILE.            
      * GENERATE THE DETAIL PHYSICAL INVENTORY AUDIT REPORT. ACCUMULATE         
      * NUMBER BY DEPARTMENT FOR THE STORE BEING PROCESSED. WHEN A STORE        
      * BREAK OCCURS, PRINT THE STORE'S SUMMARIZED PHYSICAL INVENTORY           
      * AUDIT REPORT BY DEPARTMENT. PROVIDE TOTALS FOR THE STORE. INIT          
      * ACCUMULATORS AND RESET SWITCHES FOR THE NEXT STORE.                     
      *****************************************************************         
       B200-PRINT-REPORT.                                                       
           MOVE '*B200-PRINT-REPORT.              *' TO PV-PARA                 
                                                                                
           IF IN016-STORE NOT EQUAL PV-STORE-NMBR-SAVE                          
             IF (PV-ADJUSTMENT-QTY NOT EQUAL ZEROS) OR                          
                (PV-OLD-ONHAND-QTY NOT EQUAL ZEROS) OR                          
                (PV-QTY NOT EQUAL ZEROS)            OR                          
                (PV-AFTER-RCPT NOT EQUAL ZEROS)     OR                          
                (PV-CUTOFF-INTRANS NOT EQUAL ZEROS) OR                          
                (PV-CUTOFF-STR-EXPTED NOT EQUAL ZEROS)                          
               PERFORM Z800-PRINT-HEADINGS                                      
               PERFORM B400-PRINT-DETAIL-LINE                                   
                   VARYING PV-DEPT-SUB FROM 1 BY 1                              
                   UNTIL PV-DEPT-SUB GREATER PV-TAB-MAX                         
               PERFORM B410-PRINT-STORE-TOTAL                                   
             END-IF                                                             
             MOVE IN016-STORE TO PV-STORE-NMBR-SAVE                             
W26600       MOVE IN016-STORE-9 TO XMT00001-LOC-NBR                             
             PERFORM B500-GET-STR-NAME                                          
             INITIALIZE TB-DEPT-TABLE                                           
             MOVE ZEROS       TO PV-ADJUSTMENT-QTY                              
             MOVE ZEROS       TO PV-OLD-ONHAND-QTY                              
             MOVE ZEROS       TO PV-QTY                                         
             MOVE ZEROS       TO PV-AFTER-RCPT                                  
             MOVE ZEROS       TO PV-CUTOFF-INTRANS                              
             MOVE ZEROS       TO PV-CUTOFF-STR-EXPTED                           
           END-IF.                                                              
                                                                                
           PERFORM C100-ACCUM-DEPT-TOTALS.                                      
                                                                                
           PERFORM Z200-READ-BOOKING-INPUT-FILE.                                
                                                                                
      /****************************************************************         
      * B400-PRINT-DETAIL-LINE.                                                 
      *     PRINT THE SUMMARIZED INVENTORY AUDIT REPORT FOR THE STORE           
      * BEING PROCESSED. THE SUBSCRIPT REPRESENTS THE ACTUAL DEPARTMENT.        
      * IF MORE THAN 999 DEPARTMENTS EXIST, WE WILL NEED TO EXPAND THE          
      * TABLES.                                                                 
      *****************************************************************         
       B400-PRINT-DETAIL-LINE.                                                  
           MOVE '*B400-PRINT-DETAIL-LINE.         *' TO PV-PARA                 
                                                                                
           IF TB-ADJUSTMENT-QTY (PV-DEPT-SUB) NOT EQUAL ZEROS OR                
              TB-QTY (PV-DEPT-SUB) NOT EQUAL ZEROS            OR                
              TB-OLD-ONHAND-QTY (PV-DEPT-SUB) NOT EQUAL ZEROS OR                
              TB-AFTER-RCPT (PV-DEPT-SUB) NOT EQUAL ZEROS     OR                
              TB-CUTOFF-INTRANS(PV-DEPT-SUB) NOT EQUAL ZEROS  OR                
              TB-CUTOFF-STR-EXPTED (PV-DEPT-SUB) NOT EQUAL ZEROS                
                                                                                
               MOVE SPACES                          TO D1-DETAIL-LINE           
               MOVE PV-DEPT-SUB                     TO D1-DEPT-NBR              
               MOVE TB-ADJUSTMENT-QTY (PV-DEPT-SUB) TO D1-ADJUSTMENT-QTY        
               MOVE TB-QTY (PV-DEPT-SUB)            TO D1-QTY                   
               MOVE TB-OLD-ONHAND-QTY (PV-DEPT-SUB) TO D1-ONHAND-QTY            
               MOVE TB-AFTER-RCPT (PV-DEPT-SUB)     TO D1-AFTER-RCPT            
      *        MOVE TB-CUTOFF-INTRANS(PV-DEPT-SUB)  TO D1-CUTOFF-INTRANS        
               MOVE TB-CUTOFF-STR-EXPTED(PV-DEPT-SUB)                           
                                                 TO D1-STR-EXPTED               
                                                                                
               IF PC-LINE-COUNT > +59                                           
                   PERFORM Z800-PRINT-HEADINGS                                  
               END-IF                                                           
                                                                                
               MOVE D1-DETAIL-LINE  TO REPORT-LINE                              
               MOVE 1               TO PC-LINE-SPACE                            
               PERFORM Z700-WRITE-REPORT-LINE                                   
               ADD 1                TO PC-LINE-COUNT                            
                                                                                
           END-IF.                                                              
                                                                                
      /****************************************************************         
      * B410-PRINT-STORE-TOTAL.                                                 
      *****************************************************************         
       B410-PRINT-STORE-TOTAL.                                                  
           MOVE '*B410-PRINT-STORE-TOTAL.         *' TO PV-PARA                 
                                                                                
           MOVE SPACES                   TO D1-DETAIL-LINE.                     
W26600     MOVE XMT00001-LOC-NBR         TO PV-LOC-NBR.                         
W26600     MOVE PV-LOC-NBR-X             TO C1-STORE-NBR.                       
           MOVE C1-COMMENT               TO D1-COMMENT.                         
           MOVE PV-ADJUSTMENT-QTY        TO D1-ADJUSTMENT-QTY.                  
           MOVE PV-OLD-ONHAND-QTY        TO D1-ONHAND-QTY.                      
           MOVE PV-QTY                   TO D1-QTY.                             
           MOVE PV-AFTER-RCPT            TO D1-AFTER-RCPT.                      
      *    MOVE PV-CUTOFF-INTRANS        TO D1-CUTOFF-INTRANS.                  
           MOVE PV-CUTOFF-STR-EXPTED     TO D1-STR-EXPTED.                      
                                                                                
           IF PC-LINE-COUNT > +59                                               
               PERFORM Z800-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           ADD PV-ADJUSTMENT-QTY         TO PV-TOT-ADJUSTMENT-QTY.              
           ADD PV-OLD-ONHAND-QTY         TO PV-TOT-OLD-ONHAND-QTY.              
           ADD PV-QTY                    TO PV-TOT-QTY.                         
           ADD PV-AFTER-RCPT             TO PV-TOT-AFTER-RCPT.                  
      *    ADD PV-CUTOFF-INTRANS         TO PV-TOT-CUTOFF-INTRANS.              
           ADD PV-CUTOFF-STR-EXPTED      TO PV-TOT-CUTOFF-STR-EXPTED.           
                                                                                
           MOVE D1-DETAIL-LINE           TO REPORT-LINE.                        
           MOVE 2                        TO PC-LINE-SPACE.                      
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           ADD 2                         TO PC-LINE-COUNT.                      
                                                                                
                                                                                
      /****************************************************************         
      * B500-GET-STR-NAME.                                                      
      *     USED TO PRINT HEADINGS FOR THE SUMMARIZED INVENTORY AUDIT           
      * REPORT. THE ROUTINE GRABS INFORMATION OFF THE LOCATION TABLE            
      * XMT_LOC.                                                                
      *****************************************************************         
       B500-GET-STR-NAME.                                                       
                                                                                
           EXEC SQL                                                             
W26600         SELECT LOC_NBR,                                                  
W26600                LOC_10_ABBR_NM                                            
W26600         INTO :XMT00001-LOC-NBR,                                          
W26600              :XMT00001-LOC-10-ABBR-NM                                    
W26600         FROM XMT_LOC                                                     
W26600         WHERE LOC_NBR = :XMT00001-LOC-NBR                                
P40463         WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE IS EQUAL TO ZERO                                    
W26600           MOVE XMT00001-LOC-NBR TO PV-LOC-NBR                            
               WHEN SQLCODE IS EQUAL TO +100                                    
W26600           MOVE ZEROS TO PV-LOC-NBR                                       
W26600           MOVE '***' TO XMT00001-LOC-10-ABBR-NM                          
               WHEN SQLWARN IS NOT EQUAL TO SPACES                              
               WHEN SQLCODE IS NOT EQUAL TO ZERO                                
                 DISPLAY SPACES                                                 
                 DISPLAY '** ABEND IN PROGRAM INKRP283'                         
                 DISPLAY '** ERROR PROCESSING DB2 STORE TABLE.'                 
W26600           DISPLAY 'STORE NUMBER: ', IN016-STORE-9                        
                 PERFORM Z900-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
      /****************************************************************         
      * B600-EOJ.                                                               
      *     PRINT THE SUMMARIZED INVENTORY AUDIT TRAIL REPORT, BY DEPT,         
      * FOR THE LAST STORE PROCESSED.                                           
      *     GENERATE A TOTAL PAGE FOR ALL STORES.                               
      *     GENERATE A SUMMARY PAGE, BY STORE NUMBER, OF THE INFORMATION        
      * CREATED BY DEPARTMENT. TOTAL UP FOR ALL STORES.                         
      *     TOTAL UP FOR THE DETAILS INVENTORY AUDIT TRAIL REPORT.              
      *     CLOSE THE FILES.                                                    
      *****************************************************************         
       B600-EOJ.                                                                
      *** FINAL TOTAL UP......                                                  
                                                                                
           IF (PV-ADJUSTMENT-QTY NOT EQUAL ZEROS) OR                            
              (PV-OLD-ONHAND-QTY NOT EQUAL ZEROS) OR                            
              (PV-QTY NOT EQUAL ZEROS)            OR                            
              (PV-AFTER-RCPT NOT EQUAL ZEROS)     OR                            
              (PV-CUTOFF-INTRANS NOT EQUAL ZEROS) OR                            
              (PV-CUTOFF-STR-EXPTED NOT EQUAL ZEROS)                            
               PERFORM Z800-PRINT-HEADINGS                                      
               PERFORM B400-PRINT-DETAIL-LINE                                   
                   VARYING PV-DEPT-SUB FROM 1 BY 1                              
                   UNTIL PV-DEPT-SUB GREATER PV-TAB-MAX                         
               PERFORM B410-PRINT-STORE-TOTAL                                   
           END-IF.                                                              
                                                                                
      *** NORMAL END OF JOB. PRINT STORE/DEPT TOTALS....                        
                                                                                
           MOVE '*B600-EOJ.                        ' TO PV-PARA                 
                                                                                
W26600     MOVE ZEROS  TO XMT00001-LOC-NBR                                      
W26600     MOVE SPACES TO XMT00001-LOC-10-ABBR-NM                               
           PERFORM B500-GET-STR-NAME.                                           
           PERFORM Z800-PRINT-HEADINGS.                                         
                                                                                
           MOVE SPACES                   TO D1-DETAIL-LINE.                     
           MOVE 'TOTAL'                  TO D1-COMMENT.                         
           MOVE PV-TOT-ADJUSTMENT-QTY    TO D1-ADJUSTMENT-QTY.                  
           MOVE PV-TOT-AFTER-RCPT        TO D1-AFTER-RCPT.                      
      *    MOVE PV-TOT-CUTOFF-INTRANS    TO D1-CUTOFF-INTRANS.                  
           MOVE PV-TOT-CUTOFF-STR-EXPTED TO D1-STR-EXPTED.                      
           MOVE PV-TOT-QTY               TO D1-QTY.                             
           MOVE PV-TOT-OLD-ONHAND-QTY    TO D1-ONHAND-QTY.                      
                                                                                
           MOVE D1-DETAIL-LINE           TO REPORT-LINE.                        
           MOVE 2                        TO PC-LINE-SPACE.                      
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           ADD 2                         TO PC-LINE-COUNT.                      
                                                                                
      *** PRINT A STORE TOTALS SUMMARY PAGE AFTER THE DETAIL TOTALS.            
                                                                                
           MOVE ZEROS          TO PV-ADJUSTMENT-QTY.                            
           MOVE ZEROS          TO PV-OLD-ONHAND-QTY.                            
           MOVE ZEROS          TO PV-QTY.                                       
           MOVE ZEROS          TO PV-AFTER-RCPT.                                
           MOVE ZEROS          TO PV-CUTOFF-INTRANS.                            
           MOVE ZEROS          TO PV-CUTOFF-STR-EXPTED.                         
           MOVE ZEROS          TO PV-TOT-ADJUSTMENT-QTY.                        
           MOVE ZEROS          TO PV-TOT-AFTER-RCPT.                            
           MOVE ZEROS          TO PV-TOT-CUTOFF-INTRANS.                        
           MOVE ZEROS          TO PV-TOT-CUTOFF-STR-EXPTED.                     
           MOVE ZEROS          TO PV-TOT-QTY.                                   
           MOVE ZEROS          TO PV-TOT-OLD-ONHAND-QTY.                        
                                                                                
W26600     MOVE 'TTL'          TO PV-LOC-NBR-X                                  
W26600     MOVE 'SUMMARY'      TO XMT00001-LOC-10-ABBR-NM                       
           PERFORM Z805-PRINT-HEADINGS.                                         
                                                                                
           MOVE ZEROS          TO PV-STR-NDX.                                   
           PERFORM C200-PRINT-STORE-TOTALS                                      
             VARYING PV-STR-NDX FROM 1 BY 1                                     
               UNTIL PV-STR-NDX IS GREATER THAN MAX-STORES.                     
                                                                                
      *** PRINT A STORE TOTALS SUMMARY PAGE TOTAL AFTER UNLOADING TBL.          
                                                                                
           MOVE SPACES                        TO D1-DETAIL-LINE.                
           MOVE 'TOTAL'                       TO D1-COMMENT.                    
           MOVE PV-TOT-ADJUSTMENT-QTY         TO D1-ADJUSTMENT-QTY.             
           MOVE PV-TOT-AFTER-RCPT             TO D1-AFTER-RCPT.                 
      *    MOVE PV-TOT-CUTOFF-INTRANS         TO D1-CUTOFF-INTRANS.             
           MOVE PV-TOT-CUTOFF-STR-EXPTED      TO D1-STR-EXPTED.                 
           MOVE PV-TOT-QTY                    TO D1-QTY.                        
           MOVE PV-TOT-OLD-ONHAND-QTY         TO D1-ONHAND-QTY.                 
                                                                                
           IF PC-LINE-COUNT > +59                                               
              PERFORM Z805-PRINT-HEADINGS                                       
           END-IF.                                                              
                                                                                
           MOVE D1-DETAIL-LINE           TO REPORT-LINE.                        
           MOVE 2                        TO PC-LINE-SPACE.                      
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           ADD 2                         TO PC-LINE-COUNT.                      
                                                                                
      *** CLOSE THE FILES.....                                                  
                                                                                
           CLOSE BOOK-INPUT-SUMMARY-FILE                                        
                 ADJ-PHY-INVENTORY-REPORT.                                      
                                                                                
      /****************************************************************         
      * C100-ACCUM-DEPT-TOTALS.                                                 
      *     REFORMAT THE STORE NUMBER AND THE DEPARTMENT NUMBER TO A            
      * NUMERIC VALUE TO BE USED TO SUBSCRIPT THE DEPARTMENT AND STORE          
      * TABLES. IF EITHER VALUE EXCEEDS 999, THE MODULE WILL ABEND AND          
      * AN THE JOB WILL STOP.                                                   
      *     CALCULATE THE FASHION SALES ADJUSTMENTS IN A SIMILAR                
      * FASHION AS INKUT117 DOES.                                               
      *     ACCUMUALTE THE VALUES IN THE DEPARTMENT TABLE AND THE STORE         
      * TABLE.                                                                  
      *****************************************************************         
       C100-ACCUM-DEPT-TOTALS.                                                  
           MOVE '*C100-ACCUM-DEPT-TOTALS.          ' TO PV-PARA                 
                                                                                
      *** ACCUMULATE TO THE DEPARTMENT TABLE.....                               
                                                                                
           MOVE IN016-DEPT       TO PV-DEPT-NMBR.                               
           INSPECT PV-DEPT-NMBR REPLACING ALL ' ' BY '0'.                       
                                                                                
           MOVE IN016-STORE      TO PV-STORE-NMBR.                              
           INSPECT PV-STORE-NMBR REPLACING ALL ' ' BY '0'.                      
                                                                                
           MOVE PV-DEPT-NMBR-NUM  TO PV-DEPT-SUB.                               
           IF PV-DEPT-SUB IS GREATER THAN PV-TAB-MAX                            
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY 'C100-ACCUM-DEPT-TOTALS.....   '                         
W26600         DISPLAY 'MORE THAN 9999 DEPARTMENTS EXIST'                       
               DISPLAY 'MUST EXPAND TABLE             '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY 'A B E N D                   * '                         
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *                                                                         
      * THIS COMPUTE STATEMENT WILL TAKE INTO CONSIDERATION BOTH                
      * MANIFESTED AND NON-MANIFESTED STORES.  MANIFESTED STORES WILL           
      * HAVE ZERO AFTER FLUSH RECEIPT AND NON-ZERO IN CUTOFF IN-TRANSIT.        
      * NON-MANIFESTED STORES WILL HAVE ZERO IN CUTOFF IN-TRANSIT               
      * AND NON-ZERO IN AFTER FLUSH RECEIPT.                                    
      *                                                                         
           COMPUTE PV-WRK-ADJUSTMENT-QTY = IN016-QTY -                          
                                           (IN016-OLD-ONHAND-QTY                
                                         -  IN016-AFTER-RECEIPTS                
                                         - (IN016-STR-EXP-OH-QTY -              
                                            IN016-CUTOFF-INTRANSIT)).           
                                                                                
           ADD PV-WRK-ADJUSTMENT-QTY TO TB-ADJUSTMENT-QTY (PV-DEPT-SUB).        
           ADD IN016-QTY             TO TB-QTY (PV-DEPT-SUB).                   
           ADD IN016-OLD-ONHAND-QTY  TO TB-OLD-ONHAND-QTY (PV-DEPT-SUB).        
           ADD IN016-AFTER-RECEIPTS  TO TB-AFTER-RCPT (PV-DEPT-SUB).            
      *    ADD IN016-CUTOFF-INTRANSIT TO TB-CUTOFF-INTRANS(PV-DEPT-SUB).        
           COMPUTE PV-TB-STR-EXPTED =                                           
                 (IN016-STR-EXP-OH-QTY - IN016-CUTOFF-INTRANSIT).               
           ADD PV-TB-STR-EXPTED TO TB-CUTOFF-STR-EXPTED(PV-DEPT-SUB).           
                                                                                
           ADD PV-WRK-ADJUSTMENT-QTY TO PV-ADJUSTMENT-QTY.                      
           ADD IN016-QTY             TO PV-QTY.                                 
           ADD IN016-OLD-ONHAND-QTY  TO PV-OLD-ONHAND-QTY.                      
           ADD IN016-AFTER-RECEIPTS  TO PV-AFTER-RCPT.                          
      *    ADD IN016-CUTOFF-INTRANSIT TO PV-CUTOFF-INTRANS.                     
           COMPUTE PV-STR-EXPTED =                                              
                 (IN016-STR-EXP-OH-QTY - IN016-CUTOFF-INTRANSIT).               
           ADD PV-STR-EXPTED TO PV-CUTOFF-STR-EXPTED.                           
                                                                                
      *** ACCUMULATE TO THE STORE TABLE.....                                    
                                                                                
           MOVE PV-STORE-NMBR-NUM TO PV-STR-NDX.                                
           IF PV-STR-NDX IS GREATER THAN MAX-STORES                             
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY 'C100-ACCUM-DEPT-TOTALS.....   '                         
W26600         DISPLAY 'MORE THAN 9999 STORES EXIST    '                        
               DISPLAY 'MUST EXPAND TABLE             '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY '* * * * * * * * * * * * * * * '                         
               DISPLAY 'A B E N D                   * '                         
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           ADD PV-WRK-ADJUSTMENT-QTY                                            
                         TO TB-STR-ADJUSTMENT-QTY (PV-STR-NDX).                 
           ADD IN016-QTY            TO TB-STR-QTY (PV-STR-NDX).                 
           ADD IN016-OLD-ONHAND-QTY TO TB-STR-OLD-ONHAND (PV-STR-NDX).          
           ADD IN016-AFTER-RECEIPTS TO TB-STR-AFTER-RCPT (PV-STR-NDX).          
      *    ADD IN016-CUTOFF-INTRANSIT                                           
      *        TO TB-STR-CUTOFF-INTRANS(PV-STR-NDX).                            
           COMPUTE PV-STR-TB-STR-EXPTED =                                       
                 (IN016-STR-EXP-OH-QTY - IN016-CUTOFF-INTRANSIT).               
           ADD PV-STR-TB-STR-EXPTED                                             
                                TO TB-STR-CUTOFF-STR-EXPTED(PV-STR-NDX).        
                                                                                
      /****************************************************************         
      * C200-PRINT-STORE-TOTALS.                                                
      *     PRINT THE SUMMARIZED INVENTORY REPORT STORE SUMMARY PAGE(S).        
      * THE NUMBERS ARE ACCUMULATED AS WE DEVELOP THE SUMMARY AND DETAIL        
      * REPORTS. THE PAGE IS CREATED AT END OF FILE TIME.                       
      *****************************************************************         
       C200-PRINT-STORE-TOTALS.                                                 
           MOVE '*C200-PRINT-STORE-TOTALS.         ' TO PV-PARA                 
                                                                                
           IF TB-STR-ADJUSTMENT-QTY (PV-STR-NDX) NOT EQUAL ZEROS OR             
              TB-STR-AFTER-RCPT (PV-STR-NDX) NOT EQUAL ZEROS     OR             
              TB-STR-CUTOFF-INTRANS(PV-STR-NDX) NOT EQUAL ZEROS  OR             
              TB-STR-CUTOFF-STR-EXPTED(PV-STR-NDX) NOT EQUAL ZEROS  OR          
              TB-STR-QTY (PV-STR-NDX) NOT EQUAL ZEROS            OR             
              TB-STR-OLD-ONHAND (PV-STR-NDX) NOT EQUAL ZEROS                    
                                                                                
               IF PC-LINE-COUNT > +59                                           
                   PERFORM Z805-PRINT-HEADINGS                                  
               END-IF                                                           
                                                                                
               MOVE PV-STR-NDX        TO D1-DEPT-NBR                            
PK1383*        MOVE SPACE             TO D1-DEPT-1                              
               MOVE SPACES            TO D1-COMMENT                             
               MOVE TB-STR-ADJUSTMENT-QTY (PV-STR-NDX)                          
                                      TO D1-ADJUSTMENT-QTY                      
               MOVE TB-STR-AFTER-RCPT (PV-STR-NDX)                              
                                      TO D1-AFTER-RCPT                          
      *        MOVE TB-STR-CUTOFF-INTRANS(PV-STR-NDX)                           
      *                               TO D1-CUTOFF-INTRANS                      
               MOVE TB-STR-CUTOFF-STR-EXPTED(PV-STR-NDX)                        
                                      TO D1-STR-EXPTED                          
               MOVE TB-STR-OLD-ONHAND (PV-STR-NDX)                              
                                      TO D1-ONHAND-QTY                          
               MOVE TB-STR-QTY (PV-STR-NDX)                                     
                                      TO D1-QTY                                 
                                                                                
               MOVE D1-DETAIL-LINE    TO REPORT-LINE                            
               MOVE 1                 TO PC-LINE-SPACE                          
               PERFORM Z700-WRITE-REPORT-LINE                                   
               ADD 1                  TO PC-LINE-COUNT                          
                                                                                
               ADD TB-STR-ADJUSTMENT-QTY (PV-STR-NDX)                           
                                      TO PV-TOT-ADJUSTMENT-QTY                  
               ADD TB-STR-AFTER-RCPT (PV-STR-NDX)                               
                                      TO PV-TOT-AFTER-RCPT                      
      *        ADD TB-STR-CUTOFF-INTRANS(PV-STR-NDX)                            
      *                               TO PV-TOT-CUTOFF-INTRANS                  
               ADD TB-STR-CUTOFF-STR-EXPTED(PV-STR-NDX)                         
                                      TO PV-TOT-CUTOFF-STR-EXPTED               
               ADD TB-STR-QTY (PV-STR-NDX)                                      
                                      TO PV-TOT-QTY                             
               ADD TB-STR-OLD-ONHAND (PV-STR-NDX)                               
                                      TO PV-TOT-OLD-ONHAND-QTY                  
                                                                                
           END-IF.                                                              
                                                                                
      /****************************************************************         
      * Z200-READ-BOOKING-INPUT-FILE....                                        
      *****************************************************************         
       Z200-READ-BOOKING-INPUT-FILE.                                            
           MOVE '*Z200-READ-BOOKING-INPUT-FILE     *' TO PV-PARA                
                                                                                
           READ BOOK-INPUT-SUMMARY-FILE INTO IN016-PHYSICAL-COUNT-RECORD        
               AT END  MOVE 'Y'  TO WS-PHYINV-DONE-SW.                          
                                                                                
           IF FIRST-TIME-THRU                                                   
               MOVE IN016-STORE  TO PV-STORE-NMBR-SAVE                          
               MOVE 'Y'          TO WS-FIRST-TIME-SW.                           
                                                                                
      /****************************************************************         
      * Z700-WRITE-REPORT-LINE.                                                 
      *****************************************************************         
       Z700-WRITE-REPORT-LINE.                                                  
                                                                                
           IF PC-LINE-SPACE IS EQUAL TO 999                                     
               WRITE REPORT-LINE AFTER ADVANCING PAGE                           
           ELSE                                                                 
               WRITE REPORT-LINE AFTER ADVANCING PC-LINE-SPACE LINES            
           END-IF.                                                              
                                                                                
      /****************************************************************         
      * Z800-PRINT-HEADINGS.                                                    
      *     USED TO PRINT HEADINGS FOR THE SUMMARIZED INVENTORY AUDIT           
      * REPORT. THE ROUTINE GRABS INFORMATION OFF THE LOCATION TABLE            
      * XMT_LOC.                                                                
      *****************************************************************         
       Z800-PRINT-HEADINGS.                                                     
           ADD 1              TO PC-PAGE-COUNT.                                 
                                                                                
           MOVE PC-PAGE-COUNT TO DP133O-PAGE-NUMBER.                            
           MOVE 1             TO DP133O-REPORT-NUMBER.                          
           MOVE DP133O-STANDRD-HEADER-133-COLS TO REPORT-LINE.                  
           MOVE 999           TO PC-LINE-SPACE.                                 
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           MOVE 1             TO PC-LINE-COUNT.                                 
                                                                                
           MOVE H1-HEADING TO REPORT-LINE.                                      
           MOVE 1             TO PC-LINE-SPACE.                                 
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
W26600     MOVE PV-LOC-NBR-X             TO H1B-STORE.                          
W26600     MOVE XMT00001-LOC-10-ABBR-NM  TO H1B-STORE-NAME.                     
           MOVE H1B-HEADING TO REPORT-LINE.                                     
           MOVE 1           TO PC-LINE-SPACE.                                   
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
           MOVE H2-HEADING TO REPORT-LINE.                                      
           MOVE 1          TO PC-LINE-SPACE.                                    
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
           MOVE H3-HEADING TO REPORT-LINE.                                      
           MOVE 1          TO PC-LINE-SPACE.                                    
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
           MOVE SPACES     TO REPORT-LINE.                                      
           MOVE 1          TO PC-LINE-SPACE.                                    
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
      /****************************************************************         
      * Z805-PRINT-HEADINGS.                                                    
      *     USED TO PRINT HEADINGS FOR THE SUMMARIZED INVENTORY AUDIT           
      * REPORT - STORE TOTALS PAGE.                                             
      *****************************************************************         
       Z805-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1              TO PC-PAGE-COUNT.                                 
                                                                                
           MOVE PC-PAGE-COUNT TO DP133O-PAGE-NUMBER.                            
           MOVE 1             TO DP133O-REPORT-NUMBER.                          
           MOVE DP133O-STANDRD-HEADER-133-COLS TO REPORT-LINE.                  
           MOVE 999           TO PC-LINE-SPACE.                                 
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           MOVE 1             TO PC-LINE-COUNT.                                 
                                                                                
           MOVE H1-HEADING TO REPORT-LINE.                                      
           MOVE 1             TO PC-LINE-SPACE.                                 
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
W26600     MOVE PV-LOC-NBR-X             TO H1B-STORE.                          
W26600     MOVE XMT00001-LOC-10-ABBR-NM  TO H1B-STORE-NAME.                     
           MOVE H1B-HEADING TO REPORT-LINE.                                     
           MOVE 1             TO PC-LINE-SPACE.                                 
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
           MOVE H2B-HEADING TO REPORT-LINE.                                     
           MOVE 1             TO PC-LINE-SPACE.                                 
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
           MOVE H3-HEADING TO REPORT-LINE.                                      
           MOVE 1             TO PC-LINE-SPACE.                                 
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
           MOVE SPACES     TO REPORT-LINE.                                      
           MOVE 1          TO PC-LINE-SPACE.                                    
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           ADD 1              TO PC-LINE-COUNT.                                 
                                                                                
      /****************************************************************         
      * Z900-SQL-ABEND.                                                         
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKRP283'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARA.                              
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBIAGE.               
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
