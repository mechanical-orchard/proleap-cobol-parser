       IDENTIFICATION DIVISION.                                         00001012
      ******************************************************************00002012
                                                                        00003012
       PROGRAM-ID.   XSKUP405.                                          00004027
       AUTHOR.       COGNIZANT.                                         00005030
       INSTALLATION. KOHLS DEPARTMENT STORES.                           00006012
       DATE-WRITTEN. 02/25/2015.                                        00007012
       DATE-COMPILED.                                                   00008012
                                                                        00009012
      ******************************************************************00009112
      * THIS PROGRAM WILL DELETE RECORD FROM TABLE XST_SKU_LOC AND      00009227
      * XST_SKU_LOC_ATTR. THE RECORDS ARE READ FROM INPUT FILE          00009327
      * AND THEN THEY ARE DELETED.                                      00009427
      ******************************************************************00009612
      * CHANGE HISTORY:                                                 00009712
      * WR/PBI         DATE              DESCRIPTION OF CHANGES         00009812
      * CHG0111864     06/12/2015        NEW PROGRAM                    00009929
      ******************************************************************00010012
      *                                                                 00010112
      ******************************************************************00010212
                                                                        00010312
      ******************************************************************00010412
       ENVIRONMENT DIVISION.                                            00010512
      ******************************************************************00010612
                                                                        00010712
       CONFIGURATION SECTION.                                           00010812
       SOURCE-COMPUTER.                        IBM-3090.                00010912
       OBJECT-COMPUTER.                        IBM-3090.                00011012
                                                                        00011112
       INPUT-OUTPUT SECTION.                                            00011212
       FILE-CONTROL.                                                    00011312
                                                                        00011412
           SELECT SKU-LOC-DEL-FILE      ASSIGN TO INP01.                00011527
                                                                        00011617
           SELECT SKU-LOC-ATTR-DEL-FILE ASSIGN TO INP02.                00011717
                                                                        00011912
      ******************************************************************00012012
       DATA DIVISION.                                                   00012112
      ******************************************************************00012212
       FILE SECTION.                                                    00012312
                                                                        00012912
       FD  SKU-LOC-DEL-FILE                                             00013017
           RECORDING MODE IS F                                          00014017
           LABEL RECORDS ARE STANDARD                                   00014117
           BLOCK CONTAINS 0 RECORDS.                                    00014217
       01  SKU-LOC-DEL-RECORD               PIC X(080).                 00014326
                                                                        00014417
       FD  SKU-LOC-ATTR-DEL-FILE                                        00014517
           RECORDING MODE IS F                                          00014617
           LABEL RECORDS ARE STANDARD                                   00014717
           BLOCK CONTAINS 0 RECORDS.                                    00014817
       01  SKU-LOC-ATTR-DEL-RECORD          PIC X(080).                 00014926
                                                                        00015012
      ******************************************************************00015112
       WORKING-STORAGE SECTION.                                         00015212
      ******************************************************************00015312
      * PROGRAM VARIABLES.                                             *00015412
      ******************************************************************00015512
       01  PV-PROGRAM-VARIABLES.                                        00015612
           05 PV-DEADLOCK-COUNTER           PIC S9(04)  VALUE +0.       00015712
           05 PV-SQLCODE-DISPLAY            PIC  +(8)9  VALUE ZEROES.   00015812
           05 PV-DB2-OPERATION-ATTEMPTED    PIC  X(30)  VALUE SPACES.   00015912
           05 PV-ZERO-STR-NBR               PIC  9(05)  VALUE ZEROES.   00016012
           05 PV-ZERO-PRMPT-PRIC-IND        PIC  X(01)  VALUE SPACES.   00016112
           05 PV-HIST1-INTR-UPC-ID          PIC  S9(09) VALUE +0 COMP.  00016222
           05 PV-HIST2-INTR-UPC-ID          PIC  S9(09) VALUE +0 COMP.  00016322
           05 PV-NZERO-PRMPT-PRIC-IND       PIC  X(01)  VALUE SPACES.   00016422
           05 PV-COUNT                      PIC  S9(04) VALUE +0 COMP.  00017512
                                                                        00017612
           05 PV-ZERO-LEVEL-REC.                                        00017712
              10 PV-ZERO-INTR-UPC-ID        PIC  S9(09) COMP.           00017812
              10 PV-ZERO-LOC-NBR            PIC  S9(04) COMP.           00017912
              10 PV-ZERO-BEG-TMST           PIC   X(26).                00018012
              10 PV-ZERO-END-TMST           PIC   X(26).                00018112
              10 PV-ZERO-UNT-RTL-AMT        PIC  S9(7)V9(2) COMP-3.     00018212
              10 PV-ZERO-UNT-COST-AMT       PIC  S9(7)V9(3) COMP-3.     00018312
              10 PV-ZERO-LOC-SKU-STAT-CDE   PIC   X(02).                00018412
              10 PV-ZERO-UPC-STAT-CHG-DTE   PIC   X(10).                00018512
              10 PV-ZERO-UNT-RTL-CHG-DTE    PIC   X(10).                00018612
              10 PV-ZERO-GP-PRIC-ID         PIC  S9(09) COMP.           00018712
              10 PV-ZERO-GP-RTL-QTY         PIC  S9(04) COMP.           00018812
              10 PV-ZERO-GP-RTL-AMT         PIC  S9(7)V9(2) COMP-3.     00018912
              10 PV-ZERO-LAST-UPD-TMST      PIC   X(26).                00019012
              10 PV-ZERO-SOR-LAST-MSG-TMST  PIC   X(26).                00019112
              10 PV-ZERO-MKDN-CDE           PIC   X(01).                00019212
              10 PV-ZERO-ORIG-CLR-DTE       PIC   X(10).                00019312
                                                                        00019412
           05 PV-NZERO-LEVEL-REC.                                       00019512
              10 PV-NZERO-INTR-UPC-ID       PIC  S9(9) COMP.            00019612
              10 PV-NZERO-LOC-NBR           PIC  S9(4) COMP.            00019712
              10 PV-NZERO-BEG-TMST          PIC   X(26).                00019812
              10 PV-NZERO-END-TMST          PIC   X(26).                00019912
              10 PV-NZERO-UNT-RTL-AMT       PIC  S9(7)V9(2) COMP-3.     00020012
              10 PV-NZERO-UNT-COST-AMT      PIC  S9(7)V9(3) COMP-3.     00020112
              10 PV-NZERO-LOC-SKU-STAT-CDE  PIC   X(2).                 00020212
              10 PV-NZERO-UPC-STAT-CHG-DTE  PIC   X(10).                00020312
              10 PV-NZERO-UNT-RTL-CHG-DTE   PIC   X(10).                00020412
              10 PV-NZERO-GP-PRIC-ID        PIC  S9(9) COMP.            00020512
              10 PV-NZERO-GP-RTL-QTY        PIC  S9(4) COMP.            00020612
              10 PV-NZERO-GP-RTL-AMT        PIC  S9(7)V9(2) COMP-3.     00020712
              10 PV-NZERO-LAST-UPD-TMST     PIC   X(26).                00020812
              10 PV-NZERO-SOR-LAST-MSG-TMST PIC   X(26).                00020912
              10 PV-NZERO-MKDN-CDE          PIC   X(1).                 00021012
              10 PV-NZERO-ORIG-CLR-DTE      PIC   X(10).                00021112
                                                                        00021212
       01  CC-INP1-FILE.                                                00021317
           05  CC-INP1-XS-INTR-UPC-ID       PIC  S9(9) VALUE +0 COMP.   00021527
           05  FILLER                       PIC  X(01) VALUE SPACES.    00021627
           05  CC-INP1-XS-LOC-NBR           PIC  S9(9) VALUE +0 COMP.   00021727
           05  FILLER                       PIC  X(02) VALUE SPACES.    00021827
           05  CC-INP1-BEG-TMST             PIC  X(26) VALUE SPACES.    00021927
                                                                        00022117
       01  CC-INP2-FILE.                                                00022217
           05  CC-INP2-XS-INTR-UPC-ID       PIC  S9(9) VALUE +0 COMP.   00022427
           05  FILLER                       PIC  X(01) VALUE SPACES.    00022527
           05  CC-INP2-XS-LOC-NBR           PIC  S9(9) VALUE +0 COMP.   00022627
           05  FILLER                       PIC  X(02) VALUE SPACES.    00022727
           05  CC-INP2-PRMPT-PRIC-IND       PIC  X(01) VALUE SPACES.    00022827
                                                                        00023012
      ******************************************************************00023112
      * PROGRAM SWITCHES.                                              *00023212
      ******************************************************************00023312
       01  PS-PROGRAM-SWITCHES.                                         00023412
           05  PS-END-OF-LOC-FILE-SW         PIC X(01)  VALUE 'N'.      00023515
               88  PS-END-OF-LOC-FILE                   VALUE 'Y'.      00023615
               88  PS-NOT-END-OF-LOC-FILE               VALUE 'N'.      00023715
                                                                        00023812
           05  PS-END-OF-LOCATRT-FILE-SW     PIC X(01)  VALUE 'N'.      00023915
               88  PS-END-OF-LOCATTR-FILE               VALUE 'Y'.      00024015
               88  PS-NOT-END-OF-LOCATTR-FILE           VALUE 'N'.      00024115
                                                                        00024315
      ******************************************************************00024415
      * PROGRAM ACCUMULATORS.                                          *00024515
      ******************************************************************00024615
       01  PA-PROGRAM-ACCUMULATORS.                                     00024715
           05  PA-SKU-LOC-READ              PIC S9(09) COMP-3           00025018
                                                       VALUE ZEROES.    00025115
           05  PA-SKU-LOC-ATTR-READ         PIC S9(09) COMP-3           00025218
                                                       VALUE ZEROES.    00025318
           05  PA-SKU-LOC-DELETED           PIC S9(09) COMP-3           00025418
                                                       VALUE ZEROES.    00025515
           05  PA-SKU-LOC-UPDATED           PIC S9(09) COMP-3           00025619
                                                       VALUE ZEROES.    00025719
           05  PA-SKU-LOC-ATTR-DELETED      PIC S9(09) COMP-3           00025819
                                                       VALUE ZEROES.    00025919
           05  PA-SKU-LOC-ATTR-UPDATED      PIC S9(09) COMP-3           00026019
                                                       VALUE ZEROES.    00026119
      ******************************************************************00026616
      *                           CONSTANTS                             00026716
      ******************************************************************00026816
       01  PROGRAM-CONSTANTS.                                           00026916
           05  PC-MAX-RETRIES               PIC S9(04) VALUE +3         00027016
                                                       COMP SYNC.       00027116
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'XSKUP405'.00027227
           05  PC-MAX-DEADLOCKS             PIC  S9(04) VALUE +3        00027318
                                                        COMP SYNC.      00027418
                                                                        00027516
      ******************************************************************00027616
      *                          VARIABLES                              00027716
      ******************************************************************00027816
       01  PROGRAM-VARIABLES.                                           00027916
           05  PV-SQL-SUB                   PIC  9(03) VALUE ZERO.      00028016
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.     00028116
           05  PV-DB2-OPER-ATTEMPTED        PIC  X(30) VALUE SPACE.     00028216
           05  PV-OPERATION-MSG             PIC  X(35) VALUE SPACE.     00028316
           05  PV-RET-CODE                  PIC  9(04) VALUE ZEROS.     00028416
                                                                        00028516
                                                                        00028616
       01  ABEND-CODE                       PIC S9(04) COMP SYNC        00028715
                                                       VALUE 0.         00028815
           88  ABEND-4001-CC-MISSING-ERR               VALUE +4001.     00028915
           88  ABEND-4013-DB2-ERROR                    VALUE +4013.     00029016
           88  ABEND-4002-READ-ERROR                   VALUE +4002.     00029116
                                                                        00029215
      ***************************************************************** 00029315
      * XST_SKU_LOC TABLE DCLGEN.                                       00029415
      ***************************************************************** 00029515
           EXEC SQL                                                     00029615
               INCLUDE XST00008                                         00029715
           END-EXEC.                                                    00029815
                                                                        00029915
      ***************************************************************** 00030015
      * XST_SKU_LOC_ATTR TABLE DCLGEN.                                  00030115
      ***************************************************************** 00030215
           EXEC SQL                                                     00030315
               INCLUDE XST00041                                         00030415
           END-EXEC.                                                    00030515
                                                                        00030615
      ***************************************************************** 00030715
      * TUPCPLS TABLE DCLGEN.                                           00030815
      ***************************************************************** 00030915
           EXEC SQL                                                     00031015
               INCLUDE TUPCPLS                                          00031115
           END-EXEC.                                                    00031215
                                                                        00031315
      ***************************************************************** 00031415
      * STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA.                00031515
      ***************************************************************** 00031615
           EXEC SQL                                                     00031715
               INCLUDE SQLCA                                            00031815
           END-EXEC.                                                    00031915
                                                                        00032015
      ***************************************************************** 00032115
      * COMMUNICATION WORK AREA.                                        00032215
      ***************************************************************** 00032315
           COPY SQLCA2.                                                 00032415
                                                                        00032515
      ***************************************************************** 00032615
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004.        00032715
      ***************************************************************** 00032815
           COPY DPWS004.                                                00032915
                                                                        00033015
      ***************************************************************** 00033115
       LINKAGE SECTION.                                                 00033215
                                                                        00033315
       PROCEDURE DIVISION.                                              00033415
                                                                        00033515
      ******************************************************************00033615
      * 0000-MAINLINE-PARA.                                            *00033715
      ******************************************************************00033815
       0000-MAINLINE-PARA.                                              00033915
                                                                        00034015
           PERFORM A000-INITIALIZE.                                     00034115
                                                                        00034215
           PERFORM B100-DELETE-SKU-LOC-PARA                             00034315
             UNTIL PS-END-OF-LOC-FILE.                                  00034430
                                                                        00034515
           PERFORM B200-DELETE-SKU-LOC-ATTR-PARA                        00034615
             UNTIL PS-END-OF-LOCATTR-FILE.                              00034730
                                                                        00034815
           PERFORM C000-EOJ-ROUTINE.                                    00034915
                                                                        00035015
           STOP RUN.                                                    00035115
                                                                        00035215
      ******************************************************************00035330
      * INITIALIZATION                                                  00035430
      ******************************************************************00035530
       A000-INITIALIZE.                                                 00035630
                                                                        00035730
            OPEN INPUT SKU-LOC-DEL-FILE                                 00035830
                       SKU-LOC-ATTR-DEL-FILE                            00035930
            MOVE ZEROS                     TO PV-ZERO-STR-NBR           00036030
                                                                        00036130
            PERFORM C100-READ-SKU-LOC-RECORD                            00036230
            PERFORM C200-READ-SKU-LOC-ATTR-RECORD.                      00036330
                                                                        00036430
      ******************************************************************00036515
      * PROCESS SKU LOC RECORDS                                         00036630
      ******************************************************************00036715
       B100-DELETE-SKU-LOC-PARA.                                        00036815
                                                                        00036916
            MOVE CC-INP1-XS-INTR-UPC-ID     TO XST00008-INTR-UPC-ID     00037030
            MOVE CC-INP1-XS-LOC-NBR         TO XST00008-LOC-NBR         00037130
            MOVE CC-INP1-BEG-TMST           TO XST00008-BEG-TMST        00037230
                                                                        00037330
            PERFORM D100-DELETE-SKU-LOC                                 00037415
                                                                        00037530
            IF  CC-INP1-XS-INTR-UPC-ID NOT EQUAL PV-HIST1-INTR-UPC-ID   00037621
CHG             MOVE ZEROS                  TO XST00008-LOC-NBR         00037730
                PERFORM D150-UPDATE-SKU-LOC                             00037821
                MOVE CC-INP1-XS-INTR-UPC-ID TO PV-HIST1-INTR-UPC-ID     00037921
            END-IF                                                      00038020
                                                                        00038130
            PERFORM C100-READ-SKU-LOC-RECORD.                           00038217
                                                                        00038315
      ******************************************************************00038415
      * PROCESS SKU LOC ATTR RECORDS                                    00038530
      ******************************************************************00038615
       B200-DELETE-SKU-LOC-ATTR-PARA.                                   00038715
                                                                        00038828
            MOVE CC-INP2-XS-INTR-UPC-ID   TO XST00041-INTR-UPC-ID       00038930
            MOVE CC-INP2-XS-LOC-NBR       TO XST00041-LOC-NBR           00039030
            MOVE CC-INP2-PRMPT-PRIC-IND   TO XST00041-PRMPT-FOR-PRIC-IND00039130
                                                                        00039230
            PERFORM D200-DELETE-SKU-LOC-ATTR                            00039315
                                                                        00039430
            IF  CC-INP2-XS-INTR-UPC-ID NOT EQUAL PV-HIST2-INTR-UPC-ID   00039521
CHG             MOVE ZEROS                TO XST00041-LOC-NBR           00039631
                PERFORM D250-UPDATE-SKU-LOC-ATTR                        00039722
                MOVE CC-INP2-XS-INTR-UPC-ID TO PV-HIST2-INTR-UPC-ID     00039821
            END-IF                                                      00039921
                                                                        00040030
            PERFORM C200-READ-SKU-LOC-ATTR-RECORD.                      00040117
                                                                        00040215
      ******************************************************************00040315
      *  READ SKU LOC FILE                                              00040430
      ******************************************************************00040515
       C100-READ-SKU-LOC-RECORD.                                        00040615
                                                                        00040715
            INITIALIZE CC-INP1-FILE                                     00040817
                                                                        00040930
            READ SKU-LOC-DEL-FILE         INTO CC-INP1-FILE             00041030
              AT END SET PS-END-OF-LOC-FILE TO TRUE                     00041130
              NOT AT END                                                00041316
              ADD +1 TO  PA-SKU-LOC-READ                                00041418
            END-READ.                                                   00041516
                                                                        00041616
      ******************************************************************00041715
      * READ SKU LOC ATTR FILE                                          00041830
      ******************************************************************00041915
       C200-READ-SKU-LOC-ATTR-RECORD.                                   00042015
                                                                        00042115
            INITIALIZE CC-INP2-FILE                                     00042217
                                                                        00042330
            READ SKU-LOC-ATTR-DEL-FILE INTO CC-INP2-FILE                00042417
              AT END SET PS-END-OF-LOCATTR-FILE TO TRUE                 00042530
              NOT AT END                                                00042616
              ADD +1 TO PA-SKU-LOC-ATTR-READ                            00042718
            END-READ.                                                   00042816
                                                                        00042916
      ******************************************************************00043015
      * DELETE A ROW FROM THE SKU-LOC TABLE THAT MATCHES THE INPUT KEY  00043115
      ******************************************************************00043215
       D100-DELETE-SKU-LOC.                                             00044015
                                                                        00050011
           PERFORM WITH TEST AFTER                                      00060011
               VARYING PV-SQL-SUB                                       00070011
               FROM 1 BY 1                                              00080011
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       00090011
                      OR SQLCODE = -911                                 00100011
                      OR SQLCODE = +100                                 00110011
                      OR SQLCODE = 0)                                   00120011
                                                                        00130011
           EXEC SQL                                                     00140015
               DELETE                                                   00150015
                 FROM XST_SKU_LOC                                       00160030
                WHERE INTR_UPC_ID  = :XST00008-INTR-UPC-ID              00170030
                  AND LOC_NBR      = :XST00008-LOC-NBR                  00180030
                  AND BEG_TMST     = :XST00008-BEG-TMST                 00190030
                  AND END_TMST IS NULL                                  00191030
           END-EXEC                                                     00200030
                                                                        00210015
           EVALUATE SQLCODE                                             00220015
               WHEN 0                                                   00230015
                   ADD +1             TO PA-SKU-LOC-DELETED             00240018
                   MOVE ZERO          TO PV-DEADLOCK-COUNTER            00250015
               WHEN +100                                                00260015
               DISPLAY 'NO ROWS TO DELETE IN SKU_LOC FOR INTR_UPC_ID :' 00261015
                                         XST00008-INTR-UPC-ID           00262015
                   MOVE ZERO          TO PV-DEADLOCK-COUNTER            00270015
               WHEN -911                                                00280015
                   ADD +1             TO PV-DEADLOCK-COUNTER            00290015
                   DISPLAY 'DEADLOCK -911 IN B100-DELETE-SKU-LOC'       00300015
                   IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS            00310015
                       MOVE 'D100-DELETE-SKU-LOC'                       00320019
                                      TO PV-PARAGRAPH-NAME              00330015
                       PERFORM 9000-DEADLOCK-ERROR                      00340015
                   END-IF                                               00350015
               WHEN OTHER                                               00360015
                   MOVE 'D100-DELETE-SKU-LOC'                           00370019
                                      TO PV-PARAGRAPH-NAME              00380015
                   MOVE 'DELETING XST_SKU_LOC TABLE'                    00390018
                                      TO PV-DB2-OPER-ATTEMPTED          00391018
                   DISPLAY 'KEY -' PV-NZERO-INTR-UPC-ID  '- '           00400015
                                   PV-NZERO-LOC-NBR      '- '           00410015
                                   PV-NZERO-BEG-TMST                    00420015
                   PERFORM 9100-SQL-ABEND                               00430018
             END-EVALUATE                                               00440015
           END-PERFORM.                                                 00450015
                                                                        00460015
           IF PV-SQL-SUB > PC-MAX-RETRIES                               00470015
             MOVE 'D100-DELETE-SKU-LOC' TO PV-PARAGRAPH-NAME            00480019
             MOVE 'VERIFY RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED    00490015
             PERFORM 9100-SQL-ABEND                                     00500018
           END-IF.                                                      00510015
                                                                        00510128
      ******************************************************************00511019
      * UPDATE LAST UPD TMST TO SEND THE UPDATES TO STORES              00512019
      ******************************************************************00513019
       D150-UPDATE-SKU-LOC.                                             00514019
                                                                        00515019
           PERFORM WITH TEST AFTER                                      00516019
               VARYING PV-SQL-SUB                                       00517019
               FROM 1 BY 1                                              00518032
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       00519019
                      OR SQLCODE = -911                                 00519119
                      OR SQLCODE = +100                                 00519219
                      OR SQLCODE = 0)                                   00519319
                                                                        00519419
           EXEC SQL                                                     00519519
              UPDATE XST_SKU_LOC                                        00519619
                 SET LAST_UPD_TMST = CURRENT TIMESTAMP                  00519930
               WHERE INTR_UPC_ID   = :XST00008-INTR-UPC-ID              00520030
                 AND LOC_NBR       = :XST00008-LOC-NBR                  00520130
                 AND END_TMST IS NULL                                   00520330
           END-EXEC                                                     00520530
                                                                        00521319
           EVALUATE SQLCODE                                             00521430
               WHEN 0                                                   00521519
                   ADD +1             TO PA-SKU-LOC-UPDATED             00521619
                   MOVE ZERO          TO PV-DEADLOCK-COUNTER            00521719
               WHEN +100                                                00521819
               DISPLAY 'NO ROWS TO UPDATE IN SKU_LOC FOR INTR_UPC_ID :' 00521919
                                         XST00008-INTR-UPC-ID           00522019
                   MOVE ZERO          TO PV-DEADLOCK-COUNTER            00522119
               WHEN -911                                                00522219
                   ADD +1             TO PV-DEADLOCK-COUNTER            00522319
                   DISPLAY 'DEADLOCK -911 IN B100-DELETE-SKU-LOC'       00522419
                   IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS            00522519
                       MOVE 'D150-UPDATE-SKU-LOC'                       00522619
                                      TO PV-PARAGRAPH-NAME              00522719
                       PERFORM 9000-DEADLOCK-ERROR                      00522819
                   END-IF                                               00522919
               WHEN OTHER                                               00523019
                   MOVE 'D150-UPDATE-SKU-LOC'                           00523119
                                      TO PV-PARAGRAPH-NAME              00523219
                   MOVE 'UPDATING XST_SKU_LOC TABLE'                    00523319
                                      TO PV-DB2-OPER-ATTEMPTED          00523419
                   DISPLAY 'KEY -' PV-NZERO-INTR-UPC-ID  '- '           00523519
                                   PV-NZERO-LOC-NBR      '- '           00523619
                                   PV-NZERO-BEG-TMST                    00523719
                   PERFORM 9100-SQL-ABEND                               00523819
             END-EVALUATE                                               00523919
           END-PERFORM.                                                 00524019
                                                                        00524119
           IF PV-SQL-SUB > PC-MAX-RETRIES                               00524219
             MOVE 'D150-UPDATE-SKU-LOC' TO PV-PARAGRAPH-NAME            00524319
             MOVE 'VERIFY RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED    00524419
             PERFORM 9100-SQL-ABEND                                     00524519
           END-IF.                                                      00524619
                                                                        00525019
      ******************************************************************00530011
      * DELETE A ROW FROM THE SKU-LOC TABLE THAT MATCHES THE INPUT KEY  00540011
      ******************************************************************00550011
       D200-DELETE-SKU-LOC-ATTR.                                        00560015
                                                                        00570011
           PERFORM WITH TEST AFTER                                      00580011
               VARYING PV-SQL-SUB                                       00590011
               FROM 1 BY 1                                              00600011
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       00610011
                      OR SQLCODE = -911                                 00620011
                      OR SQLCODE = +100                                 00630011
                      OR SQLCODE = 0)                                   00640011
                                                                        00650011
             EXEC SQL                                                   00660011
                 DELETE                                                 00670011
                   FROM XST_SKU_LOC_ATTR                                00680017
                  WHERE INTR_UPC_ID  = :XST00041-INTR-UPC-ID            00690030
                   AND LOC_NBR       = :XST00041-LOC-NBR                00700030
                   AND PRMPT_FOR_PRIC_IND = :XST00041-PRMPT-FOR-PRIC-IND00710030
             END-EXEC                                                   00720030
                                                                        00730011
           EVALUATE SQLCODE                                             00740015
           WHEN 0                                                       00750015
                 ADD +1             TO PA-SKU-LOC-ATTR-DELETED          00760031
                 MOVE ZERO          TO PV-DEADLOCK-COUNTER              00770015
           WHEN +100                                                    00780015
           DISPLAY 'NOROWS TO DELETE IN SKU_LOC_ATTR FOR INTR_UPC_ID :' 00781015
                                       XST00041-INTR-UPC-ID             00782015
                 MOVE ZERO          TO PV-DEADLOCK-COUNTER              00790015
           WHEN -911                                                    00800015
                 ADD +1             TO PV-DEADLOCK-COUNTER              00810015
                 DISPLAY 'DEADLOCK -911 IN D200-DELETE-SKU-LOC-ATTR'    00820019
                 IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS              00830015
                     MOVE 'D200-DELETE-SKU-LOC-ATTR'                    00840019
                                    TO PV-PARAGRAPH-NAME                00850015
                     PERFORM 9000-DEADLOCK-ERROR                        00860015
                 END-IF                                                 00870015
           WHEN OTHER                                                   00880015
                   MOVE 'D200-DELETE-SKU-LOC-ATTR'                      00890019
                                      TO PV-PARAGRAPH-NAME              00900015
                   MOVE 'DELETING SKU_LOC_ATTR TABLE'                   00910018
                                      TO PV-DB2-OPER-ATTEMPTED          00911018
                   DISPLAY 'KEY -' PV-NZERO-INTR-UPC-ID  '- '           00920015
                                   PV-NZERO-LOC-NBR                     00930015
                   PERFORM 9100-SQL-ABEND                               00940018
             END-EVALUATE                                               00950011
           END-PERFORM.                                                 00960011
                                                                        00970011
           IF PV-SQL-SUB > PC-MAX-RETRIES                               00980011
               MOVE 'D200-DELETE-SKU-LOC-ATTR' TO PV-PARAGRAPH-NAME     00990019
               MOVE 'VERIFY RETRIES EXCEEDED'  TO PV-DB2-OPER-ATTEMPTED 01000013
               PERFORM 9100-SQL-ABEND                                   01010018
           END-IF.                                                      01020011
                                                                        01030013
      ******************************************************************01031019
      * UPDATE LAST UPD TMST TO SEND THE UPDATES TO STORES              01032019
      ******************************************************************01033019
       D250-UPDATE-SKU-LOC-ATTR.                                        01034024
                                                                        01035019
           PERFORM WITH TEST AFTER                                      01036019
               VARYING PV-SQL-SUB                                       01037019
               FROM 1 BY 1                                              01038019
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       01039019
                      OR SQLCODE = -911                                 01039119
                      OR SQLCODE = +100                                 01039219
                      OR SQLCODE = 0)                                   01039319
                                                                        01039419
           EXEC SQL                                                     01039519
              UPDATE XST_SKU_LOC_ATTR                                   01039619
                 SET LAST_UPD_TMST = CURRENT TIMESTAMP                  01039730
               WHERE INTR_UPC_ID  = :XST00041-INTR-UPC-ID               01039830
                 AND LOC_NBR      = :XST00041-LOC-NBR                   01039930
                 AND PRMPT_FOR_PRIC_IND = :XST00041-PRMPT-FOR-PRIC-IND  01040030
           END-EXEC                                                     01040530
                                                                        01040619
           EVALUATE SQLCODE                                             01040719
               WHEN 0                                                   01040819
                   ADD +1             TO PA-SKU-LOC-ATTR-UPDATED        01040919
                   MOVE ZERO          TO PV-DEADLOCK-COUNTER            01041019
               WHEN +100                                                01041119
               DISPLAY 'NO ROWS TO UPDATE IN LOC_ATTRFOR INTR_UPC_ID :' 01041219
                                         XST00041-INTR-UPC-ID           01041331
                   MOVE ZERO          TO PV-DEADLOCK-COUNTER            01041419
               WHEN -911                                                01041519
                   ADD +1             TO PV-DEADLOCK-COUNTER            01041619
                   DISPLAY 'DEADLOCK -911 IN D250-UPDATE-SKU-LOC-ATTR'  01041719
                   IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS            01041819
                       MOVE 'D250-UPDATE-SKU-LOC-ATTR'                  01041919
                                      TO PV-PARAGRAPH-NAME              01042019
                       PERFORM 9000-DEADLOCK-ERROR                      01042119
                   END-IF                                               01042219
               WHEN OTHER                                               01042319
                   MOVE 'D250-UPDATE-SKU-LOC-ATTR'                      01042419
                                      TO PV-PARAGRAPH-NAME              01042519
                   MOVE 'UPDATING XST_SKU_LOC_ATTR TABLE'               01042619
                                      TO PV-DB2-OPER-ATTEMPTED          01042719
                   DISPLAY 'KEY -' PV-NZERO-INTR-UPC-ID  '- '           01042819
                                   PV-NZERO-LOC-NBR      '- '           01042919
                                   PV-NZERO-BEG-TMST                    01043019
                   PERFORM 9100-SQL-ABEND                               01043119
             END-EVALUATE                                               01043219
           END-PERFORM.                                                 01043319
                                                                        01043419
           IF PV-SQL-SUB > PC-MAX-RETRIES                               01043519
             MOVE 'D250-UPDATE-SKU-LOC-ATTR' TO PV-PARAGRAPH-NAME       01043619
             MOVE 'VERIFY RETRIES EXCEEDED' TO PV-DB2-OPER-ATTEMPTED    01043719
             PERFORM 9100-SQL-ABEND                                     01043819
           END-IF.                                                      01043919
                                                                        01044019
      ******************************************************************01045011
      *                                                                 01050018
      ******************************************************************01060011
       C000-EOJ-ROUTINE.                                                01070013
                                                                        01080011
           CLOSE SKU-LOC-DEL-FILE                                       01090013
                 SKU-LOC-ATTR-DEL-FILE                                  01100013
                                                                        01110011
           PERFORM C100-COMMIT-DATA.                                    01120013
           PERFORM C200-DISPLAY-TOTALS.                                 01120113
                                                                        01121013
      ******************************************************************01122013
      *                                                                 01123013
      ******************************************************************01124013
       C100-COMMIT-DATA.                                                01130013
                                                                        01130113
            EXEC SQL                                                    01131018
                COMMIT                                                  01132018
            END-EXEC.                                                   01133030
                                                                        01134016
      ******************************************************************01134116
      *                                                                 01134216
      ******************************************************************01134316
       9000-DEADLOCK-ERROR.                                             01135018
                                                                        01136016
              MOVE 'DEADLOCK WHILE DELETING' TO PV-DB2-OPER-ATTEMPTED   01137018
              PERFORM 9100-SQL-ABEND.                                   01138018
                                                                        01140013
      ******************************************************************01150013
      *                                                                 01160013
      ******************************************************************01170013
       C200-DISPLAY-TOTALS.                                             01180013
                                                                        01181013
           DISPLAY '******** XSKUP405 TOTALS *********'.                01182027
           DISPLAY 'SKU-LOC RECS READ        :' PA-SKU-LOC-READ.        01183018
           DISPLAY 'SKU-LOC ROWS DELETED     :' PA-SKU-LOC-DELETED.     01184018
           DISPLAY 'SKU-LOC ROWS UPDATED     :' PA-SKU-LOC-UPDATED.     01184119
           DISPLAY 'SKU-LOC-ATTR RECS READ   :' PA-SKU-LOC-ATTR-READ.   01185018
           DISPLAY 'SKU-LOC-ATTR ROWS DELETED:' PA-SKU-LOC-ATTR-DELETED.01186018
           DISPLAY 'SKU-LOC-ATTR ROWS UPDATED:' PA-SKU-LOC-ATTR-UPDATED.01186119
           DISPLAY '***** END OF XSKUP405 TOTALS *****'.                01187027
                                                                        01190013
      ******************************************************************01200016
      * ABEND THE PROGRAM IN THE EVENT THAT A SQL ERROR OR WARNING      01210016
      * CODE OCCURS.                                                    01220016
      ******************************************************************01230016
       9100-SQL-ABEND.                                                  01240018
                                                                        01250016
           DISPLAY '** ABEND     **'.                                   01280016
           DISPLAY '** SQLCODE   ** = ' SQLCODE.                        01290016
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                01300016
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              01310016
           DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.          01320016
                                                                        01330016
      ******************************************************************01340016
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    01350016
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         01360016
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     01370016
      * FORMAT.                                                         01380016
      ******************************************************************01390016
           COPY DPPD004.                                                01400016
                                                                        01410016
           SET ABEND-4013-DB2-ERROR TO TRUE.                            01420016
                                                                        01430016
           CALL 'ILBOABN0' USING ABEND-CODE.                            01440016
                                                                        01460016
