000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.   TRKUP211.                                                  
000600 AUTHOR.       MICHAEL FRIESS.                                            
000700 INSTALLATION. KOHLS DEPARTMENT STORES.                                   
000800 DATE-WRITTEN. JUNE 27, 2011.                                             
000900 DATE-COMPILED.                                                           
001000                                                                          
001006******************************************************************        
001007*  JOB TRK212 - AIR AND LANDED ASN PROCESS.  THIS PROGRAM WILL   *        
001009*  DELETE ALL ENTRIES FROM THE SNK REPOSITORY XFER TABLE WHERE   *        
001010*  DUPLICATE PURCHASE ORDER WERE FOUND FOR AN ASN.  THE INPUT    *        
001011*  FILE WILL THEN BE INSERTED INTO THE XFER TABLE DUPLICATE POS  *        
001013*  HAVING BEEN STRIPPED OFF.                                     *        
001014*                                                                *        
001018*  INPUT/OUTPUT                                                  *        
001019*                                                                *        
001022*  PV-INPUT-REC - ASN RECORDS FROM WHICH DUPLICATE PO            *        
001023*                 RECORDS HAVE BEEN STRIPPED.                    *        
001024*                                                                *        
001027*  CHANGE LOG:                                                   *        
001027*                                                                *        
001027*  01/15/2023   JEAN KURK                  CHG0278527            *        
001027*  CHANGE MADE: ADD CHECKPOINT RESTART TO PROGRAM.  NOTE THAT    *        
001027*               THIS WILL ONLY CONTROL THE COMMIT FREQUENCY.  IT *        
001027*               IS NOT USED TO RESTART THE PROGRAM.  ON AN ABEND,*        
001027*               THE JOB IS RESTARTED FROM THE TOP                *        
001027*                                                                *        
002401******************************************************************        
002402 ENVIRONMENT DIVISION.                                                    
002403******************************************************************        
002404                                                                          
002405 CONFIGURATION SECTION.                                                   
002406                                                                          
002407 SOURCE-COMPUTER.        IBM-3090.                                        
002408 OBJECT-COMPUTER.        IBM-3090.                                        
002409                                                                          
002410 INPUT-OUTPUT SECTION.                                                    
002411                                                                          
002412 FILE-CONTROL.                                                            
002500                                                                          
002600     SELECT PV-INPUT-FILE                                                 
002700         ASSIGN TO INP01.                                                 
003100                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
004000                                                                          
004100 FD  PV-INPUT-FILE                                                        
004200     RECORDING MODE IS F                                                  
004300     LABEL RECORDS ARE STANDARD                                           
004400     BLOCK CONTAINS 0 RECORDS.                                            
004410                                                                          
004420 01  PV-INPUT-ASN-RECORD    PIC X(400).                                   
004430                                                                          
004470*****************************************************************         
004480                                                                          
006300 WORKING-STORAGE SECTION.                                                 
006310                                                                          
006400******************************************************************        
006410*  DB2 FORMATTED MESSAGE AREA                                             
006420******************************************************************        
006430     COPY DPWS004.                                                        
006440*                                                                         
006450******************************************************************        
006460*  COMMUNICATIONS AREA FOR DB2                                            
006470******************************************************************        
006480     EXEC SQL                                                             
006490          INCLUDE SQLCA                                                   
006491     END-EXEC.                                                            
006492*                                                                         
006500                                                                          
006600*****************************************************************         
006700*    ASN DETAIL INPUT RECORD LAYOUT                                       
006800*****************************************************************         
006900     COPY TRRD063.                                                        
007000                                                                          
007100*****************************************************************         
007200*    INPUT RECORD FOR STRIPPED FILE                                       
007300*****************************************************************         
007400 01  PV-INPUT-REC-LAYOUT.                                                 
007600   05 PV-IN-LOD-DAT-TXT.                                                  
007610     10 PV-IN-LOD-DAT-TXT-FIRST62           PIC X(62).                    
007620     10 FILLER                              PIC X(288).                   
007700     10  PV-IN-ISA-CONTROL.                                               
007800         15  PV-IN-ISA-CNTL-NBR-SEQ         PIC 9(9).                     
007900         15  PV-IN-SHIPT-ID-SEQ             PIC X(30).                    
008000         15  PV-IN-DAT-XFER-CNTL            PIC 9(9).                     
008100     10  FILLER                             PIC X(2).                     
008200                                                                          
127338*****************************************************************         
127339 01  PA-PROGRAM-ACCUMULATORS.                                             
127340     05  PA-TOTALS.                                                       
127350         10 PA-INPUT-COUNT       PIC 9(13)      VALUE 0.                  
127360         10 PA-DELETE-XFER-COUNT PIC 9(13)      VALUE 0.                  
127362         10 PA-DELETE-XFER-COMP  PIC S9(09)     VALUE 0                   
127363                                                 COMP-3.                  
127364         10 PA-INSERT-XFER-COUNT PIC 9(13)      VALUE 0.                  
127365         10 PA-UPDATE-CNTL-TTL   PIC 9(13)      VALUE 0.                  
127366         10 PA-DELETE-XFER-TTL   PIC 9(13)      VALUE 0.                  
127367         10 PA-INSERT-XFER-TTL   PIC 9(13)      VALUE 0.                  
127368                                                                          
127369 01  PS-PROGRAM-SWITCHES.                                                 
127370     05  PS-END-OF-INPUT-SW            PIC  X(01)    VALUE 'N'.           
127371         88  PS-END-OF-INPUT                         VALUE 'Y'.           
127380         88  PS-END-OF-INPUT-NO                      VALUE 'N'.           
127451     05  PS-ASN-HAS-DUP-PO-SW          PIC  X(01)    VALUE 'N'.           
127452         88  PS-ASN-HAS-DUP-PO                       VALUE 'Y'.           
127453         88  PS-ASN-HAS-DUP-PO-NO                    VALUE 'N'.           
127457                                                                          
127458 01  PC-PROGRAM-CONSTANTS.                                                
127459     05  PC-CURRENT-PROGRAM-NAME     PIC X(08)     VALUE                  
127460         'TRKUP211'.                                                      
127486                                                                          
127487 01 PV-PROGRAM-VARIABLES.                                                 
127500                                                                          
127501     05  PV-PARAGRAPH-NAME           PIC X(30)     VALUE SPACES.          
127502     05  PV-ABEND-INFO               PIC X(80)     VALUE SPACES.          
127511                                                                          
127519     05  PV-CNTL-ID-DISPLAY          PIC 9(10) VALUE ZEROS.               
127520     05  PV-CRE-TMST-DISPLAY         PIC X(26) VALUE SPACES.              
127521     05  PV-NON-NUM-ERR-FIELD        PIC X(30) VALUE SPACES.              
127530     05  PV-COUNTER                  PIC S9(04)    VALUE +0               
127540                                                      COMP SYNC.          
063800     05  PV-HOLD-TMST                PIC X(26)     VALUE SPACES.          
063700     05  PV-TMST                     PIC X(26)     VALUE SPACES.          
127500                                                                          
127550*****************************************************************         
127560*    RECORD PURPOSE: INPUT RECORD FOR THE ADVANCE SHIP          *         
127570*                    NOTICE INPUT REPOSITORY TABLE REMOVING     *         
127580*                    PACKED FIELDS FROM PO4, TD1, TD5, CUR      *         
127581*                    SN1 AND SLN RECORDS.                       *         
127590*    RECORD LENGTH:  350 BYTES                                  *         
127600*    RECORD FORMAT:  SEQUENTIAL                                 *         
127700*****************************************************************         
127900     05  WS-PO4-SEGMENT.                                                  
128000         10  WS-PO4-ID                   PIC XXX    VALUE SPACES.         
128100         10  WS-PO401-PKG-SIZE-QTY       PIC S9(7)  VALUE 0.              
128200         10  WS-PO402-SIZE-MSMT          PIC S9(9)V9(2) VALUE 0.          
128300         10  WS-PO403-SIZE-UM-CDE        PIC X(2)   VALUE SPACES.         
128400         10  WS-PO405-WGHT-QUAL-CDE      PIC X(2)   VALUE SPACES.         
128401         10  WS-PO406-GRO-WGHT-MSMT      PIC S9(9)V9(2) VALUE 0.          
128402         10  WS-PO407-WGHT-UM-CDE        PIC X(2)   VALUE SPACES.         
128403         10  WS-PO408-GRO-VOL-MSMT       PIC S9(9)V9(2) VALUE 0.          
128404         10  WS-PO409-VOL-UM-CDE         PIC X(2)   VALUE SPACES.         
128405         10  WS-PO410-LEN-MSMT           PIC S9(9)V9(2) VALUE 0.          
128406         10  WS-PO411-WDTH-MSMT          PIC S9(9)V9(2) VALUE 0.          
128407         10  WS-PO412-HT-MSMT            PIC S9(9)V9(2) VALUE 0.          
128408         10  WS-PO413-UM-CDE             PIC X(2)   VALUE SPACES.         
128409         10  WS-PO414-INPK-EA-QTY        PIC 9(6)   VALUE 0.              
128410         10  WS-PO4-FILLER               PIC X(258) VALUE SPACES.         
128411*                                                                         
128412     05  WS-TD1-SEGMENT.                                                  
128413         10  WS-TD1-ID                   PIC XXX    VALUE SPACES.         
128414         10  WS-TD101-PKG-CDE            PIC X(5)   VALUE SPACES.         
128415         10  WS-TD102-SHIPT-QTY          PIC S9(7)  VALUE 0.              
128416         10  WS-TD106-WGHT-QUAL-CDE      PIC X(2)   VALUE SPACES.         
128417         10  WS-TD107-WGHT-MSMT          PIC S9(9)V9(2) VALUE 0.          
128418         10  WS-TD108-UM-CDE             PIC X(2)   VALUE SPACES.         
128419         10  WS-TD1-FILLER               PIC X(320) VALUE SPACES.         
128420*                                                                         
128421     05  WS-TD5-SEGMENT.                                                  
128422         10  WS-TD5-ID                   PIC XXX    VALUE SPACES.         
128423         10  WS-TD501-RTE-SEQ-CDE        PIC X(2)   VALUE SPACES.         
128424         10  WS-TD502-CARR-ID-QUAL-CDE   PIC X(2)   VALUE SPACES.         
128425         10  WS-TD503-CARR-ID-CDE        PIC X(17)  VALUE SPACES.         
128426         10  WS-TD504-TRANS-MTHD-CDE     PIC X(2)   VALUE SPACES.         
128427         10  WS-TD505-CARR-NME           PIC X(35)  VALUE SPACES.         
128428         10  WS-TD506-STATUS-CDE         PIC X(2)   VALUE SPACES.         
128429         10  WS-TD507-LOC-QUAL           PIC X(2)   VALUE SPACES.         
128430         10  WS-TD508-LOC-ID             PIC X(30)  VALUE SPACES.         
128431         10  WS-TD510-TRNS-TME-QUAL-CDE  PIC X(2)   VALUE SPACES.         
128432         10  WS-TD511-TRNST-TIME         PIC S9(4)V9(5) VALUE 0.          
128433         10  WS-TD5-FILLER               PIC X(244) VALUE SPACES.         
128434*                                                                         
128435     05  WS-CUR-SEGMENT.                                                  
128436         10  WS-CUR-ID                   PIC XXX    VALUE SPACES.         
128437         10  WS-CUR01-ID-CDE             PIC X(2)   VALUE SPACES.         
128438         10  WS-CUR02-CURR-CDE           PIC X(3)   VALUE SPACES.         
128439         10  WS-CUR03-EXCH-FCTR          PIC S9(6)V9(5) VALUE 0.          
128440         10  WS-CUR04-ID-CDE             PIC X(2)   VALUE SPACES.         
128441         10  WS-CUR05-CURR-CDE           PIC X(3)   VALUE SPACES.         
128442         10  WS-CUR-FILLER               PIC X(326) VALUE SPACES.         
128443*                                                                         
128444     05  WS-SN1-SEGMENT.                                                  
128445         10  WS-SN1-ID                   PIC XXX    VALUE SPACES.         
128446         10  WS-SN101-ID-CDE             PIC X(6)   VALUE SPACES.         
128447         10  WS-SN102-SHIP-QTY           PIC S9(7)  VALUE 0.              
128448         10  WS-SN103-SHIP-UM-CDE        PIC X(2)   VALUE SPACES.         
128449         10  WS-SN104-SHIPT-DTE-QTY      PIC S9(7)  VALUE 0.              
128450         10  WS-SN105-ORD-QTY            PIC S9(7)  VALUE 0.              
128451         10  WS-SN106-ORD-UM-CDE         PIC X(2)   VALUE SPACES.         
128452         10  WS-SN1-FILLER               PIC X(316) VALUE SPACES.         
128453*                                                                         
128454     05  WS-SLN-SEGMENT.                                                  
128455         10  WS-SLN-ID                   PIC XXX    VALUE SPACES.         
128456         10  WS-SLN01-ID                 PIC X(6)   VALUE SPACES.         
128457         10  WS-SLN03-CONFIG-CDE         PIC X(1)   VALUE SPACES.         
128458         10  WS-SLN04-QTY                PIC S9(7)  VALUE 0.              
128459         10  WS-SLN05-UM-CDE             PIC X(2)   VALUE SPACES.         
128460         10  WS-SLN06-UNIT-PRICE-AMT     PIC S9(9)V9(2) VALUE 0.          
128461         10  WS-SLN07-UNIT-PRICE-CDE     PIC X(2)   VALUE SPACES.         
128462         10  WS-SLN08-PRICE-CD           PIC X(1)   VALUE SPACES.         
128463         10  WS-PROD-ID-GROUP OCCURS 9.                                   
128464          15  WS-SLN09-PROD-ID-QUAL-CDE  PIC X(2)   VALUE SPACES.         
128465          15  WS-SLN10-PROD-ID           PIC X(30)  VALUE SPACES.         
128466         10  WS-SLN-FILLER               PIC X(29)  VALUE SPACES.         
128467                                                                          
128468 01  ABEND-CODE       PIC S9(4) COMP SYNC VALUE ZERO.                     
128469       88  ABEND-SQL-SELECT-ERROR         VALUE +4001.                    
128470       88  ABEND-SQL-DELETE-ERROR         VALUE +4002.                    
128471       88  ABEND-SQL-INSERT-ERROR         VALUE +4003.                    
128472       88  ABEND-SQL-UPDATE-ERROR         VALUE +4004.                    
128480       88  ABEND-SQL-COMMIT-ERROR         VALUE +4005.                    
247465                                                                          
247471* DB2 TABLE DECLARATION                                                   
247472* (SNT_DAT_CNTL - ASN DATA TRANSFER CONTROL TABLE)                        
247473     EXEC SQL                                                             
247474         INCLUDE SNT00000                                                 
247475     END-EXEC.                                                            
247476                                                                          
247477* DB2 TABLE DECLARATION                                                   
247478* (SNT_DAT_XFER - ASN DATA TRANSFER DETAIL TABLE)                         
247479     EXEC SQL                                                             
247480         INCLUDE SNT00001                                                 
247481     END-EXEC.                                                            
247482                                                                          
247478* CHECKPOINT RESTART CONTROL TABLE                                        
247479     EXEC SQL                                                             
247480         INCLUDE TCKRSTCN                                                 
247481     END-EXEC.                                                            
247482                                                                          
247483* INPUT LAYOUT - ASN DTL RECORD COPYBOOK - UNPACKED.                      
247484     COPY SNRD005.                                                        
247492                                                                          
247493 PROCEDURE DIVISION.                                                      
247495******************************************************************        
247496*  0000-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE PROGRAM.  *        
247497******************************************************************        
247498                                                                          
247499 0000-PROGRAM-MAINLINE.                                                   
247500                                                                          
494665***  DISPLAY '0000-PROGRAM-MAINLINE'.                                     
494666                                                                          
494667     PERFORM 1000-INITIALIZE.                                             
494668                                                                          
494669     IF PS-END-OF-INPUT                                                   
494670        DISPLAY 'EMPTY FILE'                                              
494671     ELSE                                                                 
494672        PERFORM 2000-MAIN-LINE-PROCESS                                    
494673           UNTIL PS-END-OF-INPUT                                          
494674*       *** DID LAST ASN HAVE DUPLICATE PURCHASE ORDERS?                  
494675        IF PS-ASN-HAS-DUP-PO                                              
494676            PERFORM 3400-UPDATE-CNTL-TBL                                  
494678            PERFORM 4000-WRAP-UP-ASN                                      
494679            PERFORM 3500-COMMIT                                           
494680        END-IF                                                            
494681     END-IF.                                                              
494682                                                                          
494683     PERFORM 8000-EOJ-ROUTINE.                                            
494684     STOP RUN.                                                            
494685                                                                          
494686******************************************************************        
494687*  OPENS INPUT FILE AND PERFORMS OTHER BEGINNING OF PROGRAM      *        
494688*  PROCESSES.                                                    *        
494689******************************************************************        
494690                                                                          
494691 1000-INITIALIZE.                                                         
494692                                                                          
494693***  DISPLAY '1000-INITIALIZE'.                                           
494694                                                                          
494695     OPEN INPUT PV-INPUT-FILE                                             
082200                                                                          
082300     PERFORM 4020-SET-COMMIT-TIME.                                        
082200                                                                          
082600 1010-CONTINUE-INITIALIZE.                                                
082700                                                                          
494698*    PERFORM INITIAL READ                                                 
494699     PERFORM 6000-READ-INPUT-FILE.                                        
494700                                                                          
494701     DISPLAY '+++ START LIST REWRITTEN CNTL IDS ++'.                      
494702     DISPLAY ' CNTL ID    CREATE TIMESTAMP         '                      
494703             ' DELETED   INSERTED'.                                       
494704*    DETAIL GETS DISPLAYED IN 2000-MAIN-LINE-PROCESS                      
494705                                                                          
494710 EJECT                                                                    
494764                                                                          
494765******************************************************************        
494766* MAIN PROCESSING PARAGRAPH.                                     *        
494767******************************************************************        
494768 2000-MAIN-LINE-PROCESS.                                                  
494769                                                                          
494770***  DISPLAY '2000-MAIN-LINE-PROCESS'.                                    
494771                                                                          
494772     EVALUATE TRUE                                                        
494773                                                                          
494775     WHEN ID-IDENTIFICATION-REC                                           
494776                                                                          
494777*      *** DID PREVIOUS ASN HAVE DUPLICATE PURCHASE ORDERS?               
494778       IF PS-ASN-HAS-DUP-PO                                               
494779          PERFORM 3400-UPDATE-CNTL-TBL                                    
494781          PERFORM 4000-WRAP-UP-ASN                                        
494782          PERFORM 3500-COMMIT                                             
494803       END-IF                                                             
494804                                                                          
494805       PERFORM 3000-SELECT-CNTL-ID                                        
494806                                                                          
494807*      *** DOES THIS ASN HAVE DUPLICATE PURCHASE ORDERS?                  
494808       IF PS-ASN-HAS-DUP-PO                                               
494809         MOVE SNT00000-DAT-XFER-CNTL-ID                                   
494810                                TO PV-CNTL-ID-DISPLAY                     
494811         MOVE SNT00000-CRE-TMST TO PV-CRE-TMST-DISPLAY                    
494812         PERFORM 3100-PRESEL-FOR-DEL-XFER                                 
494813*        **************************************                           
494814*        * IF THIS IS RESTART - XFER RECS MAY *                           
494815*        * ALREADY HAVE BEEN DELETED.         *                           
494816*        **************************************                           
494817         IF PA-DELETE-XFER-COMP > 0                                       
494818           PERFORM 3200-DELETE-XFER-TBL                                   
494819         END-IF                                                           
494820         MOVE PV-IN-LOD-DAT-TXT  TO SNT00001-LOD-DAT-TXT-TEXT             
494821         PERFORM 3300-INSERT-XFER-TBL                                     
494822       END-IF                                                             
494823                                                                          
494824     WHEN PO4-ITEM-PHYSICAL-DTL-REC                                       
494825       IF PS-ASN-HAS-DUP-PO                                               
494826         PERFORM 2100-UNPACK-PO4-FIELDS                                   
494827         MOVE WS-PO4-SEGMENT     TO SNT00001-LOD-DAT-TXT-TEXT             
494828         PERFORM 3300-INSERT-XFER-TBL                                     
494829       END-IF                                                             
494830                                                                          
494831     WHEN TD1-CARRIER-QTY-WGHT-REC                                        
494832       IF PS-ASN-HAS-DUP-PO                                               
494833         PERFORM 2200-UNPACK-TD1-FIELDS                                   
494834         MOVE WS-TD1-SEGMENT     TO SNT00001-LOD-DAT-TXT-TEXT             
494835         PERFORM 3300-INSERT-XFER-TBL                                     
494836       END-IF                                                             
494837                                                                          
494838     WHEN TD5-CARRIER-RTE-TRNST-REC                                       
494839       IF PS-ASN-HAS-DUP-PO                                               
494840         PERFORM 2300-UNPACK-TD5-FIELDS                                   
494841         MOVE WS-TD5-SEGMENT     TO SNT00001-LOD-DAT-TXT-TEXT             
494842         PERFORM 3300-INSERT-XFER-TBL                                     
494843       END-IF                                                             
494844                                                                          
494845     WHEN CUR-CURRENCY-REC                                                
494846       IF PS-ASN-HAS-DUP-PO                                               
494847         PERFORM 2400-UNPACK-CUR-FIELDS                                   
494848         MOVE WS-CUR-SEGMENT     TO SNT00001-LOD-DAT-TXT-TEXT             
494849         PERFORM 3300-INSERT-XFER-TBL                                     
494850       END-IF                                                             
494851                                                                          
494852     WHEN SN1-ITEM-DETAIL-REC                                             
494853       IF PS-ASN-HAS-DUP-PO                                               
494854         PERFORM 2500-UNPACK-SN1-FIELDS                                   
494855         MOVE WS-SN1-SEGMENT     TO SNT00001-LOD-DAT-TXT-TEXT             
494856         PERFORM 3300-INSERT-XFER-TBL                                     
494857       END-IF                                                             
494858                                                                          
494859     WHEN SLN-SUBITEM-DETAIL-REC                                          
494860       IF PS-ASN-HAS-DUP-PO                                               
494861         PERFORM 2600-UNPACK-SLN-FIELDS                                   
494862         MOVE WS-SLN-SEGMENT     TO SNT00001-LOD-DAT-TXT-TEXT             
494863         PERFORM 3300-INSERT-XFER-TBL                                     
494864       END-IF                                                             
494865                                                                          
494866     WHEN OTHER                                                           
494867       IF PS-ASN-HAS-DUP-PO                                               
494868         MOVE PV-IN-LOD-DAT-TXT  TO SNT00001-LOD-DAT-TXT-TEXT             
494869         PERFORM 3300-INSERT-XFER-TBL                                     
494870       END-IF                                                             
494880                                                                          
495345     END-EVALUATE.                                                        
495346                                                                          
495347     PERFORM 4100-COMMIT-CHECK.                                           
495347     PERFORM 6000-READ-INPUT-FILE.                                        
495348                                                                          
495500******************************************************************        
495600* UNPACK FIELDS FOR XFER TABLE.                                           
495800******************************************************************        
495900                                                                          
495901 2100-UNPACK-PO4-FIELDS.                                                  
495902                                                                          
495903     INITIALIZE WS-PO4-SEGMENT.                                           
495904     MOVE TR063-PO4-ID              TO WS-PO4-ID.                         
495905     MOVE TR063-PO403-SIZE-UM-CDE   TO WS-PO403-SIZE-UM-CDE.              
495906     MOVE TR063-PO405-WGHT-QUAL-CDE TO WS-PO405-WGHT-QUAL-CDE.            
495907     MOVE TR063-PO407-WGHT-UM-CDE   TO WS-PO407-WGHT-UM-CDE.              
495909     MOVE TR063-PO409-VOL-UM-CDE    TO WS-PO409-VOL-UM-CDE.               
495910     MOVE TR063-PO413-UM-CDE        TO WS-PO413-UM-CDE.                   
495911     MOVE SPACES                    TO WS-PO4-FILLER.                     
495912                                                                          
495913     IF TR063-PO401-PKG-SIZE-QTY NUMERIC                                  
495914       MOVE TR063-PO401-PKG-SIZE-QTY  TO WS-PO401-PKG-SIZE-QTY            
495915     ELSE                                                                 
495916       MOVE 'PO401-PKG-SIZE-QTY' TO PV-NON-NUM-ERR-FIELD                  
495917       PERFORM 2700-NON-NUMERIC-ERROR                                     
495918     END-IF.                                                              
495919                                                                          
495920     IF TR063-PO402-SIZE-MSMT NUMERIC                                     
495921       MOVE TR063-PO402-SIZE-MSMT     TO WS-PO402-SIZE-MSMT               
495922     ELSE                                                                 
495923       MOVE 'PO402-SIZE-MSMT' TO PV-NON-NUM-ERR-FIELD                     
495924       PERFORM 2700-NON-NUMERIC-ERROR                                     
495925     END-IF.                                                              
495926                                                                          
495928     IF TR063-PO406-GRO-WGHT-MSMT NUMERIC                                 
495929       MOVE TR063-PO406-GRO-WGHT-MSMT TO WS-PO406-GRO-WGHT-MSMT           
495930     ELSE                                                                 
495931       MOVE 'PO406-GRO-WGHT-MSMT' TO PV-NON-NUM-ERR-FIELD                 
495932       PERFORM 2700-NON-NUMERIC-ERROR                                     
495933     END-IF.                                                              
495934                                                                          
495937     IF TR063-PO408-GRO-VOL-MSMT NUMERIC                                  
495938       MOVE TR063-PO408-GRO-VOL-MSMT TO WS-PO408-GRO-VOL-MSMT             
495939     ELSE                                                                 
495940       MOVE 'PO408-GRO-VOL-MSMT' TO PV-NON-NUM-ERR-FIELD                  
495941       PERFORM 2700-NON-NUMERIC-ERROR                                     
495942     END-IF.                                                              
495945                                                                          
495947     IF TR063-PO410-LEN-MSMT NUMERIC                                      
495948       MOVE TR063-PO410-LEN-MSMT TO WS-PO410-LEN-MSMT                     
495949     ELSE                                                                 
495950       MOVE 'PO410-LEN-MSMT' TO PV-NON-NUM-ERR-FIELD                      
495951       PERFORM 2700-NON-NUMERIC-ERROR                                     
495952     END-IF.                                                              
495953                                                                          
495955     IF TR063-PO411-WDTH-MSMT NUMERIC                                     
495956       MOVE TR063-PO411-WDTH-MSMT TO WS-PO411-WDTH-MSMT                   
495957     ELSE                                                                 
495958       MOVE 'PO411-WDTH-MSMT' TO PV-NON-NUM-ERR-FIELD                     
495959       PERFORM 2700-NON-NUMERIC-ERROR                                     
495960     END-IF.                                                              
495961                                                                          
495963     IF TR063-PO412-HT-MSMT NUMERIC                                       
495964       MOVE TR063-PO412-HT-MSMT TO WS-PO412-HT-MSMT                       
495965     ELSE                                                                 
495966       MOVE 'PO412-HT-MSMT' TO PV-NON-NUM-ERR-FIELD                       
495967       PERFORM 2700-NON-NUMERIC-ERROR                                     
495968     END-IF.                                                              
495969                                                                          
495971     IF TR063-PO414-INPK-EA-QTY NUMERIC                                   
495972       MOVE TR063-PO414-INPK-EA-QTY  TO WS-PO414-INPK-EA-QTY              
495973     ELSE                                                                 
495974       MOVE 'PO414-INPK-EA-QTY' TO PV-NON-NUM-ERR-FIELD                   
495975       PERFORM 2700-NON-NUMERIC-ERROR                                     
495976     END-IF.                                                              
495977                                                                          
495978******************************************************************        
495979* UNPACK FIELDS FOR XFER TABLE.                                           
495980******************************************************************        
495981                                                                          
495982 2200-UNPACK-TD1-FIELDS.                                                  
495983                                                                          
495984     INITIALIZE WS-TD1-SEGMENT.                                           
495985     MOVE TR063-TD1-ID              TO WS-TD1-ID.                         
495986     MOVE TR063-TD101-PKG-CDE       TO WS-TD101-PKG-CDE.                  
495987     MOVE TR063-TD106-WGHT-QUAL-CDE TO WS-TD106-WGHT-QUAL-CDE.            
495990     MOVE TR063-TD108-UM-CDE        TO WS-TD108-UM-CDE.                   
495991     MOVE SPACES                    TO WS-TD1-FILLER.                     
495992                                                                          
495994     IF TR063-TD102-SHIPT-QTY NUMERIC                                     
495995       MOVE TR063-TD102-SHIPT-QTY  TO WS-TD102-SHIPT-QTY                  
495996     ELSE                                                                 
495997       MOVE 'TD102-SHIPT-QTY' TO PV-NON-NUM-ERR-FIELD                     
495998       PERFORM 2700-NON-NUMERIC-ERROR                                     
495999     END-IF.                                                              
496000                                                                          
496002     IF TR063-TD107-WGHT-MSMT NUMERIC                                     
496003       MOVE TR063-TD107-WGHT-MSMT  TO WS-TD107-WGHT-MSMT                  
496004     ELSE                                                                 
496005       MOVE 'TD107-WGHT-MSMT' TO PV-NON-NUM-ERR-FIELD                     
496006       PERFORM 2700-NON-NUMERIC-ERROR                                     
496007     END-IF.                                                              
496008                                                                          
496009******************************************************************        
496010* UNPACK FIELDS FOR XFER TABLE.                                           
496011******************************************************************        
496012                                                                          
496013 2300-UNPACK-TD5-FIELDS.                                                  
496014                                                                          
496015     INITIALIZE WS-TD5-SEGMENT.                                           
496016     MOVE TR063-TD5-ID                TO WS-TD5-ID.                       
496017     MOVE TR063-TD501-RTE-SEQ-CDE     TO WS-TD501-RTE-SEQ-CDE.            
496018     MOVE TR063-TD502-CARR-ID-QUAL-CDE                                    
496019                                 TO WS-TD502-CARR-ID-QUAL-CDE.            
496020     MOVE TR063-TD503-CARR-ID-CDE     TO WS-TD503-CARR-ID-CDE.            
496021     MOVE TR063-TD504-TRANS-MTHD-CDE                                      
496022                                   TO WS-TD504-TRANS-MTHD-CDE.            
496023     MOVE TR063-TD505-CARR-NME        TO WS-TD505-CARR-NME.               
496024     MOVE TR063-TD506-STATUS-CDE      TO WS-TD506-STATUS-CDE.             
496025     MOVE TR063-TD507-LOC-QUAL        TO WS-TD507-LOC-QUAL.               
496026     MOVE TR063-TD508-LOC-ID          TO WS-TD508-LOC-ID.                 
496028     MOVE TR063-TD511-TRNST-TIME      TO WS-TD511-TRNST-TIME.             
496029     MOVE SPACES                      TO WS-TD5-FILLER.                   
496030                                                                          
496033     IF TR063-TD510-TRNS-TME-QUAL-CDE NUMERIC                             
496034       MOVE TR063-TD510-TRNS-TME-QUAL-CDE                                 
496035                            TO WS-TD510-TRNS-TME-QUAL-CDE                 
496036     ELSE                                                                 
496037       MOVE 'TD510-TRNS-TME-QUAL-CDE' TO PV-NON-NUM-ERR-FIELD             
496038       PERFORM 2700-NON-NUMERIC-ERROR                                     
496039     END-IF.                                                              
496040                                                                          
496041******************************************************************        
496042* UNPACK FIELDS FOR XFER TABLE.                                           
496043******************************************************************        
496044                                                                          
496045 2400-UNPACK-CUR-FIELDS.                                                  
496046                                                                          
496047     INITIALIZE WS-CUR-SEGMENT.                                           
496048     MOVE TR063-CUR-ID             TO WS-CUR-ID.                          
496049     MOVE TR063-CUR01-ID-CDE       TO WS-CUR01-ID-CDE.                    
496050     MOVE TR063-CUR02-CURR-CDE     TO WS-CUR02-CURR-CDE.                  
496051     MOVE TR063-CUR04-ID-CDE       TO WS-CUR04-ID-CDE.                    
496052     MOVE TR063-CUR05-CURR-CDE     TO WS-CUR05-CURR-CDE.                  
496053     MOVE SPACES                   TO WS-CUR-FILLER.                      
496054                                                                          
496056     IF TR063-CUR03-EXCH-FCTR NUMERIC                                     
496057       MOVE TR063-CUR03-EXCH-FCTR  TO WS-CUR03-EXCH-FCTR                  
496058     ELSE                                                                 
496059       MOVE 'CUR03-EXCH-FCTR' TO PV-NON-NUM-ERR-FIELD                     
496060       PERFORM 2700-NON-NUMERIC-ERROR                                     
496061     END-IF.                                                              
496062                                                                          
496063******************************************************************        
496064* UNPACK FIELDS FOR XFER TABLE.                                           
496065******************************************************************        
496066                                                                          
496067 2500-UNPACK-SN1-FIELDS.                                                  
496068                                                                          
496069     INITIALIZE WS-SN1-SEGMENT.                                           
496070     MOVE TR063-SN1-ID              TO WS-SN1-ID.                         
496071     MOVE TR063-SN101-ID-CDE        TO WS-SN101-ID-CDE.                   
496072     MOVE TR063-SN103-SHIP-UM-CDE   TO WS-SN103-SHIP-UM-CDE.              
496076     MOVE TR063-SN106-ORD-UM-CDE    TO WS-SN106-ORD-UM-CDE.               
496077     MOVE SPACES                    TO WS-SN1-FILLER.                     
496078                                                                          
496080     IF TR063-SN102-SHIP-QTY NUMERIC                                      
496081       MOVE TR063-SN102-SHIP-QTY  TO WS-SN102-SHIP-QTY                    
496082     ELSE                                                                 
496083       MOVE 'SN102-SHIP-QTY' TO PV-NON-NUM-ERR-FIELD                      
496084       PERFORM 2700-NON-NUMERIC-ERROR                                     
496085     END-IF.                                                              
496086                                                                          
496088     IF TR063-SN104-SHIPT-DTE-QTY NUMERIC                                 
496089       MOVE TR063-SN104-SHIPT-DTE-QTY  TO WS-SN104-SHIPT-DTE-QTY          
496090     ELSE                                                                 
496091       MOVE 'SN104-SHIPT-DTE-QTY' TO PV-NON-NUM-ERR-FIELD                 
496092       PERFORM 2700-NON-NUMERIC-ERROR                                     
496093     END-IF.                                                              
496094                                                                          
496096     IF TR063-SN105-ORD-QTY NUMERIC                                       
496097       MOVE TR063-SN105-ORD-QTY  TO WS-SN105-ORD-QTY                      
496098     ELSE                                                                 
496099       MOVE 'SN105-ORD-QTY' TO PV-NON-NUM-ERR-FIELD                       
496100       PERFORM 2700-NON-NUMERIC-ERROR                                     
496101     END-IF.                                                              
496102                                                                          
496103******************************************************************        
496104* UNPACK FIELDS FOR XFER TABLE.                                           
496105******************************************************************        
496106                                                                          
496107 2600-UNPACK-SLN-FIELDS.                                                  
496108                                                                          
496109     INITIALIZE WS-SLN-SEGMENT.                                           
496110     MOVE TR063-SLN-ID               TO WS-SLN-ID.                        
496111     MOVE TR063-SLN01-ID             TO WS-SLN01-ID.                      
496112     MOVE TR063-SLN03-CONFIG-CDE     TO WS-SLN03-CONFIG-CDE.              
496113     MOVE TR063-SLN05-UM-CDE         TO WS-SLN05-UM-CDE.                  
496117     MOVE TR063-SLN07-UNIT-PRICE-CDE                                      
496118                               TO WS-SLN07-UNIT-PRICE-CDE.                
496119     MOVE TR063-SLN08-PRICE-CD       TO WS-SLN08-PRICE-CD.                
496120     MOVE SPACES                     TO WS-SLN-FILLER.                    
496121                                                                          
496122     PERFORM VARYING PV-COUNTER FROM 1 BY 1                               
496123       UNTIL PV-COUNTER > 9                                               
496124       MOVE TR063-SLN09-PROD-ID-QUAL-CDE(PV-COUNTER)                      
496125                 TO WS-SLN09-PROD-ID-QUAL-CDE(PV-COUNTER)                 
496126       MOVE TR063-SLN10-PROD-ID(PV-COUNTER)                               
496127                 TO WS-SLN10-PROD-ID(PV-COUNTER)                          
496128     END-PERFORM.                                                         
496131                                                                          
496133     IF TR063-SLN04-QTY NUMERIC                                           
496134       MOVE TR063-SLN04-QTY  TO WS-SLN04-QTY                              
496135     ELSE                                                                 
496136       MOVE 'SLN04-QTY' TO PV-NON-NUM-ERR-FIELD                           
496137       PERFORM 2700-NON-NUMERIC-ERROR                                     
496138     END-IF.                                                              
496139                                                                          
496142     IF TR063-SLN06-UNIT-PRICE-AMT NUMERIC                                
496143       MOVE TR063-SLN06-UNIT-PRICE-AMT                                    
496144                               TO WS-SLN06-UNIT-PRICE-AMT                 
496145     ELSE                                                                 
496146       MOVE 'SLN06-UNIT-PRICE-AMT' TO PV-NON-NUM-ERR-FIELD                
496147       PERFORM 2700-NON-NUMERIC-ERROR                                     
496148     END-IF.                                                              
496149                                                                          
496150******************************************************************        
496151* DISPLAY ERROR & 1ST 30 BYTES OF RECORD WHEN NON NUMERIC FOUND.          
496152******************************************************************        
496153                                                                          
496154  2700-NON-NUMERIC-ERROR.                                                 
496155                                                                          
496156     DISPLAY '  FIELD NON NUMERIC - ' PV-NON-NUM-ERR-FIELD                
496157     DISPLAY '  ' TR063-SHIP-NOTICE-DTL-RECORD (1:30)                     
496158     INITIALIZE PV-NON-NUM-ERR-FIELD.                                     
496159                                                                          
496160******************************************************************        
496161* SELECT THE CONTROL ID FOR THIS ASN.                                     
496162******************************************************************        
496163                                                                          
496164 3000-SELECT-CNTL-ID.                                                     
496165                                                                          
496166***  DISPLAY '3000-SELECT-CNTL-ID'.                                       
496170                                                                          
496180     MOVE TR063-DAT-XFER-CNTL-ID                                          
496181                             TO SNT00000-DAT-XFER-CNTL-ID.                
496190                                                                          
496200     EXEC SQL                                                             
496302         SELECT DAT_XFER_CNTL_ID,                                         
496303                STAT_CDE                                                  
496305           INTO :SNT00001-DAT-XFER-CNTL-ID                                
496306               ,:SNT00000-STAT-CDE                                        
496307           FROM SNT_DAT_CNTL                                              
496610          WHERE DAT_XFER_CNTL_ID = :SNT00000-DAT-XFER-CNTL-ID             
496710     END-EXEC.                                                            
496800                                                                          
496900     EVALUATE TRUE                                                        
497000       WHEN SQLCODE = ZERO                                                
497100           IF SNT00000-STAT-CDE = 'D'                                     
497200             SET PS-ASN-HAS-DUP-PO TO TRUE                                
497300           END-IF                                                         
497400                                                                          
497500       WHEN OTHER                                                         
497600           DISPLAY '**************************************'               
497700           DISPLAY '** ABEND                             *'               
498000           DISPLAY '** SELECT SNT_DAT_XFER               *'               
498100           DISPLAY '**************************************'               
498110           SET ABEND-SQL-SELECT-ERROR TO TRUE                             
498120           MOVE '3000-SELECT-CNTL-ID' TO PV-PARAGRAPH-NAME                
498130           MOVE 'TR063-DAT-XFER-CNTL-ID = ' TO PV-ABEND-INFO(1:25)        
498132           MOVE TR063-DAT-XFER-CNTL-ID TO PV-ABEND-INFO(26:10)            
498200           PERFORM 9000-USER-ABEND                                        
498300     END-EVALUATE.                                                        
498400                                                                          
498410******************************************************************        
498420* COUNT ROWS THAT WILL BE DELETE FOR THIS ASN WITH DUPLICATE POS.         
498430******************************************************************        
498440                                                                          
498450 3100-PRESEL-FOR-DEL-XFER.                                                
498460                                                                          
498470***  DISPLAY '3100-PRESEL-FOR-DEL-XFER'.                                  
498480                                                                          
498490     EXEC SQL                                                             
498496         SELECT COUNT(*)                                                  
498497           INTO :PA-DELETE-XFER-COMP                                      
498498           FROM SNT_DAT_XFER                                              
498500          WHERE DAT_XFER_CNTL_ID = :SNT00001-DAT-XFER-CNTL-ID             
498503     END-EXEC.                                                            
498510                                                                          
498511     EVALUATE TRUE                                                        
498512       WHEN SQLCODE = ZERO                                                
498513           MOVE PA-DELETE-XFER-COMP TO PA-DELETE-XFER-COUNT               
498514                                                                          
498515       WHEN OTHER                                                         
498516           DISPLAY '**************************************'               
498517           DISPLAY '** ABEND                             *'               
498518           DISPLAY '** SELECT COUNT SNT_DAT_XFER         *'               
498519           DISPLAY '**************************************'               
498520           SET ABEND-SQL-DELETE-ERROR TO TRUE                             
498521           MOVE '3100-PRESEL-FOR-DEL-XFER' TO PV-PARAGRAPH-NAME           
498522           MOVE 'CNTL ID = '  TO PV-ABEND-INFO(1:10)                      
498523           MOVE PV-CNTL-ID-DISPLAY TO PV-ABEND-INFO(11:10)                
498524           PERFORM 9000-USER-ABEND                                        
498525     END-EVALUATE.                                                        
498526                                                                          
498527******************************************************************        
498528* DELETE ALL XFER ROWS FOR THIS ASN WITH DUPLICATE POS.                   
498529******************************************************************        
498530                                                                          
498531 3200-DELETE-XFER-TBL.                                                    
498532                                                                          
498533***  DISPLAY '3200-DELETE-XFER-TBL'.                                      
498534                                                                          
498535     EXEC SQL                                                             
498536         DELETE                                                           
498537           FROM SNT_DAT_XFER                                              
498538          WHERE DAT_XFER_CNTL_ID = :SNT00001-DAT-XFER-CNTL-ID             
498539     END-EXEC.                                                            
498540                                                                          
498541     EVALUATE TRUE                                                        
498542       WHEN SQLCODE = ZERO                                                
498543           CONTINUE                                                       
498544                                                                          
498545       WHEN OTHER                                                         
498546           DISPLAY '**************************************'               
498547           DISPLAY '** ABEND                             *'               
498548           DISPLAY '** DELETE SNT_DAT_XFER               *'               
498549           DISPLAY '**************************************'               
498550           SET ABEND-SQL-DELETE-ERROR TO TRUE                             
498551           MOVE '3200-DELETE-XFER-TBL' TO PV-PARAGRAPH-NAME               
498552           MOVE 'CNTL ID = '  TO PV-ABEND-INFO(1:10)                      
498553           MOVE PV-CNTL-ID-DISPLAY TO PV-ABEND-INFO(11:10)                
498554           PERFORM 9000-USER-ABEND                                        
498555     END-EVALUATE.                                                        
498556                                                                          
498557******************************************************************        
498558* INSERT ROW INTO SNT_DAT_XFER FOR EACH RECORD IN ASN WHERE THERE         
498559* ARE DUPLICATE POS.                                                      
498560******************************************************************        
498561                                                                          
498562 3300-INSERT-XFER-TBL.                                                    
498563                                                                          
498564***  DISPLAY '3300-INSERT-XFER-TBL'.                                      
498565                                                                          
498566     ADD +1 TO PA-INSERT-XFER-COUNT.                                      
498567     MOVE TR063-REC-TYP-CDE     TO SNT00001-DAT-CTG-CDE.                  
498568     MOVE PA-INSERT-XFER-COUNT  TO SNT00001-PRC-LOD-SEQ-NBR.              
498569     MOVE 350                   TO SNT00001-LOD-DAT-TXT-LEN.              
498570                                                                          
498571     EXEC SQL                                                             
498572       INSERT INTO SNT_DAT_XFER                                           
498573              (DAT_XFER_CNTL_ID                                           
498574              ,PRC_LOD_SEQ_NBR                                            
498575              ,DAT_CTG_CDE                                                
498576              ,LOD_DAT_TXT                                                
498577              )                                                           
498578       VALUES                                                             
498579              (:SNT00001-DAT-XFER-CNTL-ID                                 
498580              ,:SNT00001-PRC-LOD-SEQ-NBR                                  
498581              ,:SNT00001-DAT-CTG-CDE                                      
498582              ,:SNT00001-LOD-DAT-TXT                                      
498583              )                                                           
498584     END-EXEC.                                                            
498585                                                                          
498586                                                                          
498587     EVALUATE TRUE                                                        
498588       WHEN SQLCODE = ZERO                                                
498589           CONTINUE                                                       
498590                                                                          
498591       WHEN OTHER                                                         
498592           DISPLAY '**************************************'               
498593           DISPLAY '** ABEND                             *'               
498594           DISPLAY '** INSERT SNT_DAT_XFER               *'               
498595           DISPLAY '**************************************'               
498596           SET ABEND-SQL-INSERT-ERROR TO TRUE                             
498597           MOVE '3300-INSERT-XFER-TBL' TO PV-PARAGRAPH-NAME               
498598           MOVE 'CNTL ID = '  TO PV-ABEND-INFO(1:10)                      
498599           MOVE PV-CNTL-ID-DISPLAY TO PV-ABEND-INFO(11:10)                
498600           MOVE 'SNT00001-LOD-DAT-TXT = '                                 
498601                                   TO PV-ABEND-INFO(21:23)                
498602           MOVE SNT00001-LOD-DAT-TXT                                      
498603                                   TO PV-ABEND-INFO(44:36)                
498604           PERFORM 9000-USER-ABEND                                        
498605     END-EVALUATE.                                                        
498606                                                                          
498607******************************************************************        
498610* RESET THIS ASN ID SO IT NO LONGER INDICATES DUPLICATE PO ASN.           
498700******************************************************************        
498800                                                                          
498900 3400-UPDATE-CNTL-TBL.                                                    
499000                                                                          
499100***  DISPLAY '3400-UPDATE-CNTL-TBL'.                                      
499200                                                                          
499300     EXEC SQL                                                             
499400         UPDATE SNT_DAT_CNTL                                              
499500            SET STAT_CDE = 'C'                                            
499600          WHERE DAT_XFER_CNTL_ID = :SNT00001-DAT-XFER-CNTL-ID             
499610            AND STAT_CDE = 'D'                                            
499700     END-EXEC.                                                            
499800                                                                          
499900     EVALUATE TRUE                                                        
500000       WHEN SQLCODE = ZERO                                                
500100           CONTINUE                                                       
500300                                                                          
500400       WHEN OTHER                                                         
500500           DISPLAY '**************************************'               
500600           DISPLAY '** ABEND                             *'               
500700           DISPLAY '** UPDATE SNT_DAT_CNTL               *'               
500900           DISPLAY '**************************************'               
501000           SET ABEND-SQL-UPDATE-ERROR TO TRUE                             
501100           MOVE '3400-UPDATE-CNTL-TBL' TO PV-PARAGRAPH-NAME               
501110           MOVE 'CNTL ID = '  TO PV-ABEND-INFO(1:10)                      
501120           MOVE PV-CNTL-ID-DISPLAY TO PV-ABEND-INFO(11:10)                
501300           PERFORM 9000-USER-ABEND                                        
501400     END-EVALUATE.                                                        
501500                                                                          
502800******************************************************************        
502900* DB2 COMMIT AT THE END OF EACH ASN WITH DUPLICATE POS.                   
503000******************************************************************        
503100                                                                          
503200 3500-COMMIT.                                                             
503300                                                                          
503400***  DISPLAY '3500-COMMIT'.                                               
503500                                                                          
503600     EXEC SQL                                                             
503700         COMMIT                                                           
503800     END-EXEC.                                                            
503900                                                                          
504000     EVALUATE TRUE                                                        
504100       WHEN SQLCODE = ZERO                                                
504200           DISPLAY '  **** COMMITTED HERE ****'                           
504300                                                                          
504400       WHEN OTHER                                                         
504500           DISPLAY '**************************************'               
504600           DISPLAY '** ABEND                             *'               
504700           DISPLAY '** DB2 COMMIT                        *'               
504800           DISPLAY '**************************************'               
504900           SET ABEND-SQL-COMMIT-ERROR TO TRUE                             
505000           MOVE '3500-COMMIT' TO PV-PARAGRAPH-NAME                        
505110           MOVE 'CNTL ID = '  TO PV-ABEND-INFO(1:10)                      
505120           MOVE PV-CNTL-ID-DISPLAY TO PV-ABEND-INFO(11:10)                
505200           PERFORM 9000-USER-ABEND                                        
505300     END-EVALUATE.                                                        
505400                                                                          
505500******************************************************************        
505600* DISPLAY TOTALS FOR ASN AND INITIALIZE COUNTERS.                         
505700******************************************************************        
505800                                                                          
505900 4000-WRAP-UP-ASN.                                                        
506000                                                                          
506100***  DISPLAY '4000-WRAP-UP-ASN'.                                          
506200                                                                          
506300     ADD +1 TO PA-UPDATE-CNTL-TTL.                                        
506400     DISPLAY ' ' PV-CNTL-ID-DISPLAY                                       
506500             ' ' PV-CRE-TMST-DISPLAY                                      
506600             ' ' PA-DELETE-XFER-COUNT                                     
506700             ' ' PA-INSERT-XFER-COUNT.                                    
506800     ADD PA-DELETE-XFER-COUNT TO PA-DELETE-XFER-TTL.                      
506900     ADD PA-INSERT-XFER-COUNT TO PA-INSERT-XFER-TTL.                      
507000     INITIALIZE PA-DELETE-XFER-COUNT                                      
507100                PA-INSERT-XFER-COUNT.                                     
507200     SET PS-ASN-HAS-DUP-PO-NO TO TRUE.                                    
253800                                                                          
253800                                                                          
263700******************************************************************        
263800*    SET THE TIME FOR THE NEXT CHECKPOINT.                       *        
263900******************************************************************        
264000 4020-SET-COMMIT-TIME.                                                    
264100                                                                          
264200      EXEC SQL                                                            
264300          SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)            
264400            INTO :PV-HOLD-TMST                                            
264500            FROM TCKRSTCNTL                                               
264600           WHERE PLAN_NAME = :PC-CURRENT-PROGRAM-NAME                     
264700      END-EXEC.                                                           
264800                                                                          
264900     EVALUATE TRUE                                                        
265000         WHEN SQLWARN0 NOT EQUAL SPACE                                    
265100         WHEN SQLCODE NOT EQUAL ZERO                                      
497600           DISPLAY '**************************************'               
497700           DISPLAY '** ABEND                             *'               
498000           DISPLAY '** SELECT COMMIT TIME                *'               
498100           DISPLAY '**************************************'               
498110           SET ABEND-SQL-SELECT-ERROR TO TRUE                             
498120           MOVE '4020-SET-COMMIT-TIME' TO PV-PARAGRAPH-NAME               
498130           MOVE 'TR063-DAT-XFER-CNTL-ID = ' TO PV-ABEND-INFO(1:25)        
498132           MOVE TR063-DAT-XFER-CNTL-ID TO PV-ABEND-INFO(26:10)            
498200           PERFORM 9000-USER-ABEND                                        
269400     END-EVALUATE.                                                        
270500                                                                          
270700                                                                          
270800******************************************************************        
270900*    CHECK TO SEE IF A COMMIT SHOULD BE TAKEN.                   *        
271000******************************************************************        
271100 4100-COMMIT-CHECK.                                                       
271200                                                                          
271300     EXEC SQL                                                             
271400       SET :PV-TMST = CURRENT TIMESTAMP                                   
271500     END-EXEC.                                                            
271600                                                                          
271700     EVALUATE TRUE                                                        
271800         WHEN SQLWARN0 NOT EQUAL SPACE                                    
271900         WHEN SQLCODE NOT EQUAL ZERO                                      
497600           DISPLAY '**************************************'               
497700           DISPLAY '** ABEND                             *'               
498000           DISPLAY '** SELECT CURRENT TIMESTAMP          *'               
498100           DISPLAY '**************************************'               
498110           SET ABEND-SQL-SELECT-ERROR TO TRUE                             
498120           MOVE '4100-COMMIT-CHECK   ' TO PV-PARAGRAPH-NAME               
498130           MOVE 'TR063-DAT-XFER-CNTL-ID = ' TO PV-ABEND-INFO(1:25)        
498132           MOVE TR063-DAT-XFER-CNTL-ID TO PV-ABEND-INFO(26:10)            
498200           PERFORM 9000-USER-ABEND                                        
276200     END-EVALUATE.                                                        
276300                                                                          
276400     IF PV-TMST > PV-HOLD-TMST                                            
277100         PERFORM 3500-COMMIT                                              
279100         PERFORM 4020-SET-COMMIT-TIME                                     
279200     END-IF.                                                              
279300                                                                          
279500                                                                          
809217******************************************************************        
809218* READ A RECORD FROM THE INPUT FILE.                                      
809219******************************************************************        
809220 6000-READ-INPUT-FILE.                                                    
809221                                                                          
809222***  DISPLAY '6000-READ-INPUT-FILE'.                                      
809230                                                                          
809233     READ PV-INPUT-FILE                                                   
809234         INTO PV-INPUT-REC-LAYOUT                                         
809235      AT END                                                              
809236         SET PS-END-OF-INPUT TO TRUE.                                     
809237                                                                          
809238     IF PS-END-OF-INPUT-NO                                                
809301       ADD +1 TO PA-INPUT-COUNT                                           
809302       MOVE PV-INPUT-REC-LAYOUT TO TR063-SHIP-NOTICE-DTL-RECORD           
809305     END-IF.                                                              
810409                                                                          
836192******************************************************************        
836193* CLOSE THE INPUT AND OUTPUT FILES AND DISPLAY MISCELLANEOUS     *        
836194* JOB SUMMARY INFORMATION.                                       *        
836200******************************************************************        
836300 8000-EOJ-ROUTINE.                                                        
836301                                                                          
836310***  DISPLAY '8000-EOJ-ROUTINE'.                                          
836400                                                                          
842300     CLOSE PV-INPUT-FILE.                                                 
842400     PERFORM 8010-EOJ-DISPLAY.                                            
843002                                                                          
843003******************************************************************        
843004* AT END OF JOB OR BEFORE USER ABEND, DISPLAY TOTALS FOR         *        
843005* COMMITED UPDATES AND DELETES.                                  *        
843006******************************************************************        
843007 8010-EOJ-DISPLAY.                                                        
843008                                                                          
843010     DISPLAY '+++ END LIST CONTROL IDS +++'.                              
843011     DISPLAY ' '.                                                         
843015                                                                          
843016     DISPLAY '*************************************************'.         
843017     DISPLAY '** TRKUP211 SUMMARY                            **'.         
843018     DISPLAY '*************************************************'.         
843019     DISPLAY 'TOTAL RECORDS READ        = ' PA-INPUT-COUNT.               
843022     DISPLAY '*************************************************'.         
843023     DISPLAY 'TOTAL UPDATES MADE TO SNT_DAT_CNTL  = '                     
843024                                     PA-UPDATE-CNTL-TTL.                  
843025     DISPLAY '*************************************************'.         
843026     DISPLAY 'TOTAL DELETED ROWS FROM SNT_DAT_XFER  = '                   
843027                                     PA-DELETE-XFER-TTL.                  
843028     DISPLAY '*************************************************'.         
843029     DISPLAY 'TOTAL INSERTED ROWS TO SNT_DAT_XFER  = '                    
843030                                     PA-INSERT-XFER-TTL.                  
843040                                                                          
843041******************************************************************        
843042* USER ABEND                                                     *        
843043******************************************************************        
843044 9000-USER-ABEND.                                                         
843045                                                                          
843046***  DISPLAY '9000-USER-ABEND'.                                           
843047                                                                          
843048     PERFORM 8010-EOJ-DISPLAY.                                            
843049                                                                          
843050     DISPLAY '+++++++++++++++++++++++++++++++++++++++++++++'.             
843051     DISPLAY 'PROGRAM = TRKUP211'                                         
843052     DISPLAY 'THIS IS A USER ABEND GENERATED BY PARAGRAPH -'.             
843053     DISPLAY ' ' PV-PARAGRAPH-NAME,                                       
843054     DISPLAY ' ' PV-ABEND-INFO.                                           
843055     DISPLAY ' ' 'ASN = ' PV-IN-ISA-CNTL-NBR-SEQ.                         
843056                                                                          
843057******************************************************************        
843058* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
843059* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
843060* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
843061* FORMAT.                                                        *        
843062******************************************************************        
843063     COPY DPPD004.                                                        
843064     CALL 'ILBOABN0' USING ABEND-CODE.                                    
843070                                                                          
