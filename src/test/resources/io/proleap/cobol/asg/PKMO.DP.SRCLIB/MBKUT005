00001 *************************                                         02/27/92
00002  IDENTIFICATION DIVISION.                                         MBKUT005
00003 *************************                                            LV022
00004  PROGRAM-ID.    MBKUT005.                                            CL**1
00005  AUTHOR.        MIKE FURLICK.                                        CL**1
00006  DATE-WRITTEN.  NOVEMBER 25, 1991.                                   CL**1
00007 *                                                                    CL**1
00008 *****************************************************************    CL**1
00009 *                                                               *    CL**1
00010 *    THIS PROGRAM PROCESSES THE 'MBKUT005' RECORD TYPES FROM    *    CL**1
00011 *    THE VSAM BATCH CONTROL DATASET.                            *    CL**1
00012 *                                                               *    CL**1
00013 *    THE BATCH CNTL RECORD IS READ AND A CURSOR IS OPENED BASED *    CL**1
00014 *    UPON THE CONTENTS OF THE BATCH CONTROL RECORD.  FOR EACH   *    CL**1
00015 *    ROW RETURNED, A FLAT FILE RECORD CONTAINING APLAVL LOAD    *    CL**1
00016 *    DATA IS WRITTEN.  AFTER ALL APLAVL DATA IS WRITTEN THE     *    CL**1
00017 *    BATCH CONTROL VSAM RECORD IS DELETED.  THE PROCESS IS      *    CL**1
00018 *    CONTINUED UNTIL ALL TYPE '005' RECORDS ARE PROCESSED.      *    CL**1
00019 *                                                               *    CL**1
00020 *****************************************************************    CL**1
00021 *                                                                    CL**1
00022  ENVIRONMENT DIVISION.                                               CL**1
00023  CONFIGURATION SECTION.                                              CL**1
00024  SOURCE-COMPUTER. IBM-3090.                                          CL**1
00025  OBJECT-COMPUTER. IBM-3090.                                          CL**1
00026 *                                                                    CL**1
00027  INPUT-OUTPUT SECTION.                                               CL**1
00028  FILE-CONTROL.                                                       CL**1
00029 *                                                                    CL**1
00030      SELECT BATCH-CONTROL-FILE                                       CL**1
00031          ORGANIZATION IS INDEXED                                     CL**1
00032          RECORD KEY IS BCR-KEY                                       CL**3
00033          FILE STATUS IS PV-VSAM-STATUS                               CL**1
00034          ACCESS IS SEQUENTIAL                                        CL**1
00035          ASSIGN TO INP01.                                            CL**1
00036 *                                                                    CL**1
00037      SELECT APLAVL-LOAD-FILE                                         CL**1
00038          ASSIGN TO OUT01.                                            CL**5
00039 *                                                                    CL**1
00040  DATA DIVISION.                                                      CL**1
00041  FILE SECTION.                                                       CL**1
00042 *                                                                    CL**1
00043 *                                                                    CL**1
00044  FD  BATCH-CONTROL-FILE.                                             CL**1
00045 *                                                                    CL**1
00046  01  BATCH-CONTROL-RECORD.                                           CL**3
00047      05  BCR-KEY               PIC X(30).                            CL**3
00048      05  BCR-DATA              PIC X(70).                            CL**3
00049 *                                                                    CL**1
00050 *                                                                    CL**1
00051  FD  APLAVL-LOAD-FILE                                                CL**1
00052      RECORDING MODE IS F                                             CL**1
00053      BLOCK CONTAINS 0 CHARACTERS                                     CL**1
00054      LABEL RECORDS ARE STANDARD.                                     CL**1
00055 *                                                                    CL**1
00056  01  APLAVL-LOAD-RECORD            PIC X(28).                        CL**1
00057  EJECT                                                               CL**1
00058  WORKING-STORAGE SECTION.                                            CL**1
00059 *                                                                    CL**1
00060  01  PC-PROGRAM-CONSTANTS.                                           CL**1
00061      05  PC-GMA                    PIC X(03)  VALUE 'GMA'.           CL**1
00062      05  PC-DMA                    PIC X(03)  VALUE 'DMA'.           CL**1
00063      05  PC-DPT                    PIC X(03)  VALUE 'DPT'.           CL**1
00064      05  PC-SCL                    PIC X(03)  VALUE 'SCL'.           CL**1
00065      05  PC-MAX-RETRIES            PIC S9(3)  VALUE +3               CL**1
00066                                               COMP-3.                CL**1
00067 *                                                                    CL**1
00068  01  PS-PROGRAM-SWITCHES.                                            CL**1
00069      05  PS-DATA-SW                PIC X      VALUE 'N'.             CL**1
00070          88  PS-END-OF-DATA                   VALUE 'Y'.             CL**1
00071          88  PS-NOT-END-OF-DATA               VALUE 'N'.             CL**1
00072      05  PS-ACTIVE-SW              PIC X      VALUE 'N'.             CL**1
00073          88  PS-DKEY-ACTIVE                   VALUE 'Y'.             CL**1
00074          88  PS-DKEY-INACTIVE                 VALUE 'N'.             CL**1
00075      05  PS-USIT-SWITCH            PIC X      VALUE 'N'.             CL*18
00076          88  PS-USE-IT                        VALUE 'Y'.             CL*20
00077          88  PS-ALREADY-EXISTS                VALUE 'N'.             CL*20
00078 *                                                                    CL**1
00079  01  PV-PROGRAM-VARIABLES.                                           CL**1
00080      05  PV-WRITE-COUNT            PIC S9(9)  VALUE ZERO             CL**4
00081                                               COMP-3.                CL**1
00082      05  PV-SKIP-COUNT             PIC S9(9)  VALUE ZERO             CL*18
00083                                               COMP-3.                CL*18
00084      05  PV-APLAVL-COUNT           PIC S9(9)  VALUE ZERO             CL**4
00085                                               COMP-3.                CL**4
00086      05  PV-APLAVL-SKIP-COUNT      PIC S9(9)  VALUE ZERO             CL*18
00087                                               COMP-3.                CL*18
00088      05  PV-BATCH-CNTL-COUNT       PIC S9(9)  VALUE ZERO             CL**2
00089                                               COMP-3.                CL**2
00090      05  PV-PARAGRAPH-NAME         PIC X(30)  VALUE SPACE.           CL**1
00091      05  PV-DB2-OPER-ATTEMPTED     PIC X(30)  VALUE SPACE.           CL**1
00092      05  PV-SQL-SUB                PIC S9(4)  VALUE ZERO             CL**1
00093                                               COMP.                  CL**1
00094      05  PV-VSAM-STATUS            PIC XX     VALUE SPACE.           CL**1
00095          88  PV-GOOD-STATUS                   VALUE '00'.            CL**1
00096          88  PV-AT-END                        VALUE '10'.            CL**2
00097          88  PV-VERIFIED                      VALUE '97'.            CL*10
00098 *                                                                    CL**1
00099  01  ABEND-CODE                    PIC S9(4)  VALUE ZERO             CL**1
00100                                               COMP.                  CL**1
00101      88  ABEND-4001                           VALUE 4001.            CL**1
00102      88  ABEND-4002                           VALUE 4002.            CL**6
00103      88  ABEND-4003                           VALUE 4003.            CL**6
00104      88  ABEND-4004                           VALUE 4004.            CL**6
00105      88  ABEND-4005                           VALUE 4005.            CL*13
00106      88  ABEND-4006                           VALUE 4006.            CL*12
00107      88  ABEND-4013                           VALUE 4013.            CL**1
00108      EJECT                                                           CL**1
00109 *                                                                    CL**3
00110 *    RECORD LAYOUT FOR THE BATCH CONTROL FILE                        CL**3
00111 *                                                                    CL**3
00112      COPY MBWS018.                                                   CL*22
00113 *                                                                    CL**1
00114 *    MDSEAR DCLGEN                                                   CL**1
00115 *                                                                    CL**1
00116      EXEC SQL                                                        CL**1
00117          INCLUDE TMDSEAR                                             CL**1
00118      END-EXEC.                                                       CL**1
00119 *                                                                    CL**1
00120 *    APLAVL DCLGEN                                                   CL**1
00121 *                                                                    CL**1
00122      EXEC SQL                                                        CL**1
00123          INCLUDE TAPLAVL                                             CL**1
00124      END-EXEC.                                                       CL**1
00125 *                                                                    CL**1
00126 *    SQL COMMUNICATIONS AREA DCLGEN                                  CL**1
00127 *                                                                    CL**1
00128      EXEC SQL                                                        CL**1
00129          INCLUDE SQLCA                                               CL**1
00130      END-EXEC.                                                       CL**1
00131 *                                                                    CL**1
00132 *    ERROR MESSAGE AREA FOR DB2                                      CL**1
00133 *                                                                    CL**1
00134      COPY DPWS004.                                                   CL**1
00135 *                                                                    CL**1
00136 *    CURSOR DECLARATION FOR MDSEAR.                                  CL**1
00137 *                                                                    CL**1
00138      EXEC SQL                                                        CL**1
00139          DECLARE MERCH-LIKE CURSOR                                   CL**1
00140              FOR SELECT                                              CL**1
00141                         A.MDSEAR_DKEY                                CL**1
00142                       , B.MDSEAR_STRT_DTE                            CL*17
00143                       , B.MDSEAR_END_DTE                             CL*17
00144                       , A.LIST_SEQ_NBR                               CL**1
00145              FROM                                                    CL**1
00146                         TMDSEAR A                                    CL**1
00147                       , TAPLAVL B                                    CL**1
00148              WHERE                                                   CL**1
00149                   (A.OPERCO_NBR  = :MB018-OPERATING-CO-005           CL**1
00150               AND  A.END_DTE     > :MB018-EFFECTIVE-START-DATE-005   CL**1
00151               AND  A.MDSEAR_DKEY = B.MDSEAR_DKEY                     CL**1
00152               AND  B.APLSYS_CDE  = :MB018-LIKE-APPL-SYS-CODE-005)    CL**1
00153          ORDER BY                                                    CL**1
00154                    A.LIST_SEQ_NBR                                    CL**1
00155      END-EXEC.                                                       CL**1
00156  EJECT                                                               CL**1
00157  PROCEDURE DIVISION.                                                 CL**1
00158 *                                                                    CL**1
00159      PERFORM 1000-MAINLINE.                                          CL**1
00160      GOBACK.                                                         CL**1
00161  1000-MAINLINE.                                                      CL**1
00162 *                                                                    CL**1
00163      PERFORM 2000-OPEN-BATCH-CONTROL.                                CL**1
00164      IF (PV-GOOD-STATUS                                              CL*10
00165      OR  PV-VERIFIED)                                                CL*10
00166          SET MB018-CREAT-NEW-APPL-SYS-AVAIL                          CL**1
00167                                TO  TRUE                              CL**1
00168          PERFORM 3000-PROCESS-BATCH-CONTROL                          CL**1
00169              UNTIL NOT MB018-CREAT-NEW-APPL-SYS-AVAIL                CL**1
00170          DISPLAY '* * * * * * * * * * * * * * * * * * * * * * *'     CL**7
00171          PERFORM 4000-CLOSE-BATCH-CONTROL                            CL**1
00172          PERFORM 9000-DISPLAY-TOTALS                                 CL**1
00173      ELSE                                                            CL**1
00174          DISPLAY '*******************************'                   CL**1
00175          DISPLAY '*                             *'                   CL**1
00176          DISPLAY '* EMPTY DATASET FOR MBKUT005. *'                   CL**1
00177          DISPLAY '*                             *'                   CL**1
00178          DISPLAY '* NO APLAVL RECORDS CREATED.  *'                   CL**1
00179          DISPLAY '*                             *'                   CL**1
00180          DISPLAY '* NORMAL END OF JOB OCCURED.  *'                   CL**1
00181          DISPLAY '*******************************'                   CL**1
00182      END-IF.                                                         CL**1
00183 *                                                                    CL**1
00184  EJECT                                                               CL**1
00185 *****************************************************************    CL**1
00186 *                                                               *    CL**1
00187 *    THIS ROUTINE SIMPLY OPENS THE BATCH CONTROL FILE.          *    CL**1
00188 *                                                               *    CL**1
00189 *****************************************************************    CL**1
00190 *                                                                    CL**1
00191  2000-OPEN-BATCH-CONTROL.                                            CL**1
00192 *                                                                    CL**1
00193      OPEN I-O BATCH-CONTROL-FILE                                     CL**4
00194           OUTPUT APLAVL-LOAD-FILE.                                   CL**4
00195 *                                                                    CL**1
00196      IF (PV-GOOD-STATUS                                              CL*11
00197      OR  PV-AT-END                                                   CL**1
00198      OR  PV-VERIFIED)                                                CL*11
00199          CONTINUE                                                    CL**1
00200      ELSE                                                            CL**1
00201          DISPLAY '* * * * * * * * * * * * * * * * *'                 CL**1
00202          DISPLAY '*    ***MBKUT005 ABEND***       *'                 CL**1
00203          DISPLAY '*                               *'                 CL**1
00204          DISPLAY '*   INVALID STATUS ON THE BATCH *'                 CL**1
00205          DISPLAY '*   CONTROL DATASET - OPEN.     *'                 CL**2
00206          DISPLAY '*                               *'                 CL**1
00207          DISPLAY '* STATUS = '                                       CL**1
00208                  PV-VSAM-STATUS                                      CL**1
00209                  '                  *'                               CL**1
00210          DISPLAY '*                               *'                 CL**1
00211          DISPLAY '* * * * * * * * * * * * * * * * *'                 CL**1
00212          SET ABEND-4005        TO  TRUE                              CL*12
00213          CALL 'ILBOABN0'    USING  ABEND-CODE                        CL**1
00214      END-IF.                                                         CL**1
00215 *                                                                    CL*14
00216      MOVE LOW-VALUES           TO  MB018-BCR-KEY.                    CL*14
00217      SET MB018-CREAT-NEW-APPL-SYS-AVAIL                              CL*14
00218                                TO  TRUE.                             CL*14
00219      MOVE MB018-BCR-KEY-005    TO  BCR-KEY.                          CL*14
00220      START BATCH-CONTROL-FILE                                        CL*14
00221            KEY IS GREATER THAN BCR-KEY.                              CL*14
00222 *                                                                    CL**1
00223  EJECT                                                               CL**1
00224 ****************************************************************     CL**1
00225 *                                                              *     CL**1
00226 *    READ THE VSAM RECORD, OPEN THE CURSOR, BUILD THE FLAT     *     CL**1
00227 *    FILE APLAVL RECORDS.                                      *     CL**1
00228 *                                                              *     CL**1
00229 ****************************************************************     CL**1
00230 *                                                                    CL**1
00231  3000-PROCESS-BATCH-CONTROL.                                         CL**1
00232 *                                                                    CL**2
00233      PERFORM 3100-READ-BATCH-CONTROL.                                CL**3
00234      IF  MB018-CREAT-NEW-APPL-SYS-AVAIL                              CL**1
00235          DISPLAY '* * * * * * * * * * * * * * * * * * * * * * *'     CL**7
00236          DISPLAY '*'                                                 CL**7
00237          DISPLAY '* ' MB018-BATCH-CNTL-RECORD                        CL**7
00238          DISPLAY '*'                                                 CL**7
00239          ADD 1                 TO  PV-BATCH-CNTL-COUNT               CL**2
00240          PERFORM 3200-OPEN-CURSOR                                    CL**2
00241          PERFORM 3300-CREATE-NEW-APPL-SYS                            CL**2
00242              UNTIL SQLCODE NOT EQUAL ZERO                            CL**2
00243          PERFORM 3500-CLOSE-CURSOR                                   CL**2
00244          PERFORM 3600-DELETE-BATCH-CONTROL                           CL**2
00245      END-IF.                                                         CL**1
00246 *                                                                    CL**2
00247  EJECT                                                               CL**2
00248 ****************************************************************     CL**2
00249 *                                                              *     CL**2
00250 *    HERE WE READ THE BATCH CONTROL FILE.                      *     CL**2
00251 *                                                              *     CL**2
00252 ****************************************************************     CL**2
00253 *                                                                    CL**2
00254  3100-READ-BATCH-CONTROL.                                            CL**3
00255 *                                                                    CL**2
00256      READ BATCH-CONTROL-FILE                                         CL**3
00257           INTO MB018-BATCH-CNTL-RECORD.                              CL**3
00258      IF  PV-GOOD-STATUS                                              CL**2
00259      OR  PV-AT-END                                                   CL**2
00260          CONTINUE                                                    CL**7
00261      ELSE                                                            CL**2
00262          DISPLAY '* * * * * * * * * * * * * * * * *'                 CL**2
00263          DISPLAY '*    ***MBKUT005 ABEND***       *'                 CL**2
00264          DISPLAY '*                               *'                 CL**2
00265          DISPLAY '*   INVALID STATUS ON THE BATCH *'                 CL**2
00266          DISPLAY '*   CONTROL DATASET - READ.     *'                 CL**2
00267          DISPLAY '*                               *'                 CL**2
00268          DISPLAY '* STATUS = '                                       CL**2
00269                  PV-VSAM-STATUS                                      CL**2
00270                  '                  *'                               CL**2
00271          DISPLAY '*                               *'                 CL**2
00272          DISPLAY '* * * * * * * * * * * * * * * * *'                 CL**2
00273          SET ABEND-4002        TO  TRUE                              CL**9
00274          CALL 'ILBOABN0'    USING  ABEND-CODE                        CL**2
00275      END-IF.                                                         CL**2
00276 *                                                                    CL**8
00277      IF  PV-AT-END                                                   CL**8
00278          MOVE LOW-VALUES       TO  MB018-BATCH-CNTL-RECORD           CL**8
00279      END-IF.                                                         CL**8
00280 *                                                                    CL**1
00281  EJECT                                                               CL**1
00282 ****************************************************************     CL**1
00283 *                                                              *     CL**1
00284 *    HERE WE ONLY OPEN THE CURSOR.                             *     CL**1
00285 *                                                              *     CL**1
00286 ****************************************************************     CL**1
00287 *                                                                    CL**1
00288  3200-OPEN-CURSOR.                                                   CL**2
00289 *                                                                    CL**1
00290      PERFORM                                                         CL**1
00291          WITH TEST AFTER                                             CL**1
00292          VARYING PV-SQL-SUB                                          CL**1
00293          FROM 1 BY 1                                                 CL**1
00294          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**1
00295                    OR SQLCODE = 0                                    CL**1
00296                    OR SQLCODE = +100)                                CL**1
00297 *                                                                    CL**2
00298          EXEC SQL                                                    CL**1
00299              OPEN MERCH-LIKE                                         CL**1
00300          END-EXEC                                                    CL**1
00301 *                                                                    CL**2
00302          EVALUATE TRUE                                               CL**1
00303              WHEN SQLCODE = -904                                     CL**1
00304              WHEN SQLCODE = -911                                     CL**1
00305              WHEN SQLCODE = +100                                     CL**1
00306              WHEN SQLCODE = 0                                        CL**1
00307                  CONTINUE                                            CL**1
00308              WHEN SQLWARN NOT = SPACES                               CL**1
00309              WHEN SQLCODE NOT = ZERO                                 CL**1
00310                  MOVE '3200-OPEN-CURSOR'                             CL**2
00311                                TO  PV-PARAGRAPH-NAME                 CL**1
00312                  MOVE 'OPEN ON MDSEAR CURSOR'                        CL**1
00313                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00314                  PERFORM 9900-SQL-ABEND                              CL**1
00315          END-EVALUATE                                                CL**1
00316      END-PERFORM.                                                    CL**1
00317 *                                                                    CL**1
00318      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**1
00319          MOVE '3200-OPEN-CURSOR'                                     CL**2
00320                                TO  PV-PARAGRAPH-NAME                 CL**1
00321          MOVE 'OPEN RETRIES EXCEEDED'                                CL**1
00322                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00323          PERFORM 9900-SQL-ABEND                                      CL**1
00324      END-IF.                                                         CL**1
00325 *                                                                    CL**1
00326  EJECT                                                               CL**1
00327 ****************************************************************     CL**1
00328 *                                                              *     CL**1
00329 *    THIS ROUTINE RETRIEVES A ROW AND BUILDS THE NEW APLAVL    *     CL*15
00330 *    RECORD.                                                   *     CL**2
00331 *                                                              *     CL**1
00332 ****************************************************************     CL**1
00333 *                                                                    CL**1
00334  3300-CREATE-NEW-APPL-SYS.                                           CL**2
00335 *                                                                    CL**1
00336      PERFORM 3400-FETCH-CURSOR.                                      CL**2
00337 *                                                                    CL**2
00338      IF  SQLCODE EQUAL ZERO                                          CL**2
00339          PERFORM 3700-CHECK-NEW-KEY                                  CL*18
00340          MOVE ZERO             TO  SQLCODE                           CL*21
00341          IF  PS-USE-IT                                               CL*18
00342              MOVE MB018-NEW-APPL-SYS-CODE-005                        CL*18
00343                                TO  APLAVL-APLSYS-CDE                 CL*18
00344              MOVE MDSEAR-DKEY  TO  APLAVL-MDSEAR-DKEY                CL*18
00345 *                                                                    CL*18
00346              IF  MB018-EFFECTIVE-START-DATE-005 GREATER THAN         CL*18
00347                  APLAVL-MDSEAR-STRT-DTE                              CL*18
00348                  MOVE MB018-EFFECTIVE-START-DATE-005                 CL*18
00349                                TO  APLAVL-MDSEAR-STRT-DTE            CL*18
00350              END-IF                                                  CL*18
00351 *                                                                    CL*18
00352              MOVE MB018-OPERATING-CO-005                             CL*18
00353                                TO  APLAVL-OPERCO-NBR                 CL*18
00354 *                                                                    CL*18
00355              WRITE APLAVL-LOAD-RECORD FROM DCLTAPLAVL                CL*18
00356              ADD 1             TO  PV-APLAVL-COUNT                   CL*18
00357          ELSE                                                        CL*18
00358              ADD 1             TO  PV-APLAVL-SKIP-COUNT              CL*18
00359          END-IF                                                      CL*18
00360      END-IF.                                                         CL*18
00361 *                                                                    CL*18
00362  EJECT                                                               CL**2
00363 ****************************************************************     CL**2
00364 *                                                              *     CL**2
00365 *    THIS ROUTINE RETRIEVE A ROW                               *     CL**2
00366 *                                                              *     CL**2
00367 ****************************************************************     CL**2
00368 *                                                                    CL**2
00369  3400-FETCH-CURSOR.                                                  CL**2
00370 *                                                                    CL**2
00371      PERFORM                                                         CL**1
00372          WITH TEST AFTER                                             CL**1
00373          VARYING PV-SQL-SUB                                          CL**1
00374          FROM 1 BY 1                                                 CL**1
00375          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**1
00376                    OR SQLCODE = 0                                    CL**1
00377                    OR SQLCODE = +100)                                CL**1
00378 *                                                                    CL**1
00379          EXEC SQL                                                    CL**1
00380              FETCH                                                   CL**1
00381                    MERCH-LIKE                                        CL**1
00382               INTO                                                   CL**1
00383                    :MDSEAR-DKEY                                      CL**2
00384                  , :APLAVL-MDSEAR-STRT-DTE                           CL*17
00385                  , :APLAVL-MDSEAR-END-DTE                            CL*17
00386                  , :MDSEAR-LIST-SEQ-NBR                              CL**1
00387          END-EXEC                                                    CL**1
00388 *                                                                    CL**1
00389          EVALUATE TRUE                                               CL**1
00390              WHEN SQLCODE = -904                                     CL**1
00391              WHEN SQLCODE = -911                                     CL**1
00392              WHEN SQLCODE = +100                                     CL**1
00393              WHEN SQLCODE = 0                                        CL**1
00394                  CONTINUE                                            CL**1
00395              WHEN SQLWARN NOT = SPACES                               CL**1
00396              WHEN SQLCODE NOT = ZERO                                 CL**1
00397                  MOVE '5300-FETCH-CURSOR'                            CL**2
00398                                TO  PV-PARAGRAPH-NAME                 CL**1
00399                  MOVE 'FETCH MERCH-LIKE CURSOR'                      CL**1
00400                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00401                  PERFORM 9900-SQL-ABEND                              CL**1
00402          END-EVALUATE                                                CL**1
00403      END-PERFORM.                                                    CL**1
00404 *                                                                    CL**1
00405      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**1
00406          MOVE '3400-FETCH-CURSOR'                                    CL**2
00407                                TO  PV-PARAGRAPH-NAME                 CL**1
00408          MOVE 'FETCH RETRIES EXCEEDED'                               CL**1
00409                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00410          PERFORM 9900-SQL-ABEND                                      CL**1
00411      END-IF.                                                         CL**1
00412 *                                                                    CL**2
00413  EJECT                                                               CL**2
00414 ****************************************************************     CL**2
00415 *                                                              *     CL**2
00416 *    CLOSE THE MERCH-LIKE CURSOR.                              *     CL**2
00417 *                                                              *     CL**2
00418 ****************************************************************     CL**2
00419 *                                                                    CL**2
00420  3500-CLOSE-CURSOR.                                                  CL**2
00421 *                                                                    CL**2
00422      PERFORM                                                         CL**1
00423          WITH TEST AFTER                                             CL**1
00424          VARYING PV-SQL-SUB                                          CL**1
00425          FROM 1 BY 1                                                 CL**1
00426          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**1
00427                    OR SQLCODE = 0)                                   CL**1
00428          EXEC SQL                                                    CL**1
00429             CLOSE MERCH-LIKE                                         CL**1
00430          END-EXEC                                                    CL**1
00431          EVALUATE TRUE                                               CL**1
00432              WHEN SQLCODE = -904                                     CL**1
00433              WHEN SQLCODE = -911                                     CL**1
00434              WHEN SQLCODE = 0                                        CL**1
00435                  CONTINUE                                            CL**1
00436              WHEN SQLWARN NOT = SPACES                               CL**1
00437              WHEN SQLCODE NOT = ZERO                                 CL**1
00438                  MOVE '3500-CLOSE-CURSOR'                            CL**2
00439                                TO  PV-PARAGRAPH-NAME                 CL**1
00440                  MOVE 'CLOSE ON MDSEAR CURSOR'                       CL**1
00441                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00442                  PERFORM 9900-SQL-ABEND                              CL**1
00443          END-EVALUATE                                                CL**1
00444      END-PERFORM.                                                    CL**1
00445 *                                                                    CL**1
00446      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**1
00447          MOVE '3500-CLOSE-CURSOR'                                    CL**2
00448                                TO  PV-PARAGRAPH-NAME                 CL**1
00449          MOVE 'CLOSE RETRIES EXCEEDED'                               CL**1
00450                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00451          PERFORM 9900-SQL-ABEND                                      CL**1
00452      END-IF.                                                         CL**1
00453 *                                                                    CL**1
00454  EJECT                                                               CL**1
00455 ****************************************************************     CL**1
00456 *                                                              *     CL**1
00457 *    DELETE THE CURRENT BATCH CONTROL RECORD.                  *     CL**2
00458 *                                                              *     CL**1
00459 ****************************************************************     CL**1
00460 *                                                                    CL**1
00461  3600-DELETE-BATCH-CONTROL.                                          CL**2
00462 *                                                                    CL**1
00463      DISPLAY '*' PV-APLAVL-COUNT                                     CL**4
00464              ' APLAVL RECORDS WRITTEN AND'.                          CL*18
00465      DISPLAY '*' PV-APLAVL-SKIP-COUNT                                CL*18
00466              ' APLAVL RECORDS SKIPPED FOR'.                          CL*18
00467      DISPLAY '*          PREVIOUSLY LISTED CONTROL RECORD'.          CL**7
00468      ADD PV-APLAVL-COUNT       TO  PV-WRITE-COUNT.                   CL**4
00469      ADD PV-APLAVL-SKIP-COUNT  TO  PV-SKIP-COUNT.                    CL*18
00470      MOVE ZERO                 TO  PV-APLAVL-COUNT                   CL*18
00471                                    PV-APLAVL-SKIP-COUNT.             CL*18
00472      DELETE BATCH-CONTROL-FILE RECORD.                               CL**2
00473 *                                                                    CL**2
00474      IF  PV-GOOD-STATUS                                              CL**2
00475          CONTINUE                                                    CL**2
00476      ELSE                                                            CL**2
00477          DISPLAY '* * * * * * * * * * * * * * * * *'                 CL**2
00478          DISPLAY '*    ***MBKUT005 ABEND***       *'                 CL**2
00479          DISPLAY '*                               *'                 CL**2
00480          DISPLAY '*   INVALID STATUS ON THE BATCH *'                 CL**2
00481          DISPLAY '*   CONTROL DATASET - DELETE    *'                 CL**2
00482          DISPLAY '*                               *'                 CL**2
00483          DISPLAY '* STATUS = '                                       CL**2
00484                  PV-VSAM-STATUS                                      CL**2
00485                  '                  *'                               CL**2
00486          DISPLAY '*                               *'                 CL**2
00487          DISPLAY '* * * * * * * * * * * * * * * * *'                 CL**2
00488          SET ABEND-4003        TO  TRUE                              CL**2
00489          CALL 'ILBOABN0'    USING  ABEND-CODE                        CL**2
00490      END-IF.                                                         CL**2
00491 *                                                                    CL*18
00492  EJECT                                                               CL*18
00493 *                                                                    CL*18
00494 ******************************************************************   CL*18
00495 *    READ THE APLAVL TABLE TO DETERMINE WHETHER THE ROW JUST         CL*18
00496 *    RETURNED ALREADY EXISTS FOR THE NEW APPLICATION SYSTEM.         CL*18
00497 *                                                                    CL*18
00498 *    PERFORMED BY 3300                                               CL*18
00499 ******************************************************************   CL*18
00500 *                                                                    CL*18
00501  3700-CHECK-NEW-KEY.                                                 CL*18
00502 *                                                                    CL*18
00503      PERFORM                                                         CL*18
00504          WITH TEST AFTER                                             CL*18
00505          VARYING PV-SQL-SUB                                          CL*18
00506          FROM 1 BY 1                                                 CL*18
00507          UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                CL*18
00508             OR SQLCODE = 0                                           CL*18
00509             OR SQLCODE = +100                                        CL*18
00510 *                                                                    CL*18
00511          EXEC SQL                                                    CL*18
00512               SELECT                                                 CL*18
00513                       APLSYS_CDE                                     CL*18
00514                    ,  MDSEAR_DKEY                                    CL*18
00515               INTO                                                   CL*18
00516                       :APLAVL-APLSYS-CDE                             CL*18
00517                    ,  :APLAVL-MDSEAR-DKEY                            CL*18
00518               FROM                                                   CL*18
00519                       TAPLAVL                                        CL*18
00520               WHERE                                                  CL*18
00521                       APLSYS_CDE  = :MB018-NEW-APPL-SYS-CODE-005     CL*19
00522                AND    MDSEAR_DKEY = :MDSEAR-DKEY                     CL*19
00523          END-EXEC                                                    CL*18
00524 *                                                                    CL*18
00525          EVALUATE TRUE                                               CL*18
00526              WHEN SQLCODE = -904                                     CL*18
00527              WHEN SQLCODE = -911                                     CL*18
00528              WHEN SQLCODE = ZERO                                     CL*18
00529              WHEN SQLCODE = +100                                     CL*18
00530                   CONTINUE                                           CL*18
00531              WHEN SQLWARN0 NOT EQUAL SPACE                           CL*18
00532              WHEN SQLCODE NOT EQUAL ZERO                             CL*18
00533                   MOVE '3700-CHECK-NEW-KEY'                          CL*18
00534                                TO  PV-PARAGRAPH-NAME                 CL*20
00535                   MOVE 'READ APLAVL WITH NEW KEY'                    CL*18
00536                                TO  PV-DB2-OPER-ATTEMPTED             CL*20
00537                   PERFORM 9900-SQL-ABEND                             CL*20
00538          END-EVALUATE                                                CL*18
00539      END-PERFORM.                                                    CL*18
00540 *                                                                    CL*18
00541      IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                      CL*18
00542          MOVE '3700-CHECK-NEW-KEY'                                   CL*18
00543                                TO  PV-PARAGRAPH-NAME                 CL*20
00544          MOVE 'MAX RETRIES EXCEEDED FOR APLAVL READ'                 CL*18
00545                                TO  PV-DB2-OPER-ATTEMPTED             CL*20
00546          PERFORM 9900-SQL-ABEND                                      CL*20
00547      END-IF.                                                         CL*18
00548 *                                                                    CL*18
00549      IF  SQLCODE = +100                                              CL*18
00550          SET PS-USE-IT         TO  TRUE                              CL*18
00551      ELSE                                                            CL*18
00552          SET PS-ALREADY-EXISTS TO  TRUE                              CL*18
00553      END-IF.                                                         CL*18
00554 *                                                                    CL**3
00555  EJECT                                                               CL**3
00556 ****************************************************************     CL**3
00557 *                                                              *     CL**3
00558 *    DELETE THE CURRENT BATCH CONTROL RECORD.                  *     CL**3
00559 *                                                              *     CL**3
00560 ****************************************************************     CL**3
00561 *                                                                    CL**3
00562  4000-CLOSE-BATCH-CONTROL.                                           CL**3
00563 *                                                                    CL**3
00564      CLOSE BATCH-CONTROL-FILE                                        CL**4
00565            APLAVL-LOAD-FILE.                                         CL**4
00566 *                                                                    CL**3
00567      IF  PV-GOOD-STATUS                                              CL**3
00568          CONTINUE                                                    CL**3
00569      ELSE                                                            CL**3
00570          DISPLAY '* * * * * * * * * * * * * * * * *'                 CL**3
00571          DISPLAY '*    ***MBKUT005 ABEND***       *'                 CL**3
00572          DISPLAY '*                               *'                 CL**3
00573          DISPLAY '*   INVALID STATUS ON THE BATCH *'                 CL**3
00574          DISPLAY '*   CONTROL DATASET - CLOSE     *'                 CL**3
00575          DISPLAY '*                               *'                 CL**3
00576          DISPLAY '* STATUS = '                                       CL**3
00577                  PV-VSAM-STATUS                                      CL**3
00578                  '                  *'                               CL**3
00579          DISPLAY '*                               *'                 CL**3
00580          DISPLAY '* * * * * * * * * * * * * * * * *'                 CL**3
00581          SET ABEND-4006        TO  TRUE                              CL*12
00582          CALL 'ILBOABN0'    USING  ABEND-CODE                        CL**3
00583      END-IF.                                                         CL**3
00584 *                                                                    CL**1
00585  EJECT                                                               CL**1
00586 ****************************************************************     CL**1
00587 *                                                              *     CL**1
00588 *    DISPLAY THE NUMBER OF RECORDS READ/WRITTEN.               *     CL**2
00589 *                                                              *     CL**1
00590 ****************************************************************     CL**1
00591 *                                                                    CL**1
00592  9000-DISPLAY-TOTALS.                                                CL**1
00593 *                                                                    CL**1
00594      DISPLAY ' '.                                                    CL**1
00595      DISPLAY '* * * * * * * * * * * * * * * * * * *'.                CL**7
00596      DISPLAY '*                                   *'.                CL**7
00597      DISPLAY '*       **MBKUT005 COUNTS**         *'.                CL**7
00598      DISPLAY '*  '                                                   CL**7
00599              PV-BATCH-CNTL-COUNT                                     CL**7
00600              ' CONTROL RECORDS READ   *'.                            CL**7
00601      DISPLAY '*  '                                                   CL**7
00602              PV-WRITE-COUNT                                          CL**7
00603              ' APLSYS RECORDS CREATED *'.                            CL**7
00604      DISPLAY '*  '                                                   CL*18
00605              PV-SKIP-COUNT                                           CL*18
00606              ' APLSYS RECORDS SKIPPED *'.                            CL*18
00607      DISPLAY '*                                   *'.                CL**7
00608      DISPLAY '* * * * * * * * * * * * * * * * * * *'.                CL**7
00609 *                                                                    CL**1
00610  EJECT                                                               CL**1
00611 ****************************************************************     CL**1
00612 *                                                              *     CL**1
00613 *    THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.        *     CL**1
00614 *                                                              *     CL**1
00615 ****************************************************************     CL**1
00616 *                                                                    CL**1
00617  9900-SQL-ABEND.                                                     CL**1
00618 *                                                                    CL**1
00619      SET ABEND-4013             TO TRUE.                             CL**1
00620      DISPLAY '**  ABEND     **'.                                     CL**1
00621      DISPLAY '**  PROGRAM   **  =  MBKUT005'.                        CL**2
00622      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.               CL**1
00623      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.           CL**1
00624 *                                                                    CL**1
00625 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE            CL**1
00626 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.           CL**1
00627 *                                                                    CL**1
00628      COPY DPPD004.                                                   CL**1
00629 *                                                                    CL**1
00630      CALL 'ILBOABN0'  USING ABEND-CODE.                              CL**1
00631 *                                                                    CL**1
