      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.             TFKUT491.                                00050000
       AUTHOR.                 JACK KRAMER.                             00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            APRIL 1997.                              00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ******************************************************************00110000
      *   THIS PROGRAM EXTRACTS DATA AND WRITES AN OUTPUT FILE FOR THE  00120000
      *  TRANSFER BY TYPE REPORT. IT PROCESSES 2 SEPERATE CURSORS, EACH 00130000
      *  CURSOR TWICE, SELECTING TOTAL UNITS AND RETAIL FOR TRANSFERS   00130100
      *  COMPLETED DURING A SPECIFIED TIME FRAME. THE CURSOR GROUPS     00130200
      *  DATA BY TRANSFER TYPE, DISTRICT NUMBER, AND STORE NUMBER.      00130300
      *  CURSOR 1 SELECTS DATA FOR THE FISCAL PERIOD TO DATE, FIRST FOR 00130400
      *  THIS YEAR, THEN FOR LAST YEAR.                                 00130500
      *  CURSOR 2 SELECTS DATA FOR THE FISCAL YEAR TO DATE, FIRST FOR   00130600
      *  THIS YEAR, THEN FOR LAST YEAR.                                 00130700
      *  A PROCESS DATES FILE IS ALSO WRITTEN TO PASS THE FISCAL PERIOD 00130800
      *  BEGIN AND END DATES TO THE REPORT PROGRAM TO BE PRINTED IN THE 00130900
      *  HEADINGS.                                                      00131000
      ******************************************************************00131100
100404** 10/04/2004 CHANGED BY: RAY GRAF                                00131200
100404**     EXPAND STORE NUMBER. MAKE DOMAIN COMPLIANT.                00131300
      ***************************************************************** 00131400
                                                                        00131500
      ******************************************************************00131600
       ENVIRONMENT DIVISION.                                            00131700
      ******************************************************************00131800
                                                                        00131900
       CONFIGURATION SECTION.                                           00132000
                                                                        00132100
       SOURCE-COMPUTER.        IBM-3090.                                00132200
       OBJECT-COMPUTER.        IBM-3090.                                00132300
                                                                        00132400
       INPUT-OUTPUT SECTION.                                            00132500
                                                                        00132600
       FILE-CONTROL.                                                    00132700
                                                                        00132800
           SELECT PROCESS-DATES-FILE                                    00132900
               ASSIGN TO OUT01.                                         00133000
                                                                        00134000
           SELECT TRANSFERS-BY-TYPE-FILE                                00135000
               ASSIGN TO OUT02.                                         00136000
      *                                                                 00137000
      ******************************************************************00138000
       DATA DIVISION.                                                   00139000
      ******************************************************************00140000
                                                                        00150000
       FILE SECTION.                                                    00160000
                                                                        00170000
       FD  PROCESS-DATES-FILE                                           00180000
           LABEL RECORDS ARE STANDARD                                   00190000
           RECORDING MODE IS F                                          00200000
           BLOCK CONTAINS 0 RECORDS                                     00210000
           RECORD CONTAINS 16 CHARACTERS.                               00220000
       01  PROCESS-DATES-RECORD            PIC X(16).                   00230000
                                                                        00240000
       FD  TRANSFERS-BY-TYPE-FILE                                       00250000
           LABEL RECORDS ARE STANDARD                                   00260000
           RECORDING MODE IS F                                          00270000
           BLOCK CONTAINS 0 RECORDS                                     00280000
           RECORD CONTAINS 30 CHARACTERS                                00290000
           DATA RECORD IS TRANSFER-REPORT-RECORD.                       00300000
                                                                        00310000
       01  TRANSFERS-BY-TYPE-RECORD PIC X(30).                          00320000
      *                                                                 00330000
       WORKING-STORAGE SECTION.                                         00340000
                                                                        00350000
      *----------------------------------------------------------------*00360000
      *  DATE VALIDATION CALLING PARAMETER LIST                        *00370000
      *----------------------------------------------------------------*00380000
           COPY DPWS500.                                                00390000
      *----------------------------------------------------------------*00400000
      *  RECORD LAYOUT FOR THE PROCESS DATES FILE                       00410000
      *----------------------------------------------------------------*00420000
           COPY TFRD029.                                                00430000
      *----------------------------------------------------------------*00440000
      *  RECORD LAYOUT FOR THE TRANSFERS BY TYPE FILE                   00450000
      *----------------------------------------------------------------*00460000
           COPY TFRD036.                                                00470000
           EJECT                                                        00480000
      *                                                                 00490000
       01  ABEND-CODE                  PIC S9(4)    VALUE ZERO          00500000
                                       COMP SYNC.                       00510000
                                                                        00520000
       01  PS-PROGRAM-SWITCHES.                                         00530000
           05  PS-MORE-STORES-SW       PIC  X(01)   VALUE 'Y'.          00540000
               88  PS-MORE-STORES                   VALUE 'Y'.          00550000
               88  PS-NO-MORE-STORES                VALUE 'N'.          00560000
           05  PS-MORE-DATA-SW         PIC  X(01)   VALUE 'Y'.          00570000
               88  PS-MORE-DATA                     VALUE 'Y'.          00580000
               88  PS-NO-MORE-DATA                  VALUE 'N'.          00590000
           05  PS-TOTALS-TYPE-SW       PIC  X(02)   VALUE '01'.         00600000
               88  PS-PTD-TY                        VALUE '01'.         00610000
               88  PS-PTD-LY                        VALUE '02'.         00620000
               88  PS-YTD-TY                        VALUE '03'.         00630000
               88  PS-YTD-LY                        VALUE '04'.         00640000
                                                                        00650000
       01  PC-PROGRAM-CONSTANTS.                                        00660000
           05  PC-PROGRAM-NAME         PIC X(08)    VALUE 'TFKUT491'.   00670000
           05  PC-COMPLETE               PIC  X(02)  VALUE  'CM'.       00680000
           05  PC-DS                     PIC  X(02)  VALUE  'DS'.       00690000
           05  PC-MONTH-01               PIC  S9(03) VALUE  +1  COMP-3. 00700000
           05  PC-MAX-RETRIES                 PIC S9(3)    VALUE 3      00710000
                                                           COMP-3.      00720000
           05  PC-1-PERIOD                     PIC  S9(03) VALUE  +1    00730000
                                                           COMP-3.      00740000
                                                                        00750000
       01  PV-PROGRAM-VARIABLES.                                        00760000
           05  PV-RETRY-COUNT          PIC S9(4)    VALUE ZERO          00770000
                                       COMP SYNC.                       00780000
           05  PV-NULL-QTY             PIC S9(04)  COMP.                00790000
           05  PV-NULL-RTL             PIC S9(04)  COMP.                00800000
           05  PV-PARAGRAPH-NAME       PIC X(30)   VALUE SPACE.         00810000
           05  PV-DB2-OPERATION        PIC X(30)   VALUE SPACE.         00820000
           05  PV-THIS-YEAR            PIC 9(04)     VALUE ZERO.        00830000
           05  PV-LAST-YEAR            PIC 9(04)     VALUE ZERO.        00840000
           05  PV-PERIOD               PIC S9(02)    VALUE ZERO COMP-3. 00850000
           05  PV-FISCAL-YEAR          PIC S9(04)    VALUE ZERO COMP-3. 00860000
           05  PV-FISCAL-MONTH         PIC S9(02)    VALUE ZERO COMP-3. 00870000
           05  PV-TOT-QTY              PIC S9(08)    VALUE ZERO COMP-3. 00880000
           05  PV-TOT-RTL              PIC S9(09)V99 VALUE ZERO COMP-3. 00890000
           05  PV-GREGORIAN-DATE.                                       00900000
               10  PV-GD-MONTH                 PIC  9(02)  VALUE ZEROS. 00910000
               10  PV-GD-DAY                   PIC  9(02)  VALUE ZEROS. 00920000
               10  PV-GD-YEAR                  PIC  9(02)  VALUE ZEROS. 00930000
                                                                        00940000
       01  CD-CURRENT-DATE.                                             00950000
           05  CD-CURRENT-YEAR         PIC 9(2)     VALUE ZERO.         00960000
           05  CD-CURRENT-MONTH        PIC 9(2)     VALUE ZERO.         00970000
           05  CD-CURRENT-DAY          PIC 9(2)     VALUE ZERO.         00980000
      *---------------------------------------------------------------* 00990000
      *    DB2 AREA FOR TRANSFER MASTER TABLE                         * 01000000
      *---------------------------------------------------------------* 01010000
           EXEC SQL                                                     01020000
                INCLUDE TTFMSTR                                         01030000
           END-EXEC.                                                    01040000
      *---------------------------------------------------------------* 01050000
      *    DB2 AREA FOR TRANSFER STAT TABLE                           * 01060000
      *---------------------------------------------------------------* 01070000
           EXEC SQL                                                     01080000
                INCLUDE TTFSTAT                                         01090000
           END-EXEC.                                                    01100000
      *---------------------------------------------------------------* 01110000
      *    DB2 AREA FOR TRANSFER ITEM POST TABLE                      * 01120000
      *---------------------------------------------------------------* 01130000
           EXEC SQL                                                     01140000
                INCLUDE TTFITPS                                         01150000
           END-EXEC.                                                    01160000
      *---------------------------------------------------------------* 01170000
      *    DB2 AREA FOR TRANSFER BATCH TABLE                          * 01180000
      *---------------------------------------------------------------* 01190000
           EXEC SQL                                                     01200000
                INCLUDE TTFBTCH                                         01210000
           END-EXEC.                                                    01220000
      *---------------------------------------------------------------* 01230000
      *    DB2 AREA FOR TRANSFER TYPE CODE TABLE                      * 01240000
      *---------------------------------------------------------------* 01250000
           EXEC SQL                                                     01260000
                INCLUDE TTFTYCD                                         01270000
           END-EXEC.                                                    01280000
      *---------------------------------------------------------------* 01290000
100404*    DB2 AREA FOR XST_LOC TABLE                                 * 01300000
      *---------------------------------------------------------------* 01310000
           EXEC SQL                                                     01320000
100404          INCLUDE XST00001                                        01330000
           END-EXEC.                                                    01340000
      *---------------------------------------------------------------* 01350000
100404*    DB2 AREA FOR XST_LOC_DIST TABLE                            * 01360000
      *---------------------------------------------------------------* 01370000
           EXEC SQL                                                     01380000
100404          INCLUDE XST00002                                        01390000
           END-EXEC.                                                    01400000
                                                                        01410000
           EXEC SQL                                                     01420000
               INCLUDE SQLCA                                            01430000
           END-EXEC.                                                    01440000
      *                                                                 01450000
      *  EXTENSION TO THE SQL COMMUNICATIONS AREA FOR EDITING           01460000
      *  SQL CODES FOR DISPLAY                                          01470000
      *                                                                 01480000
           COPY SQLCA2.                                                 01490000
      *                                                                 01500000
      *---------------------------------------------------------------* 01510000
      *    CURSOR TO RETRIEVE DATA FOR THE FISCAL PERIOD              * 01520000
      *---------------------------------------------------------------* 01530000
           EXEC SQL                                                     01540000
             DECLARE PTD-CSR CURSOR FOR                                 01550000
                   SELECT                                               01560000
                         T.XFER_TYP_CDE                                 01570000
100404                 , D.DIST_NBR                                     01580000
100404                 , D.LOC_NBR                                      01590000
                       , SUM(I.ITM_QTY)                                 01600000
                       , SUM(I.ITM_QTY * I.ITM_RTL_PRCE_AMT)            01610000
                   FROM                                                 01620000
                         TTFMSTR      M                                 01630000
                        ,TTFBTCH      B                                 01640000
                        ,TTFITPS      I                                 01650000
                        ,TTFTYCD      T                                 01660000
100404                  ,XST_LOC      L                                 01670000
100404                  ,XST_LOC_DIST D                                 01680000
                   WHERE                                                01690000
                      M.XFER_DKEY_ID       = I.XFER_DKEY_ID        AND  01700000
                      I.BTCH_DKEY_ID       = B.BTCH_DKEY_ID        AND  01710000
                      M.XFER_TYP_CDE       = T.XFER_TYP_CDE        AND  01720000
100404                I.FRM_PSTG_STR_NBR   = L.LOC_NBR             AND  01730000
100404                L.LOC_NBR            = D.LOC_NBR             AND  01740000
100404                D.END_DTE           IS NULL                  AND  01750000
100404                L.LOC_TYP_CDE        = :PC-DS                AND  01760000
                      I.ADJ_RSN_CDE        = :PC-COMPLETE          AND  01770000
                      T.BYP_ASMTV_RCPT_IND = 'N'                   AND  01780000
                      KOHLS_FSCL_YR        = :PV-FISCAL-YEAR       AND  01790000
                      KOHLS_FSCL_MN        = :PV-FISCAL-MONTH      AND  01800000
                      NOT EXISTS                                        01810000
                        (SELECT XFER_DKEY_ID                            01820000
                         FROM TTFSTAT                                   01830000
                         WHERE XFER_DKEY_ID = M.XFER_DKEY_ID       AND  01840000
                               XFER_STAT_CDE = 'VD')                    01850000
              GROUP BY                                                  01860000
                         T.XFER_TYP_CDE                                 01870000
100404                 , D.DIST_NBR                                     01880000
100404                 , D.LOC_NBR                                      01890000
              WITH UR                                                   01900000
           END-EXEC.                                                    01910000
      *---------------------------------------------------------------* 01920000
      *    CURSOR TO RETRIEVE DATA FOR YEAR TO DATE                   * 01930000
      *---------------------------------------------------------------* 01940000
           EXEC SQL                                                     01950000
             DECLARE YTD-CSR CURSOR FOR                                 01960000
                   SELECT                                               01970000
                         T.XFER_TYP_CDE                                 01980000
100404                 , D.DIST_NBR                                     01990000
100404                 , D.LOC_NBR                                      02000000
                       , SUM(I.ITM_QTY)                                 02010000
                       , SUM(I.ITM_QTY * I.ITM_RTL_PRCE_AMT)            02020000
                   FROM                                                 02030000
                         TTFMSTR      M                                 02040000
                        ,TTFBTCH      B                                 02050000
                        ,TTFITPS      I                                 02060000
                        ,TTFTYCD      T                                 02070000
100404                  ,XST_LOC      L                                 02080000
100404                  ,XST_LOC_DIST D                                 02090000
                   WHERE                                                02100000
                      M.XFER_DKEY_ID       = I.XFER_DKEY_ID        AND  02110000
                      I.BTCH_DKEY_ID       = B.BTCH_DKEY_ID        AND  02120000
                      M.XFER_TYP_CDE       = T.XFER_TYP_CDE        AND  02130000
100404                I.FRM_PSTG_STR_NBR   = L.LOC_NBR             AND  02140000
100404                L.LOC_NBR            = D.LOC_NBR             AND  02150000
100404                D.END_DTE           IS NULL                  AND  02160000
100404                L.LOC_TYP_CDE        = :PC-DS                AND  02170000
                      I.ADJ_RSN_CDE        = :PC-COMPLETE          AND  02180000
                      T.BYP_ASMTV_RCPT_IND = 'N'                   AND  02190000
                      KOHLS_FSCL_YR        = :PV-FISCAL-YEAR       AND  02200000
                      KOHLS_FSCL_MN       BETWEEN :PC-MONTH-01     AND  02210000
                                                  :PV-FISCAL-MONTH AND  02220000
                      NOT EXISTS                                        02230000
                        (SELECT XFER_DKEY_ID                            02240000
                         FROM TTFSTAT                                   02250000
                         WHERE XFER_DKEY_ID = M.XFER_DKEY_ID       AND  02260000
                               XFER_STAT_CDE = 'VD')                    02270000
              GROUP BY                                                  02280000
                         T.XFER_TYP_CDE                                 02290000
100404                 , D.DIST_NBR                                     02300000
100404                 , D.LOC_NBR                                      02310000
              WITH UR                                                   02320000
           END-EXEC.                                                    02330000
                                                                        02340000
      ******************************************************************02350000
       PROCEDURE DIVISION.                                              02360000
      ******************************************************************02370000
       0000-MAINLINE.                                                   02380000
           PERFORM 1000-INITIALIZE.                                     02390000
                                                                        02400000
           DISPLAY 'FISCAL MONTH '            PV-PERIOD                 02410000
           DISPLAY 'FISCAL YEAR  '            PV-THIS-YEAR              02420000
           PERFORM 2000-PROCESS-DATA.                                   02430000
                                                                        02440000
           CLOSE TRANSFERS-BY-TYPE-FILE                                 02450000
                 PROCESS-DATES-FILE.                                    02460000
           STOP RUN.                                                    02470000
                                                                        02480000
      ******************************************************************02490000
      *  SET UP FOR PROGRAM PROCESSING BY OPENING ALL FILES, GET        02500000
      *  CURRENT DATE AND USE IT TO FIND THE FISCAL MONTH AND YEAR.     02510000
      *  CALCULATE LAST FISCAL YEAR.                                    02520000
      ******************************************************************02530000
                                                                        02540000
       1000-INITIALIZE.                                                 02550000
           OPEN OUTPUT TRANSFERS-BY-TYPE-FILE                           02560000
                       PROCESS-DATES-FILE.                              02570000
           ACCEPT CD-CURRENT-DATE FROM DATE.                            02580000
           PERFORM 1050-GET-FISCAL-MONTH-DATES.                         02590000
      *                                                                 02600000
           PERFORM 8000-WRITE-PROCESS-DATES-FILE.                       02610000
      *                                                                 02620000
      ******************************************************************02630000
      * USE THE CURRENT DATE TO DETERMINE THE BEGINNING AND ENDING      02640000
      * DATES FOR THE PREVIOUS FISCAL PERIOD.                           02650000
      ******************************************************************02660000
       1050-GET-FISCAL-MONTH-DATES.                                     02670000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              02680000
      *  CALL THE CALENDAR ROUTINE WITH CURRENT DATE TO FIND THE        02690000
      *  CURRENT FISCAL PERIOD                                          02700000
                                                                        02710000
           SET  DPG51-FISCAL-454-CALENDAR                               02720000
                DPG51-DO-NOT-INCR-DECR-DATE                             02730000
                DPG52-LK-DTE-GREG          TO  TRUE.                    02740000
           MOVE CD-CURRENT-YEAR            TO  PV-GD-YEAR.              02750000
           MOVE CD-CURRENT-MONTH           TO  PV-GD-MONTH.             02760000
           MOVE CD-CURRENT-DAY             TO  PV-GD-DAY.               02770000
           MOVE PV-GREGORIAN-DATE          TO  DPG52-GREG-DATE-N.       02780000
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52          02790000
                                                   DPG53 DPG54          02800000
                                                   DPG55 DPG56.         02810000
           EVALUATE TRUE                                                02820000
               WHEN DPG54-DATE-VALID                                    02830000
                   INITIALIZE DPG51 DPG52 DPG53                         02840000
                   MOVE DPG56-FISCAL-PERIOD-NBR                         02850000
                                          TO DPG52-FISCAL-CC-PRD-PP     02860000
                   MOVE DPG56-FISCAL-YEAR-CCYY                          02870000
                                          TO DPG52-FISCAL-CC-PRD-YR     02880000
                   INITIALIZE DPG54 DPG55 DPG56                         02890000
               WHEN OTHER                                               02900000
                   DISPLAY '** ABEND **'                                02910000
                   DISPLAY '** PGM = ' PC-PROGRAM-NAME                  02920000
                   DISPLAY '** 1050-GET-FISCAL-MONTH-DATES - 1 **'      02930000
                   DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE. '  02940000
                   DISPLAY '** ERROR MESSAGE = '                        02950000
                           DPG54-ERROR-MESSAGE                          02960000
                   DISPLAY '** DPG51 = ' DPG51                          02970000
                   DISPLAY '** DPG52 = ' DPG52                          02980000
                   DISPLAY '** DPG53 = ' DPG53                          02990000
                   DISPLAY '** DPG54 = ' DPG54                          03000000
                   MOVE +4019 TO ABEND-CODE                             03010000
                   CALL 'ILBOABN0' USING ABEND-CODE                     03020000
           END-EVALUATE.                                                03030000
      *  DECREMENT 1 FISCAL PERIOD TO FIND THE PREVIOUS FISCAL PERIOD.  03040000
      *  SAVE THE FIRST AND LAST DAYS OF THAT FISCAL PERIOD FOR THE     03050000
      *  BEGIN AND END DATES FOR RETRIEVING  RECEIVED TRANSFERS.        03060000
           SET DPG51-FISCAL-454-CALENDAR                                03070000
               DPG51-DECREMENT-DATE                                     03080000
               DPG52-LK-DTE-FISC-PRD-CC TO TRUE.                        03090000
           MOVE PC-1-PERIOD         TO DPG51-INCR-DECR-FISCAL-PRD-9.    03100000
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52          03110000
                                                   DPG53 DPG54          03120000
                                                   DPG55 DPG56.         03130000
           EVALUATE TRUE                                                03140000
               WHEN DPG54-DATE-VALID                                    03150000
                   MOVE DPG56-FIRST-FP-GREG-CC                          03160000
                                            TO  TF029-PROCESS-START-DATE03170000
                   MOVE  DPG56-LAST-FP-GREG-CC                          03180000
                                            TO  TF029-PROCESS-END-DATE  03190000
                   MOVE DPG56-FISCAL-PERIOD-NBR   TO PV-PERIOD          03200000
                   MOVE DPG56-FISCAL-YEAR-CCYY    TO PV-THIS-YEAR       03210000
                   SUBTRACT +1 FROM PV-THIS-YEAR                        03220000
                                           GIVING PV-LAST-YEAR          03230000
               WHEN OTHER                                               03240000
                   DISPLAY '** ABEND **'                                03250000
                   DISPLAY '** PGM = ' PC-PROGRAM-NAME                  03260000
                   DISPLAY '** 1050-GET-FISCAL-MONTH-DATES - 2 **'      03270000
                   DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE. '  03280000
                   DISPLAY '** ERROR MESSAGE = '                        03290000
                           DPG54-ERROR-MESSAGE                          03300000
                   DISPLAY '** DPG51 = ' DPG51                          03310000
                   DISPLAY '** DPG52 = ' DPG52                          03320000
                   DISPLAY '** DPG53 = ' DPG53                          03330000
                   DISPLAY '** DPG54 = ' DPG54                          03340000
                   MOVE +4019 TO ABEND-CODE                             03350000
                   CALL 'ILBOABN0' USING ABEND-CODE                     03360000
           END-EVALUATE.                                                03370000
      *----------------------------------------------------------------*03380000
      *   CONTROL PROCESSING OF CURSORS.                                03390000
      *----------------------------------------------------------------*03400000
       2000-PROCESS-DATA.                                               03410000
                                                                        03420000
      * READ THIS YEAR PERIOD-TO-DATE TOTALS                            03430000
           MOVE PV-THIS-YEAR                 TO PV-FISCAL-YEAR.         03440000
           MOVE PV-PERIOD                    TO PV-FISCAL-MONTH.        03450000
           SET PS-MORE-DATA                                             03460000
               PS-PTD-TY                     TO TRUE.                   03470000
           PERFORM 3000-OPEN-PTD-CURSOR.                                03480000
           PERFORM 3100-READ-PTD.                                       03490000
           PERFORM 3200-CLOSE-PTD-CURSOR.                               03500000
                                                                        03510000
      * READ LAST YEAR PERIOD-TO-DATE TOTALS                            03520000
           MOVE PV-LAST-YEAR                 TO PV-FISCAL-YEAR.         03530000
           SET PS-MORE-DATA                                             03540000
               PS-PTD-LY                     TO TRUE.                   03550000
           PERFORM 3000-OPEN-PTD-CURSOR.                                03560000
           PERFORM 3100-READ-PTD.                                       03570000
           PERFORM 3200-CLOSE-PTD-CURSOR.                               03580000
                                                                        03590000
      * READ THIS YEAR YEAR-TO-DATE TOTALS                              03600000
           MOVE PV-THIS-YEAR                 TO PV-FISCAL-YEAR.         03610000
           SET PS-MORE-DATA                                             03620000
               PS-YTD-TY                     TO TRUE.                   03630000
           PERFORM 3500-OPEN-YTD-CURSOR.                                03640000
           PERFORM 3600-READ-YTD.                                       03650000
           PERFORM 3700-CLOSE-YTD-CURSOR.                               03660000
                                                                        03670000
      * READ LAST YEAR YEAR-TO-DATE TOTALS                              03680000
           MOVE PV-LAST-YEAR                 TO PV-FISCAL-YEAR.         03690000
           SET PS-MORE-DATA                                             03700000
               PS-YTD-LY                     TO TRUE.                   03710000
           PERFORM 3500-OPEN-YTD-CURSOR.                                03720000
           PERFORM 3600-READ-YTD.                                       03730000
           PERFORM 3700-CLOSE-YTD-CURSOR.                               03740000
                                                                        03750000
       EJECT                                                            03760000
      *************************************************************     03770000
      * OPEN THE PTD CURSOR                                             03780000
      *************************************************************     03790000
       3000-OPEN-PTD-CURSOR.                                            03800000
           EXEC SQL                                                     03810000
                OPEN PTD-CSR                                            03820000
           END-EXEC.                                                    03830000
                                                                        03840000
           EVALUATE TRUE                                                03850000
               WHEN SQLCODE = ZERO                                      03860000
                    CONTINUE                                            03870000
               WHEN OTHER                                               03880000
                   MOVE '3000-OPEN-PTD-CURSSOR' TO PV-PARAGRAPH-NAME    03890000
                   MOVE 'OPEN PTD CURSOR'     TO PV-DB2-OPERATION       03900000
                   PERFORM 9100-SQL-ERROR                               03910000
           END-EVALUATE.                                                03920000
      ******************************************************************03930000
      * CONTROL THE PROCESSING OF THE PTD CURSOR                        03940000
      ******************************************************************03950000
       3100-READ-PTD.                                                   03960000
           PERFORM                                                      03970000
               UNTIL (PS-NO-MORE-DATA)                                  03980000
                                                                        03990000
               PERFORM 3150-FETCH-PTD                                   04000000
                                                                        04010000
               IF  PS-MORE-DATA                                         04020000
                   IF PV-TOT-QTY > ZEROES OR PV-TOT-RTL > ZEROES        04030000
                       PERFORM 4000-PROCESS-DATA                        04040000
                   END-IF                                               04050000
               END-IF                                                   04060000
           END-PERFORM.                                                 04070000
       EJECT                                                            04080000
      *----------------------------------------------------------------*04090000
      *    FETCH THE NEXT ROW FROM THE PTD CURSOR                       04100000
      *----------------------------------------------------------------*04110000
       3150-FETCH-PTD.                                                  04120000
           PERFORM                                                      04130000
               WITH TEST AFTER                                          04140000
               VARYING PV-RETRY-COUNT                                   04150000
               FROM 1 BY 1                                              04160000
               UNTIL PV-RETRY-COUNT GREATER THAN PC-MAX-RETRIES         04170000
                  OR SQLCODE = 0                                        04180000
                  OR SQLCODE = +100                                     04190000
               EXEC SQL                                                 04200000
                    FETCH PTD-CSR                                       04210000
                    INTO  :TFTYCD-XFER-TYP-CDE                          04220000
100404                   ,:XST00002-DIST-NBR                            04230000
100404                   ,:XST00002-LOC-NBR                             04240000
                         ,:PV-TOT-QTY                                   04250000
                          :PV-NULL-QTY                                  04260000
                         ,:PV-TOT-RTL                                   04270000
                          :PV-NULL-QTY                                  04280000
               END-EXEC                                                 04290000
                                                                        04300000
               EVALUATE TRUE                                            04310000
                   WHEN SQLCODE = ZERO                                  04320000
                   WHEN SQLCODE = -904                                  04330000
                   WHEN SQLCODE = -913                                  04340000
                        CONTINUE                                        04350000
                   WHEN SQLCODE = +100                                  04360000
                        SET PS-NO-MORE-DATA       TO TRUE               04370000
                   WHEN OTHER                                           04380000
                        MOVE '3150-FETCH-PTD'                           04390000
                                     TO  PV-PARAGRAPH-NAME              04400000
                        MOVE 'FETCH FROM PTD CURSOR'                    04410000
                                            TO PV-DB2-OPERATION         04420000
                       PERFORM 9100-SQL-ERROR                           04430000
               END-EVALUATE                                             04440000
           END-PERFORM.                                                 04450000
           IF  PV-RETRY-COUNT GREATER THAN PC-MAX-RETRIES               04460000
               MOVE '3150-FETCH-PTD'        TO  PV-PARAGRAPH-NAME       04470000
               MOVE 'MAX RETRIES FETCHING FROM PTD CURSOR'              04480000
                                            TO PV-DB2-OPERATION         04490000
               PERFORM 9100-SQL-ERROR                                   04500000
           END-IF.                                                      04510000
           IF PV-NULL-QTY < +0                                          04520000
               MOVE ZEROES                  TO PV-TOT-QTY               04530000
           END-IF.                                                      04540000
           IF PV-NULL-RTL < +0                                          04550000
               MOVE ZEROES                  TO PV-TOT-RTL               04560000
           END-IF.                                                      04570000
      *************************************************************     04580000
      * CLOSE PTD CURSOR                                                04590000
      *************************************************************     04600000
       3200-CLOSE-PTD-CURSOR.                                           04610000
           EXEC SQL                                                     04620000
                CLOSE PTD-CSR                                           04630000
           END-EXEC.                                                    04640000
      *************************************************************     04650000
      * OPEN THE YTD CURSOR                                             04660000
      *************************************************************     04670000
       3500-OPEN-YTD-CURSOR.                                            04680000
           EXEC SQL                                                     04690000
                OPEN YTD-CSR                                            04700000
           END-EXEC.                                                    04710000
                                                                        04720000
           EVALUATE TRUE                                                04730000
               WHEN SQLCODE = ZERO                                      04740000
                    CONTINUE                                            04750000
               WHEN OTHER                                               04760000
                   MOVE '3500-OPEN-YTD-CURSSOR' TO PV-PARAGRAPH-NAME    04770000
                   MOVE 'OPEN YTD CURSOR'     TO PV-DB2-OPERATION       04780000
                   PERFORM 9100-SQL-ERROR                               04790000
           END-EVALUATE.                                                04800000
      ******************************************************************04810000
      * CONTROL THE PROCESSING OF THE YTD CURSOR                        04820000
      ******************************************************************04830000
       3600-READ-YTD.                                                   04840000
           PERFORM                                                      04850000
               UNTIL (PS-NO-MORE-DATA)                                  04860000
                                                                        04870000
               PERFORM 3650-FETCH-YTD                                   04880000
                                                                        04890000
               IF  PS-MORE-DATA                                         04900000
                   IF PV-TOT-QTY > ZEROES OR PV-TOT-RTL > ZEROES        04910000
                       PERFORM 4000-PROCESS-DATA                        04920000
                   END-IF                                               04930000
               END-IF                                                   04940000
           END-PERFORM.                                                 04950000
       EJECT                                                            04960000
      *----------------------------------------------------------------*04970000
      *    FETCH THE NEXT ROW FROM THE YTD CURSOR                       04980000
      *----------------------------------------------------------------*04990000
       3650-FETCH-YTD.                                                  05000000
           PERFORM                                                      05010000
               WITH TEST AFTER                                          05020000
               VARYING PV-RETRY-COUNT                                   05030000
               FROM 1 BY 1                                              05040000
               UNTIL PV-RETRY-COUNT GREATER THAN PC-MAX-RETRIES         05050000
                  OR SQLCODE = 0                                        05060000
                  OR SQLCODE = +100                                     05070000
               EXEC SQL                                                 05080000
                    FETCH YTD-CSR                                       05090000
                    INTO  :TFTYCD-XFER-TYP-CDE                          05100000
100404                   ,:XST00002-DIST-NBR                            05110000
100404                   ,:XST00002-LOC-NBR                             05120000
                         ,:PV-TOT-QTY                                   05130000
                          :PV-NULL-QTY                                  05140000
                         ,:PV-TOT-RTL                                   05150000
                          :PV-NULL-QTY                                  05160000
               END-EXEC                                                 05170000
                                                                        05180000
               EVALUATE TRUE                                            05190000
                   WHEN SQLCODE = ZERO                                  05200000
                   WHEN SQLCODE = -904                                  05210000
                   WHEN SQLCODE = -913                                  05220000
                        CONTINUE                                        05230000
                   WHEN SQLCODE = +100                                  05240000
                        SET PS-NO-MORE-DATA       TO TRUE               05250000
                   WHEN OTHER                                           05260000
                        MOVE '3650-FETCH-YTD'                           05270000
                                     TO  PV-PARAGRAPH-NAME              05280000
                        MOVE 'FETCH FROM YTD CURSOR'                    05290000
                                            TO PV-DB2-OPERATION         05300000
                       PERFORM 9100-SQL-ERROR                           05310000
               END-EVALUATE                                             05320000
           END-PERFORM.                                                 05330000
           IF  PV-RETRY-COUNT GREATER THAN PC-MAX-RETRIES               05340000
               MOVE '3160-FETCH-YTD'        TO  PV-PARAGRAPH-NAME       05350000
               MOVE 'MAX RETRIES FETCHING FROM YTD CURSOR'              05360000
                                            TO PV-DB2-OPERATION         05370000
               PERFORM 9100-SQL-ERROR                                   05380000
           END-IF.                                                      05390000
           IF PV-NULL-QTY < +0                                          05400000
               MOVE ZEROES                  TO PV-TOT-QTY               05410000
           END-IF.                                                      05420000
           IF PV-NULL-RTL < +0                                          05430000
               MOVE ZEROES                  TO PV-TOT-RTL               05440000
           END-IF.                                                      05450000
      *************************************************************     05460000
      * CLOSE YTD CURSOR                                                05470000
      *************************************************************     05480000
       3700-CLOSE-YTD-CURSOR.                                           05490000
           EXEC SQL                                                     05500000
                CLOSE YTD-CSR                                           05510000
           END-EXEC.                                                    05520000
      ******************************************************************05530000
      *  MOVE THE DATA TO THE OUTPUT FILE AND WRITE IT                 *05540000
      ******************************************************************05550000
       4000-PROCESS-DATA.                                               05560000
           INITIALIZE TF036-TRANSFERS-BY-TYPE-RECORD.                   05570000
           MOVE TFTYCD-XFER-TYP-CDE         TO TF036-XFER-TYP-CDE.      05580000
100404     MOVE XST00002-DIST-NBR           TO TF036-DISTRICT-NMBR.     05590000
100404     MOVE XST00002-LOC-NBR            TO TF036-STORE-NUMBER.      05600000
           MOVE PV-TOT-QTY                  TO TF036-TOTAL-UNITS.       05610000
           MOVE PV-TOT-RTL                  TO TF036-TOTAL-RETAIL.      05620000
           MOVE PS-TOTALS-TYPE-SW           TO TF036-TOTALS-TYPE.       05630000
           WRITE TRANSFERS-BY-TYPE-RECORD                               05640000
               FROM TF036-TRANSFERS-BY-TYPE-RECORD                      05650000
           END-WRITE.                                                   05660000
      ******************************************************************05670000
      * WRITES AN OUTPUT FILE CONTAINING THE SPECIFIED BEGINNING AND    05680000
      * ENDING DATES.                                                   05690000
      ******************************************************************05700000
       8000-WRITE-PROCESS-DATES-FILE.                                   05710000
           WRITE PROCESS-DATES-RECORD                                   05720000
               FROM TF029-PROCESS-DATES-RECORD                          05730000
           END-WRITE.                                                   05740000
      *                                                                 05750000
      ******************************************************************05760000
      *  FORMAT A DB2 ERROR MESSAGE, RELEASE ALL LOCKS, AND ABEND THE  *05770000
      *  PROGRAM.                                                      *05780000
      ******************************************************************05790000
       9100-SQL-ERROR.                                                  05800000
           MOVE SQLCODE TO SQLCODE2.                                    05810000
           MOVE SQLERRD(3) TO SQLERRD3.                                 05820000
           DISPLAY '** ABEND **'.                                       05830000
           DISPLAY 'TFKUT491 - SQL ERROR'.                              05840000
           DISPLAY '       SQLCODE = ' SQLCODE2.                        05850000
           DISPLAY '      SQLWARN0 = ' SQLWARN0.                        05860000
           DISPLAY '      SQLWARN1 = ' SQLWARN1.                        05870000
           DISPLAY '      SQLWARN2 = ' SQLWARN2.                        05880000
           DISPLAY '      SQLWARN3 = ' SQLWARN3.                        05890000
           DISPLAY '      SQLWARN4 = ' SQLWARN4.                        05900000
           DISPLAY '      SQLWARN5 = ' SQLWARN5.                        05910000
           DISPLAY '      SQLWARN6 = ' SQLWARN6.                        05920000
           DISPLAY '      SQLWARN7 = ' SQLWARN7.                        05930000
           DISPLAY 'ROWS PROCESSED = ' SQLERRD3.                        05940000
           DISPLAY                    '** PARAGRAPH ** = '              05950000
                                       PV-PARAGRAPH-NAME.               05960000
           DISPLAY                    '** OPERATION ** = '              05970000
                                       PV-DB2-OPERATION.                05980000
                                                                        05990000
           EXEC SQL                                                     06000000
               COMMIT                                                   06010000
           END-EXEC.                                                    06020000
                                                                        06030000
           MOVE +4013 TO ABEND-CODE.                                    06040000
           CALL 'ILBOABN0' USING ABEND-CODE.                            06050000
                                                                        06060000
