       IDENTIFICATION DIVISION.                                                 
      *=======================*                                                 
       PROGRAM-ID.              TFKUT509.                                       
       AUTHOR.                  COGNIZANT.                                      
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            12/14/2011.                                     
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * Remarks                                                        *        
      * -------                                                        *        
      *                                                                *        
      * Title        - Unload Transfer Data For Inventory Snapshot     *        
      *                Extract                                         *        
      *                                                                *        
      * Description  - This program produces an extract file contains  *        
      * transfer data for inventory snapshot.                          *        
      *                                                                *        
      * Note - Initially the file extracted using utility DSNTIAUL     *        
      * which runs dynamic SQL and running long due to poor access     *        
      * Path. DBA recommended to use an application program with a     *        
      * static access path                                             *        
      *                                                                *        
      *  Change history                                                *        
      ******************************************************************        
      * WR Number   Programmer       Date                              *        
      * ---------------------------------------------------------------*        
      * PKE10228    COGNIZANT        15-Feb-2012                       *        
      * Original Version                                               *        
      * SR3403      ANUJ GUPTA       12-MAR-2012  LARGE RETAIL PROJECT *        
      * WR41331     COGNIZANT        12-SEP-2012                       *        
      * Transfer 'Assumptive' Window Change from 90 to 60 days         *        
      * ****************************************************************        
C54275* CHG0154275  COGNIZANT        19-OCT-2016                       *        
C54275* Transfer 'Assumptive' Window Change from 60 to 90 days         *        
C54275******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
      *=====================*                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
      *=====================*                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *=====================*                                                   
                                                                                
       FILE-CONTROL.                                                            
      *=============*                                                           
                                                                                
      *****************************************************************         
      * Selects files used in the program                             *         
      *****************************************************************         
                                                                                
           SELECT UNLOAD-OUTFILE ASSIGN     TO  OUT01.                          
                                                                                
       DATA DIVISION.                                                           
      *=============*                                                           
                                                                                
       FILE SECTION.                                                            
      *=============*                                                           
                                                                                
       FD  UNLOAD-OUTFILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
SR3403     RECORD CONTAINS 79 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS UNLOAD-OUTREC.                                        
                                                                                
SR3403 01  UNLOAD-OUTREC                    PIC  X(79).                         
                                                                                
       EJECT                                                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *========================*                                                
                                                                                
      *-- Copybook for SQL communication area                                   
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      *-- Copybook for Transfer Status Code Table                               
           EXEC SQL                                                             
                INCLUDE TTFSTAT                                                 
           END-EXEC.                                                            
                                                                                
      *-- Copybook for Transfer table                                           
           EXEC SQL                                                             
                INCLUDE TTFMSTR                                                 
           END-EXEC.                                                            
                                                                                
      *-- Copybook for Transfer Item table                                      
           EXEC SQL                                                             
                INCLUDE TTFITEM                                                 
           END-EXEC.                                                            
                                                                                
      *-- Copybook for Transfer Item Posting Table                              
           EXEC SQL                                                             
                INCLUDE TTFITPS                                                 
           END-EXEC.                                                            
                                                                                
      *-- Copybook for Transfer Batch Table                                     
           EXEC SQL                                                             
                INCLUDE TTFBTCH                                                 
           END-EXEC.                                                            
                                                                                
      *-- Copybook for SKU Cross Reference Table                                
           EXEC SQL                                                             
                INCLUDE TSKXREF                                                 
           END-EXEC.                                                            
                                                                                
      *-- Copybook for XS Domain SKU Information Table (XST_SKU)                
           EXEC SQL                                                             
                INCLUDE XST00010                                                
           END-EXEC.                                                            
                                                                                
      *-- Copybook for XS Domain Vendor Merchandise Styles(XST_VND_STYL)        
           EXEC SQL                                                             
                INCLUDE XST00013                                                
           END-EXEC.                                                            
                                                                                
      *-- Copybook for XS Domain Store Location table (XST_LOC)                 
           EXEC SQL                                                             
                INCLUDE XST00001                                                
           END-EXEC.                                                            
                                                                                
      *-- Copybook for Inventory Parameter Table                                
           EXEC SQL                                                             
                INCLUDE TINVPAR                                                 
           END-EXEC.                                                            
                                                                                
      *-- Copybook for Transfer Control Table                                   
           EXEC SQL                                                             
                INCLUDE TINCNTL                                                 
           END-EXEC.                                                            
                                                                                
      *-- Copybook of record Layout for unload key                              
           COPY TFRD060.                                                        
                                                                                
      *-- Copybook of DB2 Error Messages Format Area                            
           COPY DPWS004.                                                        
                                                                                
      *-- Copybook of DB2 ERRORS                                                
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      *  Switches                                                      *        
      *****************************************************************         
       01  PS-PROGRAM-SWITCHES.                                                 
           03  PS-END-OF-CRS-SW          PIC X(01).                             
               88  END-OF-CRS                        VALUE 'Y'.                 
               88  NOT-END-OF-CRS                    VALUE 'N'.                 
                                                                                
      *****************************************************************         
      *  General workarea                                              *        
      *****************************************************************         
       01  PV-PROGRAM-VARIABLES.                                                
           03  PV-PARAGRAPH-NAME         PIC  X(30).                            
           03  PV-DB2-OPER-ATTEMPTED     PIC  X(30).                            
           03  PV-SQLCODE-DISPLAY        PIC  +(8)9  VALUE ZEROES.              
           03  PV-WRITE-REC-CNT          PIC  S9(6)V COMP-3 VALUE 0.            
           03  PV-NULL-IND               PIC  S9(04) COMP VALUE +0.             
           03  PV-CREATE-DT-FMT.                                                
               05  PV-NULL-FIELD         PIC  X(01) VALUE SPACE.                
               05  PV-CREATE-DT          PIC  X(10) VALUE SPACE.                
                                                                                
      *****************************************************************         
      *  General workarea sql abend                                   *         
      *****************************************************************         
       01  ABEND-CODE                    PIC S9(04) VALUE +0 COMP SYNC.         
           88  ABEND-4013                           VALUE +4013.                
                                                                                
      ***************************************************************           
      *  Cursor to retreive Transfer Data                                       
      ***************************************************************           
                                                                                
           EXEC SQL                                                             
               DECLARE TRANS_CRS CURSOR FOR                                     
               SELECT C.XFER_DKEY_ID                                            
                     ,C.FR_STR_NBR                                              
                     ,C.XFER_SEQ_NBR                                            
                     ,DATE(C.CMPT_XFER_END_TMST)                                
                     ,E.TO_PSTG_STR_NBR                                         
                     ,J.LOC_NM                                                  
                     ,F.BTCH_DKEY_ID                                            
                     ,F.BTCH_JULN_DAY                                           
                     ,E.XFER_LNE_NBR                                            
                     ,I.DEPT_NBR                                                
                     ,I.SUB_CL_NBR                                              
                     ,H.SKU_NBR                                                 
                     ,E.ITM_QTY                                                 
                     ,E.ITM_RTL_PRCE_AMT                                        
                     ,K.ACTL_INV_DTE                                            
               FROM                                                             
                      TTFSTAT                           B                       
                     ,TTFMSTR                           C                       
                     ,TTFITEM                           D                       
                     ,TTFITPS                           E                       
                     ,TTFBTCH                           F                       
                     ,TSKXREF                           G                       
                     ,XST_SKU                           H                       
                     ,XST_VND_STYL                      I                       
                     ,XST_LOC                           J                       
                     ,TINVPAR                           K                       
                     ,TINCNTL                           L                       
               WHERE                                                            
                     (B.XFER_STAT_CDE    IN ('AR','CM') OR 0=1)                 
                 AND B.XFER_STAT_TMST                                           
C54275               >= TIMESTAMP(CURRENT DATE - 90 DAYS,'00.00.00')            
                 AND B.XFER_STAT_TMST IN                                        
                 (SELECT MAX(XFER_STAT_TMST)                                    
                   FROM   TTFSTAT                                               
                   WHERE DATE (XFER_STAT_TMST) < CURRENT DATE                   
                   AND XFER_DKEY_ID = B.XFER_DKEY_ID                            
                   GROUP BY XFER_DKEY_ID )                                      
                 AND B.XFER_DKEY_ID      = C.XFER_DKEY_ID                       
                 AND C.XFER_DKEY_ID      = D.XFER_DKEY_ID                       
                 AND C.TO_STR_NBR        = J.LOC_NBR                            
                 AND D.XFER_DKEY_ID      = E.XFER_DKEY_ID                       
                 AND D.XFER_LNE_NBR      = E.XFER_LNE_NBR                       
                 AND D.ITM_NBR           = G.ITM_NBR                            
                 AND E.BTCH_DKEY_ID      = F.BTCH_DKEY_ID                       
                 AND E.PSTG_SEQ_NBR      = 1                                    
                 AND G.INTR_UPC_ID       = H.INTR_UPC_ID                        
                 AND H.STYL_ID           = I.STYL_ID                            
                 AND C.TO_STR_NBR        = K.LOC_NBR                            
                 AND K.INV_ID            = L.INV_ID                             
                 AND L.ACTV_IND          = 'Y'                                  
                 AND K.ACTL_INV_DTE      = CURRENT DATE - 1 DAYS                
                 AND K.ACTL_FIN_BK_DTE   = '9999-09-09'                         
                 AND K.LOC_INV_STAT_CDE  = 'IN'                                 
                 AND NOT (K.SCAN_INV_IND = 'Y'                                  
                            AND K.SCHD_PHY_CNT_CDE  = 'EI')                     
               UNION                                                            
               SELECT C.XFER_DKEY_ID                                            
                     ,C.FR_STR_NBR                                              
                     ,C.XFER_SEQ_NBR                                            
                     ,DATE(C.CMPT_XFER_END_TMST)                                
                     ,E.TO_PSTG_STR_NBR                                         
                     ,J.LOC_NM                                                  
                     ,F.BTCH_DKEY_ID                                            
                     ,F.BTCH_JULN_DAY                                           
                     ,E.XFER_LNE_NBR                                            
                     ,I.DEPT_NBR                                                
                     ,I.SUB_CL_NBR                                              
                     ,H.SKU_NBR                                                 
                     ,E.ITM_QTY                                                 
                     ,E.ITM_RTL_PRCE_AMT                                        
                     ,K.ACTL_INV_DTE                                            
               FROM                                                             
                      TTFSTAT                           B                       
                     ,TTFMSTR                           C                       
                     ,TTFITEM                           D                       
                     ,TTFITPS                           E                       
                     ,TTFBTCH                           F                       
                     ,TSKXREF                           G                       
                     ,XST_SKU                           H                       
                     ,XST_VND_STYL                      I                       
                     ,XST_LOC                           J                       
                     ,TINVPAR                           K                       
                     ,TINCNTL                           L                       
               WHERE                                                            
                     B.XFER_STAT_TMST    IN                                     
                 (SELECT XFER_STAT_TMST      AS TMST                            
                       FROM   TTFSTAT                                           
                  WHERE DATE(XFER_STAT_TMST) = CURRENT DATE                     
                    AND XFER_DKEY_ID = B.XFER_DKEY_ID                           
                    AND XFER_STAT_CDE        = 'AR')                            
                 AND B.XFER_DKEY_ID      = C.XFER_DKEY_ID                       
                 AND C.XFER_DKEY_ID      = D.XFER_DKEY_ID                       
                 AND C.TO_STR_NBR        = J.LOC_NBR                            
                 AND D.XFER_DKEY_ID      = E.XFER_DKEY_ID                       
                 AND D.XFER_LNE_NBR      = E.XFER_LNE_NBR                       
                 AND D.ITM_NBR           = G.ITM_NBR                            
                 AND E.BTCH_DKEY_ID      = F.BTCH_DKEY_ID                       
                 AND G.INTR_UPC_ID       = H.INTR_UPC_ID                        
                 AND H.STYL_ID           = I.STYL_ID                            
                 AND C.TO_STR_NBR        = K.LOC_NBR                            
                 AND K.INV_ID            = L.INV_ID                             
                 AND L.ACTV_IND          = 'Y'                                  
                 AND K.ACTL_INV_DTE      = CURRENT DATE - 1 DAYS                
                 AND K.ACTL_FIN_BK_DTE   = '9999-09-09'                         
                 AND K.LOC_INV_STAT_CDE  = 'IN'                                 
                 AND NOT (K.SCAN_INV_IND = 'Y'                                  
                 AND K.SCHD_PHY_CNT_CDE  = 'EI')                                
               UNION                                                            
               SELECT C.XFER_DKEY_ID                                            
                     ,C.FR_STR_NBR                                              
                     ,C.XFER_SEQ_NBR                                            
                     ,DATE(C.CMPT_XFER_END_TMST)                                
                     ,E.TO_PSTG_STR_NBR                                         
                     ,J.LOC_NM                                                  
                     ,F.BTCH_DKEY_ID                                            
                     ,F.BTCH_JULN_DAY                                           
                     ,E.XFER_LNE_NBR                                            
                     ,DECIMAL(SUBSTR(DIGITS(D.KOHLS_UPC_NBR),7,3),4,0)          
                     ,DECIMAL(SUBSTR(DIGITS(D.KOHLS_UPC_NBR),10,2),4,0)         
                     ,DECIMAL(SUBSTR(DIGITS(D.KOHLS_UPC_NBR),7,8),8,0)          
                     ,E.ITM_QTY                                                 
                     ,E.ITM_RTL_PRCE_AMT                                        
                     ,K.ACTL_INV_DTE                                            
               FROM                                                             
                      TTFSTAT                       B                           
                     ,TTFMSTR                       C                           
                     ,TTFITEM                       D                           
                     ,TTFITPS                       E                           
                     ,TTFBTCH                       F                           
                     ,XST_LOC                       J                           
                     ,TINVPAR                       K                           
                 ,TINCNTL                           L                           
               WHERE                                                            
                     (B.XFER_STAT_CDE     IN ('AR','CM') OR 0=1)                
                 AND B.XFER_STAT_TMST                                           
C54275               >= TIMESTAMP(CURRENT DATE - 90 DAYS,'00.00.00')            
                 AND B.XFER_STAT_TMST  IN                                       
                     (SELECT MAX(XFER_STAT_TMST)                                
                     FROM   TTFSTAT                                             
                     WHERE DATE (XFER_STAT_TMST) < CURRENT DATE                 
                     AND XFER_DKEY_ID = B.XFER_DKEY_ID                          
                     GROUP BY XFER_DKEY_ID                                      
                     )                                                          
                 AND B.XFER_DKEY_ID  = C.XFER_DKEY_ID                           
                 AND C.XFER_DKEY_ID  = D.XFER_DKEY_ID                           
                 AND C.TO_STR_NBR    = J.LOC_NBR                                
                 AND D.XFER_DKEY_ID  = E.XFER_DKEY_ID                           
                 AND D.XFER_LNE_NBR  = E.XFER_LNE_NBR                           
                 AND D.ITM_NBR       = 0                                        
                 AND E.BTCH_DKEY_ID  = F.BTCH_DKEY_ID                           
                 AND C.TO_STR_NBR    = K.LOC_NBR                                
                 AND K.INV_ID        = L.INV_ID                                 
                 AND L.ACTV_IND      = 'Y'                                      
                 AND K.ACTL_INV_DTE  = CURRENT DATE - 1 DAYS                    
                 AND K.ACTL_FIN_BK_DTE = '9999-09-09'                           
                 AND K.LOC_INV_STAT_CDE  = 'IN'                                 
                 AND NOT (K.SCAN_INV_IND = 'Y'                                  
                        AND K.SCHD_PHY_CNT_CDE  = 'EI')                         
               UNION                                                            
               SELECT C.XFER_DKEY_ID                                            
                     ,C.FR_STR_NBR                                              
                     ,C.XFER_SEQ_NBR                                            
                     ,DATE(C.CMPT_XFER_END_TMST)                                
                     ,E.TO_PSTG_STR_NBR                                         
                     ,J.LOC_NM                                                  
                     ,F.BTCH_DKEY_ID                                            
                     ,F.BTCH_JULN_DAY                                           
                     ,E.XFER_LNE_NBR                                            
                     ,DECIMAL(SUBSTR(DIGITS(D.KOHLS_UPC_NBR),7,3),4,0)          
                     ,DECIMAL(SUBSTR(DIGITS(D.KOHLS_UPC_NBR),10,2),4,0)         
                     ,DECIMAL(SUBSTR(DIGITS(D.KOHLS_UPC_NBR),7,8),8,0)          
                     ,E.ITM_QTY                                                 
                     ,E.ITM_RTL_PRCE_AMT                                        
                     ,K.ACTL_INV_DTE                                            
               FROM                                                             
                      TTFSTAT                       B                           
                     ,TTFMSTR                       C                           
                     ,TTFITEM                       D                           
                     ,TTFITPS                       E                           
                     ,TTFBTCH                       F                           
                     ,XST_LOC                       J                           
                     ,TINVPAR                       K                           
                     ,TINCNTL                       L                           
               WHERE                                                            
                     B.XFER_DKEY_ID  = C.XFER_DKEY_ID                           
                 AND B.XFER_STAT_TMST    IN                                     
                   (SELECT XFER_STAT_TMST      AS TMST                          
                   FROM   TTFSTAT                                               
              WHERE DATE(XFER_STAT_TMST) = CURRENT DATE                         
                 AND XFER_DKEY_ID = B.XFER_DKEY_ID                              
                 AND XFER_STAT_CDE        = 'AR')                               
                 AND C.XFER_DKEY_ID  = D.XFER_DKEY_ID                           
                 AND C.TO_STR_NBR    = J.LOC_NBR                                
                 AND D.XFER_DKEY_ID  = E.XFER_DKEY_ID                           
                 AND D.XFER_LNE_NBR  = E.XFER_LNE_NBR                           
                 AND D.ITM_NBR       = 0                                        
                 AND E.BTCH_DKEY_ID  = F.BTCH_DKEY_ID                           
                 AND E.PSTG_SEQ_NBR  = 1                                        
                 AND C.TO_STR_NBR    = K.LOC_NBR                                
                 AND K.INV_ID        = L.INV_ID                                 
                 AND L.ACTV_IND      = 'Y'                                      
                 AND K.ACTL_INV_DTE  =   CURRENT DATE - 1 DAYS                  
                 AND K.ACTL_FIN_BK_DTE = '9999-09-09'                           
                 AND K.LOC_INV_STAT_CDE  = 'IN'                                 
                 AND NOT (K.SCAN_INV_IND = 'Y'                                  
                        AND K.SCHD_PHY_CNT_CDE  = 'EI')                         
           WITH UR                                                              
           END-EXEC.                                                            
       PROCEDURE DIVISION.                                                      
      *===================*                                                     
                                                                                
       000-MAIN-CONTROL.                                                        
      *****************************************************************         
      * Description     : Control main processing flow                *         
      *****************************************************************         
                                                                                
           MOVE '*000-MAIN-CONTROL       *'   TO PV-PARAGRAPH-NAME              
                                                                                
           PERFORM 0500-INITIALIZATION                                          
                                                                                
           PERFORM 3000-PROCESS-TRANS-CRS                                       
             UNTIL END-OF-CRS                                                   
                                                                                
           PERFORM 9000-TERMINATE                                               
                                                                                
           GOBACK.                                                              
                                                                                
       0500-INITIALIZATION.                                                     
      *****************************************************************         
      * Description     : Initialise fields and working storage       *         
      *****************************************************************         
                                                                                
           INITIALIZE                            PV-PROGRAM-VARIABLES           
                                                                                
           MOVE '*0500-INITIALIZATION.   *'   TO PV-PARAGRAPH-NAME              
                                                                                
      *--  Open output clean up file                                            
           OPEN OUTPUT UNLOAD-OUTFILE                                           
                                                                                
      *--  Process the Transfer cursor                                          
           PERFORM  1000-OPEN-TRANS-CRS                                         
           SET NOT-END-OF-CRS                 TO TRUE                           
                                                                                
           INITIALIZE                      TFRD060-INV-SNAPSHOT-EXTRACT         
           PERFORM 3200-FETCH-TRANS-CRS.                                        
                                                                                
                                                                                
      ******************************************************************        
      * Description     : Open trans cursor                           *         
      ******************************************************************        
       1000-OPEN-TRANS-CRS.                                                     
                                                                                
           MOVE '*1000-OPEN-TRANS-CRS    *'   TO PV-PARAGRAPH-NAME.             
                                                                                
           EXEC SQL                                                             
               OPEN TRANS_CRS                                                   
           END-EXEC.                                                            
                                                                                
           IF  SQLCODE NOT = ZERO                                               
               MOVE 'OPEN ON TRANS CURSOR'    TO PV-DB2-OPER-ATTEMPTED          
               PERFORM 9999-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
       3000-PROCESS-TRANS-CRS.                                                  
      *****************************************************************         
      * Description     : Main Processing                             *         
      *****************************************************************         
                                                                                
           MOVE '*3000-PROCESS-TRANS-CRS *'   TO PV-PARAGRAPH-NAME              
                                                                                
           IF  NOT-END-OF-CRS                                                   
      * Note - Initially the file extracted using utility DSNTIAUL              
      * The null value of TFRD060-XFER-SEQ-NBR is moved to the first            
      * field of create date by the unload utility. To match the output         
      * file of this program with the output file of the utility we             
      * move low values to the first field of create date                       
      *                                                                         
               MOVE TFRD060-CREATE-DATE(1:10) TO PV-CREATE-DT                   
               MOVE LOW-VALUES                TO PV-NULL-FIELD                  
               MOVE SPACE                     TO TFRD060-CREATE-DATE            
               MOVE PV-CREATE-DT-FMT          TO TFRD060-CREATE-DATE            
           END-IF.                                                              
                                                                                
           PERFORM 3100-WRITE-INVENT-TRAN-XCT                                   
                                                                                
           INITIALIZE                     TFRD060-INV-SNAPSHOT-EXTRACT          
           PERFORM 3200-FETCH-TRANS-CRS.                                        
                                                                                
       3100-WRITE-INVENT-TRAN-XCT.                                              
      *****************************************************************         
      * Description  : Write the Transfer Data For Inventory Snapshot           
      *                Extract                                                  
      *****************************************************************         
                                                                                
           MOVE '*3100-WRITE-INVENT-TRANS-XCT' TO PV-PARAGRAPH-NAME             
                                                                                
           WRITE UNLOAD-OUTREC        FROM TFRD060-INV-SNAPSHOT-EXTRACT         
           COMPUTE PV-WRITE-REC-CNT = PV-WRITE-REC-CNT + 1.                     
                                                                                
      ******************************************************************        
      * Description     : Fetch TRANS cursor                           *        
      ******************************************************************        
       3200-FETCH-TRANS-CRS.                                                    
                                                                                
           MOVE '*3200-FETCH-TRANS-CRS   *'   TO PV-PARAGRAPH-NAME              
                                                                                
           EXEC SQL                                                             
               FETCH  TRANS_CRS                                                 
                INTO :TFRD060-XFER-DKEY-ID                                      
                    ,:TFRD060-FR-LOC-NBR                                        
                    ,:TFRD060-XFER-SEQ-NBR :PV-NULL-IND                         
                    ,:TFRD060-CREATE-DATE                                       
                    ,:TFRD060-TO-PSTG-LOC-NBR                                   
                    ,:TFRD060-TO-LOC-NM                                         
                    ,:TFRD060-BTCH-DKEY-ID                                      
                    ,:TFRD060-BTCH-JLN-DAY                                      
                    ,:TFRD060-XFER-LNE-NBR                                      
                    ,:TFRD060-DEPT-NBR                                          
                    ,:TFRD060-SUB-CL-NBR                                        
                    ,:TFRD060-SKU-NBR                                           
                    ,:TFRD060-ITM-QTY                                           
                    ,:TFRD060-ITM-RTL-PRCE-AMT                                  
                    ,:TFRD060-ACTL-INV-DATE                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   SET END-OF-CRS             TO TRUE                           
               WHEN OTHER                                                       
                 MOVE 'FETCH ON TRANS CURSOR' TO PV-DB2-OPER-ATTEMPTED          
                 PERFORM 9999-SQL-ERROR                                         
           END-EVALUATE.                                                        
                                                                                
       9000-TERMINATE.                                                          
      *****************************************************************         
      * Description     : Final Processing                            *         
      *****************************************************************         
                                                                                
           MOVE '*9000-TERMINATE.        *'   TO PV-PARAGRAPH-NAME              
                                                                                
           DISPLAY 'TOTAL RECORD WRITTEN TO REPORT ' PV-WRITE-REC-CNT           
           CLOSE                                 UNLOAD-OUTFILE                 
           PERFORM  9100-CLOSE-TRANS-CRS.                                       
                                                                                
      ******************************************************************        
      * Description     : Close Trans cursor                           *        
      ******************************************************************        
       9100-CLOSE-TRANS-CRS.                                                    
                                                                                
           MOVE '*9100-CLOSE-TRANS-CRS     *'  TO PV-PARAGRAPH-NAME             
                                                                                
           EXEC SQL                                                             
               CLOSE TRANS_CRS                                                  
           END-EXEC.                                                            
                                                                                
           IF  SQLCODE NOT = ZERO                                               
               MOVE 'CLOSE ON TRANS CURSOR'   TO PV-DB2-OPER-ATTEMPTED          
               PERFORM 9999-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
       9999-SQL-ERROR.                                                          
      ******************************************************************        
      * SQL abend routine                                                       
      ******************************************************************        
                                                                                
           SET ABEND-4013                     TO TRUE                           
                                                                                
           DISPLAY '**  ABEND     **'                                           
                                                                                
           DISPLAY '**  PROGRAM   **  =  TFKUT509'                              
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME                     
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED                 
      *                                                                         
      * The following copybook utilizes the DB2 abend module                    
      * DSNTIAR to convert SQLCA into understandable verbage                    
      *                                                                         
           COPY DPPD004.                                                        
      *                                                                         
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
