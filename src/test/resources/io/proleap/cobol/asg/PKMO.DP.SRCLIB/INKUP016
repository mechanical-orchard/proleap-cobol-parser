       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.             INKUP016.                                        
       AUTHOR.                 COGNIZANT.                                       
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           11/28/2012.                                      
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  INKUP016   ITEM CUTOFF INDICATOR UPDATE                                
      ******************************************************************        
      * PURPOSE :THIS PROGRAM WILL UPDATE THE TINVPAR ITEM CUTOFF      *        
      * INDICATOR FROM 'N' TO 'Y' FOR ALL STORES WHERE THE ITEM CUTOFF *        
      * IS BEING TAKEN FOR THE FIRST TIME DURING THIS INVENTORY PERIOD.*        
      *                                                                *        
      * INKUP012 ALSO PERFORMS THE SAME TASK BUT IT MISSES OUT TO FLIP *        
      * A FEW STORES. REFER PBI5882 FOR DETAILS ON THIS NEW PROGRAM    *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * WR/PITS#    DATE     DESCRIPTION                               *00200031
      * --------  --------   ------------------------------------------*00210031
      * PBI5882   01/18/2013 INITIAL INSTALLATION                      *00220031
      *                                                                *        
      ******************************************************************   CL**1
                                                                           CL**1
      ******************************************************************   CL**1
       ENVIRONMENT DIVISION.                                               CL**1
      ******************************************************************   CL**1
                                                                           CL**1
       CONFIGURATION SECTION.                                              CL**1
                                                                           CL**1
       SOURCE-COMPUTER.                        IBM-3090.                   CL**1
       OBJECT-COMPUTER.                        IBM-3090.                   CL**1
                                                                           CL**1
       INPUT-OUTPUT SECTION.                                               CL**1
                                                                           CL**1
       FILE-CONTROL.                                                       CL**1
                                                                           CL**1
           SELECT                CC-DATE-CNTL-FILE                         CL**1
                                 ASSIGN TO INP01.                          CL**1
                                                                           CL**1
                                                                           CL**1
      ******************************************************************   CL**1
       DATA DIVISION.                                                      CL**1
      ******************************************************************   CL**1
                                                                           CL**1
       FILE SECTION.                                                       CL**1
                                                                           CL**1
       FD  CC-DATE-CNTL-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CC-DATE-CNTL-REC                    PIC X(80).                       
                                                                           CL**1
       WORKING-STORAGE SECTION.                                            CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      * PROGRAM CONSTANTS                                              *   CL**1
      ******************************************************************   CL**1
                                                                           CL**1
       01  PC-PROGRAM-CONSTANTS.                                           CL**1
           05  PC-PROGRAM-NAME                 PIC  X(08)  VALUE           CL**1
               'INKUP016'.                                                 CL*17
                                                                           CL**1
       01  ABEND-CODE                          PIC S9(04)                  CL**1
                                               COMP SYNC   VALUE 0.        CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      * PROGRAM VARIABLES                                              *   CL**1
      ******************************************************************   CL**1
                                                                           CL*17
       01  PV-PROGRAM-VARIABLES.                                           CL**1
           05  PV-DB2-OPERATION-ATTEMPTED      PIC X(30)   VALUE SPACE.    CL**1
           05  PV-PARAGRAPH-NAME               PIC X(30)   VALUE SPACE.    CL**1
           05  PV-TINVPAR-LOC-NBR              PIC S9(4)   COMP.           CL**1
           05  PV-TINVPAR-INV-ID               PIC S9(9)   COMP.           CL**1
                                                                                
      ******************************************************************   CL**1
      * PROGRAM SWITCHES                                               *   CL**1
      ******************************************************************   CL**1
                                                                           CL*17
       01  PS-PROGRAM-SWITCHES.                                            CL**1
           05  PS-LOCATION-CSR-SW              PIC X(01).                  CL**1
               88  PS-EOF-LOCATION-CSR                      VALUE 'Y'.     CL**1
               88  PS-NOT-EOF-LOCATION-CSR                  VALUE 'N'.     CL**1
      ******************************************************************   CL*15
      * PROGRAM ACCUMULATORS                                           *   CL*15
      ******************************************************************   CL*15
                                                                           CL*17
       01  PA-PROGRAM-ACCUMULATORS.                                        CL*15
           05  PA-TINVPAR-ROWS-UPDATED         PIC S9(09)  COMP-3          CL*15
                                                           VALUE ZERO.     CL*15
           05  PA-TINVPAR-FETCH-COUNT          PIC 9(2)    VALUE ZERO.          
                                                                           CL**1
      ******************************************************************01140000
      *  STANDARD LAYOUT FOR DATE AREA                                     CL**1
      ******************************************************************01140000
       01  PV-PROCESS-DATE-AREA.                                                
           05  PV-PROCESS-DATE                 PIC X(10)   VALUE SPACE.         
           05  FILLER REDEFINES PV-PROCESS-DATE.                                
               10  PV-PROCESS-CCYY             PIC X(04).                       
               10  PV-PROCESS-HYPHEN1          PIC X(01).                       
               10  PV-PROCESS-MM               PIC X(02).                       
               10  PV-PROCESS-HYPHEN2          PIC X(01).                       
               10  PV-PROCESS-DD               PIC X(02).                       
           05  FILLER                          PIC X(70) VALUE SPACES.          
      ******************************************************************01140000
      *  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                   CL**1
      ******************************************************************01140000
           EXEC SQL                                                        CL**1
               INCLUDE SQLCA                                               CL**1
           END-EXEC.                                                       CL**1
                                                                           CL**1
           COPY SQLCA2.                                                    CL**1
                                                                           CL**1
      ******************************************************************01140000
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004           CL**1
      ******************************************************************01140000
           COPY DPWS004.                                                   CL**1
                                                                           CL**1
      *****************************************************************         
      *  TINVPAR TABLE DCLGEN                                                   
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  TINCNTL TABLE DCLGEN                                                   
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
      ******************************************************************        
      *****************************************************************         
      *  TSKST TABLE DCLGEN                                                     
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSKST                                                    
           END-EXEC.                                                            
      ******************************************************************        
      *  SQL DB2 LOCATION NUMBER CURSOR DECLARATION                             
      ******************************************************************        
           EXEC SQL                                                             
              DECLARE LOCATION_CSR CURSOR FOR                                   
                 SELECT A.LOC_NBR,A.INV_ID                                      
                   FROM TINVPAR A                                               
                       ,TINCNTL B                                               
                  WHERE ACTL_FIN_BK_DTE      = '9999-09-09'                     
                      AND B.ACTV_IND         = 'Y'                              
                      AND A.ITM_CTOFF_IND    = 'N'                              
                      AND PLND_INV_DTE      >= :PV-PROCESS-DATE                 
                      AND A.SCHD_PHY_CNT_CDE IN ('PI','CL','NS')                
                      AND A.LOC_INV_STAT_CDE = 'IN'                             
                      AND A.INV_ID           = B.INV_ID                         
                  ORDER BY LOC_NBR                                              
           END-EXEC.                                                            
                                                                           CL**1
       PROCEDURE DIVISION.                                                 CL**1
      ******************************************************************   CL**1
      *                 MAINLINE.                                      *   CL**6
      ******************************************************************   CL**1
       0000-MAINLINE.                                                      CL**1
                                                                           CL**1
           PERFORM 1000-INITIALIZE.                                        CL*12
                                                                           CL**1
           PERFORM 2000-GET-LOC-NBR.                                       CL*12
                                                                           CL**1
           PERFORM 9000-EOJ-ROUTINE.                                       CL*12
                                                                           CL**1
           GOBACK.                                                         CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      *  OPEN THE DATE CONTROL CARD TO PROCESS.                        *   CL**6
      ******************************************************************   CL**1
                                                                           CL**1
       1000-INITIALIZE.                                                    CL**1
           MOVE ZEROS TO PA-TINVPAR-FETCH-COUNT.                                
           MOVE ZEROS TO PV-TINVPAR-LOC-NBR.                                    
           MOVE ZEROS TO PA-TINVPAR-ROWS-UPDATED.                               
                                                                                
           OPEN INPUT CC-DATE-CNTL-FILE                                    CL**1
                                                                           CL**1
           PERFORM 1100-READ-PROCESS-DATE.                                      
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' PROCESSING DATE: ' PV-PROCESS-DATE.                        
                                                                                
           IF PV-PROCESS-CCYY NOT NUMERIC                                       
             OR PV-PROCESS-MM NOT NUMERIC                                       
             OR PV-PROCESS-DD NOT NUMERIC                                       
             OR PV-PROCESS-HYPHEN1 NOT = '-'                                    
             OR PV-PROCESS-HYPHEN2 NOT = '-'                                    
               MOVE +4002                 TO ABEND-CODE                         
               DISPLAY '**  ABEND     **'                                       
               DISPLAY '**  PROGRAM   **  =  INKUP016'                          
               DISPLAY '**  INVALID PROCESSING DATE '                           
               CALL 'ILBOABN0'  USING ABEND-CODE                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  READ THE PROCESS DATE CONTROL CARD FILE                                
      ******************************************************************        
       1100-READ-PROCESS-DATE.                                                  
                                                                                
           READ CC-DATE-CNTL-FILE INTO PV-PROCESS-DATE-AREA                     
               AT END                                                           
                   MOVE +4001                 TO ABEND-CODE                     
                   DISPLAY '**  ABEND     **'                                   
                   DISPLAY '**  PROGRAM   **  =  INKUP016'                      
                   DISPLAY '**  EMPTY DATE CONTROL FILE'                        
                   CALL 'ILBOABN0'  USING ABEND-CODE                            
           END-READ.                                                            
      ******************************************************************        
      *  READ THE INPUT FILE TO GET THE INVENTORY IDS ACTIVATED                 
      ******************************************************************        
       2000-GET-LOC-NBR.                                                        
                                                                                
           PERFORM 2300-OPEN-LOCATION-CSR.                                      
           PERFORM 2400-FETCH-LOCATION-CSR                                      
                   UNTIL PS-EOF-LOCATION-CSR.                                   
           PERFORM 2500-CLOSE-LOCATION-CSR.                                     
                                                                                
      ******************************************************************        
      *  OPEN THE LOCATION CURSOR                                               
      ******************************************************************        
       2300-OPEN-LOCATION-CSR.                                                  
               EXEC SQL                                                         
                   OPEN LOCATION_CSR                                            
               END-EXEC.                                                        
                                                                           CL**1
      ******************************************************************        
      *  FETCH THE LOCATION NUMBER FROM TINVPAR FOR THE INV ID                  
      ******************************************************************        
       2400-FETCH-LOCATION-CSR.                                                 
                                                                                
           EXEC SQL                                                             
             FETCH                                                              
               FROM LOCATION_CSR                                                
               INTO                                                             
                 :INVPAR-LOC-NBR,                                               
                 :INVPAR-INV-ID                                                 
           END-EXEC                                                             
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                        PERFORM 3000-SELECT-TSKST                               
                        ADD +1               TO PA-TINVPAR-FETCH-COUNT          
                   WHEN SQLCODE = +100                                          
                     SET PS-EOF-LOCATION-CSR TO TRUE                            
                                                                                
                     IF PA-TINVPAR-FETCH-COUNT = 0                              
                     DISPLAY '*********************************'                
                     DISPLAY ' ITM_CTFF_IND IS ALREADY SET TO Y.'               
      -              DISPLAY ' NO MORE STORES TO FLIP '                         
                     DISPLAY '*********************************'                
                     END-IF                                                     
                   WHEN OTHER                                                   
                        MOVE '2400-FETCH-LOCATION-CSR'                         C
                                       TO PV-PARAGRAPH-NAME                CL**1
                        MOVE 'SELECT TINVPAR'                              CL*11
                                       TO PV-DB2-OPERATION-ATTEMPTED       CL**1
                        PERFORM 9100-SQL-ABEND                             CL**1
                END-EVALUATE.                                                   
                                                                                
      ******************************************************************        
      *  CLOSE THE LOCATION CURSOR                                              
      ******************************************************************        
       2500-CLOSE-LOCATION-CSR.                                                 
               EXEC SQL                                                         
                   CLOSE LOCATION_CSR                                           
               END-EXEC.                                                        
                                                                           CL**1
      ******************************************************************        
      *  CHECK IF THE FETCHED LOCAITONS RETURN NO ROWS IN TSKST. IF IT          
      *  SATISFIES, THE ITM_CTOF_IND CAN BE UPDATED TO Y IN TINVPAR             
      ******************************************************************        
       3000-SELECT-TSKST.                                                       
                                                                                
               EXEC SQL                                                         
                   SELECT LOC_NBR                                           0291
                     INTO :TSKST-LOC-NBR                                        
                     FROM TSKST                                                 
                     WHERE LOC_NBR             = :INVPAR-LOC-NBR            0312
                     ORDER BY LOC_NBR                                           
                     FETCH FIRST ROW ONLY                                       
                     WITH UR                                                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                     DISPLAY '**************************************'           
                     DISPLAY 'ITM_CTOFF_IND NOT FLIPPED FOR LOC_NBR:'           
                                                       INVPAR-LOC-NBR           
                     DISPLAY 'STORE HAS UNITS IN TSKST'                         
                     DISPLAY 'INV_ID:' INVPAR-INV-ID                       CL*15
                     DISPLAY 'PLEASE ANALYSE THE ISSUE          '               
                     DISPLAY '**************************************'           
                   WHEN SQLCODE = +100                                          
                        PERFORM 4000-UPDATE-TINVPAR                        CL*15
                   WHEN OTHER                                                   
                        MOVE '3000-SELECT-TSKST'                               C
                                       TO PV-PARAGRAPH-NAME                CL**1
                        MOVE 'SELECT TSKST'                                CL*11
                                       TO PV-DB2-OPERATION-ATTEMPTED       CL**1
                        PERFORM 9100-SQL-ABEND                             CL**1
               END-EVALUATE.                                                    
                                                                                
      ******************************************************************FSKUP045
      *  UPDATE ITM_CTOFF_IND FOR STORES WHICH HAVE NO UNIT CUTOVER     FSKUP045
      *  IN TSKST                                                       FSKUP045
      ******************************************************************FSKUP045
       4000-UPDATE-TINVPAR.                                             FSKUP045
      *                                                                 FSKUP045
               EXEC SQL                                                         
                    UPDATE TINVPAR                                              
                        SET ITM_CTOFF_IND = 'Y'                                 
                        WHERE LOC_NBR      = :INVPAR-LOC-NBR                    
                        AND   INV_ID       = :INVPAR-INV-ID                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                        DISPLAY '************************************'          
                        DISPLAY 'ITM_CTOFF_IND FLIPPED TO Y FOR LOC:'      CL*15
      -                                                INVPAR-LOC-NBR           
                        DISPLAY 'INV_ID:' INVPAR-INV-ID                    CL*15
                        DISPLAY '************************************'          
                        ADD +1         TO PA-TINVPAR-ROWS-UPDATED               
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE  NOT EQUAL ZERO                                 
                        DISPLAY '*************************************'         
                        DISPLAY 'ITM_CTOFF_IND NOT FLIPPED TO Y FOR'       CL*15
      -                 DISPLAY 'LOC   :' INVPAR-LOC-NBR                   CL*15
                        DISPLAY 'INV_ID:' INVPAR-INV-ID                    CL*15
                        DISPLAY '*************************************'         
                        MOVE '4000-UPDATE-TINVPAR'                             C
                                       TO PV-PARAGRAPH-NAME                CL**1
                        MOVE 'UPDATE TINVPAR TABLE '                       CL*11
                                       TO PV-DB2-OPERATION-ATTEMPTED       CL**1
                        PERFORM 9100-SQL-ABEND                             CL**1
               END-EVALUATE.                                                    
      ******************************************************************   CL**1
      *      PERFORMS WRAP UP PROCESSING                               *   CL*12
      ******************************************************************   CL**1
                                                                           CL**1
       9000-EOJ-ROUTINE.                                                   CL**1
                                                                           CL**1
           CLOSE CC-DATE-CNTL-FILE.                                             
                                                                           CL**1
           EXEC SQL                                                        CL**5
               COMMIT WORK                                                 CL**5
           END-EXEC.                                                       CL**5
                                                                           CL*12
           PERFORM 9005-DISPLAY-TOTALS.                                    CL*16
                                                                           CL*16
      ******************************************************************   CL*16
      *      DISPLAYS PROGRAM TOTALS                                   *   CL*16
      ******************************************************************   CL*16
                                                                           CL*16
       9005-DISPLAY-TOTALS.                                                CL*16
                                                                           CL*16
           DISPLAY '************************************************'.     CL*16
           DISPLAY '** **************INKUP016 TOTALS **************'.      CL*16
           DISPLAY '* LOC NBR FETCH COUNT   : ' PA-TINVPAR-FETCH-COUNT.    CL*16
           DISPLAY '* TINVPAR ROWS UPDATED  : ' PA-TINVPAR-ROWS-UPDATED.   CL*16
           DISPLAY '** ***********END OF INKUP016 TOTALS **********'.      CL*16
           DISPLAY '************************************************'.     CL*16
                                                                           CL*16
      ******************************************************************   CL**1
      *      PERFORMS ABEND PROCESSING FOR SQL ERRORS.                 *   CL*13
      ******************************************************************   CL**1
                                                                           CL**1
       9100-SQL-ABEND.                                                     CL**1
           PERFORM 9005-DISPLAY-TOTALS.                                    CL*16
           DISPLAY ' '.                                                    CL*16
           DISPLAY '************************************************'.     CL*16
           DISPLAY '9100-SQL-ABEND'                                        CL**1
           DISPLAY '** ABEND     **'.                                      CL**1
           DISPLAY '** PROGRAM   ** = INKUP016'.                           CL*17
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                 CL**1
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.        CL**1
           DISPLAY '************************************************'.     CL*16
      ******************************************************************   CL**1
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-       CL**1
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE            CL**1
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE        CL**1
      * FORMAT.                                                            CL**1
      ******************************************************************   CL**1
           COPY DPPD004.                                                   CL**1
                                                                           CL**1
           MOVE +4013 TO ABEND-CODE.                                       CL**1
           CALL 'ILBOABN0' USING ABEND-CODE.                               CL**1
                                                                           CL*13
