       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.           DAKUP201.                                          
       AUTHOR.               V. LEE YANG                                        
       INSTALLATION.         KOHLS DEPARTMENT STORES.                           
       DATE-WRITTEN          05/01/2006.                                        
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *   DAKUP201 - E-MAIL VENDOR AND UPDATE E-MAIL STATUS            *        
      *                                                                *        
      *   THIS PROGRAM SEQUENTIALLY READS AN INPUT FILE OF E-MAIL      *        
      *   TEMPLATE WITH %%SYMBOLIC TO BE TRANSLATED/RESOLVED FOR A     *        
      *   GIVEN DEBIT ALLOWANCE WITH AN E-MAIL ADDRESS.                *        
      *   THE PROGRAM WILL GENERATE AN E-MAIL FOR EVERY DEBIT          *        
      *   ALLOWANCE THAT HAS BEEN COMPLETED WITH AN E-MAIL ADDRESS     *        
      *   AND E-MAIL STATUS OF NULL (OR NOT SENT).  THE PROGRAM        *        
      *   THEN UPDATE THE E-MAIL STATUS TO 'S' FOR SENT.               *        
      *                                                                *        
      *   THIS PROGRAM USES THE DYNAMIC ALLOCATION ROUTINE CALLED      *        
      *   FROM DPPD023.  THE E-MAIL OUTPUT FILE IS BEING OPENED        *        
      *   AND CLOSED FOR EACH E-MAIL GENERATED/SENT OUT.               *        
      *   NO RESTART LOGIC USED BECAUSE OF THE E-MAIL STATUS BEING     *        
      *   UPDATED TO 'S' WILL PREVENT SENDING TO COMMITTED UPDATE.     *        
      *                                                                *        
      *   THERE IS A POTENTIAL FOR REPEATED E-MAIL FOR SENT E-MAIL     *        
      *   IN BETWEEN COMMITS, IF THE PROGRAM FAIL.                     *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * P884121  05/26/2006  P. KEBER - INITIALIZE STRING AREAS BEFORE *        
      *                      USING THE STRING COMMAND FOR VALID FROM   *        
      *                      BUYER E-MAIL ADDRESS.                     *        
      * WR30021  05/30/2006  VL YANG  - SHORTEN THE SUBJECT LINE       *        
      *                      BECAUSE OF OVERRUN INTO THE E-MAIL BODY BY*        
      *                      ADDING NEW SYMBOLIC FOR MONTH-END DATE IN *        
      *                      THE FORMAT OF YYYY-MM-DD AND REMOVING     *        
      *                      'DEPT-' LITERAL FROM DEPT-NBR SYMBOLIC.   *        
      * P960618  12/13/2006  JILL LIN - FIX THE UNDELIVERABLE EMAIL    *        
      *                      ISSUE WHEN BUYER INFORMATION IS NOT       *        
      *                      AVAILABLE.                                *        
      * P979622  02/06/2007  JILL LIN - FIX THE EMAILS FISCAL END DATE *        
      *                      PRESENTING ISSUE.                         *        
8613F * 8613F    07-17-2015 S BARTELT  RMS 9 REMEDIATION. CHANGED FROM          
      *                              AUTO LOGON TO EXTERNAL SIGN ON             
208057* CHG0208057 9-10-2018 JIM HUNTER MAINFRAME REFACTORING -                 
208057*                                 CONVERT FROM CALLING MLPD502 TO         
208057*                                 CALLING DPKUT085 TO GET CURRENT         
208057*                                 JOB NAME.                               
      ******************************************************************        
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
8613F      SELECT ORACLE-LOGIN-FILE                                             
               ASSIGN TO ORALOGON.                                              
                                                                                
           SELECT EMAIL-TEMPLATE-FILE                                           
               ASSIGN TO INP02.                                                 
                                                                                
W30021     SELECT  WEEKLY-DATE-FILE                                             
W30021         ASSIGN TO INP03.                                                 
W30021                                                                          
      * THIS OUTPUT IS DYNAMICALLY ALLOCATED THROUGH CODES IN DPPD023           
           SELECT EMAIL-OUTPUT-FILE                                             
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
8613F  FD  ORACLE-LOGIN-FILE                                                    
8613F      RECORDING MODE IS F                                                  
8613F      LABEL RECORDS ARE STANDARD                                           
8613F      BLOCK CONTAINS 0 RECORDS                                             
8613F      DATA RECORD IS ORACLE-LOGIN-REC.                                     
8613F  01  ORACLE-LOGIN-REC.                                                    
8613F      05  FILLER                  PIC X(80).                               
                                                                                
W30021 FD  WEEKLY-DATE-FILE                                                     
W30021     RECORDING MODE IS F                                                  
W30021     LABEL RECORDS ARE STANDARD                                           
W30021     BLOCK CONTAINS 0 RECORDS.                                            
W30021                                                                          
W30021 01  WEEKLY-DATE-RECORD            PIC X(50).                             
W30021                                                                          
       FD  EMAIL-TEMPLATE-FILE                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  EMAIL-TEMPLATE-RECORD           PIC X(080).                          
                                                                                
       FD  EMAIL-OUTPUT-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  EMAIL-OUTPUT-RECORD             PIC X(080).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR CONTROL CARD FILE.                                    
8613F ****************************************************************          
8613F                                                                           
8613F  01  CC-ORACLE-CONNECTION.                                                
8613F      05  CC-ORACLE-USERID                PIC X(40).                       
8613F          88 CC-AUTO-LOGON                           VALUE '/'.            
8613F      05  CC-ORACLE-PASSWORD              PIC X(40).                       
                                                                                
      *****************************************************************         
      *  KOHLS CALENDAR ROUTINE COPYBOOK                              *         
      *****************************************************************         
      *01  DP500-KOHLS-CALENDAR-ROUTINE.                                        
      *01  DPG51.                                                               
      *01  DPG52.                                                               
      *01  DPG53.                                                               
      *01  DPG54.                                                               
      *01  DPG55.                                                               
      *01  DPG56.                                                               
           COPY DPWS500.                                                        
                                                                                
      ******************************************************************        
      *  PARAMETER LIST FOR THE DYNAMIC ALLOCATION ROUTINE                      
      ******************************************************************        
      *01  DP023-DYNALC.                                                        
      *01  DP023-DYNALC-PARMS.                                                  
      *01  DP023-DYNALC-PD-PARMS.                                               
      *01  DP023-ABEND-CODE                                                     
      *01  DP023-SUB                                                            
      *01  DP023-MAX                                                            
      *01  DP023-RETURN-CODES.                                                  
      *01  DP023-RETURN-CODE-CONSTANTS.                                         
           COPY DPWS023.                                                        
                                                                                
208057*****************************************************************         
208057* JOB NAME                                                                
208057*****************************************************************         
208057*01  DP085-GET-CURR-JOB-INFO-PGM.                                         
208057*01  DP085-GET-CURR-JOB-INFO-PARMS.                                       
208057     COPY DPWS085.                                                        
                                                                                
W30021*****************************************************************         
W30021*  WEEKLY OPEN FISCAL WEEK/MONTH DATES RECORD LAYOUT            *         
W30021*****************************************************************         
W30021     COPY DARD104.                                                        
W30021                                                                          
      *****************************************************************         
      *  PROGRAM'S ACCUMULATORS                                       *         
      *****************************************************************         
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-ROWS-FETCHED             PIC 9(09)       VALUE ZEROES.        
           05  PA-ROWS-UPDATED             PIC 9(09)       VALUE ZEROES.        
           05  PA-ROWS-TO-COMMIT           PIC 9(09)       VALUE ZEROES.        
           05  PA-NUMBER-OF-COMMIT         PIC 9(09)       VALUE ZEROES.        
           05  PA-EMAIL-GENERATED          PIC 9(09)       VALUE ZEROES.        
           05  PA-EMAIL-RECORDS-WRITE      PIC 9(09)       VALUE ZEROES.        
                                                                                
      *****************************************************************         
      *  PROGRAM'S CONSTANTS                                          *         
      *****************************************************************         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-DEADLOCKS            PIC S9(04)      VALUE +3.            
           05  PC-COMMIT-COUNT             PIC 9(03)       VALUE 50.            
           05  PC-PROGRAM-NAME             PIC X(08)       VALUE                
               'DAKUP201'.                                                      
           05  PC-ORACLE-PROD-ID           PIC X(1)        VALUE '/'.           
           05  PC-ORACLE-PROD-ID-LGTH      PIC 9(02)       VALUE 1.             
           05  PC-PROCESS-STAT-CDE         PIC X(02)       VALUE '60'.          
                                                                                
W30021     05  PC-CURR-TIME                PIC 9(08)       VALUE ZEROES.        
W30021     05  PC-AFTR-TIME                PIC 9(08)       VALUE ZEROES.        
W30021     05  PC-TIME-DIFF                PIC 9(08)       VALUE ZEROES.        
8613F      05  PC-AUTO-LOGON               PIC X(01)       VALUE '/'.           
      *****************************************************************         
      *  PROGRAM'S SWITCHES                                           *         
      *****************************************************************         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-TDAALW-SW         PIC X(01)       VALUE 'N'.           
               88  PS-END-OF-TDAALW                        VALUE 'Y'.           
               88  PS-NOT-END-OF-TDAALW                    VALUE 'N'.           
           05  PS-END-OF-EMAIL-FILE-SW     PIC X(01)       VALUE 'N'.           
               88  PS-END-OF-EMAIL-FILE                    VALUE 'Y'.           
               88  PS-NOT-END-OF-EMAIL-FILE                VALUE 'N'.           
           05  PS-END-OF-CONTROL-CARDS-SW  PIC X(01)       VALUE 'N'.           
               88  PS-END-OF-CONTROL-CARDS                 VALUE 'Y'.           
               88  PS-NOT-END-OF-CONTROL-CARDS             VALUE 'N'.           
8613F      05  PS-ORACLE-LOGIN-EOF-SW      PIC  X          VALUE 'N'.           
8613F          88  ORACLE-LOGIN-EOF                        VALUE 'Y'.           
                                                                                
      *****************************************************************         
      *  PROGRAM'S VARIABLES                                          *         
      *****************************************************************         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-TEMP-FIRST-NAME          PIC X(30)       VALUE SPACES.        
           05  PV-TEMP-LAST-NAME           PIC X(30)       VALUE SPACES.        
           05  PV-DMM-NAME                 PIC X(30)       VALUE SPACES.        
           05  PV-DMM-WORKER-NBR           PIC S9(6)V      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-DMM-USER-ID              PIC X(08)       VALUE SPACES.        
           05  PV-BUYER-NAME               PIC X(30)       VALUE SPACES.        
           05  PV-BUYER-WORKER-NBR         PIC S9(6)V      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-BUYER-USER-ID            PIC X(08)       VALUE SPACES.        
           05  PV-JOBNAME                  PIC X(08)       VALUE SPACES.        
           05  FILLER REDEFINES PV-JOBNAME.                                     
               10  PV-JOBNAME-APPL         PIC X(02).                           
                   88  PV-PROD-JOBS                        VALUES ARE           
                       'DA' 'IN' 'MJ' 'ML'.                                     
               10  FILLER                  PIC X(06).                           
           05  PV-LAST-COMMIT-RECORD       PIC 9(09)       VALUE ZEROES.        
           05  PV-LAST-COMMIT-DR-ALW-NBR   PIC X(16)       VALUE SPACES.        
           05  PV-RETRY-COUNT              PIC S9(04)      VALUE ZEROES.        
           05  PV-PARAGRAPH-NAME           PIC X(40)       VALUE SPACES.        
           05  PV-OPERATION-ATTEMPTED      PIC X(50)       VALUE SPACES.        
           05  PV-NULL-RETURN              PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-SYMBOLIC-FOUND           PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-NET-CST-AMT              PIC -ZZ,ZZZ,ZZZ,ZZ9.99.              
           05  PV-NET-CST-AMT-X REDEFINES PV-NET-CST-AMT                        
                                           PIC X(18).                           
           05  PV-EFF-FSCL-DATE-TEXT       PIC X(18)       VALUE SPACES.        
           05  PV-EFF-FSCL-MONTH-NAME      PIC X(10)       VALUE SPACES.        
           05  PV-EFF-FSCL-MONTH-END-DTE   PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES PV-EFF-FSCL-MONTH-END-DTE.                      
               10  PV-EFF-FSCL-MONTH-END-YYYY                                   
                                           PIC X(04).                           
               10  FILLER                  PIC X(01).                           
               10  PV-EFF-FSCL-MONTH-END-MM                                     
                                           PIC X(02).                           
               10  FILLER                  PIC X(01).                           
               10  PV-EFF-FSCL-MONTH-END-DD                                     
                                           PIC X(02).                           
           05  PV-CURR-FSCL-QTR-BEG.                                            
               10  PV-CURR-BEG-DTE         PIC X(10)       VALUE SPACES.        
               10  PV-CURR-BEG-TIME        PIC X(09)       VALUE                
                   '-00.00.00'.                                                 
           05  PV-CURR-FSCL-QTR-END.                                            
               10  PV-CURR-END-DTE         PIC X(10)       VALUE SPACES.        
               10  PV-CURR-END-TIME        PIC X(09)       VALUE                
                   '-23.59.59'.                                                 
8613F      05  PV-ORACLE-USERID-LEN        PIC S9(04)      VALUE ZEROS.         
8613F      05  PV-ORACLE-PASSWORD-LEN      PIC S9(04)      VALUE ZEROS.         
                                                                                
W30021     05  PV-TALLY                    PIC S9(09)      VALUE ZEROES         
W30021                         COMP.                                            
W30021     05  PV-LENGTH                   PIC S9(09)      VALUE ZEROES         
W30021                         COMP.                                            
       01  PV-ABEND-CODE                   PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  ABEND-4001-INV-WRITE-KEY                    VALUE +4001.         
           88  ABEND-4002-INV-READ-KEY                     VALUE +4002.         
           88  ABEND-4003-CNTL-CARD-ERRORS                 VALUE +4003.         
           88  ABEND-4004-INTERNAL-ERROR                   VALUE +4004.         
           88  ABEND-4005-OPEN-FILE-ERROR                  VALUE +4005.         
           88  ABEND-4006-CLOSE-FILE-ERROR                 VALUE +4006.         
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
           88  ABEND-4009-INVALID-DATA                     VALUE +4009.         
           88  ABEND-4012-BAD-INTR-SORT                    VALUE +4012.         
           88  ABEND-4013-DB-ERROR                         VALUE +4013.         
                                                                                
                                                                                
      *****************************************************************         
      *  E-MAIL MESSAGE DATA LINES                                    *         
      *****************************************************************         
       01  EM-EMAIL-MESSAGE-DATA-LINES.                                         
           05  EM-DATA-OUTPUT-LINE         PIC X(80)       VALUE SPACES.        
                                                                                
      *****************************************************************         
      *  PROGRAM'S TABLE AREA                                         *         
      *****************************************************************         
       01  PT-PROGRAM-TABLES.                                                   
           05  PT-EMAIL-TEXT-TBL.                                               
               10  PT-MAX-EMAIL-LINE       PIC S9(04)      VALUE +250           
                               COMP-3.                                          
               10  PT-EMAIL-COUNT          PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
               10  PT-EMAIL-INDX           PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
               10  PT-EMAIL-TEXT OCCURS 250 TIMES                               
                                           PIC X(080)      VALUE SPACES.        
           05  PT-TEXT-LINE-1-TBL.                                              
               10  PT-MAX-TEXT-1-CHAR      PIC S9(04)      VALUE +080           
                               COMP-3.                                          
               10  PT-TEXT-1-SAVED         PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
               10  PT-TEXT-1-INDX          PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
               10  PT-TEXT-LINE-1          PIC X(080)      VALUE SPACES.        
               10  FILLER REDEFINES PT-TEXT-LINE-1.                             
                   15  PT-TEXT-LINE-1-CHAR OCCURS 080 TIMES                     
                                           PIC X(01).                           
           05  PT-TEXT-LINE-2-TBL.                                              
               10  PT-MAX-TEXT-2-CHAR      PIC S9(04)      VALUE +080           
                               COMP-3.                                          
               10  PT-TEXT-2-COUNT         PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
               10  PT-TEXT-2-INDX          PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
               10  PT-TEXT-LINE-2          PIC X(080)      VALUE SPACES.        
               10  FILLER REDEFINES PT-TEXT-LINE-2.                             
                   15  PT-TEXT-LINE-2-CHAR OCCURS 080 TIMES                     
                                           PIC X(01).                           
           05  PT-KEY-WRD-LINE-1-TBL.                                           
               10  PT-MAX-KEY-WRD-1-CHAR   PIC S9(04)      VALUE +020           
                               COMP-3.                                          
               10  PT-KEY-WRD-1-COUNT      PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
               10  PT-KEY-WRD-1-INDX       PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
               10  PT-KEY-WRD-LINE-1       PIC X(020)      VALUE SPACES.        
                   88  PT-DA-EMAIL-ADDR-KEY                VALUE                
                       '%%DA-EMAIL-ADDRESS  '.                                  
                   88  PT-VENDOR-NAME-KEY                  VALUE                
                       '%%VENDOR-NAME       '.                                  
                   88  PT-DMM-NAME-KEY                     VALUE                
                       '%%DMM-NAME          '.                                  
                   88  PT-BUYER-NAME-KEY                   VALUE                
                       '%%BUYER-NAME        '.                                  
                   88  PT-BUYER-ID-KEY                     VALUE                
                       '%%BUYER-ID          '.                                  
                   88  PT-DA-DEPT-NBR-KEY                  VALUE                
                       '%%DA-DEPT           '.                                  
                   88  PT-DA-EFF-DTE-KEY                   VALUE                
                       '%%DA-EFF-DTE        '.                                  
                   88  PT-DA-DATE-KEY                      VALUE                
                       '%%DA-DATE           '.                                  
                   88  PT-DA-DR-ALW-NBR-KEY                VALUE                
                       '%%DA-DR-ALW-NBR     '.                                  
                   88  PT-DA-NET-CST-AMT-KEY               VALUE                
                       '%%DA-NET-CST-AMT    '.                                  
                   88  PT-DA-TYPE-DESC-KEY                 VALUE                
                       '%%DA-TYPE-DESC      '.                                  
W30021             88  PT-DA-AUTH-NUMBER                   VALUE                
W30021                 '%%DA-AUTH-NUMBER    '.                                  
               10  FILLER REDEFINES PT-KEY-WRD-LINE-1.                          
                   15  PT-KEY-WRD-LINE-1-CHAR OCCURS 020 TIMES                  
                                           PIC X(01).                           
           05  PT-KEY-WRD-LINE-2-TBL.                                           
               10  PT-MAX-KEY-WRD-2-CHAR   PIC S9(04)      VALUE +300           
                               COMP-3.                                          
               10  PT-KEY-WRD-2-COUNT      PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
               10  PT-KEY-WRD-2-INDX       PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
               10  PT-KEY-WRD-2-MODE-SW    PIC X(08)       VALUE SPACES.        
                   88  PT-KEY-WRD-2-TRIM-LEAD              VALUE                
                       'LEADING '.                                              
                   88  PT-KEY-WRD-2-TRIM-TRAIL             VALUE                
                       'TRAILING'.                                              
                   88  PT-KEY-WRD-2-TRIM-BOTH              VALUE                
                       'BOTH    '.                                              
               10  PT-KEY-WRD-LINE-2       PIC X(300)      VALUE SPACES.        
               10  FILLER REDEFINES PT-KEY-WRD-LINE-2.                          
                   15  PT-KEY-WRD-LINE-2-CHAR OCCURS 300 TIMES                  
                                           PIC X(01).                           
                                                                                
       01  WS-MSG-TEXT                     PIC X(512)      VALUE SPACES.        
       01  WS-MAX-SIZE                     PIC S9(09)      VALUE +512           
                               COMP.                                            
       01  WS-MSG-LENGTH                   PIC S9(09)      VALUE ZEROES         
                               COMP.                                            
                                                                                
      ******************************************************************        
      *    ORACLE DECLARE SECTION                                               
      ******************************************************************        
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
       01  USERNAME                        PIC X(10)      VARYING.              
       01  PASSWD                          PIC X(10)      VARYING.              
                                                                                
      ******************************************************************        
      * COBOL DECLARATION FOR DEBIT ALLOWANCE TRANSACTIONS TABLE                
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TDAALW                                                   
           END-EXEC.                                                            
           10  DAALW-RMNG-AVL-COST-AMT     PIC S9(11)V9(2) COMP-3.              
           10  DAALW-EMAIL-ADDR            PIC X(250).                          
           10  DAALW-EMAIL-ADDR-ORGN-CDE   PIC X(02).                           
           10  DAALW-EMAIL-ADDR-COM-STAT-CDE PIC X(01).                         
                                                                                
      ******************************************************************        
      * COBOL DECLARATION FOR DEBIT ALLOWANCE CODE DESCRIPTION TABLE            
      ******************************************************************        
      *    EXEC SQL                                                             
      *        INCLUDE TDACDE                                                   
      *    END-EXEC.                                                            
      ******************************************************************        
      * COBOL DECLARATION FOR TABLE TDACDE                             *        
      ******************************************************************        
       01  DCLTDACDE.                                                           
           10  DACDE-CDE-TYP-DESC          PIC X(26).                           
           10  DACDE-CDE-VAL-CDE           PIC X(2).                            
           10  DACDE-CDE-DESC              PIC X(30).                           
           10  DACDE-ACTV-IND              PIC X(1).                            
                                                                                
      ******************************************************************        
      * COBOL DECLARATION FOR DB2 VENDOR NAME TABLE                             
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TENTNME                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * COBOL DECLARATION FOR DB2 MERCHANDISE HEIRARCHY TABLE                   
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * COBOL DECLARATION FOR DB2 RESPONSIBILITY TABLE                          
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TRSPBAR                                                  
           END-EXEC.                                                            
                                                                                
W30021******************************************************************        
W30021* COBOL DECLARATION FOR DB2 TDANOTE TABLE                                 
W30021******************************************************************        
W30021     EXEC SQL                                                             
W30021         INCLUDE TDANOTE                                                  
W30021     END-EXEC.                                                            
W30021                                                                          
      ******************************************************************        
      * COBOL DECLARATION FOR DB2 WORKER INFORMATION TABLE                      
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TWORKER                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DEBIT ALLOWANCE EMAIL CURSOR                                            
      ******************************************************************        
           EXEC SQL                                                             
               DECLARE TDAALW_INFO CURSOR FOR                                   
                   SELECT                                                       
                           DR_ALW_NBR                                           
                          ,TYP_CDE                                              
                          ,DEPT_NBR                                             
                          ,ENT_ID                                               
                          ,TO_CHAR(EFF_DTE, 'YYYY-MM-DD-HH24.MI.SS')            
                          ,NET_CST_AMT                                          
                          ,EMAIL_ADDR                                           
960618                    ,CRE_BY_USR_ID                                        
                     FROM                                                       
                           TDAALW                                               
                     WHERE                                                      
                           EMAIL_ADDR IS NOT NULL                               
                       AND EMAIL_ADDR_COM_STAT_CDE IS NULL                      
                       AND NOT (TYP_CDE IN ('PR', 'PC', 'AC'))                  
                       AND STAT_CDE = :PC-PROCESS-STAT-CDE                      
           END-EXEC.                                                            
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
      *                                                                         
      * ORA COMMUNICATION AREA                                                  
      *                                                                         
        01       SQLCA.                                                         
                 05  SQLCAID               PIC X(8).                            
                 05  SQLCABC               PIC S9(9) COMPUTATIONAL.             
                 05  SQLCODE               PIC S9(9) COMPUTATIONAL.             
                 05  SQLERRM.                                                   
                     49 SQLERRML           PIC S9(4) COMPUTATIONAL.             
                     49 SQLERRMC           PIC X(70).                           
                 05  SQLERRP               PIC X(8).                            
                 05  SQLERRD OCCURS 6 TIMES                                     
                                           PIC S9(9) COMPUTATIONAL.             
                 05  SQLWARN.                                                   
                     10 SQLWARN0           PIC X(1).                            
                     10 SQLWARN1           PIC X(1).                            
                     10 SQLWARN2           PIC X(1).                            
                     10 SQLWARN3           PIC X(1).                            
                     10 SQLWARN4           PIC X(1).                            
                     10 SQLWARN5           PIC X(1).                            
                     10 SQLWARN6           PIC X(1).                            
                     10 SQLWARN7           PIC X(1).                            
                 05  SQLEXT                PIC X(8).                            
      ******************************************************************        
      * ORA COMMUNICATION AREA2                                                 
           COPY SQLCA2.                                                         
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      *    MAINLINE LOGIC                                              *        
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 8000-OPEN-TDAALW-CSR.                                        
           PERFORM 8200-FETCH-TDAALW.                                           
           PERFORM 2000-PROCESS-TDAALW                                          
               UNTIL PS-END-OF-TDAALW.                                          
           PERFORM 8100-CLOSE-TDAALW-CSR.                                       
                                                                                
           PERFORM 9900-EOJ-LOGIC.                                              
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *    LOGON TO ORACLE, AND DECLARE THE ORACLE CURSORS.            *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           INITIALIZE DPG51                                                     
                      DPG52                                                     
                      DPG53                                                     
                      DPG54                                                     
                      DPG55                                                     
                      DPG56.                                                    
                                                                                
208057*                                                                         
208057*  GET CURRENT JOB NAME                                                   
208057     CALL DP085-GET-CURR-JOB-INFO-PGM                                     
208057         USING DP085-GET-CURR-JOB-INFO-PARMS.                             
208057     MOVE DP085-JOB-NAME     TO PV-JOBNAME.                               
208057                                                                          
208057     DISPLAY 'DP085 JOBNAME - ' PV-JOBNAME.                               
                                                                                
8613F      OPEN INPUT  ORACLE-LOGIN-FILE                                        
W30021                 WEEKLY-DATE-FILE                                         
                       EMAIL-TEMPLATE-FILE.                                     
                                                                                
W30021     PERFORM 7300-READ-WEEKLY-DATE-FILE.                                  
                                                                                
                                                                                
                                                                                
           PERFORM 7100-READ-EMAIL-TEMPLATE-FILE.                               
           IF PS-END-OF-EMAIL-FILE                                              
               DISPLAY ' *** NO E-MAIL BODY TO PROCESS ***'                     
               DISPLAY ' '                                                      
           ELSE                                                                 
               PERFORM 1100-LOGON-TO-ORACLE                                     
               PERFORM 1200-LOAD-EMAIL-TEXT                                     
                   UNTIL PS-END-OF-EMAIL-FILE                                   
                       OR PT-EMAIL-INDX > PT-MAX-EMAIL-LINE                     
               MOVE PT-EMAIL-INDX    TO PT-EMAIL-COUNT                          
           END-IF.                                                              
8613F ******************************************************************        
8613F * LOGONS TO ORACLE.                                              *        
8613F ******************************************************************        
8613F *    - READ THE CONTROL CARD TO GET THE CREDENTIALS AND LOGON    *        
8613F *    - ORACLE                                                    *        
8613F *----------------------------------------------------------------*        
8613F  1100-logon-to-oracle.                                                    
8613F      DISPLAY '**********************************'.                        
8613F      DISPLAY '**------ BEGIN DAKUP200 ------**'.                          
8613F      DISPLAY '**********************************'.                        
8613F                                                                           
8613F      MOVE '1001-CONNECT-TO-ORACLE'   TO PV-PARAGRAPH-NAME.                
8613F                                                                           
8613F      READ ORACLE-LOGIN-FILE INTO CC-ORACLE-CONNECTION                     
8613F         AT END                                                            
8613F            SET ORACLE-LOGIN-EOF      TO TRUE.                             
8613F                                                                           
8613F      IF CC-AUTO-LOGON                                                     
8613F         DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                 
8613F         EXEC SQL                                                          
8613F              CONNECT :PC-AUTO-LOGON                                       
8613F         END-EXEC                                                          
8613F      ELSE                                                                 
8613F         INITIALIZE PV-ORACLE-USERID-LEN                                   
8613F                    PV-ORACLE-PASSWORD-LEN                                 
8613F         INSPECT CC-ORACLE-USERID                                          
8613F                 TALLYING PV-ORACLE-USERID-LEN                             
8613F                 FOR CHARACTERS BEFORE INITIAL SPACE                       
8613F         INSPECT CC-ORACLE-PASSWORD                                        
8613F                 TALLYING PV-ORACLE-PASSWORD-LEN                           
8613F                 FOR CHARACTERS BEFORE INITIAL SPACE                       
8613F         MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN)     TO             
8613F                 USERNAME-ARR                                              
8613F         MOVE PV-ORACLE-USERID-LEN    TO USERNAME-LEN                      
8613F         MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO             
8613F                 PASSWD-ARR                                                
8613F         MOVE PV-ORACLE-PASSWORD-LEN  TO PASSWD-LEN                        
8613F         MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO             
8613F              PASSWD-ARR                                                   
8613F         MOVE PV-ORACLE-PASSWORD-LEN  TO PASSWD-LEN                        
8613F         MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN)     TO             
8613F              USERNAME-ARR                                                 
8613F         MOVE PV-ORACLE-USERID-LEN    TO USERNAME-LEN                      
8613F         MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO             
8613F              PASSWD-ARR                                                   
8613F         MOVE PV-ORACLE-PASSWORD-LEN  TO PASSWD-LEN                        
8613F         DISPLAY PC-PROGRAM-NAME  ' - LOGON TO ORACLE AS USER '            
8613F              USERNAME-ARR                                                 
8613F         EXEC SQL                                                          
8613F              CONNECT :USERNAME IDENTIFIED BY :PASSWD                      
8613F         END-EXEC                                                          
8613F         INITIALIZE CC-ORACLE-PASSWORD                                     
8613F                    PASSWD                                                 
8613F      END-IF.                                                              
8613F                                                                           
8613F      IF (SQLCODE NOT = 0)                                                 
8613F          DISPLAY PV-PARAGRAPH-NAME                                        
8613F          PERFORM 9990-SQL-ERROR                                           
8613F      END-IF.                                                              
                                                                                
8613F *    EXEC SQL                                                             
8613F *        SELECT TO_CHAR(SYSDATE, 'YYYY-MM-DDHH24:MI:SS')                  
8613F *        INTO :PV-DTETIME                                                 
8613F *        FROM DUAL                                                        
8613F *    END-EXEC.                                                            
8613F *                                                                         
8613F *    DISPLAY 'PV-DTETIME '  PV-DTETIME.                                   
8613F *    MOVE PV-DTETIME     TO PV-TIMESTMP.                                  
8613F *    MOVE CC-CTL-DTE     TO PV-DATE.                                      
8613F *    DISPLAY 'PV-DATE '     PV-DATE.                                      
8613F *    DISPLAY 'PV-TIME '     PV-TIME.                                      
8613F *    MOVE PV-TIMESTMP    TO PV-DTETIME.                                   
8613F *    DISPLAY 'PV-DTETIME  ' PV-DTETIME.                                   
8613F *    DISPLAY '**********************************'.                        
                                                                                
       EJECT                                                                    
                                                                                
                                                                                
      ****************************************************************          
      * LOAD EMAIL TEXT                                              *          
      ****************************************************************          
       1200-LOAD-EMAIL-TEXT.                                                    
           ADD +1                   TO PT-EMAIL-INDX.                           
           IF PT-EMAIL-INDX > PT-MAX-EMAIL-LINE                                 
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM ' PC-PROGRAM-NAME ' **'                      
               DISPLAY '** INTERNAL E-MAIL WORKING STORAGE TABLE '              
                       'EXCEEDED ' PT-MAX-EMAIL-LINE ' LINES.'                  
               DISPLAY '** PLEASE INCREASE THE INTERNAL WS TABLE SIZE'          
               SET ABEND-4004-INTERNAL-ERROR   TO TRUE                          
               PERFORM 9999-ABEND                                               
           ELSE                                                                 
               MOVE EMAIL-TEMPLATE-RECORD                                       
                   TO PT-EMAIL-TEXT(PT-EMAIL-INDX)                              
           END-IF.                                                              
                                                                                
           PERFORM 7100-READ-EMAIL-TEMPLATE-FILE.                               
                                                                                
      ******************************************************************        
      *    2000-PROCESS-TDAALW                                         *        
      *      THIS IS PERFORMED ONLY VIA THE 0000-MAINLINE              *        
      *      PURPOSE: FETCH A ROW FROM THE DEBIT ALLOWANCE CURSOR,     *        
      *               BUILD E-MAIL BODY, AND WRITE OUT E-MAIL FOR      *        
      *               DELIVERY.                                        *        
      ******************************************************************        
       2000-PROCESS-TDAALW.                                                     
                                                                                
           DISPLAY ' PROCESSING DA-NBR: ' DAALW-DR-ALW-NBR                      
                   ' DEPT: ' DAALW-DEPT-NBR                                     
                   ' ENT-ID: ' DAALW-ENT-ID.                                    
                                                                                
           INITIALIZE DPG51                                                     
                      DPG52                                                     
                      DPG53                                                     
                      DPG54                                                     
                      DPG55                                                     
                      DPG56.                                                    
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                               
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
           MOVE DA104-OPN-FSCL-MN-DTE TO DPG52-DB2-ISO-DATE-FMT.                
           CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
           IF DPG54-NO-ERROR                                                    
979622         MOVE DPG55-ALPHA-MONTH                                           
                   TO PV-EFF-FSCL-MONTH-NAME                                    
               MOVE DPG56-LAST-FP-DB2-ISO-FMT                                   
                   TO PV-EFF-FSCL-MONTH-END-DTE                                 
           ELSE                                                                 
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM ' PC-PROGRAM-NAME ' **'                      
               DISPLAY '** CALENDAR CALL ROUTINE FAIL FOR '                     
                       'EFF-DTE OF ' DAALW-EFF-DTE                              
               SET ABEND-4009-INVALID-DATA     TO TRUE                          
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
           PERFORM 8400-GET-VENDOR.                                             
           PERFORM 8500-GET-GMA-DMA.                                            
                                                                                
           PERFORM 8550-GET-DMM-INFO.                                           
           PERFORM 8575-GET-BUYER-INFO.                                         
                                                                                
960618* DEFAULT BUYER INFO TO DA CREATOR FIRST, WHEN NO BUYER'S INFO            
960618* IS FOUND. IF NEITHER DA CREATOR OR BUYER INFO IS AVAILABLE,             
960618* WILL DEFAULT USER-ID TO MDA.                                            
           IF PV-BUYER-NAME = SPACES                                            
960618         IF DAALW-CRE-BY-USR-ID = SPACES                                  
960618             MOVE 'MDA'            TO PV-BUYER-USER-ID                    
960618             MOVE 'MDA'            TO PV-BUYER-NAME                       
960618         ELSE                                                             
960618             MOVE DAALW-CRE-BY-USR-ID  TO PV-BUYER-USER-ID                
960618             PERFORM 8775-GET-WORKER-NAME                                 
960618         END-IF                                                           
960618     ELSE                                                                 
960618         MOVE PV-BUYER-WORKER-NBR  TO WORKER-NBR                          
960618         PERFORM 8750-GET-USER-ID                                         
960618         IF WORKER-UID-NBR = SPACES                                       
960618             IF DAALW-CRE-BY-USR-ID = SPACES                              
960618                 MOVE 'MDA'        TO PV-BUYER-USER-ID                    
960618             ELSE                                                         
960618                 MOVE DAALW-CRE-BY-USR-ID  TO PV-BUYER-USER-ID            
960618             END-IF                                                       
960618         ELSE                                                             
960618             MOVE WORKER-UID-NBR   TO PV-BUYER-USER-ID                    
960618         END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 8800-GET-DA-TYPE-DESC.                                       
                                                                                
           INITIALIZE DP023-DYNALC-PARMS.                                       
           MOVE 'OUT01' TO DP023-DDNAME.                                        
           SET DP023-DSNAME-SYSOUT TO TRUE.                                     
           SET DP023-FREE-CLOSE-YES TO TRUE.                                    
           SET DP023-RECFM-FIXED-BLOCK TO TRUE.                                 
           MOVE 80     TO DP023-RECSZ.                                          
           MOVE 7      TO DP023-RECOUNT.                                        
           IF PV-PROD-JOBS                                                      
               MOVE 'B'    TO DP023-PRINT-CLASS                                 
           ELSE                                                                 
               MOVE 'X'    TO DP023-PRINT-CLASS                                 
           END-IF.                                                              
           MOVE 'SMTP' TO DP023-PRINT-WRITER.                                   
           PERFORM DP023-0000-DYNALC.                                           
           OPEN OUTPUT EMAIL-OUTPUT-FILE.                                       
                                                                                
           ADD +1                   TO PA-EMAIL-GENERATED.                      
           PERFORM 2200-BUILD-EMAIL                                             
               VARYING PT-EMAIL-INDX FROM 1 BY 1                                
               UNTIL PT-EMAIL-INDX > PT-EMAIL-COUNT                             
                                                                                
           CLOSE EMAIL-OUTPUT-FILE.                                             
                                                                                
W30021     ACCEPT PC-CURR-TIME FROM TIME.                                       
W30021     MOVE ZEROES TO PC-TIME-DIFF.                                         
W30021     PERFORM UNTIL PC-TIME-DIFF > 100                                     
W30021         ACCEPT PC-AFTR-TIME FROM TIME                                    
W30021         COMPUTE PC-TIME-DIFF = PC-CURR-TIME - PC-AFTR-TIME               
W30021     END-PERFORM.                                                         
W30021                                                                          
           PERFORM 8300-UPDATE-TDAALW.                                          
                                                                                
           PERFORM 8200-FETCH-TDAALW.                                           
                                                                                
      ******************************************************************        
      *  BUILD THE EMAIL FROM INTERNAL WS TABLE AND DA INFO FETCHED    *        
      ******************************************************************        
       2200-BUILD-EMAIL.                                                        
                                                                                
           MOVE PT-EMAIL-TEXT(PT-EMAIL-INDX)   TO PT-TEXT-LINE-1                
                                                  PT-TEXT-LINE-2.               
           MOVE PT-MAX-TEXT-2-CHAR      TO PT-TEXT-2-COUNT.                     
                                                                                
      * CHECK TO SEE IF THERE IS ANY %%SYMBOLIC TO RESOLVE                      
           MOVE ZEROES                TO PV-SYMBOLIC-FOUND.                     
           INSPECT PT-TEXT-LINE-1                                               
               TALLYING PV-SYMBOLIC-FOUND                                       
               FOR ALL '%%'.                                                    
      * FOUND AT LEAST ONE SYMBOLIC TO BE RESOLVED                              
           IF PV-SYMBOLIC-FOUND > ZEROES                                        
               MOVE ZEROES                TO PT-TEXT-1-INDX                     
                                             PT-TEXT-2-INDX                     
               MOVE SPACES                TO PT-TEXT-LINE-2                     
               PERFORM                                                          
                   UNTIL PT-TEXT-1-INDX >= PT-MAX-TEXT-1-CHAR                   
                       OR PT-TEXT-2-INDX >= PT-MAX-TEXT-2-CHAR                  
                   ADD +1                 TO PT-TEXT-1-INDX                     
                   IF PT-TEXT-LINE-1-CHAR(PT-TEXT-1-INDX) = '%'                 
                       PERFORM 3000-GET-SYMBOLIC-NAME                           
                       IF PT-KEY-WRD-1-COUNT > ZEROES                           
                           PERFORM 3100-RESOLVE-SYMBOL                          
                       ELSE                                                     
                           ADD +1                 TO PT-TEXT-2-INDX             
                           MOVE PT-TEXT-LINE-1-CHAR(PT-TEXT-1-INDX)             
                               TO PT-TEXT-LINE-2-CHAR(PT-TEXT-2-INDX)           
                       END-IF                                                   
                   ELSE                                                         
                       ADD +1                 TO PT-TEXT-2-INDX                 
                       MOVE PT-TEXT-LINE-1-CHAR(PT-TEXT-1-INDX)                 
                           TO PT-TEXT-LINE-2-CHAR(PT-TEXT-2-INDX)               
                   END-IF                                                       
               END-PERFORM                                                      
               MOVE PT-TEXT-2-INDX          TO PT-TEXT-2-COUNT                  
           END-IF.                                                              
                                                                                
           IF PT-TEXT-2-COUNT > ZEROES                                          
               MOVE PT-TEXT-LINE-2      TO EM-DATA-OUTPUT-LINE                  
               PERFORM 7200-WRITE-EMAIL-OUTPUT                                  
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  GET THE SYMBOLIC NAME OUT OF THE TEXT LINE                    *        
      *  LOOP THROUGH THE PROCESS UNTIL THE END OF LINE OR             *        
      *  THE CHARACTER IS ANYTHING OTHER THAN A-Z, 0-9, %, OR -        *        
      ******************************************************************        
       3000-GET-SYMBOLIC-NAME.                                                  
                                                                                
           MOVE SPACES                TO PT-KEY-WRD-LINE-1.                     
           MOVE PT-TEXT-1-INDX        TO PT-TEXT-1-SAVED.                       
           PERFORM                                                              
               VARYING PT-KEY-WRD-1-INDX FROM 1 BY 1                            
               UNTIL PT-KEY-WRD-1-INDX >= PT-MAX-KEY-WRD-1-CHAR                 
                   OR PT-TEXT-1-INDX >= PT-MAX-TEXT-1-CHAR                      
                   OR NOT (                                                     
                       (PT-TEXT-LINE-1-CHAR(PT-TEXT-1-INDX) >= 'A' AND          
                        PT-TEXT-LINE-1-CHAR(PT-TEXT-1-INDX) <= 'Z') OR          
                       (PT-TEXT-LINE-1-CHAR(PT-TEXT-1-INDX) >= '0' AND          
                        PT-TEXT-LINE-1-CHAR(PT-TEXT-1-INDX) <= '9') OR          
                       PT-TEXT-LINE-1-CHAR(PT-TEXT-1-INDX) = '%' OR             
                       PT-TEXT-LINE-1-CHAR(PT-TEXT-1-INDX) = '-'                
                          )                                                     
               MOVE PT-TEXT-LINE-1-CHAR(PT-TEXT-1-INDX)                         
                   TO PT-KEY-WRD-LINE-1-CHAR(PT-KEY-WRD-1-INDX)                 
               ADD +1                 TO PT-TEXT-1-INDX                         
           END-PERFORM.                                                         
           IF PT-KEY-WRD-1-INDX <= PT-MAX-KEY-WRD-1-CHAR                        
               MOVE PT-KEY-WRD-1-INDX     TO PT-KEY-WRD-1-COUNT                 
      * BACK 1 ON THE ORIGINAL TEXT LINE TO ACCOUNT FOR KEYWORD BREAK           
               SUBTRACT 1               FROM PT-TEXT-1-INDX                     
           ELSE                                                                 
               MOVE ZEROES                TO PT-KEY-WRD-1-COUNT                 
      * RESTORE THE ORIGINAL TEXT LINE POSITION FOR NO KEYWORD FOUND            
               MOVE PT-TEXT-1-SAVED       TO PT-TEXT-1-INDX                     
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  TAKE THE REQUEST SYMBOLIC NAME AND RESOLVE IT TO A VALUE      *        
      ******************************************************************        
       3100-RESOLVE-SYMBOL.                                                     
                                                                                
           MOVE '3100-RESOLVE-SYMBOL   ' TO PV-PARAGRAPH-NAME.                  
                                                                                
           MOVE SPACES                   TO PT-KEY-WRD-LINE-2.                  
           EVALUATE TRUE                                                        
      * RESOLVE E-MAIL ADDRESS                                                  
               WHEN PT-DA-EMAIL-ADDR-KEY                                        
                   MOVE DAALW-EMAIL-ADDR         TO PT-KEY-WRD-LINE-2           
                   SET PT-KEY-WRD-2-TRIM-BOTH    TO TRUE                        
                   PERFORM 3200-LOAD-SYMBOL-VALUE                               
      * RESOLVE VENDOR NAME                                                     
               WHEN PT-VENDOR-NAME-KEY                                          
                   MOVE ENTNME-ENT-NAME-DESC     TO PT-KEY-WRD-LINE-2           
                   SET PT-KEY-WRD-2-TRIM-BOTH    TO TRUE                        
                   PERFORM 3200-LOAD-SYMBOL-VALUE                               
      * RESOLVE DMM NAME                                                        
               WHEN PT-DMM-NAME-KEY                                             
                   MOVE PV-DMM-NAME              TO PT-KEY-WRD-LINE-2           
                   SET PT-KEY-WRD-2-TRIM-TRAIL   TO TRUE                        
                   PERFORM 3200-LOAD-SYMBOL-VALUE                               
      * RESOLVE BUYER NAME                                                      
               WHEN PT-BUYER-NAME-KEY                                           
                   MOVE PV-BUYER-NAME            TO PT-KEY-WRD-LINE-2           
                   SET PT-KEY-WRD-2-TRIM-TRAIL   TO TRUE                        
                   PERFORM 3200-LOAD-SYMBOL-VALUE                               
      * RESOLVE BUYER ID                                                        
               WHEN PT-BUYER-ID-KEY                                             
                   MOVE PV-BUYER-USER-ID         TO PT-KEY-WRD-LINE-2           
                   SET PT-KEY-WRD-2-TRIM-BOTH    TO TRUE                        
                   PERFORM 3200-LOAD-SYMBOL-VALUE                               
      * RESOLVE DEPT NUMBER                                                     
               WHEN PT-DA-DEPT-NBR-KEY                                          
W30021             MOVE SPACES               TO PT-KEY-WRD-LINE-2               
W30021             STRING 'D-'                                                  
W30021                    DELIMITED BY SIZE                                     
W30021                    DAALW-DEPT-NBR                                        
W30021                    DELIMITED BY SIZE                                     
W30021                 INTO PT-KEY-WRD-LINE-2                                   
                   SET PT-KEY-WRD-2-TRIM-TRAIL   TO TRUE                        
                   PERFORM 3200-LOAD-SYMBOL-VALUE                               
      * RESOLVE EFFECTIVE MONTH-END DATE WITH MONTH NAME                        
               WHEN PT-DA-EFF-DTE-KEY                                           
                   MOVE SPACES               TO PV-EFF-FSCL-DATE-TEXT           
                   STRING PV-EFF-FSCL-MONTH-NAME                                
                          DELIMITED BY ' '                                      
                          ' '                                                   
                          DELIMITED BY SIZE                                     
                          PV-EFF-FSCL-MONTH-END-DD                              
                          DELIMITED BY SIZE                                     
                          ', '                                                  
                          DELIMITED BY SIZE                                     
                          PV-EFF-FSCL-MONTH-END-YYYY                            
                          DELIMITED BY SIZE                                     
                       INTO PV-EFF-FSCL-DATE-TEXT                               
                   MOVE PV-EFF-FSCL-DATE-TEXT    TO PT-KEY-WRD-LINE-2           
                   SET PT-KEY-WRD-2-TRIM-TRAIL   TO TRUE                        
                   PERFORM 3200-LOAD-SYMBOL-VALUE                               
W30021* RESOLVE EFFECTIVE MONTH-END DATE WITHOUT MONTH NAME                     
W30021         WHEN PT-DA-DATE-KEY                                              
W30021             MOVE PV-EFF-FSCL-MONTH-END-DTE                               
W30021                 TO PT-KEY-WRD-LINE-2                                     
W30021             SET PT-KEY-WRD-2-TRIM-TRAIL   TO TRUE                        
W30021             PERFORM 3200-LOAD-SYMBOL-VALUE                               
      * RESOLVE DEBIT ALLOWANCE NUMBER                                          
               WHEN PT-DA-DR-ALW-NBR-KEY                                        
                   MOVE SPACES TO PT-KEY-WRD-LINE-2                             
                   STRING 'DA-'                                                 
                          DELIMITED BY SIZE                                     
                          DAALW-DR-ALW-NBR                                      
                          DELIMITED BY SIZE                                     
                       INTO PT-KEY-WRD-LINE-2                                   
                   SET PT-KEY-WRD-2-TRIM-TRAIL   TO TRUE                        
                   PERFORM 3200-LOAD-SYMBOL-VALUE                               
      * RESOLVE DEBIT ALLOWANCE NET COST                                        
               WHEN PT-DA-NET-CST-AMT-KEY                                       
                   MOVE DAALW-NET-CST-AMT        TO PV-NET-CST-AMT              
                   MOVE PV-NET-CST-AMT-X         TO PT-KEY-WRD-LINE-2           
                   SET PT-KEY-WRD-2-TRIM-BOTH    TO TRUE                        
                   PERFORM 3200-LOAD-SYMBOL-VALUE                               
      * RESOLVE DEBIT ALLOWANCE TYPE DESCRIPTION                                
               WHEN PT-DA-TYPE-DESC-KEY                                         
                   MOVE DACDE-CDE-DESC           TO PT-KEY-WRD-LINE-2           
                   SET PT-KEY-WRD-2-TRIM-BOTH    TO TRUE                        
                   PERFORM 3200-LOAD-SYMBOL-VALUE                               
W30021* RESOLVE AUTHORIZATION NUMBER                                            
W30021         WHEN PT-DA-AUTH-NUMBER                                           
W30021             MOVE SPACES TO PT-KEY-WRD-LINE-2                             
W30021             PERFORM 8900-GET-AUTH-NUMBER                                 
W30021             IF DANOTE-NOTE-TXT NOT EQUAL SPACES                          
W30021                MOVE ZEROES TO PV-TALLY                                   
W30021                               PV-LENGTH                                  
W30021                INSPECT FUNCTION REVERSE(DANOTE-NOTE-TXT)                 
W30021                    TALLYING PV-TALLY FOR LEADING SPACES                  
W30021                COMPUTE PV-LENGTH                                         
W30021                    = LENGTH OF DANOTE-NOTE-TXT - PV-TALLY                
W30021                STRING 'Credit Auth. Number: ' DELIMITED BY SIZE          
W30021                       DANOTE-NOTE-TXT(1:PV-LENGTH)                       
W30021                       '.'                     DELIMITED BY SIZE          
W30021                       INTO PT-KEY-WRD-LINE-2                             
W30021                END-STRING                                                
W30021                SET PT-KEY-WRD-2-TRIM-BOTH TO TRUE                        
W30021                PERFORM 3200-LOAD-SYMBOL-VALUE                            
W30021             END-IF                                                       
      * RESOLVE UNKNOWN KEYWORDS BY LEAVING THE KEYWORDS ALONE                  
               WHEN OTHER                                                       
                   MOVE PT-KEY-WRD-LINE-1        TO PT-KEY-WRD-LINE-2           
                   SET PT-KEY-WRD-2-TRIM-TRAIL   TO TRUE                        
                   PERFORM 3200-LOAD-SYMBOL-VALUE                               
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  LOAD/MOVE THE SYMBOLIC VALUE TO THE TEXT BEING BUILT          *        
      ******************************************************************        
       3200-LOAD-SYMBOL-VALUE.                                                  
                                                                                
      * FIND THE LAST NON-SPACE CHARACTER                                       
           MOVE PT-MAX-KEY-WRD-2-CHAR     TO PT-KEY-WRD-2-COUNT.                
           IF PT-KEY-WRD-2-TRIM-TRAIL                                           
               OR PT-KEY-WRD-2-TRIM-BOTH                                        
               PERFORM                                                          
                   UNTIL PT-KEY-WRD-2-COUNT < 1                                 
                       OR PT-KEY-WRD-LINE-2-CHAR(PT-KEY-WRD-2-COUNT)            
                           NOT = SPACE                                          
                   SUBTRACT 1                 FROM PT-KEY-WRD-2-COUNT           
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
      * FIND THE FIRST NON-SPACE CHARACTER                                      
           MOVE +1                        TO PT-KEY-WRD-2-INDX.                 
           IF PT-KEY-WRD-2-TRIM-LEAD                                            
               OR PT-KEY-WRD-2-TRIM-BOTH                                        
               PERFORM                                                          
                   UNTIL PT-KEY-WRD-2-INDX > PT-KEY-WRD-2-COUNT                 
                       OR PT-KEY-WRD-2-INDX >= PT-MAX-KEY-WRD-2-CHAR            
                       OR PT-KEY-WRD-LINE-2-CHAR(PT-KEY-WRD-2-INDX)             
                           NOT = SPACE                                          
                   ADD +1                       TO PT-KEY-WRD-2-INDX            
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
      * COPY/MOVE VALUE TO OUTPUT/BUILT RECORD                                  
           PERFORM                                                              
               UNTIL PT-KEY-WRD-2-INDX > PT-KEY-WRD-2-COUNT                     
                   OR PT-KEY-WRD-2-INDX >= PT-MAX-KEY-WRD-2-CHAR                
                   OR PT-TEXT-2-INDX >= PT-MAX-TEXT-2-CHAR                      
                                                                                
               ADD +1                       TO PT-TEXT-2-INDX                   
               MOVE PT-KEY-WRD-LINE-2-CHAR(PT-KEY-WRD-2-INDX)                   
                   TO PT-TEXT-LINE-2-CHAR(PT-TEXT-2-INDX)                       
                                                                                
      * WRITE OUT THE BUILT TEXT LINE AND START A NEW ONE                       
      * BECAUSE THE RESULTING OUTPUT RECORD IS MORE THAN MAX LENGTH             
               IF PT-TEXT-2-INDX >= PT-MAX-TEXT-2-CHAR                          
                   MOVE PT-TEXT-LINE-2      TO EM-DATA-OUTPUT-LINE              
                   PERFORM 7200-WRITE-EMAIL-OUTPUT                              
                   MOVE ZEROES              TO PT-TEXT-2-INDX                   
                   MOVE SPACES              TO PT-TEXT-LINE-2                   
               END-IF                                                           
                                                                                
               ADD +1                       TO PT-KEY-WRD-2-INDX                
           END-PERFORM.                                                         
           MOVE PT-TEXT-2-INDX          TO PT-TEXT-2-COUNT.                     
                                                                                
                                                                                
      ******************************************************************        
      *  READ E-MAIL TEMPLATE FILE                                     *        
      ******************************************************************        
       7100-READ-EMAIL-TEMPLATE-FILE.                                           
                                                                                
           READ EMAIL-TEMPLATE-FILE                                             
               AT END SET PS-END-OF-EMAIL-FILE TO TRUE.                         
                                                                                
      ******************************************************************        
      *  WRITE THE OUTPUT OF THE EMAIL                                 *        
      ******************************************************************        
       7200-WRITE-EMAIL-OUTPUT.                                                 
                                                                                
           ADD +1                    TO PA-EMAIL-RECORDS-WRITE.                 
                                                                                
           WRITE EMAIL-OUTPUT-RECORD FROM EM-DATA-OUTPUT-LINE.                  
                                                                                
W30021******************************************************************        
W30021*  READ WEEKLY DATE FILE                                         *        
W30021******************************************************************        
W30021 7300-READ-WEEKLY-DATE-FILE.                                              
W30021                                                                          
W30021     READ WEEKLY-DATE-FILE INTO DARD104-RECORD.                           
W30021                                                                          
      ******************************************************************        
      *  OPEN DEBIT ALLOWANCE CURSOR                                   *        
      ******************************************************************        
       8000-OPEN-TDAALW-CSR.                                                    
                                                                                
           MOVE '8000-OPEN-TDAALW-CSR  ' TO PV-PARAGRAPH-NAME.                  
                                                                                
           EXEC SQL                                                             
               OPEN TDAALW_INFO                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   SET PS-NOT-END-OF-TDAALW    TO TRUE                          
               WHEN SQLCODE = 1403                                              
                   SET PS-END-OF-TDAALW        TO TRUE                          
               WHEN OTHER                                                       
                   PERFORM 9990-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  CLOSE DEBIT ALLOWANCE CURSOR                                  *        
      ******************************************************************        
       8100-CLOSE-TDAALW-CSR.                                                   
                                                                                
           MOVE '8100-CLOSE-TDAALW-CSR ' TO PV-PARAGRAPH-NAME.                  
                                                                                
           EXEC SQL                                                             
               CLOSE TDAALW_INFO                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9990-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  FETCH THE NEXT ROW                                            *        
      ******************************************************************        
       8200-FETCH-TDAALW.                                                       
                                                                                
           MOVE '8200-FETCH-TDAALW   ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           EXEC SQL                                                             
               FETCH TDAALW_INFO                                                
               INTO                                                             
                    :DAALW-DR-ALW-NBR                                           
                   ,:DAALW-TYP-CDE                                              
                   ,:DAALW-DEPT-NBR                                             
                   ,:DAALW-ENT-ID                                               
                   ,:DAALW-EFF-DTE                                              
                   ,:DAALW-NET-CST-AMT                                          
                   ,:DAALW-EMAIL-ADDR         :PV-NULL-RETURN                   
960618             ,:DAALW-CRE-BY-USR-ID                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   ADD +1                      TO PA-ROWS-FETCHED               
               WHEN SQLCODE = 1403                                              
                   SET PS-END-OF-TDAALW        TO TRUE                          
               WHEN OTHER                                                       
                   PERFORM 9990-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  UPDATE EXISTING ROW                                           *        
      ******************************************************************        
       8300-UPDATE-TDAALW.                                                      
                                                                                
           MOVE '8300-UPDATE-TDAALW  ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           EXEC SQL                                                             
               UPDATE                                                           
                       TDAALW                                                   
                  SET                                                           
                      EMAIL_ADDR_COM_STAT_CDE   = 'S'                           
                 WHERE                                                          
                       TRIM(DR_ALW_NBR)  = TRIM(:DAALW-DR-ALW-NBR)              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   ADD +1                        TO PA-ROWS-UPDATED             
                   PERFORM 9000-COMMIT-CHANGES                                  
               WHEN OTHER                                                       
                   DISPLAY '** DA NUMBER:   ' DAALW-DR-ALW-NBR                  
                   PERFORM 9990-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  GET VENDOR INFORMATION                                        *        
      ******************************************************************        
       8400-GET-VENDOR.                                                         
                                                                                
           MOVE '8400-GET-VENDOR     ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                       ENT_NAME_DESC                                            
                 INTO                                                           
                      :ENTNME-ENT-NAME-DESC                                     
                 FROM                                                           
                       TENTNME                                                  
                 WHERE                                                          
                       ENT_ID = :DAALW-ENT-ID                                   
                   AND TYPE_CDE = '01'                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY '  *** COULD NOT FIND VENDOR INFO WITH '             
                           DAALW-ENT-ID                                         
                           ' AS ENT-ID'                                         
                   PERFORM 9995-GET-SQL-MSG                                     
                   IF WS-MSG-LENGTH > ZEROES                                    
                       DISPLAY '   *** SQL CODE: ' SQLCODE                      
                               ' MESSAGE: ' WS-MSG-TEXT(1:WS-MSG-LENGTH)        
                   END-IF                                                       
                   MOVE SPACES             TO ENTNME-ENT-NAME-DESC              
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  GET GMA AND DMA NUMBERS                                       *        
      ******************************************************************        
       8500-GET-GMA-DMA.                                                        
                                                                                
           MOVE '8500-GET-GMA-DMA    ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                       GMA_NBR                                                  
                      ,DMA_NBR                                                  
                 INTO                                                           
                      :MDSEAR-GMA-NBR                                           
                     ,:MDSEAR-DMA-NBR                                           
                 FROM                                                           
                       TMDSEAR A                                                
                 WHERE                                                          
                       A.DEPT_NBR   = :DAALW-DEPT-NBR                           
                   AND A.MDSELV_CDE = 'DPT'                                     
                   AND TO_CHAR(A.STRT_DTE,'YYYY-MM-DD')                         
                       <= :PV-EFF-FSCL-MONTH-END-DTE                            
                   AND (TO_CHAR(A.END_DTE,'YYYY-MM-DD')                         
                       >= :PV-EFF-FSCL-MONTH-END-DTE                            
                       OR A.END_DTE IS NULL)                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY '  *** COULD NOT FIND GMA/DMA FOR DEPT '             
                           DAALW-DEPT-NBR                                       
                           ' WITH DATE '                                        
                           PV-EFF-FSCL-MONTH-END-DTE                            
                   PERFORM 9995-GET-SQL-MSG                                     
                   IF WS-MSG-LENGTH > ZEROES                                    
                       DISPLAY '   *** SQL CODE: ' SQLCODE                      
                               ' MESSAGE: ' WS-MSG-TEXT(1:WS-MSG-LENGTH)        
                   END-IF                                                       
                   MOVE '??'              TO MDSEAR-GMA-NBR                     
                                             MDSEAR-DMA-NBR                     
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  GET DMM'S NAME AND USER-ID                                    *        
      ******************************************************************        
       8550-GET-DMM-INFO.                                                       
                                                                                
           MOVE '8550-GET-DMM-INFO   ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           MOVE 'DMA'               TO MDSEAR-MDSELV-CDE.                       
           PERFORM 8600-GET-MDSEAR.                                             
           MOVE 'DMM'               TO RSPBAR-RSPLVL-CDE.                       
           PERFORM 8700-GET-RSPBAR.                                             
           IF RSPBAR-WORKER-NBR = ZEROES                                        
               MOVE SPACES                 TO PV-DMM-NAME                       
               MOVE ZEROES                 TO PV-DMM-WORKER-NBR                 
           ELSE                                                                 
               MOVE RSPBAR-WORKER-FIR-NAME TO PV-TEMP-FIRST-NAME                
               MOVE RSPBAR-WORKER-LAST-NAME TO PV-TEMP-LAST-NAME                
               MOVE RSPBAR-WORKER-NBR      TO PV-DMM-WORKER-NBR                 
               MOVE SPACES TO PV-DMM-NAME                                       
               STRING PV-TEMP-FIRST-NAME                                        
                   DELIMITED BY '  '                                            
                      ' '                                                       
                   DELIMITED BY SIZE                                            
                      PV-TEMP-LAST-NAME                                         
                   DELIMITED BY '  '                                            
                   INTO PV-DMM-NAME                                             
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  GET BUYER'S NAME AND USER-ID                                  *        
      ******************************************************************        
       8575-GET-BUYER-INFO.                                                     
                                                                                
           MOVE '8575-GET-BUYER-INFO ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           MOVE 'DPT'               TO MDSEAR-MDSELV-CDE.                       
           PERFORM 8600-GET-MDSEAR.                                             
           MOVE 'BYR'               TO RSPBAR-RSPLVL-CDE.                       
           PERFORM 8700-GET-RSPBAR.                                             
           IF RSPBAR-WORKER-NBR = ZEROES                                        
               MOVE SPACES                 TO PV-BUYER-NAME                     
               MOVE ZEROES                 TO PV-BUYER-WORKER-NBR               
           ELSE                                                                 
               MOVE RSPBAR-WORKER-FIR-NAME TO PV-TEMP-FIRST-NAME                
               MOVE RSPBAR-WORKER-LAST-NAME TO PV-TEMP-LAST-NAME                
               MOVE RSPBAR-WORKER-NBR      TO PV-BUYER-WORKER-NBR               
               MOVE SPACES TO PV-BUYER-NAME                                     
               STRING PV-TEMP-FIRST-NAME                                        
                   DELIMITED BY '  '                                            
                      ' '                                                       
                   DELIMITED BY SIZE                                            
                      PV-TEMP-LAST-NAME                                         
                   DELIMITED BY '  '                                            
                   INTO PV-BUYER-NAME                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  GET MDSEAR KEY                                                *        
      ******************************************************************        
       8600-GET-MDSEAR.                                                         
                                                                                
           MOVE '8600-GET-MDSEAR     ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                       MDSEAR_DKEY                                              
                 INTO                                                           
                      :MDSEAR-DKEY                                              
                 FROM                                                           
                       TMDSEAR A                                                
                 WHERE                                                          
                       ((A.GMA_NBR    = :MDSEAR-GMA-NBR AND                     
                         :MDSEAR-MDSELV-CDE = 'GMA') OR                         
                        (A.DMA_NBR    = :MDSEAR-DMA-NBR AND                     
                         :MDSEAR-MDSELV-CDE = 'DMA') OR                         
                        (A.DEPT_NBR   = :DAALW-DEPT-NBR AND                     
                         :MDSEAR-MDSELV-CDE = 'DPT'))                           
                   AND A.MDSELV_CDE = :MDSEAR-MDSELV-CDE                        
                   AND TO_CHAR(A.STRT_DTE,'YYYY-MM-DD')                         
                       <= :PV-EFF-FSCL-MONTH-END-DTE                            
                   AND (TO_CHAR(A.END_DTE,'YYYY-MM-DD')                         
                       >= :PV-EFF-FSCL-MONTH-END-DTE                            
                       OR A.END_DTE IS NULL)                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   IF MDSEAR-MDSELV-CDE = 'DMA'                                 
                       DISPLAY '  *** COULD NOT FIND DMA '                      
                               MDSEAR-DMA-NBR                                   
                               ' FOR DATE '                                     
                               PV-EFF-FSCL-MONTH-END-DTE                        
                   ELSE                                                         
                       DISPLAY '  *** COULD NOT FIND DEPT '                     
                               DAALW-DEPT-NBR                                   
                               ' FOR DATE '                                     
                               PV-EFF-FSCL-MONTH-END-DTE                        
                   END-IF                                                       
                   PERFORM 9995-GET-SQL-MSG                                     
                   IF WS-MSG-LENGTH > ZEROES                                    
                       DISPLAY '   *** SQL CODE: ' SQLCODE                      
                               ' MESSAGE: ' WS-MSG-TEXT(1:WS-MSG-LENGTH)        
                   END-IF                                                       
                   MOVE ZEROES            TO MDSEAR-DKEY                        
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  GET RESPONSIBILITY PERSON'S NAME                              *        
      ******************************************************************        
       8700-GET-RSPBAR.                                                         
                                                                                
           MOVE '8700-GET-RSPBAR     ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           IF MDSEAR-DKEY = ZEROES                                              
               MOVE SPACES            TO RSPBAR-WORKER-FIR-NAME                 
                                         RSPBAR-WORKER-LAST-NAME                
               MOVE ZEROES            TO RSPBAR-WORKER-NBR                      
           ELSE                                                                 
               EXEC SQL                                                         
                   SELECT                                                       
                           B.WORKER_FIR_NAME                                    
                          ,B.WORKER_LAST_NAME                                   
                          ,B.WORKER_NBR                                         
                     INTO                                                       
                          :RSPBAR-WORKER-FIR-NAME                               
                         ,:RSPBAR-WORKER-LAST-NAME                              
                         ,:RSPBAR-WORKER-NBR                                    
                     FROM                                                       
                           TRSPBAR B                                            
                     WHERE                                                      
                           B.MDSEAR_DKEY = :MDSEAR-DKEY                         
                       AND B.RSPLVL_CDE = :RSPBAR-RSPLVL-CDE                    
                       AND TO_CHAR(B.WORKER_STRT_DTE,'YYYY-MM-DD')              
                           <= :PV-EFF-FSCL-MONTH-END-DTE                        
                       AND (TO_CHAR(B.WORKER_END_DTE,'YYYY-MM-DD')              
                           >= :PV-EFF-FSCL-MONTH-END-DTE                        
                           OR B.WORKER_END_DTE IS NULL)                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       DISPLAY '  *** COULD NOT FIND '                          
                               RSPBAR-RSPLVL-CDE                                
                               ' NAME WITH DKEY '                               
                               MDSEAR-DKEY                                      
                               ' FOR DATE '                                     
                               PV-EFF-FSCL-MONTH-END-DTE                        
                       PERFORM 9995-GET-SQL-MSG                                 
                       IF WS-MSG-LENGTH > ZEROES                                
                           DISPLAY '   *** SQL CODE: ' SQLCODE                  
                                   ' MESSAGE: '                                 
                                   WS-MSG-TEXT(1:WS-MSG-LENGTH)                 
                       END-IF                                                   
                       MOVE SPACES            TO RSPBAR-WORKER-FIR-NAME         
                                                 RSPBAR-WORKER-LAST-NAME        
                       MOVE ZEROES            TO RSPBAR-WORKER-NBR              
               END-EVALUATE                                                     
           END-IF.                                                              
                                                                                
      * CHECK TO SEE IF THE FIRST NAME COTAINED NOT ASSIGNED LITERAL            
           MOVE ZEROES                TO PV-SYMBOLIC-FOUND.                     
           INSPECT RSPBAR-WORKER-FIR-NAME                                       
               TALLYING PV-SYMBOLIC-FOUND                                       
               FOR ALL 'OPEN' 'MISCELLANEOUS'.                                  
      * THERE IS AT LEAST ONE OCCURRENCE                                        
           IF PV-SYMBOLIC-FOUND > ZEROES                                        
               DISPLAY '  *** FOUND MISC/OPEN IN FIRST-NAME: '                  
                       RSPBAR-WORKER-FIR-NAME                                   
               MOVE SPACES            TO RSPBAR-WORKER-FIR-NAME                 
                                         RSPBAR-WORKER-LAST-NAME                
               MOVE ZEROES            TO RSPBAR-WORKER-NBR                      
           END-IF.                                                              
                                                                                
      * CHECK TO SEE IF THE LAST NAME COTAINED NOT ASSIGNED LITERAL             
           MOVE ZEROES                TO PV-SYMBOLIC-FOUND.                     
           INSPECT RSPBAR-WORKER-LAST-NAME                                      
               TALLYING PV-SYMBOLIC-FOUND                                       
               FOR ALL 'OPEN' 'MISCELLANEOUS'.                                  
      * THERE IS AT LEAST ONE OCCURRENCE                                        
           IF PV-SYMBOLIC-FOUND > ZEROES                                        
               DISPLAY '  *** FOUND MISC/OPEN IN LAST-NAME: '                   
                       RSPBAR-WORKER-LAST-NAME                                  
               MOVE SPACES            TO RSPBAR-WORKER-FIR-NAME                 
                                         RSPBAR-WORKER-LAST-NAME                
               MOVE ZEROES            TO RSPBAR-WORKER-NBR                      
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  GET RESPONSIBILITY PERSON'S USER-ID                           *        
      ******************************************************************        
       8750-GET-USER-ID.                                                        
                                                                                
           MOVE '8750-GET-USER-ID    ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           IF RSPBAR-WORKER-NBR = ZEROES                                        
               MOVE SPACES            TO WORKER-UID-NBR                         
           ELSE                                                                 
               EXEC SQL                                                         
                   SELECT                                                       
                           UID_NBR                                              
                     INTO                                                       
                          :WORKER-UID-NBR                                       
                     FROM                                                       
                           TWORKER                                              
                     WHERE                                                      
                           WORKER_NBR    = :WORKER-NBR                          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       DISPLAY '  *** COULD NOT FIND USER-ID FOR'               
                               ' WORKER ' WORKER-NBR                            
                       PERFORM 9995-GET-SQL-MSG                                 
                       IF WS-MSG-LENGTH > ZEROES                                
                           DISPLAY '   *** SQL CODE: ' SQLCODE                  
                                   ' MESSAGE: '                                 
                                   WS-MSG-TEXT(1:WS-MSG-LENGTH)                 
                       END-IF                                                   
                       MOVE SPACES            TO WORKER-UID-NBR                 
               END-EVALUATE                                                     
           END-IF.                                                              
                                                                                
960618******************************************************************        
960618*  GET RESPONSIBILITY PERSON'S NAME WHEN NO BUYER INFO AVAILABLE *        
960618******************************************************************        
960618 8775-GET-WORKER-NAME.                                                    
960618     MOVE '8775-GET-WORKER-NAME' TO PV-PARAGRAPH-NAME.                    
960618     EXEC SQL                                                             
960618         SELECT  LAST_NAME                                                
960618                ,FIR_NAME                                                 
960618           INTO  :WORKER-LAST-NAME                                        
960618                ,:WORKER-FIR-NAME                                         
960618           FROM  TWORKER                                                  
960618           WHERE UID_NBR = :PV-BUYER-USER-ID                              
960618     END-EXEC.                                                            
960618                                                                          
960618     EVALUATE TRUE                                                        
960618         WHEN SQLCODE = 0                                                 
960618             CONTINUE                                                     
960618         WHEN OTHER                                                       
960618             DISPLAY '  *** COULD NOT FIND NAME FOR'                      
960618                     ' USER ID ' PV-BUYER-USER-ID                         
960618             PERFORM 9995-GET-SQL-MSG                                     
960618             IF WS-MSG-LENGTH > ZEROES                                    
960618                 DISPLAY '   *** SQL CODE: ' SQLCODE                      
960618                         ' MESSAGE: '                                     
960618                         WS-MSG-TEXT(1:WS-MSG-LENGTH)                     
960618             END-IF                                                       
960618     END-EVALUATE.                                                        
960618                                                                          
960618* CHECK TO SEE IF THE FIRST NAME COTAINED NOT ASSIGNED LITERAL            
960618     MOVE ZEROES                TO PV-SYMBOLIC-FOUND.                     
960618     INSPECT WORKER-FIR-NAME                                              
960618         TALLYING PV-SYMBOLIC-FOUND                                       
960618         FOR ALL 'OPEN' 'MISCELLANEOUS'.                                  
960618* THERE IS AT LEAST ONE OCCURRENCE                                        
960618     IF PV-SYMBOLIC-FOUND > ZEROES                                        
960618         DISPLAY '  *** FOUND MISC/OPEN IN FIRST-NAME: '                  
960618                 WORKER-FIR-NAME                                          
960618         MOVE SPACES            TO WORKER-FIR-NAME                        
960618                                   WORKER-LAST-NAME                       
960618     END-IF.                                                              
960618                                                                          
960618* CHECK TO SEE IF THE LAST NAME COTAINED NOT ASSIGNED LITERAL             
960618     MOVE ZEROES                TO PV-SYMBOLIC-FOUND.                     
960618     INSPECT WORKER-LAST-NAME                                             
960618         TALLYING PV-SYMBOLIC-FOUND                                       
960618         FOR ALL 'OPEN' 'MISCELLANEOUS'.                                  
960618* THERE IS AT LEAST ONE OCCURRENCE                                        
960618     IF PV-SYMBOLIC-FOUND > ZEROES                                        
960618         DISPLAY '  *** FOUND MISC/OPEN IN LAST-NAME: '                   
960618                 WORKER-LAST-NAME                                         
960618         MOVE SPACES            TO WORKER-FIR-NAME                        
960618                                   WORKER-LAST-NAME                       
960618     END-IF.                                                              
960618     IF WORKER-FIR-NAME = SPACES                                          
960618         OR WORKER-LAST-NAME = SPACES                                     
960618         MOVE SPACES TO PV-BUYER-NAME                                     
960618     ELSE                                                                 
960618         MOVE WORKER-FIR-NAME TO PV-TEMP-FIRST-NAME                       
960618         MOVE WORKER-LAST-NAME TO PV-TEMP-LAST-NAME                       
960618         MOVE SPACES TO PV-DMM-NAME                                       
960618         STRING PV-TEMP-FIRST-NAME                                        
960618             DELIMITED BY '  '                                            
960618                ' '                                                       
960618             DELIMITED BY SIZE                                            
960618                PV-TEMP-LAST-NAME                                         
960618             DELIMITED BY '  '                                            
960618             INTO PV-BUYER-NAME                                           
960618     END-IF.                                                              
      ******************************************************************        
      *  GET DEBIT ALLOWANCE TYPE DESCRIPTION                          *        
      ******************************************************************        
       8800-GET-DA-TYPE-DESC.                                                   
                                                                                
           MOVE '8800-GET-DA-TYPE-DESC' TO PV-PARAGRAPH-NAME.                   
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                       CDE_DESC                                                 
                 INTO                                                           
                      :DACDE-CDE-DESC                                           
                 FROM                                                           
                       TDACDE                                                   
                 WHERE                                                          
                       CDE_VAL_CDE = :DAALW-TYP-CDE                             
                   AND ACTV_IND = 'Y'                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY '  *** UNABLE TO DETERMINE DESC OF DA-TYPE '         
                           DAALW-TYP-CDE                                        
                   PERFORM 9995-GET-SQL-MSG                                     
                   IF WS-MSG-LENGTH > ZEROES                                    
                       DISPLAY '   *** SQL CODE: ' SQLCODE                      
                               ' MESSAGE: ' WS-MSG-TEXT(1:WS-MSG-LENGTH)        
                   END-IF                                                       
                   MOVE SPACES                TO DACDE-CDE-DESC                 
           END-EVALUATE.                                                        
                                                                                
W30021******************************************************************        
W30021*  GET AUTHORIZATION NUMBER FROM TDANOTE TABLE                   *        
W30021******************************************************************        
W30021 8900-GET-AUTH-NUMBER.                                                    
W30021     MOVE '8900-GET-AUTH-NUMBER' TO PV-PARAGRAPH-NAME.                    
W30021                                                                          
W30021     EXEC SQL                                                             
W30021         SELECT A.NOTE_TXT                                                
W30021         INTO   :DANOTE-NOTE-TXT                                          
W30021         FROM   TDANOTE A                                                 
W30021         WHERE TRIM(A.DR_ALW_NBR) = TRIM(:DAALW-DR-ALW-NBR)               
W30021           AND A.NOTE_TYP_CDE = 'V'                                       
W30021     END-EXEC.                                                            
W30021                                                                          
W30021     EVALUATE TRUE                                                        
W30021         WHEN SQLCODE = 0                                                 
W30021             CONTINUE                                                     
W30021         WHEN OTHER                                                       
W30021             DISPLAY '  *** UNABLE TO OBTAIN AUTH NUMBER'                 
W30021             PERFORM 9995-GET-SQL-MSG                                     
W30021             IF WS-MSG-LENGTH > ZEROES                                    
W30021                 DISPLAY '   *** SQL CODE: ' SQLCODE                      
W30021                         ' MESSAGE: ' WS-MSG-TEXT(1:WS-MSG-LENGTH)        
W30021             END-IF                                                       
W30021             MOVE SPACES TO DANOTE-NOTE-TXT                               
W30021     END-EVALUATE.                                                        
W30021                                                                          
      ******************************************************************        
      *    COMMIT-CHANGES.                                             *        
      *      PURPOSE: COMMIT CHANGES TO THUS FAR                       *        
      ******************************************************************        
       9000-COMMIT-CHANGES.                                                     
                                                                                
           ADD +1                        TO PA-ROWS-TO-COMMIT.                  
           IF PA-ROWS-TO-COMMIT >= PC-COMMIT-COUNT                              
               ADD +1                        TO PA-NUMBER-OF-COMMIT             
                                                                                
      * THE CURSOR WILL AUTOMATICALLY CLOSE ON A COMMIT ANYWAY                  
               PERFORM 8100-CLOSE-TDAALW-CSR                                    
                                                                                
               EXEC SQL                                                         
                   COMMIT                                                       
               END-EXEC                                                         
                                                                                
               MOVE ZEROES TO PA-ROWS-TO-COMMIT                                 
               MOVE PA-ROWS-FETCHED        TO PV-LAST-COMMIT-RECORD             
               MOVE DAALW-DR-ALW-NBR       TO PV-LAST-COMMIT-DR-ALW-NBR         
                                                                                
      * THE CURSOR HAS BEEN AUTOMATICALLY CLOSE ON A COMMIT, RE-OPEN            
               PERFORM 8000-OPEN-TDAALW-CSR                                     
               PERFORM 8200-FETCH-TDAALW                                        
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    9900-EOJ-LOGIC.                                             *        
      *      THIS IS PERFORMED AT END OF INPUT FILE.                   *        
      *      PURPOSE: CLOSE INPUT FILE, COMMIT CHANGES AND DISPLAY     *        
      *      RECORD COUNTS.                                            *        
      ******************************************************************        
       9900-EOJ-LOGIC.                                                          
      * FORCE A COMMIT, IF THERE ARE UNCOMMITTED ROWS                           
           IF PA-ROWS-TO-COMMIT > ZEROES                                        
               MOVE PC-COMMIT-COUNT  TO PA-ROWS-TO-COMMIT                       
               PERFORM 9000-COMMIT-CHANGES                                      
           END-IF.                                                              
                                                                                
W30021     CLOSE WEEKLY-DATE-FILE                                               
                 EMAIL-TEMPLATE-FILE.                                           
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'COMMIT EVERY ' PC-COMMIT-COUNT ' RECORD(S).'.               
           DISPLAY 'E-MAIL TEMPLATE HAS ' PT-EMAIL-COUNT ' LINES'.              
           DISPLAY 'E-MAIL GENERATED    ' PA-EMAIL-GENERATED.                   
           DISPLAY 'E-MAIL LINE WRITTEN ' PA-EMAIL-RECORDS-WRITE.               
           DISPLAY 'ROWS FETCHED        ' PA-ROWS-FETCHED.                      
           DISPLAY 'ROWS UPDATED        ' PA-ROWS-UPDATED.                      
           DISPLAY 'COMMITS             ' PA-NUMBER-OF-COMMIT.                  
           DISPLAY '**------ END ' PC-PROGRAM-NAME ' ------**'.                 
                                                                                
                                                                                
      ******************************************************************        
      * DISPLAYS ERRORS ENCOUNTERED ON SQL REQUEST                              
      ******************************************************************        
       9990-SQL-ERROR.                                                          
                                                                                
           MOVE SQLCODE    TO SQLCODE2.                                         
           MOVE SQLERRD(3) TO SQLERRD3.                                         
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-ATTEMPTED.            
           DISPLAY '** LAST COMMIT RECORD  = ' PV-LAST-COMMIT-RECORD.           
           DISPLAY '** LAST COMMIT DA-NBR  = ' PV-LAST-COMMIT-DR-ALW-NBR        
                                                                                
           PERFORM 9995-GET-SQL-MSG.                                            
           IF WS-MSG-LENGTH > ZEROES                                            
               DISPLAY ' ** MESSAGE TEXT      ** = '                            
                       WS-MSG-TEXT(1:WS-MSG-LENGTH)                             
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               ROLLBACK WORK RELEASE                                            
           END-EXEC.                                                            
                                                                                
           SET ABEND-4013-DB-ERROR  TO TRUE.                                    
                                                                                
           PERFORM 9999-ABEND.                                                  
                                                                                
      ******************************************************************        
      * GET ORACLE SQL ERROR MESSAGE                                            
      ******************************************************************        
       9995-GET-SQL-MSG.                                                        
                                                                                
           MOVE SPACES TO WS-MSG-TEXT.                                          
                                                                                
           CALL 'SQLGLM' USING WS-MSG-TEXT,                                     
                               WS-MAX-SIZE,                                     
                               WS-MSG-LENGTH.                                   
                                                                                
      ******************************************************************        
      * PROGRAM ABEND PARAGRAPH                                                 
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING PV-ABEND-CODE.                                
                                                                                
                                                                                
      *----------------------------------------------------------               
      *  DYNAMIC ALLOCATION ROUTINE                                             
      *----------------------------------------------------------               
      *DP023-0000-DYNALC.                                                       
           COPY DPPD023.                                                        
                                                                                
      ******************************************************************        
      *    END OF SOURCE CODE FOR DAKUP201                            *         
      ******************************************************************        
