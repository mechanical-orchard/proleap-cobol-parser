      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.     CWKUT024.                                                
       AUTHOR.         MARIBETH ZAMORA                                          
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   10/01/08.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  ITEM VENDOR STYLE EXTRACT PROGRAM                             *        
      *  THIS PROGRAM CREATES A VENDOR STYLE CHANGE EXTRACT FILE AND   *        
      *  BY COMPARING THE PREVIOUS AND CURRENT GENERATIONS OF THE      *        
      *  VENDOR STYLE FILES                                            *        
      *                                                                         
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT VS-PREVIOUS-FILE                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT VS-CURRENT-FILE                                               
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT VS-CHANGE-FILE                                                
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  VS-PREVIOUS-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  VS-PREVIOUS-RECORD                 PIC X(080).                       
                                                                                
       FD  VS-CURRENT-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  VS-CURRENT-RECORD                  PIC X(080).                       
                                                                                
       FD  VS-CHANGE-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  VS-CHANGE-RECORD                   PIC X(108).                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CURRENT-REC.                                                      
           05  WS-CURR-KEY-9                  PIC   9(8).                       
           05  WS-CURR-KEY-X  REDEFINES                                         
               WS-CURR-KEY-9                  PIC   X(8).                       
           05  WS-CURR-DATA                   PIC   X(100).                     
      *    05  WS-CURR-DATA                   PIC   X(99).                      
                                                                                
       01  WS-PREVIOUS-REC.                                                     
           05  WS-PREV-KEY-9                  PIC   9(8).                       
           05  WS-PREV-KEY-X  REDEFINES                                         
               WS-PREV-KEY-9                  PIC   X(8).                       
           05  WS-PREV-DATA                   PIC   X(100).                     
      *    05  WS-PREV-DATA                   PIC   X(99).                      
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-MAP-UNT-RTL-AMT             PIC S9(7)V99.                     
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08) VALUE 'CWKUT024'.            
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORDS          PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-PRE-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-CUR-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-OUTPUT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-INSERT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-UPDATE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-DELETE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-NO-CHANGE-RECORDS      PIC  9(9)       VALUE ZERO.            
                                                                                
      ***************************************************************           
      *   TEMP STORAGE AREA                                                     
      *   LENGTH = 105 BYTES                                                    
      ***************************************************************           
       01  WS-TEMP-REC.                                                         
           05  WS-TMP-RMS-STYL-ID            PIC   9(8).                        
           05  WS-TMP-DEPT-NBR               PIC   9(4).                        
           05  WS-TMP-MAJ-CL-NBR             PIC   9(4).                        
           05  WS-TMP-SUB-CL-NBR             PIC   9(4).                        
           05  WS-TMP-VS-ID                  PIC   9(10).                       
           05  WS-TMP-ENT-ID                 PIC   9(10).                       
           05  WS-TMP-VS-NBR                 PIC   X(20).                       
           05  WS-TMP-STKR-NBR               PIC   9(5).                        
           05  WS-TMP-VS-DESC                PIC   X(30).                       
      *    05  WS-TMP-MAP-UNT-RTL-AMT        PIC  S9(7)V99.                     
           05  WS-TMP-MAP-UNT-RTL-AMT        PIC  +9(7).99.                     
           05  WS-TMP-LBL-TYP-CDE            PIC   X(1).                        
                                                                                
      ******************************************************************        
      * INPUT FILE LAYOUT                                                       
      ******************************************************************        
           COPY CWRD027.                                                        
                                                                                
      ******************************************************************        
      * OUTPUT FILE LAYOUT FOR CW VENDOR STYLE EXTRACT FILE                     
      ******************************************************************        
           COPY CWRD004.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
      *  MAINLINE                                                               
      ******************************************************************        
       0000-MAINLINE.                                                           
           DISPLAY '0000-MAINLINE'.                                             
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-MAIN-ROUTINE                                            
               UNTIL WS-CURR-KEY-X = HIGH-VALUES                                
                     AND                                                        
                     WS-PREV-KEY-X = HIGH-VALUES.                               
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  INITIALIZE                                                             
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  VS-CURRENT-FILE                                          
                       VS-PREVIOUS-FILE.                                        
           OPEN OUTPUT VS-CHANGE-FILE.                                          
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
           PERFORM 8000-READ-CURRENT.                                           
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      * MAIN PROCESSING.                                                        
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
                                                                                
           INITIALIZE CW004-RECORD                                              
                                                                                
           IF WS-CURR-KEY-X  =  WS-PREV-KEY-X                                   
               PERFORM 2100-UPDATE-OR-NOCHANGE                                  
           ELSE                                                                 
               IF WS-CURR-KEY-X  <  WS-PREV-KEY-X                               
                   PERFORM 2200-INSERT-CURRENT                                  
               ELSE                                                             
                   PERFORM 2300-DELETE-PREVIOUS                                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  CHECKS RECORD FOR EQUALITY                                             
      *  IF DATA IS NOT EQUAL, THEN RECORD IS AN UPDATE                         
      ******************************************************************        
       2100-UPDATE-OR-NOCHANGE.                                                 
                                                                                
           IF WS-CURR-DATA  =  WS-PREV-DATA                                     
                                                                                
               ADD +1                 TO PA-NO-CHANGE-RECORDS                   
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
                                                                                
           ELSE                                                                 
                                                                                
               MOVE WS-CURR-KEY-9     TO CW004-KEY                              
               MOVE WS-CURR-DATA      TO CW004-DATA                             
               MOVE 'U'               TO CW004-CHANGE-IND                       
                                                                                
               ADD +1                 TO PA-UPDATE-RECORDS                      
                                                                                
               PERFORM 7100-WRITE-OUTPUT                                        
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  INSERT CURRENT DATA                                                    
      ******************************************************************        
       2200-INSERT-CURRENT.                                                     
                                                                                
           MOVE WS-CURR-KEY-9 TO CW004-KEY                                      
           MOVE WS-CURR-DATA  TO CW004-DATA                                     
           MOVE 'I'           TO CW004-CHANGE-IND                               
                                                                                
           ADD +1             TO PA-INSERT-RECORDS                              
                                                                                
           PERFORM 7100-WRITE-OUTPUT                                            
           PERFORM 8000-READ-CURRENT.                                           
                                                                                
                                                                                
      ******************************************************************        
      *  DELETE PREVIOUS DATA                                                   
      ******************************************************************        
       2300-DELETE-PREVIOUS.                                                    
                                                                                
           MOVE WS-PREV-KEY-9 TO CW004-KEY                                      
           MOVE WS-PREV-DATA  TO CW004-DATA                                     
           MOVE 'D'           TO CW004-CHANGE-IND                               
                                                                                
           ADD +1             TO PA-DELETE-RECORDS                              
                                                                                
           PERFORM 7100-WRITE-OUTPUT                                            
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      *  REFORMAT RMS DATA                                             *        
      ******************************************************************        
       3000-REFORMAT-RMS-DATA.                                                  
                                                                                
           INITIALIZE       WS-TEMP-REC.                                        
                                                                                
           MOVE CW027-RMS-STYL-ID       TO  WS-TMP-RMS-STYL-ID                  
           MOVE CW027-DEPT-NBR          TO  WS-TMP-DEPT-NBR                     
           MOVE CW027-MAJ-CL-NBR        TO  WS-TMP-MAJ-CL-NBR                   
           MOVE CW027-SUB-CL-NBR        TO  WS-TMP-SUB-CL-NBR                   
           MOVE CW027-VS-ID             TO  WS-TMP-VS-ID                        
           MOVE CW027-ENT-ID            TO  WS-TMP-ENT-ID                       
           MOVE CW027-VS-NBR            TO  WS-TMP-VS-NBR                       
           MOVE CW027-STKR-NBR          TO  WS-TMP-STKR-NBR                     
           MOVE CW027-VS-DESC           TO  WS-TMP-VS-DESC                      
                                                                                
           MOVE CW027-MAP-UNT-RTL-AMT   TO  WS-MAP-UNT-RTL-AMT                  
           MOVE WS-MAP-UNT-RTL-AMT      TO  WS-TMP-MAP-UNT-RTL-AMT              
                                                                                
           MOVE CW027-LBL-TYP-CDE       TO  WS-TMP-LBL-TYP-CDE.                 
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE THE OUTPUT RECORD                                                
      ******************************************************************        
       7100-WRITE-OUTPUT.                                                       
                                                                                
           WRITE  VS-CHANGE-RECORD FROM CW004-RECORD.                           
           ADD +1            TO PA-OUTPUT-RECORDS.                              
                                                                                
                                                                                
      ******************************************************************        
      *  READ THE CURRENT FILE.                                        *        
      ******************************************************************        
       8000-READ-CURRENT.                                                       
                                                                                
           READ VS-CURRENT-FILE INTO CW027-RECORD                               
               AT END                                                           
                   MOVE HIGH-VALUES  TO  WS-CURR-KEY-X                          
               NOT AT END                                                       
                   ADD 1             TO  PA-INPUT-RECORDS                       
                   ADD 1             TO  PA-INPUT-CUR-RECORDS                   
                   PERFORM 3000-REFORMAT-RMS-DATA                               
                   MOVE WS-TEMP-REC  TO  WS-CURRENT-REC                         
           END-READ.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *  READ THE PREVIOUS FILE.                                                
      ******************************************************************        
       8100-READ-PREVIOUS.                                                      
                                                                                
           READ VS-PREVIOUS-FILE INTO CW027-RECORD                              
               AT END                                                           
                   MOVE HIGH-VALUES  TO  WS-PREV-KEY-X                          
               NOT AT END                                                       
                   ADD 1             TO  PA-INPUT-RECORDS                       
                   ADD 1             TO  PA-INPUT-PRE-RECORDS                   
                   PERFORM 3000-REFORMAT-RMS-DATA                               
                   MOVE WS-TEMP-REC  TO  WS-PREVIOUS-REC                        
           END-READ.                                                            
                                                                                
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
           DISPLAY '9000-EOJ-ROUTINE'.                                          
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
           DISPLAY '   '.                                                       
                                                                                
           DISPLAY 'INPUT RECS READ    : ' PA-INPUT-RECORDS.                    
           DISPLAY 'INPUT PREVIOUS RECS: ' PA-INPUT-PRE-RECORDS.                
           DISPLAY 'INPUT CURRENT RECS : ' PA-INPUT-CUR-RECORDS.                
           DISPLAY '   '.                                                       
           DISPLAY 'OUTPUT RECS WRITTEN: ' PA-OUTPUT-RECORDS.                   
           DISPLAY 'INSERT RECS WRITTEN: ' PA-INSERT-RECORDS.                   
           DISPLAY 'UPDATE RECS WRITTEN: ' PA-UPDATE-RECORDS.                   
           DISPLAY 'DELETE RECS WRITTEN: ' PA-DELETE-RECORDS.                   
           DISPLAY 'NO CHANGE RECORDS  : ' PA-NO-CHANGE-RECORDS.                
                                                                                
           CLOSE VS-CURRENT-FILE                                                
                 VS-PREVIOUS-FILE                                               
                 VS-CHANGE-FILE.                                                
                                                                                
      ******************************************************************        
      ******************************************************************        
