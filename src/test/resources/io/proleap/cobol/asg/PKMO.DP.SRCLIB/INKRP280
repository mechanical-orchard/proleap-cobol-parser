       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         INKRP280.                                            
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       08/31/2004.                                          
       DATE-COMPILED.                                                           
      *                                                                         
      ******************************************************************        
      *     INKRP280 - ACTIVE SKU NOT INVENTORI TOP EXCEPTION REPORT.           
      *                (BY UNIT COUNT AND EXTENDED RETAIL)                      
      *                                                                         
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
W26600* W26600  08-29-2004 INVENTORY STORE EXPANSION CHANGES. ALSO              
W26600*                    MODIFY PROGRAM FOR EXCEPTION REPORTING               
W26600*                    RE-WRITE. REMOVE STORE-INV-FILE (INRD024).           
W26600*                    WR26600   SCOTT JACKSON                              
W27710* W27710  01-19-2005 CHANGE REPORT HEADING TO REFLECT CONTENTS            
W27710*                    WR27710   SCOTT JACKSON                              
W28545* W28545  08-01-2006 DEPARTMENT INVENTORY PROJECT                         
W28545*                    REMOVE TINVPAR, NOT REFERENCED                       
SR3403* SR3403  04-16-2012 LARGE RETAIL PROJECT                                 
SR3403*                    -ANUJ GUPTA                                          
      ******************************************************************        
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT STORE-CONTROL-FILE                                            
               ASSIGN TO INP01.                                                 
      *                                                                         
           SELECT STORE-MERGE-FILE                                              
               ASSIGN TO INP02.                                                 
      *                                                                         
           SELECT EXCEPT-CONTROL-FILE                                           
               ASSIGN TO INP03.                                                 
      *                                                                         
           SELECT STORE-INV-EXCEPTION-RPT                                       
               ASSIGN TO RPT01.                                                 
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD  STORE-CONTROL-FILE                                                   
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-CONTROL-RECORD.                                                
           05  FILLER                  PIC X(05).                               
           05  SCR-STORE-NMBR          PIC 9(04).                               
           05  SCR-REST-OF-RECORD      PIC X(71).                               
      *                                                                         
       FD  STORE-MERGE-FILE                                                     
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-MERGE-RECORD          PIC X(100).                              
      *                                                                         
       FD  EXCEPT-CONTROL-FILE                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  EXCEPT-CONTROL-RECORD.                                               
           05  ECR-EXCEPT-CONTROL      PIC 9(05).                               
           05  ECR-REST-OF-RECORD      PIC X(75).                               
      *                                                                         
       FD  STORE-INV-EXCEPTION-RPT                                              
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING  MODE IS F                                                 
           DATA RECORD IS REPORT-LINE.                                          
       01  REPORT-LINE         PIC X(132).                                      
      *                                                                         
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                                                                         
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-SUB                  PIC S9(4)    VALUE +0  COMP.             
           05  PV-PARAGRAPH-NAME       PIC X(30)    VALUE SPACES.               
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)    VALUE SPACES.               
W26600     05  PV-STORE-NUM            PIC 9(04)    VALUE ZERO.                 
W26600     05  PV-AREA-NUMBER          PIC 9(06)    VALUE ZERO.                 
      *    05  PV-ITEM-NMBR            PIC S9(15)V  VALUE +0  COMP-3.           
      *    05  PV-NUMERIC-STORE        PIC S9(9)    VALUE +0.                   
      *    05  PV-PHY-INV-OH-VAR       PIC S9(09) COMP VALUE ZERO.              
           05  PV-DEFAULT-CURRENT-DATE.                                         
               10  PV-DF-CC            PIC XX       VALUE '20'.                 
               10  PV-DF-YY            PIC XX.                                  
               10  FILLER              PIC X        VALUE '-'.                  
               10  PV-DF-MM            PIC XX.                                  
               10  FILLER              PIC X        VALUE '-'.                  
               10  PV-DF-DD            PIC XX.                                  
           05  PV-CURRENT-DATE.                                                 
               10  PV-CD-YY            PIC XX.                                  
               10  PV-CD-MM            PIC XX.                                  
               10  PV-CD-DD            PIC XX.                                  
           05  PV-TOTAL-INV-QTY        PIC S9(5)    COMP-3 VALUE 0.             
           05  PV-TOTAL-FS-QTY         PIC S9(5)    COMP-3 VALUE 0.             
           05  PV-VARIANCE-QTY         PIC S9(5)    COMP-3 VALUE 0.             
           05  PV-TOTAL-EXT-PRICE      PIC S9(13)V99 COMP-3 VALUE 0.            
           05  PV-STORE-EXCEPT-COUNT   PIC 9(05)    VALUE ZEROS.                
           05  PV-PAGE-COUNT           PIC S9(05) COMP-3 VALUE 0.               
           05  PV-LINE-COUNT           PIC S9(05) COMP-3 VALUE 0.               
           05  PV-ADVANCE-LINE         PIC S9(04) COMP   VALUE +1.              
           05  PV-DTL-LINE-PRT-ACCUM   PIC S9(13) COMP-3 VALUE 0.               
           05  PV-CURRENT-TIME.                                                 
               10  PV-CT-HH            PIC XX.                                  
               10  PV-CT-MM            PIC XX.                                  
           05  PV-LOW-RANGE            PIC S9(09) COMP-3 VALUE 0.               
           05  PV-HIGH-RANGE           PIC S9(09) COMP-3 VALUE 0.               
           05  PV-STORE-REC-COUNT      PIC S9(09) COMP-3 VALUE 0.               
      *    05  PV-STORE-COUNT          PIC S9(09) COMP-3 VALUE 0.               
           05  PV-EDIT-SKU             PIC  S9(8)V COMP-3.                      
      *                                                                         
       01  SA-SAVE-AREA.                                                        
           05  SA-STORE-NUMBER         PIC  9(04)    VALUE ZEROES.              
           05  SA-SAVE-STORE-NUMBER    PIC  9(04)    VALUE ZEROES.              
           05  SA-SKU                  PIC  9(08)    VALUE ZEROES.              
           05  SA-AREA-NUMBER          PIC  9(08)    VALUE ZEROES.              
           05  SA-SKU-ATTRIBUTES.                                               
               10  SA-DEPT-CLASS.                                               
                   15  SA-DEPT         PIC 9(04)    VALUE ZEROES.               
                   15  FILLER          PIC X(01)    VALUE '-'.                  
                   15  SA-CLASS        PIC 9(04)    VALUE ZEROES.               
                   15  FILLER          PIC X(01)    VALUE '-'.                  
               10  SA-SCL              PIC 9(04)    VALUE ZEROES.               
           05  SA-STORE-FOR-HEADER     PIC 9(04)    VALUE ZEROES.               
           05  SA-SKU-SAVE             PIC X(08)    VALUE SPACES.               
           05  SA-SKU-DESCRIP-SAVE     PIC X(25)    VALUE SPACES.               
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-INP01-DONE-SW            PIC X(01) VALUE 'N'.                 
28545          88  PS-STORE-CONTROL-AT-END-YES       VALUE 'Y'.                 
28545          88  PS-STORE-CONTROL-AT-END-NO        VALUE 'N'.                 
               88  STORE-CONTROL-DONE                VALUE 'Y'.                 
           05  PS-END-OF-INPUT-SW          PIC X(01) VALUE 'N'.                 
               88  PS-END-OF-INPUT                   VALUE 'Y'.                 
               88  PS-NOT-END-OF-INPUT               VALUE 'N'.                 
           05  END-OF-CURSOR-SW            PIC X(01) VALUE 'N'.                 
               88  END-OF-CURSOR                     VALUE 'Y'.                 
           05  PS-CLEARANCE-STAT-SW        PIC X(01) VALUE 'N'.                 
           05  PS-TUPCSKU-DONE-SW          PIC X(01) VALUE 'N'.                 
               88  NO-MORE-UPCS                      VALUE 'Y'.                 
               88  NOT-END-OF-UPCSKU-CUR             VALUE 'N'.                 
28545      05  PS-FOUND-STORE-MATCH-SW     PIC X(01) VALUE 'N'.                 
28545          88  PS-FOUND-STORE-MATCH-NO           VALUE 'N'.                 
28545          88  PS-FOUND-STORE-MATCH-YES          VALUE 'Y'.                 
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PRINT-LINE-MAX           PIC S9(04)      VALUE +57            
                               COMP.                                            
           05  PC-PROGRAM-NAME         PIC X(08)    VALUE 'INKRP280'.           
           05  PC-OPERCO-NBR           PIC X(02)    VALUE '03'.                 
      *                                                                         
       01  RP-REPORT-WORK-RECORD           PIC X(132)      VALUE SPACES.        
      *                                                                         
       01  HL1-HEADING-LINE-1.                                                  
           05  FILLER                  PIC X(35) VALUE SPACES.                  
           05  FILLER                  PIC X(23) VALUE                          
               'PHYSICAL INVENTORY TOP '.                                       
           05  HL1-EXCEPT-CONTROL      PIC Z,ZZZ9.                              
           05  FILLER                  PIC X(354) VALUE                         
W27710         ' ACTIVE SKUS NOT INVENTORIED REPORT'.                           
                                                                                
       01  HL2-HEADING-LINE-2.                                                  
           05  FILLER                  PIC X(08) VALUE ' STORE: '.              
           05  HL2-STORE-NUMBER        PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE ' - '.                   
           05  HL2-STORE-DESCRIPTION   PIC X(45) VALUE SPACES.                  
                                                                                
       01  HL3-HEADING-LINE-3.                                                  
           05  FILLER                      PIC X(14) VALUE SPACES.              
           05  FILLER                      PIC X(02) VALUE SPACES.              
           05  FILLER                      PIC X(25) VALUE SPACES.              
           05  FILLER                      PIC X(02) VALUE SPACES.              
           05  FILLER                      PIC X(15) VALUE SPACES.              
           05  FILLER                      PIC X(01) VALUE SPACES.              
           05  FILLER                      PIC X(06) VALUE SPACES.              
           05  FILLER                      PIC X(04) VALUE SPACES.              
SR3403     05  FILLER                      PIC X(19) VALUE                      
               '****INVENTORIED***'.                                            
SR3403     05  FILLER                      PIC X(03) VALUE SPACES.              
SR3403     05  FILLER                      PIC X(19) VALUE                      
               '****  CUTOFF  ****'.                                            
SR3403     05  FILLER                      PIC X(03) VALUE SPACES.              
           05  FILLER                      PIC X(06) VALUE SPACES.              
           05  FILLER                      PIC X(02) VALUE SPACES.              
           05  FILLER                      PIC X(13) VALUE                      
               '   EXTENDED  '.                                                 
      *                                                                         
       01  HL4-HEADING-LINE-4.                                                  
           05  FILLER                  PIC X(14) VALUE 'DPT  MCL  SCL '.        
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE 'SKU   '.                
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(11) VALUE 'DESCRIPTION'.           
           05  FILLER                  PIC X(11) VALUE SPACES.                  
           05  FILLER                      PIC X(15) VALUE '      UPC '.        
           05  FILLER                      PIC X(01) VALUE SPACES.              
           05  FILLER                      PIC X(09) VALUE '     QTY '.         
SR3403     05  FILLER                      PIC X(02) VALUE SPACES.              
           05  FILLER                      PIC X(08) VALUE '  PRICE '.          
SR3403     05  FILLER                      PIC X(03) VALUE SPACES.              
           05  FILLER                      PIC X(09) VALUE '  ONHAND '.         
SR3403     05  FILLER                      PIC X(02) VALUE SPACES.              
           05  FILLER                      PIC X(08) VALUE '  PRICE '.          
SR3403     05  FILLER                      PIC X(01) VALUE SPACES.              
           05  FILLER                      PIC X(09) VALUE 'VARIANCE '.         
           05  FILLER                      PIC X(13) VALUE                      
               '   VAR PRICE '.                                                 
                                                                                
       01  HL5-HEADING-LINE-5.                                                  
           05  FILLER                      PIC X(14) VALUE ALL '-'.             
           05  FILLER                      PIC X(02) VALUE SPACES.              
           05  FILLER                      PIC X(08) VALUE ALL '-'.             
           05  FILLER                      PIC X(02) VALUE SPACES.              
           05  FILLER                      PIC X(25) VALUE ALL '-'.             
           05  FILLER                      PIC X(02) VALUE SPACES.              
           05  FILLER                      PIC X(15) VALUE ALL '-'.             
           05  FILLER                      PIC X(01) VALUE SPACES.              
           05  FILLER                      PIC X(09) VALUE ALL '-'.             
SR3403     05  FILLER                      PIC X(02) VALUE SPACES.              
           05  FILLER                      PIC X(08) VALUE ALL '-'.             
SR3403     05  FILLER                      PIC X(03) VALUE SPACES.              
           05  FILLER                      PIC X(09) VALUE ALL '-'.             
SR3403     05  FILLER                      PIC X(02) VALUE SPACES.              
           05  FILLER                      PIC X(08) VALUE ALL '-'.             
SR3403     05  FILLER                      PIC X(01) VALUE SPACES.              
           05  FILLER                      PIC X(09) VALUE ALL '-'.             
           05  FILLER                      PIC X(02) VALUE SPACES.              
           05  FILLER                      PIC X(13) VALUE ALL '-'.             
                                                                                
       01  DL-DETAIL-LINE.                                                      
           05  DL1-DMS.                                                         
               10  DL1-DEPT                PIC 9(04) VALUE ZEROS.               
               10  FILLER                  PIC X(01) VALUE '-'.                 
               10  DL1-MCL                 PIC 9(04) VALUE ZEROS.               
               10  FILLER                  PIC X(01) VALUE '-'.                 
               10  DL1-SCL                 PIC 9(04) VALUE ZEROS.               
           05  FILLER                      PIC X(02) VALUE SPACES.              
           05  DL1-SKU                     PIC X(08).                           
           05  FILLER                      PIC X(02) VALUE SPACES.              
           05  DL1-SKU-DESCRIP             PIC X(25).                           
           05  FILLER                      PIC X(02) VALUE SPACES.              
           05  DL1-UPC                     PIC X(15).                           
           05  FILLER                      PIC X(04) VALUE SPACES.              
           05  DL1-INV-ONHND               PIC ZZZZ9-.                          
           05  FILLER                      PIC X(01) VALUE SPACES.              
SR3403     05  DL1-INV-PRICE               PIC ZZ,ZZZ.ZZ.                       
SR3403     05  FILLER                      PIC X(03) VALUE SPACES.              
           05  DL1-FS-ONHND                PIC ZZZZ,ZZZ-.                       
           05  FILLER                      PIC X(01) VALUE SPACES.              
SR3403     05  DL1-FS-PRICE                PIC ZZ,ZZZ.ZZ.                       
SR3403     05  FILLER                      PIC X(03) VALUE SPACES.              
           05  DL1-VARIANCE                PIC ZZZZ,ZZZ-.                       
           05  FILLER                      PIC X(02) VALUE SPACES.              
           05  DL1-EXTEND-PRICE            PIC Z,ZZZ,ZZZ.ZZ-.                   
      *                                                                         
       01  VL-VARIANCE-LINE.                                                    
           05  FILLER                  PIC X(17) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(25) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                      PIC X(15) VALUE SPACES.              
           05  FILLER                      PIC X(04) VALUE SPACES.              
           05  VL1-INV-ONHND               PIC ZZZZ9-.                          
           05  FILLER                      PIC X(01) VALUE SPACES.              
      *    05  VL1-INV-PRICE               PIC $,$$$.99.                        
SR3403     05  VL1-INV-PRICE               PIC ZZ,ZZZ.ZZ.                       
SR3403     05  FILLER                      PIC X(03) VALUE SPACES.              
           05  VL1-FS-ONHND                PIC ZZZZ,ZZ9-.                       
           05  FILLER                      PIC X(01) VALUE SPACES.              
SR3403     05  VL1-FS-PRICE                PIC ZZ,ZZZ.ZZ.                       
SR3403     05  FILLER                      PIC X(01) VALUE SPACES.              
           05  VL1-VARIANCE                PIC ZZZZ,ZZ9-.                       
      *    05  FILLER                      PIC X(02) VALUE SPACES.              
           05  VL1-EXTEND-PRICE            PIC $,$$$,$$$.99-.                   
                                                                                
       01  DL-NO-EXCEPTION-LINE.                                                
           05  FILLER                  PIC X(49)    VALUE                       
               '************* NO EXCEPTIONS EXIST ***************'.             
           05  FILLER                  PIC X(83)    VALUE SPACES.               
                                                                                
       01  DL-EXCEPTION-SUMMARY-LINE.                                           
           05  FILLER                  PIC X(10)    VALUE                       
               '** STORE: '.                                                    
           05  DLESL-STORE             PIC X(04).                               
           05  FILLER                  PIC X(32)    VALUE                       
               ' EXCEPTIONS RANGE FROM A LOW OF '.                              
           05  DLESL-LOW               PIC Z,ZZZ,ZZ9-.                          
           05  FILLER                  PIC X(14)    VALUE                       
               ' TO A HIGH OF '.                                                
           05  DLESL-HIGH              PIC Z,ZZZ,ZZ9-.                          
           05  FILLER                  PIC X(03)    VALUE ' **'.                
      *                                                                         
      ******************************************************************        
      * COPYBOOK FOR REMAINING REPORT HEADING LINES, DETAIL LINE                
      ******************************************************************        
           COPY DPWS132O.                                                       
      *                                                                         
      ******************************************************************        
      *  INVENTORY EXCEPTIONS BY STORE EXTRACT - INPUT RECORD LAYOUT            
      ******************************************************************        
           COPY INRD024.                                                        
      *                                                                         
      ******************************************************************        
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      ******************************************************************        
           COPY DPWS004.                                                        
      *                                                                         
      *                                                                         
      ******************************************************************        
      * DB2 TABLE DCLGEN - TSKXREF                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      * DB2 TABLE DCLGEN - IMV_SKU                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE IMV00001                                                 
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      * XMT_LOC TABLE DCLGEN                                                    
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      *  DB2 TABLE DCLGEN - TMDSEAR                                             
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      *  DB2 TABLE DCLGEN - TUPCSKU                                             
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TUPCSKU                                                  
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      * DB2 - CURSOR FOR - TUPCSKU                                              
      ******************************************************************        
           EXEC SQL                                                             
             DECLARE TUPCSKU_CURSOR CURSOR FOR                                  
               SELECT UPC_NBR                                                   
               FROM TUPCSKU A                                                   
                   ,TSKXREF B                                                   
               WHERE SKU        = :PV-EDIT-SKU                                  
                 AND A.ITEM_NBR = B.ITM_NBR                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 TABLE ERROR HANDLING                                                
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
           COPY SQLCA2.                                                         
      *                                                                         
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
      *                                                                         
       1000-MAIN-MODULE.                                                        
      *                                                                         
           PERFORM 1000-INITIALIZATION.                                         
      *                                                                         
           PERFORM 2000-PROCESS-INPUT                                           
               UNTIL PS-END-OF-INPUT.                                           
      *                                                                         
           PERFORM 9000-END-OF-JOB-TASKS.                                       
      *                                                                         
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
      *                                                                         
      /****************************************************************         
      * 1000-INITIALIZATION.                                                    
      *****************************************************************         
       1000-INITIALIZATION.                                                     
      *                                                                         
           DISPLAY '**********************************'.                        
           DISPLAY '* START OF INKRP280               '.                        
           DISPLAY '**********************************'.                        
      *                                                                         
           OPEN INPUT  STORE-CONTROL-FILE                                       
                INPUT  STORE-MERGE-FILE                                         
                INPUT  EXCEPT-CONTROL-FILE                                      
                OUTPUT STORE-INV-EXCEPTION-RPT.                                 
      *                                                                         
           ACCEPT PV-CURRENT-DATE    FROM DATE.                                 
           ACCEPT PV-CURRENT-TIME    FROM TIME.                                 
      *                                                                         
           MOVE PV-CD-MM            TO DP132O-RUN-MONTH.                        
           MOVE PV-CD-DD            TO DP132O-RUN-DAY.                          
           MOVE PV-CD-YY            TO DP132O-RUN-YEAR.                         
           MOVE PV-CT-HH            TO DP132O-RUN-HOUR.                         
           MOVE PV-CT-MM            TO DP132O-RUN-MINUTE.                       
           MOVE PC-PROGRAM-NAME     TO DP132O-PROGRAM-NAME.                     
           MOVE 1                   TO DP132O-REPORT-NUMBER.                    
           MOVE ZEROS               TO PV-TOTAL-INV-QTY.                        
           MOVE ZEROS               TO PV-TOTAL-EXT-PRICE.                      
           MOVE ZEROS               TO PV-STORE-REC-COUNT.                      
           MOVE ZEROS               TO PV-PAGE-COUNT                            
                                       PV-LINE-COUNT.                           
      *                                                                         
      ** GET THE TOP XXXXX CONTROL RECORD FOR THE REPORT.                       
           PERFORM 5100-READ-EXCEPT-CONTROL-FILE.                               
      *                                                                         
      ** DETERMINE WHICH STORES ARE BEING PROCESSED THIS RUN.                   
           PERFORM 5300-READ-STORE-CONTROL.                                     
28545 *                                                                         
28545      IF PS-STORE-CONTROL-AT-END-YES                                       
28545          SET PS-END-OF-INPUT TO TRUE                                      
28545          MOVE ZEROS              TO SA-STORE-FOR-HEADER                   
28545          MOVE ZEROS              TO PV-PAGE-COUNT                         
28545          PERFORM 7100-PRINT-HEADINGS                                      
28545          MOVE DL-NO-EXCEPTION-LINE TO RP-REPORT-WORK-RECORD               
28545          MOVE 2                    TO PV-ADVANCE-LINE                     
28545          PERFORM 7300-PRINT-DETAIL-RECORD                                 
28545      ELSE                                                                 
28545          PERFORM 2500-PROCESS-STORE-CONTROL                               
28545 *                                                                         
28545 ** READ OF THE EXCEPTION MERGE FILE.                                      
28545 *                                                                         
28545          PERFORM 5000-READ-INV-FILE                                       
28545 *                                                                         
28545          IF PS-END-OF-INPUT                                               
28545              MOVE ZEROS              TO SA-STORE-FOR-HEADER               
28545              MOVE ZEROS              TO PV-PAGE-COUNT                     
28545              PERFORM 7100-PRINT-HEADINGS                                  
28545              MOVE DL-NO-EXCEPTION-LINE TO RP-REPORT-WORK-RECORD           
28545              MOVE 2                    TO PV-ADVANCE-LINE                 
28545              PERFORM 7300-PRINT-DETAIL-RECORD                             
28545          ELSE                                                             
28545              IF IN024-STORE-NUMBER NOT = SCR-STORE-NMBR                   
28545                  SET PS-FOUND-STORE-MATCH-NO TO TRUE                      
28545                  PERFORM 1100-NONE-FOR-STORE                              
28545                      UNTIL PS-FOUND-STORE-MATCH-YES OR                    
28545                            PS-STORE-CONTROL-AT-END-YES                    
28545                  IF PS-FOUND-STORE-MATCH-YES                              
28545                      PERFORM 1110-STORE-MATCH                             
28545                  END-IF                                                   
28545              ELSE                                                         
28545                  PERFORM 1110-STORE-MATCH                                 
28545              END-IF                                                       
28545          END-IF                                                           
28545      END-IF.                                                              
28545 *                                                                         
28545                                                                           
28545                                                                           
28545                                                                           
28545  1100-NONE-FOR-STORE.                                                     
28545                                                                           
28545      MOVE SCR-STORE-NMBR TO SA-STORE-FOR-HEADER.                          
28545      MOVE ZEROS          TO PV-PAGE-COUNT.                                
28545                                                                           
28545      PERFORM 7100-PRINT-HEADINGS.                                         
28545      MOVE DL-NO-EXCEPTION-LINE TO RP-REPORT-WORK-RECORD.                  
28545      MOVE 2                    TO PV-ADVANCE-LINE.                        
28545      PERFORM 7300-PRINT-DETAIL-RECORD.                                    
28545                                                                           
28545      PERFORM 5300-READ-STORE-CONTROL.                                     
28545                                                                           
28545      IF PS-STORE-CONTROL-AT-END-NO                                        
28545          IF IN024-STORE-NUMBER = SCR-STORE-NMBR                           
28545              SET PS-FOUND-STORE-MATCH-YES TO TRUE.                        
28545                                                                           
28545  1110-STORE-MATCH.                                                        
28545                                                                           
28545      MOVE IN024-STORE-NUMBER             TO SA-STORE-NUMBER               
28545                                             SA-STORE-FOR-HEADER.          
28545      MOVE IN024-DEPT                     TO SA-DEPT.                      
28545      MOVE IN024-CLASS                    TO SA-CLASS.                     
28545      MOVE IN024-SUB-CLASS                TO SA-SCL.                       
28545      MOVE IN024-SKU                      TO SA-SKU.                       
28545      MOVE IN024-ABSOLUTE-VAR-QTY         TO PV-HIGH-RANGE.                
28545                                                                           
28545      PERFORM 2500-PROCESS-STORE-CONTROL.                                  
28545      PERFORM 7100-PRINT-HEADINGS.                                         
28545                                                                           
      /****************************************************************         
      * 2000-PROCESS-INPUT.                                                     
      * 1. IF MERGE-FILE STORE <> CONTROL-FILE-STORE - NO EXCEPTIONS            
      * 2. IF MERGE-FILE STORE =  MERGE-FILE STORE CONTINUE ALL RECS            
      *****************************************************************         
       2000-PROCESS-INPUT.                                                      
      ** STORE/SKU CHANGES.....                                                 
           IF IN024-STORE-NUMBER     IS EQUAL TO SA-SAVE-STORE-NUMBER           
               IF IN024-STORE-NUMBER IS EQUAL TO SA-STORE-NUMBER                
                   PERFORM 2200-PROCESS-SKU                                     
               ELSE                                                             
                   PERFORM 2400-PROCESS-STORE-BREAK                             
                   PERFORM 2200-PROCESS-SKU                                     
               END-IF                                                           
      *                                                                         
      **       READ OF THE EXCEPTION MERGE FILE.                                
               PERFORM 5000-READ-INV-FILE                                       
           ELSE                                                                 
               PERFORM 2400-PROCESS-STORE-BREAK                                 
28545          PERFORM 2200-PROCESS-SKU                                         
           END-IF.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       2200-PROCESS-SKU.                                                        
      *                                                                         
           MOVE 'N' TO PS-TUPCSKU-DONE-SW                                       
           MOVE IN024-STORE-NUMBER TO SA-STORE-NUMBER                           
                                      DLESL-STORE                               
           MOVE IN024-SKU          TO SA-SKU                                    
                                      PV-EDIT-SKU                               
                                                                                
           PERFORM 8800-OPEN-TUPCSKU-CSR                                        
           PERFORM 6000-MOVE-MERGE-RECORD                                       
      *                                                                         
           PERFORM 2300-PROCESS-SKU-TOTALS.                                     
      *                                                                         
           ADD 1 TO PV-STORE-REC-COUNT.                                         
      *                                                                         
      *****************************************************************         
      * 2300-PROCESS-SKU-TOTALS.                                                
      *****************************************************************         
       2300-PROCESS-SKU-TOTALS.                                                 
      *                                                                         
           PERFORM 3000-VARIANCE-TOTALS.                                        
      *                                                                         
           MOVE IN024-STORE-NUMBER   TO SA-STORE-NUMBER.                        
           MOVE IN024-DEPT                TO SA-DEPT                            
           MOVE IN024-CLASS               TO SA-CLASS                           
           MOVE IN024-SUB-CLASS           TO SA-SCL                             
           MOVE IN024-SKU            TO SA-SKU.                                 
      *                                                                         
      *****************************************************************         
      * 2400-PROCESS-STORE-BREAK.                                               
      *****************************************************************         
       2400-PROCESS-STORE-BREAK.                                                
      *                                                                         
           IF PV-STORE-REC-COUNT IS GREATER THAN 0                              
               PERFORM 7700-PRINT-EXCEPTION-RANGE                               
           ELSE                                                                 
               MOVE DL-NO-EXCEPTION-LINE TO RP-REPORT-WORK-RECORD               
               MOVE 2                    TO PV-ADVANCE-LINE                     
               PERFORM 7300-PRINT-DETAIL-RECORD                                 
           END-IF.                                                              
      *                                                                         
           MOVE ZEROS                TO PV-PAGE-COUNT                           
                                        PV-LINE-COUNT                           
                                        PV-STORE-REC-COUNT.                     
28545                                                                           
28545      SET PS-FOUND-STORE-MATCH-NO TO TRUE.                                 
28545                                                                           
28545      PERFORM 2410-GET-STORE-CONTROL                                       
28545          UNTIL PS-FOUND-STORE-MATCH-YES OR                                
28545                PS-STORE-CONTROL-AT-END-YES                                
28545                  IF PS-FOUND-STORE-MATCH-YES                              
28545                      PERFORM 2500-PROCESS-STORE-CONTROL                   
28545                      MOVE IN024-STORE-NUMBER                              
28545                                          TO SA-STORE-NUMBER               
28545                                             SA-STORE-FOR-HEADER           
28545                      MOVE IN024-SKU      TO SA-SKU                        
                           MOVE IN024-DEPT     TO SA-DEPT                       
                           MOVE IN024-CLASS    TO SA-CLASS                      
                           MOVE IN024-SUB-CLASS TO SA-SCL                       
                           MOVE IN024-ABSOLUTE-VAR-QTY    TO                    
                                                  PV-HIGH-RANGE                 
28545                      PERFORM 7100-PRINT-HEADINGS                          
28545                  END-IF.                                                  
28545                                                                           
28545                                                                           
28545  2410-GET-STORE-CONTROL.                                                  
28545                                                                           
28545      PERFORM 5300-READ-STORE-CONTROL.                                     
28545                                                                           
28545      IF PS-STORE-CONTROL-AT-END-NO                                        
28545          IF IN024-STORE-NUMBER = SCR-STORE-NMBR                           
28545              SET PS-FOUND-STORE-MATCH-YES TO TRUE                         
28545          ELSE                                                             
28545              MOVE SCR-STORE-NMBR TO SA-STORE-FOR-HEADER                   
28545              MOVE ZEROS          TO PV-PAGE-COUNT                         
28545                                                                           
28545              PERFORM 7100-PRINT-HEADINGS                                  
28545              MOVE DL-NO-EXCEPTION-LINE TO RP-REPORT-WORK-RECORD           
28545              MOVE 2                    TO PV-ADVANCE-LINE                 
28545              PERFORM 7300-PRINT-DETAIL-RECORD                             
28545          END-IF                                                           
28545      END-IF.                                                              
      *                                                                         
      *****************************************************************         
      * 2500-PROCESS-STORE-CONTROL.                                             
      *****************************************************************         
       2500-PROCESS-STORE-CONTROL.                                              
      *                                                                         
           MOVE SCR-STORE-NMBR  TO SA-SAVE-STORE-NUMBER                         
                                   SA-STORE-FOR-HEADER.                         
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 3000-VARIANCE-TOTALS.                                                   
      ******************************************************************        
       3000-VARIANCE-TOTALS.                                                    
      *                                                                         
           MOVE SPACES               TO RP-REPORT-WORK-RECORD                   
           MOVE SPACES               TO VL-VARIANCE-LINE                        
      *                                                                         
     ****  INVENTORIED QTY / PRICE                                              
           MOVE ZERO                      TO VL1-INV-ONHND.                     
           MOVE ZERO                      TO VL1-INV-PRICE.                     
                                                                                
     ****  FASHION SALES QTY                                                    
           IF IN024-MFSTNG-IND IS EQUAL TO 'Y'                                  
               COMPUTE PV-TOTAL-FS-QTY    = IN024-INV-CUTOFF-OH-QTY -           
                                            (IN024-CUTOFF-STR-EXPECT -          
                                             IN024-CUTOFF-INTRANSIT)            
           ELSE                                                                 
               COMPUTE PV-TOTAL-FS-QTY    = IN024-INV-CUTOFF-OH-QTY             
           END-IF.                                                              
      *                                                                         
           MOVE PV-TOTAL-FS-QTY           TO VL1-FS-ONHND.                      
                                                                                
     ****  FASHION SALES PRICE                                                  
           MOVE ZERO                      TO VL1-FS-PRICE.                      
                                                                                
     ****  VARIANCE                                                             
           MOVE IN024-VARIANCE-QTY        TO VL1-VARIANCE                       
                                                                                
     ****  EXTENDED VARIANCE                                                    
           COMPUTE VL1-EXTEND-PRICE = IN024-SYSTEM-PRICE                        
                                    * IN024-VARIANCE-QTY.                       
     ****  EXTENDED VARIANCE                                                    
      *    MOVE VL1-EXTEND-PRICE          TO VL1-EXTEND-PRICE.                  
                                                                                
      *                                                                         
           MOVE VL-VARIANCE-LINE          TO RP-REPORT-WORK-RECORD.             
           PERFORM 7300-PRINT-DETAIL-RECORD.                                    
           MOVE SPACES                    TO RP-REPORT-WORK-RECORD.             
           PERFORM 7300-PRINT-DETAIL-RECORD.                                    
      *                                                                         
      /*****************************************************************        
      * 5000-READ-INV-FILE.                                                     
      *    READ THE MERGED INPUT EXCEPTION SKUS.                                
      ******************************************************************        
       5000-READ-INV-FILE.                                                      
      *                                                                         
           READ STORE-MERGE-FILE                                                
             INTO IN024-INV-EXCEPTS-STR-REC                                     
                AT END  SET PS-END-OF-INPUT TO TRUE                             
           END-READ.                                                            
      *                                                                         
      ******************************************************************        
      * 5100-READ THE EXCEPTION CONTROL FILE.                                   
      *    READ THE TOP EXCEPTION CONTROL FILE.                                 
      ******************************************************************        
       5100-READ-EXCEPT-CONTROL-FILE.                                           
                                                                                
           READ EXCEPT-CONTROL-FILE                                             
             AT END                                                             
               SET PS-END-OF-INPUT TO TRUE                                      
               DISPLAY '*************************************'                  
               DISPLAY '**  ABEND PROGRAM = INKRP280'                           
               DISPLAY '**  PARAGRAPH     = 1000-INITIALIZTION'                 
               DISPLAY '**'                                                     
               DISPLAY '**  MISSING EXCEPTION CONTROL RECORD'                   
               DISPLAY '**  INP03'                                              
               DISPLAY '*************************************'                  
               MOVE +4008          TO ABEND-CODE                                
               PERFORM 9990-ABEND                                               
           END-READ.                                                            
      *                                                                         
      ******************************************************************        
      * 5300-READ THE STORE CONTROL FILE.                                       
      *    READ THE STORE CONTROL FILE WHICH LETS US KNOW WHICH STORES          
      *  WE ARE PROCESSING THIS RUN.                                            
      ******************************************************************        
       5300-READ-STORE-CONTROL.                                                 
                                                                                
           READ STORE-CONTROL-FILE NEXT                                         
                AT END  MOVE 'Y'    TO PS-INP01-DONE-SW                         
           END-READ.                                                            
      *                                                                         
      /*****************************************************************        
      * 6000-MOVE-MERGE-RECORD.                                                 
      * MOVE THE COPY BOOK (INRD288) VALUES TO THE OUTPUT RECORD VALUES         
      ******************************************************************        
       6000-MOVE-MERGE-RECORD.                                                  
      *                                                                         
           MOVE SPACES                    TO DL-DETAIL-LINE                     
           MOVE IN024-SKU                 TO DL1-SKU, PV-EDIT-SKU,              
                                             SA-SKU-SAVE.                       
      *    MOVE IN024-SKU-ATTRIBUTES      TO SA-SKU-ATTRIBUTES.                 
           MOVE IN024-DEPT                TO SA-DEPT.                           
           MOVE IN024-CLASS               TO SA-CLASS.                          
           MOVE IN024-SUB-CLASS           TO SA-SCL.                            
           MOVE SA-SKU-ATTRIBUTES         TO DL1-DMS.                           
           MOVE ZEROES                    TO DL1-INV-ONHND.                     
           MOVE IN024-ABSOLUTE-VAR-QTY    TO PV-LOW-RANGE.                      
                                                                                
           PERFORM 8600-SELECT-IMV-SKU.                                         
           PERFORM 8825-SELECT-TUPCSKU.                                         
                                                                                
           PERFORM                                                              
             UNTIL NO-MORE-UPCS                                                 
               PERFORM 8400-PRT-DETAIL                                          
               MOVE SPACES TO DL-DETAIL-LINE                                    
               PERFORM 8825-SELECT-TUPCSKU                                      
           END-PERFORM.                                                         
           PERFORM 8850-CLOSE-TUPCSKU-CSR.                                      
      *                                                                         
           IF PV-DTL-LINE-PRT-ACCUM IS EQUAL TO ZEROS                           
               PERFORM 8400-PRT-DETAIL                                          
           END-IF.                                                              
                                                                                
           MOVE 'N'                       TO PS-TUPCSKU-DONE-SW.                
           MOVE ZEROS                     TO PV-DTL-LINE-PRT-ACCUM.             
      *                                                                         
      ******************************************************************        
      * 7100-PRINT-HEADINGS.                                                    
      ******************************************************************        
       7100-PRINT-HEADINGS.                                                     
      *                                                                         
           ADD 1                    TO PV-PAGE-COUNT.                           
           MOVE PV-PAGE-COUNT       TO DP132O-PAGE-NUMBER.                      
           MOVE PC-PROGRAM-NAME     TO DP132O-PROGRAM-NAME.                     
           MOVE 1                   TO DP132O-REPORT-NUMBER.                    
                                                                                
           WRITE REPORT-LINE                                                    
             FROM DP132O-STANDRD-HEADER-132-COLS AFTER ADVANCING PAGE.          
                                                                                
           MOVE ECR-EXCEPT-CONTROL  TO HL1-EXCEPT-CONTROL.                      
      *                                                                         
           WRITE REPORT-LINE                                                    
             FROM HL1-HEADING-LINE-1 AFTER ADVANCING 1 LINE.                    
                                                                                
           PERFORM 8500-SELECT-XMTLOC.                                          
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL2-HEADING-LINE-2 AFTER ADVANCING 1 LINE.                    
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL3-HEADING-LINE-3 AFTER ADVANCING 1 LINE.                    
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL4-HEADING-LINE-4 AFTER ADVANCING 1 LINE.                    
                                                                                
           WRITE REPORT-LINE                                                    
             FROM HL5-HEADING-LINE-5 AFTER ADVANCING 1 LINE.                    
                                                                                
           MOVE 7                   TO PV-LINE-COUNT.                           
      *                                                                         
      ******************************************************************        
      * 7300-PRINT-DETAIL-RECORD.                                               
      ******************************************************************        
       7300-PRINT-DETAIL-RECORD.                                                
      *                                                                         
           IF PV-LINE-COUNT = 0                                                 
               OR (PV-LINE-COUNT + PV-ADVANCE-LINE > PC-PRINT-LINE-MAX)         
               PERFORM 7100-PRINT-HEADINGS                                      
           END-IF.                                                              
      *                                                                         
           WRITE REPORT-LINE FROM RP-REPORT-WORK-RECORD                         
               AFTER ADVANCING PV-ADVANCE-LINE LINE.                            
      *                                                                         
           ADD PV-ADVANCE-LINE  TO PV-LINE-COUNT.                               
           MOVE +1              TO PV-ADVANCE-LINE.                             
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 7700-PRINT THE STORE EXCEPTION SUMMARY LINE.                            
      ******************************************************************        
       7700-PRINT-EXCEPTION-RANGE.                                              
      *                                                                         
           MOVE SA-STORE-NUMBER TO DLESL-STORE.                                 
           MOVE PV-LOW-RANGE    TO DLESL-LOW.                                   
           MOVE PV-HIGH-RANGE   TO DLESL-HIGH.                                  
      *                                                                         
           MOVE DL-EXCEPTION-SUMMARY-LINE TO RP-REPORT-WORK-RECORD.             
           MOVE 3                    TO PV-ADVANCE-LINE.                        
           PERFORM 7300-PRINT-DETAIL-RECORD.                                    
      *                                                                         
           MOVE ZEROS           TO PV-HIGH-RANGE.                               
           MOVE ZEROS           TO PV-LOW-RANGE.                                
      *                                                                         
      *                                                                         
      *****************************************************************         
      * 8400-PRT-DETAIL.                                                        
      *****************************************************************         
       8400-PRT-DETAIL.                                                         
      *                                                                         
           MOVE DL-DETAIL-LINE       TO RP-REPORT-WORK-RECORD.                  
           PERFORM 7300-PRINT-DETAIL-RECORD.                                    
           ADD 1                     TO PV-DTL-LINE-PRT-ACCUM.                  
      *                                                                         
      /*****************************************************************        
      * 8500-SELECT-XMTLOC.                                                     
      ******************************************************************        
       8500-SELECT-XMTLOC.                                                      
      *                                                                         
           INITIALIZE DCLXMT00001.                                              
      *                                                                         
           MOVE SA-STORE-FOR-HEADER TO XMT00001-LOC-NBR                         
                                       HL2-STORE-NUMBER.                        
      *                                                                         
           EXEC SQL                                                             
             SELECT LOC_NM                                                      
               INTO :XMT00001-LOC-NM                                            
               FROM XMT_LOC                                                     
              WHERE LOC_NBR = :XMT00001-LOC-NBR                                 
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE XMT00001-LOC-NM        TO HL2-STORE-DESCRIPTION         
               WHEN OTHER                                                       
                   MOVE 'NOT FOUND ON XMT00001'                                 
                       TO HL2-STORE-DESCRIPTION                                 
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * 8600-SELECT-IMV-SKU.                                                    
      ******************************************************************        
       8600-SELECT-IMV-SKU.                                                     
      *                                                                         
           INITIALIZE DCLIMV00001.                                              
      *                                                                         
           MOVE PV-EDIT-SKU         TO IMV00001-SKU-NBR.                        
      *                                                                         
           EXEC SQL                                                             
             SELECT                                                             
                   SKU_DESC                                                     
             INTO                                                               
                  :IMV00001-SKU-DESC                                            
             FROM IMV_SKU                                                       
             WHERE                                                              
                   SKU_NBR         = :IMV00001-SKU-NBR                          
           END-EXEC.                                                            
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   MOVE IMV00001-SKU-DESC (1:25)  TO DL1-SKU-DESCRIP,           
                                                    SA-SKU-DESCRIP-SAVE         
               WHEN OTHER                                                       
                   MOVE '****'        TO DL1-SKU-DESCRIP,                       
                                         SA-SKU-DESCRIP-SAVE                    
           END-EVALUATE.                                                        
      *                                                                         
      *****************************************************************         
      * 8800-OPEN-TUPCSKU-CSR.                                                  
      *****************************************************************         
       8800-OPEN-TUPCSKU-CSR.                                                   
                                                                                
           SET NOT-END-OF-UPCSKU-CUR TO TRUE.                                   
                                                                                
           EXEC SQL                                                             
               OPEN TUPCSKU_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
               DISPLAY '**  ABEND     **'                                       
               MOVE '8800-OPEN-TUPCSKU-CSR' TO PV-PARAGRAPH-NAME                
               MOVE 'OPEN CURSOR'     TO  PV-DB2-OPER-ATTEMPTED                 
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 8825-SELECT-TUPCSKU.                                                    
      ******************************************************************        
       8825-SELECT-TUPCSKU.                                                     
                                                                                
           MOVE SPACES TO DL1-UPC.                                              
                                                                                
           EXEC SQL                                                             
             FETCH TUPCSKU_CURSOR                                               
               INTO :TUPCSKU-UPC-NBR                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE IS EQUAL TO ZERO                                    
                   MOVE TUPCSKU-UPC-NBR TO DL1-UPC                              
               WHEN SQLCODE IS EQUAL TO +100                                    
                   SET NO-MORE-UPCS      TO TRUE                                
               WHEN SQLWARN IS NOT EQUAL TO SPACES                              
               WHEN SQLCODE IS NOT EQUAL TO ZERO                                
                   SET NO-MORE-UPCS      TO TRUE                                
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * 8850-CLOSE-TUPCSKU-CSR.                                                 
      *****************************************************************         
       8850-CLOSE-TUPCSKU-CSR.                                                  
                                                                                
           EXEC SQL                                                             
               CLOSE TUPCSKU_CURSOR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
               DISPLAY '**  ABEND     **'                                       
               MOVE '8850-CLOSE-TUPCSKU-CSR' TO PV-PARAGRAPH-NAME               
               MOVE 'CLOSE CURSOR'     TO  PV-DB2-OPER-ATTEMPTED                
               PERFORM 9900-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
      /****************************************************************         
      * 9000-END-OF-JOB-TASKS.                                                  
      *****************************************************************         
       9000-END-OF-JOB-TASKS.                                                   
      *                                                                         
           IF PV-STORE-REC-COUNT IS GREATER THAN 0                              
               PERFORM 7700-PRINT-EXCEPTION-RANGE                               
           ELSE                                                                 
               MOVE DL-NO-EXCEPTION-LINE TO RP-REPORT-WORK-RECORD               
               MOVE 2                    TO PV-ADVANCE-LINE                     
               PERFORM 7300-PRINT-DETAIL-RECORD                                 
           END-IF.                                                              
                                                                                
28545      PERFORM 9010-NO-EXCEPT-DISPLAY                                       
28545          UNTIL PS-STORE-CONTROL-AT-END-YES.                               
      *                                                                         
           CLOSE STORE-MERGE-FILE                                               
                 EXCEPT-CONTROL-FILE                                            
                 STORE-CONTROL-FILE                                             
                 STORE-INV-EXCEPTION-RPT.                                       
      *                                                                         
           DISPLAY '**********************************'.                        
           DISPLAY '* END OF INKRP280                 '.                        
           DISPLAY '**********************************'.                        
28545                                                                           
28545                                                                           
28545  9010-NO-EXCEPT-DISPLAY.                                                  
28545                                                                           
28545      PERFORM 5300-READ-STORE-CONTROL.                                     
28545                                                                           
28545      IF PS-STORE-CONTROL-AT-END-NO                                        
28545          MOVE SCR-STORE-NMBR TO SA-STORE-FOR-HEADER                       
28545          MOVE ZEROS          TO PV-PAGE-COUNT                             
28545                                                                           
28545          PERFORM 7100-PRINT-HEADINGS                                      
28545          MOVE DL-NO-EXCEPTION-LINE TO RP-REPORT-WORK-RECORD               
28545          MOVE 2                    TO PV-ADVANCE-LINE                     
28545          PERFORM 7300-PRINT-DETAIL-RECORD                                 
28545      END-IF.                                                              
      *                                                                         
      /****************************************************************         
      * 9900-SQL-ABEND.                                                         
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      *****************************************************************         
       9900-SQL-ABEND.                                                          
      *                                                                         
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKRP280'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
      *                                                                         
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
      *                                                                         
           COPY DPPD004.                                                        
      *                                                                         
           PERFORM 9990-ABEND.                                                  
      *                                                                         
       9990-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
