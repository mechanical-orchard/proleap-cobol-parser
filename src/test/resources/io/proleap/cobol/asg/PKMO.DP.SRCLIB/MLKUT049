       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUT049.                                         00030001
       AUTHOR.        JOHN SELWITSCHKA.                                 00040001
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  08/31/2010.                                       00060001
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *    MLKUT049 - DETERMINE FREIGHT, SALVAGE, LICENSE FEE CODES    *00100001
      ******************************************************************00110000
      ******************************************************************00110100
      *    WR/PITS#     DATE        DESCRIPTION                        *00110200
      *    --------   --------     ---------------------------------   *00110300
      *    WR37965    08/31/10     INITIAL CREATION                    *00110401
      ******************************************************************00110500
                                                                        00110600
       ENVIRONMENT DIVISION.                                            00110700
                                                                        00110800
       CONFIGURATION SECTION.                                           00110900
       SOURCE-COMPUTER.        IBM-3090.                                00111000
       OBJECT-COMPUTER.        IBM-3090.                                00112000
       INPUT-OUTPUT SECTION.                                            00113000
       FILE-CONTROL.                                                    00114000
           SELECT AUDIT-DTL-FILE-IN                                     00115000
               ASSIGN TO INP01.                                         00116000
                                                                        00117000
           SELECT CNTL-DATE-FILE                                        00117110
               ASSIGN TO INP02.                                         00117210
                                                                        00117310
           SELECT AUDIT-DTL-FILE-OUT                                    00118000
               ASSIGN TO OUT01.                                         00118100
                                                                        00118200
       DATA DIVISION.                                                   00118300
       FILE SECTION.                                                    00118400
                                                                        00118500
       FD  AUDIT-DTL-FILE-IN                                            00118600
           LABEL RECORDS ARE STANDARD                                   00118700
           RECORDING MODE IS F                                          00118800
           BLOCK CONTAINS 0 RECORDS.                                    00118900
       01  AUDIT-DTL-IN-REC             PIC X(200).                     00119000
                                                                        00120000
       FD  CNTL-DATE-FILE                                               00121008
           LABEL RECORDS ARE STANDARD                                   00122008
           RECORDING MODE IS F                                          00123008
           BLOCK CONTAINS 0 RECORDS.                                    00124008
       01  CNTL-DATE-REC                PIC X(80).                      00125008
                                                                        00126008
       FD  AUDIT-DTL-FILE-OUT                                           00130000
           LABEL RECORDS ARE STANDARD                                   00140000
           RECORDING MODE IS F                                          00150000
           BLOCK CONTAINS 0 RECORDS.                                    00160000
       01  AUDIT-DTL-OUT-REC            PIC X(200).                     00170000
      *                                                                 00180000
                                                                        00190000
       WORKING-STORAGE SECTION.                                         00200000
                                                                        00210000
       01  FILLER                      PIC X(28)   VALUE                00220000
                'BEGINNING OF WORKING STORAGE'.                         00230000
                                                                        00240000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00250000
                                                                        00260000
       01  PC-PROGRAM-CONSTANTS.                                        00270000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK049'.    00280020
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUT049'.  00290020
      *                                                                 00300000
       01  PS-PROGRAM-SWITCHES.                                         00310000
           05  PS-EOF-AUD-DTL-FILE-SW       PIC X(01)     VALUE 'N'.    00320000
               88  PS-EOF-AUD-DTL-FILE                    VALUE 'Y'.    00330000
           05  PS-LOCATION-FOUND-SW         PIC X(01)     VALUE 'N'.    00340000
               88  PS-LOCATION-FOUND-Y                    VALUE 'Y'.    00350000
               88  PS-LOCATION-FOUND-N                    VALUE 'N'.    00360000
           05  PS-LOC-CSR-SW                PIC X(01)     VALUE 'N'.    00370000
               88  PS-END-OF-LOC-CSR                      VALUE 'Y'.    00380000
               88  PS-MORE-LOC-ROWS                       VALUE 'N'.    00390000
           05  PS-CNTL-DATE-FILE-SW         PIC X(01)     VALUE 'N'.    00391008
               88  PS-END-OF-CNTL-DTE-FILE                VALUE 'Y'.    00392008
      *                                                                 00400000
       01  CC-CONTROL-CARD.                                             00401008
           05  CC-CNTL-DATE              PIC  X(10) VALUE SPACES.       00402008
           05  FILLER                    PIC  X(70) VALUE SPACES.       00403008
      *                                                                 00404008
       01  PROGRAM-VARIABLES.                                           00410000
           05  PV-SUB                    PIC 9(04)  VALUE ZEROES.       00420000
           05  PV-PARAGRAPH-NAME         PIC  X(30) VALUE SPACES.       00430000
           05  PV-OPERATION-ATTEMPTED    PIC  X(50) VALUE SPACES.       00440000
           05  PV-DB2-DATE-EXISTS        PIC  X(01) VALUE SPACES.       00441011
      *                                                                 00450000
       01  PA-PROGRAM-ACCUMULATORS.                                     00460000
           05  PA-RECORD-COUNTS.                                        00470000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      00480000
               10  PA-OUTPUT-COUNT       PIC  9(09)  VALUE ZEROES.      00490000
               10  PA-PADJ-CODE-FND      PIC  9(09)  VALUE ZEROES.      00500001
               10  PA-PADJ-CODE-NOTFND   PIC  9(09)  VALUE ZEROES.      00501001
               10  PA-PADJ-CODE-DUPFND   PIC  9(09)  VALUE ZEROES.      00502001
               10  PA-PRCHG-CODE-FND     PIC  9(09)  VALUE ZEROES.      00503001
               10  PA-PRCHG-CODE-NOTFND  PIC  9(09)  VALUE ZEROES.      00504001
               10  PA-PRCHG-CODE-DUPFND  PIC  9(09)  VALUE ZEROES.      00505001
      *                                                                 00510000
      ******************************************************************00590000
      * MLRD090 - AUDIT DETAIL TRANSACTIONS                             00600000
      ******************************************************************00610000
           COPY MLRD090.                                                00620022
      *                                                                 00630000
      ******************************************************************00640000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                00650000
      ******************************************************************00660000
           COPY DPWS004.                                                00670000
      *                                                                 00680000
      ******************************************************************00690000
      *  USED FOR DB2 ERRORS                                            00700000
      ******************************************************************00710000
           EXEC SQL                                                     00720000
               INCLUDE SQLCA                                            00730000
           END-EXEC.                                                    00740000
      *                                                                 00750000
      ******************************************************************00751008
      * DATE ATTRIBUTE TABLE                                            00752008
      ******************************************************************00753008
           EXEC SQL                                                     00754008
               INCLUDE TDTATTR                                          00755009
           END-EXEC.                                                    00756008
      *                                                                 00757008
      ******************************************************************00760000
      *  PURCHASE ADJUSTMENT AUDIT DETAIL TABLE (MLT_PADJ_TRN_DTL)      00770001
      ******************************************************************00780000
           EXEC SQL                                                     00790000
               INCLUDE MLT00039                                         00800001
           END-EXEC.                                                    00810000
      *                                                                 00820000
      ******************************************************************00821001
      *  PRICE CHANGE AUDIT DETAIL TABLE (MLT_PRCHG_TRN_DTL)            00822001
      ******************************************************************00823001
           EXEC SQL                                                     00824001
               INCLUDE MLT00038                                         00825001
           END-EXEC.                                                    00826001
      *                                                                 00827001
                                                                        00828001
      *-------------------*                                             00960000
       PROCEDURE DIVISION.                                              00970000
      *-------------------*                                             00980000
                                                                        00990000
       0000-MLKUT053.                                                   01000000
                                                                        01010000
           PERFORM 1000-INITIALIZATION.                                 01020000
      *                                                                 01030000
           PERFORM 2000-PROCESS-INPUT                                   01040000
                UNTIL PS-EOF-AUD-DTL-FILE.                              01050000
      *                                                                 01060000
           PERFORM 8000-EOJ-ROUTINE.                                    01070000
                                                                        01080000
           STOP RUN.                                                    01090000
                                                                        01100000
      ******************************************************************01110000
      * OPEN FILES, READ FIRST INPUT RECORD, READ DATE CONTROL          01120021
      ******************************************************************01130000
      *                                                                 01140000
       1000-INITIALIZATION.                                             01150000
      *                                                                 01160000
           OPEN INPUT AUDIT-DTL-FILE-IN                                 01170008
                      CNTL-DATE-FILE.                                   01171008
                                                                        01180000
           OPEN OUTPUT AUDIT-DTL-FILE-OUT.                              01190000
                                                                        01200000
           PERFORM 4000-READ-AUD-DTL-FILE.                              01230000
                                                                        01231008
           PERFORM 6000-READ-DATE-CNTL-FILE.                            01232008
      *                                                                 01240000
      ******************************************************************01570000
      * PROCESS THE INPUT AUDIT FILE                                    01580000
      ******************************************************************01590000
       2000-PROCESS-INPUT.                                              01600000
      *                                                                 01610000
           IF CC-CNTL-DATE = SPACES                                     01611008
               CONTINUE                                                 01612008
           ELSE                                                         01613008
               MOVE CC-CNTL-DATE TO ML090-PRCG-DTE                      01614008
           END-IF.                                                      01615008
                                                                        01616008
           PERFORM 2100-GET-CODES.                                      01620001
                                                                        01630000
           PERFORM 5000-WRITE-AUD-DTL-FILE.                             01640000
                                                                        01650000
           PERFORM 4000-READ-AUD-DTL-FILE.                              01660000
      *                                                                 01670000
      ******************************************************************01680000
      *  GET CODES.                                                     01690001
      *     ACCESS AUDIT TABLES FOR THE FISCAL WEEK END DATE AND        01691002
      *     PROCESS DATE TO DETERMINE THE ORIGINAL CODES FOR FREIGHT,   01692001
      *     SALVAGE AND LICENSE FEE AUDIT DETAIL RECORDS.               01693001
      ******************************************************************01700000
       2100-GET-CODES.                                                  01710001
                                                                        01722001
           IF (ML090-PADJ-GRO-COST-AMT  NOT = ZERO OR                   01723014
               ML090-PADJ-NET-COST-AMT  NOT = ZERO OR                   01724014
               ML090-PADJ-RTL-AMT       NOT = ZERO OR                   01725014
               ML090-PADJ-DISC-COST-AMT NOT = ZERO) AND                 01726014
               ML090-PRORATE-REVERSAL                                   01726114
               PERFORM 8100-SELECT-PADJ-CODE                            01726201
           ELSE                                                         01726315
               MOVE MLT00039-AP-TRN-CDE TO ML090-AP-TRN-CDE             01726416
           END-IF.                                                      01727001
                                                                        01728001
           IF ML090-PRCHG-RTL-AMT  NOT = ZERO AND                       01729014
              ML090-PRORATE-REVERSAL                                    01729114
               PERFORM 8200-SELECT-PRCHG-CODE                           01729401
           ELSE                                                         01729515
               MOVE MLT00038-PRCHG-RSN-CDE TO ML090-PRCHG-RSN-CDE       01729615
               MOVE MLT00038-PRCHG-CTG-CDE TO ML090-PRCHG-CTG-CDE       01729715
           END-IF.                                                      01729815
      *                                                                 01840000
      ******************************************************************01850000
      * READS THE CONDENSED FILE INTO THE AUDIT DETAIL RECORD LAYOUT    01860000
      ******************************************************************01870000
       4000-READ-AUD-DTL-FILE.                                          01880000
           READ AUDIT-DTL-FILE-IN INTO ML090-AUDIT-REC                  01890000
              AT END                                                    01900000
                MOVE 'Y' TO PS-EOF-AUD-DTL-FILE-SW.                     01910000
                                                                        01920000
           IF NOT PS-EOF-AUD-DTL-FILE                                   01921000
                ADD 1                   TO PA-INPUT-COUNT               01930000
           END-IF.                                                      01931000
      *                                                                 01940000
      ******************************************************************01950000
      * WRITES THE CONDENSED FILE FROM THE AUDIT DETAIL RECORD LAYOUT   01960000
      ******************************************************************01970000
       5000-WRITE-AUD-DTL-FILE.                                         01980000
           WRITE AUDIT-DTL-OUT-REC  FROM ML090-AUDIT-REC.               01990000
                                                                        02000000
           ADD 1                        TO PA-OUTPUT-COUNT.             02010000
      *                                                                 02020000
      ******************************************************************02021008
      * READ DATE CONTROL FILE                                          02022008
      ******************************************************************02023008
       6000-READ-DATE-CNTL-FILE.                                        02024008
           READ CNTL-DATE-FILE INTO CC-CONTROL-CARD                     02025008
               AT END                                                   02026008
                   MOVE SPACES TO CC-CONTROL-CARD                       02027008
                   SET PS-END-OF-CNTL-DTE-FILE TO TRUE                  02028008
                   DISPLAY  'PROCESSING DATE CONTROL CARD EMPTY'.       02029008
           IF NOT PS-END-OF-CNTL-DTE-FILE                               02029108
               PERFORM 6100-VALIDATE-CNTL-DATE                          02029208
           END-IF.                                                      02029308
      *                                                                 02029408
      ******************************************************************02029508
      * VALIDATE CONTROL DATE                                           02029608
      ******************************************************************02029708
       6100-VALIDATE-CNTL-DATE.                                         02029808
           EXEC SQL                                                     02029908
              SELECT 'Y'                                                02030008
                INTO :PV-DB2-DATE-EXISTS                                02030108
                FROM TDTATTR                                            02030208
               WHERE KY_DTE = :CC-CNTL-DATE                             02030308
           END-EXEC                                                     02030408
                                                                        02030508
           EVALUATE TRUE                                                02030608
               WHEN SQLCODE = 0                                         02030708
                   CONTINUE                                             02030808
               WHEN SQLCODE = 100                                       02030908
                   DISPLAY 'INVALID DATE IN CONTROL CARD'               02031008
                   DISPLAY 'CONTROL DATE ', CC-CNTL-DATE                02031108
                   MOVE '6100-VALIDATE-CNTL-DATE'                       02031208
                                   TO PV-PARAGRAPH-NAME                 02031308
                   MOVE 'ERROR WHILE VALIDATING CONTROL DATE'           02031408
                                   TO PV-OPERATION-ATTEMPTED            02031508
                   PERFORM 9999-SQL-ABEND                               02031608
               WHEN OTHER                                               02031708
                   MOVE '6100-VALIDATE-CNTL-DATE'                       02031808
                                   TO PV-PARAGRAPH-NAME                 02031908
                   MOVE 'ERROR WHILE VALIDATING CONTROL DATE'           02032008
                                   TO PV-OPERATION-ATTEMPTED            02032108
                   PERFORM 9999-SQL-ABEND                               02032208
           END-EVALUATE.                                                02032308
      *                                                                 02032408
      ******************************************************************02033008
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *02040000
      ******************************************************************02050000
       8000-EOJ-ROUTINE.                                                02060000
                                                                        02070000
           DISPLAY SPACES.                                              02080000
           DISPLAY 'INPUT RECORDS PROCESSED    ', PA-INPUT-COUNT.       02090001
           DISPLAY SPACES.                                              02091001
           DISPLAY 'PURCH ADJ CODES FOUND      ', PA-PADJ-CODE-FND.     02100001
           DISPLAY 'PURCH ADJ CODES NOT FOUND  ', PA-PADJ-CODE-NOTFND.  02100101
           DISPLAY 'PURCH ADJ CODES DUP FOUND  ', PA-PADJ-CODE-DUPFND.  02100201
           DISPLAY SPACES.                                              02100301
           DISPLAY 'PRICE CHG CODES FOUND      ', PA-PRCHG-CODE-FND.    02101001
           DISPLAY 'PRICE CHG CODES NOT FOUND  ', PA-PRCHG-CODE-NOTFND. 02102001
           DISPLAY 'PRICE CHG CODES DUP FOUND  ', PA-PRCHG-CODE-DUPFND. 02103001
           DISPLAY SPACES.                                              02104001
           DISPLAY 'OUTPUT RECORDS PROCESSED   ', PA-OUTPUT-COUNT.      02110001
           DISPLAY SPACES.                                              02120000
      *                                                                 02130000
           CLOSE AUDIT-DTL-FILE-IN                                      02140000
                 CNTL-DATE-FILE                                         02141012
                 AUDIT-DTL-FILE-OUT.                                    02150000
      *                                                                 02160000
      ******************************************************************02170000
      *  SELECT PURCHASE ADJUSTMENT AP TRAN CODE                       *02180001
      ******************************************************************02190000
       8100-SELECT-PADJ-CODE.                                           02200001
                                                                        02210000
           DISPLAY 'ML090-FSCL-WK-END-DTE  ' ML090-FSCL-WK-END-DTE      02211003
           DISPLAY 'ML090-PRCG-DTE         ' ML090-PRCG-DTE             02212003
           DISPLAY 'ML090-DEPT-NBR         ' ML090-DEPT-NBR             02213003
           DISPLAY 'ML090-MAJ-CL-NBR       ' ML090-MAJ-CL-NBR           02214003
           DISPLAY 'ML090-SUB-CL-NBR       ' ML090-SUB-CL-NBR           02215003
           DISPLAY 'ML090-LOC-NBR          ' ML090-LOC-NBR              02216003
           DISPLAY 'ML090-ORIG-ENT-ID      ' ML090-ORIG-ENT-ID          02217003
           DISPLAY 'ML090-ORIG-LBL-SEQ-NBR ' ML090-ORIG-LBL-SEQ-NBR     02218003
           DISPLAY 'ML090-TRN-TYP-CDE      ' ML090-TRN-TYP-CDE          02218114
                                                                        02219003
           EXEC SQL                                                     02220000
               SELECT AP_TRN_CDE                                        02230001
               INTO :MLT00039-AP-TRN-CDE                                02230101
               FROM MLT_PADJ_TRN_DTL                                    02231001
               WHERE FSCL_WK_END_DTE  =:ML090-FSCL-WK-END-DTE           02232001
                 AND PRCG_DTE         =:ML090-PRCG-DTE                  02233001
                 AND DEPT_NBR         =:ML090-DEPT-NBR                  02234001
                 AND MAJ_CL_NBR       =:ML090-MAJ-CL-NBR                02235001
                 AND SUB_CL_NBR       =:ML090-SUB-CL-NBR                02236001
                 AND LOC_NBR          =:ML090-LOC-NBR                   02237001
                 AND ORIG_ENT_ID      =:ML090-ORIG-ENT-ID               02238006
                 AND ORIG_LBL_SEQ_NBR IN (:ML090-ORIG-LBL-SEQ-NBR, 0)   02239005
                 AND FINCL_PRC_GP_CDE = 'BVEDIT'                        02239217
                 AND TRN_TYP_CDE      =:ML090-TRN-TYP-CDE               02239313
           END-EXEC.                                                    02240000
                                                                        02250000
           EVALUATE TRUE                                                02260000
               WHEN SQLCODE = ZERO                                      02270000
                   MOVE MLT00039-AP-TRN-CDE TO ML090-AP-TRN-CDE         02280001
                   ADD 1 TO PA-PADJ-CODE-FND                            02280101
               WHEN SQLCODE = +100                                      02281001
                   MOVE SPACES              TO ML090-AP-TRN-CDE         02281118
                                               MLT00039-AP-TRN-CDE      02281219
                   ADD 1 TO PA-PADJ-CODE-NOTFND                         02282001
               WHEN SQLCODE = -811                                      02283001
                   MOVE MLT00039-AP-TRN-CDE TO ML090-AP-TRN-CDE         02283107
                   ADD 1 TO PA-PADJ-CODE-DUPFND                         02284001
               WHEN OTHER                                               02290000
                   MOVE '8100-SELECT-PADJ-CODE' TO PV-PARAGRAPH-NAME    02300001
                   MOVE 'SELECT      '         TO PV-OPERATION-ATTEMPTED02310001
                   PERFORM 9999-SQL-ABEND                               02320000
           END-EVALUATE.                                                02330000
                                                                        02340000
      *                                                                 02340101
      ******************************************************************02341001
      *  SELECT PRICE CHANGE RESAON AND CATEGORY CODE                  *02342001
      ******************************************************************02343001
       8200-SELECT-PRCHG-CODE.                                          02344001
                                                                        02345001
           EXEC SQL                                                     02346001
               SELECT PRCHG_RSN_CDE                                     02347001
                     ,PRCHG_CTG_CDE                                     02347101
               INTO :MLT00038-PRCHG-RSN-CDE                             02347201
                   ,:MLT00038-PRCHG-CTG-CDE                             02347301
               FROM MLT_PRCHG_TRN_DTL                                   02348001
               WHERE FSCL_WK_END_DTE  =:ML090-FSCL-WK-END-DTE           02349001
                 AND PRCG_DTE         =:ML090-PRCG-DTE                  02349101
                 AND DEPT_NBR         =:ML090-DEPT-NBR                  02349201
                 AND MAJ_CL_NBR       =:ML090-MAJ-CL-NBR                02349301
                 AND SUB_CL_NBR       =:ML090-SUB-CL-NBR                02349401
                 AND LOC_NBR          =:ML090-LOC-NBR                   02349501
                 AND ORIG_ENT_ID      =:ML090-ORIG-ENT-ID               02349601
                 AND ORIG_LBL_SEQ_NBR IN (:ML090-ORIG-LBL-SEQ-NBR, 0)   02349706
                 AND FINCL_PRC_GP_CDE = 'BVEDIT'                        02349817
                 AND TRN_TYP_CDE      =:ML090-TRN-TYP-CDE               02349913
           END-EXEC.                                                    02350001
                                                                        02350101
           EVALUATE TRUE                                                02350201
               WHEN SQLCODE = ZERO                                      02350301
                   MOVE MLT00038-PRCHG-RSN-CDE TO ML090-PRCHG-RSN-CDE   02350415
                   MOVE MLT00038-PRCHG-CTG-CDE TO ML090-PRCHG-CTG-CDE   02350515
                   ADD 1 TO PA-PRCHG-CODE-FND                           02350601
               WHEN SQLCODE = +100                                      02350701
                   MOVE SPACES              TO ML090-PRCHG-RSN-CDE      02350819
                                               ML090-PRCHG-CTG-CDE      02350919
                                               MLT00038-PRCHG-RSN-CDE   02351019
                                               MLT00038-PRCHG-CTG-CDE   02351119
                   ADD 1 TO PA-PRCHG-CODE-NOTFND                        02351219
               WHEN SQLCODE = -811                                      02351319
                   MOVE MLT00038-PRCHG-RSN-CDE TO ML090-PRCHG-RSN-CDE   02351419
                   MOVE MLT00038-PRCHG-CTG-CDE TO ML090-PRCHG-CTG-CDE   02351519
                   ADD 1 TO PA-PRCHG-CODE-DUPFND                        02351619
               WHEN OTHER                                               02351719
                   MOVE '8200-SELECT-PRCHG-CODE' TO PV-PARAGRAPH-NAME   02351819
                   MOVE 'SELECT      '         TO PV-OPERATION-ATTEMPTED02351919
                   PERFORM 9999-SQL-ABEND                               02352019
           END-EVALUATE.                                                02352119
                                                                        02353019
      *                                                                 02730000
      ******************************************************************02740000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               02750000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             02760000
      ******************************************************************02770000
       9999-SQL-ABEND.                                                  02780000
                                                                        02790000
           MOVE +4013                 TO ABEND-CODE.                    02800000
           DISPLAY '**  ABEND     **'.                                  02810000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              02820000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            02830000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       02840000
                                                                        02850000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         02860000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        02870000
                                                                        02880000
           COPY DPPD004.                                                02890000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           02900000
