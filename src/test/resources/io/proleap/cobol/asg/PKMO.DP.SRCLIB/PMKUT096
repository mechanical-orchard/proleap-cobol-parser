      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PMKUT096.                                        
       AUTHOR.                 AMANDA NEWHART                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           APRIL 2003.                                      
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PMKUT096 -                                                  *        
      *      LOG ONTO ORACLE.  FIND THE CLEARANCE RECORDS FROM THE     *        
      *      SLS_RSLT_LOD TABLE.  LOAD THOSE RECORDS INTO THE          *        
      *      MDSE_LNE_CLRNC_FCT TABLE.  WHEN ALL THE CLEARANCE RECORDS *        
      *      HAVE BEEN SELECTED & INSERTED LOOK FOR THE TIERED RECORDS *        
      *      FROM THE SLS_RSLT_LOD TABLE.  INSERT THOSE RECORDS INTO   *        
      *      THE MDSE_SELN_LNE_TIER_FCT TABLE.  WHEN ALL THE TIERED    *        
      *      RECORDS HAVE BEEN SELECTED AND INSERTERD DO A COMMIT TO   *        
      *      MAKE SURE ALL THE INSERTS STAY.  THEN DISPLAY A COUNT OF  *        
      *      WHAT WAS FOUND AND INSERTED.                              *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      *    07/17/2003 - AMANDA NEWHART - WR21770                       *        
      *     - DO NOT ABEND WHEN THE SALES DATE FILE IS EMPTY.  JUST    *        
      *       DISPLAY A MESSAGE INSTEAD.                               *        
      *                                                                *        
      *    08/05/2003 - COLLEEN CUMMINGS - P000572233                  *        
      *     - ABENDED ON TRYING TO FETCH INTO A VARIABLE NOT DEFINED   *        
      *       WITH A SIGN (PV-MKDN-RTL-AMT WAS NEGATIVE - BUT GOING    *        
      *       INTO A 9(10)V99 FIELD). CHANGED ALL THE PV- AMT FIELDS   *        
      *       TO BE S9(08)V99 COMP-3 INSTEAD.                          *        
      *       "ORA-24355: ATTEMPT TO STORE A NEGATIVE NUMBER IN AN     *        
      *       UNSIGNED DISPLAY..."                                     *        
      *                                                                *        
      *    07/30/2018 - COGNIZANT - CHG0204126                         *        
      *     - ORACLE LOGON STANDARDS PROGRAM CHANGE                    *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SALES-DATE-FILE                                               
               ASSIGN                       TO INP01.                           
                                                                                
070941     SELECT ORACLE-LOGON-FILE                                             
070941         ASSIGN                       TO ORALOGON.                        
070941                                                                          
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  SALES-DATE-FILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  SALES-DATE-LINE                  PIC X(80).                          
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD           PIC X(80).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  CC-SALES-DATE-FILE.                                                  
           05  CC-SALES-DATE                 PIC X(10).                         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-SALES-DATE-EXIST-SWITCH    PIC  X(01) VALUE 'N'.              
               88  PS-SALES-DATE-EXIST                  VALUE 'Y'.              
               88  PS-SALES-DATE-NOT-EXIST              VALUE 'N'.              
           05  PS-SALES-DATE-FILE-SWITCH     PIC  X(01) VALUE 'N'.              
               88  PS-SALES-DATE-FILE-END               VALUE 'Y'.              
               88  PS-SALES-DATE-FILE-START             VALUE 'N'.              
           05  PS-CLRNC-PROMO-CSR-SWITCH     PIC X(01)  VALUE 'N'.              
               88  PS-END-CLRNC-PROMO-CSR               VALUE 'Y'.              
               88  PS-NOT-END-CLRNC-PROMO-CSR           VALUE 'N'.              
           05  PS-TIER-PROMO-CSR-SWITCH      PIC X(01)  VALUE 'N'.              
               88  PS-END-TIER-PROMO-CSR                VALUE 'Y'.              
               88  PS-NOT-END-TIER-PROMO-CSR            VALUE 'N'.              
070941     05  PS-END-OF-LOGON-FILE-SW       PIC X(1)   VALUE 'N'.              
070941         88  PS-END-OF-LOGON-FILE                 VALUE 'Y'.              
070941     05  PS-ORA-SRC-CTL-DONE-SW        PIC X(1)   VALUE 'N'.              
070941         88  PS-ORA-SRC-EMPTY                     VALUE 'Y'.              
070941                                                                          
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID              PIC X(40).                         
070941         88 CC-AUTO-LOGON                         VALUE '/'.              
070941     05  CC-ORACLE-PASSWORD            PIC X(40).                         
070941                                                                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)  VALUE                   
                                                           'PMKUT096'.          
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN         PIC S9(4)   VALUE ZEROS             
070941                                                  COMP SYNC.              
070941     05  PV-ORACLE-PASSWORD-LEN       PIC S9(4)   VALUE ZEROS             
070941                                                  COMP SYNC.              
           05  PV-SQL                       PIC S9(04)  VALUE ZEROS.            
           05  PV-TABLE-NAME                PIC  X(12)  VALUE SPACES.           
           05  PV-OPERATION-MSG             PIC  X(60)  VALUE SPACES.           
           05  PV-OPERATION-MSG-1           PIC  X(40)  VALUE SPACES.           
           05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACES.           
                                                                                
       01  PV-PROMO-VARIABLES.                                                  
           05  PV-TM-DIM-KY-DTE             PIC  X(10)   VALUE SPACES.          
           05  PV-STR-SELN-ID               PIC  9(04)   VALUE ZEROS.           
           05  PV-PROMO-ID                  PIC  9(10)   VALUE ZEROS.           
           05  PV-PROMO-INTFC-SUB-ID        PIC  9(10)   VALUE ZEROS.           
           05  PV-MDSE-SELN-LNE-ID          PIC  9(10)   VALUE ZEROS.           
           05  PV-SLS-UNT-QTY               PIC S9(08)V9(2) COMP-3              
                                                        VALUE ZEROS.            
           05  PV-SLS-COST-AMT              PIC S9(08)V9(2) COMP-3              
                                                        VALUE ZEROS.            
           05  PV-SLS-RTL-AMT               PIC S9(08)V9(2) COMP-3              
                                                        VALUE ZEROS.            
           05  PV-MKDN-RTL-AMT              PIC S9(08)V9(2) COMP-3              
                                                        VALUE ZEROS.            
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-CLRNC-PROMO-CNT           PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-TIER-PROMO-CNT            PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-CLRNC-INSERT              PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-TIER-INSERT               PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - SET UP ORACLE                                              *         
      *  - RUN PARAGRAPHS TO LOAD THE PMT_MDSE_LNE_CLRNC_VEC TABLE.   *         
      *    THIS IS FOR THE CLRNC PROMOS WITHOUT PROMO ITEMS.          *         
      *  - RUN PARAGRAPHS TO LOAD THE PMT_MDSSE_SELN_LNE_TIER_FCT     *         
      *    TABLE.  THIS IS FOR THE TIERED PROMOS WITHOUT PROMO ITEMS. *         
      *  - COMMIT WHAT WAS JUST INSERTED.                             *         
      *  - DISPLAY TOTALS.                                            *         
      * ==> PERFORMED FROM: START                                     *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
070941     PERFORM B110-ORACLE-SETUP.                                           
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B200-READ-SALES-DATE-FILE.                                   
                                                                                
           IF PS-SALES-DATE-EXIST                                               
                                                                                
               PERFORM C200-DECLARE-CLRNC-PROMO-CSR                             
               PERFORM C300-OPEN-CLRNC-PROMO-CSR                                
               PERFORM C400-FETCH-CLRNC-PROMO-CSR                               
               PERFORM C500-PROCESS-CLRNC-PROMO-CSR                             
                   UNTIL PS-END-CLRNC-PROMO-CSR                                 
               PERFORM C600-CLOSE-CLRNC-PROMO-CSR                               
                                                                                
               PERFORM C210-DECLARE-TIER-PROMO-CSR                              
               PERFORM C310-OPEN-TIER-PROMO-CSR                                 
               PERFORM C410-FETCH-TIER-PROMO-CSR                                
               PERFORM C510-PROCESS-TIER-PROMO-CSR                              
                   UNTIL PS-END-TIER-PROMO-CSR                                  
               PERFORM C610-CLOSE-TIER-PROMO-CSR                                
                                                                                
               PERFORM C120-COMMIT                                              
                                                                                
               DISPLAY 'FINAL CLRNC PROMO COUNT  = ' PA-CLRNC-PROMO-CNT         
               DISPLAY 'FINAL TIER PROMO COUNT  = ' PA-TIER-PROMO-CNT           
               DISPLAY 'FINAL CLRNC INSERT COUNT = ' PA-CLRNC-INSERT            
               DISPLAY 'FINAL TIER INSERT COUNT = ' PA-TIER-INSERT              
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM B900-CLOSE-FILES.                                            
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES.                                              *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE INPUT FILE                                        *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  SALES-DATE-FILE.                                         
                                                                                
070941 B110-ORACLE-SETUP.                                                       
070941                                                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     PERFORM B120-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-CURRENT-PROGRAM-NAME                                  
070941                 ' - ORACLE LOGON CONTROL CARD NOT PROVIDED'              
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-CURRENT-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'        
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-CURRENT-PROGRAM-NAME ' -LOGON TO ORACLE AS USER '        
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-CURRENT-PROGRAM-NAME                                  
070941                 ' - ERROR LOGGING ON TO ORACLE'                          
070941         PERFORM Z100-ABEND                                               
070941     END-IF.                                                              
070941                                                                          
070941******************************************************************        
070941* NEEDS TO BE IN FOR NOW TIL KURT FIXES CURSOR SHARING BUG                
070941* OTHERWISE GET AN ORACLE ERROR (IE: 3113).                               
070941******************************************************************        
070941     EXEC SQL                                                             
070941      ALTER SESSION                                                       
070941            SET CURSOR_SHARING = 'EXACT'                                  
070941     END-EXEC.                                                            
070941                                                                          
070941                                                                          
070941*****************************************************************         
070941*  B120-READ-ORACLE-LOGON.                                      *         
070941* ==> PROCESSES:                                                *         
070941*   - READ ORCALE CONTROL CARD                                  *         
070941* ==> PERFORMED FROM:  B110-ORACLE-SETUP                        *         
070941*****************************************************************         
070941 B120-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
070941                                                                          
      *****************************************************************         
      * B200-READ-SALES-DATE-FILE.                                    *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE SALES DATE FILE                                *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B200-READ-SALES-DATE-FILE.                                               
                                                                                
           READ SALES-DATE-FILE INTO CC-SALES-DATE-FILE                         
              AT END                                                            
                 SET PS-SALES-DATE-FILE-END TO TRUE                             
           END-READ.                                                            
                                                                                
           IF (PS-SALES-DATE-FILE-END) OR (CC-SALES-DATE-FILE = SPACES)         
               DISPLAY 'SALES DATE HAS NOTHING TO PROCESS'                      
               DISPLAY '**** END OF PROGRAM ****'                               
               SET PS-SALES-DATE-NOT-EXIST TO TRUE                              
           ELSE                                                                 
               DISPLAY 'CC-SALES-DATE = ' CC-SALES-DATE                         
               SET PS-SALES-DATE-EXIST TO TRUE                                  
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE SALES DATE FILE                                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  SALES-DATE-FILE.                                              
                                                                                
      *****************************************************************         
      * C100-INSERT-CLRNC                                             *         
      * ==> PROCESSES:                                                *         
      *  - INSERT THE CLRNC ROW THAT WAS SELECTED FROM THE            *         
      *    PMT_SLS_RSLT_LOD TABLE.                                    *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = INSERT WAS SUCCEFULL                                   *         
      * ==> PERFORMED FROM: C500-PROCESS-CLRNC-PROMO-CSR              *         
      *****************************************************************         
       C100-INSERT-CLRNC.                                                       
                                                                                
           EXEC SQL                                                             
               INSERT INTO PMT_MDSE_SELN_LNE_CLRNC_FCT                          
                   (PROMO_ID                                                    
                   ,MDSE_SELN_LNE_ID                                            
                   ,TM_DIM_KY_DTE                                               
                   ,STR_SELN_ID                                                 
                   ,SLS_UNT_QTY                                                 
                   ,SLS_COST_AMT                                                
                   ,SLS_RTL_AMT                                                 
                   ,MKDN_RTL_AMT)                                               
               VALUES                                                           
                   (:PV-PROMO-ID                                                
                   ,:PV-MDSE-SELN-LNE-ID                                        
                   ,:PV-TM-DIM-KY-DTE                                           
                   ,:PV-STR-SELN-ID                                             
                   ,:PV-SLS-UNT-QTY                                             
                   ,:PV-SLS-COST-AMT                                            
                   ,:PV-SLS-RTL-AMT                                             
                   ,:PV-MKDN-RTL-AMT)                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 ADD 1 TO PA-CLRNC-INSERT                                       
                                                                                
             WHEN OTHER                                                         
               DISPLAY 'PROMO ID             = ' PV-PROMO-ID                    
               DISPLAY 'PV-MDSE-SELN-LNE-ID  = ' PV-MDSE-SELN-LNE-ID            
               DISPLAY 'PV-TM-DIM-KY-DTE     = ' PV-TM-DIM-KY-DTE               
               DISPLAY 'PV-STR-SELN-ID       = ' PV-STR-SELN-ID                 
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'DUAL'                TO PV-TABLE-NAME                      
               MOVE 'C100-INSERT-CLRNC'   TO PV-PARAGRAPH-NAME                  
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C110-INSERT-TIER                                              *         
      * ==> PROCESSES:                                                *         
      *  - INSERT THE TIERED ROW THAT WAS SELECTED FROM THE           *         
      *    PMT_PROMO_ITM_SLS_RSLT_LOD TABLE.                          *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = INSERT WAS SUCCEFULL                                   *         
      * ==> PERFORMED FROM: C510-PROCESS-TIER-PROMO-CSR               *         
      *****************************************************************         
       C110-INSERT-TIER.                                                        
                                                                                
           EXEC SQL                                                             
               INSERT INTO PMT_MDSE_SELN_LNE_TIER_FCT                           
                   (PROMO_ID                                                    
                   ,MDSE_SELN_LNE_ID                                            
                   ,PROMO_INTFC_SUB_ID                                          
                   ,TM_DIM_KY_DTE                                               
                   ,STR_SELN_ID                                                 
                   ,SLS_RTL_AMT                                                 
                   ,SLS_UNT_QTY                                                 
                   ,MKDN_RTL_AMT                                                
                   ,SLS_COST_AMT)                                               
               VALUES                                                           
                   (:PV-PROMO-ID                                                
                   ,:PV-MDSE-SELN-LNE-ID                                        
                   ,:PV-PROMO-INTFC-SUB-ID                                      
                   ,:PV-TM-DIM-KY-DTE                                           
                   ,:PV-STR-SELN-ID                                             
                   ,:PV-SLS-RTL-AMT                                             
                   ,:PV-SLS-UNT-QTY                                             
                   ,:PV-MKDN-RTL-AMT                                            
                   ,:PV-SLS-COST-AMT)                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 ADD 1 TO PA-TIER-INSERT                                        
                 CONTINUE                                                       
             WHEN OTHER                                                         
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'DUAL'                TO PV-TABLE-NAME                      
               MOVE 'C100-INSERT-CLRNC'   TO PV-PARAGRAPH-NAME                  
               PERFORM Z100-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C120-COMMIT                                                   *         
      * ==> PROCESSES:                                                *         
      *  - SAVE WHAT HAS BEEN DONE TO THE DATABASE SO FAR             *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = COMMIT WAS SUCCEFULL                                   *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       C120-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
067400                                                                          
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
067600         CONTINUE                                                         
             WHEN OTHER                                                         
067800         MOVE 'PGM ABEND DURING A COMMIT' TO PV-OPERATION-MSG             
067900         MOVE 'C120-COMMIT'               TO PV-PARAGRAPH-NAME            
068000         PERFORM Z910-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C200-DECLARE-CLRNC-PROMO-CSR                                  *         
      * ==> PROCESSES:                                                *         
      *  - DECLARE THE CURSOR USED TO FIND THE CLRNC NONPROMO ITEMS.  *         
      *  - IN THE CURSOR USE SUM TO ADD UP ALL THE ROWS ARE GROUPED   *         
      *    TOGETHER                                                   *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C200-DECLARE-CLRNC-PROMO-CSR.                                            
                                                                                
           EXEC SQL                                                             
               DECLARE CLRNCPROMOCSR                                            
               CURSOR FOR                                                       
                   SELECT DISTINCT                                              
                        A.TRN_SLS_DTE                                           
                       ,A.PROMO_ID                                              
                       ,A.MDSE_SELN_LNE_ID                                      
                       ,B.STR_SELN_ID                                           
                       ,SUM(A.SLS_UNT_QTY)                                      
                       ,SUM(A.SLS_COST_AMT)                                     
                       ,SUM(A.SLS_RTL_AMT)                                      
                       ,SUM(A.MKDN_RTL_AMT)                                     
                   FROM                                                         
                        PMTT_SLS_RSLT_LOD A                                     
                       ,PMTT_STR_SELN_STR_ASSOC B                               
                   WHERE                                                        
                        A.PROMO_ID = B.PROMO_ID                                 
                    AND A.STR_NBR = B.STR_NBR                                   
                    AND A.PROMO_SLS_TYP_CDE = 'C'                               
                    AND A.TRN_SLS_DTE =                                         
                                TO_DATE(:CC-SALES-DATE, 'YYYY-MM-DD')           
                   GROUP BY                                                     
                        A.TRN_SLS_DTE                                           
                       ,A.PROMO_ID                                              
                       ,A.MDSE_SELN_LNE_ID                                      
                       ,B.STR_SELN_ID                                           
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C210-DECLARE-TIER-PROMO-CSR                                   *         
      * ==> PROCESSES:                                                *         
      *  - DECLARE THE CURSOR USED TO FIND THE TIERED PROMO ITEMS.    *         
      *  - IN THE CURSOR USE THE SUM TO ADD UP ALL THE ROWS THAT ARE  *         
      *    GROUPED TOGETHER.                                          *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C210-DECLARE-TIER-PROMO-CSR.                                             
                                                                                
           EXEC SQL                                                             
               DECLARE TIERPROMOCSR                                             
               CURSOR FOR                                                       
                   SELECT DISTINCT                                              
                        A.TRN_SLS_DTE                                           
                       ,A.PROMO_ID                                              
                       ,A.PROMO_INTFC_SUB_ID                                    
                       ,B.STR_SELN_ID                                           
                       ,SUM(A.SLS_UNT_QTY)                                      
                       ,SUM(A.SLS_COST_AMT)                                     
                       ,SUM(A.SLS_RTL_AMT)                                      
                       ,SUM(A.MKDN_RTL_AMT)                                     
                   FROM                                                         
                        PMTT_SLS_RSLT_LOD A                                     
                       ,PMTT_STR_SELN_STR_ASSOC B                               
                   WHERE                                                        
                        A.PROMO_ID = B.PROMO_ID                                 
                    AND A.STR_NBR = B.STR_NBR                                   
                    AND A.PROMO_SLS_TYP_CDE = 'T'                               
                    AND A.TRN_SLS_DTE =                                         
                                TO_DATE(:CC-SALES-DATE, 'YYYY-MM-DD')           
                   GROUP BY                                                     
                        A.TRN_SLS_DTE                                           
                       ,A.PROMO_ID                                              
                       ,A.PROMO_INTFC_SUB_ID                                    
                       ,B.STR_SELN_ID                                           
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C300-OPEN-CLRNC-PROMO-CSR.                                    *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE CLRNC CURSOR.                                     *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = WAS OPENED SUCCESSFULLY.                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C300-OPEN-CLRNC-PROMO-CSR.                                               
                                                                                
           EXEC SQL                                                             
                 OPEN CLRNCPROMOCSR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C300-OPEN-CLRNC-PROMO-CSR   '                          
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON CLRNCPROMOCSR '                          
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C310-OPEN-TIER-PROMO-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE TIERED CURSOR.                                    *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = WAS OPENED SUCCESSFULLY.                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C310-OPEN-TIER-PROMO-CSR.                                                
                                                                                
           EXEC SQL                                                             
                 OPEN TIERPROMOCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C310-OPEN-TIER-PROMO-CSR   '                           
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON TIERPROMOCSR '                           
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C400-FETCH-CLRNC-PROMO-CSR.                                   *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C500-PROCESS-CLRNC-PROMO-CSR*        
      *****************************************************************         
       C400-FETCH-CLRNC-PROMO-CSR.                                              
                                                                                
           EXEC SQL                                                             
               FETCH CLRNCPROMOCSR                                              
                INTO :PV-TM-DIM-KY-DTE                                          
                    ,:PV-PROMO-ID                                               
                    ,:PV-MDSE-SELN-LNE-ID                                       
                    ,:PV-STR-SELN-ID                                            
                    ,:PV-SLS-UNT-QTY                                            
                    ,:PV-SLS-COST-AMT                                           
                    ,:PV-SLS-RTL-AMT                                            
                    ,:PV-MKDN-RTL-AMT                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-CLRNC-PROMO-CNT                                  
                                                                                
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'CLRNC PROMO COUNT FROM FETCH = '                    
                                                   PA-CLRNC-PROMO-CNT           
                   DISPLAY 'END OF CLRNC PROMO CSR'                             
                   SET PS-END-CLRNC-PROMO-CSR TO TRUE                           
                                                                                
               WHEN OTHER                                                       
                   DISPLAY 'PROMO ID = ' PV-PROMO-ID                            
                   DISPLAY 'PRIOR ROW SUCCESSFULLY FETCHED = '                  
                           PA-CLRNC-PROMO-CNT                                   
                   MOVE 'C400-FETCH-CLRNC-PROMO-CSR     '                       
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON CLRNCPROMOCSR   '                       
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C410-FETCH-TIER-PROMO-CSR.                                    *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C510-PROCESS-TIER-PROMO-CSR*         
      *****************************************************************         
       C410-FETCH-TIER-PROMO-CSR.                                               
                                                                                
           EXEC SQL                                                             
               FETCH TIERPROMOCSR                                               
                INTO :PV-TM-DIM-KY-DTE                                          
                    ,:PV-PROMO-ID                                               
                    ,:PV-PROMO-INTFC-SUB-ID                                     
                    ,:PV-STR-SELN-ID                                            
                    ,:PV-SLS-UNT-QTY                                            
                    ,:PV-SLS-COST-AMT                                           
                    ,:PV-SLS-RTL-AMT                                            
                    ,:PV-MKDN-RTL-AMT                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-TIER-PROMO-CNT                                   
                                                                                
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'TIER PROMO COUNT FROM FETCH = '                     
                                                    PA-TIER-PROMO-CNT           
                   DISPLAY 'END OF TIER PROMO CSR'                              
                   SET PS-END-TIER-PROMO-CSR TO TRUE                            
                                                                                
               WHEN OTHER                                                       
                   DISPLAY 'PROMO ID = ' PV-PROMO-ID                            
                   MOVE 'C410-FETCH-TIER-PROMO-CSR     '                        
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON TIERPROMOCSR   '                        
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C500-PROCESS-CLRNC-PROMO-CSR.                                 *         
      * ==> PROCESSES:                                                *         
      *  - CALL THE INSERT PARAGRAPH                                  *         
      *  - INITIALIZE THE VARIABLES TO MAKE SURE NO DATA IS LEFT OVER *         
      *    FROM THE PREVIOUS FETCH.                                   *         
      *  - CALL FETCH PARAGRAPH TO GET THE NEXT ROW FROM THE CURSOR   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C500-PROCESS-CLRNC-PROMO-CSR.                                            
                                                                                
           PERFORM C100-INSERT-CLRNC.                                           
                                                                                
           INITIALIZE PV-PROMO-VARIABLES.                                       
           PERFORM C400-FETCH-CLRNC-PROMO-CSR.                                  
                                                                                
      *****************************************************************         
      * C510-PROCESS-TIER-PROMO-CSR.                                  *         
      * ==> PROCESSES:                                                *         
      *  - CALL THE INSERT PARAGRAPH                                  *         
      *  - INITIALIZE THE VARIABLES TO MAKE SURE NO DATA IS LEFT OVER *         
      *    FROM THE PREVIOUS FETCH.                                   *         
      *  - CALL FETCH PARAGRAPH TO GET THE NEXT ROW FROM THE CURSOR   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C510-PROCESS-TIER-PROMO-CSR.                                             
                                                                                
           PERFORM C110-INSERT-TIER.                                            
                                                                                
           INITIALIZE PV-PROMO-VARIABLES.                                       
           PERFORM C410-FETCH-TIER-PROMO-CSR.                                   
                                                                                
      *****************************************************************         
      * C600-CLOSE-CLRNC-PROMO-CSR.                                   *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE CLRNC CURSOR                                     *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = THE CLOSE WAS SUCCESSFUL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C600-CLOSE-CLRNC-PROMO-CSR.                                              
                                                                                
           EXEC SQL                                                             
                CLOSE CLRNCPROMOCSR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C600-CLOSE-CLRNC-PROMO-CSR   '                          
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON CLRNCPROMOCSR     '                      
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C610-CLOSE-TIER-PROMO-CSR.                                    *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE TIERED CURSOR                                    *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = THE CLOSE WAS SUCCESSFUL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C610-CLOSE-TIER-PROMO-CSR.                                               
                                                                                
           EXEC SQL                                                             
                CLOSE TIERPROMOCSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C610-CLOSE-TIER-PROMO-CSR   '                           
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON PROMO-CSR     '                          
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PMPD700.                                                        
                                                                                
      *****************************************************************         
      * Z100-ABEND.                                                   *         
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE            *         
      *****************************************************************         
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                           
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
