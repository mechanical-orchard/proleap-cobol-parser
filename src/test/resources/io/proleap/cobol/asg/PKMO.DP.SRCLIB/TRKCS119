000100*-----------------------*                                         05/25/95
000200 IDENTIFICATION DIVISION.                                         TRKCS119
000300*-----------------------*                                            LV001
000400 PROGRAM-ID.    TRKCS119.                                         TRKCS119
000500 AUTHOR.        DAN HINTZ.                                        TRKCS119
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                          TRKCS119
000700 DATE-WRITTEN.  01-05-94.                                         TRKCS119
000800 DATE-COMPILED.                                                   TRKCS119
000900*----------------------------------------------------------------*TRKCS119
001000*    Y119 - OPEN SHIPMENT SELECTION CRITERIA SPECIFICATION       *TRKCS119
001100*                                                                *TRKCS119
001200*    THIS PROGRAM ALLOWS THE USER ENTER A DATE RANGE THAT IS     *TRKCS119
001300*    USED TO SELECT A LIST OF OPEN SHIPMENTS.  THESE TWO DATES   *TRKCS119
001400*    ARE NOT REQUIRED ENTRIES.  IF THEY ARE ENTERED, A CHECK IS  *TRKCS119
001500*    DONE TO MAKE SURE THEY WERE ENTERED IN A VALID DATE FORMAT  *TRKCS119
001600*    AND CONTROL IS THEN PASSED TO THE OPEN SHIPMENT LIST PROGRAM*TRKCS119
001700*    (TRKCS120).  IF THEY ARE NOT ENTERED ON THE SCREEN, CONTROL *TRKCS119
001800*    IS PASSED TO TRKCS120 WHERE THESE DATES ARE SET TO DEFAULT  *TRKCS119
001900*    VALUES.                                                     *TRKCS119
002000*----------------------------------------------------------------*TRKCS119
001300* CHANGED 10-27-2014 CHERI PARMLEY           CHG0094517          *TRKCS114
001300*         EXPANDED PO# FROM 7 TO 8 DIGITS                        *TRKCS114
002000*----------------------------------------------------------------*TRKCS119
273427* CHANGED 08/16/2022    BY BARB SCHULTZ    WR: CHG0273427         TRKCS119
273427* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE     TRKCS119
273427* MAKING THE CALL DYNAMIC VS STATIC.                              TRKCS119
273427*-----------------------------------------------------------------TRKCS119
CICS  * CHANGED 11/21/2022    BY JIM HUNTER      WR: CHG0278495                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
002300                                                                  TRKCS119
002400 ENVIRONMENT DIVISION.                                            TRKCS119
002500                                                                  TRKCS119
002600                                                                  TRKCS119
002700                                                                  TRKCS119
002800 DATA DIVISION.                                                   TRKCS119
002900                                                                  TRKCS119
003000                                                                  TRKCS119
003100 WORKING-STORAGE SECTION.                                         TRKCS119
003200*                                                                 TRKCS119
003300 01  DK-DB2-KEYS.                                                 TRKCS119
003400     05  DK-OWNER-SCAC-ID             PIC X(04)  VALUE SPACES.    TRKCS119
003500*                                                                 TRKCS119
003600 01  PC-PROGRAM-CONSTANTS.                                        TRKCS119
003700     05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'TR119A  '. TRKCS119
003800     05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'TRKM119 '. TRKCS119
003900     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'TRKCS119'. TRKCS119
004000     05  PC-TSYMSG-NUMBERS.                                       TRKCS119
004100         10  PC-TSYMSG-00008       PIC  9(05)  VALUE  00008.      TRKCS119
004200         10  PC-TSYMSG-00009       PIC  9(05)  VALUE  00009.      TRKCS119
004300         10  PC-TSYMSG-00047       PIC  9(05)  VALUE  00047.      TRKCS119
004400         10  PC-TSYMSG-00069       PIC  9(05)  VALUE  00069.      TRKCS119
004500         10  PC-TSYMSG-00070       PIC  9(05)  VALUE  00070.      TRKCS119
004600         10  PC-TSYMSG-00095       PIC  9(05)  VALUE  00095.      TRKCS119
004700         10  PC-TSYMSG-00143       PIC  9(05)  VALUE  00143.      TRKCS119
004800         10  PC-TSYMSG-00224       PIC  9(05)  VALUE  00224.      TRKCS119
004900         10  PC-TSYMSG-00778       PIC  9(05)  VALUE  00778.      TRKCS119
005100*                                                                 TRKCS119
005200 01  PV-PROGRAM-VARIABLES.                                        TRKCS119
005300     05  PV-CICS-RESPONSE          PIC S9(04)  VALUE +0           TRKCS119
005400                                               COMP SYNC.         TRKCS119
005500*                                                                 TRKCS119
005600 01  PS-PROGRAM-SWITCHES.                                         TRKCS119
005700     05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.        TRKCS119
005800         88  PS-ERROR                          VALUE  'Y'.        TRKCS119
005900         88  PS-NO-ERROR                       VALUE  'N'.        TRKCS119
006000                                                                  TRKCS119
006100*---------------------------------------------------------------* TRKCS119
006200*  RECORD LAYOUT FOR THE LIST SELECTION TEMP STORAGE QUEUE.     * TRKCS119
006300*---------------------------------------------------------------* TRKCS119
006400                                                                  TRKCS119
006500     COPY TRWS006.                                                TRKCS119
006600                                                                  TRKCS119
006700/                                                                 TRKCS119
006800*----------------------------------------------------------------*TRKCS119
006900*    MAP LAYOUT                                                  *TRKCS119
007000*----------------------------------------------------------------*TRKCS119
007100                                                                  TRKCS119
007200     COPY TRKM119.                                                TRKCS119
007300     EJECT                                                        TRKCS119
007400 EJECT                                                            TRKCS119
007500***************************************************************** TRKCS119
007600*    COPY BOOKS FOR THE VALIDATION ROUTINE FOR THE FREIGHT BILL   TRKCS119
007700*    BILL OF LADING, AND VEHICLE NUMBER                           TRKCS119
007800***************************************************************** TRKCS119
007900     COPY RCWS023.                                                TRKCS119
008000                                                                  TRKCS119
008100*----------------------------------------------------------------*TRKCS119
008200*    ATTRIBUTE SETTINGS COPYBOOK.                                *TRKCS119
008300*----------------------------------------------------------------*TRKCS119
008400                                                                  TRKCS119
008500     COPY DPWS015.                                                TRKCS119
008600                                                                  TRKCS119
008700*----------------------------------------------------------------*TRKCS119
008800*    FUNCTION KEYS COPYBOOK                                      *TRKCS119
008900*----------------------------------------------------------------*TRKCS119
009000                                                                  TRKCS119
009100     COPY DPWS016.                                                TRKCS119
009200 EJECT                                                            TRKCS119
009300******************************************************************TRKCS119
009400** THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT    **TRKCS119
009500** SUB-ROUTINE (DPKUT100).                                      **TRKCS119
009600******************************************************************TRKCS119
009700     COPY DPWS010I.                                               TRKCS119
009800 EJECT                                                            TRKCS119
009900*----------------------------------------------------------------*TRKCS119
010000*  WORK AREA PASSED IN CALL TO DPKUT500                          *TRKCS119
010100*----------------------------------------------------------------*TRKCS119
010200     COPY DPWS500.                                                TRKCS119
010300 EJECT                                                            TRKCS119
010400******************************************************************TRKCS119
010500** THE CALLING PARAMETER LIST (DPWS030) FOR THE CICS            **TRKCS119
010600** ARCHITECTURE API (BLACK BOX) SUB-ROUTINE.                    **TRKCS119
010700******************************************************************TRKCS119
010800     COPY DPWS030.                                                TRKCS119
CICS       COPY DPWS930.                                                TRKCS119
010900 EJECT                                                            TRKCS119
011000***************************************************************** TRKCS119
011100*    STANDARD COMMAREA.                                           TRKCS119
011200***************************************************************** TRKCS119
011300                                                                  TRKCS119
011400     COPY DPWS020.                                                TRKCS119
011500     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                TRKCS119
011600                                                                  TRKCS119
011700***************************************************************** TRKCS119
011800*    INTER-APPLICATION COMMAREA.                                  TRKCS119
011900***************************************************************** TRKCS119
012000                                                                  TRKCS119
012100         10  INTER-APPL-COMM-AREA PIC X(200).                     TRKCS119
012200     COPY TRWS005.                                                TRKCS119
012300     COPY TRWS007.                                                TRKCS119
012400     COPY TRWS009.                                                TRKCS119
012500***************************************************************** TRKCS119
012600*    SPECIFIC COMMMAREA FOR TRKCS119.                             TRKCS119
012700***************************************************************** TRKCS119
012800         10  ASC-SPECIFIC-COMMAREA.                               TRKCS119
012900             15  ASC-DELIVERY-SCAC-CDE       PIC  X(04).          TRKCS119
      *CLP 10/27/14                                                             
013000             15  ASC-PO-NBR                  PIC X(08).           TRKCS119
013100             15  ASC-PO-NBR-N REDEFINES ASC-PO-NBR                TRKCS119
013200                                             PIC 9(08).           TRKCS119
013300             15  ASC-PRO-NBR                 PIC  X(22).          TRKCS119
013400             15  ASC-BOL-NBR                 PIC  X(30).          TRKCS119
013500             15  ASC-TRLR-NBR                PIC  X(10).          TRKCS119
013600             15  ASC-TRIP-ID                 PIC S9(09) COMP.     TRKCS119
013700             15  ASC-OPER-UNIT-ID            PIC S9(04) COMP.     TRKCS119
013800             15  ASC-FCLTY-ID                PIC S9(04) COMP.     TRKCS119
013900             15  ASC-ORIGINAL-SCAC-CDE       PIC  X(04).          TRKCS119
014000             15  ASC-ETA-FROM-DATE.                               TRKCS119
014100                 20  ASC-ETA-FROM-DATE-MM    PIC  X(02).          TRKCS119
014200                 20  ASC-ETA-FROM-DATE-DD    PIC  X(02).          TRKCS119
014300                 20  ASC-ETA-FROM-DATE-CCYY  PIC  X(04).          TRKCS119
014400             15  ASC-ETA-FROM-DATE-ISO       PIC  X(10).          TRKCS119
014500             15  ASC-ETA-TO-DATE.                                 TRKCS119
014600                 20  ASC-ETA-TO-DATE-MM      PIC  X(02).          TRKCS119
014700                 20  ASC-ETA-TO-DATE-DD      PIC  X(02).          TRKCS119
014800                 20  ASC-ETA-TO-DATE-CCYY    PIC  X(04).          TRKCS119
014900             15  ASC-ETA-TO-DATE-ISO         PIC  X(10).          TRKCS119
015000             15  ASC-USE-SELECTION-QUEUE-IND                      TRKCS119
015100                                        PIC  X(01).               TRKCS119
015200                 88  ASC-USE-SELECTION-QUEUE        VALUE 'Y'.    TRKCS119
015300             15  ASC-SEL-QUEUE-NAME     PIC  X(08).               TRKCS119
015400             15  ASC-NUMBER-OF-TS-ITEMS PIC S9(05)  COMP-3.       TRKCS119
015500             15  ASC-NUMBER-OF-SELECTED-ITEMS                     TRKCS119
015600                                        PIC S9(09)  COMP-3.       TRKCS119
015700             15  ASC-SEL-SUB            PIC S9(04)  COMP.         TRKCS119
015800             15  ASC-SEL-ITEM           PIC S9(04)  COMP.         TRKCS119
015900                                                                  TRKCS119
016000     EJECT                                                        TRKCS119
016100*----------------------------------------------------------------*TRKCS119
016200*    ABEND PROCESSING COPYBOOK.                                  *TRKCS119
016300*----------------------------------------------------------------*TRKCS119
016400                                                                  TRKCS119
016500     COPY DPWS013.                                                TRKCS119
016600                                                                  TRKCS119
016700                                                                  TRKCS119
016800 EJECT                                                            TRKCS119
016900 LINKAGE SECTION.                                                 TRKCS119
017000                                                                  TRKCS119
017100 01  DFHCOMMAREA.                                                 TRKCS119
017200     05  FILLER                         OCCURS  1 TO 4072 TIMES   TRKCS119
017300                                        DEPENDING ON EIBCALEN.    TRKCS119
017400         10  FILLER                     PIC X.                    TRKCS119
017500                                                                  TRKCS119
017600                                                                  TRKCS119
017700 PROCEDURE DIVISION.                                              TRKCS119
017800***************************************************************** TRKCS119
017900** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. ** TRKCS119
018000** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  ** TRKCS119
018100** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            ** TRKCS119
018200**                                                             ** TRKCS119
018300** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  ** TRKCS119
018400** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       ** TRKCS119
018500***************************************************************** TRKCS119
018600 0000-MAIN-MODULE.                                                TRKCS119
018700     INITIALIZE DP030-CICS-API-FIELDS.                            TRKCS119
018800     MOVE +1                       TO DP030-NUMBER-OF-MAPS.       TRKCS119
018900     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          TRKCS119
019000     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         TRKCS119
019100     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       TRKCS119
019200     MOVE LENGTH OF TR119AI        TO DP030-MAP-LENGTH (1).       TRKCS119
019300     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     TRKCS119
019400     PERFORM 0001-CALL-CICS-ARCH-API.                             TRKCS119
019500*                                                                 TRKCS119
019600     PERFORM 1000-CONTROL-PROCESSING                              TRKCS119
019700*                                                                 TRKCS119
019800     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     TRKCS119
019900     PERFORM 0001-CALL-CICS-ARCH-API.                             TRKCS119
020000                                                                  TRKCS119
020100 0001-CALL-CICS-ARCH-API.                                         TRKCS119
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
020300                                    DFHCOMMAREA                           
020400                                    DP030-CICS-API-FIELDS                 
020500                                    DP020-STANDARD-COMMAREA               
020600                                    TR119AI.                              
020700*                                                                 TRKCS119
020800     IF  DP030-RC-CALL-SUCCESSFUL                                 TRKCS119
020900         CONTINUE                                                 TRKCS119
021000     ELSE                                                         TRKCS119
021100         SET DP013-NO-ROLLBACK                                    TRKCS119
021200             DP013-XCTL-DISPLAY-RESTART                           TRKCS119
021300             DP013-CICS-ABEND      TO TRUE                        TRKCS119
021400         MOVE 'BEFORE 0000-MAIN-MODULE'                           TRKCS119
021500                                   TO DP013-PARAGRAPH             TRKCS119
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE OTRKCS119
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)      TRKCS119
021800         MOVE DP030-RETURN-CODE                                   TRKCS119
021900                                   TO DP013-MESSAGE-TEXT (3)      TRKCS119
022000         PERFORM DP013-0000-PROCESS-ABEND                         TRKCS119
022100     END-IF.                                                      TRKCS119
022200 EJECT                                                            TRKCS119
022300*----------------------------------------------------------------*TRKCS119
022400*    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *TRKCS119
022500*    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *TRKCS119
022600*----------------------------------------------------------------*TRKCS119
022700                                                                  TRKCS119
022800 1000-CONTROL-PROCESSING.                                         TRKCS119
022900                                                                  TRKCS119
023000     EVALUATE TRUE                                                TRKCS119
023100         WHEN DP020-NEXT-ACT-INITIAL                              TRKCS119
023200             INITIALIZE ASC-SPECIFIC-COMMAREA                     TRKCS119
023300**DANH       IF  TR007-NUMBER-OF-SELECTED-ITEMS > 1               TRKCS119
023400             IF  TR007-NUMBER-OF-SELECTED-ITEMS > 0               TRKCS119
023500**DANH           MOVE PC-TSYMSG-00047                             TRKCS119
023600                 MOVE PC-TSYMSG-00778                             TRKCS119
023700                                 TO DP020-MSG-NUMBER              TRKCS119
023800                 SET DP020-MSG-WARNING                            TRKCS119
023900                     DP020-NEXT-ACT-APPL-ERROR                    TRKCS119
024000                                 TO TRUE                          TRKCS119
024100             ELSE                                                 TRKCS119
024200                 SET ASC-USE-SELECTION-QUEUE                      TRKCS119
024300                                 TO TRUE                          TRKCS119
024400                 MOVE TR007-NUMBER-OF-SELECTED-ITEMS              TRKCS119
024500                                 TO ASC-NUMBER-OF-SELECTED-ITEMS  TRKCS119
024600                 MOVE TR007-NUMBER-OF-TS-ITEMS                    TRKCS119
024700                                 TO ASC-NUMBER-OF-TS-ITEMS        TRKCS119
024800                 MOVE TR007-SEL-QUEUE-NAME                        TRKCS119
024900                                 TO ASC-SEL-QUEUE-NAME            TRKCS119
025000                 MOVE TR007-TRIP-ID                               TRKCS119
025100                                 TO ASC-TRIP-ID                   TRKCS119
025200**DANH           PERFORM 1120-PREPARE-LIST-ENTRY                  TRKCS119
025300                                                                  TRKCS119
025400                 PERFORM 4000-BUILD-INITIAL-PANEL                 TRKCS119
025500             END-IF                                               TRKCS119
025600                                                                  TRKCS119
025700         WHEN DP020-NEXT-ACT-READ-MAP                             TRKCS119
025800             PERFORM 2000-FKEY-PROCESSING                         TRKCS119
025900                                                                  TRKCS119
026000         WHEN DP020-NEXT-ACT-RETURN                               TRKCS119
026100             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 TRKCS119
026200             PERFORM 3000-EDIT-DATA-IN-COMMAREA                   TRKCS119
026300                                                                  TRKCS119
026400         WHEN OTHER                                               TRKCS119
026500             SET DP013-LOGIC-ABEND                                TRKCS119
026600                 DP013-NO-ROLLBACK TO TRUE                        TRKCS119
026700             MOVE '1000-CONTROL-PROCESSING'                       TRKCS119
026800                                   TO  DP013-PARAGRAPH            TRKCS119
026900             MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'   TRKCS119
027000                                   TO  DP013-MESSAGE-TEXT(1)      TRKCS119
027100             MOVE DP020-NEXT-APPL-ACTIVITY                        TRKCS119
027200                                   TO DP013-MESSAGE-TEXT(2)       TRKCS119
027300             PERFORM DP013-0000-PROCESS-ABEND                     TRKCS119
027400     END-EVALUATE.                                                TRKCS119
027500                                                                  TRKCS119
027600*----------------------------------------------------------------*TRKCS119
027700* ENTRY IS FROM A LIST.  GET THE FORMAT NUMBER FROM THE TEMP     *TRKCS119
027800* STORAGE QUEUE PASSED FROM THE LIST.                            *TRKCS119
027900*----------------------------------------------------------------*TRKCS119
028000 1120-PREPARE-LIST-ENTRY.                                         TRKCS119
028100                                                                  TRKCS119
028200     MOVE TR006-MAX-ITEMS          TO ASC-SEL-SUB.                TRKCS119
028300     MOVE ZERO                     TO ASC-SEL-ITEM.               TRKCS119
028400     PERFORM 2110-GET-NEXT-RECORD.                                TRKCS119
028500                                                                  TRKCS119
028600/                                                                 TRKCS119
028700*----------------------------------------------------------------*TRKCS119
028800* FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*TRKCS119
028900* SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*TRKCS119
029000* EDITS.                                                         *TRKCS119
029100*----------------------------------------------------------------*TRKCS119
029200                                                                  TRKCS119
029300 2000-FKEY-PROCESSING.                                            TRKCS119
029400                                                                  TRKCS119
029500     EVALUATE TRUE                                                TRKCS119
029600                                                                  TRKCS119
029700         WHEN DP020-SRC-AID = DP016-CLEAR                         TRKCS119
029800             PERFORM 3000-EDIT-DATA-IN-COMMAREA                   TRKCS119
029900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 TRKCS119
030000                                                                  TRKCS119
030100         WHEN DP020-FK-REFRESH(DP020-SRC-AID)                     TRKCS119
030200             INITIALIZE ASC-SPECIFIC-COMMAREA                     TRKCS119
030300             PERFORM 4000-BUILD-INITIAL-PANEL                     TRKCS119
030400             PERFORM 3200-RESET-ATTRIBUTES                        TRKCS119
030500                                                                  TRKCS119
030600         WHEN OTHER                                               TRKCS119
030700             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                 TRKCS119
030800             PERFORM 3000-EDIT-DATA-IN-COMMAREA                   TRKCS119
030900             IF  PS-NO-ERROR                                      TRKCS119
031000                 SET DP030-CONTINUE-GO TO TRUE                    TRKCS119
031100                 PERFORM 2100-CHECK-FUNCTION-KEY                  TRKCS119
031200             ELSE                                                 TRKCS119
031300                 SET DP030-OVERRIDE-GO TO TRUE                    TRKCS119
031400             END-IF                                               TRKCS119
031500     END-EVALUATE.                                                TRKCS119
031600******************************************************************TRKCS119
031700** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **TRKCS119
031800** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **TRKCS119
031900** FROM THE CICS ARCHITECTURE API.                              **TRKCS119
032000******************************************************************TRKCS119
032100 2100-CHECK-FUNCTION-KEY.                                         TRKCS119
032200     EVALUATE TRUE                                                TRKCS119
032300         WHEN DP020-FK-LOCAL-GOTO (DP020-SRC-AID)                 TRKCS119
032400             PERFORM 2110-PREPARE-INTER-APPL-COMM                 TRKCS119
032500                                                                  TRKCS119
032600         WHEN DP020-SRC-AID = DP016-ENTER                         TRKCS119
032700             CONTINUE                                             TRKCS119
032800                                                                  TRKCS119
032900         WHEN OTHER                                               TRKCS119
033000             SET DP013-NO-ROLLBACK                                TRKCS119
033100                 DP013-XCTL-DISPLAY-RESTART                       TRKCS119
033200                 DP013-CICS-ABEND  TO TRUE                        TRKCS119
033300             MOVE '2100-CHECK-FUNCTION-KEY'                       TRKCS119
033400                                   TO DP013-PARAGRAPH             TRKCS119
033500             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      TRKCS119
033600                                   TO DP013-MESSAGE-TEXT (1)      TRKCS119
033700             PERFORM DP013-0000-PROCESS-ABEND                     TRKCS119
033800     END-EVALUATE.                                                TRKCS119
033900******************************************************************TRKCS119
034000** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **TRKCS119
034100** INTER-APPLICATION COMMAREA.                                  **TRKCS119
034200******************************************************************TRKCS119
034300 2110-PREPARE-INTER-APPL-COMM.                                    TRKCS119
034400     INITIALIZE TR009-INTER-APPL-COMMAREA.                        TRKCS119
034500     MOVE ASC-DELIVERY-SCAC-CDE    TO TR009-DELIVERY-SCAC-CDE.    TRKCS119
034600     MOVE ASC-TRLR-NBR             TO TR009-TRLR-NBR.             TRKCS119
034700     MOVE ASC-TRIP-ID              TO TR009-TRIP-ID.              TRKCS119
034800     MOVE ASC-ETA-FROM-DATE-ISO    TO TR009-ETA-FROM-DATE-ISO.    TRKCS119
034900     MOVE ASC-ETA-TO-DATE-ISO      TO TR009-ETA-TO-DATE-ISO.      TRKCS119
035000     MOVE ASC-PO-NBR-N             TO TR009-PO-NBR.               TRKCS119
035100     MOVE ASC-PRO-NBR              TO RC023-FREIGHT-BILL.         TRKCS119
035200     MOVE ASC-BOL-NBR              TO RC023-BILL-OF-LADING.       TRKCS119
035300     CALL RC023-FB-BOL-VEH-FORMAT USING RC023-FORMAT-DATA-AREA.   TRKCS119
035400     MOVE RC023-FREIGHT-BILL       TO TR009-PRO-NBR.              TRKCS119
035500     MOVE RC023-BILL-OF-LADING     TO TR009-BOL-NBR.              TRKCS119
035600     MOVE ASC-OPER-UNIT-ID         TO TR009-OPER-UNIT-ID.         TRKCS119
035700     MOVE ASC-FCLTY-ID             TO TR009-FCLTY-ID.             TRKCS119
035800     MOVE ASC-ORIGINAL-SCAC-CDE    TO TR009-ORIGINAL-SCAC-CDE.    TRKCS119
035900 EJECT                                                            TRKCS119
036000                                                                  TRKCS119
036100 2110-GET-NEXT-RECORD.                                            TRKCS119
036200                                                                  TRKCS119
036300     ADD +1                        TO ASC-SEL-SUB.                TRKCS119
036400     IF  ASC-SEL-SUB > TR006-MAX-ITEMS                            TRKCS119
036500         ADD +1                    TO ASC-SEL-ITEM                TRKCS119
036600         MOVE +1                   TO ASC-SEL-SUB                 TRKCS119
036700     END-IF.                                                      TRKCS119
036800                                                                  TRKCS119
036900     IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                TRKCS119
037000         PERFORM 9100-READ-SEL-QUEUE                              TRKCS119
037100         IF  TR006-ITEM (ASC-SEL-SUB) > ZERO                      TRKCS119
037200**           MOVE TR006-TRIP-ID (ASC-SEL-SUB)                     TRKCS119
037300**                                 TO ASC-TRIP-ID                 TRKCS119
037400             MOVE TR006-PRO-NBR (ASC-SEL-SUB)                     TRKCS119
037500                                   TO ASC-PRO-NBR                 TRKCS119
037600             MOVE TR006-BOL-NBR (ASC-SEL-SUB)                     TRKCS119
037700                                   TO ASC-BOL-NBR                 TRKCS119
037800             SET TR006-INQ (ASC-SEL-SUB)                          TRKCS119
037900                                   TO TRUE                        TRKCS119
038000             PERFORM 9200-REWRITE-LIST-QUEUE                      TRKCS119
038100         ELSE                                                     TRKCS119
038200             SUBTRACT +1 FROM ASC-SEL-SUB                         TRKCS119
038300*            --- BOTTOM OF LIST ---                               TRKCS119
038400             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER            TRKCS119
038500             SET DP020-MSG-INFORMATIONAL                          TRKCS119
038600                                   TO TRUE                        TRKCS119
038700         END-IF                                                   TRKCS119
038800     ELSE                                                         TRKCS119
038900         SUBTRACT +1 FROM ASC-SEL-ITEM                            TRKCS119
039000         MOVE TR006-MAX-ITEMS      TO ASC-SEL-SUB                 TRKCS119
039100         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER            TRKCS119
039200         SET DP020-MSG-INFORMATIONAL                              TRKCS119
039300                                   TO TRUE                        TRKCS119
039400     END-IF.                                                      TRKCS119
039500                                                                  TRKCS119
039600/                                                                 TRKCS119
039700 2120-GET-PREV-RECORD.                                            TRKCS119
039800                                                                  TRKCS119
039900     SUBTRACT +1 FROM ASC-SEL-SUB.                                TRKCS119
040000     IF  ASC-SEL-SUB < +1                                         TRKCS119
040100         SUBTRACT +1 FROM ASC-SEL-ITEM                            TRKCS119
040200         MOVE TR006-MAX-ITEMS      TO ASC-SEL-SUB                 TRKCS119
040300     END-IF.                                                      TRKCS119
040400                                                                  TRKCS119
040500     IF  ASC-SEL-ITEM > ZERO                                      TRKCS119
040600         PERFORM 9100-READ-SEL-QUEUE                              TRKCS119
040700**       MOVE TR006-TRIP-ID (ASC-SEL-SUB)                         TRKCS119
040800**                                 TO ASC-TRIP-ID                 TRKCS119
040900         MOVE TR006-PRO-NBR (ASC-SEL-SUB)                         TRKCS119
041000                                   TO ASC-PRO-NBR                 TRKCS119
041100         MOVE TR006-BOL-NBR (ASC-SEL-SUB)                         TRKCS119
041200                                   TO ASC-BOL-NBR                 TRKCS119
041300     ELSE                                                         TRKCS119
041400         MOVE +1                   TO ASC-SEL-ITEM                TRKCS119
041500                                      ASC-SEL-SUB                 TRKCS119
041600*        --- TOP OF LIST ---                                      TRKCS119
041700         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER            TRKCS119
041800         SET DP020-MSG-INFORMATIONAL                              TRKCS119
041900                                   TO TRUE                        TRKCS119
042000     END-IF.                                                      TRKCS119
042100                                                                  TRKCS119
042200/                                                                 TRKCS119
042300*----------------------------------------------------------------*TRKCS119
042400* MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*TRKCS119
042500* THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *TRKCS119
042600* APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *TRKCS119
042700*----------------------------------------------------------------*TRKCS119
042800                                                                  TRKCS119
042900 2200-MOVE-SCREEN-TO-COMMAREA.                                    TRKCS119
043000*                                                                 TRKCS119
043100     IF  ADSCACL > ZERO                                           TRKCS119
043200         MOVE ADSCACI              TO ASC-DELIVERY-SCAC-CDE       TRKCS119
043300     ELSE                                                         TRKCS119
043400        IF  ADSCACF = DP015-ERASE-EOF                             TRKCS119
043500            MOVE SPACES            TO ASC-DELIVERY-SCAC-CDE       TRKCS119
043600        END-IF                                                    TRKCS119
043700     END-IF.                                                      TRKCS119
043800                                                                  TRKCS119
043900     IF  APONBRL > ZERO                                           TRKCS119
044000         MOVE APONBRI              TO ASC-PO-NBR                  TRKCS119
044100     ELSE                                                         TRKCS119
044200        IF  APONBRF = DP015-ERASE-EOF                             TRKCS119
044300            MOVE SPACES            TO ASC-PO-NBR                  TRKCS119
044400        END-IF                                                    TRKCS119
044500     END-IF.                                                      TRKCS119
044600                                                                  TRKCS119
044700     IF  APRONBRL > ZERO                                          TRKCS119
044800         MOVE APRONBRI             TO ASC-PRO-NBR                 TRKCS119
044900     ELSE                                                         TRKCS119
045000         IF  APRONBRF = DP015-ERASE-EOF                           TRKCS119
045100             MOVE SPACES           TO ASC-PRO-NBR                 TRKCS119
045200         END-IF                                                   TRKCS119
045300     END-IF.                                                      TRKCS119
045400                                                                  TRKCS119
045500     IF  ABOLNBRL > ZERO                                          TRKCS119
045600         MOVE ABOLNBRI             TO ASC-BOL-NBR                 TRKCS119
045700     ELSE                                                         TRKCS119
045800         IF  ABOLNBRF = DP015-ERASE-EOF                           TRKCS119
045900             MOVE SPACES           TO ASC-BOL-NBR                 TRKCS119
046000         END-IF                                                   TRKCS119
046100     END-IF.                                                      TRKCS119
046200                                                                  TRKCS119
046300     IF  AFDTMML > ZERO                                           TRKCS119
046400         MOVE AFDTMMI              TO ASC-ETA-FROM-DATE-MM        TRKCS119
046500     ELSE                                                         TRKCS119
046600         IF  AFDTMMF = DP015-ERASE-EOF                            TRKCS119
046700             MOVE SPACE            TO ASC-ETA-FROM-DATE-MM        TRKCS119
046800         END-IF                                                   TRKCS119
046900     END-IF.                                                      TRKCS119
047000                                                                  TRKCS119
047100     IF  AFDTDDL > ZERO                                           TRKCS119
047200         MOVE AFDTDDI              TO ASC-ETA-FROM-DATE-DD        TRKCS119
047300     ELSE                                                         TRKCS119
047400         IF  AFDTDDF = DP015-ERASE-EOF                            TRKCS119
047500             MOVE SPACE            TO ASC-ETA-FROM-DATE-DD        TRKCS119
047600         END-IF                                                   TRKCS119
047700     END-IF.                                                      TRKCS119
047800                                                                  TRKCS119
047900     IF  AFDTYYL > ZERO                                           TRKCS119
048000         MOVE AFDTYYI              TO ASC-ETA-FROM-DATE-CCYY      TRKCS119
048100     ELSE                                                         TRKCS119
048200         IF  AFDTYYF = DP015-ERASE-EOF                            TRKCS119
048300             MOVE SPACE            TO ASC-ETA-FROM-DATE-CCYY      TRKCS119
048400         END-IF                                                   TRKCS119
048500     END-IF.                                                      TRKCS119
048600                                                                  TRKCS119
048700     IF  ATDTMML > ZERO                                           TRKCS119
048800         MOVE ATDTMMI              TO ASC-ETA-TO-DATE-MM          TRKCS119
048900     ELSE                                                         TRKCS119
049000         IF  ATDTMMF = DP015-ERASE-EOF                            TRKCS119
049100             MOVE SPACE            TO ASC-ETA-TO-DATE-MM          TRKCS119
049200         END-IF                                                   TRKCS119
049300     END-IF.                                                      TRKCS119
049400                                                                  TRKCS119
049500     IF  ATDTDDL > ZERO                                           TRKCS119
049600         MOVE ATDTDDI              TO ASC-ETA-TO-DATE-DD          TRKCS119
049700     ELSE                                                         TRKCS119
049800         IF  ATDTDDF = DP015-ERASE-EOF                            TRKCS119
049900             MOVE SPACE            TO ASC-ETA-TO-DATE-DD          TRKCS119
050000         END-IF                                                   TRKCS119
050100     END-IF.                                                      TRKCS119
050200                                                                  TRKCS119
050300     IF  ATDTYYL > ZERO                                           TRKCS119
050400         MOVE ATDTYYI              TO ASC-ETA-TO-DATE-CCYY        TRKCS119
050500     ELSE                                                         TRKCS119
050600         IF  ATDTYYF = DP015-ERASE-EOF                            TRKCS119
050700             MOVE SPACE            TO ASC-ETA-TO-DATE-CCYY        TRKCS119
050800         END-IF                                                   TRKCS119
050900     END-IF.                                                      TRKCS119
051000*----------------------------------------------------------------*TRKCS119
051100* ENSURE THE DATA IS CORRECT.  NOTE THAT IN A CRITERIA SPECIFI-  *TRKCS119
051200* CATION SCREEN ONLY MINIMAL EDITS ARE DONE.  EDITS RELATED TO   *TRKCS119
051300* A SPECIFIC SCREEN THAT THE USER IS GOING TO (LOCAL-GO) ARE DONE*TRKCS119
051400* IN THE DESTINATION PROGRAM, NOT HERE.                          *TRKCS119
051500*----------------------------------------------------------------*TRKCS119
051600                                                                  TRKCS119
051700 3000-EDIT-DATA-IN-COMMAREA.                                      TRKCS119
051800     PERFORM 3200-RESET-ATTRIBUTES.                               TRKCS119
051900*                                                                 TRKCS119
052000     IF  ASC-ETA-TO-DATE-MM > SPACES                              TRKCS119
052100     OR  ASC-ETA-TO-DATE-DD > SPACES                              TRKCS119
052200     OR  ASC-ETA-TO-DATE-CCYY > SPACES                            TRKCS119
052300        INITIALIZE DPG51 DPG52 DPG53                              TRKCS119
052400                   DPG54 DPG55 DPG56                              TRKCS119
052500         STRING ASC-ETA-TO-DATE-MM             DELIMITED BY SIZE  TRKCS119
052600                ASC-ETA-TO-DATE-DD             DELIMITED BY SIZE  TRKCS119
052700                ASC-ETA-TO-DATE-CCYY           DELIMITED BY SIZE  TRKCS119
052800                               INTO DPG52-LK-DATE-INPUT           TRKCS119
052900        SET DPG51-ACTUAL-CALENDAR-ONLY                            TRKCS119
053000            DPG51-DO-NOT-INCR-DECR-DATE                           TRKCS119
053100            DPG52-LK-DTE-GREG-CC TO TRUE                          TRKCS119
053200        CALL DP500-KOHLS-CALENDAR-ROUTINE                         TRKCS119
053300            USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56             TRKCS119
053400        IF (DPG54-DATE-INVALID                                    TRKCS119
053500        OR  DPG54-SEVERE-ERROR)                                   TRKCS119
053600            SET PS-ERROR                                          TRKCS119
053700                DP020-MSG-FATAL   TO TRUE                         TRKCS119
053800            MOVE PC-TSYMSG-00009  TO DP020-MSG-NUMBER             TRKCS119
053900            MOVE DP015-UNP-ALP-BRT-MDT                            TRKCS119
054000                                  TO ATDTMMA                      TRKCS119
054100                                     ATDTDDA                      TRKCS119
054200                                     ATDTYYA                      TRKCS119
054300            MOVE DP015-RED        TO ATDTMMC                      TRKCS119
054400                                     ATDTDDC                      TRKCS119
054500                                     ATDTYYC                      TRKCS119
054600            MOVE DP015-REVERSE    TO ATDTMMH                      TRKCS119
054700                                     ATDTDDH                      TRKCS119
054800                                     ATDTYYH                      TRKCS119
054900            MOVE -1               TO ATDTMML                      TRKCS119
055000            SET DP030-SET-CURSOR-APPL-1                           TRKCS119
055100                                  TO TRUE                         TRKCS119
055200        ELSE                                                      TRKCS119
055300            MOVE DPG55-DB2-ISO-DATE-FMT                           TRKCS119
055400                                  TO ASC-ETA-TO-DATE-ISO          TRKCS119
055500        END-IF                                                    TRKCS119
055600     ELSE                                                         TRKCS119
055700        MOVE SPACES               TO ASC-ETA-TO-DATE-ISO          TRKCS119
055800     END-IF.                                                      TRKCS119
055900                                                                  TRKCS119
056000     IF  ASC-ETA-FROM-DATE-MM > SPACES                            TRKCS119
056100     OR  ASC-ETA-FROM-DATE-DD > SPACES                            TRKCS119
056200     OR  ASC-ETA-FROM-DATE-CCYY > SPACES                          TRKCS119
056300        INITIALIZE DPG51 DPG52 DPG53                              TRKCS119
056400                   DPG54 DPG55 DPG56                              TRKCS119
056500         STRING ASC-ETA-FROM-DATE-MM           DELIMITED BY SIZE  TRKCS119
056600                ASC-ETA-FROM-DATE-DD           DELIMITED BY SIZE  TRKCS119
056700                ASC-ETA-FROM-DATE-CCYY         DELIMITED BY SIZE  TRKCS119
056800                               INTO DPG52-LK-DATE-INPUT           TRKCS119
056900        SET DPG51-ACTUAL-CALENDAR-ONLY                            TRKCS119
057000            DPG51-DO-NOT-INCR-DECR-DATE                           TRKCS119
057100            DPG52-LK-DTE-GREG-CC TO TRUE                          TRKCS119
057200        CALL DP500-KOHLS-CALENDAR-ROUTINE                         TRKCS119
057300            USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56             TRKCS119
057400        IF (DPG54-DATE-INVALID                                    TRKCS119
057500        OR  DPG54-SEVERE-ERROR)                                   TRKCS119
057600            SET PS-ERROR                                          TRKCS119
057700                DP020-MSG-FATAL   TO TRUE                         TRKCS119
057800            MOVE PC-TSYMSG-00009  TO DP020-MSG-NUMBER             TRKCS119
057900            MOVE DP015-UNP-ALP-BRT-MDT                            TRKCS119
058000                                  TO AFDTMMA                      TRKCS119
058100                                     AFDTDDA                      TRKCS119
058200                                     AFDTYYA                      TRKCS119
058300            MOVE DP015-RED        TO AFDTMMC                      TRKCS119
058400                                     AFDTDDC                      TRKCS119
058500                                     AFDTYYC                      TRKCS119
058600            MOVE DP015-REVERSE    TO AFDTMMH                      TRKCS119
058700                                     AFDTDDH                      TRKCS119
058800                                     AFDTYYH                      TRKCS119
058900            MOVE -1               TO AFDTMML                      TRKCS119
059000            SET DP030-SET-CURSOR-APPL-1                           TRKCS119
059100                                  TO TRUE                         TRKCS119
059200        ELSE                                                      TRKCS119
059300            MOVE DPG55-DB2-ISO-DATE-FMT                           TRKCS119
059400                                  TO ASC-ETA-FROM-DATE-ISO        TRKCS119
059500        END-IF                                                    TRKCS119
059600     ELSE                                                         TRKCS119
059700        MOVE SPACES               TO ASC-ETA-FROM-DATE-ISO        TRKCS119
059800     END-IF.                                                      TRKCS119
059900                                                                  TRKCS119
060000     IF  (ASC-ETA-FROM-DATE-ISO > SPACES)                         TRKCS119
060100     AND (ASC-ETA-TO-DATE-ISO > SPACES)                           TRKCS119
060200         IF  ASC-ETA-FROM-DATE-ISO > ASC-ETA-TO-DATE-ISO          TRKCS119
060300             SET PS-ERROR                                         TRKCS119
060400                 DP020-MSG-FATAL   TO TRUE                        TRKCS119
060500             MOVE PC-TSYMSG-00143  TO DP020-MSG-NUMBER            TRKCS119
060600             MOVE DP015-UNP-ALP-BRT-MDT                           TRKCS119
060700                                      TO AFDTMMA                  TRKCS119
060800                                         AFDTDDA                  TRKCS119
060900                                         AFDTYYA                  TRKCS119
061000                                         ATDTMMA                  TRKCS119
061100                                         ATDTDDA                  TRKCS119
061200                                         ATDTYYA                  TRKCS119
061300             MOVE DP015-RED           TO AFDTMMC                  TRKCS119
061400                                         AFDTDDC                  TRKCS119
061500                                         AFDTYYC                  TRKCS119
061600                                         ATDTMMC                  TRKCS119
061700                                         ATDTDDC                  TRKCS119
061800                                         ATDTYYC                  TRKCS119
061900             MOVE DP015-REVERSE       TO AFDTMMH                  TRKCS119
062000                                         AFDTDDH                  TRKCS119
062100                                         AFDTYYH                  TRKCS119
062200                                         ATDTMMH                  TRKCS119
062300                                         ATDTDDH                  TRKCS119
062400                                         ATDTYYH                  TRKCS119
062500             MOVE -1                  TO AFDTMML                  TRKCS119
062600             SET DP030-SET-CURSOR-APPL-1                          TRKCS119
062700                                      TO TRUE                     TRKCS119
062800         END-IF                                                   TRKCS119
062900     END-IF                                                       TRKCS119
063000                                                                  TRKCS119
063100     IF  ASC-PO-NBR = (SPACES OR ZEROES)                          TRKCS119
063200         CONTINUE                                                 TRKCS119
063300     ELSE                                                         TRKCS119
063400         MOVE ASC-PO-NBR            TO DP010I-UNEDITED-FIELD      TRKCS119
063500         MOVE LENGTH OF ASC-PO-NBR  TO DP010I-MAXIMUM-DIGITS      TRKCS119
063600         MOVE ZERO                  TO DP010I-MAXIMUM-DECIMALS    TRKCS119
063700         SET  DP010I-NEGATIVE-NOT-ALLOWED                         TRKCS119
063800                                    TO TRUE                       TRKCS119
273427         CALL DP010I-NUMERIC-EDIT-ROUTINE                         TRKCS119
273427              USING DP010I-NUMERIC-EDIT-AREA                      TRKCS119
064000         IF  DP010I-ERROR-DETECTED                                TRKCS119
064100             SET PS-ERROR                                         TRKCS119
064200                 DP020-MSG-FATAL    TO TRUE                       TRKCS119
064300             MOVE PC-TSYMSG-00008   TO DP020-MSG-NUMBER           TRKCS119
064400             MOVE DP015-UNP-ALP-BRT-OFF                           TRKCS119
064500                                    TO APONBRA                    TRKCS119
064600             MOVE DP015-RED         TO APONBRC                    TRKCS119
064700             MOVE DP015-REVERSE     TO APONBRH                    TRKCS119
064800             MOVE -1                TO APONBRL                    TRKCS119
064900             SET DP030-SET-CURSOR-APPL-1                          TRKCS119
065000                                    TO TRUE                       TRKCS119
065100         ELSE                                                     TRKCS119
065200             MOVE DP010I-NUMERIC-FIELD                            TRKCS119
065300                                      TO ASC-PO-NBR-N             TRKCS119
065400     END-IF.                                                      TRKCS119
065500                                                                  TRKCS119
065600*                                                                 TRKCS119
065700     IF  PS-NO-ERROR                                              TRKCS119
065800         MOVE -1                   TO APONBRL                     TRKCS119
065900         SET DP030-SET-CURSOR-APPL-1                              TRKCS119
066000                                   TO TRUE                        TRKCS119
066100     END-IF.                                                      TRKCS119
066200                                                                  TRKCS119
066300*                                                                 TRKCS119
066400******************************************************************TRKCS119
066500** MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE **TRKCS119
066600** MAP.                                                         **TRKCS119
066700******************************************************************TRKCS119
066800 3200-RESET-ATTRIBUTES.                                           TRKCS119
066900     MOVE DP015-UNP-ALP-NOR-OFF    TO ADSCACA                     TRKCS119
067000                                      APONBRA                     TRKCS119
067100                                      APRONBRA                    TRKCS119
067200                                      ABOLNBRA                    TRKCS119
067300                                      AFDTMMA                     TRKCS119
067400                                      AFDTDDA                     TRKCS119
067500                                      AFDTYYA                     TRKCS119
067600                                      ATDTMMA                     TRKCS119
067700                                      ATDTDDA                     TRKCS119
067800                                      ATDTYYA.                    TRKCS119
067900     MOVE DP015-GREEN              TO ADSCACC                     TRKCS119
068000                                      APONBRC                     TRKCS119
068100                                      APRONBRC                    TRKCS119
068200                                      ABOLNBRC                    TRKCS119
068300                                      AFDTMMC                     TRKCS119
068400                                      AFDTDDC                     TRKCS119
068500                                      AFDTYYC                     TRKCS119
068600                                      ATDTMMC                     TRKCS119
068700                                      ATDTDDC                     TRKCS119
068800                                      ATDTYYC.                    TRKCS119
068900     MOVE DP015-UNDERLINE          TO ADSCACH                     TRKCS119
069000                                      APONBRH                     TRKCS119
069100                                      APRONBRH                    TRKCS119
069200                                      ABOLNBRH                    TRKCS119
069300                                      AFDTMMH                     TRKCS119
069400                                      AFDTDDH                     TRKCS119
069500                                      AFDTYYH                     TRKCS119
069600                                      ATDTMMH                     TRKCS119
069700                                      ATDTDDH                     TRKCS119
069800                                      ATDTYYH.                    TRKCS119
069900*                                                                 TRKCS119
070000*----------------------------------------------------------------*TRKCS119
070100*  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *TRKCS119
070200*  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *TRKCS119
070300*  IT HERE.                                                      *TRKCS119
070400*----------------------------------------------------------------*TRKCS119
070500 4000-BUILD-INITIAL-PANEL.                                        TRKCS119
070600     SET DP030-SET-CURSOR-APPL-1   TO TRUE                        TRKCS119
070700     MOVE -1                       TO APONBRL.                    TRKCS119
070800*    MOVE TR005-DELIVERY-SCAC-CDE  TO ASC-DELIVERY-SCAC-CDE       TRKCS119
070801                                                                          
070802*    MOVE TR007-OWNER-SCAC-ID      TO ADSCACO.                    TRKCS119
070810*    MOVE TR007-OWNER-SCAC-ID      TO ASC-DELIVERY-SCAC-CDE.      TRKCS119
070811     MOVE SPACES                   TO ASC-DELIVERY-SCAC-CDE.      TRKCS119
070820     MOVE TR007-OWNER-SCAC-ID      TO ASC-ORIGINAL-SCAC-CDE.      TRKCS119
070830                                                                          
071200     MOVE TR007-OPER-UNT-ID        TO ASC-OPER-UNIT-ID.           TRKCS119
071300     MOVE TR007-FCLTY-ID           TO ASC-FCLTY-ID.               TRKCS119
071400*    MOVE TR005-TRLR-NBR           TO ASC-TRLR-NBR.               TRKCS119
071500     MOVE TR007-VEH-NBR            TO ASC-TRLR-NBR.               TRKCS119
071600     MOVE ASC-PRO-NBR              TO APRONBRO.                   TRKCS119
071700     MOVE ASC-BOL-NBR              TO ABOLNBRO.                   TRKCS119
071800*                                                                 TRKCS119
071900*----------------------------------------------------------------*TRKCS119
072000*    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *TRKCS119
072100*----------------------------------------------------------------*TRKCS119
072200 4400-MOVE-COMMAREA-TO-SCREEN.                                    TRKCS119
072300     MOVE ASC-DELIVERY-SCAC-CDE    TO ADSCACO.                    TRKCS119
072400     MOVE ASC-PO-NBR-N             TO APONBRO.                    TRKCS119
072500     MOVE ASC-PRO-NBR              TO APRONBRO.                   TRKCS119
072600     MOVE ASC-BOL-NBR              TO ABOLNBRO.                   TRKCS119
072700     MOVE ASC-ETA-FROM-DATE-MM     TO AFDTMMO.                    TRKCS119
072800     MOVE ASC-ETA-FROM-DATE-DD     TO AFDTDDO.                    TRKCS119
072900     MOVE ASC-ETA-FROM-DATE-CCYY   TO AFDTYYO.                    TRKCS119
073000     MOVE ASC-ETA-TO-DATE-MM       TO ATDTMMO.                    TRKCS119
073100     MOVE ASC-ETA-TO-DATE-DD       TO ATDTDDO.                    TRKCS119
073200     MOVE ASC-ETA-TO-DATE-CCYY     TO ATDTYYO.                    TRKCS119
073300 EJECT                                                            TRKCS119
073400                                                                  TRKCS119
073500 9100-READ-SEL-QUEUE.                                             TRKCS119
073600     EXEC CICS                                                    TRKCS119
073700         READQ TS                                                 TRKCS119
073800             QUEUE (ASC-SEL-QUEUE-NAME)                           TRKCS119
073900             INTO  (TR006-DOCK-SCHEDULE-REC)                      TRKCS119
074000             ITEM  (ASC-SEL-ITEM)                                 TRKCS119
074100             RESP  (PV-CICS-RESPONSE)                             TRKCS119
074200     END-EXEC.                                                    TRKCS119
074300                                                                  TRKCS119
074400     IF  PV-CICS-RESPONSE EQUAL DFHRESP(NORMAL)                   TRKCS119
074500         CONTINUE                                                 TRKCS119
074600     ELSE                                                         TRKCS119
074700         SET DP013-LOGIC-ABEND                                    TRKCS119
074800             DP013-NO-ROLLBACK     TO TRUE                        TRKCS119
074900         MOVE '9100-READ-SEL-QUEUE'                               TRKCS119
075000                                   TO DP013-PARAGRAPH             TRKCS119
075100         MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'   TRKCS119
075200                                   TO DP013-MESSAGE-TEXT (1)      TRKCS119
075300         PERFORM DP013-0000-PROCESS-ABEND                         TRKCS119
075400     END-IF.                                                      TRKCS119
075500                                                                  TRKCS119
075600/                                                                 TRKCS119
075700*----------------------------------------------------------------*TRKCS119
075800* UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *TRKCS119
075900*----------------------------------------------------------------*TRKCS119
076000 9200-REWRITE-LIST-QUEUE.                                         TRKCS119
076100                                                                  TRKCS119
076200     EXEC CICS                                                    TRKCS119
076300          WRITEQ TS                                               TRKCS119
076400              QUEUE  (ASC-SEL-QUEUE-NAME)                         TRKCS119
076500              FROM   (TR006-DOCK-SCHEDULE-REC)                    TRKCS119
076600              ITEM   (ASC-SEL-ITEM)                               TRKCS119
076700              RESP   (PV-CICS-RESPONSE)                           TRKCS119
076800              REWRITE                                             TRKCS119
076900     END-EXEC.                                                    TRKCS119
077000                                                                  TRKCS119
077100     IF  PV-CICS-RESPONSE NOT EQUAL DFHRESP(NORMAL)               TRKCS119
077200         SET DP013-CICS-ABEND                                     TRKCS119
077300             DP013-NO-ROLLBACK     TO TRUE                        TRKCS119
077400         MOVE '9200-REWRITE-LIST-QUEUE'                           TRKCS119
077500                                   TO DP013-PARAGRAPH             TRKCS119
077600         MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'   TRKCS119
077700                                   TO DP013-MESSAGE-TEXT (1)      TRKCS119
077800         PERFORM DP013-0000-PROCESS-ABEND                         TRKCS119
077900     END-IF.                                                      TRKCS119
078000                                                                  TRKCS119
078100/                                                                 TRKCS119
078200*----------------------------------------------------------------*TRKCS119
078300*    ABEND PROCESSOR MODULE                                      *TRKCS119
078400*----------------------------------------------------------------*TRKCS119
078500                                                                  TRKCS119
078600     COPY DPPD013.                                                TRKCS119
