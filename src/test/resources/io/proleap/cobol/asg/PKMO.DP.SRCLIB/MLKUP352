       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP352                                          00030017
       AUTHOR.        ERIN SEARL.                                       00040019
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  8-09-04.                                          00060020
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *           MLKUP352 - WEEKLY DETAILED MERCHANDISE LEDGER        *00100055
      *                      TABLE UPDATE (MLT_WKLY_SUM)               *00110059
      ******************************************************************00110113
      ******************************************************************00110299
      *    WR/PITS#     DATE        DESCRIPTION                        *00110399
      *    --------   --------     ---------------------------------   *00110499
      *    W28582     12/14/2005   CHANGED ENT-ID TO ML-LWST-ID OR     *00110699
      *                            ML-LO-MDSE-LVL-ID.      C. MCCAMMON *00110799
R28582* W28582 11-21-2006 ROD JANSSEN  ADD ICA1, ICA2, EIC1, EIC2, GM$  00110899
      ******************************************************************00112099
                                                                        00140000
       ENVIRONMENT DIVISION.                                            00150000
                                                                        00160000
       CONFIGURATION SECTION.                                           00170000
       SOURCE-COMPUTER.        IBM-3090.                                00180000
       OBJECT-COMPUTER.        IBM-3090.                                00190000
       INPUT-OUTPUT SECTION.                                            00200000
       FILE-CONTROL.                                                    00210000
           SELECT CONDENSED-FILE                                        00220000
               ASSIGN TO INP01.                                         00230000
                                                                        00240000
                                                                        00250000
       DATA DIVISION.                                                   00260000
       FILE SECTION.                                                    00270000
                                                                        00280000
       FD  CONDENSED-FILE                                               00290000
           LABEL RECORDS ARE STANDARD                                   00300000
           RECORDING MODE IS F                                          00310000
           BLOCK CONTAINS 0 RECORDS.                                    00320000
       01  CONDENSED-RECORD            PIC X(400).                      00330071
      *                                                                 00340000
                                                                        00350000
       WORKING-STORAGE SECTION.                                         00360000
                                                                        00370000
       01  FILLER                      PIC X(28)   VALUE                00384000
                'BEGINNING OF WORKING STORAGE'.                         00390000
                                                                        00400000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00410000
                                                                        00420000
       01  PC-PROGRAM-CONSTANTS.                                        00430000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK182'.    00440052
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP352'.  00450021
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00460000
                                                                        00470000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00480000
           05  PE-MSG-01                       PIC X(50) VALUE          00490000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00500000
           05  PE-MSG-02                       PIC X(50) VALUE          00510000
                'ATTEMPT TO UPDATE ROW ON TABLE MLT_WKLY_SUM FAILED'.   00520059
           05  PE-MSG-03                       PIC X(50) VALUE          00530000
                'ATTEMPT TO INSERT ROW ON TABLE MLT_WKLY_SUM FAILED'.   00540059
           05  PE-MSG-04                       PIC X(50) VALUE          00550000
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00560000
           05  PE-MSG-05                       PIC X(50) VALUE          00570000
                'UPDATE TO TCKRSTINF FAILED                      '.     00580000
           05  PE-MSG-06                       PIC X(50) VALUE          00590000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00600000
           05  PE-MSG-07                       PIC X(50) VALUE          00610000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00620000
      *                                                                 00630000
       01  PS-PROGRAM-SWITCHES.                                         00640000
           05  PS-EOF-CONDENSED-FILE-SW     PIC X(01)     VALUE 'N'.    00650000
               88  PS-EOF-CONDENSED-FILE                  VALUE 'Y'.    00660000
           05  PS-MLT00005-CURSOR-EOF-SW    PIC X(01)     VALUE 'N'.    00670062
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00680000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00690000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00700000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00710000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00720000
                                                                        00730000
       01  PI-PROGRAM-INDICATORS.                                       00740000
           05  PI-PROCESSING-OPTIONS-IND    PIC X(01)     VALUE 'P'.    00750000
               88  PI-UPDATE-MLT00005-ROW                 VALUE 'U'.    00760062
               88  PI-INSERT-MLT00005-ROW                 VALUE 'I'.    00770062
      *                                                                 00780000
       01  PROGRAM-VARIABLES.                                           00790000
           05  PV-RETRY-COUNT            PIC S9(04) VALUE +0.           00800000
           05  PV-DEADLOCK-COUNT         PIC S9(04) VALUE +0.           00810000
W28582     05  PV-ML-LWST-ID             PIC 9(09)  VALUE 0.            00820099
W28582***  05  PV-ENT-ID                 PIC 9(09)  VALUE 0.            00821099
      *                                                                 00830000
           05  PV-PARAGRAPH-NAME         PIC  X(30) VALUE SPACES.       00840000
           05  PV-ABEND-MESSAGE          PIC  X(75) VALUE SPACES.       00850000
           05  PV-OPERATION-ATTEMPTED    PIC  X(50) VALUE SPACES.       00860000
           05  PV-CURRENT-TIMESTAMP      PIC  X(26) VALUE SPACES.       00870000
           05  PV-COMMIT-TIMESTAMP       PIC  X(26) VALUE SPACES.       00880000
           05  PV-MKUP-NET-AMT           PIC S9(11)V99 VALUE +0 COMP-3. 00890031
           05  PV-MKUP-INIT-AMT          PIC S9(11)V99 VALUE +0 COMP-3. 00900031
           05  PV-NET-COST               PIC S9(11)V99 VALUE +0 COMP-3. 00900131
           05  PV-NET-RETAIL             PIC S9(11)V99 VALUE +0 COMP-3. 00901031
                                                                        00910075
       01  PA-PROGRAM-ACCUMULATORS.                                     00920000
           05  PA-RECORD-COUNTS.                                        00930000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      00940000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      00950000
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      00960000
               10  PA-MAJ-CL-PROCESSED   PIC  9(09)  VALUE ZEROES.      00970000
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      00980000
                                                                        00990000
       01  PD-PROGRAM-DISPLAYS.                                         01000000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01010000
       01  SAVE-AREA.                                                   01020000
           05  SV-PRIMARY-KEY.                                          01030000
               10  SV-PARTN-NBR          PIC S9(4)  COMP    VALUE +0.   01040057
               10  SV-DEPT-NBR           PIC S9(4)V COMP-3  VALUE +0.   01060032
               10  SV-MAJ-CL-NBR         PIC S9(4)V COMP-3  VALUE +0.   01070032
               10  SV-SUB-CL-NBR         PIC S9(4)V COMP-3  VALUE +0.   01071081
               10  SV-LOC-NBR            PIC S9(4)  COMP    VALUE +0.   01071181
W28582         10  SV-ML-LWST-ID         PIC S9(9)  COMP    VALUE +0.   01071299
W28582***      10  SV-ENT-ID             PIC S9(9)  COMP    VALUE +0.   01071399
           05  SV-UPDATE-FIELDS.                                        01080000
               10  SV-SLS-REG-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01090045
               10  SV-SLS-PROMO-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01100045
               10  SV-SLS-PROCLR-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01110045
               10  SV-SLS-CLR-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01120045
               10  SV-SLS-OTH-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01130045
               10  SV-MKDN-PLU-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01140045
               10  SV-MKDN-PROMO-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01150045
               10  SV-MKDN-RGST-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01162045
               10  SV-MKDN-GLDST-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01170045
               10  SV-MKDN-UMTCH-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01180045
               10  SV-MKDN-BYR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01190045
               10  SV-MKDN-STR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01200045
               10  SV-MKDN-PRPT-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01201045
               10  SV-MKDN-OTH-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01210045
               10  SV-MKDN-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01220045
               10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01230045
               10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01240045
               10  SV-XFER-OUT-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01250045
               10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01260045
               10  SV-RCPT-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01270045
               10  SV-RCPT-GRO-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01280045
               10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01290045
               10  SV-PADJ-FRGT-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01300045
               10  SV-PADJ-DISC-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01310045
               10  SV-PADJ-RTV-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01320045
               10  SV-PADJ-RTV-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01330045
               10  SV-PADJ-DA-COST-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01340045
               10  SV-PADJ-DA-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01350045
               10  SV-PADJ-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01351045
               10  SV-PADJ-OTH-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01352045
               10  SV-PADJ-OTH-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01353045
               10  SV-DR-ALW-RTL-AMT     PIC S9(11)V99  COMP-3 VALUE +0.01354045
               10  SV-RTV-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01355045
               10  SV-MKUP-INIT-AMT      PIC S9(11)V99  COMP-3 VALUE +0.01356045
               10  SV-MKUP-NET-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01357045
               10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01358045
               10  SV-ONORD-RTL-AMT      PIC S9(11)V99  COMP-3 VALUE +0.01359045
               10  SV-ONORD-COST-AMT     PIC S9(11)V99  COMP-3 VALUE +0.01359145
               10  SV-SHNK-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01359245
               10  SV-END-INV-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01359345
      *                                                                 01360000
      ******************************************************************01370000
      * MLRD155 - WK DETAILED MERCH LEDGER UPDATE TXN LAYOUT            01380099
      ******************************************************************01390000
           COPY MLRD155.                                                01400086
      *                                                                 01410000
      ******************************************************************01420000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01430000
      ******************************************************************01440000
           COPY DPWS004.                                                01450000
      *                                                                 01460000
      ******************************************************************01470000
      *  USED FOR DB2 ERRORS                                            01480000
      ******************************************************************01490000
           EXEC SQL                                                     01500000
               INCLUDE SQLCA                                            01510000
           END-EXEC.                                                    01520000
      *                                                                 01530000
      ******************************************************************01540000
      *  MERCHANDISE LEDGER WEEKLY DETAIL TABLE                         01550099
      ******************************************************************01560000
           EXEC SQL                                                     01570048
               INCLUDE MLT00005                                         01580062
           END-EXEC.                                                    01590048
      *                                                                 01600000
      ***************************************************************** 01614000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01620000
      ***************************************************************** 01630000
           EXEC SQL                                                     01640000
               INCLUDE TCKRSTCN                                         01650000
           END-EXEC.                                                    01660000
      *                                                                 01670000
           EXEC SQL                                                     01680000
               INCLUDE TCKRSTIN                                         01690000
           END-EXEC.                                                    01700000
      *                                                                 01710000
      ******************************************************************01720000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01730000
      ******************************************************************01740000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01750000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +35  COMP. 01760007
           05  CR-CHECKPOINT-RESTART-VAR.                               01770000
               10  CR-PREV-DTL-KEY.                                     01780099
                   15  CR-PARTN-NBR         PIC  S9(4)  COMP            01800099
                                                       VALUE ZEROES.    01810099
                   15  CR-DEPT-NBR          PIC  S9(4)V COMP-3          01820099
                                                       VALUE ZEROES.    01821099
                   15  CR-MAJ-CL-NBR        PIC  S9(4)V COMP-3          01830099
                                                       VALUE ZEROES.    01830199
                   15  CR-SUB-CL-NBR        PIC  S9(4)V COMP-3          01831099
                                                       VALUE ZEROES.    01831199
                   15  CR-LOC-NBR           PIC  S9(4)  COMP            01831299
                                                       VALUE ZEROES.    01831399
W28582             15  CR-ML-LWST-ID        PIC  S9(9)  COMP            01831499
W28582***          15  CR-ENT-ID            PIC  S9(9)  COMP            01831599
                                                       VALUE ZEROES.    01831699
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01850000
      *                                                                 01860000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01870000
           05  CK-SAVE-PREV-KEY.                                        01880099
               10  CK-PARTN-NBR         PIC  S9(4)  COMP VALUE ZEROES.  01900099
               10  CK-DEPT-NBR          PIC  S9(4)V COMP-3              01920099
                                                         VALUE ZEROES.  01921099
               10  CK-MAJ-CL-NBR        PIC  S9(4)V COMP-3              01930099
                                                         VALUE ZEROES.  01931099
               10  CK-SUB-CL-NBR        PIC  S9(4)V COMP-3              01940099
                                                         VALUE ZEROES.  01940199
               10  CK-LOC-NBR           PIC  S9(4)  COMP VALUE ZEROES.  01940299
W28582         10  CK-ML-LWST-ID        PIC  S9(9)  COMP VALUE ZEROES.  01940499
W28582***      10  CK-ENT-ID            PIC  S9(9)  COMP VALUE ZEROES.  01940599
                                                                        01950000
                                                                        01960045
      *-------------------*                                             01970000
       PROCEDURE DIVISION.                                              01980000
      *-------------------*                                             01990000
                                                                        02000000
       0000-MLKUP352.                                                   02010021
                                                                        02020000
           PERFORM 1000-INITIALIZATION.                                 02030000
      *                                                                 02040000
           PERFORM 2000-PROCESS-INPUT                                   02050000
                UNTIL PS-EOF-CONDENSED-FILE.                            02060000
      *                                                                 02070000
           PERFORM 8000-EOJ-ROUTINE.                                    02080000
                                                                        02090000
           STOP RUN.                                                    02100000
                                                                        02110000
      ******************************************************************02120000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02130000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02140000
      ******************************************************************02150000
      *                                                                 02160000
       1000-INITIALIZATION.                                             02170000
      *                                                                 02180000
           OPEN INPUT CONDENSED-FILE.                                   02190000
                                                                        02200000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02210000
                                                                        02220000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02230000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02240000
               PERFORM 7500-SELECT-RESTART-INFO                         02250000
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02260000
                                            CR-CHECKPOINT-RESTART-VAR   02270000
               MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY        02280000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                02290000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   02320000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 02330000
               DISPLAY 'SUB CLASS NBR   ' CR-SUB-CL-NBR                 02340081
               DISPLAY 'STORE NBR       ' CR-LOC-NBR                    02340181
W28582         DISPLAY 'ML LWST ID      ' CR-ML-LWST-ID                 02340299
W28582***      DISPLAY 'ENT-ID          ' CR-ENT-ID                     02340399
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           02350000
           ELSE                                                         02360000
               SET PS-FIRST-COMMIT TO TRUE                              02370000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02380000
           END-IF.                                                      02390000
      *                                                                 02400000
           PERFORM 4000-READ-CONDENSED-FILE UNTIL                       02410000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02420000
            OR PS-EOF-CONDENSED-FILE.                                   02430000
      *                                                                 02440000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02450000
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                02460000
                        PA-INPUT-COUNT                                  02470000
               DISPLAY 'PARTITION NUMBER      ' ML155-WK-PARTN-NBR      02480099
               DISPLAY 'DEPARTMENT NBR        ' ML155-DEPT-NBR          02500087
               DISPLAY 'MAJOR CLASS NBR       ' ML155-MAJ-CL-NBR        02510087
               DISPLAY 'SUB CLASS NBR         ' ML155-SUB-CL-NBR        02511087
               DISPLAY 'LOCATION NBR          ' ML155-LOC-NBR           02511187
W28582         MOVE ML155-ML-LWST-ID TO PV-ML-LWST-ID                   02511299
W28582         DISPLAY 'ML LWST ID            ' PV-ML-LWST-ID           02511399
W28582***      MOVE ML155-ENT-ID   TO PV-ENT-ID                         02511499
W28582***      DISPLAY 'ENT-ID                ' PV-ENT-ID               02511699
           END-IF.                                                      02540000
                                                                        02550000
           IF PS-EOF-CONDENSED-FILE                                     02560000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02570000
                   CONTINUE                                             02580000
               ELSE                                                     02590000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02600000
           ELSE                                                         02610000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02620000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02630000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02640000
           END-IF.                                                      02650000
                                                                        02660000
      ******************************************************************02670000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02680000
      ******************************************************************02690000
       2000-PROCESS-INPUT.                                              02700000
      *                                                                 02710000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02720000
      *                                                                 02730000
           PERFORM 7000-SELECT-DB-ROW.                                  02740000
      *                                                                 02750000
           IF PS-PROGRAM-DEADLOCKED                                     02760000
               CONTINUE                                                 02770000
           ELSE                                                         02780000
               EVALUATE TRUE                                            02790000
                   WHEN PI-UPDATE-MLT00005-ROW                          02800062
                       PERFORM 7100-UPDATE-MLT00005-ROW                 02810062
                   WHEN PI-INSERT-MLT00005-ROW                          02820062
                       PERFORM 7200-INSERT-MLT00005-ROW                 02830062
                   WHEN OTHER                                           02840000
                       MOVE '7000-SELECT-DB-ROW' TO PV-PARAGRAPH-NAME   02850000
                       MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED         02860000
                       PERFORM 9999-SQL-ABEND                           02870000
               END-EVALUATE                                             02880000
           END-IF.                                                      02890000
      *                                                                 02900000
           IF PS-PROGRAM-DEADLOCKED                                     02910000
               CONTINUE                                                 02920000
           ELSE                                                         02930000
               PERFORM 7700-COMMIT-PROCESSING                           02940000
               PERFORM 4000-READ-CONDENSED-FILE                         02950000
           END-IF.                                                      02960000
      *                                                                 02970000
      *                                                                 02980000
      ******************************************************************02990000
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         03000000
      ******************************************************************03010000
       2100-CALCULATE-UPDATES.                                          03020000
      * ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES     03030000
      *                                                                 03040000
           COMPUTE SV-SLS-REG-RTL-AMT =                                 03050000
                   MLT00005-SLS-REG-RTL-AMT + ML155-SLS-REG-RTL-AMT.    03060087
           COMPUTE SV-SLS-PROMO-RTL-AMT =                               03070000
                   MLT00005-SLS-PROMO-RTL-AMT + ML155-SLS-PROMO-RTL-AMT.03080087
           COMPUTE SV-SLS-PROCLR-RTL-AMT =                              03090000
                 MLT00005-SLS-PROCLR-RTL-AMT + ML155-SLS-PROCLR-RTL-AMT.03100087
           COMPUTE SV-SLS-CLR-RTL-AMT =                                 03110000
                   MLT00005-SLS-CLR-RTL-AMT + ML155-SLS-CLR-RTL-AMT.    03120087
           COMPUTE SV-SLS-OTH-RTL-AMT =                                 03130000
                   MLT00005-SLS-OTH-RTL-AMT + ML155-SLS-OTH-RTL-AMT.    03140087
           COMPUTE SV-MKDN-PLU-RTL-AMT  =                               03160000
                   MLT00005-MKDN-PLU-RTL-AMT + ML155-MKDN-PLU-RTL-AMT.  03170087
           COMPUTE SV-MKDN-RGST-RTL-AMT =                               03191031
                   MLT00005-MKDN-RGST-RTL-AMT + ML155-MKDN-RGST-RTL-AMT.03192087
           COMPUTE SV-MKDN-UMTCH-RTL-AMT =                              03200000
                 MLT00005-MKDN-UMTCH-RTL-AMT + ML155-MKDN-UMTCH-RTL-AMT.03210087
           COMPUTE SV-MKDN-BYR-RTL-AMT  =                               03220000
                   MLT00005-MKDN-BYR-RTL-AMT + ML155-MKDN-BYR-RTL-AMT.  03230087
           COMPUTE SV-MKDN-STR-RTL-AMT  =                               03240000
                   MLT00005-MKDN-STR-RTL-AMT + ML155-MKDN-STR-RTL-AMT.  03250087
           COMPUTE SV-MKDN-RTL-AMT      =                               03270099
                   MLT00005-MKDN-RTL-AMT    + ML155-MKDN-RTL-AMT.       03280099
           COMPUTE SV-MKUP-RTL-AMT      =                               03281099
                   MLT00005-MKUP-RTL-AMT    + ML155-MKUP-RTL-AMT.       03282099
           COMPUTE SV-XFER-IN-RTL-AMT   =                               03290000
                   MLT00005-XFER-IN-RTL-AMT + ML155-XFER-IN-RTL-AMT.    03300087
           COMPUTE SV-XFER-OUT-RTL-AMT  =                               03310000
                   MLT00005-XFER-OUT-RTL-AMT + ML155-XFER-OUT-RTL-AMT.  03320087
           COMPUTE SV-RCPT-RTL-AMT      =                               03330000
                   MLT00005-RCPT-RTL-AMT    + ML155-RCPT-RTL-AMT.       03340087
           COMPUTE SV-RCPT-NET-COST-AMT =                               03350046
                   MLT00005-RCPT-NET-COST-AMT + ML155-RCPT-NET-COST-AMT.03360087
           COMPUTE SV-RCPT-GRO-COST-AMT =                               03370000
                   MLT00005-RCPT-GRO-COST-AMT + ML155-RCPT-GRO-COST-AMT.03380087
           COMPUTE SV-PADJ-DISC-COST-AMT =                              03410012
                 MLT00005-PADJ-DISC-COST-AMT + ML155-PADJ-DISC-COST-AMT.03420087
           COMPUTE SV-PADJ-RTL-AMT      =                               03430000
                   MLT00005-PADJ-RTL-AMT    + ML155-PADJ-RTL-AMT.       03440087
           COMPUTE SV-PADJ-NET-COST-AMT =                               03450046
                   MLT00005-PADJ-NET-COST-AMT + ML155-PADJ-NET-COST-AMT.03460087
           COMPUTE SV-PADJ-FRGT-COST-AMT =                              03461099
                 MLT00005-PADJ-FRGT-COST-AMT + ML155-PADJ-FRGT-COST-AMT.03461199
           COMPUTE SV-DR-ALW-RTL-AMT    =                               03470000
                   MLT00005-DR-ALW-RTL-AMT  + ML155-DR-ALW-RTL-AMT.     03480087
           COMPUTE SV-RTV-RTL-AMT       =                               03490000
                   MLT00005-RTV-RTL-AMT     + ML155-RTV-RTL-AMT.        03500087
           COMPUTE SV-INV-ADJ-RTL-AMT   =                               03510000
                   MLT00005-INV-ADJ-RTL-AMT + ML155-INV-ADJ-RTL-AMT.    03520087
           COMPUTE SV-PADJ-RTV-COST-AMT  = MLT00005-PADJ-RTV-COST-AMT   03520199
                                         + ML155-PADJ-RTV-COST-AMT.     03520299
           COMPUTE SV-PADJ-RTV-RTL-AMT   = MLT00005-PADJ-RTV-RTL-AMT    03520499
                                         + ML155-PADJ-RTV-RTL-AMT.      03520599
           COMPUTE SV-PADJ-DA-COST-AMT   = MLT00005-PADJ-DA-COST-AMT    03520699
                                         + ML155-PADJ-DA-COST-AMT.      03520799
           COMPUTE SV-PADJ-DA-RTL-AMT    = MLT00005-PADJ-DA-RTL-AMT     03520899
                                         + ML155-PADJ-DA-RTL-AMT.       03520999
           COMPUTE SV-PADJ-OTH-COST-AMT  = MLT00005-PADJ-OTH-COST-AMT   03521099
                                         + ML155-PADJ-OTH-COST-AMT.     03521199
           COMPUTE SV-PADJ-OTH-RTL-AMT   = MLT00005-PADJ-OTH-RTL-AMT    03521299
                                         + ML155-PADJ-OTH-RTL-AMT.      03521399
           COMPUTE SV-MKDN-PROMO-RTL-AMT = MLT00005-MKDN-PROMO-RTL-AMT  03521499
                                         + ML155-MKDN-PROMO-RTL-AMT.    03521599
           COMPUTE SV-MKDN-PRPT-RTL-AMT  = MLT00005-MKDN-PRPT-RTL-AMT   03521699
                                         + ML155-MKDN-PRPT-RTL-AMT.     03521799
           COMPUTE SV-MKDN-GLDST-RTL-AMT = MLT00005-MKDN-GLDST-RTL-AMT  03521899
                                         + ML155-MKDN-GLDST-RTL-AMT.    03521999
           COMPUTE SV-MKDN-OTH-RTL-AMT   = MLT00005-MKDN-OTH-RTL-AMT    03522099
                                         + ML155-MKDN-OTH-RTL-AMT.      03522199
                                                                        03523014
      *--- INITIAL MARKUP AMOUNT                                        03530014
           COMPUTE PV-MKUP-INIT-AMT =                                   03540000
                   ML155-RCPT-RTL-AMT - ML155-RCPT-NET-COST-AMT.        03550087
      *            ML155-RCPT-RTL-AMT - ML155-RCPT-GRO-COST-AMT.        03551087
           COMPUTE SV-MKUP-INIT-AMT   =                                 03560096
                   MLT00005-MKUP-INIT-AMT + PV-MKUP-INIT-AMT.           03570096
                                                                        03580000
      *--- NET MARKUP AMOUNT                                            03581014
      *    COMPUTE PV-MKUP-NET-AMT =                                    03590031
      *           (ML155-RCPT-RTL-AMT -                                 03600087
      *           (ML155-RCPT-GRO-COST-AMT +                            03610087
      *            ML155-PADJ-FRGT-COST-AMT) -                          03620087
      *            ML155-PADJ-DISC-COST-AMT).                           03630087
           COMPUTE PV-NET-RETAIL =                                      03630131
                  (ML155-RCPT-RTL-AMT +                                 03630287
                   ML155-PADJ-RTL-AMT +                                 03630387
                   ML155-MKUP-RTL-AMT +                                 03630487
                  (ML155-XFER-IN-RTL-AMT -                              03630587
                   ML155-XFER-OUT-RTL-AMT))                             03630687
           END-COMPUTE.                                                 03631031
           COMPUTE PV-NET-COST = ML155-RCPT-NET-COST-AMT +              03631187
                                 ML155-PADJ-NET-COST-AMT                03631287
           END-COMPUTE.                                                 03632031
                                                                        03632114
           COMPUTE PV-MKUP-NET-AMT = PV-NET-RETAIL - PV-NET-COST        03632231
           END-COMPUTE.                                                 03633031
                                                                        03634014
           COMPUTE SV-MKUP-NET-AMT     =                                03640096
                   MLT00005-MKUP-NET-AMT + PV-MKUP-NET-AMT.             03650096
                                                                        03660000
           COMPUTE SV-INV-ADJ-RTL-AMT  =                                03670000
                   MLT00005-INV-ADJ-RTL-AMT + ML155-INV-ADJ-RTL-AMT.    03680087
      *                                                                 03690000
      ******************************************************************03700000
      * READS THE CONDENSED FILE INTO THE MLT_WKLY_SUM LAYOUT           03710059
      ******************************************************************03720000
       4000-READ-CONDENSED-FILE.                                        03730000
           READ CONDENSED-FILE INTO ML155-UPDATE-REC                    03740087
              AT END                                                    03750000
                MOVE 'Y' TO PS-EOF-CONDENSED-FILE-SW.                   03760000
     *                                                                  03770000
           IF PS-EOF-CONDENSED-FILE                                     03780000
               MOVE ML155-WK-PARTN-NBR        TO CR-PARTN-NBR           03790087
               MOVE ML155-DEPT-NBR            TO CR-DEPT-NBR            03810087
               MOVE ML155-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          03820087
               MOVE ML155-SUB-CL-NBR          TO CR-SUB-CL-NBR          03821087
               MOVE ML155-LOC-NBR             TO CR-LOC-NBR             03821187
W28582         MOVE ML155-ML-LWST-ID          TO PV-ML-LWST-ID          03821299
W28582         MOVE PV-ML-LWST-ID             TO CR-ML-LWST-ID          03821399
W28582***      MOVE ML155-ENT-ID              TO PV-ENT-ID              03821499
W28582***      MOVE PV-ENT-ID                 TO CR-ENT-ID              03821599
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    03850000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03860000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03870000
                                             TCKRSTINF-WORK-STRG-DESC   03880000
               EXEC SQL                                                 03890000
                 UPDATE TCKRSTINF                                       03900000
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03910000
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03920000
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03930000
               END-EXEC                                                 03940000
               IF SQLCODE = 0                                           03950000
                   CONTINUE                                             03960000
               ELSE                                                     03970000
                   MOVE PE-MSG-05          TO PV-OPERATION-ATTEMPTED    03980000
                   MOVE '* 4000-READ-CONDENSED *' TO PV-PARAGRAPH-NAME  03990000
                   PERFORM 9999-SQL-ABEND                               04000000
               END-IF                                                   04010000
            ELSE                                                        04020000
                ADD 1                        TO PA-INPUT-COUNT          04030000
            END-IF.                                                     04040000
      *                                                                 04050000
      ******************************************************************04060000
      * SELECT A ROW FROM THE WEEKLY DTL TABLE THAT MATCHES INPUT KEY   04070099
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      04080000
      ******************************************************************04090000
       7000-SELECT-DB-ROW.                                              04100000
      *                                                                 04110000
           EXEC SQL                                                     04111085
               SELECT SLS_REG_RTL_AMT                                   04112085
                     ,SLS_PROMO_RTL_AMT                                 04113085
                     ,SLS_PROCLR_RTL_AMT                                04114085
                     ,SLS_CLR_RTL_AMT                                   04115085
                     ,SLS_OTH_RTL_AMT                                   04116085
                     ,MKDN_PLU_RTL_AMT                                  04117085
                     ,MKDN_PROMO_RTL_AMT                                04118085
                     ,MKDN_RGST_RTL_AMT                                 04119085
                     ,MKDN_GLDST_RTL_AMT                                04119185
                     ,MKDN_UMTCH_RTL_AMT                                04119285
                     ,MKDN_BYR_RTL_AMT                                  04119385
                     ,MKDN_STR_RTL_AMT                                  04119485
                     ,MKDN_PRPT_RTL_AMT                                 04119585
                     ,MKDN_OTH_RTL_AMT                                  04119685
                     ,MKDN_RTL_AMT                                      04119785
                     ,MKUP_RTL_AMT                                      04119885
                     ,XFER_IN_RTL_AMT                                   04119985
                     ,XFER_OUT_RTL_AMT                                  04120085
                     ,RCPT_RTL_AMT                                      04120185
                     ,RCPT_NET_COST_AMT                                 04120285
                     ,RCPT_GRO_COST_AMT                                 04120385
                     ,PADJ_RTL_AMT                                      04120485
                     ,PADJ_FRGT_COST_AMT                                04120585
                     ,PADJ_DISC_COST_AMT                                04120685
                     ,PADJ_RTV_COST_AMT                                 04120785
                     ,PADJ_RTV_RTL_AMT                                  04120885
                     ,PADJ_DA_COST_AMT                                  04120985
                     ,PADJ_DA_RTL_AMT                                   04121085
                     ,PADJ_NET_COST_AMT                                 04121185
                     ,PADJ_OTH_COST_AMT                                 04121285
                     ,PADJ_OTH_RTL_AMT                                  04121385
                     ,DR_ALW_RTL_AMT                                    04121485
                     ,RTV_RTL_AMT                                       04121585
                     ,MKUP_INIT_AMT                                     04121696
                     ,MKUP_NET_AMT                                      04121796
                     ,INV_ADJ_RTL_AMT                                   04121885
                 INTO :MLT00005-SLS-REG-RTL-AMT                         04122385
                     ,:MLT00005-SLS-PROMO-RTL-AMT                       04122485
                     ,:MLT00005-SLS-PROCLR-RTL-AMT                      04122585
                     ,:MLT00005-SLS-CLR-RTL-AMT                         04122685
                     ,:MLT00005-SLS-OTH-RTL-AMT                         04122785
                     ,:MLT00005-MKDN-PLU-RTL-AMT                        04122885
                     ,:MLT00005-MKDN-PROMO-RTL-AMT                      04122985
                     ,:MLT00005-MKDN-RGST-RTL-AMT                       04123085
                     ,:MLT00005-MKDN-GLDST-RTL-AMT                      04123185
                     ,:MLT00005-MKDN-UMTCH-RTL-AMT                      04123285
                     ,:MLT00005-MKDN-BYR-RTL-AMT                        04123385
                     ,:MLT00005-MKDN-STR-RTL-AMT                        04123485
                     ,:MLT00005-MKDN-PRPT-RTL-AMT                       04123585
                     ,:MLT00005-MKDN-OTH-RTL-AMT                        04123685
                     ,:MLT00005-MKDN-RTL-AMT                            04123785
                     ,:MLT00005-MKUP-RTL-AMT                            04123885
                     ,:MLT00005-XFER-IN-RTL-AMT                         04123985
                     ,:MLT00005-XFER-OUT-RTL-AMT                        04124085
                     ,:MLT00005-RCPT-RTL-AMT                            04124185
                     ,:MLT00005-RCPT-NET-COST-AMT                       04124285
                     ,:MLT00005-RCPT-GRO-COST-AMT                       04124385
                     ,:MLT00005-PADJ-RTL-AMT                            04124485
                     ,:MLT00005-PADJ-FRGT-COST-AMT                      04124585
                     ,:MLT00005-PADJ-DISC-COST-AMT                      04124685
                     ,:MLT00005-PADJ-RTV-COST-AMT                       04124785
                     ,:MLT00005-PADJ-RTV-RTL-AMT                        04124885
                     ,:MLT00005-PADJ-DA-COST-AMT                        04124985
                     ,:MLT00005-PADJ-DA-RTL-AMT                         04125085
                     ,:MLT00005-PADJ-NET-COST-AMT                       04125185
                     ,:MLT00005-PADJ-OTH-COST-AMT                       04125285
                     ,:MLT00005-PADJ-OTH-RTL-AMT                        04125385
                     ,:MLT00005-DR-ALW-RTL-AMT                          04125485
                     ,:MLT00005-RTV-RTL-AMT                             04125585
                     ,:MLT00005-MKUP-INIT-AMT                           04125696
                     ,:MLT00005-MKUP-NET-AMT                            04125796
                     ,:MLT00005-INV-ADJ-RTL-AMT                         04125885
               FROM MLT_WKLY_SUM                                        04540059
              WHERE   PARTN_NBR           = :ML155-WK-PARTN-NBR         04550085
                AND   DEPT_NBR            = :ML155-DEPT-NBR             04570085
                AND   MAJ_CL_NBR          = :ML155-MAJ-CL-NBR           04580085
                AND   SUB_CL_NBR          = :ML155-SUB-CL-NBR           04581085
                AND   LOC_NBR             = :ML155-LOC-NBR              04581185
W28582          AND   ML_LO_MDSE_LVL_ID   = :ML155-ML-LWST-ID           04581299
W28582***       AND   ENT_ID              = :ML155-ENT-ID               04581399
           END-EXEC.                                                    04600000
      *                                                                 04610000
           EVALUATE SQLCODE                                             04620000
               WHEN 0                                                   04630000
                   SET PI-UPDATE-MLT00005-ROW     TO TRUE               04640062
               WHEN +100                                                04650000
                   SET PI-INSERT-MLT00005-ROW     TO TRUE               04660062
               WHEN -911                                                04670000
                   DISPLAY '*** -911 OCCURRED ON SELECT ***'            04680000
                   ADD +1             TO PV-DEADLOCK-COUNT              04690000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04700000
                       MOVE '7000-SELECT-DB-ROW' TO                     04710000
                                                    PV-PARAGRAPH-NAME   04720000
                       PERFORM 9000-DEADLOCK-ERROR                      04730000
                   ELSE                                                 04740000
                       PERFORM 7999-RESTART-PROCESS                     04750000
                   END-IF                                               04760000
               WHEN OTHER                                               04770000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     04780000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             04790000
                   PERFORM 9999-SQL-ABEND                               04800000
           END-EVALUATE.                                                04810000
      *                                                                 04820000
      *                                                                 04830000
      ***************************************************************** 04840000
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON PARTITION NBR,   04850099
      * DEPT, MAJ CL, SUB CL, LOC NBR, AND ENT ID.  CURRENT TABLE       04860099
      * VALUES UPDATED TO INCLUDE VALUES FROM INPUT REC MLRD154         04870099
      ***************************************************************** 04880000
       7100-UPDATE-MLT00005-ROW.                                        04890062
      *                                                                 04900000
           PERFORM 2100-CALCULATE-UPDATES.                              04910000
      *                                                                 04920000
           EXEC SQL                                                     04930000
               UPDATE MLT_WKLY_SUM                                      04940059
                   SET SLS_REG_RTL_AMT     = :SV-SLS-REG-RTL-AMT        04950000
                     , SLS_PROMO_RTL_AMT   = :SV-SLS-PROMO-RTL-AMT      04960000
                     , SLS_PROCLR_RTL_AMT  = :SV-SLS-PROCLR-RTL-AMT     04970000
                     , SLS_CLR_RTL_AMT     = :SV-SLS-CLR-RTL-AMT        04980000
                     , SLS_OTH_RTL_AMT     = :SV-SLS-OTH-RTL-AMT        04990000
                     , MKDN_PLU_RTL_AMT    = :SV-MKDN-PLU-RTL-AMT       05000000
                     , MKDN_PROMO_RTL_AMT  = :SV-MKDN-PROMO-RTL-AMT     05010045
                     , MKDN_RGST_RTL_AMT   = :SV-MKDN-RGST-RTL-AMT      05011045
                     , MKDN_GLDST_RTL_AMT  = :SV-MKDN-GLDST-RTL-AMT     05012045
                     , MKDN_UMTCH_RTL_AMT  = :SV-MKDN-UMTCH-RTL-AMT     05020000
                     , MKDN_BYR_RTL_AMT    = :SV-MKDN-BYR-RTL-AMT       05030000
                     , MKDN_STR_RTL_AMT    = :SV-MKDN-STR-RTL-AMT       05040000
                     , MKDN_PRPT_RTL_AMT   = :SV-MKDN-PRPT-RTL-AMT      05041045
                     , MKDN_OTH_RTL_AMT    = :SV-MKDN-OTH-RTL-AMT       05042045
                     , MKDN_RTL_AMT        = :SV-MKDN-RTL-AMT           05043045
                     , MKUP_RTL_AMT        = :SV-MKUP-RTL-AMT           05050000
                     , XFER_IN_RTL_AMT     = :SV-XFER-IN-RTL-AMT        05060000
                     , XFER_OUT_RTL_AMT    = :SV-XFER-OUT-RTL-AMT       05070000
                     , RCPT_RTL_AMT        = :SV-RCPT-RTL-AMT           05080000
                     , RCPT_NET_COST_AMT   = :SV-RCPT-NET-COST-AMT      05090046
                     , RCPT_GRO_COST_AMT   = :SV-RCPT-GRO-COST-AMT      05100000
                     , PADJ_RTL_AMT        = :SV-PADJ-RTL-AMT           05110045
                     , PADJ_FRGT_COST_AMT  = :SV-PADJ-FRGT-COST-AMT     05111045
                     , PADJ_DISC_COST_AMT  = :SV-PADJ-DISC-COST-AMT     05120012
                     , PADJ_RTV_COST_AMT   = :SV-PADJ-RTV-COST-AMT      05130045
                     , PADJ_RTV_RTL_AMT    = :SV-PADJ-RTV-RTL-AMT       05131045
                     , PADJ_DA_COST_AMT    = :SV-PADJ-DA-COST-AMT       05132045
                     , PADJ_DA_RTL_AMT     = :SV-PADJ-DA-RTL-AMT        05133045
                     , PADJ_NET_COST_AMT   = :SV-PADJ-NET-COST-AMT      05134045
                     , PADJ_OTH_COST_AMT   = :SV-PADJ-OTH-COST-AMT      05135045
                     , PADJ_OTH_RTL_AMT    = :SV-PADJ-OTH-RTL-AMT       05136045
                     , DR_ALW_RTL_AMT      = :SV-DR-ALW-RTL-AMT         05150000
                     , RTV_RTL_AMT         = :SV-RTV-RTL-AMT            05160000
                     , MKUP_INIT_AMT       = :SV-MKUP-INIT-AMT          05170096
                     , MKUP_NET_AMT        = :SV-MKUP-NET-AMT           05180096
                     , INV_ADJ_RTL_AMT     = :SV-INV-ADJ-RTL-AMT        05191045
              WHERE   PARTN_NBR            = :ML155-WK-PARTN-NBR        05200087
                AND   DEPT_NBR             = :ML155-DEPT-NBR            05220087
                AND   MAJ_CL_NBR           = :ML155-MAJ-CL-NBR          05230087
                AND   SUB_CL_NBR           = :ML155-SUB-CL-NBR          05231087
                AND   LOC_NBR              = :ML155-LOC-NBR             05231187
W28582          AND   ML_LO_MDSE_LVL_ID    = :ML155-ML-LWST-ID          05231299
W28582***       AND   ENT_ID               = :ML155-ENT-ID              05231399
           END-EXEC                                                     05250000
      *                                                                 05260000
           EVALUATE SQLCODE                                             05270000
               WHEN 0                                                   05280000
                   CONTINUE                                             05290000
               WHEN -911                                                05300000
                   ADD +1             TO PV-DEADLOCK-COUNT              05310000
                   DISPLAY '*** -911 OCCURRED ON UPDATE ***'            05320000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05330000
                       MOVE '7100-UPDATE-MLT00005-ROW' TO               05340062
                                                    PV-PARAGRAPH-NAME   05350000
                       PERFORM 9000-DEADLOCK-ERROR                      05360000
                   ELSE                                                 05370000
                       PERFORM 7999-RESTART-PROCESS                     05380000
                   END-IF                                               05390000
               WHEN OTHER                                               05400000
                   MOVE '7100-UPDATE-MLT00005-ROW' TO PV-PARAGRAPH-NAME 05410062
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED05420000
                   PERFORM 9999-SQL-ABEND                               05430000
           END-EVALUATE.                                                05440000
      *                                                                 05450000
           ADD 1 TO PA-UPDATE-COUNT.                                    05460000
      *                                                                 05470000
      ***************************************************************** 05480000
      * THERE WAS NO MATCHING ROW ON THE WEEKLY DTL TABLE. INITIAL    * 05490099
      * ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW.             * 05500014
      * CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN RECALC* 05510014
      ***************************************************************** 05520000
       7200-INSERT-MLT00005-ROW.                                        05530062
      *                                                                 05540000
           COMPUTE PV-MKUP-INIT-AMT =                                   05541003
                   ML155-RCPT-RTL-AMT - ML155-RCPT-NET-COST-AMT.        05541187
      *            ML155-RCPT-RTL-AMT - ML155-RCPT-GRO-COST-AMT.        05542087
                                                                        05545003
      *    COMPUTE PV-MKUP-NET-AMT =                                    05546031
      *           (ML155-RCPT-RTL-AMT -                                 05547087
      *           (ML155-RCPT-GRO-COST-AMT +                            05548087
      *            ML155-PADJ-FRGT-COST-AMT) -                          05549087
      *            ML155-PADJ-DISC-COST-AMT).                           05549187
      *                                                                 05550000
           COMPUTE PV-NET-RETAIL =                                      05550131
                  (ML155-RCPT-RTL-AMT +                                 05550287
                   ML155-PADJ-RTL-AMT +                                 05550387
                   ML155-MKUP-RTL-AMT +                                 05550487
                  (ML155-XFER-IN-RTL-AMT -                              05550587
                   ML155-XFER-OUT-RTL-AMT))                             05550687
           END-COMPUTE.                                                 05550731
           COMPUTE PV-NET-COST = ML155-RCPT-NET-COST-AMT +              05550887
                                 ML155-PADJ-NET-COST-AMT                05550987
           END-COMPUTE.                                                 05551031
                                                                        05551114
           COMPUTE PV-MKUP-NET-AMT = PV-NET-RETAIL - PV-NET-COST        05551231
           END-COMPUTE.                                                 05552031
           EXEC SQL                                                     05560000
               INSERT INTO MLT_WKLY_SUM                                 05570059
                    (  PARTN_NBR                                        05580066
                     , DEPT_NBR                                         05600066
                     , MAJ_CL_NBR                                       05610000
                     , SUB_CL_NBR                                       05611024
                     , LOC_NBR                                          05612043
W28582               , ML_LO_MDSE_LVL_ID                                05613099
W28582***            , ENT_ID                                           05613199
                     , FSCL_WK_END_DTE                                  05614054
                     , SLS_REG_RTL_AMT                                  05630000
                     , SLS_PROMO_RTL_AMT                                05640000
                     , SLS_PROCLR_RTL_AMT                               05650000
                     , SLS_CLR_RTL_AMT                                  05660000
                     , SLS_OTH_RTL_AMT                                  05670000
                     , MKDN_PLU_RTL_AMT                                 05680000
                     , MKDN_PROMO_RTL_AMT                               05690024
                     , MKDN_RGST_RTL_AMT                                05695024
                     , MKDN_GLDST_RTL_AMT                               05696040
                     , MKDN_UMTCH_RTL_AMT                               05700000
                     , MKDN_BYR_RTL_AMT                                 05710000
                     , MKDN_STR_RTL_AMT                                 05720000
                     , MKDN_PRPT_RTL_AMT                                05721040
                     , MKDN_OTH_RTL_AMT                                 05722024
                     , MKDN_RTL_AMT                                     05723024
                     , MKUP_RTL_AMT                                     05730000
                     , XFER_IN_RTL_AMT                                  05740000
                     , XFER_OUT_RTL_AMT                                 05750000
                     , RCPT_RTL_AMT                                     05760000
                     , RCPT_NET_COST_AMT                                05770000
                     , RCPT_GRO_COST_AMT                                05780000
                     , PADJ_RTL_AMT                                     05790024
                     , PADJ_FRGT_COST_AMT                               05791024
                     , PADJ_DISC_COST_AMT                               05800012
                     , PADJ_RTV_COST_AMT                                05810024
                     , PADJ_RTV_RTL_AMT                                 05820024
                     , PADJ_DA_COST_AMT                                 05821024
                     , PADJ_DA_RTL_AMT                                  05822024
                     , PADJ_NET_COST_AMT                                05823040
                     , PADJ_OTH_COST_AMT                                05824040
                     , PADJ_OTH_RTL_AMT                                 05825040
                     , DR_ALW_RTL_AMT                                   05830000
                     , RTV_RTL_AMT                                      05840000
                     , MKUP_INIT_AMT                                    05850093
                     , MKUP_NET_AMT                                     05860093
                     , INV_ADJ_RTL_AMT                                  05890040
                     , ONORD_RTL_AMT                                    05900024
                     , ONORD_COST_AMT                                   05910093
                     , SHNK_RTL_AMT                                     05920093
                     , END_INV_RTL_AMT                                  05930099
R28582               , END_INV_COST_AMT                                 05931099
R28582               , INV_COST_ADJ1_AMT                                05932099
R28582               , INV_COST_ADJ2_AMT                                05933099
R28582               , END_INV_COST1_AMT                                05934099
R28582               , END_INV_COST2_AMT                                05935099
R28582               , ADJ_GRO_MRGN_AMT   )                             05936099
               VALUES                                                   05940000
                   (   :ML155-WK-PARTN-NBR                              05950087
                     , :ML155-DEPT-NBR                                  05970087
                     , :ML155-MAJ-CL-NBR                                05980087
                     , :ML155-SUB-CL-NBR                                05981087
                     , :ML155-LOC-NBR                                   05981187
W28582               , :ML155-ML-LWST-ID                                05981299
W28582***            , :ML155-ENT-ID                                    05981399
                     , :ML155-FSCL-WK-END-DTE                           05981499
                     , :ML155-SLS-REG-RTL-AMT                           06000087
                     , :ML155-SLS-PROMO-RTL-AMT                         06010087
                     , :ML155-SLS-PROCLR-RTL-AMT                        06020087
                     , :ML155-SLS-CLR-RTL-AMT                           06030087
                     , :ML155-SLS-OTH-RTL-AMT                           06040087
                     , :ML155-MKDN-PLU-RTL-AMT                          06050087
                     , :ML155-MKDN-PROMO-RTL-AMT                        06060087
                     , :ML155-MKDN-RGST-RTL-AMT                         06061087
                     , :ML155-MKDN-GLDST-RTL-AMT                        06062087
                     , :ML155-MKDN-UMTCH-RTL-AMT                        06070087
                     , :ML155-MKDN-BYR-RTL-AMT                          06080087
                     , :ML155-MKDN-STR-RTL-AMT                          06090087
                     , :ML155-MKDN-PRPT-RTL-AMT                         06091087
                     , :ML155-MKDN-OTH-RTL-AMT                          06092087
                     , :ML155-MKDN-RTL-AMT                              06093087
                     , :ML155-MKUP-RTL-AMT                              06100087
                     , :ML155-XFER-IN-RTL-AMT                           06110087
                     , :ML155-XFER-OUT-RTL-AMT                          06120087
                     , :ML155-RCPT-RTL-AMT                              06130087
                     , :ML155-RCPT-NET-COST-AMT                         06140087
                     , :ML155-RCPT-GRO-COST-AMT                         06150087
                     , :ML155-PADJ-RTL-AMT                              06160087
                     , :ML155-PADJ-FRGT-COST-AMT                        06161087
                     , :ML155-PADJ-DISC-COST-AMT                        06170087
                     , :ML155-PADJ-RTV-COST-AMT                         06171087
                     , :ML155-PADJ-RTV-RTL-AMT                          06172087
                     , :ML155-PADJ-DA-COST-AMT                          06173087
                     , :ML155-PADJ-DA-RTL-AMT                           06174087
                     , :ML155-PADJ-NET-COST-AMT                         06190087
                     , :ML155-PADJ-OTH-COST-AMT                         06191087
                     , :ML155-PADJ-OTH-RTL-AMT                          06192087
                     , :ML155-DR-ALW-RTL-AMT                            06200087
                     , :ML155-RTV-RTL-AMT                               06210087
                     , :PV-MKUP-INIT-AMT                                06220096
                     , :PV-MKUP-NET-AMT                                 06230096
                     , :ML155-INV-ADJ-RTL-AMT                           06260087
                     , 0                                                06270096
                     , 0                                                06280096
                     , 0                                                06290093
                     , 0                                                06300099
R28582               , 0                                                06301099
R28582               , 0                                                06302099
R28582               , 0                                                06303099
R28582               , 0                                                06304099
R28582               , 0                                                06305099
R28582               , 0                       )                        06306099
           END-EXEC                                                     06310000
      *                                                                 06320000
           EVALUATE SQLCODE                                             06330000
               WHEN 0                                                   06340000
                   CONTINUE                                             06350000
               WHEN -911                                                06360000
                   ADD +1             TO PV-DEADLOCK-COUNT              06370000
                   DISPLAY '*** -911 OCCURRED ON INSERT ***'            06380000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              06390000
                       MOVE '7200-INSERT-MLT00005-ROW' TO               06400062
                                                    PV-PARAGRAPH-NAME   06410000
                       PERFORM 9000-DEADLOCK-ERROR                      06420000
                   ELSE                                                 06430000
                       PERFORM 7999-RESTART-PROCESS                     06440000
                   END-IF                                               06450000
               WHEN OTHER                                               06460000
                   MOVE '7200-INSERT-MLT00005-ROW' TO PV-PARAGRAPH-NAME 06470062
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED06480000
                   PERFORM 9999-SQL-ABEND                               06490000
           END-EVALUATE.                                                06500000
                                                                        06510000
           ADD 1 TO PA-INSERT-COUNT.                                    06520000
                                                                        06530000
      ******************************************************************06540000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *06550000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *06560000
      *            CONTROL TABLE                                       *06570000
      ******************************************************************06580000
       7300-GET-COMMIT-TIMESTAMP.                                       06590000
                                                                        06600000
           EXEC SQL                                                     06610000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        06620000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       06630000
               INTO :PV-COMMIT-TIMESTAMP                                06640000
               FROM TCKRSTCNTL                                          06650000
              WHERE JOB_NAME  = :PC-JOB-NAME                            06660000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        06670000
           END-EXEC.                                                    06680000
                                                                        06690000
           EVALUATE TRUE                                                06700000
               WHEN SQLCODE = 0                                         06710000
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  06720000
                   CONTINUE                                             06730000
               WHEN SQLCODE NOT EQUAL ZERO                              06740000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME06750000
                   PERFORM 9999-SQL-ABEND                               06760000
           END-EVALUATE.                                                06770000
      *                                                                 06780000
      *                                                                 06790000
      ******************************************************************06800000
      * 7400-INIT-CHECKPOINT-SELECT                                    *06810000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *06820000
      *            IF WE ARE IN A RESTART CONDITION.                   *06830000
      ******************************************************************06840000
       7400-INIT-CHECKPOINT-SELECT.                                     06850000
                                                                        06860000
           EXEC SQL                                                     06870000
             SELECT  CKPT_STAT_CODE                                     06880000
                    ,CKPT_FRQNCY_QTY                                    06890000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         06900000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        06910000
               FROM  TCKRSTCNTL                                         06920000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06930000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06940000
           END-EXEC.                                                    06950000
                                                                        06960000
           IF SQLCODE = 0                                               06970000
               CONTINUE                                                 06980000
           ELSE                                                         06990000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  07000000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     07010000
               PERFORM 9999-SQL-ABEND                                   07020000
           END-IF.                                                      07030000
      *                                                                 07040000
      *                                                                 07050000
      ******************************************************************07060000
      * 7500-SELECT-RESTART-INFO                                       *07070000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *07080000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *07090000
      ******************************************************************07100000
       7500-SELECT-RESTART-INFO.                                        07110000
                                                                        07120000
           EXEC SQL                                                     07130000
             SELECT  WORK_STRG_DESC                                     07140000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          07150000
               FROM  TCKRSTINF                                          07160000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07170000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07180000
           END-EXEC.                                                    07190000
                                                                        07200000
           IF SQLCODE = 0                                               07210000
               CONTINUE                                                 07220000
           ELSE                                                         07230000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   07240000
               PERFORM 9999-SQL-ABEND                                   07250000
           END-IF.                                                      07260000
                                                                        07270000
      ******************************************************************07280000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *07290000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *07300000
      ******************************************************************07310000
       7600-SET-CURRENT-TIMESTAMP.                                      07320000
                                                                        07330000
           EXEC SQL                                                     07340000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           07350000
           END-EXEC.                                                    07360000
                                                                        07370000
           IF SQLCODE = 0                                               07380000
               CONTINUE                                                 07390000
           ELSE                                                         07400000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 07410000
               PERFORM 9999-SQL-ABEND                                   07420000
           END-IF.                                                      07430000
      *                                                                 07440000
      *                                                                 07450000
      ******************************************************************07460000
      * 7700-COMMIT-PROCESSING.                                        *07470000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *07480000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*07490000
      ******************************************************************07500000
       7700-COMMIT-PROCESSING.                                          07510000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     07520000
                                                                        07530000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          07540000
                                                                        07550000
      * PREPARE COMMIT RECORD FOR UPDATE                                07560000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                07570000
               MOVE ML155-WK-PARTN-NBR        TO CR-PARTN-NBR           07580087
               MOVE ML155-DEPT-NBR            TO CR-DEPT-NBR            07600087
               MOVE ML155-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          07610087
               MOVE ML155-SUB-CL-NBR          TO CR-SUB-CL-NBR          07611087
               MOVE ML155-LOC-NBR             TO CR-LOC-NBR             07611187
W28582         MOVE ML155-ML-LWST-ID          TO PV-ML-LWST-ID          07611299
W28582         MOVE PV-ML-LWST-ID             TO CR-ML-LWST-ID          07611399
W28582***      MOVE ML155-ENT-ID              TO PV-ENT-ID              07611499
W28582***      MOVE PV-ENT-ID                 TO CR-ENT-ID              07611599
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    07640000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  07650000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       07660000
                                             TCKRSTINF-WORK-STRG-DESC   07670000
               IF PS-FIRST-COMMIT                                       07680000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                07690000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                07700000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       07710000
               END-IF                                                   07720000
               PERFORM 7800-COMMIT-CHANGES                              07730000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        07740000
           END-IF.                                                      07750000
      *                                                                 07760000
      *                                                                 07770000
      ******************************************************************07780000
      * 7800-COMMIT-CHANGES.                                            07790000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      07800000
      *            TAKE A COMMIT.                                       07810000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    07820000
      ******************************************************************07830000
       7800-COMMIT-CHANGES.                                             07840000
                                                                        07850000
           EXEC SQL                                                     07860000
             UPDATE TCKRSTINF                                           07870000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          07880000
              WHERE JOB_NAME       = :PC-JOB-NAME                       07890000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   07900000
           END-EXEC.                                                    07910000
                                                                        07920000
           IF SQLCODE = 0                                               07930000
               CONTINUE                                                 07941006
           ELSE                                                         07950000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED07960000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07970000
               PERFORM 9999-SQL-ABEND                                   07980000
           END-IF.                                                      07990000
                                                                        08000000
           EXEC SQL                                                     08010000
               COMMIT                                                   08020000
           END-EXEC.                                                    08030000
                                                                        08040000
           IF SQLCODE = 0                                               08050000
               ADD 1 TO PA-COMMIT-COUNT                                 08051006
           ELSE                                                         08070000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED08080000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     08090000
               PERFORM 9999-SQL-ABEND                                   08100000
           END-IF.                                                      08110000
      *                                                                 08120000
      ******************************************************************08130000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   08140000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   08150000
      ******************************************************************08160000
       7900-UPDATE-CHECKPOINT-STATUS.                                   08170000
                                                                        08180000
           EXEC SQL                                                     08190000
             UPDATE  TCKRSTCNTL                                         08200000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        08210000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           08220000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       08230000
           END-EXEC.                                                    08240000
                                                                        08250000
           IF SQLCODE = 0                                               08260000
               CONTINUE                                                 08270000
           ELSE                                                         08280000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME08290000
               PERFORM 9999-SQL-ABEND                                   08300000
           END-IF.                                                      08310000
                                                                        08320000
           EJECT                                                        08330000
      *                                                                 08340000
      *                                                                 08350000
      ******************************************************************08360000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST MLT_WKLY_SUM ROW FOR  08370059
      * UPDATE.  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST  08380059
      * COMMIT.                                                         08381059
      ******************************************************************08390000
       7999-RESTART-PROCESS.                                            08400000
      *                                                                 08410000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           08420000
      *                                                                 08430000
           CLOSE CONDENSED-FILE.                                        08440000
      *                                                                 08450000
           PERFORM 7500-SELECT-RESTART-INFO.                            08460000
      *                                                                 08470000
           DISPLAY '**** RESTART **** '.                                08480000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 08490000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   08500000
                                                                        08510000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      08520000
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        08530000
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       08540000
           IF PS-FIRST-COMMIT                                           08550000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)               08560000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     08570000
                      ' BEGINNING'                                      08580000
           ELSE                                                         08590000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           08600000
                        TCKRSTINF-WORK-STRG-DESC (1:35)                 08610010
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 08620000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         08630000
                    CR-CHECKPOINT-RESTART-AREA                          08640000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                08650000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   08680000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 08690000
               DISPLAY 'SUB CLASS NBR   ' CR-SUB-CL-NBR                 08691081
               DISPLAY 'LOCATION NBR    ' CR-LOC-NBR                    08691181
W28582         DISPLAY 'ML LWST ID      ' CR-ML-LWST-ID                 08691299
W28582***      DISPLAY 'ENT-ID          ' CR-ENT-ID                     08691399
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           08710000
           END-IF.                                                      08720000
                                                                        08730000
           OPEN INPUT CONDENSED-FILE.                                   08740000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      08750000
      *                                                                 08760000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          08770000
           PERFORM 4000-READ-CONDENSED-FILE                             08780000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              08790000
           DISPLAY 'INPUT RECORD RESTARTED ON RECORD '                  08800000
                    PA-INPUT-COUNT                                      08810000
           DISPLAY 'PARTITION NBR         ' ML155-WK-PARTN-NBR.         08820087
           DISPLAY 'DEPARTMENT NBR        ' ML155-DEPT-NBR.             08840087
           DISPLAY 'MAJOR CLASS NBR       ' ML155-MAJ-CL-NBR.           08850087
           DISPLAY 'SUB CLASS NBR         ' ML155-SUB-CL-NBR.           08851087
           DISPLAY 'LOCATION NBR          ' ML155-LOC-NBR.              08851187
W28582     MOVE ML155-ML-LWST-ID TO PV-ML-LWST-ID.                      08851299
W28582     DISPLAY 'ML LWST ID            ' PV-ML-LWST-ID.              08851399
W28582***  MOVE ML155-ENT-ID TO PV-ENT-ID.                              08851499
W28582***  DISPLAY 'ENT-ID                ' PV-ENT-ID.                  08851599
      ******************************************************************08880000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *08890000
      ******************************************************************08900000
       8000-EOJ-ROUTINE.                                                08910000
      *                                                                 08920000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       08930000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       08940000
                                                                        08950000
      *                                                                 08960000
           DISPLAY SPACES.                                              08970000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         08980000
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        08990000
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        09000000
           DISPLAY SPACES.                                              09010000
      *                                                                 09020000
           CLOSE CONDENSED-FILE.                                        09030000
      *                                                                 09040000
      *                                                                 09050000
      ******************************************************************09060000
      *  PERFORMED BY 7100-UPDATE AND 7200-INSERT                       09070003
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   09080000
      ******************************************************************09090000
       9000-DEADLOCK-ERROR.                                             09100000
      *                                                                 09110000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     09120000
               PERFORM 9999-SQL-ABEND.                                  09130000
      *                                                                 09140000
      ******************************************************************09150000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               09160000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             09170000
      ******************************************************************09180000
       9999-SQL-ABEND.                                                  09190000
                                                                        09200000
           MOVE +4013                 TO ABEND-CODE.                    09210000
           DISPLAY '**  ABEND     **'.                                  09220000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              09230000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            09240000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       09250000
                                                                        09260000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         09270000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        09280000
                                                                        09290000
           COPY DPPD004.                                                09300000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           09310000
