       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      XSKUT020.                                       00020000
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040000
       DATE-WRITTEN.    10/02/2002.                                     00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **XS DOMAIN TABLE INTEGRITY PROCESS - FOR TABLE XST_SKU           00080000
      ******************************************************************00090000
      **THIS PROGRAM VALIDATES THE DATA IN THE XS DOMAIN IS THE SAME    00100000
      **AS THE DATA IN THE SOR - VSM.                                   00110000
      ******************************************************************00120000
      *DATE:10/02/2002   MADE BY:JULIE SMITH INITIAL PROGRAM WRITE      00130000
011403*DATE:01/14/2003   MADE BY:JULIE SMITH FIXES                      00140000
022703*DATE:02/07/2003   MADE BY:JULIE SMITH FIX TO COMP FIELD MOVES    00150000
091004*DATE:09/10/2004   MADE BY:JULIE TEWES ADD CUSTFACG_CLOR_DESC AND 00160067
112006*                  POG_IND                                        00170069
112006*DATE:11/20/2006   MADE BY:RICH KELLEN COMMENT OUT WT-UM-CDE CHECK00171069
112006*DATE:11/20/2006   AS THE SOURCE FIELDS USED IN THIS CHECK ARE OF 00172070
112006*DATE:11/20/2006   DIFFERENT FORMATS MAKING THE CHECK INVALID.    00173070
      ******************************************************************00180000
       ENVIRONMENT DIVISION.                                            00190000
       CONFIGURATION SECTION.                                           00200000
       SOURCE-COMPUTER.        IBM-3090.                                00210000
       OBJECT-COMPUTER.        IBM-3090.                                00220000
       INPUT-OUTPUT SECTION.                                            00230000
       FILE-CONTROL.                                                    00240000
                                                                        00250000
           SELECT CONTROL-CARD                                          00260000
                  ASSIGN TO INP00.                                      00270000
           SELECT VSM-EXTRACT                                           00280000
                  ASSIGN TO INP01.                                      00290000
           SELECT XS-EXTRACT                                            00300000
                  ASSIGN TO INP02.                                      00310000
           SELECT DIFFERENCES-FILE                                      00320000
                  ASSIGN TO OUT01.                                      00330000
                                                                        00340000
       DATA DIVISION.                                                   00350000
       FILE SECTION.                                                    00360000
                                                                        00370000
       FD  CONTROL-CARD                                                 00380000
           RECORDING MODE F                                             00390000
           BLOCK CONTAINS 0 RECORDS                                     00400000
           LABEL RECORDS ARE OMITTED                                    00410000
           DATA RECORD IS CONTROL-CARD-RECORD.                          00420000
       01  CONTROL-CARD-RECORD             PIC X(080).                  00430000
                                                                        00440000
       FD  VSM-EXTRACT                                                  00450000
           RECORDING MODE F                                             00460000
           BLOCK CONTAINS 0 RECORDS                                     00470000
           LABEL RECORDS ARE OMITTED                                    00480000
           DATA RECORD IS VSM-RECORD.                                   00490000
       01  VSM-RECORD                      PIC X(500).                  00500000
                                                                        00510000
       FD  XS-EXTRACT                                                   00520000
           RECORDING MODE F                                             00530000
           BLOCK CONTAINS 0 RECORDS                                     00540000
           LABEL RECORDS ARE OMITTED                                    00550000
           DATA RECORD IS XS-RECORD.                                    00560000
       01  XS-RECORD                       PIC X(155).                  00570072
                                                                        00580000
       FD  DIFFERENCES-FILE                                             00590000
           RECORDING MODE F                                             00600000
           BLOCK CONTAINS 0 RECORDS                                     00610000
           LABEL RECORDS ARE OMITTED                                    00620000
           DATA RECORD IS DIFFERENCES-RECORD.                           00630000
       01  DIFFERENCES-RECORD              PIC X(400).                  00640067
                                                                        00650000
       WORKING-STORAGE SECTION.                                         00660000
      ******************************************************************00670000
      **SWITCHES                                                        00680000
      ******************************************************************00690000
       01  PS-PROGRAM-SWITCHES.                                         00700000
           05  PS-END-OF-VSM-EXTRACT-SW        PIC X(01) VALUE 'N'.     00710000
               88  PS-END-OF-VSM-EXTRACT                 VALUE 'Y'.     00720000
               88  PS-NOT-END-OF-VSM-EXTRACT             VALUE 'N'.     00730000
           05  PS-END-OF-XS-EXTRACT-SW         PIC X(01) VALUE 'N'.     00740000
               88  PS-END-OF-XS-EXTRACT                  VALUE 'Y'.     00750000
               88  PS-NOT-END-OF-XS-EXTRACT              VALUE 'N'.     00760000
           05  PS-ERROR-FOUND-SW               PIC X(01) VALUE 'N'.     00770000
               88  PS-ERROR-FOUND                        VALUE 'Y'.     00780000
               88  PS-ERROR-NOT-FOUND                    VALUE 'N'.     00790000
      ******************************************************************00800000
      **CONTROL CARD LAYOUT                                             00810000
      ******************************************************************00820000
       01  CONTROL-CARD-LAYOUT.                                         00830000
           05  CC-START-POINT                  PIC 9(05) VALUE ZEROS.   00840000
           05  FILLER                          PIC X(01) VALUE SPACES.  00850000
           05  CC-READ-FREQUENCY               PIC 9(05) VALUE ZEROS.   00860000
           05  FILLER                          PIC X(69) VALUE SPACES.  00870000
      ******************************************************************00880000
      **TIMESTAMP VARIABLES                                             00890000
      ******************************************************************00900000
       01  SD-TIMESTAMP                        PIC X(26) VALUE SPACES.  00910000
       01  FILLER REDEFINES SD-TIMESTAMP.                               00920000
           05  SD-CURR-DTE                     PIC X(10).               00930000
           05  FILLER                          PIC X(01).               00940000
           05  SD-HOUR                         PIC X(02).               00950000
           05  FILLER                          PIC X(01).               00960000
           05  SD-MINUTE                       PIC X(02).               00970000
           05  FILLER                          PIC X(01).               00980000
           05  SD-SECOND                       PIC X(02).               00990000
           05  FILLER                          PIC X(01).               01000000
           05  SD-MICROSEC                     PIC X(06).               01010000
      ******************************************************************01020000
      **PROGRAM CONSTANTS                                               01030000
      ******************************************************************01040000
       01  PC-PROGRAM-CONSTANTS.                                        01050000
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)                01060000
                                                       VALUE 'XSKUT020'.01070000
      ******************************************************************01080000
      **PROGRAM COUNTERS                                                01090000
      ******************************************************************01100000
       01  PC-PROGRAM-COUNTER.                                          01110000
           05  PC-RECORD-COUNT                 PIC S9(09) VALUE ZEROS   01120000
                                                          COMP-3.       01130000
           05  PC-VSM-EXTRACT-RECS-READ        PIC 9(09) VALUE ZEROS.   01140000
           05  PC-XS-EXTRACT-RECS-READ         PIC 9(09) VALUE ZEROS.   01150000
           05  PC-ERROR-RECORD-WRITTEN         PIC 9(09) VALUE ZEROS.   01160000
      ******************************************************************01170000
      **PROGRAM VARIABLES                                               01180000
      ******************************************************************01190000
       01  PV-PROGRAM-VARIABLES.                                        01200000
           05  PV-VSM-INTRNL-UPC-ID-COMP       PIC S9(15) COMP.         01210000
           05  PV-VSM-INTRNL-UPC-ID            PIC 9(10).               01220000
           05  PV-XS-INTRNL-UPC-ID-COMP        PIC S9(15) COMP.         01230000
           05  PV-XS-INTRNL-UPC-ID             PIC 9(10).               01240000
      ******************************************************************01250000
      **ERROR HANDLING                                                  01260000
      ******************************************************************01270000
       01  PV-MISC-ABEND-FIELDS.                                        01280000
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.      01290000
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.      01300000
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.      01310000
           05  PV-DB2-OPERATION             PIC X(30) VALUE SPACE.      01320000
       01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 01330000
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           01340000
      ******************************************************************01350000
      **COPYBOOKS                                                       01360000
      ******************************************************************01370000
      ***VSM EXTRACT FILE LAYOUT                                        01380000
           COPY IMRD053.                                                01390000
      ***XS EXTRACT FILE LAYOUT                                         01400000
           COPY XSRD011.                                                01410000
      ***XS ERROR FILE LAYOUT                                           01420000
           COPY XSRD012.                                                01430000
      ***VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        01440000
           COPY DPWS004.                                                01450000
      ***DB2 ABEND COPYBOOK                                             01460000
           COPY DPWS900.                                                01470000
      ***DB2 MESSAGE AREA                                               01480000
           COPY SQLCA2.                                                 01490000
      ******************************************************************01500000
      **TABLE INCLUSIONS                                                01510000
      ******************************************************************01520000
      ***DB2 COMMUNICATIONS AREA                                        01530000
           EXEC SQL                                                     01540000
                INCLUDE SQLCA                                           01550000
           END-EXEC.                                                    01560000
      ***TABLE XST_SKU                                                  01570000
           EXEC SQL                                                     01580000
                INCLUDE XST00010                                        01590000
           END-EXEC.                                                    01600000
                                                                        01610000
       PROCEDURE DIVISION.                                              01620000
      ***************************************************************** 01630000
       000-MAINLINE.                                                    01640000
                                                                        01650000
           PERFORM 1000-INITIALIZATION.                                 01660000
           PERFORM 2000-PROCESS-RECORDS                                 01670000
                   UNTIL PS-END-OF-VSM-EXTRACT                          01680000
                      OR PS-END-OF-XS-EXTRACT.                          01690000
           PERFORM 3000-PROCESS-EXTRA-RECORDS                           01700000
                   UNTIL PS-END-OF-VSM-EXTRACT                          01710000
                     AND PS-END-OF-XS-EXTRACT.                          01720000
           PERFORM 9999-WRAPUP.                                         01730000
           GOBACK.                                                      01740000
                                                                        01750000
       EJECT.                                                           01760000
      ******************************************************************01770000
      **INITIALIZATION AREA                                             01780000
      ******************************************************************01790000
       1000-INITIALIZATION.                                             01800000
                                                                        01810000
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01820000
                                                                        01830000
           INITIALIZE PC-PROGRAM-COUNTER.                               01840000
                                                                        01850000
           OPEN INPUT  CONTROL-CARD                                     01860000
                       VSM-EXTRACT                                      01870000
                       XS-EXTRACT                                       01880000
                OUTPUT DIFFERENCES-FILE.                                01890000
                                                                        01900000
           PERFORM 1100-SELECT-DB2-TIME.                                01910000
           PERFORM 1200-SELECT-RECORD-COUNT.                            01920000
                                                                        01930000
           PERFORM 9999-READ-CONTROL-CARD.                              01940000
           PERFORM 9999-READ-VSM-EXTRACT CC-START-POINT TIMES.          01950000
           PERFORM 9999-READ-XS-EXTRACT  CC-START-POINT TIMES.          01960000
                                                                        01970000
       EJECT.                                                           01980000
      ******************************************************************01990000
      **RETRIEVE TIME FROM DB2                                          02000000
      ******************************************************************02010000
       1100-SELECT-DB2-TIME.                                            02020000
                                                                        02030000
           MOVE '1100-SELECT-DB2-TIME' TO PV-PARAGRAPH-NAME.            02040000
                                                                        02050000
           EXEC SQL                                                     02060000
                SELECT CURRENT TIMESTAMP                                02070000
                INTO  :SD-TIMESTAMP                                     02080000
                FROM   SYSIBM.SYSDUMMY1                                 02090000
           END-EXEC.                                                    02100000
                                                                        02110000
           EVALUATE TRUE                                                02120000
              WHEN SQLCODE = ZERO                                       02130000
                   CONTINUE                                             02140000
              WHEN OTHER                                                02150000
               PERFORM 9999-ABEND-ROUTINE                               02160000
           END-EVALUATE.                                                02170000
                                                                        02180000
       EJECT.                                                           02190000
      ******************************************************************02200000
      **RETRIEVE TIME FROM DB2                                          02210000
      ******************************************************************02220000
       1200-SELECT-RECORD-COUNT.                                        02230000
                                                                        02240000
           MOVE '1200-SELECT-RECORD-COUNT' TO PV-PARAGRAPH-NAME.        02250000
                                                                        02260000
           EXEC SQL                                                     02270000
                SELECT COUNT(*)                                         02280000
                INTO  :PC-RECORD-COUNT                                  02290000
                FROM   XST_SKU                                          02300000
           END-EXEC.                                                    02310000
                                                                        02320000
           EVALUATE TRUE                                                02330000
              WHEN SQLCODE = ZERO                                       02340000
                   CONTINUE                                             02350000
              WHEN OTHER                                                02360000
                   PERFORM 9999-ABEND-ROUTINE                           02370000
           END-EVALUATE.                                                02380000
                                                                        02390000
       EJECT.                                                           02400000
      ******************************************************************02410000
      **PROCESSES INPUT FILE                                            02420000
      ******************************************************************02430000
       2000-PROCESS-RECORDS.                                            02440000
                                                                        02450000
           MOVE '2000-PROCESS-RECORDS' TO PV-PARAGRAPH-NAME.            02460000
                                                                        02470000
           INITIALIZE XSRD012-SKU-ERR-RECORD.                           02480000
                                                                        02490000
022703     MOVE IM053-INTR-UPC-ID         TO PV-VSM-INTRNL-UPC-ID-COMP. 02500000
022703     MOVE PV-VSM-INTRNL-UPC-ID-COMP TO PV-VSM-INTRNL-UPC-ID.      02510000
022703     MOVE XSRD011-INTR-UPC-ID       TO PV-XS-INTRNL-UPC-ID-COMP.  02520000
022703     MOVE PV-XS-INTRNL-UPC-ID-COMP  TO PV-XS-INTRNL-UPC-ID.       02530000
                                                                        02540000
           EVALUATE TRUE                                                02550000
              WHEN PV-VSM-INTRNL-UPC-ID = PV-XS-INTRNL-UPC-ID           02560000
                   PERFORM 2100-COMPARE-FIELDS                          02570000
                   PERFORM 9999-READ-XS-EXTRACT  CC-READ-FREQUENCY TIMES02580000
                   PERFORM 9999-READ-VSM-EXTRACT CC-READ-FREQUENCY TIMES02590000
              WHEN PV-VSM-INTRNL-UPC-ID > PV-XS-INTRNL-UPC-ID           02600000
                   PERFORM 9999-MOVE-XS-VALUES                          02610000
                   PERFORM 9999-WRITE-ERROR-RECORD                      02620000
                   PERFORM 9999-READ-XS-EXTRACT                         02630000
              WHEN PV-VSM-INTRNL-UPC-ID < PV-XS-INTRNL-UPC-ID           02640000
                   PERFORM 9999-MOVE-VSM-VALUES                         02650000
                   PERFORM 9999-WRITE-ERROR-RECORD                      02660000
                   PERFORM 9999-READ-VSM-EXTRACT                        02670000
           END-EVALUATE.                                                02680000
                                                                        02690000
       EJECT.                                                           02700000
      ******************************************************************02710000
      **COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   02720000
      ******************************************************************02730000
       2100-COMPARE-FIELDS.                                             02740000
                                                                        02750000
           MOVE '2100-COMPARE-FIELDS' TO PV-PARAGRAPH-NAME.             02760000
                                                                        02770000
           INITIALIZE PS-ERROR-FOUND-SW.                                02780000
                                                                        02790000
           IF  XSRD011-STYL-ID      NOT EQUAL IM053-RMS-STYL-ID         02800000
               SET PS-ERROR-FOUND     TO TRUE                           02810000
           END-IF.                                                      02820000
                                                                        02830000
           IF  XSRD011-SKU-NBR      NOT EQUAL IM053-SKU                 02840000
               SET PS-ERROR-FOUND     TO TRUE                           02850000
           END-IF.                                                      02860000
                                                                        02870000
           IF  XSRD011-SKU-DESC     NOT EQUAL IM053-INTR-UPC-DESC       02880000
               SET PS-ERROR-FOUND     TO TRUE                           02890000
           END-IF.                                                      02900000
                                                                        02910000
           IF  XSRD011-KOHLS-SIZE-CDE NOT EQUAL IM053-KOHLS-SIZE-CDE    02920000
               SET PS-ERROR-FOUND     TO TRUE                           02930000
           END-IF.                                                      02940000
                                                                        02950000
           IF  XSRD011-SIZE-DESC    NOT EQUAL IM053-KOHLS-SIZE-DESC     02960000
               SET PS-ERROR-FOUND     TO TRUE                           02970000
           END-IF.                                                      02980000
                                                                        02990000
           IF  XSRD011-NRF-CLOR-CDE NOT EQUAL IM053-NRF-CLOR-CDE        03000000
               SET PS-ERROR-FOUND     TO TRUE                           03010000
           END-IF.                                                      03020000
                                                                        03030000
           IF  XSRD011-NRF-CLOR-DESC NOT EQUAL IM053-NRF-CLOR-DESC      03040000
               SET PS-ERROR-FOUND     TO TRUE                           03050000
           END-IF.                                                      03060000
                                                                        03070000
           IF  XSRD011-CSTM-CLOR-DESC NOT EQUAL IM053-CSTM-CLOR-DESC    03080000
               SET PS-ERROR-FOUND     TO TRUE                           03090000
           END-IF.                                                      03100000
                                                                        03110000
           IF  XSRD011-REGN-PRIC-IND NOT EQUAL IM053-REGN-PRIC-IND      03120000
               SET PS-ERROR-FOUND     TO TRUE                           03130000
           END-IF.                                                      03140000
                                                                        03150000
           IF  XSRD011-TX-PRD-CDE-DESC NOT EQUAL IM053-TX-PRD-CDE-DESC  03160000
               SET PS-ERROR-FOUND     TO TRUE                           03170000
           END-IF.                                                      03180000
                                                                        03190000
           IF  XSRD011-E-BUS-ITM-IND NOT EQUAL IM053-E-BUS-ITM-IND      03200000
               SET PS-ERROR-FOUND     TO TRUE                           03210000
           END-IF.                                                      03220000
                                                                        03230000
           IF  XSRD011-E-BUS-PRD-GP-NBR NOT EQUAL IM053-PRD-GP-NBR      03240000
               SET PS-ERROR-FOUND     TO TRUE                           03250000
           END-IF.                                                      03260000
                                                                        03270000
112006*    IF  XSRD011-WT-UM-CDE     NOT EQUAL IM053-WT-UM-CDE          03280070
112006*        IF  IM053-WT-UM-CDE = '0 ' AND XSRD011-WT-UM-CDE = SPACES03290070
112006*            CONTINUE                                             03300070
112006*        ELSE                                                     03310070
112006*            SET PS-ERROR-FOUND     TO TRUE                       03320070
112006*        END-IF                                                   03330070
112006*    END-IF.                                                      03340070
                                                                        03350000
           IF  XSRD011-WT-UM-QTY     NOT EQUAL IM053-WT-UM-QTY          03360000
               SET PS-ERROR-FOUND     TO TRUE                           03370000
           END-IF.                                                      03380000
                                                                        03390000
           IF  XSRD011-SIZE-UM-CDE   NOT EQUAL IM053-SIZE-UM-CDE        03400000
               SET PS-ERROR-FOUND     TO TRUE                           03410000
           END-IF.                                                      03420000
                                                                        03430000
           IF  XSRD011-WD-UM-QTY     NOT EQUAL IM053-WD-UM-QTY          03440000
               SET PS-ERROR-FOUND     TO TRUE                           03450000
           END-IF.                                                      03460000
                                                                        03470000
           IF  XSRD011-HT-UM-QTY     NOT EQUAL IM053-HT-UM-QTY          03480000
               SET PS-ERROR-FOUND     TO TRUE                           03490000
           END-IF.                                                      03500000
                                                                        03510000
           IF  XSRD011-DPTH-UM-QTY   NOT EQUAL IM053-DPTH-UM-QTY        03520000
               SET PS-ERROR-FOUND     TO TRUE                           03530000
           END-IF.                                                      03540000
                                                                        03550000
091004     IF  XSRD011-CUSTFACG-CLOR-DESC                               03560067
091004                               NOT EQUAL IM053-CUSTFACG-CLOR-DESC 03570067
091004         SET PS-ERROR-FOUND     TO TRUE                           03580067
091004     END-IF.                                                      03590067
                                                                        03600067
091004     IF  XSRD011-POG-IND       NOT EQUAL IM053-POG-IND            03610067
091004         SET PS-ERROR-FOUND     TO TRUE                           03620067
091004     END-IF.                                                      03630067
122007     IF  XSRD011-GP-CDE        NOT EQUAL IM053-GP-CDE             03631071
122007         SET PS-ERROR-FOUND     TO TRUE                           03632071
122007     END-IF.                                                      03633071
                                                                        03640067
           IF  PS-ERROR-FOUND                                           03650000
               PERFORM 9999-MOVE-XS-VALUES                              03660000
               PERFORM 9999-MOVE-VSM-VALUES                             03670000
               PERFORM 9999-WRITE-ERROR-RECORD                          03680000
           END-IF.                                                      03690000
                                                                        03700000
       EJECT.                                                           03710000
      ******************************************************************03720000
      **COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   03730000
      ******************************************************************03740000
       3000-PROCESS-EXTRA-RECORDS.                                      03750000
                                                                        03760000
           MOVE '3000-PROCESS-EXTRA-RECORDS' TO PV-PARAGRAPH-NAME.      03770000
                                                                        03780000
           INITIALIZE DIFFERENCES-RECORD.                               03790000
                                                                        03800000
           IF  PS-NOT-END-OF-VSM-EXTRACT                                03810000
               PERFORM 9999-MOVE-VSM-VALUES                             03820000
               PERFORM 9999-WRITE-ERROR-RECORD                          03830000
               PERFORM 9999-READ-VSM-EXTRACT                            03840000
           END-IF.                                                      03850000
                                                                        03860000
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03870000
               PERFORM 9999-MOVE-XS-VALUES                              03880000
               PERFORM 9999-WRITE-ERROR-RECORD                          03890000
               PERFORM 9999-READ-XS-EXTRACT                             03900000
           END-IF.                                                      03910000
                                                                        03920000
       EJECT.                                                           03930000
      ***************************************************************** 03940000
      **READ CONTROL CARD FOR FREQUENCY OF COMPARE                      03950000
      ***************************************************************** 03960000
       9999-READ-CONTROL-CARD.                                          03970000
                                                                        03980000
           READ CONTROL-CARD INTO CONTROL-CARD-LAYOUT.                  03990000
                                                                        04000000
       EJECT.                                                           04010000
      ***************************************************************** 04020000
      **READ VSM EXTRACT FILE                                           04030000
      ***************************************************************** 04040000
       9999-READ-VSM-EXTRACT.                                           04050000
                                                                        04060000
           IF  PS-NOT-END-OF-VSM-EXTRACT                                04070000
               READ VSM-EXTRACT INTO IM053-ITEM-SKU-DOMAIN-REC          04080000
                    AT END                                              04090000
                       SET PS-END-OF-VSM-EXTRACT TO TRUE                04100000
               END-READ                                                 04110000
           END-IF.                                                      04120000
                                                                        04130000
           IF  PS-NOT-END-OF-VSM-EXTRACT                                04140000
               ADD +1                       TO PC-VSM-EXTRACT-RECS-READ 04150000
           END-IF.                                                      04160000
                                                                        04170000
       EJECT.                                                           04180000
      ***************************************************************** 04190000
      **READ XS EXTRACT FILE                                            04200000
      ***************************************************************** 04210000
       9999-READ-XS-EXTRACT.                                            04220000
                                                                        04230000
           IF  PS-NOT-END-OF-XS-EXTRACT                                 04240000
               READ XS-EXTRACT INTO XSRD011-SKU-UNLOAD-FILE             04250000
                    AT END                                              04260000
                       SET PS-END-OF-XS-EXTRACT TO TRUE                 04270000
               END-READ                                                 04280000
           END-IF.                                                      04290000
                                                                        04300000
           IF  PS-NOT-END-OF-XS-EXTRACT                                 04310000
               ADD +1                       TO PC-XS-EXTRACT-RECS-READ  04320000
           END-IF.                                                      04330000
                                                                        04340000
       EJECT.                                                           04350000
      ***************************************************************** 04360000
      **MOVES XS EXTRACT VALUES TO ERROR RECORD                         04370000
      ***************************************************************** 04380000
       9999-MOVE-XS-VALUES.                                             04390000
                                                                        04400000
022703     MOVE PV-XS-INTRNL-UPC-ID      TO XSRD012-XS-INTRL-UPC-ID.    04410000
           MOVE XSRD011-STYL-ID          TO XSRD012-XS-STYL-ID.         04420000
           MOVE XSRD011-SKU-NBR          TO XSRD012-XS-SKU-NBR.         04430000
           MOVE XSRD011-SKU-DESC         TO XSRD012-XS-SKU-DESC.        04440000
           MOVE XSRD011-KOHLS-SIZE-CDE   TO XSRD012-XS-KOHLS-SIZE-CDE.  04450000
           MOVE XSRD011-SIZE-DESC        TO XSRD012-XS-SIZE-DESC.       04460000
           MOVE XSRD011-NRF-CLOR-CDE     TO XSRD012-XS-NRF-CLOR-CDE.    04470000
           MOVE XSRD011-NRF-CLOR-DESC    TO XSRD012-XS-NRF-CLOR-DESC.   04480000
           MOVE XSRD011-CSTM-CLOR-DESC   TO XSRD012-XS-CSTM-CLOR-DESC.  04490000
           MOVE XSRD011-REGN-PRIC-IND    TO XSRD012-XS-REGN-PRIC-IND.   04500000
           MOVE XSRD011-TX-PRD-CDE-DESC  TO XSRD012-XS-TX-PRD-CDE-DESC. 04510000
           MOVE XSRD011-E-BUS-ITM-IND    TO XSRD012-XS-E-BUS-ITM-IND.   04520000
           MOVE XSRD011-E-BUS-PRD-GP-NBR                                04530000
                                        TO XSRD012-XS-E-BUS-PRD-GRP-NBR.04540000
           MOVE XSRD011-WT-UM-CDE        TO XSRD012-XS-WT-UM-CDE.       04550000
           MOVE XSRD011-WT-UM-QTY        TO XSRD012-XS-WT-UM-QTY.       04560000
           MOVE XSRD011-SIZE-UM-CDE      TO XSRD012-XS-SIZE-UM-CDE.     04570000
           MOVE XSRD011-WD-UM-QTY        TO XSRD012-XS-WD-UM-QTY.       04580000
           MOVE XSRD011-HT-UM-QTY        TO XSRD012-XS-HT-UM-QTY.       04590000
           MOVE XSRD011-DPTH-UM-QTY      TO XSRD012-XS-DPTH-UM-QTY.     04600000
091004     MOVE XSRD011-CUSTFACG-CLOR-DESC                              04610067
091004                                 TO XSRD012-XS-CUSTFACG-CLOR-DESC.04620067
091004     MOVE XSRD011-POG-IND          TO XSRD012-XS-POG-IND.         04630067
122007     MOVE XSRD011-GP-CDE           TO XSRD012-XS-GP-CDE.          04631071
                                                                        04640000
       EJECT.                                                           04650000
      ***************************************************************** 04660000
      **MOVES VSM EXTRACT VALUES TO ERROR RECORD                        04670000
      ***************************************************************** 04680000
       9999-MOVE-VSM-VALUES.                                            04690000
                                                                        04700000
022703     MOVE PV-VSM-INTRNL-UPC-ID  TO XSRD012-VSM-INTRL-UPC-ID.      04710000
011403     MOVE IM053-RMS-STYL-ID     TO XSRD012-VSM-STYL-ID.           04720000
011403     MOVE IM053-SKU             TO XSRD012-VSM-SKU-NBR.           04730000
011403     MOVE IM053-INTR-UPC-DESC   TO XSRD012-VSM-SKU-DESC.          04740000
011403     MOVE IM053-KOHLS-SIZE-CDE  TO XSRD012-VSM-KOHLS-SIZE-CDE.    04750000
011403     MOVE IM053-KOHLS-SIZE-DESC TO XSRD012-VSM-SIZE-DESC.         04760000
011403     MOVE IM053-NRF-CLOR-CDE    TO XSRD012-VSM-NRF-CLOR-CDE.      04770000
011403     MOVE IM053-NRF-CLOR-DESC   TO XSRD012-VSM-NRF-CLOR-DESC.     04780000
011403     MOVE IM053-CSTM-CLOR-DESC  TO XSRD012-VSM-CSTM-CLOR-DESC.    04790000
011403     MOVE IM053-REGN-PRIC-IND   TO XSRD012-VSM-REGN-PRIC-IND.     04800000
011403     MOVE IM053-TX-PRD-CDE-DESC TO XSRD012-VSM-TX-PRD-CDE-DESC.   04810000
011403     MOVE IM053-E-BUS-ITM-IND   TO XSRD012-VSM-E-BUS-ITM-IND.     04820000
011403     MOVE IM053-PRD-GP-NBR      TO XSRD012-VSM-E-BUS-PRD-GRP-NBR. 04830000
011403     MOVE IM053-WT-UM-CDE       TO XSRD012-VSM-WT-UM-CDE.         04840000
011403     MOVE IM053-WT-UM-QTY       TO XSRD012-VSM-WT-UM-QTY.         04850000
011403     MOVE IM053-SIZE-UM-CDE     TO XSRD012-VSM-SIZE-UM-CDE.       04860000
011403     MOVE IM053-WD-UM-QTY       TO XSRD012-VSM-WD-UM-QTY.         04870000
011403     MOVE IM053-HT-UM-QTY       TO XSRD012-VSM-HT-UM-QTY.         04880000
011403     MOVE IM053-DPTH-UM-QTY     TO XSRD012-VSM-DPTH-UM-QTY.       04890000
091004     MOVE IM053-CUSTFACG-CLOR-DESC                                04900067
091004                                TO XSRD012-VSM-CUSTFACG-CLOR-DESC.04910067
091004     MOVE IM053-POG-IND         TO XSRD012-VSM-POG-IND.           04920067
122007     MOVE IM053-GP-CDE          TO XSRD012-VSM-GP-CDE.            04921071
                                                                        04930000
       EJECT.                                                           04940000
      ***************************************************************** 04950000
      **ERROR FILE                                                      04960000
      ***************************************************************** 04970000
       9999-WRITE-ERROR-RECORD.                                         04980000
                                                                        04990000
           WRITE DIFFERENCES-RECORD FROM XSRD012-SKU-ERR-RECORD.        05000000
                                                                        05010000
           ADD +1                       TO PC-ERROR-RECORD-WRITTEN.     05020000
                                                                        05030000
       EJECT.                                                           05040000
      ****************************************************************  05050000
      *END OF PROGRAM ROUTINE                                           05060000
      ****************************************************************  05070000
       9999-WRAPUP.                                                     05080000
                                                                        05090000
           CLOSE CONTROL-CARD                                           05100000
                 VSM-EXTRACT                                            05110000
                 XS-EXTRACT                                             05120000
                 DIFFERENCES-FILE.                                      05130000
                                                                        05140000
           PERFORM 9999-DISPLAY-STATISTICS.                             05150000
                                                                        05160000
           IF  PC-ERROR-RECORD-WRITTEN > 0                              05170000
               MOVE 04               TO RETURN-CODE                     05180000
           END-IF.                                                      05190000
                                                                        05200000
       EJECT.                                                           05210000
      ****************************************************************  05220000
      *DISPLAY PROGRAM STATISTICS                                       05230000
      ****************************************************************  05240000
       9999-DISPLAY-STATISTICS.                                         05250000
                                                                        05260000
           DISPLAY '*************************************************'. 05270000
           DISPLAY '* PROGRAM..............: ' PC-CURRENT-PROGRAM-NAME. 05280000
           DISPLAY '* DATE/TIMESTAMP.......: ' SD-TIMESTAMP.            05290000
           DISPLAY '* RECORDS IN TABLE.....: ' PC-RECORD-COUNT.         05300000
           DISPLAY '* VSM EXTRACT RECORDS..: ' PC-VSM-EXTRACT-RECS-READ.05310000
           DISPLAY '* XS EXTRACT RECORDS...: ' PC-XS-EXTRACT-RECS-READ. 05320000
           DISPLAY '* ERROR RECORDS WRITTEN: ' PC-ERROR-RECORD-WRITTEN. 05330000
           DISPLAY '*************************************************'. 05340000
                                                                        05350000
       EJECT.                                                           05360000
      ******************************************************************05370000
      **SQL ABEND ROUTINE                                               05380000
      ******************************************************************05390000
       9999-ABEND-ROUTINE.                                              05400000
                                                                        05410000
           DISPLAY '** ABEND     **'.                                   05420000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        05430000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              05440000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               05450000
      ******************************************************************05460000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     05470000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      05480000
      ******************************************************************05490000
           COPY DPPD004.                                                05500000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            05510000
           CALL 'ILBOABN0' USING ABEND-CODE.                            05520000
                                                                        05530000
       EJECT                                                            05540000
