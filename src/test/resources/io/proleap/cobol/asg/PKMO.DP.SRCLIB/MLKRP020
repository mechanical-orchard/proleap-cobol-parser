00001  ID DIVISION.                                                             
206118 PROGRAM-ID. MLKRP020.                                                    
00003  AUTHOR. GIL WINSTON.                                                     
00005 *          THIS PROGRAM REPLACES THE KOHLS EASYTRIEVE PROGRAM             
00006 *           TO PRODUCE MONTHLY BATCH TOTALS.                              
00006 ******************************************************************        
00006 *  CHANGE - WR5701                                                        
00006 *      THIS PROGRAM WAS MOVED FROM PROC MLK1571 TO MLK1701.               
00006 *      THE PURPOSE OF THIS PROGRAM IS TO PRODUCE THE MONTHLY              
00006 *      'BATCH' TOTALS REPORT.                                             
00006 *                                                                         
00006 *      ALL INTERNAL SORT LOGIC HAS BE REMOVED.                            
00006 *      DATA FILE WILL BE SORTED PRIOR TO ENTERING THIS PROGRAM.           
00006 *      THE OUTPUT FILE ('OUTFILE' ASSIGN TO OUT01) HAS BEEN               
00006 *      REMOVED. THIS FILE IS NO LONGER PRODUCED OR NEEDED.                
      *                                                                         
      * DATE      PITS #/WR #  DESCRIPTION                                      
      * --------  -----------  ----------------------------------------         
      * 11/24/99               INCREASE THE LENGTH OF THE AMOUNT FIELDS         
      *                        FROM 99,999,999.00 TO 99,999,999,999.00          
      * 02/14/00               ADDED LOGIC TO HANDLE TRANS CODE '80' -          
      *                        INVENTORY ADJUSTMENTS.                           
      * 03/12/02   23511       MARKDOWN/MARKUP TRUNCATION                       
      * 12/20/05  W29908       ISSUE REPORTING ON INVENTORY ADJUSTMENTS         
      *                        CODE '80' WAS NOT INCLUDED ON REPORT             
      * 02/03/06  W29908       FIX INTERNAL COPYBOOK FOR SAVE-TRAN-80           
      *                        INPUT IS ONLY S9(3)V99 - NOT S9(11)V99           
206118* 08/20/18  CHG0206118   PROGRAM RENAMED FROM KDM2055 WHICH DID           
206118*                        NOT MEET STANDARDS FOR MAINFRAME                 
206118*           JIM HUNTER   REFACTORING.                                     
00006 ******************************************************************        
00007      SKIP1                                                                
00008  ENVIRONMENT DIVISION.                                                    
00009  INPUT-OUTPUT SECTION.                                                    
00010  FILE-CONTROL.                                                            
00011      SELECT  INFILE               ASSIGN INP01.                           
00012      SELECT  LEDGER-CONTROL-FILE  ASSIGN INP02.                           
00014      SELECT  PRINT-FILE           ASSIGN OUT01.                           
00016 *                                                                         
00017      SKIP1                                                                
00018  DATA DIVISION.                                                           
00019  FILE SECTION.                                                            
00020  FD  INFILE                                                               
00021      RECORDING MODE F                                                     
00022      LABEL RECORDS STANDARD                                               
00023      BLOCK CONTAINS  0 RECORDS                                            
00024      DATA RECORDS ARE IN-REC                                              
00025                       BATCH-10                                            
00026                       BATCH-30                                            
00027                       BATCH-31                                            
00028                       BATCH-40                                            
00029                       BATCH-50                                            
W29908                      BATCH-80.                                           
00030  01  IN-REC              PIC X(100).                                      
00031  01  BATCH-10.                                                            
00032      05  IN-CODE         PIC XX.                                          
00033       88  VALID-CODE      VALUE '10' '30' '31' '40' '50' '80'.            
23511      05  FILLER          PIC X(61).                                       
23511      05  NET-COST-10     PIC S9(7)V99   COMP-3.                           
23511      05  RETAIL-10       PIC S9(7)V99   COMP-3.                           
00037      05  FILLER          PIC X(8).                                        
00038      05  BTC10           PIC 999.                                         
23511      05  FILLER          PIC X(16).                                       
00040      SKIP1                                                                
00041  01  BATCH-30.                                                            
00042      05  FILLER          PIC X(38).                                       
23511      05  UNITS-30        PIC S9(9).                                       
23511      05  RETAIL-30       PIC S9(5)V99.                                    
00045      05  FILLER          PIC X.                                           
00046      05  BTC30           PIC 999.                                         
23511      05  FILLER          PIC X(42).                                       
00048      SKIP1                                                                
00049  01  BATCH-31.                                                            
00050      05  FILLER          PIC X(38).                                       
23511      05  UNITS-31        PIC S9(9).                                       
23511      05  RETAIL-31       PIC S9(5)V99.                                    
00053      05  FILLER          PIC X.                                           
00054      05  BTC31           PIC 999.                                         
23511      05  FILLER          PIC X(42).                                       
00056      SKIP1                                                                
00057  01  BATCH-40.                                                            
00058      05  FILLER          PIC XXX.                                         
00059      05  BTC40           PIC 999.                                         
00060      05  FILLER          PIC X(33).                                       
23511      05  OLD-RETAIL-40   PIC S9(5)V99.                                    
23511      05  NEW-RETAIL-40   PIC S9(5)V99.                                    
23511      05  QTY40           PIC S9(9).                                       
23511      05  FILLER          PIC X(38).                                       
00065      SKIP1                                                                
00066  01  BATCH-50.                                                            
00067      05  FILLER          PIC X(34).                                       
00068      05  OLD-RETAIL-50   PIC S9(5)V99   COMP-3.                           
00069      05  NEW-RETAIL-50   PIC S9(5)V99   COMP-3.                           
23511      05  QTY50           PIC S9(9)      COMP-3.                           
00071      05  FILLER          PIC X.                                           
00072      05  BTC50           PIC 999.                                         
00073      05  FILLER          PIC X(49).                                       
W29908     SKIP1                                                                
W29908 01  BATCH-80.                                                            
W29908     05  FILLER          PIC X(33).                                       
W29908     05  BATCH80-RETAIL  PIC S9(11)V99.                                   
W29908     05  BATCH80-COST    PIC S9(11)V99.                                   
W29908     05  FILLER          PIC X(36).                                       
W29908     05  BATCH80-BATCH   PIC X(03).                                       
W29908     05  FILLER          PIC X(02).                                       
00074      SKIP3                                                                
00075  FD  LEDGER-CONTROL-FILE                                                  
00076      RECORDING MODE F                                                     
00077      LABEL RECORDS STANDARD                                               
00078      BLOCK CONTAINS  0 RECORDS.                                           
00079  01  LEDGER-CONTROL-RECORD PIC X(80).                                     
00102 *                                                                         
00095  FD  PRINT-FILE                                                           
00096      RECORDING MODE F                                                     
00097      LABEL RECORDS OMITTED                                                
00098      BLOCK CONTAINS  0 RECORDS.                                           
00099  01  PRINT-REC.                                                           
00100 *    05  PRINT-CC        PIC X.                                           
00101      05  PRINT-LINE      PIC X(132).                                      
00102 *                                                                         
00080 *                                                                         
00103  WORKING-STORAGE SECTION.                                                 
00104      SKIP1                                                                
00105  01  FILLER              PIC  X(24) VALUE 'BEGIN WORKING-STORAGE'.        
23511  01  EXTRACT-RETAIL      PIC S9(13)V99 COMP-3 VALUE +0.                   
23511  01  EXTRACT-NETCOST     PIC S9(13)V99 COMP-3 VALUE +0.                   
23511  01  RETAIL-TOTAL        PIC S9(13)V99 COMP-3 VALUE +0.                   
23511  01  COST-TOTAL          PIC S9(13)V99 COMP-3 VALUE +0.                   
23511  01  GRAND-RETAIL        PIC S9(13)V99 COMP-3 VALUE +0.                   
23511  01  GRAND-COST          PIC S9(13)V99 COMP-3 VALUE +0.                   
00112  01  LINE-CNT            PIC 99               VALUE 0.                    
00113  01  PAGE-CNT            PIC 99               VALUE 0.                    
       01  RECORD-CNT          PIC 9(8)             VALUE 0.                    
00117 *                                                                         
00118  01  PS-PROGRAM-SWITCHES.                                                 
00119      05  PS-MORE-RECORDS-SW          PIC X  VALUE 'Y'.                    
00119          88 PS-NO-MORE-RECORDS              VALUE 'N'.                    
00119      05  PS-FIRST-TIME-SW            PIC X  VALUE 'Y'.                    
00119          88 PS-FIRST-TIME                   VALUE 'Y'.                    
00119          88 PS-NOT-FIRST-TIME               VALUE 'N'.                    
00119      05  PS-PRINT-LINE-SW            PIC X  VALUE 'N'.                    
00119          88 PS-CODE-TOTAL                   VALUE 'C'.                    
00119          88 PS-BATCH-TOTAL                  VALUE 'B'.                    
00119          88 PS-FINAL-TOTAL                  VALUE 'F'.                    
00119          88 PS-RECORD-TOTAL                 VALUE 'T'.                    
00119                                                                           
00118  01  LC-LEDGER-CONTROL-RECORD.                                            
00119      05  LC-LEDGER-IND   PIC X.                                           
00120          88  LC-MERCH-LEDGER                 VALUE 'M'.                   
00121          88  LC-INV-LEDGER                   VALUE 'I'.                   
00122      05  FILLER          PIC X(79).                                       
00123                                                                           
00124  01  PC-PROGRAM-CONSTANTS.                                                
00125       05  PC-MERCH-LEDGER-LIT                                             
00126                          PIC X(20)           VALUE                        
00127                                            '(MERCHANDISE LEDGER)'.        
00128       05  PC-INV-LEDGER-LIT                                               
00129                          PIC X(20)           VALUE                        
00130                                             '(INVENTORY LEDGER)'.         
00131  01  CURRENT-DATE.                                                        
00132      05  CD-YY           PIC X(02)           VALUE SPACES.                
00134      05  CD-MM           PIC X(02)           VALUE SPACES.                
00134      05  CD-DD           PIC X(02)           VALUE SPACES.                
00137                                                                           
00131  01  FORMATTED-DATE.                                                      
00132      05  FMT-MM          PIC X(02)           VALUE SPACES.                
00132      05  FILLER          PIC X(01)           VALUE '/'.                   
00134      05  FMT-DD          PIC X(02)           VALUE SPACES.                
00134      05  FILLER          PIC X(01)           VALUE '/'.                   
00134      05  FMT-YY          PIC X(02)           VALUE SPACES.                
   37                                                                           
00131  01  HOLD-AREA.                                                           
00132      05  HD-TRAN-CODE    PIC X(02)           VALUE SPACES.                
00133      05  FILLER          PIC X(93)           VALUE SPACES.                
00134      05  HD-BATCH-NBR    PIC 9(03)           VALUE ZEROES.                
00135      05  FILLER          PIC X(02)           VALUE SPACES.                
00137                                                                           
00131  01  SAVE-AREA.                                                           
00132      05  SA-TRAN-CODE    PIC X(02)           VALUE SPACES.                
00133      05  FILLER          PIC X(93)           VALUE SPACES.                
00134      05  SA-BATCH-NBR    PIC 9(03)           VALUE ZEROES.                
00135      05  FILLER          PIC X(02)           VALUE SPACES.                
00137                                                                           
00131  01  SAVE-TRAN-10.                                                        
23511      05  FILLER          PIC X(63).                                       
23511      05  SAVE-NETCOST-10 PIC S9(7)V99  COMP-3.                            
23511      05  SAVE-RETAIL-10  PIC S9(7)V99   COMP-3.                           
00135      05  FILLER          PIC X(8).                                        
00136      05  SAVE-BTC-10     PIC 999.                                         
23511      05  FILLER          PIC X(11).                                       
00137      05  SAVE-BATCH-10   PIC X(03).                                       
00137      05  FILLER          PIC X(02).                                       
                                                                                
00131  01  SAVE-TRAN-80.                                                        
00132      05  FILLER          PIC X(33).                                       
00133      05  SAVE-RETAIL-80  PIC S9(11)V99.                                   
W29908*    05  SAVE-COST-80    PIC S9(11)V99.                                   
W29908     05  SAVE-COST-80    PIC S9(03)V99.                                   
00135      05  FILLER          PIC X(37).                                       
00137      05  SAVE-BATCH-80   PIC X(03).                                       
00137      05  FILLER          PIC X(02).                                       
00138 *                                                                         
00138  01  SAVE-TRAN-30.                                                        
00139      05  FILLER          PIC X(38).                                       
23511      05  SAVE-UNITS-30   PIC S9(9).                                       
23511      05  SAVE-RETAIL-30  PIC S9(5)V99.                                    
00142      05  FILLER          PIC X.                                           
00143      05  SAVE-BTC30      PIC 999.                                         
23511      05  FILLER          PIC X(37).                                       
00144      05  SAVE-BATCH-30   PIC X(03).                                       
00144      05  FILLER          PIC X(02).                                       
00144 *                                                                         
00145  01  SAVE-TRAN-31.                                                        
00146      05  FILLER          PIC X(38).                                       
23511      05  SAVE-UNITS-31   PIC S9(9).                                       
23511      05  SAVE-RETAIL-31  PIC S9(5)V99.                                    
00149      05  FILLER          PIC X.                                           
00150      05  SAVE-BTC31      PIC 999.                                         
23511      05  FILLER          PIC X(37).                                       
00151      05  SAVE-BATCH-31   PIC X(03).                                       
00151      05  FILLER          PIC X(02).                                       
00144 *                                                                         
00152  01  SAVE-TRAN-40.                                                        
00153      05  FILLER          PIC XXX.                                         
00154      05  SAVE-BTC40      PIC 999.                                         
00155      05  FILLER          PIC X(33).                                       
23511      05  SAVE-OLD-RETAIL-40 PIC S9(5)V99.                                 
23511      05  SAVE-NEW-RETAIL-40 PIC S9(5)V99.                                 
23511      05  SAVE-QTY40      PIC S9(9).                                       
23511      05  FILLER          PIC X(33).                                       
00159      05  SAVE-BATCH-40   PIC X(03).                                       
00159      05  FILLER          PIC X(02).                                       
00159 *                                                                         
00160  01  SAVE-TRAN-50.                                                        
00161      05  FILLER          PIC X(34).                                       
00162      05  SAVE-OLD-RETAIL-50 PIC S9(5)V99 COMP-3.                          
00163      05  SAVE-NEW-RETAIL-50 PIC S9(5)V99 COMP-3.                          
23511      05  SAVE-QTY50      PIC S9(9)      COMP-3.                           
00167      05  FILLER          PIC X(01).                                       
00167      05  SAVE-BTC50      PIC 999.                                         
00167      05  FILLER          PIC X(44).                                       
00167      05  SAVE-BATCH-50   PIC X(03).                                       
00167      05  FILLER          PIC X(02).                                       
00168 *                                                                         
00168 *                                                                         
00169  01  HEAD-1.                                                              
00170 *    05  HD1-CC          PIC X   VALUE '1'.                               
00171      05  HD1-DATE        PIC X(8).                                        
00172      05  FILLER          PIC X(28) VALUE SPACES.                          
00173      05  FILLER          PIC X(89) VALUE                                  
00174       'K O H L S   D E P A R T M E N T   S T O R E S'.                    
00175      05  FILLER          PIC X(5)  VALUE 'PAGE '.                         
00176      05  HD1-PAGE        PIC ZZ.                                          
00177      SKIP1                                                                
00178  01  HEAD-2.                                                              
206118*    05  FILLER          PIC X(50) VALUE '0PGM: MLKRP020'.                
206118     05  FILLER          PIC X(39) VALUE 'PGM: MLKRP020'.                 
00180      05  FILLER          PIC X(20) VALUE                                  
00181       'MONTHLY BATCH TOTALS'.                                             
00182      05  FILLER          PIC X(2)  VALUE SPACE.                           
00183      05  H2-LEDGER-LIT   PIC X(20).                                       
00184                                                                           
00185  01  HEAD-3.                                                              
00186 *    05  FILLER          PIC X(37) VALUE '0'.                             
00186      05  FILLER          PIC X(32) VALUE SPACES.                          
00187      05  FILLER          PIC X(64) VALUE                                  
00188       'CODE  BATCH NO  RETAIL AMOUNT          COST AMOUNT     '.          
00189       SKIP1                                                               
00190  01  DETAIL-LINE.                                                         
00191 *    05  DETAIL-CC       PIC X(38)  VALUE SPACE.                          
00191      05  DETAIL-CC       PIC X(33)  VALUE SPACE.                          
00192      05  DETAIL-CODE     PIC XX.                                          
00193      05  FILLER          PIC X(5)    VALUE SPACES.                        
00194      05  DETAIL-BATCH    PIC 999.                                         
00195      05  DETAIL-RETAIL   PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.                      
00196      05  DETAIL-COST     PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.                      
00197       SKIP1                                                               
00198  01  TOTAL-LINE.                                                          
00199 *    05  TOTAL-CC        PIC X(35)    VALUE SPACE.                        
00199      05  TOTAL-CC        PIC X(30)    VALUE SPACE.                        
00200      05  TOTAL-LITERAL   PIC X(13).                                       
00201      05  TOTAL-RETAIL    PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.                      
00202      05  TOTAL-COST      PIC ZZ,ZZZ,ZZZ,ZZZ,ZZZ.99-.                      
00203  01  RECORD-TOTAL-LINE.                                                   
00204 *    05  FILLER          PIC X        VALUE '0'.                          
00205      05  TOTAL-RECORDS   PIC ZZ,ZZZ,ZZZ.                                  
00206      05  FILLER          PIC X(20)    VALUE                               
00207       ' RECORDS TOTALED'.                                                 
00208  01  ABEND-CODE          PIC S9(4)    VALUE ZERO COMP SYNC.               
00209      EJECT                                                                
00210 *                                                                         
00210 *                                                                         
00210  PROCEDURE DIVISION.                                                      
00214 *                                                                         
00211  0000-MAINLINE.                                                           
00212      PERFORM 1000-INITIALIZATION.                                         
00214 *                                                                         
00214      PERFORM 5000-OUTPUT-PROCESS                                          
00228           UNTIL PS-NO-MORE-RECORDS.                                       
00214 *                                                                         
00214      PERFORM 8000-EOJ-ROUTINE.                                            
00227 *                                                                         
00229      STOP RUN.                                                            
00230      EJECT                                                                
00227 *                                                                         
00212  1000-INITIALIZATION.                                                     
00212      OPEN INPUT  INFILE                                                   
00212                  LEDGER-CONTROL-FILE                                      
00212           OUTPUT PRINT-FILE.                                              
00259 *                                                                         
00259      MOVE 'Y' TO PS-FIRST-TIME-SW.                                        
00212      ACCEPT CURRENT-DATE FROM DATE.                                       
00212          MOVE CD-MM          TO FMT-MM.                                   
00212          MOVE CD-DD          TO FMT-DD.                                   
00212          MOVE CD-YY          TO FMT-YY.                                   
00212          MOVE FORMATTED-DATE TO HD1-DATE.                                 
00259 *                                                                         
00259      PERFORM 2000-READ-FILE.                                              
00259 *                                                                         
00213      PERFORM 0100-PROCESS-LEDGER-CONTROL.                                 
00259 *                                                                         
00259 *                                                                         
00231  0100-PROCESS-LEDGER-CONTROL.                                             
00232 *                                                                         
00235      READ LEDGER-CONTROL-FILE INTO LC-LEDGER-CONTROL-RECORD               
206118         AT END DISPLAY '*** ABEND *** IN MLKRP020'                       
00237                 DISPLAY 'LEDGER CONTROL CARD EMPTY'                       
00238                 MOVE +4003 TO ABEND-CODE                                  
00239                 CALL 'ILBOABN0' USING ABEND-CODE.                         
00240                                                                           
206118     DISPLAY 'MLKRP020 LEDGER CONTROL CARD:'                              
00242               LC-LEDGER-CONTROL-RECORD.                                   
00243                                                                           
00244      IF LC-MERCH-LEDGER                                                   
00245          MOVE PC-MERCH-LEDGER-LIT TO H2-LEDGER-LIT                        
00246      ELSE                                                                 
00247          IF LC-INV-LEDGER                                                 
00248              MOVE PC-INV-LEDGER-LIT   TO H2-LEDGER-LIT                    
00249          ELSE                                                             
206118             DISPLAY '*** ABEND *** IN MLKRP020'                          
00251              DISPLAY 'INVALID LEDGER INDICATOR ON CONTROL CARD.'          
00252              MOVE +4003 TO ABEND-CODE                                     
00253              CALL 'ILBOABN0' USING ABEND-CODE.                            
00254                                                                           
00266  2000-READ-FILE.                                                          
00267      READ INFILE INTO HOLD-AREA                                           
00268         AT END MOVE 'N' TO PS-MORE-RECORDS-SW.                            
00270 *                                                                         
00296  5000-OUTPUT-PROCESS.                                                     
00311      IF PS-FIRST-TIME                                                     
00311          SET PS-NOT-FIRST-TIME TO TRUE                                    
00312          PERFORM 6000-WRITE-HEADINGS                                      
               MOVE HOLD-AREA       TO SAVE-AREA                                
00313      END-IF                                                               
00314 *                                                                         
00319      IF VALID-CODE                                                        
00315          IF HD-TRAN-CODE NOT = SA-TRAN-CODE                               
00316              PERFORM 5200-WRITE-BATCH-LINE                                
00317              PERFORM 5300-WRITE-CODE-TOTAL                                
00318          ELSE                                                             
00319              IF HD-BATCH-NBR NOT = SA-BATCH-NBR                           
00320                  PERFORM 5200-WRITE-BATCH-LINE                            
00321              END-IF                                                       
00321          END-IF                                                           
00321      END-IF.                                                              
00321 *                                                                         
           MOVE HOLD-AREA       TO SAVE-AREA.                                   
00321 *                                                                         
00322      IF SA-TRAN-CODE = '10'                                               
00322          MOVE HOLD-AREA      TO SAVE-TRAN-10                              
00323          COMPUTE EXTRACT-RETAIL =                                         
00323                                 EXTRACT-RETAIL + SAVE-RETAIL-10           
00324          COMPUTE EXTRACT-NETCOST =                                        
00324                                 EXTRACT-NETCOST + SAVE-NETCOST-10         
00324      END-IF                                                               
00324 *                                                                         
00325      IF SA-TRAN-CODE = '30'                                               
00322          MOVE HOLD-AREA      TO SAVE-TRAN-30                              
00326          COMPUTE EXTRACT-RETAIL = EXTRACT-RETAIL +                        
00326                                  (SAVE-UNITS-30 * SAVE-RETAIL-30)         
00324      END-IF                                                               
00324 *                                                                         
00327      IF SA-TRAN-CODE = '31'                                               
00322          MOVE HOLD-AREA      TO SAVE-TRAN-31                              
00328          COMPUTE EXTRACT-RETAIL = EXTRACT-RETAIL +                        
0032                                   (SAVE-UNITS-31 * SAVE-RETAIL-31)         
00324      END-IF                                                               
00324 *                                                                         
00329      IF SA-TRAN-CODE = '40'                                               
00322          MOVE HOLD-AREA      TO SAVE-TRAN-40                              
00330          COMPUTE EXTRACT-RETAIL = EXTRACT-RETAIL +                        
00331          ((SAVE-OLD-RETAIL-40 - SAVE-NEW-RETAIL-40) * SAVE-QTY40)         
00324      END-IF                                                               
00331 *                                                                         
00332      IF SA-TRAN-CODE = '50'                                               
00322          MOVE HOLD-AREA      TO SAVE-TRAN-50                              
00333          COMPUTE EXTRACT-RETAIL = EXTRACT-RETAIL +                        
00334          ((SAVE-NEW-RETAIL-50 - SAVE-OLD-RETAIL-50) * SAVE-QTY50)         
00324      END-IF                                                               
                                                                                
00331 *                                                                         
00332      IF SA-TRAN-CODE = '80'                                               
00322          MOVE HOLD-AREA      TO SAVE-TRAN-80                              
00323          COMPUTE EXTRACT-RETAIL =                                         
00323                                 EXTRACT-RETAIL + SAVE-RETAIL-80           
00324          COMPUTE EXTRACT-NETCOST =                                        
00324                                 EXTRACT-NETCOST + SAVE-COST-80            
00324      END-IF                                                               
                                                                                
00335      ADD 1 TO RECORD-CNT.                                                 
00336      PERFORM 2000-READ-FILE.                                              
00337 *                                                                         
00338 *                                                                         
00342  5200-WRITE-BATCH-LINE.                                                   
00343      IF LINE-CNT > 58                                                     
00344          PERFORM 6000-WRITE-HEADINGS.                                     
00345      MOVE SA-TRAN-CODE TO DETAIL-CODE.                                    
00346      MOVE SA-BATCH-NBR TO DETAIL-BATCH.                                   
00347      MOVE EXTRACT-RETAIL TO DETAIL-RETAIL.                                
00348      ADD EXTRACT-RETAIL TO RETAIL-TOTAL.                                  
00349      MOVE +0 TO EXTRACT-RETAIL.                                           
00350      IF SA-TRAN-CODE = '10'                                               
00351         MOVE EXTRACT-NETCOST TO DETAIL-COST                               
00352         ADD  EXTRACT-NETCOST TO COST-TOTAL                                
00353         MOVE ZERO TO EXTRACT-NETCOST                                      
00354        ELSE                                                               
00355         MOVE ZERO TO DETAIL-COST.                                         
00356 *    MOVE DETAIL-LINE TO PRINT-REC.                                       
           SET PS-BATCH-TOTAL            TO TRUE.                               
00357      PERFORM 7000-WRITE.                                                  
00414      ADD 1 TO LINE-CNT.                                                   
00358      SKIP3                                                                
00359  5300-WRITE-CODE-TOTAL.                                                   
           SET PS-CODE-TOTAL            TO TRUE.                                
00360      MOVE 'CODE TOTAL' TO TOTAL-LITERAL.                                  
00361      MOVE RETAIL-TOTAL TO TOTAL-RETAIL.                                   
00362      MOVE COST-TOTAL TO TOTAL-COST.                                       
00363 *    MOVE TOTAL-LINE TO PRINT-REC.                                        
00364      PERFORM 7000-WRITE.                                                  
00414      ADD 2 TO LINE-CNT.                                                   
00365      ADD RETAIL-TOTAL TO GRAND-RETAIL.                                    
00366      ADD COST-TOTAL TO GRAND-COST.                                        
00367      MOVE ZERO TO RETAIL-TOTAL                                            
00368                   COST-TOTAL.                                             
00369      MOVE HD-TRAN-CODE TO SA-TRAN-CODE.                                   
00370 *    MOVE SPACE TO PRINT-REC.                                             
00371 *    MOVE '0' TO PRINT-CC.                                                
00372 *    PERFORM 7000-WRITE.                                                  
00373 *                                                                         
00374  5400-WRITE-FINAL-TOTAL.                                                  
           SET PS-FINAL-TOTAL           TO TRUE.                                
00375      MOVE 'FINAL TOTALS' TO TOTAL-LITERAL.                                
00376      MOVE GRAND-RETAIL TO TOTAL-RETAIL.                                   
00377      MOVE GRAND-COST TO TOTAL-COST.                                       
00378      MOVE TOTAL-LINE TO PRINT-REC.                                        
00379      PERFORM 7000-WRITE.                                                  
00414      ADD 2 TO LINE-CNT.                                                   
00380      SKIP1                                                                
           SET PS-RECORD-TOTAL          TO TRUE.                                
00381      MOVE RECORD-CNT TO TOTAL-RECORDS.                                    
00382      MOVE RECORD-TOTAL-LINE TO PRINT-REC.                                 
00383      PERFORM 7000-WRITE.                                                  
00414      ADD 2 TO LINE-CNT.                                                   
00384      SKIP3                                                                
00399  6000-WRITE-HEADINGS.                                                     
00400      ADD 1 TO PAGE-CNT.                                                   
00401      MOVE PAGE-CNT TO HD1-PAGE.                                           
00402      WRITE PRINT-REC FROM HEAD-1                                          
00402          AFTER ADVANCING         PAGE.                                    
00402      WRITE PRINT-REC FROM HEAD-2                                          
00402          AFTER ADVANCING         2 LINES.                                 
00402      WRITE PRINT-REC FROM HEAD-3                                          
00402          AFTER ADVANCING         2 LINES.                                 
00402      MOVE SPACES       TO PRINT-REC.                                      
00402      WRITE PRINT-REC                                                      
00402          AFTER ADVANCING         1 LINE.                                  
00410      MOVE 6 TO LINE-CNT.                                                  
00402 *        MOVE HEAD-1 TO PRINT-REC.                                        
00403 *    PERFORM 7000-WRITE.                                                  
00404 *        MOVE HEAD-2 TO PRINT-REC.                                        
00405 *    PERFORM 7000-WRITE.                                                  
00406 *        MOVE HEAD-3 TO PRINT-REC.                                        
00407 *    PERFORM 7000-WRITE.                                                  
00408 *        MOVE SPACES TO PRINT-REC.                                        
00409 *    PERFORM 7000-WRITE.                                                  
00411      SKIP3                                                                
00412  7000-WRITE.                                                              
00412       IF PS-BATCH-TOTAL                                                   
00412           WRITE PRINT-REC FROM DETAIL-LINE                                
00412       END-IF                                                              
00412       IF PS-CODE-TOTAL                                                    
00412           WRITE PRINT-REC FROM TOTAL-LINE                                 
00412           MOVE SPACES       TO TOTAL-LINE                                 
00412           WRITE PRINT-REC FROM TOTAL-LINE                                 
00412               AFTER ADVANCING  2 LINES                                    
00412       END-IF                                                              
00412       IF PS-FINAL-TOTAL                                                   
00412           WRITE PRINT-REC FROM TOTAL-LINE                                 
00412               AFTER  ADVANCING 2 LINES                                    
00412       END-IF                                                              
00412       IF PS-RECORD-TOTAL                                                  
00412           WRITE PRINT-REC FROM RECORD-TOTAL-LINE                          
00412       END-IF                                                              
00412       MOVE 'N' TO PS-PRINT-LINE-SW.                                       
00413 *    IF PRINT-CC = ' '                                                    
00419 *        WRITE PRINT-REC AFTER ADVANCING 1 LINES                          
00414 *        ADD 1 TO LINE-CNT.                                               
00415 *    IF PRINT-CC = '0'                                                    
00419 *        WRITE PRINT-REC AFTER ADVANCING 2 LINES                          
00416 *        ADD 2 TO LINE-CNT                                                
00417 *    IF PRINT-CC = '-'                                                    
00419 *        WRITE PRINT-REC AFTER ADVANCING 3 LINES                          
00418 *        ADD 3 TO LINE-CNT.                                               
00419 *    WRITE PRINT-REC AFTER POSITIONING PRINT-CC LINES.                    
00420 *    SKIP3                                                                
00214  8000-EOJ-ROUTINE.                                                        
00307      PERFORM 5200-WRITE-BATCH-LINE.                                       
00308      PERFORM 5300-WRITE-CODE-TOTAL.                                       
00309      PERFORM 5400-WRITE-FINAL-TOTAL.                                      
00212 *                                                                         
00212      CLOSE INFILE                                                         
00212            LEDGER-CONTROL-FILE                                            
00212            PRINT-FILE.                                                    
