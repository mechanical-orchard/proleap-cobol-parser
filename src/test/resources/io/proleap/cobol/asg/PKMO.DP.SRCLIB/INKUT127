      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    INKUT127.                                                 
       AUTHOR.        JULIE SEIDLER.                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  11-09-99.                                                 
                                                                                
      ******************************************************************        
      *    SPLIT VARIANCE PHYSICAL SITE LOAD FILE                               
      ******************************************************************        
      *    THIS PROGRAM WILL SPLIT THE PRODUCT PHYSICAL SITE LOAD FILE          
      *    INTO 4 SMALLER FILES FOR FTP PURPOSES.                               
      *                                                                         
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES (MADE BY)          *        
      * -------  ----------  ----------------------------------------  *        
      * XXXXXX   01-17-2000  WHEN NO DATA IS PROCESSED, THE COUNT FILE *        
      *                      SHOULD BE EMPTY.       (JULIE SEIDLER)    *        
      * XXXXXX   02-04-2000  CHANGE FILE SIZE TO PASS 17 MILLION       *        
      *                      RECORDS PER FILE SEND A MAX OF 69,900,000.*        
      *                                             (JULIE SEIDLER)    *        
      * 24521    05-09-2003  CHANGE RECORD LENGTH OF INPUT AND OUTPUT  *        
      *                      FILE AND ADD AN OPTIONAL CONTROL CARD     *        
      *                      CREATED FROM INKUT125 TO DIVIDE UP THE    *        
      *                      INPUT FILE MORE EVENLY.                   *        
      *                                             (V. LEE YANG)      *        
      * W33304   08-12-2008  HILO PROJECT                              *        
      *                      INRD010 HAS BEEN REPLACED WITH INRD138    *        
      *                      AND ALL ASSOCIATED FILES ARE NOW LRECL    *        
      *                      100.                                      *        
      *                                             (D. THEEL)         *        
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT PRDXSITE-INP01                                                
               ASSIGN TO INP01.                                                 
                                                                                
24521      SELECT OPTIONAL CONTROL-CARD-FILE                                    
24521          ASSIGN TO INP02.                                                 
24521                                                                           
           SELECT PRDXSITE-OUT01                                                
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT PRDXSITE-OUT02                                                
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT PRDXSITE-OUT03                                                
               ASSIGN TO OUT03.                                                 
                                                                                
           SELECT PRDXSITE-OUT04                                                
               ASSIGN TO OUT04.                                                 
                                                                                
           SELECT PRDXSITE-TTLRECS                                              
               ASSIGN TO OUT05.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PRDXSITE-INP01                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
W33304     RECORD CONTAINS 100 CHARACTERS                                       
           DATA RECORD IS PRDXSITE-INP01-RECORD.                                
                                                                                
W33304 01  PRDXSITE-INP01-RECORD       PIC X(100).                              
                                                                                
24521  FD  CONTROL-CARD-FILE                                                    
24521      RECORDING MODE IS F                                                  
24521      LABEL RECORDS ARE STANDARD                                           
24521      BLOCK CONTAINS 0 RECORDS                                             
24521      RECORD CONTAINS 080 CHARACTERS                                       
24521      DATA RECORD IS CONTROL-CARD-RECORD.                                  
24521                                                                           
24521  01  CONTROL-CARD-RECORD         PIC X(080).                              
24521                                                                           
       FD  PRDXSITE-OUT01                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
W33304     RECORD CONTAINS 100 CHARACTERS                                       
           DATA RECORD IS PRDXSITE-OUT01-RECORD.                                
                                                                                
W33304 01  PRDXSITE-OUT01-RECORD       PIC X(100).                              
                                                                                
       FD  PRDXSITE-OUT02                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
W33304     RECORD CONTAINS 100 CHARACTERS                                       
           DATA RECORD IS PRDXSITE-OUT02-RECORD.                                
                                                                                
W33304 01  PRDXSITE-OUT02-RECORD       PIC X(100).                              
                                                                                
       FD  PRDXSITE-OUT03                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
W33304     RECORD CONTAINS 100 CHARACTERS                                       
           DATA RECORD IS PRDXSITE-OUT03-RECORD.                                
                                                                                
W33304 01  PRDXSITE-OUT03-RECORD       PIC X(100).                              
                                                                                
       FD  PRDXSITE-OUT04                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
W33304     RECORD CONTAINS 100 CHARACTERS                                       
           DATA RECORD IS PRDXSITE-OUT04-RECORD.                                
                                                                                
W33304 01  PRDXSITE-OUT04-RECORD       PIC X(100).                              
                                                                                
       FD  PRDXSITE-TTLRECS                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS PRDXSITE-TTLRECS-RECORD.                              
                                                                                
       01  PRDXSITE-TTLRECS-RECORD     PIC X(80).                               
                                                                                
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  ABEND-CODE                       PIC S9(04) COMP                     
                                                       VALUE ZERO.              
                                                                                
       01 PC-PROGRAM-CONSTANTS.                                                 
           05  PC-PROGRAM-NAME                PIC X(8) VALUE 'INKUT127'.        
                                                                                
       01 PS-PROGRAM-SWITCHES.                                                  
24521      05 PS-END-OF-PRDXSITE-FILE      PIC X(01)       VALUE 'N'.           
24521         88 END-OF-PRDXSITE-FILE                      VALUE 'Y'.           
24521      05 PS-END-OF-CONTROL-CARD-FILE  PIC X(01)       VALUE 'N'.           
24521         88 END-OF-CONTROL-CARD-FILE                  VALUE 'Y'.           
                                                                                
      ********************************************************                  
      *    COUNTER AREA                                      *                  
      ********************************************************                  
24521  01  WS-COUNTER-AREA.                                                     
24521      05  WS-COUNT-PER-FILE           PIC S9(09)      VALUE ZEROES         
24521                          COMP.                                            
24521      05  WS-TEMP-CALC                PIC S9(09)      VALUE ZEROES         
24521                          COMP.                                            
24521      05  WS-MAX-OUT01-COUNT          PIC S9(9)    VALUE +17000000         
24521                          COMP.                                            
24521      05  WS-MAX-OUT02-COUNT          PIC S9(9)    VALUE +34000000         
24521                          COMP.                                            
24521      05  WS-MAX-OUT03-COUNT          PIC S9(9)    VALUE +51000000         
24521                          COMP.                                            
24521      05  WS-MAX-OUT04-COUNT          PIC S9(9)    VALUE +75000000         
24521                          COMP.                                            
24521      05  WS-CURR-OUTPUT-COUNT        PIC S9(9)       VALUE ZEROES         
24521                          COMP.                                            
24521      05  WS-IN-COUNTER1              PIC 9(9)        VALUE ZEROES.        
24521      05  WS-OUT-COUNTER1             PIC 9(9)        VALUE ZEROES.        
24521      05  WS-OUT-COUNTER2             PIC 9(9)        VALUE ZEROES.        
24521      05  WS-OUT-COUNTER3             PIC 9(9)        VALUE ZEROES.        
24521      05  WS-OUT-COUNTER4             PIC 9(9)        VALUE ZEROES.        
      *                                                                         
      ********************************************************                  
      *    RECORD AREA                                       *                  
      ********************************************************                  
W33304*01  IN138-VARIANCE-SUMM-RECORD                                           
W33304     COPY INRD138.                                                        
                                                                                
24521 ******************************************************************        
24521 *  CONTROL CARD RECORD                                           *        
24521 ******************************************************************        
24521  01  RQ-REQUEST-CONTROL-CARD.                                             
24521      05  FILLER                      PIC X(01)       VALUE SPACES.        
24521          88  RQ-COMMENT-RECORD                       VALUE '*'.           
24521      05  RQ-TOTAL-RECORD-COUNT       PIC X(11)       VALUE ZEROES.        
24521      05  RQ-TOTAL-RECORD-COUNT-9 REDEFINES RQ-TOTAL-RECORD-COUNT          
24521                                      PIC 9(11).                           
24521      05  FILLER                      PIC X(68)       VALUE SPACES.        
                                                                                
       01  WS-TTLRECS-RECORD.                                                   
           05 WS-TTLRECS-FILL1         PIC X(41) VALUE                          
                 'INRD138 NUMBER OF RECORDS PROCESSED WAS '.                    
           05 WS-TTLRECS-COUNT         PIC 9(9)  VALUE ZERO.                    
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE.                                                           
                                                                                
           OPEN INPUT  PRDXSITE-INP01                                           
24521                  CONTROL-CARD-FILE                                        
                OUTPUT PRDXSITE-OUT01                                           
                       PRDXSITE-OUT02                                           
                       PRDXSITE-OUT03                                           
                       PRDXSITE-OUT04                                           
                       PRDXSITE-TTLRECS.                                        
                                                                                
24521      READ CONTROL-CARD-FILE INTO RQ-REQUEST-CONTROL-CARD                  
24521          AT END                                                           
24521              SET END-OF-CONTROL-CARD-FILE TO TRUE.                        
24521                                                                           
24521      DISPLAY '  ----+----1----+----2----+----3----+----4'                 
24521              '----+----5----+----6----+----7'                             
24521      DISPLAY 'REQUESTED CC:'                                              
24521                                                                           
24521      PERFORM 2000-PROCESS-CONTROL                                         
24521          UNTIL END-OF-CONTROL-CARD-FILE.                                  
24521                                                                           
           PERFORM 9000-READ-PRDXSITE-INP01-FILE.                               
           PERFORM 1000-MAIN-PROCESSING                                         
               UNTIL END-OF-PRDXSITE-FILE.                                      
           PERFORM 9900-WRAP-UP.                                                
                                                                                
           CLOSE PRDXSITE-INP01                                                 
24521            CONTROL-CARD-FILE                                              
                 PRDXSITE-OUT01                                                 
                 PRDXSITE-OUT02                                                 
                 PRDXSITE-OUT03                                                 
                 PRDXSITE-OUT04                                                 
                 PRDXSITE-TTLRECS.                                              
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *   PROCESS THE INPUT FILE RECORDS                                        
      ******************************************************************        
       1000-MAIN-PROCESSING.                                                    
                                                                                
           IF WS-CURR-OUTPUT-COUNT < WS-MAX-OUT01-COUNT                         
               WRITE PRDXSITE-OUT01-RECORD                                      
W33304               FROM IN138-VARIANCE-SUMM-RECORD                            
               ADD 1 TO WS-OUT-COUNTER1                                         
           ELSE                                                                 
                                                                                
               IF WS-CURR-OUTPUT-COUNT < WS-MAX-OUT02-COUNT                     
                   WRITE PRDXSITE-OUT02-RECORD                                  
W33304                 FROM IN138-VARIANCE-SUMM-RECORD                          
                   ADD 1 TO WS-OUT-COUNTER2                                     
               ELSE                                                             
                                                                                
                   IF WS-CURR-OUTPUT-COUNT < WS-MAX-OUT03-COUNT                 
                       WRITE PRDXSITE-OUT03-RECORD                              
W33304                     FROM IN138-VARIANCE-SUMM-RECORD                      
                       ADD 1 TO WS-OUT-COUNTER3                                 
                   ELSE                                                         
                       WRITE PRDXSITE-OUT04-RECORD                              
W33304                     FROM IN138-VARIANCE-SUMM-RECORD                      
                       ADD 1 TO WS-OUT-COUNTER4                                 
                       IF WS-IN-COUNTER1 > WS-MAX-OUT04-COUNT                   
                           DISPLAY '*------------------------------'            
                           DISPLAY 'INPUT FILE CONTAINS MORE THAN '             
                                   WS-MAX-OUT04-COUNT   ' RECORDS'              
                           DISPLAY '*------------------------------'            
                           MOVE +4003 TO ABEND-CODE                             
                           PERFORM 9999-ABEND                                   
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-CURR-OUTPUT-COUNT.                                       
                                                                                
           PERFORM 9000-READ-PRDXSITE-INP01-FILE.                               
                                                                                
                                                                                
      ******************************************************************        
      *   PROCESS THE CONTROL CARD FILE                                         
      ******************************************************************        
       2000-PROCESS-CONTROL.                                                    
                                                                                
24521      DISPLAY '  ' RQ-REQUEST-CONTROL-CARD.                                
24521      IF RQ-COMMENT-RECORD                                                 
24521          CONTINUE                                                         
24521      ELSE                                                                 
24521          IF RQ-TOTAL-RECORD-COUNT IS NUMERIC                              
24521              AND RQ-TOTAL-RECORD-COUNT-9 GREATER ZEROES                   
24521              DISPLAY '    *** FILE SPLIT CHANGED AS FOLLOW: '             
24521              COMPUTE WS-COUNT-PER-FILE =                                  
24521                           RQ-TOTAL-RECORD-COUNT-9 / 4                     
24521              IF WS-COUNT-PER-FILE = ZEROES                                
24521                  MOVE 1   TO WS-COUNT-PER-FILE                            
24521              END-IF                                                       
24521                                                                           
24521              COMPUTE WS-TEMP-CALC ROUNDED =                               
24521                      WS-COUNT-PER-FILE * 1                                
24521              DISPLAY '        FILE 1 THRESHOLD FROM '                     
24521                      WS-MAX-OUT01-COUNT ' TO '                            
24521                      WS-TEMP-CALC                                         
24521              MOVE WS-TEMP-CALC TO WS-MAX-OUT01-COUNT                      
24521                                                                           
24521              COMPUTE WS-TEMP-CALC ROUNDED =                               
24521                      WS-COUNT-PER-FILE * 2                                
24521              DISPLAY '        FILE 2 THRESHOLD FROM '                     
24521                      WS-MAX-OUT02-COUNT ' TO '                            
24521                      WS-TEMP-CALC                                         
24521              MOVE WS-TEMP-CALC TO WS-MAX-OUT02-COUNT                      
24521                                                                           
24521              COMPUTE WS-TEMP-CALC ROUNDED =                               
24521                      WS-COUNT-PER-FILE * 3                                
24521              DISPLAY '        FILE 3 THRESHOLD FROM '                     
24521                      WS-MAX-OUT03-COUNT ' TO '                            
24521                      WS-TEMP-CALC                                         
24521              MOVE WS-TEMP-CALC TO WS-MAX-OUT03-COUNT                      
24521                                                                           
24521              DISPLAY '        FILE 4 THRESHOLD FROM '                     
24521                      WS-MAX-OUT04-COUNT ' TO '                            
24521                      RQ-TOTAL-RECORD-COUNT-9                              
24521              MOVE RQ-TOTAL-RECORD-COUNT-9 TO WS-MAX-OUT04-COUNT           
24521          END-IF                                                           
24521      END-IF.                                                              
24521                                                                           
24521      READ CONTROL-CARD-FILE INTO RQ-REQUEST-CONTROL-CARD                  
24521          AT END                                                           
24521              SET END-OF-CONTROL-CARD-FILE TO TRUE.                        
                                                                                
      ************************************************************              
      *  READ THE SALES CORRECTIONS REPORT FILE                                 
      ************************************************************              
       9000-READ-PRDXSITE-INP01-FILE.                                           
                                                                                
W33304     READ PRDXSITE-INP01 INTO IN138-VARIANCE-SUMM-RECORD                  
               AT END                                                           
                   SET END-OF-PRDXSITE-FILE TO TRUE                             
               NOT AT END                                                       
                   ADD 1          TO   WS-IN-COUNTER1.                          
                                                                                
      ************************************************************              
      *  DISPLAY RECORD COUNTS AT END OF JOB                                    
      ************************************************************              
       9900-WRAP-UP.                                                            
      *                                                                         
JS0100     IF WS-IN-COUNTER1 NOT = ZERO                                         
               MOVE WS-IN-COUNTER1    TO WS-TTLRECS-COUNT                       
               WRITE PRDXSITE-TTLRECS-RECORD                                    
                     FROM WS-TTLRECS-RECORD                                     
           END-IF.                                                              
      *                                                                         
           DISPLAY '***** PROGRAM: ' PC-PROGRAM-NAME.                           
      *                                                                         
           DISPLAY '***** CONTROL REPORT / RECORD COUNT ****'.                  
      *                                                                         
           DISPLAY 'TOTAL INP01   RECORDS : ' WS-IN-COUNTER1.                   
      *                                                                         
           DISPLAY 'TOTAL OUT01   RECORDS : ' WS-OUT-COUNTER1.                  
      *                                                                         
           DISPLAY 'TOTAL OUT02   RECORDS : ' WS-OUT-COUNTER2.                  
      *                                                                         
           DISPLAY 'TOTAL OUT03   RECORDS : ' WS-OUT-COUNTER3.                  
      *                                                                         
           DISPLAY 'TOTAL OUT04   RECORDS : ' WS-OUT-COUNTER4.                  
      *                                                                         
      *                                                                         
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = INKUT127'                               
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
                                                                                
