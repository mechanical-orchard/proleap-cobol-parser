       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.         ETKUP046.                                            
       AUTHOR.             TIA JOHNSTON.                                        
       DATE-WRITTEN.       02/29/00.                                            
       DATE-COMPILED.                                                           
                                                                                
************************************************************************        
******* THIS PROGRAM WILL USE THE EXTRACT FILES CREATED FOR     ********        
******* ALL INBOUND DOCUMENTS WITH STATUS UPDATES AND UPDATE    ********        
******* THE TRANSLATION ERROR INDICATOR ON TFGTRST AND INSERT   ********        
******* THE STATUS RECORD ON TTNSTLC.                           ********        
************************************************************************        
0201JF* 02-09-2001 JON FIEDLER  0201JF                                          
0201JF* ADDED VENDOR NAME TO SYSOUT ERROR MESSAGE.                              
0201JF******************************************************************        
*201JF* 02-19-2001 JON FIEDLER *201JF                                           
*201JF* ADDED TRN_DTL_TEXT TO SYSOUT ERROR MESSAGE.                             
*201JF******************************************************************        
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS INPUT-STATUS-RECORD.                                  
                                                                                
           COPY ETRD024.                                                        
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  END-OF-FILE-SW            PIC X(1)  VALUE 'N'.                   
               88  END-OF-FILE                     VALUE 'Y'.                   
           05  ERRORS-FOUND-SW           PIC X(1)  VALUE 'N'.                   
               88  ERRORS-FOUND                    VALUE 'Y'.                   
           05  ERROR-THIS-REC-SW         PIC X(1)  VALUE 'N'.                   
               88  NO-ERROR-THIS-REC               VALUE 'N'.                   
               88  ERROR-THIS-REC                  VALUE 'Y'.                   
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TFGTRST                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTNSTLC                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TENTNME                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
      ******************************************************************        
      * OPEN THE INPUT FILE                                            *        
      * READ INPUT FILE                                                *        
      *     GET TFGTRST KEY                                            *        
      *     UPDATE TFGTRST RECORD                                      *        
      *     INSERT TTNSTLC RECORD                                      *        
      *     READ INPUT FILE                                            *        
      * CLOSE THE INPUT FILE                                           *        
      ******************************************************************        
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT INPUT-FILE.                                               
                                                                                
           PERFORM 1100-READ-INPUT-FILE                                         
              THRU 1100-READ-INPUT-FILE-EXIT.                                   
                                                                                
           PERFORM 1000-PROCESS-INPUT-FILE                                      
              THRU 1000-PROCESS-INPUT-FILE-EXIT                                 
                 UNTIL END-OF-FILE.                                             
                                                                                
           IF ERRORS-FOUND                                                      
              MOVE 4095 TO RETURN-CODE.                                         
                                                                                
           CLOSE INPUT-FILE.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-PROCESS-INPUT-FILE.                                                 
                                                                                
           PERFORM 1010-GET-TFGTRST-KEY                                         
              THRU 1010-GET-TFGTRST-KEY-EXIT.                                   
                                                                                
           PERFORM 1020-INSERT-TTNSTLC-REC                                      
              THRU 1020-INSERT-TTNSTLC-REC-EXIT.                                
                                                                                
           PERFORM 1030-UPDATE-TFGTRST-REC                                      
              THRU 1030-UPDATE-TFGTRST-REC-EXIT.                                
                                                                                
           SET NO-ERROR-THIS-REC TO TRUE.                                       
           PERFORM 1100-READ-INPUT-FILE                                         
              THRU 1100-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-PROCESS-INPUT-FILE-EXIT.                                            
           EXIT.                                                                
                                                                                
                                                                                
       1010-GET-TFGTRST-KEY.                                                    
                                                                                
           MOVE ET024-TP-DKEY           TO FGTRST-TP-DKEY.                      
           MOVE ET024-TP-DKEY-TMST      TO FGTRST-TP-DKEY-TMST.                 
           MOVE ET024-FUNC-CDE-CNTL-NBR TO FGTRST-FUNC-CDE-CNTL-NBR.            
           MOVE ET024-TRN-SET-CNTL-NBR  TO FGTRST-TRN-SET-CNTL-NBR.             
           MOVE ET024-TRN-DTL-TXT       TO FGTRST-TRN-DTL-TXT.                  
                                                                                
           EXEC SQL                                                             
              SELECT TP_DKEY,                                                   
                     TP_DKEY_TMST,                                              
                     FUNC_CDE_SEQ_NBR,                                          
                     TRN_SET_SEQ_NBR                                            
                INTO :FGTRST-TP-DKEY,                                           
                     :FGTRST-TP-DKEY-TMST,                                      
                     :FGTRST-FUNC-CDE-SEQ-NBR,                                  
                     :FGTRST-TRN-SET-SEQ-NBR                                    
                FROM TFGTRST                                                    
               WHERE TP_DKEY = :FGTRST-TP-DKEY                                  
                 AND TP_DKEY_TMST = :FGTRST-TP-DKEY-TMST                        
                 AND FUNC_CDE_CNTL_NBR = :FGTRST-FUNC-CDE-CNTL-NBR              
                 AND TRN_SET_CNTL_NBR = :FGTRST-TRN-SET-CNTL-NBR                
                 AND TRN_DTL_TXT = :FGTRST-TRN-DTL-TXT                          
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE SQLCODE TO SQLCODE2                                          
0201JF        PERFORM 1040-GET-VENDOR-NAME                                      
              DISPLAY SPACES                                                    
              DISPLAY 'ERROR RETREIVING TFGTRST KEY'                            
0201JF        DISPLAY 'VENDOR NAME = ' ENTNME-ENT-NAME-DESC                     
              DISPLAY 'TP-DKEY = ' ET024-TP-DKEY                                
              DISPLAY 'TP-DKEY-TMST = ' ET024-TP-DKEY-TMST                      
              DISPLAY 'FUNC-CDE-CNTL-NBR = ' ET024-FUNC-CDE-CNTL-NBR            
              DISPLAY 'TRN-SET-CNTL-NBR = ' ET024-TRN-SET-CNTL-NBR              
              DISPLAY 'TRN-SET-CDE = ' ET024-TRN-SET-CDE                        
*201JF        DISPLAY 'TRN-DTL-TXT = ' ET024-TRN-DTL-TXT                        
              DISPLAY 'STATUS CODE = ' ET024-STATUS-CDE                         
              DISPLAY 'ERROR CODE = ' SQLCODE2                                  
              SET ERRORS-FOUND TO TRUE                                          
              SET ERROR-THIS-REC TO TRUE.                                       
                                                                                
       1010-GET-TFGTRST-KEY-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       1020-INSERT-TTNSTLC-REC.                                                 
                                                                                
           IF NO-ERROR-THIS-REC                                                 
              MOVE FGTRST-TP-DKEY           TO TNSTLC-TP-DKEY                   
              MOVE FGTRST-TP-DKEY-TMST      TO TNSTLC-TP-DKEY-TMST              
              MOVE FGTRST-FUNC-CDE-SEQ-NBR  TO TNSTLC-FUNC-CDE-SEQ-NBR          
              MOVE FGTRST-TRN-SET-SEQ-NBR   TO TNSTLC-TRN-SET-SEQ-NBR           
              MOVE ET024-AUD-TMST           TO TNSTLC-TRN-AUD-TMST              
              MOVE ET024-STATUS-CDE         TO TNSTLC-TRN-STAT-CDE              
                                                                                
              EXEC SQL                                                          
                 INSERT INTO TTNSTLC                                            
                       (TP_DKEY,                                                
                        TP_DKEY_TMST,                                           
                        FUNC_CDE_SEQ_NBR,                                       
                        TRN_SET_SEQ_NBR,                                        
                        TRN_AUD_TMST,                                           
                        TRN_STAT_CDE)                                           
                 VALUES                                                         
                       (:TNSTLC-TP-DKEY,                                        
                        :TNSTLC-TP-DKEY-TMST,                                   
                        :TNSTLC-FUNC-CDE-SEQ-NBR,                               
                        :TNSTLC-TRN-SET-SEQ-NBR,                                
                        :TNSTLC-TRN-AUD-TMST,                                   
                        :TNSTLC-TRN-STAT-CDE)                                   
              END-EXEC                                                          
                                                                                
              IF SQLCODE NOT = +000                                             
                 MOVE SQLCODE TO SQLCODE2                                       
0201JF           PERFORM 1040-GET-VENDOR-NAME                                   
                 DISPLAY SPACES                                                 
                 DISPLAY 'ERROR INSERTING TO TTNSTLC TABLE'                     
0201JF           DISPLAY 'VENDOR NAME = ' ENTNME-ENT-NAME-DESC                  
                 DISPLAY 'TP-DKEY = ' ET024-TP-DKEY                             
                 DISPLAY 'TP-DKEY-TMST = ' ET024-TP-DKEY-TMST                   
                 DISPLAY 'FUNC-CDE-SEQ-NBR = ' FGTRST-FUNC-CDE-SEQ-NBR          
                 DISPLAY 'TRN-SET-SEQ-NBR = ' FGTRST-TRN-SET-SEQ-NBR            
                 DISPLAY 'TRN-SET-CDE = ' ET024-TRN-SET-CDE                     
*201JF           DISPLAY 'TRN-DTL-TXT = ' ET024-TRN-DTL-TXT                     
                 DISPLAY 'TRN-AUD-TMST = ' ET024-AUD-TMST                       
                 DISPLAY 'STATUS CODE = ' ET024-STATUS-CDE                      
                 DISPLAY 'ERROR CODE = ' SQLCODE2                               
                 SET ERRORS-FOUND TO TRUE                                       
                 SET ERROR-THIS-REC TO TRUE                                     
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
       1020-INSERT-TTNSTLC-REC-EXIT.                                            
           EXIT.                                                                
                                                                                
                                                                                
       1030-UPDATE-TFGTRST-REC.                                                 
                                                                                
           IF NO-ERROR-THIS-REC                                                 
              MOVE 'Y' TO FGTRST-TRANSL-ERR-IND                                 
                                                                                
              EXEC SQL                                                          
                 UPDATE TFGTRST                                                 
                    SET TRANSL_ERR_IND = :FGTRST-TRANSL-ERR-IND                 
                  WHERE TP_DKEY = :FGTRST-TP-DKEY                               
                    AND TP_DKEY_TMST = :FGTRST-TP-DKEY-TMST                     
                    AND FUNC_CDE_SEQ_NBR = :FGTRST-FUNC-CDE-SEQ-NBR             
                    AND TRN_SET_SEQ_NBR = :FGTRST-TRN-SET-SEQ-NBR               
              END-EXEC                                                          
                                                                                
              IF SQLCODE NOT = +000                                             
                 MOVE SQLCODE TO SQLCODE2                                       
0201JF           PERFORM 1040-GET-VENDOR-NAME                                   
                 DISPLAY SPACES                                                 
                 DISPLAY 'ERROR UPDATING TFGTRST TABLE'                         
0201JF           DISPLAY 'VENDOR NAME = ' ENTNME-ENT-NAME-DESC                  
                 DISPLAY 'TP-DKEY = ' ET024-TP-DKEY                             
                 DISPLAY 'TP-DKEY-TMST = ' ET024-TP-DKEY-TMST                   
                 DISPLAY 'FUNC-CDE-SEQ-NBR = ' TNSTLC-FUNC-CDE-SEQ-NBR          
                 DISPLAY 'TRN-SET-SEQ-NBR = ' TNSTLC-TRN-SET-SEQ-NBR            
                 DISPLAY 'TRN-SET-CDE = ' ET024-TRN-SET-CDE                     
*201JF           DISPLAY 'TRN-DTL-TXT = ' ET024-TRN-DTL-TXT                     
                 DISPLAY 'STATUS CODE = ' ET024-STATUS-CDE                      
                 DISPLAY 'ERROR CODE = ' SQLCODE2                               
                 SET ERRORS-FOUND TO TRUE                                       
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
       1030-UPDATE-TFGTRST-REC-EXIT.                                            
           EXIT.                                                                
0201JF                                                                          
0201JF 1040-GET-VENDOR-NAME.                                                    
0201JF                                                                          
0201JF     EXEC SQL                                                             
0201JF        SELECT ENT_NAME_DESC                                              
0201JF          INTO :ENTNME-ENT-NAME-DESC                                      
0201JF          FROM TENTNME E                                                  
0201JF              ,TTRDPRT T                                                  
0201JF         WHERE TP_DKEY = :FGTRST-TP-DKEY                                  
0201JF           AND T.ENT_ID = E.ENT_ID                                        
0201JF           AND TYPE_CDE = '01'                                            
0201JF     END-EXEC.                                                            
0201JF                                                                          
0201JF     IF SQLCODE NOT = +000                                                
0201JF        MOVE 'NOT FOUND' TO ENTNME-ENT-NAME-DESC                          
0201JF     END-IF.                                                              
0201JF                                                                          
       1100-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE                                                      
              AT END                                                            
                 SET END-OF-FILE TO TRUE.                                       
                                                                                
       1100-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
