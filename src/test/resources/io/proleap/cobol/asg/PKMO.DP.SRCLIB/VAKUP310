       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    VAKUP310.                                         00020001
       AUTHOR.        JIMMY PHILIP - INFOSYS.                           00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  06/17/09.                                         00050000
       DATE-COMPILED.                                                   00060000
                                                                        00070000
      ******************************************************************00080000
      ******************************************************************00230000
                                                                        00240000
      ******************** HISTORY OF CHANGES **************************00241000
      * CHG ID/                                                        *00242000
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *00243000
      * -------  ----------  ----------------------------------------  *00244000
      * W33304   06-17-2009  NEW PGM FOR HILO - DELETE ALREADY CUTOVER *00245004
      *                      STORE RECORDS FROM SHRINK TABLE (TINSHNK)* 00246004
      ******************************************************************00247000
      ******************************************************************00250000
       ENVIRONMENT DIVISION.                                            00260000
      ******************************************************************00270000
                                                                        00280000
       CONFIGURATION SECTION.                                           00290000
                                                                        00300000
       SOURCE-COMPUTER.        IBM-3090.                                00310000
       OBJECT-COMPUTER.        IMB-3090.                                00320000
                                                                        00330000
       INPUT-OUTPUT SECTION.                                            00340000
                                                                        00350000
       FILE-CONTROL.                                                    00360000
                                                                        00361000
           SELECT CNTL-STR-DATE-FILE                                    00370000
               ASSIGN TO INP01.                                         00380000
                                                                        00380100
           SELECT AUDIT-FILE                                            00381000
               ASSIGN TO OUT01.                                         00382000
                                                                        00382100
      ******************************************************************00383000
       DATA DIVISION.                                                   00384000
      ******************************************************************00385000
                                                                        00386000
       FILE SECTION.                                                    00387000
                                                                        00388000
       FD  CNTL-STR-DATE-FILE                                           00389000
           RECORDING  MODE IS F.                                        00390000
       01  CNTL-DATE-REC                    PIC X(22).                  00400000
                                                                        00401000
       FD  AUDIT-FILE                                                   00420000
           RECORDING  MODE IS F.                                        00430000
       01  AUDIT-REC                        PIC X(51).                  00440000
                                                                        00450000
       WORKING-STORAGE SECTION.                                         00470000
                                                                        00530000
       01  PS-PROGRAM-SWITCHES.                                         00540000
           05  PS-SHRINK-CSR-AT-END-SW       PIC X     VALUE 'N'.       00580000
               88  PS-SHRINK-CSR-AT-END-NO             VALUE 'N'.       00590000
               88  PS-SHRINK-CSR-AT-END-YES            VALUE 'Y'.       00600000
           05  PS-CONTROL-FILE-AT-END-SW     PIC X     VALUE 'N'.       00610000
               88  PS-CONTROL-FILE-AT-END-NO           VALUE 'N'.       00620000
               88  PS-CONTROL-FILE-AT-END-YES          VALUE 'Y'.       00630000
                                                                        00660000
       01  PV-PROGRAM-VARIABLES.                                        00661000
           05 PV-PARAGRAPH-NAME            PIC X(30)  VALUE SPACES.     00662000
                                                                        00663000
       01  PC-PROGRAM-CONSTANTS.                                        00670000
           05 PC-PGM-NAME                  PIC X(8)  VALUE 'VAKUP310'.  00680001
           05 PC-4005                      PIC S9(04) VALUE +4005 COMP. 00690000
                                                                        00702000
       01  PA-PROGRAM-ACCUMULATORS.                                     00720000
           05  PA-DELETE-COUNT             PIC  9(11) VALUE ZEROS.      00750000
           05  PA-AUDIT-COUNT              PIC  9(11) VALUE ZEROS.      00760000
                                                                        00800000
       01  ABEND-CODE                        PIC S9(4) VALUE ZEROS      00801000
                                                       COMP SYNC.       00802000
                                                                        00803000
       01  ABEND-CODE-SQL              PIC S9(04)  VALUE ZERO COMP SYNC.00804000
                                                                        00805000
      ***************************************************************** 00810000
      *  INPUT FILE LAYOUT                                              00820000
      ***************************************************************** 00830000
       01  CC-CONTROL-REC.                                              00840000
           05  CC-STR-NBR                      PIC S9(4) COMP.          00850000
           05  CC-FSCL-MN-END-DTE              PIC X(10).               00860000
           05  CC-ML-PSTG-MN-END-DTE           PIC X(10).               00861000
                                                                        00950000
      ***************************************************************** 00952000
      *  AUDIT FILE OF ACTION TAKEN - SHRINK TABLE LAYOUT               00953000
      ***************************************************************** 00954000
       01  PV-AUDIT-REC.                                                00960000
           05  PV-AUDIT-FSCL-WK-END-DTE      PIC   X(10).               00970000
           05  PV-AUDIT-ML-PSTG-MN-END-DTE   PIC   X(10).               00980000
           05  PV-AUDIT-DEPT-NBR             PIC   X(3).                00990000
           05  PV-AUDIT-SUB-CL-NBR           PIC   X(2).                01000000
           05  PV-AUDIT-STR-NBR              PIC  S9(4) COMP.           01010000
           05  PV-AUDIT-FSCL-MN-END-DTE      PIC   X(10).               01020000
           05  PV-AUDIT-SLS-RTL-AMT          PIC  S9(11)V9(2) COMP-3.   01030000
           05  PV-AUDIT-SHNK-RTL-AMT         PIC  S9(11)V9(2) COMP-3.   01040000
                                                                        01060000
      ******************************************************************01070000
      *  DB2 SQL ERROR MESSAGE FORMAT AREA                              01080000
      ******************************************************************01090000
           COPY DPWS004.                                                01100000
                                                                        01110000
      ******************************************************************01120000
      * SHRINK TABLE TINSHNK                                            01130001
      ******************************************************************01140000
           EXEC SQL                                                     01150000
             INCLUDE TINSHNK                                            01160001
           END-EXEC.                                                    01170000
      ***************************************************************** 01420000
      *    DB2 AREA FOR COMMUNICATIONS                                * 01430000
      ***************************************************************** 01440000
           EXEC SQL                                                     01450000
                INCLUDE SQLCA                                           01460000
           END-EXEC.                                                    01470000
                                                                        01480000
                                                                        01580000
           EXEC SQL                                                     01590000
              DECLARE SHRINK_CSR CURSOR FOR                             01600000
                SELECT                                                  01610000
                       FSCL_WK_END_DTE                                  01620000
                     , ML_PSTG_MN_END_DTE                               01630000
                     , DEPT_NBR                                         01640000
                     , SUB_CL_NBR                                       01650000
                     , STR_NBR                                          01660000
                     , FSCL_MN_END_DTE                                  01670000
                     , SLS_RTL_AMT                                      01680000
                     , SHNK_RTL_AMT                                     01690000
                    FROM TINSHNK                                        02160001
                WHERE STR_NBR           = :CC-STR-NBR                   02170000
                  AND FSCL_MN_END_DTE   = :CC-FSCL-MN-END-DTE           02180000
                  AND ML_PSTG_MN_END_DTE= :CC-ML-PSTG-MN-END-DTE        02181000
           END-EXEC.                                                    02234000
      ******************************************************************02235000
       PROCEDURE DIVISION.                                              02236000
      ******************************************************************02237000
                                                                        02238000
       0000-MAINLINE.                                                   02239000
                                                                        02240000
           PERFORM 1000-INITIALIZE.                                     02250000
                                                                        02260000
           PERFORM 2000-PROCESS-RECS                                    02261000
             UNTIL PS-CONTROL-FILE-AT-END-YES.                          02262000
                                                                        02262400
           DISPLAY 'PA-AUDIT-COUNT     ' PA-AUDIT-COUNT.                02262500
           DISPLAY 'PA-DELETE-COUNT    ' PA-DELETE-COUNT.               02262900
                                                                        02263000
           IF PA-AUDIT-COUNT EQUAL PA-DELETE-COUNT                      02263100
              IF PA-AUDIT-COUNT EQUAL ZERO                              02263200
                 DISPLAY 'ZERO RECORDS PROCESSED/ DELETED'              02263300
              ELSE                                                      02263400
                 DISPLAY 'RUN STATS BALANCE'                            02263500
                 DISPLAY 'ALL DELETED RECS ARE IN AUDIT FILE'           02263600
              END-IF                                                    02263700
           ELSE                                                         02263800
              DISPLAY 'PROGRAM STATS OUT OF BALANCE'                    02263900
              DISPLAY '** ERROR ** ERROR ** ERROR **'                   02264000
           END-IF.                                                      02264100
                                                                        02264200
           CLOSE CNTL-STR-DATE-FILE                                     02264300
                 AUDIT-FILE.                                            02264400
                                                                        02264500
           STOP RUN.                                                    02264600
                                                                        02265000
                                                                        02430000
      ******************************************************************02440000
      *  PERFORM THE NECESSARY INITIALIZATION STEPS                     02450000
      ******************************************************************02460000
       1000-INITIALIZE.                                                 02470000
                                                                        02480000
           OPEN INPUT  CNTL-STR-DATE-FILE                               02490000
                OUTPUT AUDIT-FILE.                                      02500000
                                                                        02510000
           INITIALIZE PV-AUDIT-REC.                                     02520000
                                                                        02530000
           PERFORM 1100-READ-CNTL-STR-DATE-FILE.                        02540000
                                                                        02550000
                                                                        02560000
      ******************************************************************02570000
      * 1100-READ-CNTL-STR-DATE-FILE.                                   02570100
      ******************************************************************02570200
                                                                        02570300
       1100-READ-CNTL-STR-DATE-FILE.                                    02570400
           READ CNTL-STR-DATE-FILE INTO CC-CONTROL-REC                  02570500
               AT END                                                   02570600
                   DISPLAY '** PROGRAM ABEND **'                        02570700
                   DISPLAY 'VAKUP310-CNTL-STR-DATE-FILE EMPTY'          02570801
                   MOVE PC-4005 TO ABEND-CODE                           02570900
                   CALL 'ILBOABN0' USING ABEND-CODE.                    02571000
                                                                        02571100
      ******************************************************************02571200
      * 2000-PROCESS-RECS.                                              02571300
      ******************************************************************02571400
                                                                        02571500
       2000-PROCESS-RECS.                                               02571600
                                                                        02571700
           PERFORM 2100-OPEN-SHRINK-CURSOR.                             02571800
                                                                        02572000
           PERFORM 2200-FETCH-SHRINK-CURSOR                             02573000
             UNTIL PS-SHRINK-CSR-AT-END-YES.                            02574000
                                                                        02575000
           PERFORM 2300-CLOSE-SHRINK-CURSOR.                            02576000
                                                                        02577000
           READ CNTL-STR-DATE-FILE INTO CC-CONTROL-REC                  02578000
               AT END SET PS-CONTROL-FILE-AT-END-YES TO TRUE.           02579000
                                                                        02580000
       2100-OPEN-SHRINK-CURSOR.                                         03290000
                                                                        03300000
           EXEC SQL                                                     03310000
               OPEN SHRINK_CSR                                          03320000
           END-EXEC.                                                    03330000
                                                                        03340000
           IF SQLCODE NOT = ZERO                                        03350000
               MOVE '2100-OPEN-SHRINK-CURSOR '                          03360000
                                       TO PV-PARAGRAPH-NAME             03370000
               PERFORM 9999-SQL-ABEND                                   03380000
           END-IF.                                                      03390000
                                                                        03400000
                                                                        03410000
       2200-FETCH-SHRINK-CURSOR.                                        03410100
                                                                        03410200
           EXEC SQL                                                     03410300
               FETCH SHRINK_CSR                                         03410400
               INTO    :INSHNK-FSCL-WK-END-DTE                          03410502
                      ,:INSHNK-ML-PSTG-MN-END-DTE                       03410602
                      ,:INSHNK-DEPT-NBR                                 03410702
                      ,:INSHNK-SUB-CL-NBR                               03410802
                      ,:INSHNK-STR-NBR                                  03410902
                      ,:INSHNK-FSCL-MN-END-DTE                          03411002
                      ,:INSHNK-SLS-RTL-AMT                              03411102
                      ,:INSHNK-SHNK-RTL-AMT                             03411202
           END-EXEC.                                                    03411300
                                                                        03411400
           EVALUATE TRUE                                                03411500
               WHEN SQLCODE = ZERO                                      03411600
                   SET PS-SHRINK-CSR-AT-END-NO  TO TRUE                 03411700
                   PERFORM 2210-WRITE-TO-AUDIT-FILE                     03411800
                   PERFORM 2220-DELETE-FROM-SHRINK-TBL                  03411903
               WHEN SQLCODE = +100                                      03412000
                   SET PS-SHRINK-CSR-AT-END-YES TO TRUE                 03412100
               WHEN OTHER                                               03412200
                   MOVE '2200-FETCH-SHRINK-CURSOR   '                   03412300
                                       TO PV-PARAGRAPH-NAME             03412400
                   PERFORM 9999-SQL-ABEND                               03412500
           END-EVALUATE.                                                03412600
                                                                        03412700
                                                                        03412800
       2300-CLOSE-SHRINK-CURSOR.                                        03413000
                                                                        03420000
           EXEC SQL                                                     03430000
               CLOSE SHRINK_CSR                                         03440000
           END-EXEC.                                                    03450000
                                                                        03460000
           IF SQLCODE NOT = ZERO                                        03470000
               MOVE '2300-CLOSE-SHRINK-CURSOR '                         03480000
                                       TO PV-PARAGRAPH-NAME             03490000
               PERFORM 9999-SQL-ABEND                                   03500000
           END-IF.                                                      03510000
                                                                        03520000
       2210-WRITE-TO-AUDIT-FILE.                                        04660000
                                                                        05790000
           MOVE INSHNK-FSCL-WK-END-DTE                                  05800002
             TO PV-AUDIT-FSCL-WK-END-DTE.                               05801000
           MOVE INSHNK-ML-PSTG-MN-END-DTE                               05810002
             TO PV-AUDIT-ML-PSTG-MN-END-DTE.                            05811000
           MOVE INSHNK-DEPT-NBR                                         05820002
             TO PV-AUDIT-DEPT-NBR.                                      05821000
           MOVE INSHNK-SUB-CL-NBR                                       05830002
             TO PV-AUDIT-SUB-CL-NBR.                                    05831000
           MOVE INSHNK-STR-NBR                                          05840002
             TO PV-AUDIT-STR-NBR.                                       05841000
           MOVE INSHNK-FSCL-MN-END-DTE                                  05850002
             TO PV-AUDIT-FSCL-MN-END-DTE.                               05851000
           MOVE INSHNK-SLS-RTL-AMT                                      05860002
             TO PV-AUDIT-SLS-RTL-AMT.                                   05861000
           MOVE INSHNK-SHNK-RTL-AMT                                     05870002
             TO PV-AUDIT-SHNK-RTL-AMT.                                  05880000
                                                                        06010000
           WRITE AUDIT-REC FROM PV-AUDIT-REC.                           06011000
             ADD 1  TO PA-AUDIT-COUNT.                                  06012000
                                                                        06020000
      ****************************************************************  07230000
      * 2220-DELETE-FROM-SHRINK-TBL.                                    07240000
      ****************************************************************  07250000
                                                                        07250100
       2220-DELETE-FROM-SHRINK-TBL.                                     07250200
                                                                        07250300
           EXEC SQL                                                     07250400
             DELETE FROM TINSHNK                                        07250501
               WHERE FSCL_WK_END_DTE    = :INSHNK-FSCL-WK-END-DTE       07250602
                 AND ML_PSTG_MN_END_DTE = :INSHNK-ML-PSTG-MN-END-DTE    07250702
                 AND DEPT_NBR           = :INSHNK-DEPT-NBR              07250802
                 AND SUB_CL_NBR         = :INSHNK-SUB-CL-NBR            07250902
                 AND STR_NBR            = :INSHNK-STR-NBR               07251002
                 AND FSCL_MN_END_DTE    = :INSHNK-FSCL-MN-END-DTE       07251102
                 AND SLS_RTL_AMT        = :INSHNK-SLS-RTL-AMT           07251202
                 AND SHNK_RTL_AMT       = :INSHNK-SHNK-RTL-AMT          07251302
                                                                        07252100
           END-EXEC.                                                    07252200
                                                                        07252300
           EVALUATE SQLCODE                                             07252400
               WHEN ZERO                                                07252500
                   ADD 1                      TO PA-DELETE-COUNT        07252600
               WHEN OTHER                                               07253100
                    DISPLAY '*************************************'     07253200
                    DISPLAY 'ROW IN PROCESS : '                         07253300
                    'FSCL-WK-END-DTE :' INSHNK-FSCL-WK-END-DTE,         07253402
                    'ML-PSTG-DTE     :' INSHNK-ML-PSTG-MN-END-DTE,      07253502
                    'DEPT-NBR        :' INSHNK-DEPT-NBR,                07253602
                    'SUB-CL-NBR      :' INSHNK-SUB-CL-NBR,              07253702
                    'STR-NBR         :' INSHNK-STR-NBR,                 07253802
                    'FSCL-MN-END-DTE :' INSHNK-FSCL-MN-END-DTE,         07253902
                    'SLS-RTL-AMT     :' INSHNK-SLS-RTL-AMT,             07254002
                    'SHNK-RTL-AMT    :' INSHNK-SHNK-RTL-AMT             07254102
                    DISPLAY '*************************************'     07254200
                    MOVE '2220-DELETE-FROM-SHRINK-TBL'                  07254300
                                          TO PV-PARAGRAPH-NAME          07254400
                    PERFORM 9999-SQL-ABEND                              07254500
           END-EVALUATE.                                                07254600
                                                                        07254700
      ******************************************************************07863000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               07864000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             07865000
      ******************************************************************07866000
       9999-SQL-ABEND.                                                  07867000
                                                                        07868000
           DISPLAY '**  ABEND     **'.                                  07870000
           DISPLAY '**  PROGRAM   **  = ' PC-PGM-NAME.                  07880000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            07890000
                                                                        07900000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         07910000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        07920000
                                                                        07930000
           COPY DPPD004.                                                07940000
                                                                        07950000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           07960000
                                                                        07970000
