      ******************************************************************        
      *                                                                *        
      * INPD050 - DATE VALIDATION FOR ALTERNATE DATE CONTROL CARD.     *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *                                                                *        
      * DESCRIPTION: THIS COPY BOOK IS TO EDIT/VALIDATE THE INCOMING   *        
      *              ALTERNATE DATE CONTROL CARD.                      *        
      *                                                                *        
      *----------------------------------------------------------------*        
      * HOW TO USE THIS COPYBOOK :                                     *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *  1.  INCLUDE COPY BOOK INWS050 IN THE MAIN PROGRAM.            *        
      *                                                                *        
      *  2.  INCLUDE SQL FOR TDTATTR IF NOT ALREADY IN THE MAIN PROGRAM*        
      *                                                                *        
      *  3.  IN THE MAIN PROGRAM INITIALIZATION OPEN THE INPUT         *        
      *      ALTERNATE-DATE-CNTL-FILE ALONG WITH ANY OTHER FILES BEING *        
      *      USED. SET THE INWS050-PROGRAM-NUMBER TO THE INCLUDING     *        
      *      PROGRAM.                                                  *        
      *                                                                *        
      *  4.  ERROR HANDLING IS TAKEN CARE IN THE COPY BOOK ITSELF.     *        
      *                                                                *        
      *----------------------------------------------------------------*        
      * CHANGE HISTORY:                                                *        
      * WR NUMBER     PROGRAMMER       DATE                            *        
      * ---------------------------------------------------------------*        
      * WR34557       KARTHIK M        09/13/2011                      *        
      * ORIGINAL VERSION                                               *        
      *                                                                *        
      ******************************************************************        
                                                                                
      ******************************************************************        
      * 1100-EDIT-VALIDATE-CNTL.                                       *        
      *  - UNDER NORMAL CONDITIONS THE EDIT/VALIDATE IS NOT NECESSARY  *        
      *    BECAUSE THE CARD IS SYSTEM GENERATED WEEKLY. IF, HOWEVER,   *        
      *    DURING RERUN SITUATIONS, THE CARD IS MANUALLY               *        
      *    PREPARED / MANIPULATED, SOME EDITING/VALIDATION MUST BE DONE*        
      ******************************************************************        
       1100-EDIT-VALIDATE-CNTL.                                                 
                                                                                
           PERFORM IN050-1120-READ-DATE-FILE.                                   
           SET PS-IN050-ERR-FLG-NO          TO TRUE                             
                                                                                
      *--  IN001-OPN-FSCL-MN-DTE DATE VALIDATION                                
           MOVE IN001-OPN-FSCL-MN-DTE       TO PV-IN050-PROCESS-DATE            
                                               DTATTR-KY-DTE.                   
           MOVE SPACES                      TO PV-IN050-DTE-FLD-NME.            
           MOVE 'IN001-OPN-FSCL-MN-DTE'     TO PV-IN050-DTE-FLD-NME.            
           PERFORM IN050-1130-EDIT-DATE-PARA.                                   
           PERFORM IN050-1150-SELECT-TDTATTR.                                   
           MOVE SPACES                      TO PV-IN050-DTE-FLD-NME.            
           MOVE 'IN001-OPN-FSCL-MN-DTE-MM'  TO PV-IN050-DTE-FLD-NME.            
                                                                                
           IF  IN001-OPN-FSCL-MN-NBR IS NUMERIC                                 
           AND IN001-OPN-FSCL-MN-NBR = DTATTR-FSCL-MN-NBR                       
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY '**'                                                     
               DISPLAY 'INVALID MONTH IN ALTERNATE DATE CNTL RECORD'            
               DISPLAY 'IN001-OPN-FSCL-MN-NBR = ' IN001-OPN-FSCL-MN-NBR E-MM    
               DISPLAY 'DTATTR-FSCL-MN-NBR  = '   DTATTR-FSCL-MN-NBR            
               SET PS-IN050-ERR-FLG-YES     TO TRUE                             
           END-IF.                                                              
                                                                                
      *--  IN001-OPN-FSCL-WK-DTE DATE VALIDATION                                
           MOVE IN001-OPN-FSCL-WK-DTE       TO PV-IN050-PROCESS-DATE            
                                               DTATTR-KY-DTE.                   
           MOVE SPACES                      TO PV-IN050-DTE-FLD-NME.            
           MOVE 'IN001-OPN-FSCL-WK-DTE'     TO PV-IN050-DTE-FLD-NME.            
           PERFORM IN050-1130-EDIT-DATE-PARA.                                   
           PERFORM IN050-1150-SELECT-TDTATTR.                                   
                                                                                
      *--  IN001-OPN-FSCL-WK-BEG-DTE DATE VALIDATION                            
           MOVE IN001-OPN-FSCL-WK-BEG-DTE   TO PV-IN050-PROCESS-DATE            
                                               DTATTR-KY-DTE.                   
           MOVE SPACES                      TO PV-IN050-DTE-FLD-NME.            
           MOVE 'IN001-OPN-FSCL-WK-BEG-DTE' TO PV-IN050-DTE-FLD-NME.            
           PERFORM IN050-1130-EDIT-DATE-PARA.                                   
           PERFORM IN050-1150-SELECT-TDTATTR.                                   
                                                                                
           IF  PS-IN050-ERR-FLG-YES                                             
               SET PS-IN050-ABEND-4015-DATE-ER                                  
                                            TO TRUE                             
               MOVE '1100-EDIT-VALIDATE-CNTL'                                   
                                            TO PV-IN050-PARAGRAPH-NAME          
               MOVE 'INVALID DATA IN ALTERNATE DATE CNTL RECORD.'               
                                            TO PV-IN050-MSG-1                   
               PERFORM IN050-9200-ABEND                                         
           END-IF.                                                              
                                                                                
       IN050-1120-READ-DATE-FILE.                                               
      *****************************************************************         
      * DESCRIPTION     : READ ALTERNATE DATE CONTROL CARD                      
      *****************************************************************         
                                                                                
           READ ALTERNATE-DATE-CNTL-FILE  INTO IN001-RECORD                     
             AT END                                                             
               SET PS-IN050-ABEND-4003-CTLCRD-ER                                
                                            TO TRUE                             
               MOVE 'IN050-1120-READ-DATE-FILE'                                 
                                            TO PV-IN050-PARAGRAPH-NAME          
               MOVE 'READ ALTERNATE-DATE-CNTL-FILE'                             
                                            TO PV-IN050-MSG-1                   
               MOVE 'CLOSE WEEK DATE FILE IS EMPTY'                             
                                            TO PV-IN050-MSG-2                   
               PERFORM IN050-9200-ABEND                                         
           END-READ.                                                            
                                                                                
       IN050-1130-EDIT-DATE-PARA.                                               
      *****************************************************************         
      * DESCRIPTION     : VALIDATE THE DATE IN  ALTERNATE DATE CONTROL          
      *                   CARD                                                  
      *****************************************************************         
                                                                                
           IF  PV-IN050-PROCESS-DATE-CCYY IS NUMERIC                            
           AND PV-IN050-PROCESS-DATE-MM IS NUMERIC                              
           AND PV-IN050-PROCESS-DATE-DD IS NUMERIC                              
           AND PV-IN050-PROCESS-DT-DASH-1                                       
           AND PV-IN050-PROCESS-DT-DASH-2                                       
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-IN050-ABEND-4015-DATE-ER TO TRUE                          
               MOVE 'IN050-1130-EDIT-DATE-PARA'                                 
                                              TO PV-IN050-PARAGRAPH-NAME        
               DISPLAY PV-IN050-DTE-FLD-NME '= ' PV-IN050-PROCESS-DATE          
               MOVE 'DATE IS INVALID'         TO PV-IN050-MSG-1                 
               PERFORM IN050-9200-ABEND                                         
           END-IF.                                                              
                                                                                
       IN050-1150-SELECT-TDTATTR.                                               
      *****************************************************************         
      * DESCRIPTION     : SELECTS FSCL MONTH DTATTR-FSCL-MN-NBR                 
      *                   ATTRIBUTE FROM TDTATTR                                
      *****************************************************************         
                                                                                
           PERFORM WITH TEST AFTER                                              
                 VARYING PV-IN050-RETRY-COUNT FROM 1 BY 1                       
                   UNTIL (SQLCODE = +0)                                         
                      OR (SQLCODE = +100)                                       
                      OR (PV-IN050-RETRY-COUNT > PC-IN050-MAX-RETRY-CNT) )      
                                                                                
               EXEC SQL                                                         
                   SELECT  FSCL_MN_NBR                                          
                     INTO :DTATTR-FSCL-MN-NBR                                   
                     FROM TDTATTR                                               
                    WHERE KY_DTE = :DTATTR-KY-DTE                               
                   WITH UR                                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = 100                                           
                       DISPLAY '**'                                             
                       DISPLAY PV-IN050-DTE-FLD-NME ' = '                       
                               PV-IN050-PROCESS-DATE                            
                       DISPLAY 'DATE NOT FOUND IN TDTATTR'                      
                       SET PS-IN050-ERR-FLG-YES                                 
                                            TO TRUE                             
                   WHEN OTHER                                                   
                       MOVE 'IN050-1150-SELECT-TDTATTR'                         
                                            TO PV-IN050-PARAGRAPH-NAME          
                       MOVE 'SELECT ROW FROM TDTATTR'                           
                                            TO PV-IN050-DB2-OPER-ATTEMPT TED    
                       DISPLAY 'BAD RETURN FROM SELECT OF TDTATTR.'             
                       DISPLAY 'DTATTR-KY-DTE = ' DTATTR-KY-DTE                 
                       PERFORM IN050-9900-SQL-ABEND                             
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-IN050-RETRY-COUNT > PC-IN050-MAX-RETRY-CNT                    
               DISPLAY '** MAX RETRIES EXCEEDED ON TDTATTR **'                  
               MOVE 'IN050-1150-SELECT-TDTATTR'                         E       
                                         TO PV-IN050-PARAGRAPH-NAME     E       
               MOVE 'SELECT ROW FROM TDTATTR'                                   
                                         TO PV-IN050-DB2-OPER-ATTEMPT           
               DISPLAY 'DTATTR-KY-DTE = '   DTATTR-KY-DTE                       
               PERFORM IN050-9900-SQL-ABEND                                     
           END-IF.                                                              
                                                                                
                                                                                
      *****************************************************************         
      * IN050-9200-ABEND.                                             *         
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO FILE              *         
      *****************************************************************         
       IN050-9200-ABEND.                                                        
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PV-IN050-PGM-NUMBER.              
           DISPLAY '** PARAGRAPH       ** = ' PV-IN050-PARAGRAPH-NAME.          
           DISPLAY '**                 ** = ' PV-IN050-MSG-1.                   
           DISPLAY '**                 ** = ' PV-IN050-MSG-2.                   
           PERFORM IN050-9950-ABEND.                                            
                                                                                
       IN050-9900-SQL-ABEND.                                                    
      ******************************************************************        
      * SQL ABEND ROUTINE                                                       
      ******************************************************************        
                                                                                
           SET PS-IN050-ABEND-4013-DB2-ER     TO TRUE                           
           DISPLAY '**  ABEND          **'.                                     
           DISPLAY '**  PROGRAM        **  = ' PV-IN050-PGM-NUMBER              
           DISPLAY '**  PARAGRAPH      **  = ' PV-IN050-PARAGRAPH-NAME          
           DISPLAY '**  OPERATION      **  = ' PV-IN050-DB2-OPER-ATTEMPT.       
      *                                                                         
      * THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                    
      * DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                   
      *                                                                         
           COPY DPPD004.                                                        
                                                                                
           PERFORM IN050-9950-ABEND.                                            
      *                                                                         
       IN050-9950-ABEND.                                                        
                                                                                
           CALL 'ILBOABN0'  USING PS-IN050-ABEND-CODE.                          
