000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400                                                                          
000500 PROGRAM-ID.             MRKUT079.                                        
000600 AUTHOR.                 DON TOMLINSON.                                   
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000800 DATE-WRITTEN            09-27-2002.                                      
000900 DATE-COMPILED.                                                           
001000                                                                          
001100*****************************************************************         
001200*USE UNLOAD OF DB2 TABLE TMRITEM AS DRIVER, AND COMPARE TO WH   *         
001300*EXTRACT INPUT FILE. OUTPUT ALL WH EXTRACT RECORDS THAT ARE ON  *         
001400*THE TMRITEM FILE.                                              *         
001500*****************************************************************         
001600*  04/11/03 - DONALD TOMLINSON                                          11
001700*             (P000539451) ADDED CHECK FOR FILES OUT OF SEQUENCE.       11
001710*             PGM WILL ABEND WHEN THIS OCCURS.                            
001800*****************************************************************         
001600*  06/22/04 - BOB FORD  WR# 26363     VERSION 1                         11
001700*             MODIFIED TO HANDLE 5-DIGIT STORE NUMBERS ON INPUT         11
001700*             FROM DW.  STORE REFORMATTED TO X(3) ON OUTPUT FILE.       11
001800*****************************************************************         
001600*  07/22/04 - BOB FORD  WR# 26363     VERSION 2                         11
001700*             REFORMATTING REMOVED, OUTPUT HAS 9(4) COMP STORE.         11
001800*****************************************************************         
001900 ENVIRONMENT DIVISION.                                                    
002000*****************************************************************         
002100                                                                          
002200 CONFIGURATION SECTION.                                                   
002300                                                                          
002400 SOURCE-COMPUTER.        IBM-3090.                                        
002500 OBJECT-COMPUTER.        IBM-3090.                                        
002600                                                                          
002700 INPUT-OUTPUT SECTION.                                                    
002800                                                                          
002900 FILE-CONTROL.                                                            
003000                                                                          
003100     SELECT TMRITEM-UNLOAD        ASSIGN TO INP01.                        
003200*                                                                         
003300     SELECT WH-EXTRACT-IN         ASSIGN TO INP02.                        
003400*                                                                         
003500     SELECT WH-EXTRACT-OUT        ASSIGN TO OUT01.                        
003600*                                                                         
003700*****************************************************************         
003800 DATA DIVISION.                                                           
003900*****************************************************************         
004000*                                                                         
004100 FILE SECTION.                                                            
004200                                                                          
004300 FD  TMRITEM-UNLOAD                                                       
004400     RECORDING MODE F                                                     
004500     BLOCK CONTAINS 0 RECORDS                                             
004600     RECORD CONTAINS 46 CHARACTERS                                        
004700     LABEL RECORDS ARE STANDARD.                                          
004800                                                                          
004900 01  TMRITEM-UNLOAD-REC               PIC X(46).                          
005000                                                                          
005100                                                                          
005200 FD  WH-EXTRACT-IN                                                        
005300     RECORDING MODE F                                                     
005400     BLOCK CONTAINS 0 RECORDS                                             
005500     RECORD CONTAINS 43 CHARACTERS                                        
005600     LABEL RECORDS ARE STANDARD.                                          
005700                                                                          
005800 01  WH-EXTRACT-IN-REC                PIC X(43).                          
005900                                                                          
006000 FD  WH-EXTRACT-OUT                                                       
006100     RECORDING MODE F                                                     
006200     BLOCK CONTAINS 0 RECORDS                                             
006300     RECORD CONTAINS 18 CHARACTERS                                        
006400     LABEL RECORDS ARE STANDARD.                                          
006500                                                                          
006600 01  WH-EXTRACT-OUT-REC               PIC X(18).                          
006700                                                                          
006800*                                                                         
006900******************************************                                
007000 WORKING-STORAGE SECTION.                                                 
007100******************************************                                
007200                                                                          
007300******************************************                                
007400*WORKING-STORAGE SWITCHES                                                 
007500******************************************                                
007600 01  PC-SWITCHES.                                                         
007700     05  PS-EOF-TMRITEM              PIC  X(01)    VALUE 'N'.             
007800         88  EOF-TMRITEM-YES                       VALUE 'Y'.             
007900         88  NOT-EOF-TMRITEM                       VALUE 'N'.             
008000                                                                          
008100     05  PS-EOF-WH-EXTRACT           PIC  X(01)    VALUE 'N'.             
008200         88  EOF-WH-EXTRACT-YES                    VALUE 'Y'.             
008300         88  NOT-EOF-WH-EXTRACT                    VALUE 'N'.             
008440                                                                          
008500******************************************                                
008600*WORKING-STORAGE CONSTANTS                                                
008700******************************************                                
008800 01  PC-CONSTANTS.                                                        
009100     05  PC-1                        PIC   9(01)   VALUE  1.              
009200                                                                          
009300******************************************                                
009400*WORKING-STORAGE VARIABLES                                                
009500******************************************                                
009600 01  PV-MISC-FIELDS.                                                      
009900     05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.          
009901     05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO.            
009910                                                                          
010200     05  PV-WH-EXTRACT-ITEM          PIC   9(15)   VALUE  0.              
010300     05  PV-WH-EXTRACT-PREV-ITEM     PIC   9(15)   VALUE  0.              
010310                                                                          
010400     05  PV-TMRITEM-UNLOAD-ITEM      PIC   9(15)   VALUE  0.              
010401     05  PV-TMRITEM-PREV-ITEM        PIC   9(15)   VALUE  0.              
010402                                                                          
010403     05  PV-WH-EXTRACT-IN-CNT        PIC   9(12)   VALUE  0.              
010410     05  PV-TMRITEM-IN-CNT           PIC   9(12)   VALUE  0.              
010420     05  PV-WH-EXTRACT-OUT-CNT       PIC   9(12)   VALUE  0.              
010500                                                                          
010800                                                                          
010900******************************************                                
011000* TMRITEM DB2 TABLE UNLOAD RECORD                                         
011100* COBOL DECLARATION FOR TABLE TMRITEM                                     
011200******************************************                                
011300 01  TMRITEM-UNLOAD-RECORD.                                               
011400     10 MRITEM-ITEM-NBR               PIC  S9(15)V COMP-3.                
011500     10 MRITEM-UCYCLE-CDE             PIC   X(1).                         
011600     10 MRITEM-TSL-NBR                PIC  SV9(1) COMP-3.                 
011700     10 MRITEM-BYPDDU-CDE             PIC   X(1).                         
011800     10 MRITEM-BYPSCL-NBR             PIC  S9(1)V9(2)                     
011810                                           COMP-3.                        
011900     10 MRITEM-FRCDDU-CDE             PIC   X(1).                         
012000     10 MRITEM-BYPZSL-CDE             PIC   X(1).                         
012100     10 MRITEM-BYPTFP-CDE             PIC   X(1).                         
012200     10 MRITEM-BYPZOH-CDE             PIC   X(1).                         
012300     10 MRITEM-BYPBEF-CDE             PIC   X(1).                         
012400     10 MRITEM-BYPAFT-CDE             PIC   X(1).                         
012500     10 MRITEM-LUMPSW-CDE             PIC   X(1).                         
012600     10 MRITEM-MINBI-FCTR             PIC  SV9(1) COMP-3.                 
012700     10 MRITEM-DDFTSW-CDE             PIC   X(1).                         
012800     10 MRITEM-RT-NBR                 PIC  S9(3)V COMP-3.                 
012900     10 MRITEM-RTF-FCTR               PIC  SV9(1) COMP-3.                 
013000     10 MRITEM-ZOPSW-CDE              PIC   X(1).                         
013100     10 MRITEM-OUTLSW-CDE             PIC   X(1).                         
013200     10 MRITEM-ZSSTOCK-CDE            PIC   X(1).                         
013300     10 MRITEM-PACKSW-CDE             PIC   X(1).                         
013400     10 MRITEM-DSER-CDE               PIC   X(1).                         
013500     10 MRITEM-LT-QTY                 PIC  S9(3)V COMP-3.                 
013600     10 MRITEM-IMIN-QTY               PIC  S9(7)V COMP-3.                 
013700     10 MRITEM-IMAX-QTY               PIC  S9(7)V COMP-3.                 
013800     10 MRITEM-SSADJ-PCT              PIC  SV9(1) COMP-3.                 
013900     10 MRITEM-RT-DAY-NBR             PIC  S9(3)V COMP-3.                 
014000     10 MRITEM-DDUPRM-SW              PIC   X(1).                         
014100     10 MRITEM-ORD-GROUP-SW           PIC   X(1).                         
014200     10 MRITEM-STAT-CDE               PIC   X(1).                         
014300                                                                          
014400************************************************                          
014500* WAREHOUSE EXTRACT RECORD WITH FIELD DELIMITERS                          
014600************************************************                          
014700 01  WH-EXTRACT-RECORD.                                                   
014800     05  WH-ITEM-SIGN                PIC  X(01).                          
014900     05  WH-ITEM                     PIC  9(15).                          
015000     05  FILLER                      PIC  X(02).                          
015100     05  WH-STORE                    PIC  9(05).                          
015200     05  FILLER                      PIC  X(01).                          
015300     05  WH-FSCL-YR                  PIC  9(04).                          
015400     05  FILLER                      PIC  X(01).                          
015500     05  WH-FSCL-WK-NBR              PIC  9(02).                          
015600     05  FILLER                      PIC  X(01).                          
015700     05  WH-SLS-UNIT-QTY-SIGN        PIC  X(01).                          
015800     05  WH-SLS-UNT-QTY              PIC  9(09).                          
015900     05  FILLER                      PIC  X(01).                          
016000                                                                          
016100************************************************                          
016200* WAREHOUSE EXTRACT RECORD WITHOUT DELIMITERS                             
016300************************************************                          
016400     COPY MRRD167.                                                        
016500                                                                          
016600*****************************************************************         
016700 PROCEDURE DIVISION.                                                      
016800*****************************************************************         
016900 0000-MAIN-PROCESSING.                                                    
016901                                                                          
016920     MOVE '0000-MAIN-PROCESSING ' TO PV-ABEND-PARAGRAPH.                  
017000                                                                          
017100     PERFORM 1000-INIT-ROUTINE.                                           
017200                                                                          
017300     PERFORM 2000-PROCESS-LOOP                                            
017400         UNTIL EOF-TMRITEM-YES                                            
017500         OR    EOF-WH-EXTRACT-YES.                                        
017600                                                                          
017700     PERFORM 3000-END-PROCESS.                                            
017800                                                                          
017900     GOBACK.                                                              
018000                                                                          
018100******************************************************************        
018200*INITIALIZATION ROUTINE.                                                  
018300******************************************************************        
018400 1000-INIT-ROUTINE.                                                       
018401                                                                          
018420     MOVE '1000-INIT-ROUTINE '    TO PV-ABEND-PARAGRAPH.                  
018430                                                                          
018500     PERFORM 1100-OPEN-FILES.                                             
018600                                                                          
018700     PERFORM 1200-READ-WH-EXTRACT.                                        
018720     MOVE PV-WH-EXTRACT-ITEM      TO PV-WH-EXTRACT-PREV-ITEM.             
018800                                                                          
018900     PERFORM 1300-READ-TMRITEM-UNLOAD.                                    
018920     MOVE PV-TMRITEM-UNLOAD-ITEM  TO PV-TMRITEM-PREV-ITEM.                
019000                                                                          
019100******************************************************************        
019200*OPEN BOTH INPUT FILES AND THE OUTPUT FILE.                               
019300******************************************************************        
019400 1100-OPEN-FILES.                                                         
019500                                                                          
019800     MOVE '1100-OPEN-FILES '      TO PV-ABEND-PARAGRAPH.                  
019900                                                                          
020000     OPEN   INPUT TMRITEM-UNLOAD                                          
020100                  WH-EXTRACT-IN                                           
020200           OUTPUT WH-EXTRACT-OUT.                                         
020300                                                                          
020400                                                                          
020500******************************************************************        
020600*READ THE WAREHOUSE EXTRACT FLAT FILE.                                    
020700******************************************************************        
020800 1200-READ-WH-EXTRACT.                                                    
020900                                                                          
021100     MOVE '1200-READ-WH-EXTRACT'                                          
021200                                  TO PV-ABEND-PARAGRAPH.                  
021300                                                                          
021400     READ WH-EXTRACT-IN INTO WH-EXTRACT-RECORD                            
021500         AT END SET EOF-WH-EXTRACT-YES TO TRUE.                           
021600                                                                          
021700     IF EOF-WH-EXTRACT-YES                                                
021800         NEXT SENTENCE                                                    
021900     ELSE                                                                 
021910         ADD PC-1     TO PV-WH-EXTRACT-IN-CNT                             
022000         MOVE WH-ITEM             TO PV-WH-EXTRACT-ITEM                   
022100     END-IF.                                                              
022200                                                                          
022300******************************************************************        
022400*READ THE DB2 TMRITEM UNLOAD FLAT FILE.                                   
022500******************************************************************        
022600 1300-READ-TMRITEM-UNLOAD.                                                
022700                                                                          
022900     MOVE '1300-READ-TMRITEM-UNLOAD'                                      
023000                                  TO PV-ABEND-PARAGRAPH.                  
023100                                                                          
023200     READ TMRITEM-UNLOAD        INTO TMRITEM-UNLOAD-RECORD                
023300         AT END SET EOF-TMRITEM-YES TO TRUE.                              
023400                                                                          
023500     IF EOF-TMRITEM-YES                                                   
023600         NEXT SENTENCE                                                    
023700     ELSE                                                                 
023710         ADD PC-1     TO PV-TMRITEM-IN-CNT                                
023800         MOVE MRITEM-ITEM-NBR     TO PV-TMRITEM-UNLOAD-ITEM.              
023900                                                                          
024000******************************************************************        
024100*READ AND MATCH THE TWO INPUT FILES. WHEN SAME ITEM NUMBER IS             
024200*FOUND ON BOTH FILES, THEN OUTPUT THE WH-EXTRACT FILE.                    
024300******************************************************************        
024400 2000-PROCESS-LOOP.                                                       
024401                                                                          
024420     MOVE '2000-PROCESS-LOOP    ' TO PV-ABEND-PARAGRAPH.                  
024430                                                                          
024500     IF PV-TMRITEM-UNLOAD-ITEM = PV-WH-EXTRACT-ITEM                       
025000         PERFORM 2100-WRITE-WH-EXTRACT                                    
025100         PERFORM 1200-READ-WH-EXTRACT                                     
025800     ELSE                                                                 
025900         IF PV-TMRITEM-UNLOAD-ITEM < PV-WH-EXTRACT-ITEM                   
025910             MOVE PV-TMRITEM-UNLOAD-ITEM                                  
025920                                  TO PV-TMRITEM-PREV-ITEM                 
026000             PERFORM 1300-READ-TMRITEM-UNLOAD                             
026100         ELSE                                                             
026101             MOVE PV-WH-EXTRACT-ITEM                                      
026110                                  TO PV-WH-EXTRACT-PREV-ITEM              
026200             PERFORM 1200-READ-WH-EXTRACT                                 
026300         END-IF                                                           
026400     END-IF.                                                              
026401                                                                          
026410     IF PV-WH-EXTRACT-ITEM < PV-WH-EXTRACT-PREV-ITEM                      
026420         DISPLAY 'NEW EXT ITEM = ' PV-WH-EXTRACT-ITEM                     
026430         DISPLAY 'OLD EXT ITEM = ' PV-WH-EXTRACT-PREV-ITEM                
026440         PERFORM 9900-ABEND.                                              
026441                                                                          
026450     IF PV-TMRITEM-UNLOAD-ITEM < PV-TMRITEM-PREV-ITEM                     
026460         DISPLAY 'NEW UNL ITEM = ' PV-TMRITEM-UNLOAD-ITEM                 
026470         DISPLAY 'OLD UNL ITEM = ' PV-TMRITEM-PREV-ITEM                   
026471         PERFORM 9900-ABEND.                                              
026500                                                                          
026600******************************************************************        
026700*WRITE THE WAREHOUSE EXTRACT RECORD OUT WHEN ITEM NUMBER FOUND ON         
026800*TMRITEM DB2 EXTRACT FILE.                                                
026900******************************************************************        
027000 2100-WRITE-WH-EXTRACT.                                                   
027100                                                                          
027300     MOVE '2100-WRITE-WH-EXTRACT' TO PV-ABEND-PARAGRAPH.                  
027400                                                                          
027410     MOVE WH-ITEM                 TO MR167-ITEM.                          
027420     MOVE WH-STORE                TO MR167-STORE                          
027430     MOVE WH-FSCL-YR              TO MR167-FSCL-YEAR.                     
027440     MOVE WH-FSCL-WK-NBR          TO MR167-FSCL-WEEK.                     
027450     MOVE WH-SLS-UNT-QTY          TO MR167-SLS-UNIT-QTY.                  
027460                                                                          
027500     WRITE WH-EXTRACT-OUT-REC FROM MR167-WH-EXTRACT-RECORD.               
027600     ADD PC-1     TO PV-WH-EXTRACT-OUT-CNT.                               
028000                                                                          
028100******************************************************************        
028200*PROGRAM CLEANUP AFTER ALL RECORDS ARE PROCESSED.                         
028300******************************************************************        
028400 3000-END-PROCESS.                                                        
028401                                                                          
028420     MOVE '3000-CLOSE-FILES '     TO PV-ABEND-PARAGRAPH.                  
028430                                                                          
028440     DISPLAY 'MRKUT079 WH IN COUNT = ' PV-WH-EXTRACT-IN-CNT.              
028441     DISPLAY 'MRKUT079 WH OUT COUNT = ' PV-WH-EXTRACT-OUT-CNT.            
028442     DISPLAY 'MRKUT079 ITEM IN COUNT = ' PV-TMRITEM-IN-CNT.               
028450                                                                          
028500     PERFORM 3100-CLOSE-FILES.                                            
028600                                                                          
028700******************************************************************        
028800*CLOSE BOTH INPUT FILES AND THE OUTPUT FILE.                              
028900******************************************************************        
029000 3100-CLOSE-FILES.                                                        
029100                                                                          
029400     MOVE '3100-CLOSE-FILES '     TO PV-ABEND-PARAGRAPH.                  
029410                                                                          
029500     CLOSE TMRITEM-UNLOAD                                                 
029600           WH-EXTRACT-IN                                                  
029700           WH-EXTRACT-OUT.                                                
029710                                                                          
029810******************************************************************        
029820*ABEND PARAGRAPH                                                          
029830******************************************************************        
029840 9900-ABEND.                                                              
029850       DISPLAY '** ABEND **'.                                             
029860       DISPLAY '** PROGRAM    = MRKUT079'.                                
029870       DISPLAY '** PARAGRAPH  = ' PV-ABEND-PARAGRAPH.                     
029880       DISPLAY '** MESSAGE    = '                                         
029890               'FILE OUT OF SEQUENCE '                                    
029900       MOVE +4005                TO PV-ABEND-CODE.                        
029910       CALL 'ILBOABN0'           USING PV-ABEND-CODE.                     
