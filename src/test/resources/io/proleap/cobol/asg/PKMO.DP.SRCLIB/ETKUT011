       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    ETKUT011                                                  
       AUTHOR.        VLADIMIR FILIPOVIC.                                       
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  07/30/04.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *                                                                         
      * THIS PROGRAM PULLS STORE RELATIONSHIPS FOR SPECIFIC                     
      * DISTRIBUTION CENTERS FOR SPECIFIC DATES.                                
      *                                                                         
      * ONLY STORES THAT ARE OPENED OR WILL BE OPENED TO RECEIVE                
      * WITHIN 120 DAYS OF THE RUN DATE ARE EXTRACTED.                          
      *                                                                         
      * THE RUN DATE IS THE CURRENT SYSTEM DATE.  THIS DATE CAN BE              
      * OVERRIDDEN WITH A CONTROL DATE USING THE CONTROL CARD.                  
      *                                                                         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT IN-DC-CHG-FILE ASSIGN TO INP01.                               
           SELECT OUT-FUT-REL-FILE ASSIGN TO OUT01.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  IN-DC-CHG-FILE                                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 30 CHARACTERS                                        
           RECORDING  MODE IS F                                                 
           DATA RECORD IS IN-DC-CHG-RECORD.                                     
       01  IN-DC-CHG-RECORD             PIC X(30).                              
                                                                                
       FD  OUT-FUT-REL-FILE                                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 30 CHARACTERS                                        
           RECORDING  MODE IS F                                                 
           DATA RECORD IS OUT-FUT-REL-RECORD.                                   
       01  OUT-FUT-REL-RECORD           PIC X(30).                              
                                                                                
      *****************************************************************         
      * WORKING STORAGE                                                         
      *****************************************************************         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES               PIC S9(03)  VALUE +3                
                                                        COMP SYNC.              
           05  PC-STORE-CODE                PIC X(02)   VALUE 'DS'.             
           05  PC-DC-CODE                   PIC X(02)   VALUE 'DC'.             
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-CONTROL-CARD.                                                 
               10  PV-CONTROL-DATE          PIC X(10)   VALUE SPACES.           
               10  FILLER                   PIC X(70)   VALUE SPACES.           
           05  PV-RETRY-COUNT               PIC S9(04)  VALUE +0                
                                                        COMP.                   
           05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACE.            
           05  PV-DB2-OPERATION-ATTEMPTED   PIC X(30)   VALUE SPACE.            
                                                                                
           05  PV-EFF-STOP-DTE              PIC X(10)  VALUE SPACES.            
           05  PV-LOC-NBR-FROM-LOC-CURSOR   PIC S9(05) VALUE ZERO COMP.         
           05  PV-EFF-BEG-DTE-MINUS-ONE     PIC X(10)  VALUE SPACES.            
           05  PV-OUT-LOC-NMBR-TEMP         PIC 9(05)  VALUE ZEROS COMP.        
                                                                                
       01  SW-SWITCHES.                                                         
           05  END-OF-INPUT-SW              PIC X          VALUE 'N'.           
               88  END-OF-INPUT                            VALUE 'Y'.           
           05  END-OF-LOCATION-CSR-SW          PIC X       VALUE 'N'.           
               88  END-OF-LOCATION-CSR                     VALUE 'Y'.           
           05  END-OF-DCREL-CSR-SW             PIC X       VALUE 'N'.           
               88  END-OF-DCREL-CSR                        VALUE 'Y'.           
                                                                                
       01  ABEND-CODE                       PIC S9(04) VALUE ZERO               
                                                       COMP SYNC.               
       01  IN-DC-CHG-REC.                                                       
           05  IN-TYPE-CODE                PIC X(02)   VALUE SPACES.            
           05  IN-DC-NMBR                  PIC S9(05)  VALUE ZERO.              
           05  IN-CHG-DATE                 PIC X(10)   VALUE SPACES.            
       01  PV-IN-DC-NMBR               PIC S9(05)  VALUE ZEROS COMP.            
                                                                                
       01  OUT-FUT-REL-REC.                                                     
           05  OUT-TYPE-CODE               PIC X(02)   VALUE SPACES.            
           05  OUT-LOC-NMBR                PIC X(05)   VALUE ZEROS.             
           05  OUT-CHG-DATE                PIC X(10)   VALUE SPACES.            
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * DB2 TABLE DCLGENS NEEDED FOR THIS PROGRAM.                              
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE XIT00000                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE XIT00001                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  LOCATION CURSOR DECLARATION                                            
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               DECLARE LOCATION_CURSOR CURSOR                                   
               FOR SELECT DISTINCT A.LOC_NBR                                    
               FROM XIT_LOC B, XIT_DC_LOC_ASGMT A                               
               WHERE ((A.LOC_NBR = B.LOC_NBR)          AND                      
                      (A.DC_NBR = :PV-IN-DC-NMBR)      AND                      
                      (B.LOC_TYP_CDE = :PC-STORE-CODE) AND                      
                      (B.CLO_DTE > :PV-CONTROL-DATE))                           
               ORDER BY A.LOC_NBR                                               
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *   STORE/DC RELATIONSHIP CURSOR                                          
      *****************************************************************         
           EXEC SQL                                                             
               DECLARE DCREL_CURSOR CURSOR                                      
               FOR SELECT LOC_NBR                                               
                        , DC_NBR                                                
                        , EFF_BEG_DTE                                           
                        , EFF_BEG_DTE - 1 DAY                                   
               FROM XIT_DC_LOC_ASGMT                                            
               WHERE LOC_NBR = :PV-LOC-NBR-FROM-LOC-CURSOR                      
                  ORDER BY LOC_NBR                                              
                         , EFF_BEG_DTE DESC                                     
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
       LINKAGE SECTION.                                                         
                                                                                
      ******************************************************************        
      *  PROCEDURE DIVISION                                                     
      ******************************************************************        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           PERFORM 1000-INITIALIZE THRU 1000-EXIT.                              
           PERFORM 2000-PROCESS-INPUT THRU 2000-EXIT                            
              UNTIL END-OF-INPUT.                                               
           PERFORM 9000-EOJ-ROUTINE THRU 9000-EXIT.                             
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  INITIALIZATION ROUTINE                                                 
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           ACCEPT PV-CONTROL-CARD.                                              
                                                                                
           IF PV-CONTROL-DATE = 'CURRENT'                                       
              EXEC SQL                                                          
                   SET :PV-CONTROL-DATE = CURRENT_DATE                          
              END-EXEC.                                                         
                                                                                
           OPEN INPUT IN-DC-CHG-FILE.                                           
           OPEN OUTPUT OUT-FUT-REL-FILE.                                        
                                                                                
           READ IN-DC-CHG-FILE INTO IN-DC-CHG-REC                               
              AT END SET END-OF-INPUT TO TRUE.                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *  PROCESSING:                                                            
      *  FOR EACH INPUT RECORD WRITE THE DC RECORD.                             
      *  FOR EACH STORE ASSOCIATED WITH THE DC WRITE A STORE (DS) REC.          
      ******************************************************************        
       2000-PROCESS-INPUT.                                                      
                                                                                
           MOVE IN-DC-NMBR TO PV-IN-DC-NMBR.                                    
                                                                                
           INITIALIZE OUT-FUT-REL-REC.                                          
           MOVE PC-DC-CODE TO OUT-TYPE-CODE.                                    
           MOVE IN-DC-NMBR TO OUT-LOC-NMBR.                                     
           MOVE IN-CHG-DATE TO OUT-CHG-DATE.                                    
           WRITE OUT-FUT-REL-RECORD FROM OUT-FUT-REL-REC.                       
                                                                                
           MOVE 'N' TO END-OF-LOCATION-CSR-SW.                                  
           PERFORM 2100-OPEN-LOCATION-CURSOR THRU 2100-EXIT.                    
           PERFORM 2300-FETCH-LOCATION-CURSOR THRU 2300-EXIT.                   
           PERFORM 2200-PROCESS-LOCATION THRU 2200-EXIT                         
              UNTIL END-OF-LOCATION-CSR.                                        
           PERFORM 2400-CLOSE-LOCATION-CURSOR THRU 2400-EXIT.                   
                                                                                
           READ IN-DC-CHG-FILE INTO IN-DC-CHG-REC                               
              AT END SET END-OF-INPUT TO TRUE.                                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2100-OPEN-LOCATION-CURSOR.                                               
                                                                                
           EXEC SQL                                                             
                OPEN LOCATION_CURSOR                                            
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
              CONTINUE                                                          
           ELSE                                                                 
              MOVE '2100-OPEN-LOCATION-CURSOR' TO PV-PARAGRAPH-NAME             
              MOVE 'OPEN LOCATION CURSOR' TO PV-DB2-OPERATION-ATTEMPTED         
              PERFORM 9999-SQL-ABEND.                                           
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *                                                                         
      *****************************************************************         
       2200-PROCESS-LOCATION.                                                   
                                                                                
           INITIALIZE OUT-FUT-REL-REC.                                          
           PERFORM 2500-OPEN-DCREL-CURSOR.                                      
           MOVE 'N' TO END-OF-DCREL-CSR-SW.                                     
           PERFORM 2600-FETCH-DCREL-CURSOR.                                     
           IF NOT END-OF-DCREL-CSR                                              
             IF XIT00001-EFF-BEG-DTE <= IN-CHG-DATE AND                         
               XIT00001-DC-NBR = IN-DC-NMBR                                     
                 MOVE PC-STORE-CODE             TO OUT-TYPE-CODE                
                 MOVE XIT00001-LOC-NBR          TO PV-OUT-LOC-NMBR-TEMP         
                 MOVE PV-OUT-LOC-NMBR-TEMP      TO OUT-LOC-NMBR                 
                 WRITE OUT-FUT-REL-RECORD       FROM OUT-FUT-REL-REC            
             ELSE                                                               
                 MOVE PV-EFF-BEG-DTE-MINUS-ONE  TO PV-EFF-STOP-DTE              
                 PERFORM 2600-FETCH-DCREL-CURSOR                                
                 IF XIT00001-EFF-BEG-DTE <= IN-CHG-DATE  AND                    
                    XIT00001-DC-NBR = IN-DC-NMBR         AND                    
JF0405              PV-EFF-STOP-DTE >= IN-CHG-DATE                              
                       MOVE PC-STORE-CODE       TO OUT-TYPE-CODE                
                       MOVE XIT00001-LOC-NBR    TO PV-OUT-LOC-NMBR-TEMP         
                       MOVE PV-OUT-LOC-NMBR-TEMP TO OUT-LOC-NMBR                
                       WRITE OUT-FUT-REL-RECORD FROM OUT-FUT-REL-REC            
             END-IF                                                             
           END-IF.                                                              
           PERFORM 2700-CLOSE-DCREL-CURSOR.                                     
           PERFORM 2300-FETCH-LOCATION-CURSOR THRU 2300-EXIT.                   
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2300-FETCH-LOCATION-CURSOR.                                              
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
                   UNTIL ((SQLCODE = 0)                                         
                      OR  (SQLCODE = +100)                                      
                      OR  (PV-RETRY-COUNT > PC-MAX-RETRIES))                    
              EXEC SQL                                                          
                   FETCH LOCATION_CURSOR                                        
                   INTO  :PV-LOC-NBR-FROM-LOC-CURSOR                            
              END-EXEC                                                          
                                                                                
              EVALUATE SQLCODE                                                  
                 WHEN 0                                                         
                      CONTINUE                                                  
                 WHEN +100                                                      
                      SET END-OF-LOCATION-CSR TO TRUE                           
                 WHEN -904                                                      
                      ADD +1 TO PV-RETRY-COUNT                                  
                 WHEN -913                                                      
                      ADD +1 TO PV-RETRY-COUNT                                  
                 WHEN OTHER                                                     
                      MOVE '2300-FETCH-LOCATION-CURSOR' TO                      
                         PV-PARAGRAPH-NAME                                      
                      MOVE 'FETCH' TO PV-DB2-OPERATION-ATTEMPTED                
                      PERFORM 9999-SQL-ABEND                                    
              END-EVALUATE                                                      
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF SQLCODE = -904 OR SQLCODE = -913                                  
              MOVE '2300-FETCH-LOCATION-CURSOR' TO PV-PARAGRAPH-NAME            
              MOVE 'FETCH' TO PV-DB2-OPERATION-ATTEMPTED                        
              PERFORM 9999-SQL-ABEND                                            
           END-IF.                                                              
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2400-CLOSE-LOCATION-CURSOR.                                              
                                                                                
           EXEC SQL                                                             
                CLOSE LOCATION_CURSOR                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
              CONTINUE                                                          
           ELSE                                                                 
              MOVE '2400-CLOSE-LOCATION-CURSOR' TO PV-PARAGRAPH-NAME            
              MOVE 'CLOSE LOCATION CURSOR' TO PV-DB2-OPERATION-ATTEMPTED        
              PERFORM 9999-SQL-ABEND.                                           
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2500-OPEN-DCREL-CURSOR.                                                  
                                                                                
           EXEC SQL                                                             
               OPEN DCREL_CURSOR                                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
              MOVE '2500-OPEN-DCREL-CURSOR' TO PV-PARAGRAPH-NAME                
              MOVE 'OPEN DCREL CURSOR' TO PV-DB2-OPERATION-ATTEMPTED            
              PERFORM 9999-SQL-ABEND.                                           
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2600-FETCH-DCREL-CURSOR.                                                 
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
           PERFORM WITH TEST AFTER                                              
                   UNTIL ((SQLCODE = 0)                                         
                       OR (SQLCODE = +100)                                      
                       OR (PV-RETRY-COUNT > PC-MAX-RETRIES))                    
               EXEC SQL                                                         
                   FETCH DCREL_CURSOR                                           
                   INTO :XIT00001-LOC-NBR                                       
                      , :XIT00001-DC-NBR                                        
                      , :XIT00001-EFF-BEG-DTE                                   
                      , :PV-EFF-BEG-DTE-MINUS-ONE                               
               END-EXEC                                                         
                                                                                
               EVALUATE SQLCODE                                                 
                   WHEN 0                                                       
                       CONTINUE                                                 
                   WHEN +100                                                    
                       SET END-OF-DCREL-CSR TO TRUE                             
                   WHEN -904                                                    
                       ADD +1 TO PV-RETRY-COUNT                                 
                   WHEN OTHER                                                   
                       MOVE '2600-FETCH-DCREL-CURSOR'                           
                             TO PV-PARAGRAPH-NAME                               
                       MOVE 'FETCH DCREL CURSOR'                                
                             TO PV-DB2-OPERATION-ATTEMPTED                      
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
           IF SQLCODE = -904 OR SQLCODE = -913                                  
               MOVE '2600-FETCH-DCREL-CURSOR' TO                                
                          PV-PARAGRAPH-NAME                                     
               MOVE 'FETCH DCREL CURSOR' TO PV-DB2-OPERATION-ATTEMPTED          
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2700-CLOSE-DCREL-CURSOR.                                                 
                                                                                
           EXEC SQL                                                             
               CLOSE DCREL_CURSOR                                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '2700-CLOSE-DCREL-CURSOR' TO PV-PARAGRAPH-NAME              
               MOVE 'CLOSE DCREL CURSOR' TO PV-DB2-OPERATION-ATTEMPTED          
               PERFORM 9999-SQL-ABEND.                                          
                                                                                
       2700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *  END OF JOB ROUTINE.                                           *        
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE IN-DC-CHG-FILE                                                 
                 OUT-FUT-REL-FILE.                                              
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  ETKUT011'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPERATION-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
