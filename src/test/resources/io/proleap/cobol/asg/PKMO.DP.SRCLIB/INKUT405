       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT405.                                       
       AUTHOR.                  JIM ARENDT.                                     
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            07/30/2013.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                         
      *  INVENTORY LEDGER STORE-PARTIAL SHIP ADJUSTMENT CREATION                
      *                                                                         
      *  THIS PROGRAM WILL PROCESS AND CALCULATE THE NECESSARY DATA TO          
      *  BUILD THE REVERSAL ADJUSTMENT TRANSACTIONS FOR INVENTORY PARTL         
      *  SHIP AND THE CUTOFF DATABASE UNITS FOR THE STORE-EXPECTED UNITS        
      *  THIS PROGRAM WILL ONLY CONTAIN DATA FOR MANIFESTED STORES.             
      *                                                                         
      *  THE INPUT TO THIS PROGRAM WILL BE THE INVENTORY DATE                   
      *  IN THE FORMAT OF CCYY-MM-DD.  FOR EXAMPLE: '2005-06-20'                
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE       PGMR        DESCRIPTION OF CHANGES                  
      * -------  ---------- ----------- --------------------------------        
      * CHG61608 07-30-2013 J. ARENDT   INITIAL IMPLEMENTATION.                 
P48331* CHG89593 09-19-2014 COGNIZANT   EXPANDED ORDER NUMBER FROM 9            
P48331*                                 BYTES TO 10 BYTES TO AVOID              
P48331*                                 TRUNCATION.MODIFIED THE FIELDS          
P48331*                                 IN THE COPYBOOKS WSRD063 AND            
P48331*                                 INRD157.                                
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INV-DATE-CNTL-FILE                                            
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT STORE-EXPECTED-EXTRACT                                        
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT STORE-EXPECTED-TINSCLS-FMT                                    
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT STORE-EXPECTED-TINSCLS-FMT2                                   
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT ERROR-FILE                                                    
               ASSIGN TO OUT03.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INV-DATE-CNTL-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INV-DATE-CNTL-REC                PIC  X(80).                         
                                                                                
       FD  STORE-EXPECTED-EXTRACT                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
P48331 01  STORE-EXPECTED-RECORD            PIC  X(109).                        
                                                                                
       FD  STORE-EXPECTED-TINSCLS-FMT                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-EXPECTED-TINSCLS-REC       PIC  X(100).                        
                                                                                
       FD  STORE-EXPECTED-TINSCLS-FMT2                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-EXPECTED-TINSCLS-REC2      PIC  X(129).                        
                                                                                
       FD  ERROR-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
P48331 01  ERROR-REC                        PIC  X(109).                        
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC  X(08)  VALUE                   
                                                        'INKUT405'.             
           05  PC-999                       PIC  9(03)  VALUE 999.              
                                                                                
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-INPUT-LOC                 PIC  9(05)  VALUE ZERO.             
           05  PV-HOLD-LOC-NBR              PIC S9(04)  VALUE ZERO COMP.        
           05  PV-HOLD-KEY-DATE             PIC  X(10)  VALUE SPACES.           
           05  PV-PLND-INV-DTE              PIC  X(10)  VALUE SPACES.           
           05  PV-STR-NBR                   PIC S9(04)  VALUE ZERO COMP.        
           05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACES.           
           05  PV-DB2-OPER-ATTEMPTED        PIC  X(30)  VALUE SPACES.           
           05  PV-HOLD-AREA.                                                    
               10  PV-HOLD-DEPT-NBR         PIC  9(04)  VALUE ZERO.             
               10  PV-HOLD-SCL-NBR          PIC  9(04)  VALUE ZERO.             
               10  PV-HOLD-SKU-NBR          PIC  9(08)  VALUE ZERO.             
               10  PV-HOLD-UPC-NBR          PIC  9(13)  VALUE ZERO.             
           05  PV-DISP-NUMBER               PIC  Z,ZZZ,ZZ9.                     
           05  PV-DATE-TIME.                                                    
               10  PV-NEW-DATE.                                                 
                   15  PV-HOLD-MM-DD        PIC  X(06)  VALUE SPACES.           
                   15  PV-HOLD-CC           PIC  X(02)  VALUE SPACES.           
                   15  PV-HOLD-YY           PIC  X(02)  VALUE SPACES.           
               10  FILLER                   PIC  X(09)  VALUE SPACES.           
                                                                                
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-CTR                 PIC S9(07)  COMP-3                  
                                                        VALUE ZERO.             
           05  PA-WROTE-CTR                 PIC S9(07)  COMP-3                  
                                                        VALUE ZERO.             
           05  PA-ERROR-CTR                 PIC S9(07)  COMP-3                  
                                                        VALUE ZERO.             
                                                                                
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-CC-SW              PIC  X(01)  VALUE 'N'.              
               88  PS-END-OF-CC                         VALUE 'Y'.              
               88  PS-NOT-END-OF-CC                     VALUE 'N'.              
           05  PS-END-OF-FILE-SW            PIC  X(01)  VALUE 'N'.              
               88  PS-END-OF-FILE                       VALUE 'Y'.              
               88  PS-NOT-END-OF-FILE                   VALUE 'N'.              
           05  PS-VALID-LOC-SW              PIC  X(01)  VALUE 'N'.              
               88  PS-VALID-LOCATION                    VALUE 'Y'.              
               88  PS-INVALID-LOCATION                  VALUE 'N'.              
           05  PS-END-OF-CSR-SW             PIC  X(01)  VALUE SPACES.           
               88  PS-END-OF-CSR                        VALUE 'Y'.              
                                                                                
      ******************************************************************        
      *     VALID CONTROL TABLE                                                 
      ******************************************************************        
       01  TABLE-AREA.                                                          
           10  TABLE-MAX.                                                       
               15  PARM-TABLE-MAX           PIC  9(05)  VALUE 32767.            
                                                                                
           10  TABLE-PARMS.                                                     
               15  TINVPAR-TBL-SIZE         PIC  9(05)  VALUE ZEROES.           
                                                                                
           10  TINVPAR-TABLE.                                                   
               15  TINVPAR-ENTRY    OCCURS 32767 TIMES                          
                                    ASCENDING KEY IS PARM-LOC-NBR               
                                    INDEXED BY PARM-INDEX.                      
                   20  PARM-LOC-NBR         PIC  9(05).                         
                   20  PARM-FSCL-MN-DTE     PIC  X(10).                         
                   20  PARM-FSCL-MN-NBR     PIC  X(02).                         
                                                                                
      ******************************************************************        
      *  INPUT CONTROL CARD                                                     
       01  WS-CONTROL-CARD-DATE.                                                
           05  WS-CC-DATE                   PIC  X(10)  VALUE SPACES.           
           05  FILLER                       PIC  X(70)  VALUE SPACES.           
                                                                                
      ******************************************************************        
      *  INPUT FORMAT                                                           
           COPY WSRD063.                                                        
                                                                                
      ******************************************************************        
      *  OUTPUT FORMATS                                                         
           COPY INRD137.                                                        
           COPY INRD157.                                                        
                                                                                
      ******************************************************************        
      *  COMMON PRICING MODULE                                                  
           COPY INWS400.                                                        
                                                                                
      ******************************************************************        
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
           COPY DPWS004.                                                        
                                                                                
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
      *  XMT_LOC                                                                
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
      *  XMT_DC_LOC_ASGMT                                                       
           EXEC SQL                                                             
               INCLUDE XMT00002                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
       01  ABEND-CODE               PIC S9(04)  VALUE ZERO                      
                                                COMP SYNC.                      
                                                                                
      ******************************************************************        
           EXEC SQL                                                             
                DECLARE TINVPAR_CSR CURSOR FOR                                  
                 SELECT A.LOC_NBR                                               
                       ,B.OPN_FSCL_MN_DTE                                       
                       ,B.OPN_FSCL_MN_NBR                                       
                   FROM TINVPAR A                                               
                       ,TINCNTL B                                               
                 WHERE A.ACTL_FIN_BK_DTE  = '9999-09-09'                        
                   AND A.LOC_INV_STAT_CDE = 'IN'                                
                   AND A.INV_ID           = B.INV_ID                            
                   AND B.ACTV_IND         = 'Y'                                 
                 ORDER BY LOC_NBR                                               
                 WITH UR                                                        
           END-EXEC.                                                            
                                                                                
       LINKAGE SECTION.                                                         
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
                                                                                
       1000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1100-OPEN-FILES.                                             
                                                                                
           PERFORM 6100-LOAD-TINVPAR-TBL.                                       
                                                                                
           PERFORM 2000-PROCESS-FILE                                            
               UNTIL PS-END-OF-FILE.                                            
                                                                                
           PERFORM 1900-CLOSE-FILES.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
       1100-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  INV-DATE-CNTL-FILE                                       
                       STORE-EXPECTED-EXTRACT                                   
                OUTPUT STORE-EXPECTED-TINSCLS-FMT                               
                       STORE-EXPECTED-TINSCLS-FMT2                              
                       ERROR-FILE.                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '***************************************'.                   
           DISPLAY '*** PROGRAM NAME: '         PC-PROGRAM-NAME.                
           DISPLAY ' '.                                                         
                                                                                
           PERFORM 4000-READ-CONTROL-FILE.                                      
                                                                                
           PERFORM 4500-READ-INPUT-FILE.                                        
           IF PS-END-OF-FILE                                                    
               DISPLAY ' '                                                      
               DISPLAY ' *** INPUT FILE IS EMPTY ***'                           
               DISPLAY ' '                                                      
           END-IF.                                                              
                                                                                
      ******************************************************************        
       1900-CLOSE-FILES.                                                        
                                                                                
           CLOSE INV-DATE-CNTL-FILE                                             
                 STORE-EXPECTED-EXTRACT                                         
                 STORE-EXPECTED-TINSCLS-FMT                                     
                 STORE-EXPECTED-TINSCLS-FMT2                                    
                 ERROR-FILE.                                                    
                                                                                
           MOVE PA-INPUT-CTR                TO PV-DISP-NUMBER.                  
           DISPLAY ' RECORDS READ  = '         PV-DISP-NUMBER.                  
           MOVE PA-WROTE-CTR                TO PV-DISP-NUMBER.                  
           DISPLAY ' RECORDS WROTE = '         PV-DISP-NUMBER.                  
           MOVE PA-ERROR-CTR                TO PV-DISP-NUMBER.                  
           DISPLAY ' ERRORS WROTE  = '         PV-DISP-NUMBER.                  
           DISPLAY ' '.                                                         
                                                                                
      ******************************************************************        
       2000-PROCESS-FILE.                                                       
                                                                                
           INITIALIZE IN400-PV-INPUT-AREA.                                      
           INITIALIZE IN137-STR-EXPTED-RECORD.                                  
           INITIALIZE IN157-STR-EXPTED-RECORD.                                  
                                                                                
           MOVE WS063-SHIP-NODE    TO PV-INPUT-LOC.                             
           MOVE PV-INPUT-LOC       TO PV-HOLD-LOC-NBR                           
                                      IN400-PV-LOC-IN.                          
           MOVE WS-CC-DATE         TO PV-HOLD-KEY-DATE                          
                                      IN400-PV-EFF-DTE-IN.                      
                                                                                
           PERFORM 7000-SELECT-XMTLOC.                                          
                                                                                
           IF PS-VALID-LOCATION                                                 
               IF WS063-SKU-NBR > 0                                             
                   MOVE WS063-SKU-NBR      TO IN400-PV-SKU-IN                   
                   SET IN400-PV-SKU-LOOKUP TO TRUE                              
               END-IF                                                           
                                                                                
               PERFORM IN400-LOOKUP-RETAIL                                      
               IF IN400-PS-RTL-FOUND                                            
      **         MOVE LOOKUP VALUES TO THE CORRESPONDING OUTPUT FIELD           
                                                                                
                   PERFORM 3000-FORMAT-TINSCLS-RECORD                           
                   PERFORM 5000-WRITE-TINSCLS-RECORD                            
                                                                                
               ELSE                                                             
                   DISPLAY '** INVALID 1: '                                     
                           '  SKU: '     WS063-SKU-NBR                          
                           '  STORE: '   WS063-SHIP-NODE                        
                           '  DATE: '    PV-HOLD-KEY-DATE                       
                   DISPLAY IN400-PV-PARAGRAPH                                   
                   PERFORM 5200-WRITE-ERROR-RECORD                              
               END-IF                                                           
           ELSE                                                                 
               DISPLAY '** INVALID 2: '                                         
                       '  SKU: '         WS063-SKU-NBR                          
                       '  STORE: '       WS063-SHIP-NODE                        
                       '  DATE: '        PV-HOLD-KEY-DATE                       
               DISPLAY IN400-PV-PARAGRAPH                                       
               PERFORM 5200-WRITE-ERROR-RECORD                                  
           END-IF.                                                              
                                                                                
           PERFORM 4500-READ-INPUT-FILE.                                        
                                                                                
      ******************************************************************        
      * 3000-FORMAT-TINSCLS-RECORD                                              
      ******************************************************************        
       3000-FORMAT-TINSCLS-RECORD.                                              
                                                                                
           MOVE WS063-SHIP-NODE          TO IN137-STORE-NBR-9                   
                                            IN157-STORE-NBR-9.                  
                                                                                
           MOVE WS-CC-DATE               TO IN137-ADJ-DATE                      
                                            IN157-ADJ-DATE.                     
                                                                                
           MOVE IN400-PRC-SKU-OUT        TO PV-HOLD-SKU-NBR.                    
           MOVE PV-HOLD-SKU-NBR          TO IN137-SKU-9                         
                                            IN157-SKU-9.                        
                                                                                
           MOVE IN400-ATT-DEPT-OUT       TO PV-HOLD-DEPT-NBR.                   
           MOVE PV-HOLD-DEPT-NBR         TO IN137-DEPT-9                        
                                            IN157-DEPT-9.                       
                                                                                
           MOVE IN400-ATT-SCL-OUT        TO PV-HOLD-SCL-NBR.                    
           MOVE PV-HOLD-SCL-NBR          TO IN137-CLASS-9                       
                                            IN157-CLASS-9.                      
                                                                                
           MOVE WS063-QTY-SHIPPED        TO IN137-BAL-QTY                       
                                            IN157-BAL-QTY.                      
           MOVE IN400-PRC-UNIT-RTL-OUT   TO IN137-RTL-AMT                       
                                            IN157-RTL-AMT.                      
                                                                                
           COMPUTE IN400-PRC-EXTD-RTL-OUT ROUNDED =                             
                  (IN400-PRC-UNIT-RTL-OUT * IN137-BAL-QTY).                     
           MOVE IN400-PRC-EXTD-RTL-OUT   TO IN137-EXTD-RTL-AMT                  
                                            IN157-EXTD-RTL-AMT.                 
                                                                                
           MOVE WS063-ORDER-NBR1         TO IN157-ORDER-NBR.                    
                                                                                
           MOVE WS063-CREATE-DATE-TIME   TO PV-DATE-TIME.                       
           MOVE PV-NEW-DATE              TO IN157-CREATE-DATE.                  
                                                                                
           MOVE WS063-SHIP-DATE-TIME     TO PV-DATE-TIME.                       
           MOVE PV-NEW-DATE              TO IN157-SHIP-DATE.                    
                                                                                
      ******************************************************************        
      * 4000-READ-CONTROL-FILE.                                                 
      ******************************************************************        
       4000-READ-CONTROL-FILE.                                                  
                                                                                
           READ INV-DATE-CNTL-FILE                                              
               INTO WS-CONTROL-CARD-DATE                                        
                   AT END                                                       
                       MOVE 'Y' TO PS-END-OF-CC-SW                              
           END-READ.                                                            
                                                                                
           IF PS-END-OF-CC                                                      
               DISPLAY ' '                                                      
               DISPLAY ' *** NO CONTROL CARD FOUND TO PROCESS ***'              
               DISPLAY ' '                                                      
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 4500-READ-INPUT-FILE.                                                   
      ******************************************************************        
       4500-READ-INPUT-FILE.                                                    
                                                                                
           READ STORE-EXPECTED-EXTRACT                                          
               INTO WS063-PARTIAL-SHIP-REC                                      
                   AT END                                                       
                       MOVE 'Y' TO PS-END-OF-FILE-SW                            
           END-READ.                                                            
                                                                                
           IF PS-END-OF-FILE                                                    
              CONTINUE                                                          
           ELSE                                                                 
              ADD 1             TO PA-INPUT-CTR                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 5000-WRITE-TINSCLS-RECORD                                               
      ******************************************************************        
       5000-WRITE-TINSCLS-RECORD.                                               
                                                                                
           WRITE STORE-EXPECTED-TINSCLS-REC FROM                                
                 IN137-STR-EXPTED-RECORD.                                       
                                                                                
           WRITE STORE-EXPECTED-TINSCLS-REC2 FROM                               
                 IN157-STR-EXPTED-RECORD.                                       
                                                                                
           ADD 1 TO PA-WROTE-CTR.                                               
                                                                                
      ******************************************************************        
      * 5200-WRITE-ERROR-RECORD                                                 
      ******************************************************************        
       5200-WRITE-ERROR-RECORD.                                                 
                                                                                
           WRITE ERROR-REC FROM                                                 
                 WS063-PARTIAL-SHIP-REC.                                        
                                                                                
           ADD 1 TO PA-ERROR-CTR.                                               
                                                                                
      ******************************************************************        
      *  LOAD WORKING-STORAGE LOCATION TABLE WITH VALID LOCATIONS.              
      ******************************************************************        
       6100-LOAD-TINVPAR-TBL.                                                   
                                                                                
           PERFORM 6200-OPEN-TINVPAR-CSR.                                       
                                                                                
           MOVE ZERO TO TINVPAR-TBL-SIZE.                                       
           MOVE 'N'  TO PS-END-OF-CSR-SW.                                       
           PERFORM 6300-FETCH-PARM-CSR.                                         
                                                                                
           PERFORM                                                              
             VARYING PARM-INDEX FROM 1 BY 1                                     
                UNTIL PARM-INDEX > PARM-TABLE-MAX                               
                    OR PS-END-OF-CSR                                            
               MOVE INVPAR-LOC-NBR         TO  PARM-LOC-NBR(PARM-INDEX)         
               MOVE INCNTL-OPN-FSCL-MN-NBR TO                                   
                                           PARM-FSCL-MN-NBR(PARM-INDEX)         
               MOVE INCNTL-OPN-FSCL-MN-DTE TO                                   
                                           PARM-FSCL-MN-DTE(PARM-INDEX)         
               ADD 1 TO TINVPAR-TBL-SIZE                                        
               PERFORM 6300-FETCH-PARM-CSR                                      
           END-PERFORM.                                                         
                                                                                
           IF PARM-INDEX > PARM-TABLE-MAX                                       
             DISPLAY '*********** ABEND ************************'               
             DISPLAY ' TABLE OVERFLOW ON PARMS-TABLE            '               
             DISPLAY ' INCREASE PARM-TABLE-MAX LIMIT            '               
             DISPLAY ' CURRENT LIMIT = ' PARM-TABLE-MAX                         
             DISPLAY '******************************************'               
             DISPLAY '  '                                                       
             MOVE +4004 TO ABEND-CODE                                           
             CALL 'ILBOABN0' USING ABEND-CODE                                   
           END-IF.                                                              
                                                                                
           PERFORM 6400-CLOSE-TINVPAR-CSR.                                      
                                                                                
      ******************************************************************        
      *  OPEN THE TINVPAR_CSR CURSOR                                            
      ******************************************************************        
       6200-OPEN-TINVPAR-CSR.                                                   
                                                                                
           EXEC SQL                                                             
               OPEN TINVPAR_CSR                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN +0                                                           
                CONTINUE                                                        
                                                                                
              WHEN OTHER                                                        
                MOVE '6200-OPEN-TINVPAR-CSR' TO PV-PARAGRAPH-NAME               
                MOVE 'OPEN TINVPAR CURSOR'   TO PV-DB2-OPER-ATTEMPTED           
                PERFORM 9900-SQL-ABEND                                          
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  READ A RECORD FROM THE TINVPAR_CSR CURSOR                              
      ******************************************************************        
       6300-FETCH-PARM-CSR.                                                     
                                                                                
           EXEC SQL                                                             
               FETCH TINVPAR_CSR                                                
               INTO :INVPAR-LOC-NBR                                             
                   ,:INCNTL-OPN-FSCL-MN-DTE                                     
                   ,:INCNTL-OPN-FSCL-MN-NBR                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
             WHEN +0                                                            
                   CONTINUE                                                     
                                                                                
             WHEN +100                                                          
                   SET PS-END-OF-CSR TO TRUE                                    
                                                                                
             WHEN OTHER                                                         
                   MOVE '6300-FETCH-PARM-CSR'  TO PV-PARAGRAPH-NAME             
                   MOVE 'FETCH TINVPAR CURSOR' TO                               
                        PV-DB2-OPER-ATTEMPTED                                   
                   PERFORM 9900-SQL-ABEND                                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  CLOSE THE TINVPAR_CSR CURSOR                                           
      ******************************************************************        
       6400-CLOSE-TINVPAR-CSR.                                                  
                                                                                
           EXEC SQL                                                             
               CLOSE TINVPAR_CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN +0                                                           
                CONTINUE                                                        
                                                                                
              WHEN OTHER                                                        
                MOVE '6400-CLOSE-TINVPAR-CSR' TO PV-PARAGRAPH-NAME              
                MOVE 'CLOSE TINVPAR CURSOR'   TO PV-DB2-OPER-ATTEMPTED          
                PERFORM 9900-SQL-ABEND                                          
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 7000-SELECT-XMTLOC                                                      
      ******************************************************************        
       7000-SELECT-XMTLOC.                                                      
                                                                                
           EXEC SQL                                                             
                SELECT A.LOC_NBR                                                
                  INTO :XMT00001-LOC-NBR                                        
                  FROM XMT_LOC          A                                       
                      ,XMT_DC_LOC_ASGMT C                                       
                 WHERE A.LOC_NBR       = :PV-HOLD-LOC-NBR                       
                   AND A.LOC_NBR       = C.LOC_NBR                              
                   AND C.EFF_BEG_DTE  <= :PV-HOLD-KEY-DATE                      
                   AND A.CLO_DTE      >= :PV-HOLD-KEY-DATE                      
                   AND A.FINCL_CLO_DTE = '9999-09-09'                           
                   AND C.EFF_BEG_DTE   =                                        
                       (SELECT MIN(B.EFF_BEG_DTE)                               
                           FROM XMT_DC_LOC_ASGMT B                              
                          WHERE B.LOC_NBR = :PV-HOLD-LOC-NBR)                   
                 WITH UR                                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-VALID-LOCATION TO TRUE                                
                   PERFORM 8000-VALIDATE-INV-LOC                                
                                                                                
               WHEN SQLCODE = +100                                              
                   SET PS-INVALID-LOCATION TO TRUE                              
                                                                                
               WHEN OTHER                                                       
                   DISPLAY 'LOC-NBR  = '          PV-HOLD-LOC-NBR               
                   DISPLAY 'KEY-DATE = '          PV-HOLD-KEY-DATE              
                   MOVE '7000-SELECT-XMTLOC'   TO PV-PARAGRAPH-NAME             
                   MOVE 'SELECT ROW FROM XMT_LOC'                               
                                               TO PV-DB2-OPER-ATTEMPTED         
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  FIND THE LOCATION NUMBER IN THE INTERNAL PARMS TABLE AND GET           
      *  THE INVENTORY CONTROL FISAL MONTH DATA.                                
      ******************************************************************        
       8000-VALIDATE-INV-LOC.                                                   
           MOVE '8000-VALIDATE-INV-LOC' TO PV-PARAGRAPH-NAME.                   
                                                                                
           SET PARM-INDEX TO 1.                                                 
           SEARCH TINVPAR-ENTRY                                                 
             AT END                                                             
                 SET PS-INVALID-LOCATION TO TRUE                                
                                                                                
             WHEN PARM-LOC-NBR(PARM-INDEX) = PV-INPUT-LOC                       
                 SET PS-VALID-LOCATION TO TRUE                                  
                 MOVE PARM-FSCL-MN-DTE(PARM-INDEX) TO                           
                                 IN137-FSCL-MN-END-DTE                          
                                 IN157-FSCL-MN-END-DTE                          
                 MOVE PARM-FSCL-MN-NBR(PARM-INDEX) TO                           
                                 IN137-FSCL-MN-NBR                              
                                 IN157-FSCL-MN-NBR                              
                                                                                
             WHEN PARM-INDEX > TINVPAR-TBL-SIZE                                 
                 SET PS-INVALID-LOCATION TO TRUE                                
           END-SEARCH.                                                          
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKUT405'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      *IN400-LOOKUP-BY-SKU.                                                     
      *IN400-LOOKUP-BY-UPC-NBR.                                                 
      *IN400-LOOKUP-BY-UPC-ID.                                                  
      *IN400-LOOKUP-BY-ITM-NBR.                                                 
           COPY INPD400.                                                        
                                                                                
