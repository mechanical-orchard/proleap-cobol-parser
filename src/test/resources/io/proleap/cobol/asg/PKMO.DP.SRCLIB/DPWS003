00001 ***************************************************************** 06/16/94
00002 *    COPY MEMBER:  DPWS003                       LRECL = 1237   * DPWS003 
00003 *                                                               *    LV001
00004 *    THIS MEMBER IS USED TO GET MBS INFORMATION FROM A BATCH    * DPWS003 
00005 *    SUBROUTINE (DPKUT039) OR A CICS PROGRAM (DPKUT038).  THIS  * DPWS003 
00006 *    MEMBER MUST BE INCLUDED IN ANY BATCH PROGRAM THAT CALLS    * DPWS003 
00007 *    SUBROUTINE DPKUT039 OR CICS PROGRAM THAT LINKS TO DPKUT038.* DPWS003 
00008 *                                                               * DPWS003 
00009 *                                                               * DPWS003 
00010 *    TO CALL THE BATCH SUBROUTINE, DO THE FOLLOWING:            * DPWS003 
00011 *        05  PC-MBS-PGM          PIC X(08)   VALUE 'DPKUT039'.  * DPWS003 
00012 *                                                               * DPWS003 
00013 *        CALL PC-MBS-PGM USING DP003-MBS-SUBROUTINE-FIELDS.     * DPWS003 
00014 *                                                               * DPWS003 
00015 *                                                               * DPWS003 
00016 *    TO LINK TO THE CICS PROGRAM, DO THE FOLLOWING:             * DPWS003 
00017 *        05  PC-MBS-PGM          PIC X(08)   VALUE 'DPKUT038'.  * DPWS003 
00018 *                                                               * DPWS003 
00019 *        EXEC CICS                                              * DPWS003 
00020 *             LINK PROGRAM  (PC-MBS-PGM)                        * DPWS003 
00021 *                  COMMAREA (DP003-MBS-SUBROUTINE-FIELDS)       * DPWS003 
00022 *                  LENGTH   (..........)                        * DPWS003 
00023 *                  RESP     (..........)                        * DPWS003 
00024 *        END-EXEC.                                              * DPWS003 
00025 *                                                               * DPWS003 
00026 *    USING A VARIABLE NAME RATHER THAN A LITERAL IN THE CALL    * DPWS003 
00027 *    AND LINK STATEMENTS MAKES THEM DYNAMIC RATHER THAN         * DPWS003 
00028 *    STATIC.  IF YOU USE STATIC CALLS, THE 'CALLING' PROGRAM    * DPWS003 
00029 *    MUST BE RECOMPILED ANY TIME THE 'CALLED' PROGRAM CHANGES.  * DPWS003 
00030 *    IN OTHER WORDS, USE DYNAMIC CALLS.                         * DPWS003 
00031 *                                                               * DPWS003 
00032 *---------------------------------------------------------------* DPWS003 
00033 *                                                               * DPWS003 
00034 *    THE FOLLOWING IS A DESCRIPTION OF THE FUNCTIONS AND        * DPWS003 
00035 *    USAGE OF THIS MEMBER.                                      * DPWS003 
00036 *                                                               * DPWS003 
00037 *                                                               * DPWS003 
00038 *    PROCESSING FUNCTIONS                                       * DPWS003 
00039 *    --------------------                                       * DPWS003 
00040 *    THE FOLLOWING SHOWS EACH OF THE PROCESSING FUNCTIONS       * DPWS003 
00041 *    THAT MAY BE REQUESTED BY THE 'CALLING' PROGRAM.  ALONG     * DPWS003 
00042 *    WITH EACH FUNCTION IS A LIST OF THE INPUT FIELDS THAT      * DPWS003 
00043 *    MUST BE FILLED IN BY THE CALLING PROGRAM AND A LIST OF     * DPWS003 
00044 *    THE OUTPUT FIELDS RETURNED BY THE SUBPROGRAM AVAILABLE     * DPWS003 
00045 *    TO THE 'CALLING' PROGRAM.                                  * DPWS003 
00046 *                                                               * DPWS003 
00047 *    ALL OF THE FUNCTIONS CAN BE USED INDEPENDENTLY OR IN       * DPWS003 
00048 *    COMBINATION WITH ANY OF THE OTHER FUNCTIONS.  AS AN        * DPWS003 
00049 *    EXAMPLE, A CALL MAY BE MADE FOR THE FOLLOWING ONE          * DPWS003 
00050 *    FUNCTION:                                                  * DPWS003 
00051 *        GET BUYER NAME                                         * DPWS003 
00052 *    ALTERNATIVELY, A CALL COULD BE MADE TO DO THE FOLLOWING    * DPWS003 
00053 *    FOUR FUNCTIONS:                                            * DPWS003 
00054 *        GET SUBCLASS    DESCRIPTION                            * DPWS003 
00055 *        GET MAJOR CLASS DESCRIPTION                            * DPWS003 
00056 *        GET DEPARTMENT  DESCRIPTION                            * DPWS003 
00057 *        GET BUYER       NAME                                   * DPWS003 
00058 *    THE CALLING PROGRAM SHOULD INITIALIZE THE INCLUDE (OR      * DPWS003 
00059 *    AT A MINIMUM THE INPUT FIELDS) BEFORE SETTING UP THE       * DPWS003 
00060 *    CALL.  THIS IS NECESSARY BECAUSE THE CALLING PROGRAM       * DPWS003 
00061 *    RETURNS THE INPUT FIELDS INTACT.  IF THE CALLING PROGRAM   * DPWS003 
00062 *    MAKES A CALL FOR THE DEPARTMENT DESCRIPTION AFTER MAKING   * DPWS003 
00063 *    A CALL FOR THE SUBCLASS DESCRIPTION WITHOUT INITIALIZING   * DPWS003 
00064 *    THE INPUT FIELDS, THE CALLING PROGRAM WOULD PERFORM THE    * DPWS003 
00065 *    SUBCLASS DESCRIPTION FUNCTION ON THE SECOND CALL, POSSIBLY * DPWS003 
00066 *    CAUSING AN UNNECESSARY ERROR.                              * DPWS003 
00067 *                                                               * DPWS003 
00068 *    WHEN REQUESTING TO PROCESS ANY OF THE 'NAME' FUNCTIONS,    * DPWS003 
00069 *    THE CALLING PROGRAM CAN SELECT WHICH WAY THE NAME IS TO    * DPWS003 
00070 *    BE RETURNED TO IT.  THE OPTIONS ARE:                       * DPWS003 
00071 *        FIRSTNAME LASTNAME    (THIS IS THE DEFAULT)            * DPWS003 
00072 *        LASTNAME, FIRSTNAME                                    * DPWS003 
00073 *        LASTNAME                                               * DPWS003 
00074 *    IF NO FORMAT IS SPECIFIED FOR A 'NAME' FUNCTION, THE       * DPWS003 
00075 *    NAME WILL DEFAULT TO FIRSTNAME LASTNAME.                   * DPWS003 
00076 *                                                               * DPWS003 
00077 *---------------------------------------------------------------* DPWS003 
00078 *    FUNCTION:  GET SUBCLASS DESCRIPTION                        * DPWS003 
00079 *       INPUT:  SEE 'COMMON INPUT FIELDS' BELOW                 * DPWS003 
00080 *               SET DP003-SCL-LOOKUP TO TRUE.                   * DPWS003 
00081 *               MOVE 'XX'            TO DP003-MCL-INPUT.        * DPWS003 
00082 *               MOVE 'XXX'           TO DP003-SCL-INPUT.        * DPWS003 
00083 *      OUTPUT:  DP003-SCL-DESCRIPTION                           * DPWS003 
00084 *               DP003-RETURN-CODE:                              * DPWS003 
00085 *                   SEE 'COMMON RETURN CODES' BELOW             * DPWS003 
00086 *                   DP003-SCL-MUST-BE-NUMERIC                   * DPWS003 
00087 *                   DP003-MCL-MUST-BE-NUMERIC                   * DPWS003 
00088 *                   DP003-SCL-NOT-FOUND                         * DPWS003 
00089 *                   DP003-SCL-DB2-ERROR          DP003-SQLCA    * DPWS003 
00090 *                   DP003-SCL-MAX-RETRIES        DP003-SQLCA    * DPWS003 
00091 *               DP003-RETURN-MSG                                * DPWS003 
00092 *---------------------------------------------------------------* DPWS003 
00093 *    FUNCTION:  GET MAJOR CLASS DESCRIPTION                     * DPWS003 
00094 *       INPUT:  SEE 'COMMON INPUT FIELDS' BELOW                 * DPWS003 
00095 *               SET DP003-MCL-LOOKUP TO TRUE.                   * DPWS003 
00096 *               MOVE 'XXX'           TO DP003-MCL-INPUT.        * DPWS003 
00097 *      OUTPUT:  DP003-MCL-DESCRIPTION                           * DPWS003 
00098 *               DP003-RETURN-CODE:                              * DPWS003 
00099 *                   SEE 'COMMON RETURN CODES' BELOW             * DPWS003 
00100 *                   DP003-MCL-MUST-BE-NUMERIC                   * DPWS003 
00101 *                   DP003-MCL-NOT-FOUND                         * DPWS003 
00102 *                   DP003-MCL-DB2-ERROR          DP003-SQLCA    * DPWS003 
00103 *                   DP003-MCL-MAX-RETRIES        DP003-SQLCA    * DPWS003 
00104 *               DP003-RETURN-MSG                                * DPWS003 
00105 *---------------------------------------------------------------* DPWS003 
00106 *    FUNCTION:  GET DEPARTMENT DESCRIPTION                      * DPWS003 
00107 *       INPUT:  SEE 'COMMON INPUT FIELDS' BELOW                 * DPWS003 
00108 *               SET DP003-MCL-LOOKUP TO TRUE.                   * DPWS003 
00109 *      OUTPUT:  DP003-DPT-DESCRIPTION                           * DPWS003 
00110 *               DP003-RETURN-CODE:                              * DPWS003 
00111 *                   SEE 'COMMON RETURN CODES' BELOW             * DPWS003 
00112 *                   DP003-DPT-NOT-FOUND                         * DPWS003 
00113 *                   DP003-DPT-DB2-ERROR          DP003-SQLCA    * DPWS003 
00114 *                   DP003-DPT-MAX-RETRIES        DP003-SQLCA    * DPWS003 
00115 *               DP003-RETURN-MSG                                * DPWS003 
00116 *---------------------------------------------------------------* DPWS003 
00117 *    FUNCTION:  GET ALL DEPARTMENTS FOR A BUYER                 * DPWS003 
00118 *       INPUT:  SEE 'COMMON INPUT FIELDS' BELOW                 * DPWS003 
00119 *               SET DP003-DPT-SEARCH TO TRUE.                   * DPWS003 
00120 *      OUTPUT:  DP003-BYR-NMBR                                  * DPWS003 
00121 *               DP003-DPT-COUNT                                 * DPWS003 
00122 *               DP003-DPT-TABLE                                 * DPWS003 
00123 *               DP003-RETURN-CODE:                              * DPWS003 
00124 *                   SEE 'COMMON RETURN CODES' BELOW             * DPWS003 
00125 *                   DP003-CSR-OPEN-ERROR         DP003-SQLCA    * DPWS003 
00126 *                   DP003-DPT-TABLE-OVERFLOW                    * DPWS003 
00127 *                   DP003-CSR-FETCH-ERROR        DP003-SQLCA    * DPWS003 
00128 *                   DP003-CSR-CLOSE-ERROR        DP003-SQLCA    * DPWS003 
00129 *               DP003-RETURN-MSG                                * DPWS003 
00130 *---------------------------------------------------------------* DPWS003 
00131 *    FUNCTION:  GET BUYER NUMBER (NUMBER ONLY)                  * DPWS003 
00132 *       INPUT:  SEE 'COMMON INPUT FIELDS' BELOW                 * DPWS003 
00133 *               SET DP003-BYR-LOOKUP-NMBR TO TRUE.              * DPWS003 
00134 *      OUTPUT:  DP003-BYR-NMBR                                  * DPWS003 
00135 *               DP003-RETURN-CODE:                              * DPWS003 
00136 *                   SEE 'COMMON RETURN CODES' BELOW             * DPWS003 
00137 *                   DP003-BYR-NMBR-NOT-FOUND                    * DPWS003 
00138 *                   DP003-BYR-NMBR-DB2-ERROR     DP003-SQLCA    * DPWS003 
00139 *                   DP003-BYR-NMBR-MAX-RETRIES   DP003-SQLCA    * DPWS003 
00140 *               DP003-RETURN-MSG                                * DPWS003 
00141 *---------------------------------------------------------------* DPWS003 
00142 *    FUNCTION:  GET BUYER NAME (NAME AND NUMBER)                * DPWS003 
00143 *       INPUT:  SEE 'COMMON INPUT FIELDS' BELOW                 * DPWS003 
00144 *               SET DP003-BYR-LOOKUP-NAME TO TRUE.              * DPWS003 
00145 *               SET DP003-BYR-NAME-IND    TO DESIRED VALUE.     * DPWS003 
00146 *      OUTPUT:  DP003-BYR-NMBR                                  * DPWS003 
00147 *               DP003-BYR-NAME                                  * DPWS003 
00148 *               DP003-RETURN-CODE:                              * DPWS003 
00149 *                   SEE 'COMMON RETURN CODES' BELOW             * DPWS003 
00150 *                   DP003-BYR-NAME-NOT-FOUND                    * DPWS003 
00151 *                   DP003-BYR-NAME-DB2-ERROR     DP003-SQLCA    * DPWS003 
00152 *                   DP003-BYR-NAME-MAX-RETRIES   DP003-SQLCA    * DPWS003 
00153 *               DP003-RETURN-MSG                                * DPWS003 
00154 *---------------------------------------------------------------* DPWS003 
00155 *    FUNCTION:  GET DMM NUMBER (NUMBER ONLY)                    * DPWS003 
00156 *       INPUT:  SEE 'COMMON INPUT FIELDS' BELOW                 * DPWS003 
00157 *               SET DP003-DMM-LOOKUP-NMBR TO TRUE.              * DPWS003 
00158 *      OUTPUT:  DP003-DMM-NMBR                                  * DPWS003 
00159 *               DP003-RETURN-CODE:                              * DPWS003 
00160 *                   SEE 'COMMON RETURN CODES' BELOW             * DPWS003 
00161 *                   DP003-DMM-NMBR-NOT-FOUND                    * DPWS003 
00162 *                   DP003-DMM-NMBR-DB2-ERROR     DP003-SQLCA    * DPWS003 
00163 *                   DP003-DMM-NMBR-MAX-RETRIES   DP003-SQLCA    * DPWS003 
00164 *               DP003-RETURN-MSG                                * DPWS003 
00165 *---------------------------------------------------------------* DPWS003 
00166 *    FUNCTION:  GET DMM NAME (NAME AND NUMBER)                  * DPWS003 
00167 *       INPUT:  SEE 'COMMON INPUT FIELDS' BELOW                 * DPWS003 
00168 *               SET DP003-DMM-LOOKUP-NAME TO TRUE.              * DPWS003 
00169 *               SET DP003-DMM-NAME-IND    TO DESIRED VALUE.     * DPWS003 
00170 *      OUTPUT:  DP003-DMM-NMBR                                  * DPWS003 
00171 *               DP003-DMM-NAME                                  * DPWS003 
00172 *               DP003-RETURN-CODE:                              * DPWS003 
00173 *                   SEE 'COMMON RETURN CODES' BELOW             * DPWS003 
00174 *                   DP003-DMM-NAME-NOT-FOUND                    * DPWS003 
00175 *                   DP003-DMM-NAME-DB2-ERROR     DP003-SQLCA    * DPWS003 
00176 *                   DP003-DMM-NAME-MAX-RETRIES   DP003-SQLCA    * DPWS003 
00177 *               DP003-RETURN-MSG                                * DPWS003 
00178 *---------------------------------------------------------------* DPWS003 
00179 *    FUNCTION:  GET GMM NUMBER (NUMBER ONLY)                    * DPWS003 
00180 *       INPUT:  SEE 'COMMON INPUT FIELDS' BELOW                 * DPWS003 
00181 *               SET DP003-GMM-LOOKUP-NMBR TO TRUE.              * DPWS003 
00182 *      OUTPUT:  DP003-GMM-NMBR                                  * DPWS003 
00183 *               DP003-RETURN-CODE:                              * DPWS003 
00184 *                   SEE 'COMMON RETURN CODES' BELOW             * DPWS003 
00185 *                   DP003-GMM-NMBR-NOT-FOUND                    * DPWS003 
00186 *                   DP003-GMM-NMBR-DB2-ERROR     DP003-SQLCA    * DPWS003 
00187 *                   DP003-GMM-NMBR-MAX-RETRIES   DP003-SQLCA    * DPWS003 
00188 *               DP003-RETURN-MSG                                * DPWS003 
00189 *---------------------------------------------------------------* DPWS003 
00190 *    FUNCTION:  GET GMM NAME (NAME AND NUMBER)                  * DPWS003 
00191 *       INPUT:  SEE 'COMMON INPUT FIELDS' BELOW                 * DPWS003 
00192 *               SET DP003-GMM-LOOKUP-NAME TO TRUE.              * DPWS003 
00193 *               SET DP003-GMM-NAME-IND    TO DESIRED VALUE.     * DPWS003 
00194 *      OUTPUT:  DP003-GMM-NMBR                                  * DPWS003 
00195 *               DP003-GMM-NAME                                  * DPWS003 
00196 *               DP003-RETURN-CODE:                              * DPWS003 
00197 *                   SEE 'COMMON RETURN CODES' BELOW             * DPWS003 
00198 *                   DP003-GMM-NAME-NOT-FOUND                    * DPWS003 
00199 *                   DP003-GMM-NAME-DB2-ERROR     DP003-SQLCA    * DPWS003 
00200 *                   DP003-GMM-NAME-MAX-RETRIES   DP003-SQLCA    * DPWS003 
00201 *               DP003-RETURN-MSG                                * DPWS003 
00202 *---------------------------------------------------------------* DPWS003 
00203 *    FUNCTION:  GET ANL NAME                                    * DPWS003 
00204 *       INPUT:  SEE 'COMMON INPUT FIELDS' BELOW                 * DPWS003 
00205 *               SET DP003-ANL-LOOKUP-NAME TO TRUE.              * DPWS003 
00206 *               SET DP003-ANL-NAME-IND    TO DESIRED VALUE.     * DPWS003 
00207 *      OUTPUT:  DP003-ANL-NAME-TABLE (UP TO 19 NAMES)           * DPWS003 
00208 *               DP003-ANL-NAME-COUNT                            * DPWS003 
00209 *               DP003-RETURN-CODE:                              * DPWS003 
00210 *                   SEE 'COMMON RETURN CODES' BELOW             * DPWS003 
00211 *                   DP003-ANL-NAME-NOT-FOUND                    * DPWS003 
00212 *                   DP003-ANL-TABLE-OVERFLOW                    * DPWS003 
00213 *                   DP003-ANL-NAME-DB2-ERROR     DP003-SQLCA    * DPWS003 
00214 *                   DP003-ANL-NAME-MAX-RETRIES   DP003-SQLCA    * DPWS003 
00215 *               DP003-RETURN-MSG                                * DPWS003 
00216 *---------------------------------------------------------------* DPWS003 
00217 *    FUNCTION:  GET ANM NAME                                    * DPWS003 
00218 *       INPUT:  SEE 'COMMON INPUT FIELDS' BELOW                 * DPWS003 
00219 *               SET DP003-ANM-LOOKUP-NAME TO TRUE.              * DPWS003 
00220 *               SET DP003-ANM-NAME-IND    TO DESIRED VALUE.     * DPWS003 
00221 *      OUTPUT:  DP003-ANM-NAME                                  * DPWS003 
00222 *               DP003-RETURN-CODE:                              * DPWS003 
00223 *                   SEE 'COMMON RETURN CODES' BELOW             * DPWS003 
00224 *                   DP003-ANM-NAME-NOT-FOUND                    * DPWS003 
00225 *                   DP003-ANM-NAME-DB2-ERROR     DP003-SQLCA    * DPWS003 
00226 *                   DP003-ANM-NAME-MAX-RETRIES   DP003-SQLCA    * DPWS003 
00227 *               DP003-RETURN-MSG                                * DPWS003 
00228 *---------------------------------------------------------------* DPWS003 
00229 *                                                               * DPWS003 
00230 *                                                               * DPWS003 
00231 *    COMMON INPUT FIELDS                                        * DPWS003 
00232 *    -------------------                                        * DPWS003 
00233 *    THE FOLLOWING FIELDS ARE REQUIRED FOR EVERY USAGE OF THIS  * DPWS003 
00234 *    ROUTINE:                                                   * DPWS003 
00235 *        INITIALIZE         DP003-MBS-SUBROUTINE-FIELDS.        * DPWS003 
00236 *        MOVE 'XX'       TO DP003-SYSTEM-CODE.                  * DPWS003 
00237 *        MOVE 'XX'       TO DP003-OP-CO-NMBR.                   * DPWS003 
00238 *        MOVE 'YYMMDD'   TO DP003-CURRENT-DATE.                 * DPWS003 
00239 *        MOVE 'YYMMDD'   TO DP003-EFF-DATE.                     * DPWS003 
00240 *        MOVE 'XXX'      TO DP003-DPT-INPUT.                    * DPWS003 
00241 *                                                               * DPWS003 
00242 *    EVERY FUNCTION USES THE DEPARTMENT NUMBER AS INPUT.        * DPWS003 
00243 *                                                               * DPWS003 
00244 *    DP003-CURRENT-DATE SHOULD BE FILLE IN WITH THE CURRENT     * DPWS003 
00245 *    DATE.  THIS IS NEED FOR THE DATE SUBROUTINE.               * DPWS003 
00246 *                                                               * DPWS003 
00247 *    DP003-EFF-DATE SHOULD BE FILLED IN WITH THE DATE THAT THE  * DPWS003 
00248 *    PROGRAM IS REQUESTING THE INFORMATION FOR.  THIS COULD BE  * DPWS003 
00249 *    DIFFERENT THAN THE CURRENT DATE.  ONE PROGRAM MAY WANT TO  * DPWS003 
00250 *    KNOW THE BUYER FOR DEPARTMENT 055 FOR THE CURRENT DATE.    * DPWS003 
00251 *    IT COULD BE JOE SCHMO.  ANOTHER PROGRAM MAY NEED TO KNOW   * DPWS003 
00252 *    THE BUYER FOR THE SAME DEPARTMENT, BUT THREE WEEKS INTO    * DPWS003 
00253 *    THE FUTURE.  BY THEN THE BUYER COULD BE JANE DOE.          * DPWS003 
00254 *                                                               * DPWS003 
00255 *                                                               * DPWS003 
00256 *    COMMON RETURN CODES                                        * DPWS003 
00257 *    -------------------                                        * DPWS003 
00258 *    THE FOLLOWING IS A LIST OF THE COMMON RETURN CODES THAT    * DPWS003 
00259 *    CAN BE RETURNED FOR EVERY CALL TO THE SUBROUTINE:          * DPWS003 
00260 *                                                               * DPWS003 
00261 *        RETURN CODE               | MEANING                    * DPWS003 
00262 *        -----------               | -------                    * DPWS003 
00263 *                                  |                            * DPWS003 
00264 *        DP003-SUCCESSFUL          | ALL THE INFORMATION        * DPWS003 
00265 *                                  | REQUESTED BY THE CALLING   * DPWS003 
00266 *                                  | PROGRAM IS RETURNED.       * DPWS003 
00267 *        --------------------------|--------------------------- * DPWS003 
00268 *        DP003-NO-INPUT-SELECTION  | IF ANY OF THESE RETURN     * DPWS003 
00269 *        DP003-SYSTEM-CODE-ERROR   | CODES APPEAR, NONE OF THE  * DPWS003 
00270 *        DP003-OP-CO-ERROR         | INFORMATION REQUESTED IS   * DPWS003 
00271 *        DP003-CURRENT-DATE-ERROR  | RETURNED.  AS A GENERAL    * DPWS003 
00272 *        DP003-EFF-DATE-ERROR      | RULE, THESE COMMON ERRORS  * DPWS003 
00273 *        DP003-DPT-MUST-BE-NUMERIC | ARE MORE FOR TESTING THAN  * DPWS003 
00274 *                                  | PRODUCTION AND WOULDN'T BE * DPWS003 
00275 *                                  | NECESSARY TO SPECIFICALLY  * DPWS003 
00276 *                                  | CHECK FOR.  IT WOULD BE AN * DPWS003 
00277 *                                  | APPLICATION DECISION TO    * DPWS003 
00278 *                                  | CONTINUE OR ABEND.         * DPWS003 
00279 *                                                               * DPWS003 
00280 *                                                               * DPWS003 
00281 *    SPECIFIC RETURN CODES                                      * DPWS003 
00282 *    ---------------------                                      * DPWS003 
00283 *    THERE ARE SPECIFIC RETURN CODES FOR EACH INPUT FUNCTION    * DPWS003 
00284 *    BUT ALMOST ALL OF THE FUNCTIONS USE THE SAME PATTERN.      * DPWS003 
00285 *    THE PATTERN IS:                                            * DPWS003 
00286 *        NOT FOUND                                              * DPWS003 
00287 *        DB2 ERROR                                              * DPWS003 
00288 *        MAX RETRIES                                            * DPWS003 
00289 *    FOR THE 'DB2 ERROR' AND THE 'MAX RETRIES' ERRORS (AND      * DPWS003 
00290 *    THE ERRORS INVOLVING THE USE OF A CURSOR), THE SUBROUTINE  * DPWS003 
00291 *    WILL PASS BACK THE ENTIRE SQLCA.  THE CALLING PROGRAM CAN  * DPWS003 
00292 *    THEN MOVE THE RETURNED SQLCA TO ITS OWN SQLCA AND THEN     * DPWS003 
00293 *    CALL THE DB2 ERROR ROUTINE TO FORMAT A MESSAGE USING THE   * DPWS003 
00294 *    SQLCODE.                                                   * DPWS003 
00295 *                                                               * DPWS003 
00296 *    FOR THE 'NOT FOUND' ERRORS AND OTHER FUNCTION-SPECIFIC     * DPWS003 
00297 *    ERRORS, THE SUBROUTINE WILL NOT PASS BACK THE SQLCA.       * DPWS003 
00298 *    IT WILL BE INCUMBENT ON THE PROGRAMMER OF THE CALLING      * DPWS003 
00299 *    PROGRAM TO CHOOSE TO CONTINUE OR ABEND.  IF THE CHOICE     * DPWS003 
00300 *    IS TO ABEND, THE FOLLOWING FIELDS SHOULD BE DISPLAYED      * DPWS003 
00301 *    AT A MINIMUM:                                              * DPWS003 
00302 *        DP003-INPUT-FIELDS -- SHOWS INPUT TO SUBROUTINE        * DPWS003 
00303 *        DP003-RETURN-DATA  -- THIS GROUP FIELD CONTAINS:       * DPWS003 
00304 *                                  DP003-RETURN-CODE            * DPWS003 
00305 *                                  DP003-RETURN-MSG             * DPWS003 
00306 *                                                               * DPWS003 
00307 ***************************************************************** DPWS003 
00308  01  DP003-MBS-SUBROUTINE-FIELDS.                                 DPWS003 
00309      05  DP003-INPUT-FIELDS.                                      DPWS003 
00310          10  DP003-SCL-LOOKUP-SW     PIC  X(01).                  DPWS003 
00311              88  DP003-SCL-LOOKUP                  VALUE 'Y'.     DPWS003 
00312          10  DP003-MCL-LOOKUP-SW     PIC  X(01).                  DPWS003 
00313              88  DP003-MCL-LOOKUP                  VALUE 'Y'.     DPWS003 
00314          10  DP003-DPT-LOOKUP-SW     PIC  X(01).                  DPWS003 
00315              88  DP003-DPT-LOOKUP                  VALUE 'Y'.     DPWS003 
00316          10  DP003-DPT-SEARCH-SW     PIC  X(01).                  DPWS003 
00317              88  DP003-DPT-SEARCH                  VALUE 'Y'.     DPWS003 
00318          10  DP003-BYR-LOOKUP-IND    PIC  X(01).                  DPWS003 
00319              88  DP003-BYR-LOOKUP-NMBR             VALUE '#'.     DPWS003 
00320              88  DP003-BYR-LOOKUP-NAME             VALUE 'N'.     DPWS003 
00321          10  DP003-DMM-LOOKUP-IND    PIC  X(01).                  DPWS003 
00322              88  DP003-DMM-LOOKUP-NMBR             VALUE '#'.     DPWS003 
00323              88  DP003-DMM-LOOKUP-NAME             VALUE 'N'.     DPWS003 
00324          10  DP003-GMM-LOOKUP-IND    PIC  X(01).                  DPWS003 
00325              88  DP003-GMM-LOOKUP-NMBR             VALUE '#'.     DPWS003 
00326              88  DP003-GMM-LOOKUP-NAME             VALUE 'N'.     DPWS003 
00327          10  DP003-ANL-LOOKUP-SW     PIC  X(01).                  DPWS003 
00328              88  DP003-ANL-LOOKUP-NAME             VALUE 'Y'.     DPWS003 
00329          10  DP003-ANM-LOOKUP-SW     PIC  X(01).                  DPWS003 
00330              88  DP003-ANM-LOOKUP-NAME             VALUE 'Y'.     DPWS003 
00331 *                                                                 DPWS003 
00332          10  DP003-BYR-NAME-IND      PIC  X(01).                  DPWS003 
00333              88  DP003-BYR-NAME-DEFAULT            VALUE 'F'.     DPWS003 
00334              88  DP003-BYR-NAME-FIRST-LAST         VALUE 'F'.     DPWS003 
00335              88  DP003-BYR-NAME-LAST-FIRST         VALUE 'L'.     DPWS003 
00336              88  DP003-BYR-NAME-LAST-ONLY          VALUE 'O'.     DPWS003 
00337          10  DP003-DMM-NAME-IND      PIC  X(01).                  DPWS003 
00338              88  DP003-DMM-NAME-DEFAULT            VALUE 'F'.     DPWS003 
00339              88  DP003-DMM-NAME-FIRST-LAST         VALUE 'F'.     DPWS003 
00340              88  DP003-DMM-NAME-LAST-FIRST         VALUE 'L'.     DPWS003 
00341              88  DP003-DMM-NAME-LAST-ONLY          VALUE 'O'.     DPWS003 
00342          10  DP003-GMM-NAME-IND      PIC  X(01).                  DPWS003 
00343              88  DP003-GMM-NAME-DEFAULT            VALUE 'F'.     DPWS003 
00344              88  DP003-GMM-NAME-FIRST-LAST         VALUE 'F'.     DPWS003 
00345              88  DP003-GMM-NAME-LAST-FIRST         VALUE 'L'.     DPWS003 
00346              88  DP003-GMM-NAME-LAST-ONLY          VALUE 'O'.     DPWS003 
00347          10  DP003-ANL-NAME-IND      PIC  X(01).                  DPWS003 
00348              88  DP003-ANL-NAME-DEFAULT            VALUE 'F'.     DPWS003 
00349              88  DP003-ANL-NAME-FIRST-LAST         VALUE 'F'.     DPWS003 
00350              88  DP003-ANL-NAME-LAST-FIRST         VALUE 'L'.     DPWS003 
00351              88  DP003-ANL-NAME-LAST-ONLY          VALUE 'O'.     DPWS003 
00352          10  DP003-ANM-NAME-IND      PIC  X(01).                  DPWS003 
00353              88  DP003-ANM-NAME-DEFAULT            VALUE 'F'.     DPWS003 
00354              88  DP003-ANM-NAME-FIRST-LAST         VALUE 'F'.     DPWS003 
00355              88  DP003-ANM-NAME-LAST-FIRST         VALUE 'L'.     DPWS003 
00356              88  DP003-ANM-NAME-LAST-ONLY          VALUE 'O'.     DPWS003 
00357 *                                                                 DPWS003 
00358          10  DP003-SCL-INPUT         PIC  X(03).                  DPWS003 
00359          10  DP003-MCL-INPUT         PIC  X(02).                  DPWS003 
00360          10  DP003-DPT-INPUT         PIC  X(03).                  DPWS003 
00361 *                                                                 DPWS003 
00362          10  DP003-SYSTEM-CODE       PIC  X(02).                  DPWS003 
00363          10  DP003-OP-CO-NMBR        PIC  X(02).                  DPWS003 
00364          10  DP003-CURRENT-DATE.                                  DPWS003 
00365              15  DP003-CURRENT-DATE-YY                            DPWS003 
00366                                      PIC  X(02).                  DPWS003 
00367              15  DP003-CURRENT-DATE-MM                            DPWS003 
00368                                      PIC  X(02).                  DPWS003 
00369              15  DP003-CURRENT-DATE-DD                            DPWS003 
00370                                      PIC  X(02).                  DPWS003 
00371          10  DP003-EFF-DATE.                                      DPWS003 
00372              15  DP003-EFF-DATE-YY   PIC  X(02).                  DPWS003 
00373              15  DP003-EFF-DATE-MM   PIC  X(02).                  DPWS003 
00374              15  DP003-EFF-DATE-DD   PIC  X(02).                  DPWS003 
00375 *                                                                 DPWS003 
00376          10  FILLER                  PIC  X(25).                  DPWS003 
00377 *-----------------------------------------------------------------DPWS003 
00378      05  DP003-OUTPUT-FIELDS.                                     DPWS003 
00379          10  DP003-SCL-DESCRIPTION   PIC  X(25).                  DPWS003 
00380          10  DP003-MCL-DESCRIPTION   PIC  X(25).                  DPWS003 
00381          10  DP003-DPT-DESCRIPTION   PIC  X(25).                  DPWS003 
00382 *                                                                 DPWS003 
00383          10  DP003-BYR-NMBR          PIC  X(03).                  DPWS003 
00384          10  DP003-BYR-NAME          PIC  X(32).                  DPWS003 
00385 *                                                                 DPWS003 
00386          10  DP003-DMM-NMBR          PIC  X(02).                  DPWS003 
00387          10  DP003-DMM-NAME          PIC  X(32).                  DPWS003 
00388 *                                                                 DPWS003 
00389          10  DP003-GMM-NMBR          PIC  X(02).                  DPWS003 
00390          10  DP003-GMM-NAME          PIC  X(32).                  DPWS003 
00391 *                                                                 DPWS003 
00392          10  DP003-ANL-COUNT         PIC S9(03)    COMP-3.        DPWS003 
00393          10  DP003-ANL-TABLE         PIC  X(608).                 DPWS003 
00394          10  DP003-ANL-TABLE-RDF     REDEFINES DP003-ANL-TABLE.   DPWS003 
00395              15  DP003-ANL-OCC       OCCURS 19 TIMES              DPWS003 
00396                                      INDEXED BY DP003-ANLX.       DPWS003 
00397                  20  DP003-ANL-NAME  PIC  X(32).                  DPWS003 
00398 *                                                                 DPWS003 
00399          10  DP003-ANM-NAME          PIC  X(32).                  DPWS003 
00400 *                                                                 DPWS003 
00401          10  DP003-DPT-COUNT         PIC S9(03)    COMP-3.        DPWS003 
00402          10  DP003-DPT-TABLE         PIC  X(60).                  DPWS003 
00403          10  DP003-DPT-TABLE-RDF     REDEFINES DP003-DPT-TABLE.   DPWS003 
00404              15  DP003-DPT-OCC       OCCURS 20 TIMES              DPWS003 
00405                                      INDEXED BY DP003-DPTX.       DPWS003 
00406                  20  DP003-DPT       PIC  X(03).                  DPWS003 
00407 *                                                                 DPWS003 
00408          10  DP003-RETURN-DATA.                                   DPWS003 
00409              15  DP003-RETURN-CODE   PIC  X(02).                  DPWS003 
00410                  88  DP003-SUCCESSFUL              VALUE '00'.    DPWS003 
00411                  88  DP003-NO-INPUT-SELECTION      VALUE '01'.    DPWS003 
00412                  88  DP003-SYSTEM-CODE-ERROR       VALUE '02'.    DPWS003 
00413                  88  DP003-CURRENT-DATE-ERROR      VALUE '03'.    DPWS003 
00414                  88  DP003-EFF-DATE-ERROR          VALUE '04'.    DPWS003 
00415                  88  DP003-SCL-MUST-BE-NUMERIC     VALUE '05'.    DPWS003 
00416                  88  DP003-SCL-NOT-FOUND           VALUE '06'.    DPWS003 
00417                  88  DP003-SCL-DB2-ERROR           VALUE '07'.    DPWS003 
00418                  88  DP003-SCL-MAX-RETRIES         VALUE '08'.    DPWS003 
00419                  88  DP003-MCL-MUST-BE-NUMERIC     VALUE '09'.    DPWS003 
00420                  88  DP003-MCL-NOT-FOUND           VALUE '10'.    DPWS003 
00421                  88  DP003-MCL-DB2-ERROR           VALUE '11'.    DPWS003 
00422                  88  DP003-MCL-MAX-RETRIES         VALUE '12'.    DPWS003 
00423                  88  DP003-DPT-MUST-BE-NUMERIC     VALUE '13'.    DPWS003 
00424                  88  DP003-DPT-NOT-FOUND           VALUE '14'.    DPWS003 
00425                  88  DP003-DPT-DB2-ERROR           VALUE '15'.    DPWS003 
00426                  88  DP003-DPT-MAX-RETRIES         VALUE '16'.    DPWS003 
00427                  88  DP003-CSR-OPEN-ERROR          VALUE '17'.    DPWS003 
00428                  88  DP003-DPT-TABLE-OVERFLOW      VALUE '18'.    DPWS003 
00429                  88  DP003-CSR-FETCH-ERROR         VALUE '19'.    DPWS003 
00430                  88  DP003-CSR-CLOSE-ERROR         VALUE '20'.    DPWS003 
00431                  88  DP003-BYR-NAME-NOT-FOUND      VALUE '21'.    DPWS003 
00432                  88  DP003-BYR-NAME-DB2-ERROR      VALUE '22'.    DPWS003 
00433                  88  DP003-BYR-NAME-MAX-RETRIES    VALUE '23'.    DPWS003 
00434                  88  DP003-BYR-NMBR-NOT-FOUND      VALUE '24'.    DPWS003 
00435                  88  DP003-BYR-NMBR-DB2-ERROR      VALUE '25'.    DPWS003 
00436                  88  DP003-BYR-NMBR-MAX-RETRIES    VALUE '26'.    DPWS003 
00437                  88  DP003-DMM-NMBR-NOT-FOUND      VALUE '27'.    DPWS003 
00438                  88  DP003-DMM-NMBR-DB2-ERROR      VALUE '28'.    DPWS003 
00439                  88  DP003-DMM-NMBR-MAX-RETRIES    VALUE '29'.    DPWS003 
00440                  88  DP003-DMM-NAME-NOT-FOUND      VALUE '30'.    DPWS003 
00441                  88  DP003-DMM-NAME-DB2-ERROR      VALUE '31'.    DPWS003 
00442                  88  DP003-DMM-NAME-MAX-RETRIES    VALUE '32'.    DPWS003 
00443                  88  DP003-GMM-NMBR-NOT-FOUND      VALUE '33'.    DPWS003 
00444                  88  DP003-GMM-NMBR-DB2-ERROR      VALUE '34'.    DPWS003 
00445                  88  DP003-GMM-NMBR-MAX-RETRIES    VALUE '35'.    DPWS003 
00446                  88  DP003-GMM-NAME-NOT-FOUND      VALUE '36'.    DPWS003 
00447                  88  DP003-GMM-NAME-DB2-ERROR      VALUE '37'.    DPWS003 
00448                  88  DP003-GMM-NAME-MAX-RETRIES    VALUE '38'.    DPWS003 
00449                  88  DP003-OP-CO-ERROR             VALUE '39'.    DPWS003 
00450                  88  DP003-ANL-NAME-NOT-FOUND      VALUE '40'.    DPWS003 
00451                  88  DP003-ANL-TABLE-OVERFLOW      VALUE '41'.    DPWS003 
00452                  88  DP003-ANL-NAME-DB2-ERROR      VALUE '42'.    DPWS003 
00453                  88  DP003-ANL-NAME-MAX-RETRIES    VALUE '43'.    DPWS003 
00454                  88  DP003-ANM-NAME-DB2-ERROR      VALUE '44'.    DPWS003 
00455                  88  DP003-ANM-NAME-MAX-RETRIES    VALUE '45'.    DPWS003 
00456                  88  DP003-ANM-NAME-NOT-FOUND      VALUE '46'.    DPWS003 
00457              15  DP003-RETURN-MSG    PIC  X(40).                  DPWS003 
00458          10  DP003-SQLCA             PIC  X(150).                 DPWS003 
00459 *                                                                 DPWS003 
00460          10  DP003-SQLCODE           PIC  ZZZZZZZZ9-.             DPWS003 
00461          10  FILLER                  PIC  X(90).                  DPWS003 
