000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400                                                                          
000500 PROGRAM-ID.             SAKUT125.                                        
000600 AUTHOR.                 COGNIZANT.                                       
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000800 DATE-WRITTEN.           07/15/2015.                                      
000900 DATE-COMPILED.                                                           
001000                                                                          
001100*****************************************************************         
001200*  THIS PROGRAM EXTRACTS ECOMMERCE AND OFFLINE ASSOCIATE DISCOUNT         
001300*  INFORMATION AND GENERATES A FILE USED TO MAKE ADJUSTMENTS TO           
001400*  THE GENERAL LEDGER. WHEN A KOHL'S CHARGE ASSOC DISCOUNT TRANS          
001500*  TAKES PLACE OFFLINE, THE SALE OR RETURN AMOUNT IS OVERSTATED           
001600*  IN THE GL BUCKETS.  ANY TEPEID (ASSOC DISC) ENTRY WITH A               
001700*  VALUE OF 'S' (SALES HUB GENERATED) IN THE EMP_DISC_SRC_CDE             
001800*  FIELD IS ONE THAT NEEDS TO BE COLLECTED IN THE TOTALS BY               
001900*  STORE, PLACED ON THE OUTPUT FILE, AND ADJUSTED IN THE GL.              
002000*                                                                         
002100*  INPUT:  INP01 - CONTAINS THE START AND END DATES FOR THE               
002200*                  PREVIOUS FISCAL MONTH.                                 
002300*  OUTPUT: OUT01- ASSOCIATE DISCOUNT ADJUSTMENT TOTALS FOR THE            
002400*                  FISCAL MONTH IN QUESTION ORGANIZED BY STORE            
002500*                  WITH A TOTALS LINE                                     
002600*                                                                         
002800*CHANGE HISTORY:                                                          
002900* WR/PBI      DATE        DESCRIPTION OF CHANGES                          
003000* -------     ----------  ----------------------------------------        
003100* CHG0111856  07-15-2015  INITIAL CREATION - COGNIZANT                    
003200******************************************************************        
STCFEE* CHANGE #: CHG0160001  FIND 'STCFEE'                                     
STCFEE* --------------------                                                    
STCFEE* PROCESS TRANSACTIONS WITH THE EMPLOYEE DISCOUNT CALCULATED BY           
STCFEE* THE SALES HUB FOR THE SALES TAX COMPLIANCE PROJECT.                     
STCFEE* THIS PROGRAM WILL NOW NEED TO PICK UP RECORDS WHERE                     
STCFEE* EMP_DISC_SRC_CDE = 'H' IN THE EXTRACT_CURSOR.                           
STCFEE* BREAK OUT THE ADJUSTED TAXES INTO IT'S OWN FIELD ON THE OUTPUT          
STCFEE* FILE.                                                                   
STCFEE* WHO: JIM HUNTER                               DATE:2016-02-17           
TPCDSC******************************************************************        
TPCDSC* CHANGE #: CHG0242874  FIND 'TPCDSC'                                     
TPCDSC* --------------------                                                    
TPCDSC* ADDED AN EXISTS FOR TEPTND TABLE IN THE CURSOR TO LOOK FOR ONLY         
TPCDSC* KOHLS CHARGE TRANSACTIONS.                                              
TPCDSC* WHO: BARB SCHULTZ                             DATE:2020-04-16           
WEEKLY******************************************************************        
WEEKLY* CHANGE #: CHG0292310  FIND 'WEEKLY'                                     
WEEKLY* --------------------                                                    
WEEKLY* BUSINESS NEEDS THIS JOB TO BE RUN WEEKLY INSTEAD OF MONTHLY SO          
WEEKLY* THAT THEY DON'T HAVE PARTIAL NUMBERS UNTIL THE END OF THE MONTH.        
WEEKLY* CHANGE TO USE CTLCD SACC035 CREATED IN SAK135 WHICH CONTAINS THE        
WEEKLY* FIRST AND LAST DAYS OF THE PREVIOUS FISCAL WEEK.                        
WEEKLY* CHANGE THE FORMAT OF THE TEXT IN THE OUTPUT FILE TO REFLECT THE         
WEEKLY* LAST DATE OF THE FISCAL WEEK BEING PROCESSED INSTEAD OF THE             
WEEKLY* FISCAL MONTH.                                                           
WEEKLY* WHO: JIM HUNTER                               DATE:2023-08-18           
003300******************************************************************        
003400                                                                          
003500 ENVIRONMENT DIVISION.                                                    
003600 CONFIGURATION SECTION.                                                   
003700                                                                          
003800 SOURCE-COMPUTER.        IBM-3090.                                        
003900 OBJECT-COMPUTER.        IBM-3090.                                        
004000                                                                          
004100 INPUT-OUTPUT SECTION.                                                    
004200 FILE-CONTROL.                                                            
004300                                                                          
WEEKLY     SELECT FISC-WEEK-CARD-FILE                                           
004500         ASSIGN TO INP01.                                                 
004600                                                                          
004700     SELECT ASSOC-DISC-ADJ-FILE                                           
004800         ASSIGN TO OUT01.                                                 
004900                                                                          
004910******************************************************************        
005000 DATA DIVISION.                                                           
005110******************************************************************        
005120                                                                          
005130 FILE SECTION.                                                            
005200                                                                          
WEEKLY FD  FISC-WEEK-CARD-FILE                                                  
005400     RECORDING MODE IS F                                                  
005500     BLOCK CONTAINS 0 RECORDS                                             
005600     LABEL RECORDS ARE STANDARD.                                          
005700                                                                          
WEEKLY 01  FISC-WEEK-CARD-RECORD            PIC  X(80).                         
005900                                                                          
006000 FD  ASSOC-DISC-ADJ-FILE                                                  
006100     RECORDING MODE IS F                                                  
006200     BLOCK CONTAINS 0  RECORDS                                            
006210     LABEL RECORDS ARE STANDARD.                                          
006300                                                                          
006400 01  ASSOC-DISC-ADJ-RECORD            PIC  X(80).                         
006500                                                                          
006600******************************************************************        
006700 WORKING-STORAGE SECTION.                                                 
006800******************************************************************        
006900 01  FILLER                           PIC  X(50)  VALUE                   
006910                                                                          
007000     ' *** WORKING STORAGE STARTS HERE FOR SAKUT125 *** '.                
007100                                                                          
007110******************************************************************        
007120*          P R O G R A M     A B E N D     C O D E               *        
007130******************************************************************        
007131                                                                          
007140 01  AC-ABEND-CODE                    PIC S9(04) VALUE ZERO               
007150                                                 COMP SYNC.               
007160     88  AC-CONTROL-CARD-ERROR                   VALUE +4003.             
007170     88  AC-DB2-ERROR                            VALUE +4013.             
007180     88  AC-CALENDAR-ROUTINE-ERROR               VALUE +4019.             
007190                                                                          
007200******************************************************************        
WEEKLY* RECORD LAYOUT FOR INPUT DATE CARD (FISCAL WEEK)                         
007400* SAMPLE - 2013-04-072013-03-032013-04-0603                               
007500******************************************************************        
007510                                                                          
007600 01  CC-CONTROL-CARD.                                                     
007700     05  CC-CONTROL-CARD-DISPLAY.                                         
WEEKLY         10  CC-FISC-WEEK-START-CYMD  PIC  X(10) VALUE SPACES.            
008500         10  FILLER                   PIC  X     VALUE SPACE.             
WEEKLY         10  CC-FISC-WEEK-END-CYMD    PIC  X(10) VALUE SPACES.            
008500     05  FILLER                       PIC  X(59) VALUE SPACE.             
008600                                                                          
009100                                                                          
009200******************************************************************        
009300*              P R O G R A M     S W I T C H E S                 *        
009400******************************************************************        
009700                                                                          
009900 01  PS-PROGRAM-SWITCHES.                                                 
010000     05  PS-END-OF-CURSOR-SW          PIC  X(01) VALUE 'N'.               
010100         88  PS-END-OF-CURSOR                    VALUE 'Y'.               
010200     05  PS-END-OF-CARD-FILE-SW       PIC  X(01) VALUE 'N'.               
010300         88  PS-END-OF-CARD-FILE                 VALUE 'Y'.               
010300         88  PS-NOT-END-OF-CARD-FILE             VALUE 'N'.               
010400                                                                          
010410******************************************************************        
010420*              P R O G R A M     C O N S T A N T S               *        
010430******************************************************************        
010500 01  PC-CONSTANTS.                                                        
010600     05  PC-ROW-NOT-FOUND             PIC S9(04) VALUE +100               
010700                                                 COMP SYNC.               
010800     05  PC-CURRENT-PROGRAM-NAME      PIC  X(08) VALUE 'SAKUT125'.        
010900     05  PC-SPACE                     PIC  X(30) VALUE SPACES.            
011000     05  PC-COMMA                     PIC  X(01) VALUE ','.               
011100     05  PC-STORE-TOTLINE             PIC  9(05) VALUE 90700.             
011200     05  PC-GL-STORE-ADJ              PIC  9(05) VALUE 10000.             
011300     05  PC-GL-CODE-STORE             PIC  X(05) VALUE '40000'.           
011400     05  PC-GL-CODE-TOTLINE           PIC  X(05) VALUE '10243'.           
011500     05  PC-REPORT-NAME               PIC  X(25)                          
WEEKLY         VALUE 'MM/DD/YY Emp Discount Exp'.                               
011700     05  FILLER REDEFINES PC-REPORT-NAME.                                 
WEEKLY         10  PC-RPT-MONTH             PIC X(2).                           
WEEKLY         10  FILLER                   PIC X.                              
WEEKLY         10  PC-RPT-DAY               PIC X(2).                           
WEEKLY         10  FILLER                   PIC X.                              
WEEKLY         10  PC-RPT-YEAR              PIC X(2).                           
012000         10  PC-DEFAULT-TEXT          PIC X(17).                          
012100                                                                          
012110******************************************************************        
012120*              P R O G R A M     V A R I A B L E S               *        
012130******************************************************************        
012200 01  PV-PROGRAM-VARIABLES.                                                
012300     05 PV-GL-STR-NBR                 PIC  S9(5) COMP-3                   
012400        VALUE ZEROES.                                                     
012500     05 PV-EPEID-STR-NBR              PIC  S9(4) COMP-3                   
012600        VALUE ZEROES.                                                     
012700     05 PV-EMP-DISC-AMT               PIC  S9(8)V9(5) COMP-3              
012800        VALUE ZEROES.                                                     
012900     05 PV-EMP-DISC-TAX-AMT           PIC  S9(6)V9(5) COMP-3              
013000        VALUE ZEROES.                                                     
013100     05 PV-TOTAL-ADJ-AMT              PIC  S9(8)V9(5) COMP-3              
013200        VALUE ZEROES.                                                     
013300     05 PV-TOTAL-ADJ-AMT-NEG          PIC  S9(8)V9(5) COMP-3              
013400        VALUE ZEROES.                                                     
013500     05 PV-SUM-TOTAL-ADJ-AMT          PIC  S9(8)V9(5) COMP-3              
013600        VALUE ZEROES.                                                     
013700     05 PV-SUM-TMP-ADJ-AMT            PIC  S9(8)V9(5) COMP-3              
013800        VALUE ZEROES.                                                     
013900     05 PV-SUM-TOTAL-ADJ-AMT-NEG      PIC  S9(8)V9(5) COMP-3              
014000        VALUE ZEROES.                                                     
STCFEE     05 PV-TOTAL-ADJ-TAX-NEG          PIC  S9(8)V9(5) COMP-3              
STCFEE        VALUE ZEROES.                                                     
STCFEE     05 PV-SUM-TOT-TAX-ADJ-AMT        PIC  S9(8)V9(5) COMP-3              
STCFEE        VALUE ZEROES.                                                     
           05  PV-WORK-DATE                 PIC  X(10)  VALUE SPACES.           
014100                                                                          
014200 01  PV-MISC-FIELDS.                                                      
014300     05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.             
014400     05  PV-OPERATION-MSG-1           PIC  X(40) VALUE SPACE.             
014500     05  PV-OPERATION-MSG-2           PIC  X(40) VALUE SPACE.             
014600     05  PV-DB2-OPERATION             PIC  X(30) VALUE SPACE.             
014700     05  PV-CURSOR-CNT                PIC  9(09) VALUE ZERO.              
014800                                                                          
007200******************************************************************        
015703*             C O P Y B O O K      L A Y OU T S                  *        
015704******************************************************************        
015900*----------------------------------------------------------               
016000*  WS FIELDS FOR SQLCA MESSAGE ROUTINE                                    
016100*----------------------------------------------------------               
016200     COPY DPWS004.                                                        
                                                                                
015900*----------------------------------------------------------               
016000*  COPYBOOK FOR OUTPUT LAYOUT                                             
016100*----------------------------------------------------------               
016200     COPY SAWS061.                                                        
016300                                                                          
016400*----------------------------------------------------------               
016500*  DB2 COMMUNICATION AREA.                                                
016600*----------------------------------------------------------               
016700     EXEC SQL                                                             
016800          INCLUDE SQLCA                                                   
016900     END-EXEC.                                                            
017000                                                                          
      *--------------------------------------------------------------*          
      * DATE ROUTINE COPY MEMBER                                                
      *--------------------------------------------------------------*          
           COPY DPWS500.                                                        
                                                                                
017100*----------------------------------------------------------               
017200*  DB2 TABLE TEPPART - PARTITION TABLE                                    
017300*----------------------------------------------------------               
017400     EXEC SQL                                                             
017500          INCLUDE TEPPART                                                 
017600     END-EXEC.                                                            
017700                                                                          
017800*----------------------------------------------------------               
017900*  DB2 TABLE TEPTRN - TRANSACTION TABLE                                   
018000*----------------------------------------------------------               
018100     EXEC SQL                                                             
018200          INCLUDE TEPTRN                                                  
018300     END-EXEC.                                                            
018400                                                                          
TPCDSC*----------------------------------------------------------               
TPCDSC*  DB2 TABLE TEPTND - TENDER TABLE                                        
TPCDSC*----------------------------------------------------------               
TPCDSC     EXEC SQL                                                             
TPCDSC          INCLUDE TEPTND                                                  
TPCDSC     END-EXEC.                                                            
TPCDSC                                                                          
018500*----------------------------------------------------------               
018600*  DB2 TABLE TEPITM - ITEM TABLE                                          
018700*----------------------------------------------------------               
018800     EXEC SQL                                                             
018900          INCLUDE TEPITM                                                  
019000     END-EXEC.                                                            
019100                                                                          
019200*----------------------------------------------------------               
019300*  DB2 TABLE TEPEID - EMPLOYEE ITEM DISCOUNT TABLE                        
019400*----------------------------------------------------------               
019500     EXEC SQL                                                             
019600          INCLUDE TEPEID                                                  
019700     END-EXEC.                                                            
019800                                                                          
019900******************************************************************        
020000*  CURSOR TO RETRIEVE THE ASSOCIATE DISCOUNT TOTALS BY STORE FOR          
020100*  THE PREVIOUS FISCAL MONTH                                              
020200******************************************************************        
020300     EXEC SQL                                                             
020400       DECLARE EXTRACT-CURSOR CURSOR FOR                                  
020500           SELECT                                                         
020600                   EID.STR_NBR                                            
020700                  ,SUM(EID.EMP_DISC_AMT)                                  
020800                  ,SUM(ITM.TX_RT_PCT * EID.EMP_DISC_AMT)                  
020900             FROM                                                         
021000                  TEPPART PRT                                             
021100                 ,TEPTRN  TRN                                             
021200                 ,TEPITM  ITM                                             
021300                 ,TEPEID  EID                                             
021400            WHERE                                                         
021500                  PRT.SLS_DTE BETWEEN :CC-FISC-WEEK-START-CYMD            
021600                                  AND :CC-FISC-WEEK-END-CYMD              
021700              AND PRT.DB_STRUC_CDE  = 'O'                                 
021800              AND PRT.STR_GP_CDE    = 'A'                                 
021900              AND TRN.PARTN_NBR     = PRT.PARTN_NBR                       
022000              AND ITM.PARTN_NBR     = TRN.PARTN_NBR                       
022100              AND ITM.STR_NBR       = TRN.STR_NBR                         
022200              AND ITM.RGST_ID       = TRN.RGST_ID                         
022300              AND ITM.TRN_NBR       = TRN.TRN_NBR                         
022400              AND ITM.STRT_TM       = TRN.STRT_TM                         
022500              AND ITM.SLS_CORR_SEQ_NBR = TRN.SLS_CORR_SEQ_NBR             
022600              AND ITM.PARTN_NBR     = EID.PARTN_NBR                       
022700              AND ITM.STR_NBR       = EID.STR_NBR                         
022800              AND ITM.RGST_ID       = EID.RGST_ID                         
022900              AND ITM.TRN_NBR       = EID.TRN_NBR                         
023000              AND ITM.STRT_TM       = EID.STRT_TM                         
023100              AND ITM.SLS_CORR_SEQ_NBR = EID.SLS_CORR_SEQ_NBR             
023200              AND ITM.LNE_ITM_SEQ_NBR  = EID.LNE_ITM_SEQ_NBR              
023300              AND TRN.TRN_STAT_CDE IN ('L','Z','R','V')                   
023400              AND TRN.VOID_ABRT_CDE = 'N'                                 
023500              AND ITM.LIV_RSLU_CDE IN ('+','-')                           
023600              AND EID.EMP_DISC_CTG_CDE = 'K'                              
STCFEE              AND EID.EMP_DISC_SRC_CDE IN ('S','H')                       
TPCDSC              AND EXISTS                                                  
TPCDSC                 (SELECT 1                                                
TPCDSC                  FROM TEPTND  TND                                        
TPCDSC                  WHERE  TRN.PARTN_NBR     = TND.PARTN_NBR                
TPCDSC                     AND TRN.STR_NBR       = TND.STR_NBR                  
TPCDSC                     AND TRN.RGST_ID       = TND.RGST_ID                  
TPCDSC                     AND TRN.TRN_NBR       = TND.TRN_NBR                  
TPCDSC                     AND TRN.STRT_TM       = TND.STRT_TM                  
TPCDSC                     AND TRN.SLS_CORR_SEQ_NBR =                           
TPCDSC                                          TND.SLS_CORR_SEQ_NBR            
TPCDSC                     AND TND.TNDR_TYP_CDE = '04')                         
023800            GROUP BY EID.STR_NBR                                          
023900            WITH UR                                                       
024000     END-EXEC.                                                            
024100*****************************************************************         
024200 PROCEDURE DIVISION.                                                      
024300******************************************************************        
024400* 0000-MAINLINE-PARAGRAPH                                                 
024500******************************************************************        
024600 0000-MAINLINE-PARAGRAPH.                                                 
024900                                                                          
025000     PERFORM 1000-INITIALIZE-PROGRAM.                                     
025100                                                                          
025410     PERFORM 2000-PROCESS-INFORMATION                                     
025500         UNTIL PS-END-OF-CURSOR.                                          
025600                                                                          
025710     PERFORM 2500-EOJ-ROUTINE.                                            
025800                                                                          
026800     STOP RUN.                                                            
026900                                                                          
027000******************************************************************        
027100* 1000-INITIALIZE-PROGRAM                                                 
027200*     OPEN FILES, PROCESS CONTROL CARD.                                   
027300******************************************************************        
027400 1000-INITIALIZE-PROGRAM.                                                 
027500                                                                          
WEEKLY     OPEN INPUT  FISC-WEEK-CARD-FILE                                      
027900          OUTPUT ASSOC-DISC-ADJ-FILE.                                     
028000     INITIALIZE SA061-PL-OUTPUT-RECORD.                                   
028100                                                                          
WEEKLY     READ FISC-WEEK-CARD-FILE INTO CC-CONTROL-CARD                        
028300         AT END                                                           
028400         SET PS-END-OF-CARD-FILE TO TRUE.                                 
                                                                                
           IF  PS-NOT-END-OF-CARD-FILE                                          
WEEKLY         MOVE CC-FISC-WEEK-START-CYMD TO PV-WORK-DATE                     
VDATE          PERFORM 8100-VALIDATE-DATE                                       
                                                                                
WEEKLY         MOVE CC-FISC-WEEK-END-CYMD   TO PV-WORK-DATE                     
VDATE          PERFORM 8100-VALIDATE-DATE                                       
           END-IF.                                                              
                                                                                
028800     IF PS-END-OF-CARD-FILE                                               
028900         MOVE '1000-INITIALIZE-PROGRAM'  TO PV-PARAGRAPH-NAME             
029000         MOVE 'NO CONTROL CARD TO PROCESS' TO PV-OPERATION-MSG-1          
029100         SET AC-CONTROL-CARD-ERROR TO TRUE                                
029200         PERFORM 9999-ABEND                                               
029300     END-IF.                                                              
029300                                                                          
WEEKLY     CLOSE FISC-WEEK-CARD-FILE.                                           
029400                                                                          
029500     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
029600             ' - CONTROL CARD = (' CC-CONTROL-CARD-DISPLAY  ')'.          
029700                                                                          
WEEKLY     MOVE CC-FISC-WEEK-END-CYMD (6:2) TO PC-RPT-MONTH.                    
WEEKLY     MOVE CC-FISC-WEEK-END-CYMD (9:2) TO PC-RPT-DAY.                      
WEEKLY     MOVE CC-FISC-WEEK-END-CYMD (3:2) TO PC-RPT-YEAR.                     
032700                                                                          
032710     PERFORM 3100-OPEN-EXTRACT-CURSOR.                                    
032710     PERFORM 3200-FETCH-CURSOR.                                           
032710                                                                          
036712******************************************************************        
036713* 2000-PROCESS-INFORMATION                                                
036713*     PROCESS INFORMATION                                                 
036714******************************************************************        
036715 2000-PROCESS-INFORMATION.                                                
036716                                                                          
037100     COMPUTE PV-GL-STR-NBR = PV-EPEID-STR-NBR +                           
037200                             PC-GL-STORE-ADJ                              
037300     END-COMPUTE.                                                         
037600     MOVE PV-GL-STR-NBR        TO SA061-PL-OUT-GL-STORE-NBR.              
037700     MOVE PC-GL-CODE-STORE     TO SA061-PL-OUT-GL-CODE                    
037800     COMPUTE PV-TOTAL-ADJ-AMT = PV-EMP-DISC-AMT +                         
037900                                PV-EMP-DISC-TAX-AMT                       
038000     END-COMPUTE.                                                         
038400     COMPUTE PV-TOTAL-ADJ-AMT-NEG = PV-TOTAL-ADJ-AMT * -1                 
038500     END-COMPUTE.                                                         
038800     MOVE PV-TOTAL-ADJ-AMT-NEG TO SA061-PL-OUT-GL-ADJ-AMOUNT.             
038900     MOVE PV-SUM-TOTAL-ADJ-AMT TO PV-SUM-TMP-ADJ-AMT.                     
039300     COMPUTE PV-SUM-TOTAL-ADJ-AMT = PV-SUM-TMP-ADJ-AMT +                  
039400                                    PV-TOTAL-ADJ-AMT-NEG                  
039500     END-COMPUTE.                                                         
039800     MOVE PV-TOTAL-ADJ-AMT-NEG TO SA061-PL-OUT-GL-ADJ-AMOUNT.             
039900     MOVE PC-REPORT-NAME       TO SA061-PL-OUT-GL-ADJ-ENTRY-NAME.         
STCFEE     COMPUTE PV-TOTAL-ADJ-TAX-NEG = PV-EMP-DISC-TAX-AMT  * -1             
STCFEE     END-COMPUTE.                                                         
STCFEE     MOVE PV-TOTAL-ADJ-TAX-NEG TO SA061-PL-OUT-GL-ADJ-TAX-AMT.            
STCFEE     COMPUTE PV-SUM-TOT-TAX-ADJ-AMT = PV-SUM-TOT-TAX-ADJ-AMT              
STCFEE                                    + PV-TOTAL-ADJ-TAX-NEG                
STCFEE     END-COMPUTE.                                                         
040000     ADD 1                     TO PV-CURSOR-CNT                           
040100                                                                          
040200     PERFORM 8000-WRITE-TO-OUTPUT-FILE.                                   
040200     PERFORM 3200-FETCH-CURSOR.                                           
046810******************************************************************        
046820* 2500-EOJ-ROUTINE                                                        
046820*     EOJ-ROUTINE                                                         
046830******************************************************************        
046840 2500-EOJ-ROUTINE.                                                        
046850                                                                          
046870     PERFORM 3300-CLOSE-CURSOR.                                           
046892                                                                          
046904     DISPLAY '****************************************'                   
046905     DISPLAY '*************** SAKUT125 ***************'                   
046906     DISPLAY '****************************************'                   
046907     DISPLAY 'ROWS FETCHED........'                                       
046908         PV-CURSOR-CNT.                                                   
046909     DISPLAY '****************************************'                   
046910     DISPLAY '****************************************'                   
046911     DISPLAY '****************************************'                   
046912                                                                          
046914     CLOSE ASSOC-DISC-ADJ-FILE.                                           
046915                                                                          
032800******************************************************************        
033200* 3100-OPEN-EXTRACT-CURSOR                                                
033200*     OPEN EXTRACT CURSOR                                                 
033300******************************************************************        
033310 3100-OPEN-EXTRACT-CURSOR.                                                
                                                                                
033500     EXEC SQL                                                             
033600          OPEN EXTRACT-CURSOR                                             
033700     END-EXEC.                                                            
033800                                                                          
033900     EVALUATE TRUE                                                        
034000         WHEN SQLCODE = ZERO                                              
034100              CONTINUE                                                    
034200         WHEN SQLWARN0 NOT EQUAL SPACE                                    
034300         WHEN SQLCODE NOT EQUAL ZERO                                      
034400              MOVE '3100-OPEN-EXTRACT-CURSOR'                             
034500                                   TO PV-PARAGRAPH-NAME                   
034600              MOVE 'ERROR ON CURSOR OPEN'                                 
034700                                   TO PV-DB2-OPERATION                    
034800              PERFORM 9100-SQL-ABEND                                      
034900     END-EVALUATE.                                                        
035000                                                                          
035100******************************************************************        
035500* 3200-FETCH-CURSOR                                                       
035500*     FETCH THE CURSOR DATA                                               
035600******************************************************************        
035610 3200-FETCH-CURSOR.                                                       
035620                                                                          
035800     EXEC SQL                                                             
035900          FETCH EXTRACT-CURSOR                                            
036000           INTO :PV-EPEID-STR-NBR                                         
036100               ,:PV-EMP-DISC-AMT                                          
036200               ,:PV-EMP-DISC-TAX-AMT                                      
036300     END-EXEC.                                                            
036400                                                                          
036500     EVALUATE TRUE                                                        
036600         WHEN SQLCODE = ZERO                                              
036700             CONTINUE                                                     
036702         WHEN SQLCODE = PC-ROW-NOT-FOUND                                  
                   SET PS-END-OF-CURSOR  TO TRUE                                
036704         WHEN SQLWARN0 NOT EQUAL SPACE                                    
036705         WHEN SQLCODE NOT EQUAL ZERO                                      
036706             MOVE '3200-FETCH-CURSOR'                                     
036707                                   TO PV-PARAGRAPH-NAME                   
036708             MOVE 'ERROR ON CURSOR FETCH'                                 
036709                                   TO PV-DB2-OPERATION                    
036710             PERFORM 9100-SQL-ABEND                                       
036711     END-EVALUATE.                                                        
           IF PS-END-OF-CURSOR                                                  
               PERFORM 3500-PROCESS-LAST-RECORD                                 
           END-IF.                                                              
046920******************************************************************        
047100* 3300-CLOSE-CURSOR                                                       
047100*     CLOSE CURSOR                                                        
047200******************************************************************        
047300 3300-CLOSE-CURSOR.                                                       
                                                                                
047300     EXEC SQL                                                             
047300      CLOSE EXTRACT-CURSOR                                                
047300     END-EXEC.                                                            
047300                                                                          
046893     EVALUATE TRUE                                                        
046894         WHEN SQLCODE = ZERO                                              
046895              CONTINUE                                                    
046896         WHEN SQLWARN0 NOT EQUAL SPACE                                    
046897         WHEN SQLCODE NOT EQUAL ZERO                                      
046898             MOVE '3300-CLOSE-CURSOR'                                     
046899                                   TO PV-PARAGRAPH-NAME                   
046900             MOVE 'ERROR ON CURSOR CLOSE'                                 
046901                                   TO PV-DB2-OPERATION                    
046902             PERFORM 9100-SQL-ABEND                                       
046903     END-EVALUATE.                                                        
047300                                                                          
040210******************************************************************        
040210******************************************************************        
040220* 3500-PROCESS-LAST-RECORD                                                
040220*     PROCESS LAST RECORD                                                 
040230******************************************************************        
040240 3500-PROCESS-LAST-RECORD.                                                
040250                                                                          
040700     MOVE PC-STORE-TOTLINE     TO SA061-PL-OUT-GL-STORE-NBR.              
040800     MOVE PC-GL-CODE-TOTLINE   TO SA061-PL-OUT-GL-CODE.                   
040900     COMPUTE PV-SUM-TOTAL-ADJ-AMT-NEG =                                   
041000                     PV-SUM-TOTAL-ADJ-AMT * -1                            
041300     END-COMPUTE.                                                         
041400     MOVE PV-SUM-TOTAL-ADJ-AMT-NEG TO SA061-PL-OUT-GL-ADJ-AMOUNT.         
041600     MOVE PC-REPORT-NAME       TO SA061-PL-OUT-GL-ADJ-ENTRY-NAME.         
STCFEE     COMPUTE PV-SUM-TOT-TAX-ADJ-AMT  =                                    
STCFEE                     PV-SUM-TOT-TAX-ADJ-AMT  * -1                         
STCFEE     END-COMPUTE.                                                         
STCFEE     MOVE PV-SUM-TOT-TAX-ADJ-AMT TO SA061-PL-OUT-GL-ADJ-TAX-AMT.          
041700     ADD 1 TO PV-CURSOR-CNT                                               
041800                                                                          
041900     PERFORM 8000-WRITE-TO-OUTPUT-FILE.                                   
043000                                                                          
045400******************************************************************        
045600* 8000-WRITE-TO-OUTPUT-FILE                                               
045600*    WRITE OUTPUT                                                         
045800******************************************************************        
045810 8000-WRITE-TO-OUTPUT-FILE.                                               
045820                                                                          
045900     MOVE PC-COMMA TO SA061-PL-OUT-COMMA1                                 
046000                      SA061-PL-OUT-COMMA2                                 
046100                      SA061-PL-OUT-COMMA3                                 
046100                      SA061-PL-OUT-COMMA4.                                
046200                                                                          
046300     WRITE ASSOC-DISC-ADJ-RECORD                                          
046400         FROM SA061-PL-OUTPUT-RECORD.                                     
046500                                                                          
046600     INITIALIZE ASSOC-DISC-ADJ-RECORD                                     
046610                SA061-PL-OUTPUT-RECORD.                                   
046620                                                                          
046920******************************************************************        
      *--------------------------------------------------------------*          
      * 8100-VALIDATE-DATE.                                                     
      *     VALIDATE THE INPUT DATE FROM CONTROL CARD                           
      *--------------------------------------------------------------*          
       8100-VALIDATE-DATE.                                                      
                                                                                
           INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.                 
           SET  DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                             
           SET  DPG52-LK-DTE-ISO-FMT       TO TRUE.                             
           MOVE PV-WORK-DATE               TO DPG52-LK-DATE-INPUT.              
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
                                                   DPG54 DPG55 DPG56.           
           EVALUATE TRUE                                                        
               WHEN DPG54-NO-ERROR                                              
                   DISPLAY 'CONTROL CARD  DATE VALID: '                         
                      DPG55-DB2-ISO-DATE-FMT                                    
               WHEN DPG54-SEVERE-ERROR                                          
                   DISPLAY ' '                                                  
                   DISPLAY '** ABEND ** IN SAKUT125 '                           
                   DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'            
                   DISPLAY '** INVALID DATE :'                                  
                            PV-WORK-DATE                                        
                   DISPLAY '** CALLED BY 8100-VALIDATE-DATE '                   
                   DISPLAY '** ERROR MESSAGE = '                                
                                     DPG54-ERROR-MESSAGE                        
                   DISPLAY '** DPG51 = ' DPG51                                  
                   DISPLAY '** DPG52 = ' DPG52                                  
                   DISPLAY '** DPG53 = ' DPG53                                  
                   DISPLAY '** DPG54 = ' DPG54                                  
                   DISPLAY '** DPG55 = ' DPG55                                  
                   DISPLAY '** DPG56 = ' DPG56                                  
                   MOVE '8100-VALIDATE-DATE '                                   
                                        TO PV-PARAGRAPH-NAME                    
                   MOVE 'ERROR VALIDATING DATE '                                
                                        TO PV-OPERATION-MSG-1                   
                   MOVE DPG54-ERROR-MESSAGE  TO PV-OPERATION-MSG-2              
                   MOVE 4019 TO AC-ABEND-CODE                                   
                   CALL 'ILBOABN0' USING AC-ABEND-CODE                          
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
047100* 9100-SQL-ABEND                                                          
047100*     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.        
047200******************************************************************        
047300 9100-SQL-ABEND.                                                          
047400                                                                          
047600     DISPLAY '** ABEND     **'.                                           
047700     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
047800     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
047900     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.                       
048000                                                                          
048100******************************************************************        
048200* COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED             
048300* TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.              
048400******************************************************************        
048500     COPY DPPD004.                                                        
048600                                                                          
048700     SET AC-DB2-ERROR TO TRUE.                                            
048800     CALL 'ILBOABN0' USING AC-ABEND-CODE.                                 
048900                                                                          
049000******************************************************************        
049200* 9999-ABEND                                                              
049200*     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                          
049300******************************************************************        
049400 9999-ABEND.                                                              
049500                                                                          
049700     DISPLAY '** ABEND           **'.                                     
049800     DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
049900     DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
050000     DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.               
050100     DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.               
050200     CALL 'ILBOABN0' USING AC-ABEND-CODE.                                 
