      ******************************************************************00050000
       IDENTIFICATION DIVISION.                                         00060000
      ******************************************************************00070000
                                                                        00080000
       PROGRAM-ID.       XMKCS003.                                      00090004
       AUTHOR.           KOHLS.                                         00100000
       INSTALLATION.     KOHLS.                                         00110000
       DATE-WRITTEN      SEPT, 2002.                                    00120000
       DATE-COMPILED.                                                   00130000
                                                                        00140000
      ******************************************************************00150000
      *  THIS SUBROUTINE WILL PASS ONORDER BACK TO THE CALLING          00151001
      *  PROGRAM.                                                       00152000
      ******************************************************************00210000
                                                                        00440000
      ******************************************************************00450000
       ENVIRONMENT DIVISION.                                            00460000
      ******************************************************************00470000
                                                                        00480000
      ******************************************************************00490000
       DATA DIVISION.                                                   00500000
      ******************************************************************00510000
                                                                        00520000
       WORKING-STORAGE SECTION.                                         00540000
                                                                        00600000
       01  PV-PROGRAM-VARIABLES.                                        00610001
           05  PV-ITEM-NBR                   PIC S9(15)  VALUE ZEROS    00620001
                                                         COMP-3.        00630001
           05  PV-STR-NBR                    PIC S9(04)  VALUE ZEROS    00640001
                                                         COMP.          00650001
           05  PV-ONORD-UNT-QTY              PIC S9(07)  VALUE ZEROS    00651001
                                                         COMP.          00652001
           05  PV-NULL-IND                   PIC S9(04)  COMP.          00653007
                                                                        02660000
      *---------------------------------------------------------------  04020700
      *  DCLGEN LAYOUT AND COBOL DECLARATION FOR ON-ORDER QTY TABLE     04020801
      *---------------------------------------------------------------  04021000
           EXEC SQL                                                     04021200
               INCLUDE TSKSTOO                                          04021301
           END-EXEC.                                                    04021400
                                                                        04021600
      *----------------------------------------------------------------*04021700
      * COMMAREA USED WHEN LINKING FROM A CALLING PROGRAM               04021800
      *----------------------------------------------------------------*04021900
           COPY XMWS003.                                                04022009
                                                                        04023000
      *---------------------------------------------------------------  04023200
      *  COMMUNICATIONS AREA FOR DB2                                    04023300
      *---------------------------------------------------------------  04023400
           EXEC SQL                                                     04023500
               INCLUDE SQLCA                                            04023600
           END-EXEC.                                                    04023700
                                                                        04026000
       LINKAGE SECTION.                                                 04027000
       01  DFHCOMMAREA.                                                 04028000
           05  FILLER                         OCCURS  1 TO 4072 TIMES   04029000
                                            DEPENDING ON EIBCALEN.      04029100
               10  FILLER                   PIC X.                      04029200
                                                                        04029300
                                                                        04029400
      ******************************************************************04780000
       PROCEDURE DIVISION.                                              04790000
      ******************************************************************04800000
                                                                        04810000
           INITIALIZE                      XM003-ITEM-COMMAREA.         04811009
           MOVE DFHCOMMAREA            TO  XM003-ITEM-COMMAREA.         04812009
           SET XM003-SUCCESSFUL        TO TRUE.                         04813009
                                                                        04814000
           PERFORM 1000-SELECT-ONORDER.                                 04861601
                                                                        04861800
           MOVE XM003-ITEM-COMMAREA TO DFHCOMMAREA.                     04863009
                                                                        04864000
           EXEC CICS                                                    04865000
                RETURN                                                  04866000
           END-EXEC.                                                    04867000
                                                                        04868000
      *---------------------------------------------------------------  04921006
      *  SUM ON-ORDER QANTITY FOR A SPECIFIC ITEM/STORE.                04922006
      *---------------------------------------------------------------  04923006
       1000-SELECT-ONORDER.                                             04930002
                                                                        04931006
           MOVE XM003-ITEM-NBR        TO  PV-ITEM-NBR.                  04940009
           IF XM003-KEY-STR-NBR       > ZERO                            04940109
               MOVE XM003-KEY-STR-NBR TO  PV-STR-NBR                    04940209
                                                                        04941000
               EXEC SQL                                                 04942001
                   SELECT SUM(ONORD_UNT_QTY)                            04943001
                   INTO :PV-ONORD-UNT-QTY:PV-NULL-IND                   04944007
                   FROM TSKSTOO                                         04945001
                  WHERE ITM_NBR   = :PV-ITEM-NBR                        04946001
                    AND STR_NBR = :PV-STR-NBR                           04947003
               END-EXEC                                                 04949001
           ELSE                                                         04950001
               EXEC SQL                                                 04960001
                   SELECT SUM(ONORD_UNT_QTY)                            04970001
                   INTO :PV-ONORD-UNT-QTY:PV-NULL-IND                   04980007
                   FROM TSKSTOO                                         04990001
                  WHERE ITM_NBR   = :PV-ITEM-NBR                        05000001
               END-EXEC                                                 05012001
           END-IF.                                                      05013001
                                                                        05020000
           EVALUATE TRUE                                                05021000
               WHEN SQLCODE = ZERO                                      05022000
                   IF (PV-NULL-IND = -1)                                05022307
                      MOVE ZERO TO PV-ONORD-UNT-QTY                     05022407
                   ELSE                                                 05022508
                      MOVE PV-ONORD-UNT-QTY    TO XM003-ONORDER         05022609
                   END-IF                                               05022707
               WHEN SQLCODE = +100                                      05025000
                   MOVE 0                      TO XM003-ONORDER         05025209
                   MOVE SQLCODE                TO XM003-RETURN-SQLCODE  05025309
               WHEN SQLWARN0 NOT = SPACES                               05029500
               WHEN SQLCODE < ZERO                                      05029600
               WHEN SQLCODE > ZERO                                      05029700
                   SET XM003-ONORDER-ERROR-Y TO TRUE                    05030109
                   MOVE SQLCODE                TO XM003-RETURN-SQLCODE  05030209
           END-EVALUATE.                                                05031000
                                                                        05040000
