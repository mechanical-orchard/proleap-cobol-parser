000200 IDENTIFICATION DIVISION.                                                 
000500 PROGRAM-ID.    RVKUT012.                                                 
000600 AUTHOR.        A. PROGRAMMER.                                            
000800 DATE-WRITTEN   AUGUST, 2000.                                             
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
000700*    COBOL 2 WITH SQL                                                     
000700*                                                                         
001200*  THIS PROGRAM EXTRACTS DATA FROM VENDOR ANALYSIS DEPARTMENT             
001300*  LEVEL DB2 TABLE FOR USE IN THE QUARTERLY VENDOR RETURN                 
001400*  AGREEMENT REPORT.                                                      
001800*                                                                         
001900* INPUT:                                                                  
002000*  1. VENDOR ANALYSIS DEPARTMENT TABLE (TVADPT )                          
002800*  2. EXTERNAL ENTERPRISE              (TEXTENT)                          
002900*  3. CONTROL CARD WITH BEGIN AND END PERIODS                             
003100*                                                                         
003200* OUTPUT:                                                                 
003300*  1. VENDOR ANALYSIS EXTRACT FOR SPECIFIED PERIOD (RVRD004)              
003100*                                                                         
003200* CALLED MODULES:                                                         
003300*  1. KOHLS DATE ROUTINE (DPKUT500)                                       
001500******************************************************************        
001600                                                                          
001800 ENVIRONMENT DIVISION.                                                    
002100 CONFIGURATION SECTION.                                                   
002300 SOURCE-COMPUTER.        IBM-3090.                                        
002400 OBJECT-COMPUTER.        IBM-3090.                                        
002500                                                                          
002600 INPUT-OUTPUT SECTION.                                                    
002800 FILE-CONTROL.                                                            
002900                                                                          
003000     SELECT VND-ANALYSIS-EXTRACT                                          
003100         ASSIGN TO OUT01.                                                 
003200                                                                          
003300                                                                          
003500 DATA DIVISION.                                                           
003800 FILE SECTION.                                                            
003900                                                                          
004000 FD  VND-ANALYSIS-EXTRACT                                                 
004100     RECORDING MODE IS F                                                  
004200     LABEL RECORDS ARE STANDARD                                           
004300     BLOCK CONTAINS 0 RECORDS.                                            
004400                                                                          
004500 01  VND-ANALYSIS-EXTRACT-RECORD     PIC  X(130).                         
004600                                                                          
005000 WORKING-STORAGE SECTION.                                                 
005400******************************************************************        
005500*  DB2 FORMATTED MESSAGE AREA                                             
005600******************************************************************        
005700     COPY DPWS004.                                                        
005900*                                                                         
006000******************************************************************        
006100*  VENDOR ANALYSIS EXTRACT RECORD LAYOUT                                  
006200******************************************************************        
006300     COPY RVRD004.                                                        
006500*                                                                         
006600******************************************************************        
006700*  WORK AREA FOR FISCAL CALENDAR ROUTINE.                                 
006800******************************************************************        
006900     COPY DPWS500.                                                        
007100*                                                                         
007200******************************************************************        
007300*  COMMUNICATIONS AREA FOR DB2                                            
007400******************************************************************        
007500     EXEC SQL                                                             
007600          INCLUDE SQLCA                                                   
007700     END-EXEC.                                                            
007900*                                                                         
008000******************************************************************        
008100*  DB2 TABLE DECLARATION - EXTERNAL ENTITY TABLE (TEXTENT)                
008200******************************************************************        
008300     EXEC SQL                                                             
008400          INCLUDE TEXTENT                                                 
008500     END-EXEC.                                                            
008800*                                                                         
008900******************************************************************        
009000*  DB2 TABLE DECLARATION - VENDOR ANALYSIS DEPARTMENT LEVEL TABLE         
009100******************************************************************        
009200     EXEC SQL                                                             
009300          INCLUDE TVADPT                                                  
009400     END-EXEC.                                                            
009500                                                                          
009600 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
009700                                                   COMP SYNC.             
009900 01  CC-CONTROL-CARD.                                                     
010000     05  CC-BEGIN-PERIOD                 PIC  X(6)  VALUE SPACE.          
010200     05  CC-END-PERIOD                   PIC  X(6)  VALUE SPACE.          
010400                                                                          
010600 01  PS-PROGRAM-SWITCHES.                                                 
010700     05  PS-END-OF-CURSOR-SW         PIC  X(01)    VALUE 'N'.             
010800         88  PS-END-OF-CURSOR                      VALUE 'Y'.             
010900         88  PS-NOT-END-OF-CURSOR                  VALUE 'N'.             
011900                                                                          
012100 01  PV-PROGRAM-VARIABLES.                                                
012200     05  PV-BEGIN-DATE                   PIC  X(10)  VALUE SPACE.         
012300     05  PV-END-DATE                     PIC  X(10)  VALUE SPACE.         
012400     05  FETCH-COUNT                     PIC  9(11)  VALUE ZERO.          
012500*                                                                         
012600*  CURSOR DECLARATION FOR VENDOR ANALYSIS DATA FOR THE FISCAL             
012700*  PERIOD END DATES BETWEEN THE PERIODS FROM THE CONTROL CARD.            
012800*                                                                         
013000     EXEC SQL                                                             
013100          DECLARE VADPT_CURSOR CURSOR FOR                                 
013300           SELECT FSCL_MN_NBR                                             
013400                , FSCL_MN_END_DTE                                         
013500                , DEPT_NBR                                                
013600                , DUN_NBR                                                 
013700                , RCPT_NET_COST_AMT                                       
013800                , PADJ_NET_COST_AMT                                       
013900            FROM  TVADPT    A                                             
014000                , TEXTENT   B                                             
014100            WHERE A.ENT_ID          = B.ENT_ID                            
014200              AND A.FSCL_MN_END_DTE BETWEEN :PV-BEGIN-DATE                
014300                                        AND :PV-END-DATE                  
014400              AND B.STATUS_CDE      = 'A'                                 
014500     END-EXEC.                                                            
014600*                                                                         
014700******************************************************************        
014800 LINKAGE SECTION.                                                         
014900******************************************************************        
015000                                                                          
015100******************************************************************        
015200 PROCEDURE DIVISION.                                                      
015300******************************************************************        
015400                                                                          
015500******************************************************************        
015600*  0000-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE PROGRAM            
015700******************************************************************        
015800 0000-PROGRAM-MAINLINE.                                                   
016000     PERFORM 1000-INITIALIZE.                                             
016100                                                                          
016200     PERFORM 2000-CREATE-EXTRACT-FILE                                     
016300         UNTIL PS-END-OF-CURSOR                                           
016400                                                                          
016500     PERFORM 9000-EOJ-ROUTINE.                                            
016600     GOBACK.                                                              
016700                                                                          
016800******************************************************************        
016900*  1000-INITIALIZE -  OPENS ALL THE REPORT FILE, ACCEPTS, EDITS,          
017000*  AND DISPLAYS CONTROL CARD INFORMATION, OPENS AND FETCHES FIRST         
017100*  OCCURANCE OF DB2 CURSOR, PRINTS INITIAL PAGE HEADING TO REPORT         
017200*  FILE.                                                                  
017300******************************************************************        
017400 1000-INITIALIZE.                                                         
017700     INITIALIZE RV004-EXTRACT-RECORD.                                     
017800                                                                          
017900     OPEN OUTPUT VND-ANALYSIS-EXTRACT.                                    
018000                                                                          
018100     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
018200     DISPLAY 'CONTROL CARD = ' CC-CONTROL-CARD.                           
018400                                                                          
018500     PERFORM 1100-VALIDATE-CONTROL-CARD.                                  
018600                                                                          
018700     DISPLAY 'DATES: ' PV-BEGIN-DATE ' ' PV-END-DATE.                     
018800                                                                          
018900     PERFORM 7000-OPEN-VADPT-CURSOR.                                      
019000                                                                          
019100     PERFORM 7200-FETCH-VADPT-CURSOR.                                     
019300                                                                          
019400******************************************************************        
019500* 1100-VALIDATE-CONTROL-CARD - VERIFIES THAT THE DATE RANGE SUP-          
019600* PLIED IS NUMERIC AND WITHIN VALID VALUES.                               
019700******************************************************************        
019800 1100-VALIDATE-CONTROL-CARD.                                              
020000* THE FIRST CALL TO THE CALENDAR ROUTINE IS TO GET THE PERIOD             
020100* START DATE FOR THE BEGIN PERIOD.                                        
020300     INITIALIZE  DPG51 DPG52 DPG53                                        
020400                 DPG54 DPG55 DPG56.                                       
020900                                                                          
021000     SET DPG51-FISCAL-454-CALENDAR  TO TRUE.                              
021100     SET DPG52-LK-DTE-FISC-PRD-CC   TO TRUE.                              
021200     MOVE CC-BEGIN-PERIOD           TO DPG52-LK-DATE-INPUT.               
021300                                                                          
021400     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
021500          USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
021600                                                                          
021700     EVALUATE TRUE                                                        
021800         WHEN DPG54-NO-ERROR                                              
021900             MOVE DPG56-FIRST-FP-DB2-ISO-FMT TO PV-BEGIN-DATE             
022200         WHEN DPG54-DATE-INVALID                                          
022300             DISPLAY '** ABEND **'                                        
022400             DISPLAY '** PROGRAM = RVKUT012 - CALLED MODULE '             
022500                     'DP500-KOHLS-CALENDAR-ROUTINE ***'                   
022600             DISPLAY '** PARAGRAPH = 1100-VALIDATE-DATES. **'             
022700             DISPLAY '** INVALID START DATE: ' CC-BEGIN-PERIOD            
022800             DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '            
022900             DISPLAY DPG54-ERROR-MESSAGE                                  
023000             MOVE +4003 TO ABEND-CODE                                     
023100             CALL 'ILBOABN0' USING ABEND-CODE                             
023300         WHEN OTHER                                                       
023400             DISPLAY DPG54-ERROR-MESSAGE                                  
023600     END-EVALUATE.                                                        
023700                                                                          
023800* THE SECOND CALL TO THE CALENDAR ROUTINE IS TO GET THE PERIOD            
023900* END DATE FOR THE END PERIOD.                                            
024200     INITIALIZE  DPG51 DPG52 DPG53                                        
024300                 DPG54 DPG55 DPG56.                                       
024800                                                                          
024900     SET DPG51-FISCAL-454-CALENDAR  TO TRUE.                              
025000     SET DPG52-LK-DTE-FISC-PRD-CC   TO TRUE.                              
025100     MOVE CC-END-PERIOD             TO DPG52-LK-DATE-INPUT.               
025200                                                                          
025300     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
025400          USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
025500                                                                          
025600     EVALUATE TRUE                                                        
025700         WHEN DPG54-NO-ERROR                                              
025800              MOVE DPG56-LAST-FP-DB2-ISO-FMT TO PV-END-DATE               
026000         WHEN DPG54-DATE-INVALID                                          
026100             DISPLAY '** ABEND **'                                        
026200             DISPLAY '** PROGRAM = RVKUT012 - CALLED MODULE '             
026300                     'DP500-KOHLS-CALENDAR-ROUTINE **'                    
026400             DISPLAY '** PARAGRAPH = 1100-VALIDATE-DATES. **'             
026500             DISPLAY '** INVALID END DATE: ' CC-END-PERIOD                
026600             DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '            
026700             DISPLAY DPG54-ERROR-MESSAGE                                  
026800             MOVE +4003 TO ABEND-CODE                                     
026900             CALL 'ILBOABN0' USING ABEND-CODE                             
027100         WHEN OTHER                                                       
027200             DISPLAY DPG54-ERROR-MESSAGE                                  
027400     END-EVALUATE.                                                        
027600                                                                          
027700******************************************************************        
027800* 2000-CREATE-EXTRACT-FILE                                                
027900* PROCESS:  THIS PARAGRAPH WILL SET UP AND WRITE EXTRACT RECORDS          
028000******************************************************************        
028100 2000-CREATE-EXTRACT-FILE.                                                
028300     INITIALIZE RV004-EXTRACT-RECORD.                                     
028400                                                                          
028700     MOVE VADPT-DEPT-NBR              TO RV004-DEPT-NMBR.                 
028800     MOVE EXTENT-DUN-NBR              TO RV004-VENDOR-DUNNS.              
028900     COMPUTE RV004-MERCH-COST =                                           
029000        VADPT-RCPT-NET-COST-AMT + VADPT-PADJ-NET-COST-AMT.                
029100                                                                          
029200     PERFORM 8000-WRITE-EXTRACT-RECORD.                                   
029300                                                                          
029400     PERFORM 7200-FETCH-VADPT-CURSOR.                                     
029500*                                                                         
029600******************************************************************        
029700* 7000-OPEN-VADPT-CURSOR.                                        *        
029800******************************************************************        
029900 7000-OPEN-VADPT-CURSOR.                                                  
030100     EXEC SQL                                                             
030200          OPEN VADPT_CURSOR                                               
030300     END-EXEC.                                                            
030400                                                                          
030500     EVALUATE TRUE                                                        
030600         WHEN SQLWARN0 NOT EQUAL SPACE                                    
030700         WHEN SQLCODE NOT EQUAL ZERO                                      
030800             DISPLAY '** ABEND **'                                        
030900             DISPLAY '** PROGRAM = RVKUT012 **'                           
031000             DISPLAY '** PARAGRAPH = 7000-OPEN-VADPT-CURSOR **'           
031100             PERFORM 9100-SQL-ABEND                                       
031200     END-EVALUATE.                                                        
031300                                                                          
031500******************************************************************        
031600* 7100-CLOSE-VADPT-CURSOR.                                       *        
031700******************************************************************        
031800 7100-CLOSE-VADPT-CURSOR.                                                 
032100     EXEC SQL                                                             
032200         CLOSE VADPT_CURSOR                                               
032300     END-EXEC.                                                            
032400                                                                          
032500     EVALUATE TRUE                                                        
032600         WHEN SQLWARN0 NOT EQUAL SPACE                                    
032700         WHEN SQLCODE NOT EQUAL ZERO                                      
032800             DISPLAY '** ABEND **'                                        
032900             DISPLAY '** PROGRAM = RVKUT012 **'                           
033000             DISPLAY '** PARAGRAPH = 7100-CLOSE-VADPT-CURSOR**'           
033100             PERFORM 9100-SQL-ABEND                                       
033200     END-EVALUATE.                                                        
033300                                                                          
033500******************************************************************        
033600* 7200-FETCH-VADPT-CURSOR.                                       *        
033700******************************************************************        
033800 7200-FETCH-VADPT-CURSOR.                                                 
034000     EXEC SQL                                                             
034100          FETCH VADPT_CURSOR                                              
034300          INTO    :VADPT-FSCL-MN-NBR                                      
034400                , :VADPT-FSCL-MN-END-DTE                                  
034500                , :VADPT-DEPT-NBR                                         
034600                , :EXTENT-DUN-NBR                                         
034700                , :VADPT-RCPT-NET-COST-AMT                                
034800                , :VADPT-PADJ-NET-COST-AMT                                
034900     END-EXEC.                                                            
035000                                                                          
035100     EVALUATE TRUE                                                        
035200         WHEN SQLCODE = ZERO                                              
035300              ADD +1 TO FETCH-COUNT                                       
035500         WHEN SQLCODE = +100                                              
035600              SET PS-END-OF-CURSOR TO  TRUE                               
035800         WHEN SQLWARN0 NOT EQUAL SPACE                                    
035900         WHEN SQLCODE NOT EQUAL ZERO                                      
036000             DISPLAY '** ABEND **'                                        
036100             DISPLAY '** PROGRAM = RVKUT012 **'                           
036200             DISPLAY '** PARAGRAPH = 7200-FETCH-VADPT-CURSOR**'           
036300             PERFORM 9100-SQL-ABEND                                       
036400     END-EVALUATE.                                                        
036500                                                                          
036800*****************************************************************         
036900*  WRITE THE STYLE SUMMARY EXTRACT RECORD TO AN OUTPUT FILE.    *         
037000*****************************************************************         
037100 8000-WRITE-EXTRACT-RECORD.                                               
037200     WRITE VND-ANALYSIS-EXTRACT-RECORD                                    
037300       FROM RV004-EXTRACT-RECORD.                                         
037400                                                                          
037700******************************************************************        
037800* 9000-EOJ-ROUTINE - PRINTS A TOTAL RECORDS FETCHED, CLOSES               
037900* CURSOR AND THE EXTRACT FILE.                                            
038000******************************************************************        
038100 9000-EOJ-ROUTINE.                                                        
038200                                                                          
038300     DISPLAY 'PROGRAM RVKUT012 EOJ'.                                      
038400     DISPLAY 'VENDOR ANALYSIS ROWS FETCHED = ' FETCH-COUNT.               
038500                                                                          
038510     PERFORM 7100-CLOSE-VADPT-CURSOR.                                     
038600     CLOSE VND-ANALYSIS-EXTRACT.                                        02
038700                                                                          
038800******************************************************************        
038900* 9100-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE              
039000* EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                         
039100******************************************************************        
039200 9100-SQL-ABEND.                                                          
039300     COPY DPPD004.                                                        
039400                                                                          
039500     MOVE +4013 TO ABEND-CODE.                                            
039600     CALL 'ILBOABN0' USING ABEND-CODE.                                    
