       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             VAKUP315.                                        
       AUTHOR.                 PAUL KEBER.                                      
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           08/01/2008.                                      
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *                                                                         
      *    THIS PROGRAM IS A COPY OF INKUP315 WITH THE INVENTORY TABLE          
      *    UPDATES REMOVED.  IT CREATES ADJUSTMENT RECORDS THAT WILL            
      *    BE USED TO BOOK THE FINAL INVENTORY.                                 
      *                                                                         
      * WR/PROJ DATE       PROGAMMER    DESCRIPTION OF CHANGES                  
      * ------- ---------- ------------ --------------------------              
      * WR34517 08/01/2008 PAUL KEBER   INITIAL CREATION                        
W33304* W33304  12-12-2008 HILO CHANGES.PROCESS ONMLY THOSE RECS WITH           
W33304*                    FINCL-INV-STAT-CDE='BK'                              
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SHORT-EXTR-FILE            ASSIGN TO INP01.                   
           SELECT SHORTAGE-ADJUSTMENT-FILE   ASSIGN TO OUT01.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  SHORT-EXTR-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
W33304     RECORD CONTAINS 170 CHARACTERS                                       
           DATA RECORD IS SHORT-REC.                                            
W33304 01  SHORT-REC                   PIC X(170).                              
                                                                                
       FD  SHORTAGE-ADJUSTMENT-FILE                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS ADJUST-REC.                                           
       01  ADJUST-REC                  PIC X(80).                               
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  PV-PROGRAM-VARIBLES.                                                 
           05  PV-PARAGRAPH-NAME               PIC X(30)   VALUE SPACE.         
           05  PV-DB2-OPERATION-ATTEMPTED      PIC X(30)   VALUE SPACE.         
           05  PV-TOTAL-RTL-ADJ-NUM            PIC S9(11)V99                    
                                                           VALUE ZERO.          
           05  PV-TOTAL-RTL-ADJ-DIS            PIC ZZZ,ZZZ,ZZZ,ZZ9.99-          
                                                            VALUE ZERO.         
           05  PV-TOTAL-EST-RTL-ADJ-NUM        PIC S9(11)V99                    
                                                            VALUE ZERO.         
           05  PV-TOTAL-EST-RTL-ADJ-DIS        PIC ZZZ,ZZZ,ZZZ,ZZ9.99-          
                                                            VALUE ZERO.         
           05  PV-RETRY-COUNT                  PIC S9(4)                        
                                               COMP SYNC   VALUE +0.            
           05  PV-STR-NBR                      PIC 9(05).                       
           05  FILLER REDEFINES PV-STR-NBR.                                     
      *        10  FILLER                      PIC X(01).                       
               10  PV-STR-NBR-X                PIC X(05).                       
           05  PV-SQLCODE                      PIC S9(04) VALUE ZERO.           
                                                                                
           05  PV-OPN-FSCL-MTH-END-DTE.                                         
               10  PV-OPN-FSCL-FSCL-YYCC.                                       
                   15 PV-OPN-FSCL-FSCL-CC      PIC X(02)   VALUE SPACE.         
                   15 PV-OPN-FSCL-FSCL-YY      PIC X(02)   VALUE SPACE.         
               10  FILLER                      PIC X(01)   VALUE '-'.           
               10  PV-OPN-FSCL-FSCL-MM         PIC X(02)   VALUE SPACE.         
               10  FILLER                      PIC X(01)   VALUE '-'.           
               10  PV-OPN-FSCL-FSCL-DD         PIC X(02)   VALUE SPACE.         
                                                                                
           05  PV-CURRENT-DATE.                                                 
               10 PV-CURRENT-DATE-CCYY   PIC X(04)   VALUE SPACE.               
               10  FILLER                PIC X(01)   VALUE '-'.                 
               10  PV-CURRENT-DATE-MM    PIC X(02)   VALUE SPACE.               
               10  FILLER                PIC X(01)   VALUE '-'.                 
               10  PV-CURRENT-DATE-DD    PIC X(02)   VALUE SPACE.               
                                                                                
           05  PV-BOOK-ADJ-FSCL-PER      PIC X(02).                     INKUT119
           05  PV-DB2-FIN-BOOK-DATE      PIC X(10).                     INKUT119
           05  PV-SQL-SUB                PIC S9(4)  COMP.               INKUT119
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES            PIC S9(4)  COMP VALUE +3.      INKUT119
           05  PC-VAKUP315               PIC X(08)  VALUE 'VAKUP315'.   INKUT119
           05  PC-5161                   PIC 9(09)  VALUE 5161.         INKUT119
           05  PC-2230                   PIC 9(05)  VALUE 2230.         INKUT119
                                                                                
       01  ABEND-CODE                    PIC S9(04)   VALUE +0.                 
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EXTR-EOF-SW            PIC X(01)     VALUE 'N'.               
               88  PS-SHORT-EXTR-EOF                   VALUE 'Y'.               
W33304     05  PS-FINCL-INV-STAT-CDE     PIC X(02)     VALUE SPACES.            
W33304         88  PS-LOCATION-PROCESS   VALUE 'BK'.                            
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-READ                    PIC 9(09) VALUE ZERO.            
           05  PA-RECS-BYPASSED                PIC 9(09) VALUE ZERO.            
           05  PA-RECS-WRITTEN                 PIC 9(09) VALUE ZERO.            
                                                                                
      ******************************************************************        
      *    INPUT - INVENTORY SHORTAGE EXTRACT RECORD                            
      ******************************************************************        
           COPY INRD014.                                                        
                                                                                
      ******************************************************************        
      *    OUTPUT - MERCH LEDGER ADJUSTMENT RECORD                              
      ******************************************************************        
           COPY MLRD018.                                                        
                                                                                
      *****************************************************************         
      * DB2 DEPT BRAND VENDOR RELATINSHIP TABLE  -                              
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE IMT00017                                                 
           END-EXEC.                                                            
      ******************************************************************        
      * DA BRAND PRORATION PERCENT TABLE FOR MARKDOWN ALLOWANCES                
      ******************************************************************        
           EXEC SQL                                                             
             INCLUDE IMT00020                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * ML UNKNOWN VENDOR/BRAND TABLE                                           
      ******************************************************************        
           EXEC SQL                                                             
             INCLUDE MLT00011                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * ML BRAND/VENDOR/DEPT RELATIONSHIP VIEW                                  
      ******************************************************************        
           EXEC SQL                                                             
             INCLUDE MLV00000                                                   
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      *  ERROR MESSAGE AREA FOR DB2                                             
      ***************************************************************           
           COPY DPWS004.                                                        
                                                                                
      ***************************************************************           
      *    USED FOR DB2 ERRORS.                                                 
      ***************************************************************           
           COPY SQLCA2.                                                         
                                                                                
      ***************************************************************           
      *    LAYOUT USED FOR DB2 M/L OPEN FISCAL MONTH TABLE                      
      ***************************************************************           
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      *    USED FOR DB2 ERRORS.                                                 
      ***************************************************************           
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  BRAND UTILITY BVWS001                                                  
      ******************************************************************        
           COPY BVWS001.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-VAKUP315.                                                           
                                                                                
           OPEN INPUT   SHORT-EXTR-FILE                                         
                OUTPUT  SHORTAGE-ADJUSTMENT-FILE.                               
                                                                                
           PERFORM 1000-INIT-PROCESS.                                           
                                                                                
           PERFORM 4000-READ-SHORT-EXTR.                                        
                                                                                
           IF PS-SHORT-EXTR-EOF                                                 
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 2000-PROCESS-SHORTAGE-RECORDS                            
                   UNTIL PS-SHORT-EXTR-EOF.                                     
                                                                                
           MOVE PV-TOTAL-RTL-ADJ-NUM     TO PV-TOTAL-RTL-ADJ-DIS.               
           MOVE PV-TOTAL-EST-RTL-ADJ-NUM TO PV-TOTAL-EST-RTL-ADJ-DIS.           
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * * '.              
           DISPLAY '* TOTAL RECORDS READ      : ' PA-RECS-READ.                 
           DISPLAY '* TOTAL RECORDS BYPASSED  : ' PA-RECS-BYPASSED.             
           DISPLAY '* TOTAL RECORDS WRITTEN   : ' PA-RECS-WRITTEN.              
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * * '.              
           DISPLAY '* TOTAL ESTIMATED ADJUSTMENTS: '                            
                      PV-TOTAL-EST-RTL-ADJ-DIS.                                 
           DISPLAY '* TOTAL RETAIL ADJUSTMENTS: ' PV-TOTAL-RTL-ADJ-DIS.         
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * * '.              
                                                                                
           CLOSE SHORT-EXTR-FILE                                                
                 SHORTAGE-ADJUSTMENT-FILE.                                      
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       1000-INIT-PROCESS.                                                       
                                                                                
           MOVE FUNCTION CURRENT-DATE (1:4) TO PV-CURRENT-DATE-CCYY.            
           MOVE FUNCTION CURRENT-DATE (5:2) TO PV-CURRENT-DATE-MM.              
           MOVE FUNCTION CURRENT-DATE (7:2) TO PV-CURRENT-DATE-DD.              
           MOVE PV-CURRENT-DATE             TO PV-DB2-FIN-BOOK-DATE.            
                                                                                
           PERFORM 5000-READ-FSCL-MN-TABLE.                                     
                                                                                
           DISPLAY 'MLCNTL-OPEN-FSCL-MN-DTE: '                                  
                    MLCNTL-OPN-FSCL-MN-DTE.                                     
                                                                                
           MOVE MLCNTL-OPN-FSCL-MN-DTE TO PV-OPN-FSCL-MTH-END-DTE.              
                                                                                
      ******************************************************************        
      *  THIS PARAGRAPH WRITES ADJUSTMENT RECORDS THAT WILL BE USED             
      *  IN THE FINAL BOOKING OF THE INVENTORY.  A TRANSACTION CODE             
      *  OF "8" FLAGS THE RECORD AS AN INVENTORY ADJUSTMENT RECORD.             
      *  THE BOOKING MONTH IS THE MONTH THAT THESE ADJUSTMENTS ARE TO           
      *  BE APPLIED.                                                            
      ******************************************************************        
       2000-PROCESS-SHORTAGE-RECORDS.                                           
                                                                                
W33304     MOVE IN014-FINCL-INV-STAT-CDE                                        
W33304                                 TO PS-FINCL-INV-STAT-CDE.                
W33304     IF PS-LOCATION-PROCESS                                               
W33304        MOVE '8'                    TO ML018-ADJ-CDE                      
W33304        MOVE IN014-STR-NBR          TO PV-STR-NBR                         
W33304        MOVE PV-STR-NBR-X           TO ML018-ADJ-STORE-NBR                
W33304        MOVE IN014-DEPT-NBR         TO ML018-ADJ-DEPT-NBR                 
W33304        MOVE IN014-SUB-CL-NBR       TO ML018-ADJ-CLASS-NBR                
W33304        MOVE MLCNTL-OPN-FSCL-MN-NBR TO ML018-ADJ-KOHLS-MM                 
W33304        MOVE PV-OPN-FSCL-FSCL-YY    TO ML018-ADJ-YY                       
W33304        MOVE PV-OPN-FSCL-FSCL-MM    TO ML018-ADJ-MM                       
W33304        MOVE PV-OPN-FSCL-FSCL-DD    TO ML018-ADJ-DD                       
W33304        MOVE IN014-ML-LWST-ID       TO ML018-ADJ-ML-LWST-ID               
W33304        PERFORM  3000-LOOKUP-ENT-ID                                       
W33304        MOVE ZERO                   TO ML018-ADJ-AT-COST-AMT              
W33304        COMPUTE ML018-ADJ-AT-RTL-AMT ROUNDED =                            
W33304            (IN014-SHNK-RTL-AMT     +                                     
W33304             IN014-SHORTAGE-RTL-AMT +                                     
W33304             IN014-ESTIMATED-INV-ADJ-AMT) * -1                            
W33304                                                                          
W33304        ADD ML018-ADJ-AT-RTL-AMT        TO PV-TOTAL-RTL-ADJ-NUM           
W33304        ADD IN014-ESTIMATED-INV-ADJ-AMT                                   
W33304                                      TO PV-TOTAL-EST-RTL-ADJ-NUM         
W33304                                                                          
W33304*    ONLY RECORDS WITH ADJUSTMENTS ARE WRITTEN OUT                        
W33304        IF ML018-ADJ-AT-RTL-AMT = ZERO                                    
W33304            ADD 1 TO PA-RECS-BYPASSED                                     
W33304        ELSE                                                              
W33304            WRITE ADJUST-REC FROM ML018-ADJUSTMENT-RECORD                 
W33304            ADD 1 TO PA-RECS-WRITTEN                                      
W33304        END-IF                                                       INKUT
W33304     ELSE                                                                 
W33304            ADD 1 TO PA-RECS-BYPASSED                                     
W33304     END-IF.                                                              
                                                                                
           PERFORM 4000-READ-SHORT-EXTR.                                        
                                                                                
      ******************************************************************        
      * PROCESS BRAND VENDOR LOOKUP FOR ENT ID FROM MLV_BRND_VND                
      * WITH BV0001 UTILITY SEARCING BY DEPT NR, AND DATALESS KEY.              
      ******************************************************************        
       3000-LOOKUP-ENT-ID.                                                      
                                                                                
            INITIALIZE ML018-ADJ-ENT-ID.                                        
            IF IN014-ML-LWST-ID = 0                                             
                MOVE PC-5161 TO BV001-PV-ENT-ID-IN-9                            
                MOVE PC-2230 TO BV001-PV-BRAND-IN-9                             
                SET BV001-PV-BRND-VND-DPT-LOOKUP TO TRUE                        
                MOVE IN014-DEPT-NBR   TO BV001-PV-DEPT-IN-9                     
                PERFORM BV001-LOOKUP-MAIN                                       
                IF BV001-PV-FOUND-REL-OR-UNK                                    
                    MOVE BV001-PV-ENT-ID-OUT  TO ML018-ADJ-ENT-ID               
                    MOVE BV001-PV-RLTNSHP-ID-OUT                                
                                              TO ML018-ADJ-ML-LWST-ID           
                ELSE                                                            
                    DISPLAY 'ERROR LOOKING UP UNKNOWN DBV'                      
                    DISPLAY 'ML018-ADJ-CDE........', ML018-ADJ-CDE              
                    DISPLAY 'PV-STR-NBR...........', PV-STR-NBR                 
                    DISPLAY 'ML018-ADJ-STORE-NBR..', ML018-ADJ-STORE-NBR        
                    DISPLAY 'ML018-ADJ-DEPT-NBR...', ML018-ADJ-DEPT-NBR         
                    DISPLAY 'ML018-ADJ-CLASS-NBR..', ML018-ADJ-CLASS-NBR        
                    DISPLAY 'ML018-ADJ-KOHLS-MM...', ML018-ADJ-KOHLS-MM         
                    DISPLAY 'ML018-ADJ-YY.........', ML018-ADJ-YY               
                    DISPLAY 'ML018-ADJ-MM.........', ML018-ADJ-MM               
                    DISPLAY 'ML018-ADJ-DD.........', ML018-ADJ-DD               
                   DISPLAY 'ML018-ADJ-ML-LWST-ID.', ML018-ADJ-ML-LWST-ID        
                    PERFORM 9000-BV-LOOKUP-ERROR                                
                END-IF                                                          
            ELSE                                                                
                                                                                
                MOVE IN014-ML-LWST-ID                                           
                       TO BV001-PV-RLTNSHP-ID-IN-9                              
                MOVE IN014-DEPT-NBR   TO BV001-PV-DEPT-IN-9                     
                                                                                
                SET BV001-PV-REL-ID-LOOKUP TO TRUE                              
                PERFORM BV001-LOOKUP-MAIN                                       
                                                                                
                IF BV001-PV-FOUND-REL-OR-UNK                                    
                    MOVE BV001-PV-ENT-ID-OUT  TO ML018-ADJ-ENT-ID               
                ELSE                                                            
                    DISPLAY 'ERROR LOOKING UP ENT ID WITH DATALESS KEY'         
                    DISPLAY 'ML018-ADJ-CDE........', ML018-ADJ-CDE              
                    DISPLAY 'PV-STR-NBR...........', PV-STR-NBR                 
                    DISPLAY 'ML018-ADJ-STORE-NBR..', ML018-ADJ-STORE-NBR        
                    DISPLAY 'ML018-ADJ-DEPT-NBR...', ML018-ADJ-DEPT-NBR         
                    DISPLAY 'ML018-ADJ-CLASS-NBR..', ML018-ADJ-CLASS-NBR        
                    DISPLAY 'ML018-ADJ-KOHLS-MM...', ML018-ADJ-KOHLS-MM         
                    DISPLAY 'ML018-ADJ-YY.........', ML018-ADJ-YY               
                    DISPLAY 'ML018-ADJ-MM.........', ML018-ADJ-MM               
                    DISPLAY 'ML018-ADJ-DD.........', ML018-ADJ-DD               
                   DISPLAY 'ML018-ADJ-ML-LWST-ID.', ML018-ADJ-ML-LWST-ID        
                    PERFORM 9000-BV-LOOKUP-ERROR                                
                END-IF                                                          
            END-IF.                                                             
                                                                                
                                                                                
       4000-READ-SHORT-EXTR.                                                    
                                                                                
           READ SHORT-EXTR-FILE INTO IN014-SHORTAGE-EXTR-REC                    
               AT END                                                           
                   SET PS-SHORT-EXTR-EOF TO TRUE.                               
                                                                                
           IF PS-SHORT-EXTR-EOF                                                 
               CONTINUE                                                         
           ELSE                                                                 
               ADD 1 TO PA-RECS-READ                                            
           END-IF.                                                              
                                                                                
      ***************************************************************           
      *    READ THE OPEN FISCAL MONTH FILE                                      
      ***************************************************************           
       5000-READ-FSCL-MN-TABLE.                                                 
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                    OR (SQLCODE         = +100)                                 
                                                                                
               EXEC SQL                                                         
                    SELECT  OPN_FSCL_MN_DTE                                     
                         ,  OPN_FSCL_MN_NBR                                     
                    INTO   :MLCNTL-OPN-FSCL-MN-DTE                              
                         , :MLCNTL-OPN-FSCL-MN-NBR                              
                    FROM    TMLCNTL                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE        = +000                                   
                   WHEN SQLCODE        = -904                                   
                   WHEN SQLCODE        = -911                                   
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE    '5000-READ-OPEN-FSCL-MN-TABLE'                   
                                                TO PV-PARAGRAPH-NAME            
                       MOVE    'SELECT ROW FROM TMLCNTL'                        
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                       PERFORM 9950-SQL-ERROR                                   
               END-EVALUATE                                                     
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
               DISPLAY '** MAX RETRIES EXCEEDED ON TMLCNTL **'                  
               MOVE '5000-READ-OPEN-FSCL-MN-TABLE' TO PV-PARAGRAPH-NAME         
               MOVE 'SELECT ROW FROM TMLCNTL'                                   
                                          TO PV-DB2-OPERATION-ATTEMPTED         
               PERFORM 9950-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  ABEND CALL FOR ERRORS RETURNED BY BRAND VENDOR LOOKUP UTILITY          
      ******************************************************************        
       9000-BV-LOOKUP-ERROR.                                                    
           DISPLAY 'GOT TO 9000-BV-LOOKUP-ERROR'.                               
           DISPLAY '***************************************************'        
           DISPLAY BV001-PV-RETURN-MSG-1                                        
           DISPLAY '***************************************************'        
           DISPLAY BV001-PV-RETURN-MSG-2                                        
           DISPLAY '***************************************************'        
           IF BV001-PV-SQL-ERROR                                                
              MOVE BV001-PV-SQLCODE-OUT TO PV-SQLCODE                           
              DISPLAY 'SQL CODE = ' PV-SQLCODE                                  
           END-IF.                                                              
           PERFORM 9950-SQL-ERROR.                                              
                                                                                
                                                                                
                                                                                
       9950-SQL-ERROR.                                                          
           MOVE +4013 TO ABEND-CODE.                                            
           DISPLAY '** ABEND **     ** = SQLERROR'.                             
           DISPLAY '** PROGRAM      ** = VAKUP315'.                             
           DISPLAY '** PARAGRAPH    ** = ' PV-PARAGRAPH-NAME.                   
           DISPLAY '** OPERATION    ** = ' PV-DB2-OPERATION-ATTEMPTED.          
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
       EJECT                                                                    
      *****************************************************************         
      *                                                                         
      *****************************************************************         
           COPY BVPD001.                                                        
