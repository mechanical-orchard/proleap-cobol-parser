00001 ******************************************************************07/18/95
00002  IDENTIFICATION DIVISION.                                         DBKUT004
00003 ******************************************************************   LV030
00004                                                                      CL**1
00005  PROGRAM-ID.             DBKUT004.                                   CL**1
00006  AUTHOR.                 BRAD DORN.                                  CL**1
00007  INSTALLATION.           KOHLS.                                      CL**1
00008  DATE-WRITTEN            FEBRUARY 2008.                              CL**1
00009  DATE-COMPILED.                                                      CL**1
00010                                                                      CL**1
00011 ******************************************************************   CL**1
00012 * THIS PROGRAM WILL COMPARE CAPACITIES OF PARTITIONED DATA SETS      CL**1
00012 * AGAINST TWO THRESHOLDS SUPPLIED ON A CONTROL CARD.  THE FIRST      CL**1
00012 * THRESHOLD IS USED TO TRIGGER AN EMAIL ALERT.  THE SECOND           CL**1
00012 * THRESHOLD IS USED TO TRIGGER BOTH AN EMAIL ALERT AND AN SMS        CL**1
00012 * TEXT MESSAGE TO THE DB2 DBA BLACKBERRIES.  THIS SECOND THRESH-     CL**1
00012 * HOLD WILL HAVE A HIGHER VALUE THAN THE FIRST THRESHOLD.            CL**1
00012 *                                                                    CL**1
00012 * DBKUT004 WILL SET THE RETURN-CODE REGISTER TO THE FOLLOWING        CL**1
00012 * VALUES DEPENDING ON WHETHER ANY THRESHOLDS ARE HIT.  THESE         CL**1
00012 * RETURN CODES WILL BE CHECKED BY LATER STEPS IN THE JOB THAT        CL**1
00012 * WILL GENERATE THE APPROPRIATE EMAIL OR SMS TEXT MESSAGE:           CL**1
00014 *                                                                    CL**1
00013 *          RETURN-CODE = 20: SEND EMAIL AND SMS TEXT                 CL**1
00013 *          RETURN-CODE = 30: SEND EMAIL                              CL**1
00014 *                                                                    CL**1
00014 * DBKUT004 WILL CHECK DB2 TABLE DBT_PART_DSET_ALERT_XCPT FOR         CL**1
00014 * ANY EXCEPTIONS TO ALERT PROCESSING.  THESE TYPICALLY WOULD         CL**1
00014 * INVOLVE OBJECTS THAT HAVE STABILIZED AT A SPECIFIC SIZE, BUT       CL**1
00014 * THE SIZE EXCEEDS THE THRESHOLD VALUES.                             CL**1
00014 *                                                                    CL**1
00016 ******************************************************************   CL**1
00020                                                                      CL**1
00021 ******************************************************************   CL**1
00022  ENVIRONMENT DIVISION.                                               CL**1
00023 ******************************************************************   CL**1
00024                                                                      CL**1
00025  CONFIGURATION SECTION.                                              CL**1
00026                                                                      CL**1
00027  SOURCE-COMPUTER.        IBM.                                        CL**1
00028  OBJECT-COMPUTER.        IBM.                                        CL**1
00029                                                                      CL**1
00030  INPUT-OUTPUT SECTION.                                               CL**1
00031                                                                      CL**1
00032  FILE-CONTROL.                                                       CL**1
00033                                                                      CL**1
00034      SELECT LISTCAT-DATA             ASSIGN TO INP01.                CL**3
00034      SELECT TRIGGER-THRESH           ASSIGN TO INP02.                CL**3
00034      SELECT EMAIL-DATA               ASSIGN TO OUT01.                CL**3
00039                                                                      CL**1
00040 ******************************************************************   CL**1
00041  DATA DIVISION.                                                      CL**1
00042 ******************************************************************   CL**1
00043                                                                      CL**1
00044  FILE SECTION.                                                       CL**1
00045                                                                      CL**1
00046  FD  LISTCAT-DATA                                                    CL**3
00047      RECORDING MODE IS F                                             CL**1
00048      LABEL RECORDS ARE STANDARD                                      CL**1
00049      BLOCK CONTAINS 0 RECORDS.                                       CL**1
00050                                                                      CL**1
00051  01  LISTCAT-DATA-REC                PIC  X(56).                     CL**3
00052                                                                      CL**1
00046  FD  TRIGGER-THRESH                                                  CL**3
00047      RECORDING MODE IS F                                             CL**1
00048      LABEL RECORDS ARE STANDARD                                      CL**1
00049      BLOCK CONTAINS 0 RECORDS.                                       CL**1
00050                                                                      CL**1
00051  01  TRIGGER-THRESH-REC              PIC  X(80).                     CL**3
00052                                                                      CL**1
00046  FD  EMAIL-DATA                                                      CL**3
00047      RECORDING MODE IS F                                             CL**1
00049      BLOCK CONTAINS 0 RECORDS.                                       CL**1
00050                                                                      CL**1
00051  01  EMAIL-DATA-REC                  PIC  X(80).                     CL**3
00052                                                                      CL**1
00073                                                                      CL**1
00081 ******************************************************************   CL**1
00082  WORKING-STORAGE SECTION.                                            CL**1
00083 ******************************************************************   CL**1
00084                                                                      CL**1
       01 DP004-DB2-ERROR-FIELDS.                                               
           05  DP004-ASTERISK-LINE       PIC  X(80)  VALUE ALL '*'.             
           05  DP004-MAX-MESSAGE-LINES   PIC S9(04)  VALUE +12                  
                                                     COMP SYNC.                 
           05  DP004-ERROR-LINE-LENGTH   PIC S9(09)  VALUE +75                  
                                                     COMP SYNC.                 
           05  DP004-FORMATTED-MESSAGE.                                         
               10  FILLER                PIC S9(04)  VALUE +900                 
                                                     COMP SYNC.                 
               10  DP004-MESSAGE-LINE    OCCURS 12 TIMES                        
                                         INDEXED BY DP004-MSG-IDX               
                                         PIC  X(75).                            
                                                                                
00117  01  DH1-DETAIL-HEADING-LINE-1.                                      CL**1
00118      05  FILLER                      PIC  X(07)      VALUE SPACES.   CL*26
00119      05  FILLER                      PIC  X(17)      VALUE           CL*12
00120          'PARTITION DATASET'.                                        CL**1
00123      05  FILLER                      PIC  X(26)      VALUE SPACES.   CL*26
00123      05  FILLER                      PIC  X(03)      VALUE           CL*26
00124          'MAX'.                                                      CL**1
00123      05  FILLER                      PIC  X(10)      VALUE SPACES.   CL*26
00125      05  FILLER                      PIC  X(04)      VALUE           CL*26
00126          'USED'.                                                     CL*26
00123      05  FILLER                      PIC  X(03)      VALUE SPACES.   CL*26
00127      05  FILLER                      PIC  X(03)      VALUE           CL*26
00128          'PCT'.                                                      CL*26
00129                                                                      CL**1
00130  01  DH1-DETAIL-HEADING-LINE-2.                                      CL**1
00131      05  FILLER                      PIC  X(50)      VALUE SPACES.   CL*26
00132      05  FILLER                      PIC  X(03)      VALUE           CL*12
00133          'GIG'.                                                      CL**1
00131      05  FILLER                      PIC  X(10)      VALUE SPACES.   CL*26
00132      05  FILLER                      PIC  X(03)      VALUE           CL*12
00133          'GIG'.                                                      CL**1
00131      05  FILLER                      PIC  X(04)      VALUE SPACES.   CL*26
00132      05  FILLER                      PIC  X(06)      VALUE           CL*12
00133          'FILLED'.                                                   CL**1
00150                                                                      CL**1
00151  01  DL-DETAIL-LINE.                                                 CL**1
00153      05  DL-DATASET                  PIC  X(42)      VALUE SPACES.   CL**1
00154      05  FILLER                      PIC  X(02)      VALUE SPACES.   CL*12
00155      05  DL-MAX-GIG                  PIC  ZZZZZZZ99  VALUE ZERO.     CL**1
00156      05  FILLER                      PIC  X(02)      VALUE SPACES.   CL*12
00157      05  DL-USED-GIG                 PIC  ZZZZZZZ99.99                    
                                                           VALUE ZERO.          
00158      05  FILLER                      PIC  X(02)      VALUE SPACES.   CL*26
00159      05  DL-PCT-FULL                 PIC  ZZ9        VALUE ZERO.     CL*26
           05  FILLER                      PIC  X(01)      VALUE '%'.           
00171                                                                      CL**1
00287                                                                      CL**1
00288  01  END-OF-REPORT-LINE.                                             CL*30
00289      05  FILLER                      PIC  X(25)      VALUE SPACE.    CL*30
00290      05  FILLER                      PIC  X(25)      VALUE           CL*30
00291          '***** END OF REPORT *****'.                                CL*30
00293                                                                      CL*30
00294  01  BLANK-LINE                      PIC X(80)       VALUE SPACES.   CL**1
00295                                                                      CL**1
00305                                                                      CL**1
00310  01  FILLER.                                                         CL**1
           05  WS-ABEND-CODE              PIC S9(04) VALUE +0 COMP SYNC.        
00317      05  WS-DUMMY-VALUE             PIC  X(01)    VALUE SPACE.       CL**3
00347      05  WS-GENERATE-EMAIL          PIC  X(01)    VALUE 'N'.         CL**6
00347      05  WS-GENERATE-PAGE           PIC  X(01)    VALUE 'N'.         CL**6
00317      05  WS-LISTCAT-DATA-EOF-SW     PIC  X(01)    VALUE 'N'.         CL**3
00319                                                                      CL**1
00329                                                                      CL**1
00344  01  WS-LISTCAT-DATA-REC.                                            CL**3
00345      05  WS-LISTCAT-DSN            PIC  X(42)      VALUE SPACES.     CL**6
00347      05  WS-LISTCAT-MAX-GIG        PIC  9(09)      COMP  VALUE 0.    CL**6
00347      05  WS-LISTCAT-USED-GIG       PIC  9(09)V99   COMP  VALUE 0.    CL**6
00347      05  WS-LISTCAT-PCT-FULL       PIC  9V99       COMP  VALUE 0.    CL**6
00367                                                                      CL**1
00367                                                                      CL**1
00344  01  WS-TRIGGER-THRESH-REC.                                          CL**3
00345      05  WS-EMAIL-TRIGGER          PIC  999        VALUE 0.          CL**6
           05  FILLER                    PIC  X(01)      VALUE SPACES.          
00345      05  WS-PAGE-TRIGGER           PIC  999        VALUE 0.          CL**6
                                                                                
                                                                                
      *  DBT_PART_DSET_ALERT_XCPT                                               
           EXEC SQL                                                             
               INCLUDE DBT00022                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
00436 ******************************************************************   CL**1
00437  PROCEDURE DIVISION.                                                 CL**1
00438 ******************************************************************   CL**1
00439  0000-PROGRAM-MAINLINE.                                              CL**1
00440                                                                      CL**1
00441      PERFORM 1000-INITIALIZATION.                                    CL**6
00442                                                                      CL**1
00443      PERFORM 1100-READ-INPUT-FILE.                                   CL**6
00444                                                                      CL**1
00454      PERFORM 2000-PROCESS-LISTCAT                                    CL**6
00455         UNTIL WS-LISTCAT-DATA-EOF-SW = 'Y'.                          CL**6
00456                                                                      CL**1
00466      PERFORM 3000-END-OF-JOB.                                        CL**1
00467                                                                      CL**1
00468      GOBACK.                                                         CL**1
00469                                                                      CL**1
00470 ******************************************************************   CL**1
00472 * - OPEN ALL INPUT AND OUTPUT FILES                                  CL**6
00473 * - READ IN THE THRESHOLD VALUES USED TO TRIGGER AN EMAIL OR PAGE    CL**1
00473 * - WRITE INITIAL RECORDS TO THE EMAIL FILE                          CL**1
00476 ******************************************************************   CL**1
00477                                                                      CL**1
00478  1000-INITIALIZATION.                                                CL**1
00479                                                                      CL**1
00480      OPEN INPUT  LISTCAT-DATA                                        CL**6
                       TRIGGER-THRESH.                                          
00482      OPEN OUTPUT EMAIL-DATA.                                         CL**6
00495                                                                      CL**1
00587      READ TRIGGER-THRESH INTO WS-TRIGGER-THRESH-REC                  CL**6
588          AT END                                                             
                MOVE +4013      TO WS-ABEND-CODE                                
                DISPLAY '*****************************************'             
                DISPLAY '*** PROGRAM ERROR - DBKUT004          ***'             
                DISPLAY '***                                   ***'             
                DISPLAY '*** EMPTY TRIGGER THRESHOLD FILE      ***'             
                DISPLAY '*** 1000-INITIALIZATION.              ***'             
                DISPLAY '*****************************************'             
                PERFORM 9999-NONSQL-ABEND.                                      
00589                                                                      CL**6
00770      WRITE EMAIL-DATA-REC                                            CL**1
00771        FROM BLANK-LINE.                                              CL**1
00773                                                                      CL**1
00770      WRITE EMAIL-DATA-REC                                            CL**1
00771        FROM DH1-DETAIL-HEADING-LINE-1.                               CL**1
00773                                                                      CL**1
00774      WRITE EMAIL-DATA-REC                                            CL**1
00775        FROM DH1-DETAIL-HEADING-LINE-2.                               CL**1
00777                                                                      CL**1
00778      WRITE EMAIL-DATA-REC                                            CL**1
00779        FROM BLANK-LINE.                                              CL**1
00783                                                                      CL**1
00548                                                                      CL*17
00580 ******************************************************************   CL**6
00582 * - READ FILE CONTAINING OBJECT CAPACITY INFORMATION                 CL**6
00583 ******************************************************************   CL**6
00584                                                                      CL**6
00585  1100-READ-INPUT-FILE.                                               CL**6
00586                                                                      CL**6
00587      READ LISTCAT-DATA INTO WS-LISTCAT-DATA-REC                      CL**6
00588          AT END MOVE 'Y' TO WS-LISTCAT-DATA-EOF-SW.                  CL**6
00589                                                                      CL**6
00611                                                                      CL**6
00549 ******************************************************************   CL**1
00551 * - FOR EACH INPUT RECORD, CHECK DATASET CAPACITY AGAINST THE        CL*21
00551 *   EMAIL THRESHOLD. PERFORM FURTHER PROCESSING IF THE CAPACITY      CL*21
00551 *   MEETS OR EXCEEDS THE THRESHOLD.                                  CL*21
00555 ******************************************************************   CL**1
 0556                                                                      CL**1
00557  2000-PROCESS-LISTCAT.                                               CL**6
                                                                                
           IF (WS-LISTCAT-PCT-FULL * 100) < WS-EMAIL-TRIGGER                    
               NEXT SENTENCE                                                    
           ELSE                                                                 
             PERFORM 2100-PROCESS-ALERT                                         
           END-IF.                                                              
                                                                           CL**1
           PERFORM 1100-READ-INPUT-FILE.                                   CL**6
                                                                           CL**1
                                                                           CL**1
00549 ******************************************************************   CL**1
00551 *   DO NOT GENERATE AN EMAIL OR PAGE IF THE DATASET AND PERCENT      CL*21
00551 *   FULL ARE FOUND IN THE ALERT EXCEPTION TABLE.  OTHERWISE          CL*21
00551 *   PERFORM LOGIC TO BUILD AN EMAIL AND/OR PAGE.                     CL*21
00555 ******************************************************************   CL**1
 0556                                                                      CL**1
00557  2100-PROCESS-ALERT.                                                 CL**6
                                                                                
           MOVE WS-LISTCAT-DSN      TO DBT00022-DATASET-NAME.                   
           MOVE WS-LISTCAT-PCT-FULL TO DBT00022-PCT-FULL.                       
                                                                                
           EXEC SQL                                                             
               SELECT '1' INTO :WS-DUMMY-VALUE                                  
                 FROM DBT_PART_DSET_ALERT_XCPT                                  
                   WHERE DATASET_NAME = :DBT00022-DATASET-NAME                  
                     AND PCT_FULL     = :DBT00022-PCT-FULL                      
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = ZERO                                                    
             NEXT SENTENCE                                                      
           ELSE                                                                 
             IF SQLCODE = +100                                                  
               PERFORM 2110-GO-AHEAD-WITH-ALERT                                 
             ELSE                                                               
               DISPLAY '******************************************'             
               DISPLAY '*** PROGRAM ERROR - DBKUT004           ***'             
               DISPLAY '***                                    ***'             
               DISPLAY '*** PARAGRAPH 2100-PROCESS-ALERT       ***'             
               DISPLAY '*** BAD SELECT FROM EXCEPTION TABLE    ***'             
               DISPLAY '******************************************'             
               PERFORM 9999-DB2-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
00549 ******************************************************************   CL**1
00551 *  CREATE EMAIL DETAIL RECORDS AND SET APPROPRIATE SWITCHES.         CL*21
00555 ******************************************************************   CL**1
 0556                                                                      CL**1
       2110-GO-AHEAD-WITH-ALERT.                                           CL**6
                                                                                
           MOVE WS-LISTCAT-DSN       TO DL-DATASET.                             
           MOVE WS-LISTCAT-MAX-GIG   TO DL-MAX-GIG.                             
           MOVE WS-LISTCAT-USED-GIG  TO DL-USED-GIG.                            
           COMPUTE DL-PCT-FULL = WS-LISTCAT-PCT-FULL * 100.                     
           WRITE EMAIL-DATA-REC FROM DL-DETAIL-LINE.                            
                                                                                
           IF (WS-LISTCAT-PCT-FULL * 100) >= WS-PAGE-TRIGGER                    
             MOVE 'Y' TO WS-GENERATE-PAGE                                       
           ELSE                                                                 
             MOVE 'Y' TO WS-GENERATE-EMAIL                                      
           END-IF.                                                              
                                                                           CL**1
                                                                                
00741                                                                      CL**1
01055 ******************************************************************   CL**1
01056 * THIS PARAGRAPH PERFORMS THE FOLLOWING:                             CL**1
01057 * - IF A PAGE OR EMAIL WILL BE GENERATED, WRITE FINAL RECORD TO      CL**1
01057 *   THE EMAIL FILE AND SET RETURN CODES AS FOLLOWS:                  CL**1
01057 *   RC = 20 IF PAGE AND EMAIL ARE TO BE GENERATED                    CL**1
01057 *   RC = 30 IF ONLY AN EMAIL IS TO BE GENERATED                      CL**1
01058 ******************************************************************   CL**1
01059                                                                      CL**1
01060  3000-END-OF-JOB.                                                    CL**1
01061                                                                      CL**1
           IF WS-GENERATE-PAGE = 'Y'                                            
               WRITE EMAIL-DATA-REC FROM BLANK-LINE                            C
               WRITE EMAIL-DATA-REC FROM END-OF-REPORT-LINE                    C
               MOVE 20 TO RETURN-CODE                                           
           ELSE                                                                 
             IF WS-GENERATE-EMAIL = 'Y'                                         
                 WRITE EMAIL-DATA-REC FROM BLANK-LINE                           
                 WRITE EMAIL-DATA-REC FROM END-OF-REPORT-LINE                   
                 MOVE 30 TO RETURN-CODE                                         
             END-IF                                                             
           END-IF.                                                              
01067                                                                      CL*30
01074      CLOSE LISTCAT-DATA                                              CL*10
                 TRIGGER-THRESH                                                 
01076            EMAIL-DATA.                                               CL**6
01078                                                                      CL**1
                                                                           CL**1
       9999-NONSQL-ABEND.                                                       
           CALL 'ILBOABN0' USING WS-ABEND-CODE.                                 
                                                                                
                                                                                
       9999-DB2-ABEND.                                                          
           CALL 'DSNTIAR' USING SQLCA                                           
                                DP004-FORMATTED-MESSAGE                         
                                DP004-ERROR-LINE-LENGTH.                        
                                                                                
           DISPLAY DP004-ASTERISK-LINE.                                         
                                                                                
           PERFORM                                                              
             VARYING DP004-MSG-IDX                                              
               FROM 1 BY 1                                                      
                 UNTIL DP004-MSG-IDX > DP004-MAX-MESSAGE-LINES                  
                   DISPLAY '**'                                                 
                    DP004-MESSAGE-LINE (DP004-MSG-IDX)                          
                    ' **'                                                       
           END-PERFORM.                                                         
                                                                                
           DISPLAY DP004-ASTERISK-LINE.                                         
                                                                                
           MOVE +4013      TO WS-ABEND-CODE                                     
           CALL 'ILBOABN0' USING WS-ABEND-CODE.                                 
                                                                                
