       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT012.                                       
       AUTHOR.                  PAUL KEBER.                                     
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            06/08/2004.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  CREATE CUTOFF DATABASE STORE UNLOAD CONTROL CARDS.            *        
      *                                                                *        
      *  THIS PROGRAM WILL EXAMINE THE INVENTORY PARAMETER TABLE TO    *        
      *  FIND ALL STORES BEING PROCESSED FOR THE INVENTORY CUTOFF      *        
      *  DATABASE.  THESE STORES WILL BE EVENLY SPLIT INTO SIX STORE   *        
      *  UNLOAD CONTROL CARD FILES.                                    *        
      *                                                                *        
      *  THE INPUT TO THIS PROGRAM WILL CONTAIN A CONTROL CARD FOR     *        
      *  THE PROCESSING DATE.                                          *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * 26600    08/10/2004  PAUL KEBER - INITIAL INSTALLATION.                 
W27808* W27808   04-29-2005  ADD SORT CARD CONTROL FILE MANIFEST PROJ           
W28545* W28545   07-24-2006  DEPARTMENT LEVEL INVENTORY. INCORPORATE            
W28545*                      INVENTORY ID.                                      
W28545*                      J SELWITSCHKA / STRATAGEM                          
W33304* W33304   04-03-2008  HOLD INVENTORY LEDGER OPEN ACROSS PERIODS          
W33304*                      CHUCK EBERT STRATAGEM                              
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT CC-DATE-CNTL-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT STORE-UNLOAD-CNTL1-FILE                                       
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT STORE-UNLOAD-CNTL2-FILE                                       
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT STORE-UNLOAD-CNTL3-FILE                                       
               ASSIGN TO OUT03.                                                 
                                                                                
           SELECT STORE-UNLOAD-CNTL4-FILE                                       
               ASSIGN TO OUT04.                                                 
                                                                                
           SELECT STORE-UNLOAD-CNTL5-FILE                                       
               ASSIGN TO OUT05.                                                 
                                                                                
           SELECT STORE-UNLOAD-CNTL6-FILE                                       
               ASSIGN TO OUT06.                                                 
                                                                                
W27808     SELECT STORE-SORT-CNTL-FILE                                          
W27808         ASSIGN TO OUT07.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  CC-DATE-CNTL-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CC-DATE-CNTL-REC                PIC X(80).                           
                                                                                
       FD  STORE-UNLOAD-CNTL1-FILE                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-UNLOAD-CNTL1-REC          PIC X(80).                           
                                                                                
       FD  STORE-UNLOAD-CNTL2-FILE                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-UNLOAD-CNTL2-REC          PIC X(80).                           
                                                                                
       FD  STORE-UNLOAD-CNTL3-FILE                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-UNLOAD-CNTL3-REC          PIC X(80).                           
                                                                                
       FD  STORE-UNLOAD-CNTL4-FILE                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-UNLOAD-CNTL4-REC          PIC X(80).                           
                                                                                
       FD  STORE-UNLOAD-CNTL5-FILE                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-UNLOAD-CNTL5-REC          PIC X(80).                           
                                                                                
       FD  STORE-UNLOAD-CNTL6-FILE                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-UNLOAD-CNTL6-REC          PIC X(80).                           
                                                                                
W27808 FD  STORE-SORT-CNTL-FILE                                                 
W27808     RECORDING MODE IS F                                                  
W27808     LABEL RECORDS ARE STANDARD                                           
W27808     BLOCK CONTAINS 0 RECORDS.                                            
W27808 01  STORE-SORT-CNTL-REC             PIC X(80).                           
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-TINVPAR-CSR       PIC X(1)        VALUE 'N'.           
               88  END-OF-TINVPAR-CSR                      VALUE 'Y'.           
           05  PS-FIRST-STORE-SW           PIC X(1)        VALUE 'Y'.           
               88  PS-FIRST-STORE                          VALUE 'Y'.           
               88  PS-NOT-FIRST-STORE                      VALUE 'N'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLES BEGIN ***'.                               
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                                                           COMP SYNC.           
                                                                                
       01  PROGRAM-CONSTANTS.                                                   
           05  PC-MAX-RETRY-COUNT          PIC S9(4)       VALUE +2             
                                                           COMP SYNC.           
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-RECS-WRITTEN             PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-RETRY-COUNT              PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-STORE-HOLD               PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-STORE-FILE-COUNT         PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-STORE-FILE-CNT-REMAIN    PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-STORE-FILE               PIC S9(4)       VALUE +6             
                                                           COMP SYNC.           
           05  PV-STORE-CNTL1-FILE-CNT     PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-STORE-CNTL2-FILE-CNT     PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-STORE-CNTL3-FILE-CNT     PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-STORE-CNTL4-FILE-CNT     PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-STORE-CNTL5-FILE-CNT     PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-STORE-CNTL6-FILE-CNT     PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-STORE-SORT-FILE-CNT      PIC S9(4)       VALUE +0             
                                                           COMP SYNC.           
           05  PV-REQUEST-COUNT            PIC S9(9)       VALUE ZEROES.        
           05  PV-NUMERIC-DISPLAY          PIC Z(8)9       VALUE ZEROES.        
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-PARAGRAPH-NAME           PIC X(40)       VALUE SPACES.        
                                                                                
       01  PV-PROCESS-DATE-AREA.                                                
           05  PV-PROCESS-DATE             PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES PV-PROCESS-DATE.                                
               10  PV-PROCESS-CCYY         PIC X(04).                           
               10  PV-PROCESS-HYPHEN1      PIC X(01).                           
               10  PV-PROCESS-MM           PIC X(02).                           
               10  PV-PROCESS-HYPHEN2      PIC X(01).                           
               10  PV-PROCESS-DD           PIC X(02).                           
           05  FILLER                      PIC X(70)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** STORE TABLE BEGIN ***'.                                         
                                                                                
       01  PT-STORE-TBL-WORK-AREA.                                              
           05  PT-STORE-TBL-LOADED         PIC S9(4)       VALUE ZEROES         
                               COMP.                                            
           05  PT-STORE-TBL-INDEX          PIC S9(4)       VALUE ZEROES         
                               COMP.                                            
           05  PT-STORE-TBL-MAX            PIC S9(4)       VALUE +9999          
                               COMP.                                            
           05  PT-STORE-TBL-AREA.                                               
               10  PT-STORE-TBL OCCURS 9999 TIMES.                              
                   15  PT-STORE-NBR        PIC S9(4)       COMP.                
                                                                                
       01  OR-OUTPUT-RECORD.                                                    
           05  FILLER                        PIC X(02)   VALUE SPACE.           
           05  FILLER                        PIC X(01)   VALUE SPACE.           
               88  OR-UNLOAD-COMMA                       VALUE ','.             
               88  OR-UNLOAD-NO-COMMA                    VALUE SPACE.           
               88  PURGE-UNLOAD-COMMA                    VALUE ','.             
               88  PURGE-UNLOAD-NO-COMMA                 VALUE SPACE.           
           05  FILLER                        PIC X(01)   VALUE SPACE.           
           05  OR-UNLOAD-STORE-NBR           PIC 9(04)   VALUE ZEROES.          
           05  FILLER                        PIC X(72)   VALUE SPACES.          
                                                                                
       01  SR-SORT-OUTPUT-RECORD.                                               
           05  FILLER                        PIC X(02)   VALUE SPACES.          
           05  SR-SORT-PART-1                PIC X(23)   VALUE SPACES.          
               88  SR-SORT-COPY-CMD                      VALUE                  
                   'SORT   FIELDS=COPY     '.                                   
               88  SR-SORT-OUT-FILE-A                    VALUE                  
                   'OUTFIL FILES=A,        '.                                   
               88  SR-SORT-OUT-FILE-B                    VALUE                  
                   'OUTFIL FILES=B,        '.                                   
               88  SR-SORT-OUT-FILE-C                    VALUE                  
                   'OUTFIL FILES=C,        '.                                   
               88  SR-SORT-OUT-FILE-D                    VALUE                  
                   'OUTFIL FILES=D,        '.                                   
               88  SR-SORT-OUT-FILE-E                    VALUE                  
                   'OUTFIL FILES=E,        '.                                   
               88  SR-SORT-OUT-FILE-F                    VALUE                  
                   'OUTFIL FILES=F,        '.                                   
               88  SR-SORT-INCLUDE-BEG                   VALUE                  
                   '  INCLUDE=(01,02,BI,EQ,'.                                   
               88  SR-SORT-INCLUDE-MID                   VALUE                  
                   '           01,02,BI,EQ,'.                                   
           05  SR-SORT-STORE-NBR             PIC X(04)   VALUE SPACES.          
           05  SR-SORT-STORE-NBR-9 REDEFINES SR-SORT-STORE-NBR                  
                                             PIC 9(04).                         
           05  SR-SORT-PART-2                PIC X(23)   VALUE SPACES.          
               88  SR-SORT-INCLUDE-END                   VALUE                  
                   ')       * IN050-LOC-NBR'.                                   
               88  SR-SORT-INCLUDE-CONT                  VALUE                  
                   ',OR,    * IN050-LOC-NBR'.                                   
           05  FILLER                        PIC X(28)   VALUE SPACES.          
       01  SR-SORT-COMMENT-RECORD REDEFINES SR-SORT-OUTPUT-RECORD.              
           05  FILLER                        PIC X(50).                         
               88  SR-SORT-COMMENT-1                     VALUE                  
                   '* THIS SORT CARD IS GENERATED WITHIN INKUT012.    '.        
               88  SR-SORT-COMMENT-2                     VALUE                  
                   '* THIS CARD WILL SPLIT UP THE INPUT INTO FILES AS '.        
               88  SR-SORT-COMMENT-3                     VALUE                  
                   '* DEFINE IN THE OUTFIL COMMAND.  ONLY THE FILE(S) '.        
               88  SR-SORT-COMMENT-4                     VALUE                  
                   '* WITH DATA WILL BE DEFINED IN THIS CONTROL.  THE '.        
               88  SR-SORT-COMMENT-5                     VALUE                  
                   '* JCL WILL STILL NEED TO DEFINE ALL THE OUTPUT    '.        
               88  SR-SORT-COMMENT-6                     VALUE                  
                   '* FILE.  SEE INRD050 FOR DATA LAYOUT DETAIL.      '.        
           05  FILLER                        PIC X(30).                         
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** DB2 STORAGE/DEFINITION BEGIN ***'.                              
                                                                                
      ******************************************************************        
      *  DB2 ERROR MESSAGES FORMAT AREA.                                        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  USED FOR DB2 ERRORS                                                    
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  LAYOUT USED FOR INVENTORY PARMS TABLE                                  
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
W28545******************************************************************        
W28545*  LAYOUT USED FOR INVENTORY CYCLE CONTROL TABLE                          
W28545******************************************************************        
W28545     EXEC SQL                                                             
W28545         INCLUDE TINCNTL                                                  
W28545     END-EXEC.                                                            
W28545                                                                          
      ******************************************************************        
      *  SQL DB2 TINVPAR CURSOR DECLARATION                                     
      ******************************************************************        
           EXEC SQL                                                             
               DECLARE TINVPAR_CSR CURSOR FOR                                   
                   SELECT LOC_NBR                                               
                         ,PLND_INV_DTE                                          
W28545               FROM TINVPAR A                                             
W28545                   ,TINCNTL B                                             
                    WHERE ACTL_FIN_BK_DTE   = '9999-09-09'                      
                      AND PLND_INV_DTE     >= :PV-PROCESS-DATE                  
W33304                AND A.SCHD_PHY_CNT_CDE IN ('PI','CL','NS')                
W28545                AND B.ACTV_IND        = 'Y'                               
W28545                AND A.INV_ID          = B.INV_ID                          
W33304                AND A.LOC_INV_STAT_CDE = 'IN'                             
                 ORDER BY LOC_NBR                                               
                         ,PLND_INV_DTE                                          
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM B200-PROCESS-TINVPAR.                                        
                                                                                
           IF PT-STORE-TBL-LOADED > ZERO                                        
               PERFORM B300-PROCESS-STORES                                      
           END-IF.                                                              
                                                                                
           PERFORM B400-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * OPEN ALL FILES AND GET THE PROCESS DATE.                                
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
           OPEN INPUT  CC-DATE-CNTL-FILE                                        
                OUTPUT STORE-UNLOAD-CNTL1-FILE                                  
                       STORE-UNLOAD-CNTL2-FILE                                  
                       STORE-UNLOAD-CNTL3-FILE                                  
                       STORE-UNLOAD-CNTL4-FILE                                  
                       STORE-UNLOAD-CNTL5-FILE                                  
W27808                 STORE-UNLOAD-CNTL6-FILE                                  
W27808                 STORE-SORT-CNTL-FILE.                                    
                                                                                
           PERFORM R100-READ-PROCESS-DATE.                                      
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' PROCESSING DATE: ' PV-PROCESS-DATE.                        
                                                                                
           IF PV-PROCESS-CCYY NOT NUMERIC                                       
             OR PV-PROCESS-MM NOT NUMERIC                                       
             OR PV-PROCESS-DD NOT NUMERIC                                       
             OR PV-PROCESS-HYPHEN1 NOT = '-'                                    
             OR PV-PROCESS-HYPHEN2 NOT = '-'                                    
               MOVE +4002                 TO ABEND-CODE                         
               DISPLAY '**  ABEND     **'                                       
               DISPLAY '**  PROGRAM   **  =  INKUT012'                          
               DISPLAY '**  INVALID PROCESSING DATE '                           
               CALL 'ILBOABN0'  USING ABEND-CODE                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    PROCESS TINVPAR                                                      
      ******************************************************************        
       B200-PROCESS-TINVPAR.                                                    
                                                                                
           PERFORM Z100-OPEN-TINVPAR-CSR.                                       
                                                                                
           PERFORM Z200-FETCH-TINVPAR-CSR.                                      
                                                                                
           MOVE ZEROES TO PT-STORE-TBL-LOADED.                                  
           PERFORM C100-LOAD-STORE-TABLE                                        
               UNTIL END-OF-TINVPAR-CSR.                                        
                                                                                
           PERFORM Z300-CLOSE-TINVPAR-CSR.                                      
                                                                                
      ******************************************************************        
      *    PROCESS INVENTORY STORES                                             
      ******************************************************************        
       B300-PROCESS-STORES.                                                     
                                                                                
           SET SR-SORT-COMMENT-1         TO TRUE.                               
           PERFORM W900-WRITE-STORE-SORT-CNTL.                                  
           SET SR-SORT-COMMENT-2         TO TRUE.                               
           PERFORM W900-WRITE-STORE-SORT-CNTL.                                  
           SET SR-SORT-COMMENT-3         TO TRUE.                               
           PERFORM W900-WRITE-STORE-SORT-CNTL.                                  
           SET SR-SORT-COMMENT-4         TO TRUE.                               
           PERFORM W900-WRITE-STORE-SORT-CNTL.                                  
           SET SR-SORT-COMMENT-5         TO TRUE.                               
           PERFORM W900-WRITE-STORE-SORT-CNTL.                                  
           SET SR-SORT-COMMENT-6         TO TRUE.                               
           PERFORM W900-WRITE-STORE-SORT-CNTL.                                  
           SET SR-SORT-COPY-CMD          TO TRUE.                               
           PERFORM W900-WRITE-STORE-SORT-CNTL.                                  
                                                                                
           DIVIDE PT-STORE-TBL-LOADED BY 6                                      
               GIVING PV-STORE-FILE-COUNT                                       
               REMAINDER PV-STORE-FILE-CNT-REMAIN.                              
                                                                                
           MOVE ZERO TO PT-STORE-TBL-INDEX.                                     
                                                                                
           PERFORM C200-CREATE-STORE-FILES                                      
               VARYING PV-STORE-FILE                                            
               FROM 1 BY 1                                                      
               UNTIL PV-STORE-FILE > 6.                                         
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B400-FINALIZE.                                                           
                                                                                
           CLOSE   CC-DATE-CNTL-FILE                                            
                   STORE-UNLOAD-CNTL1-FILE                                      
                   STORE-UNLOAD-CNTL2-FILE                                      
                   STORE-UNLOAD-CNTL3-FILE                                      
                   STORE-UNLOAD-CNTL4-FILE                                      
                   STORE-UNLOAD-CNTL5-FILE                                      
W27808             STORE-UNLOAD-CNTL6-FILE                                      
W27808             STORE-SORT-CNTL-FILE.                                        
                                                                                
           DISPLAY ' ------------------------------------------------'.         
           MOVE PT-STORE-TBL-LOADED          TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' TOTAL STORE(S) PROCESSED:  ' PV-NUMERIC-DISPLAY.           
           DISPLAY ' '.                                                         
           MOVE PV-STORE-CNTL1-FILE-CNT      TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' OUTPUT FILE 1 STORE COUNT: ' PV-NUMERIC-DISPLAY.           
           MOVE PV-STORE-CNTL2-FILE-CNT      TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' OUTPUT FILE 2 STORE COUNT: ' PV-NUMERIC-DISPLAY.           
           MOVE PV-STORE-CNTL3-FILE-CNT      TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' OUTPUT FILE 3 STORE COUNT: ' PV-NUMERIC-DISPLAY.           
           MOVE PV-STORE-CNTL4-FILE-CNT      TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' OUTPUT FILE 4 STORE COUNT: ' PV-NUMERIC-DISPLAY.           
           MOVE PV-STORE-CNTL5-FILE-CNT      TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' OUTPUT FILE 5 STORE COUNT: ' PV-NUMERIC-DISPLAY.           
           MOVE PV-STORE-CNTL6-FILE-CNT      TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' OUTPUT FILE 6 STORE COUNT: ' PV-NUMERIC-DISPLAY.           
           MOVE PV-STORE-SORT-FILE-CNT       TO PV-NUMERIC-DISPLAY.             
           DISPLAY ' SORT CONTROL FILE COUNT:   ' PV-NUMERIC-DISPLAY.           
                                                                                
      ******************************************************************        
      *    LOAD INVENTORYING STORES FROM INVENTORY PARM TABLE                   
      ******************************************************************        
       C100-LOAD-STORE-TABLE.                                                   
                                                                                
           IF INVPAR-LOC-NBR > PV-STORE-HOLD                                    
               ADD 1 TO PT-STORE-TBL-LOADED                                     
               IF PT-STORE-TBL-LOADED > PT-STORE-TBL-MAX                        
                   MOVE +4003 TO ABEND-CODE                                     
                   DISPLAY '**  ABEND     **'                                   
                   DISPLAY '**  PROGRAM   **  =  INKUT012'                      
                   DISPLAY '**  INTERNAL STORE TABLE OVERFLOW'                  
                   CALL 'ILBOABN0'  USING ABEND-CODE                            
               END-IF                                                           
               MOVE INVPAR-LOC-NBR TO PT-STORE-NBR (PT-STORE-TBL-LOADED).       
           END-IF.                                                              
                                                                                
           MOVE INVPAR-LOC-NBR TO PV-STORE-HOLD.                                
                                                                                
           PERFORM Z200-FETCH-TINVPAR-CSR.                                      
                                                                                
      ******************************************************************        
      *    PRODUCE INVENTORY CUTOFF STORES COMMA DELIMITED FILE                 
      ******************************************************************        
       C200-CREATE-STORE-FILES.                                                 
                                                                                
           IF PV-STORE-FILE = 6                                                 
               ADD PV-STORE-FILE-CNT-REMAIN TO PV-STORE-FILE-COUNT              
           END-IF.                                                              
                                                                                
           SET PS-FIRST-STORE TO TRUE.                                          
           SET OR-UNLOAD-NO-COMMA   TO TRUE.                                    
RONNA *    SET PURGE-UNLOAD-COMMA TO TRUE.                                      
                                                                                
           PERFORM C210-WRITE-STORE-FILE-RECS                                   
               VARYING PV-RECS-WRITTEN FROM 1 BY 1                              
               UNTIL PV-RECS-WRITTEN > PV-STORE-FILE-COUNT.                     
                                                                                
      ******************************************************************        
      *    FORMAT AND WRITE THE STORE FILE RECORD TO THE CORRECT FILE           
      ******************************************************************        
       C210-WRITE-STORE-FILE-RECS.                                              
                                                                                
           ADD +1 TO PT-STORE-TBL-INDEX.                                        
           MOVE PT-STORE-NBR (PT-STORE-TBL-INDEX)                               
               TO OR-UNLOAD-STORE-NBR.                                          
                                                                                
           EVALUATE PV-STORE-FILE                                               
               WHEN 1                                                           
                   PERFORM W100-WRITE-STORE-UNLOAD-CNTL1                        
                   SET SR-SORT-OUT-FILE-A      TO TRUE                          
               WHEN 2                                                           
                   PERFORM W200-WRITE-STORE-UNLOAD-CNTL2                        
                   SET SR-SORT-OUT-FILE-B      TO TRUE                          
               WHEN 3                                                           
                   PERFORM W300-WRITE-STORE-UNLOAD-CNTL3                        
                   SET SR-SORT-OUT-FILE-C      TO TRUE                          
               WHEN 4                                                           
                   PERFORM W400-WRITE-STORE-UNLOAD-CNTL4                        
                   SET SR-SORT-OUT-FILE-D      TO TRUE                          
               WHEN 5                                                           
                   PERFORM W500-WRITE-STORE-UNLOAD-CNTL5                        
                   SET SR-SORT-OUT-FILE-E      TO TRUE                          
               WHEN OTHER                                                       
                   PERFORM W600-WRITE-STORE-UNLOAD-CNTL6                        
                   SET SR-SORT-OUT-FILE-F      TO TRUE                          
           END-EVALUATE.                                                        
                                                                                
           IF PS-FIRST-STORE                                                    
               SET PS-NOT-FIRST-STORE   TO TRUE                                 
                                                                                
               PERFORM W900-WRITE-STORE-SORT-CNTL                               
               SET SR-SORT-INCLUDE-BEG  TO TRUE                                 
           ELSE                                                                 
               SET SR-SORT-INCLUDE-MID  TO TRUE                                 
           END-IF.                                                              
           SET OR-UNLOAD-COMMA      TO TRUE.                                    
                                                                                
           IF PV-RECS-WRITTEN = PV-STORE-FILE-COUNT                             
               SET SR-SORT-INCLUDE-END  TO TRUE                                 
           ELSE                                                                 
               SET SR-SORT-INCLUDE-CONT TO TRUE                                 
           END-IF.                                                              
           MOVE PT-STORE-NBR (PT-STORE-TBL-INDEX)                               
               TO SR-SORT-STORE-NBR-9.                                          
           PERFORM W900-WRITE-STORE-SORT-CNTL.                                  
                                                                                
      ******************************************************************        
      *    READ THE PROCESS DATE CONTROL CARD FILE                              
      ******************************************************************        
       R100-READ-PROCESS-DATE.                                                  
                                                                                
           READ CC-DATE-CNTL-FILE INTO PV-PROCESS-DATE-AREA                     
               AT END                                                           
                   MOVE +4001                 TO ABEND-CODE                     
                   DISPLAY '**  ABEND     **'                                   
                   DISPLAY '**  PROGRAM   **  =  INKUT012'                      
                   DISPLAY '**  EMPTY DATE CONTROL FILE'                        
                   CALL 'ILBOABN0'  USING ABEND-CODE                            
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *    WRITE OUT A STORE UNLOAD CONTROL 1 FILE RECORD                       
      ******************************************************************        
       W100-WRITE-STORE-UNLOAD-CNTL1.                                           
                                                                                
           ADD +1 TO PV-STORE-CNTL1-FILE-CNT.                                   
                                                                                
           WRITE STORE-UNLOAD-CNTL1-REC FROM OR-OUTPUT-RECORD.                  
                                                                                
      ******************************************************************        
      *    WRITE OUT A STORE UNLOAD CONTROL 2 FILE RECORD                       
      ******************************************************************        
       W200-WRITE-STORE-UNLOAD-CNTL2.                                           
                                                                                
           ADD +1 TO PV-STORE-CNTL2-FILE-CNT.                                   
                                                                                
           WRITE STORE-UNLOAD-CNTL2-REC FROM OR-OUTPUT-RECORD.                  
                                                                                
      ******************************************************************        
      *    WRITE OUT A STORE UNLOAD CONTROL 3 FILE RECORD                       
      ******************************************************************        
       W300-WRITE-STORE-UNLOAD-CNTL3.                                           
                                                                                
           ADD +1 TO PV-STORE-CNTL3-FILE-CNT.                                   
                                                                                
           WRITE STORE-UNLOAD-CNTL3-REC FROM OR-OUTPUT-RECORD.                  
                                                                                
      ******************************************************************        
      *    WRITE OUT A STORE UNLOAD CONTROL 4 FILE RECORD                       
      ******************************************************************        
       W400-WRITE-STORE-UNLOAD-CNTL4.                                           
                                                                                
           ADD +1 TO PV-STORE-CNTL4-FILE-CNT.                                   
                                                                                
           WRITE STORE-UNLOAD-CNTL4-REC FROM OR-OUTPUT-RECORD.                  
                                                                                
      ******************************************************************        
      *    WRITE OUT A STORE UNLOAD CONTROL 5 FILE RECORD                       
      ******************************************************************        
       W500-WRITE-STORE-UNLOAD-CNTL5.                                           
                                                                                
           ADD +1 TO PV-STORE-CNTL5-FILE-CNT.                                   
                                                                                
           WRITE STORE-UNLOAD-CNTL5-REC FROM OR-OUTPUT-RECORD.                  
                                                                                
      ******************************************************************        
      *    WRITE OUT A STORE UNLOAD CONTROL 6 FILE RECORD                       
      ******************************************************************        
       W600-WRITE-STORE-UNLOAD-CNTL6.                                           
                                                                                
           ADD +1 TO PV-STORE-CNTL6-FILE-CNT.                                   
                                                                                
           WRITE STORE-UNLOAD-CNTL6-REC FROM OR-OUTPUT-RECORD.                  
                                                                                
      ******************************************************************        
      *    WRITE OUT A STORE SORT CONTROL FILE RECORD                           
      ******************************************************************        
       W900-WRITE-STORE-SORT-CNTL.                                              
                                                                                
           ADD +1 TO PV-STORE-SORT-FILE-CNT.                                    
                                                                                
           WRITE STORE-SORT-CNTL-REC    FROM SR-SORT-OUTPUT-RECORD.             
           MOVE SPACES                  TO SR-SORT-OUTPUT-RECORD.               
                                                                                
      ******************************************************************        
      *    OPEN INVENTORY PARAMETER CURSOR                                      
      ******************************************************************        
       Z100-OPEN-TINVPAR-CSR.                                                   
                                                                                
           EXEC SQL                                                             
               OPEN TINVPAR_CSR                                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'Z100-OPEN-TINVPAR-CSR  '                               
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'OPEN TINVPAR       '                                   
                       TO PV-OPER-ATTEMPTED                                     
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    FETCH INVENTORY PARAMETER CURSOR                                     
      ******************************************************************        
       Z200-FETCH-TINVPAR-CSR.                                                  
                                                                                
           PERFORM                                                              
           WITH TEST AFTER                                                      
           VARYING PV-RETRY-COUNT FROM 1 BY 1                                   
           UNTIL (PV-RETRY-COUNT > PC-MAX-RETRY-COUNT                           
                  OR SQLCODE = 0                                                
                  OR SQLCODE = +100)                                            
                                                                                
               EXEC SQL                                                         
                   FETCH TINVPAR_CSR                                            
                   INTO  :INVPAR-LOC-NBR                                        
                        ,:INVPAR-PLND-INV-DTE                                   
               END-EXEC                                                         
                                                                                
               EVALUATE SQLCODE                                                 
                   WHEN +0                                                      
                       DISPLAY '** STORE NUMBER: ' INVPAR-LOC-NBR               
                               '  PLANNED INVENTORY DATE: '                     
                               INVPAR-PLND-INV-DTE                              
                   WHEN -904                                                    
                   WHEN -911                                                    
                       CONTINUE                                                 
                   WHEN +100                                                    
                       SET END-OF-TINVPAR-CSR TO TRUE                           
                   WHEN OTHER                                                   
                       MOVE 'Z200-FETCH-TINVPAR-CSR '                           
                           TO PV-PARAGRAPH-NAME                                 
                       MOVE 'FETCH TINVPAR      '                               
                           TO PV-OPER-ATTEMPTED                                 
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAX-RETRY-COUNT                               
               MOVE 'Z200-FETCH-TINVPAR-CSR' TO                                 
                     PV-PARAGRAPH-NAME                                          
               MOVE 'MAX FETCH RETRY COUNT EXCEEDED' TO                         
                     PV-OPER-ATTEMPTED                                          
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    CLOSE INVENTORY PARAMETER CURSOR                                     
      ******************************************************************        
       Z300-CLOSE-TINVPAR-CSR.                                                  
                                                                                
           EXEC SQL                                                             
               CLOSE TINVPAR_CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'Z300-CLOSE-TINVPAR-CSR '                               
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'CLOSE TINVPAR      '                                   
                       TO PV-OPER-ATTEMPTED                                     
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 9999-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE              
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                         
      ******************************************************************        
      *                                                                         
       9999-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = INKUT012'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-OPER-ATTEMPTED.                      
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
