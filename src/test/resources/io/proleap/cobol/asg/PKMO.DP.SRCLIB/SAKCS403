000100*-----------------------*                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*-----------------------*                                                 
000400 PROGRAM-ID.    SAKCS403.                                                 
000500 AUTHOR.        MATTHEW STIFF.                                            
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000700 DATE-WRITTEN.  04/15/1999.                                               
000800 DATE-COMPILED.                                                           
000900                                                                          
001000*---------------------------------------------------------------*         
001100*    S403 - TRANSACTION ITEM INQUIRY PROGRAM.                   *         
001200*                                                               *         
001300*    THIS PROGRAM DISPLAYS ALL ITEMS AND DISCOUNTS THAT ARE     *         
001400*    PART OF A PARTICULAR TRANSACTION.                          *         
001500*                                                               *         
001600*---------------------------------------------------------------*         
001700*                          CHANGES                              *         
001800*---------------------------------------------------------------*         
001900*  CHANGED 08/02/2001    BY LIHUA LIANG                                   
002000*                                                                         
002100*  ADDED LOGIC TO NOT DISPLAY ORDER NUMBER AFTER CALLING THE              
002100*  TENDER PROGRAM IF IT'S NOT AN E-COM TRANSACTION.                       
001800*---------------------------------------------------------------*         
001900*  CHANGED 05/07/2001    BY LIHUA LIANG                                   
002000*                                                                         
002100*  ADDED LOGIC TO DISPLAY SPECIAL CHARGES.                                
002210*---------------------------------------------------------------*         
001900*  CHANGED 04/24/2001    BY DOUG JONES                                    
002000*                                                                         
002100*  ADDED UNION ALL TO ITEM AND ITEM HISTORY CURSOR TO PICK UP             
002200*  MULTIPLE LID'S.                                                        
002210*---------------------------------------------------------------*         
002220*  CHANGED 03/29/2001    BY LIHUA LIANG                                   
002230*                                                                         
002240*  ADDED LOGIC TO DISPLAY ORDER NUMBER ON THE SCREEN.                     
002250*----------------------------------------------------------------         
002300*  CHANGED 08/10/2000    BY LIHUA LIANG                                   
002400*                                                                         
002500*  ADDED TEPEID TO CUSOR FOR OTR PHASE 2.                                 
002600*----------------------------------------------------------------         
002700*  CHANGED 03/16/2000    BY PAT DOYLE / MATT STIFF                        
002800*                                                                         
002900*  ADDED ACCESS TO THE HISTORICAL TABLES FROM THIS PROGRAM.               
003000*  THE NEW ELECTRONIC JOURNAL SCREEN THAT CALLS THIS SCREEN               
003100*  CAN ACCESS BOTH OPERATIONAL AND HISTORICAL DATA.                       
003200*----------------------------------------------------------------         
003300*  CHANGED 02/22/2000    BY MATT STIFF                                    
003400*                                                                         
003500*  THE DESCRIPTION FOR GIFT CARDS AND GIFT CERTIFICATES WAS               
003600*  DISPLAYING MERCAHNDISE.  CAHNGE THIS TO DISPLAY 'GIFT CARD'            
003700*  OR 'GIFT CERTIFICATE'.                                                 
003800*----------------------------------------------------------------         
003900*  CHANGED FEBRUARY 2000 BY MATT STIFF                                    
004000*                                                                         
004100*  ADDED FUNCTIONALITY TO BE ABLE TO MOVE LEFT (PF10) AND RIGHT           
004200*  (PF11) TO THE PROGRAM.  THIS WAS NEEDED TO SUPPORT THE NEW             
004300*  ITEM DESCRIPTION THAT WAS ADDED TO THE 'RIGHT' SIDE OF THE             
004400*  SCREEN.  ALSO ADDED THE KEYED INDICATOR TO THE 'LEFT' SIDE             
004500*  OF THE SCREEN.  IF Y, THE ITEM WAS NOT SCANNED AT THE REGISTER,        
004600*  IF BLANK, THE ITEM WAS SCANNED AT THE REGISTER.                        
004601*-----------------------------------------------------------------        
002400*  CHANGED 06/13/2002    BY CHRIS CLARK     WR: 23433                     
002500*                                                                         
002600*  CHANGED DEPT/CLASS/SKU LOGIC TO USE NEW SKU FIELD.                     
002800*-----------------------------------------------------------------        
002400*  CHANGED 08/01/2002    BY CHRIS CLARK     WR: 23433                     
002500*                                                                         
002600*  CHANGED ORDER BY FOR ITEMS CURSOR.                                     
002800*-----------------------------------------------------------------        
002400*  CHANGED 09/15/2003    BY CHRIS CLARK     WR: 25349                     
002500*                                                                         
002600*  MODIFIED SCREEN FOR ENHANCED BOGO.                                     
002800*-----------------------------------------------------------------        
002400*  CHANGED 05/05/2004    BY CHRIS CLARK     WR: P000654394                
002500*                                                                         
002600*  ADDED BOGO PHASE II/III LOGIC TO SCREEN.                               
002800*-----------------------------------------------------------------        
C62728*  CHANGED 10/04/2013    BY COGNIZANT       WR: CHG0062728                
C62728*                                                                         
C62728*  REMOVED LOGIC THAT REFERENCING THE FOLLOWING COSA HISTORY              
C62728*  TABLES FOR 9607 - FSH 2013-CFMC PROJECT.                               
C62728*                                                                         
C62728*  TEPEIDH,TEPSHPH,TEPLISH,TEPORDH,TEPTLDH, TEPTRNH,TEPITMH,              
C62728*  TEPLIDH                                                                
C00000*-----------------------------------------------------------------        
C00000*  CHANGED 09/02/2021    BY GERMAN BANDA    WR: CHG0261091                
C00000*                                                                         
C00000*  CHANGE CALL OF DPKUT100 TO                                             
C00000*  CALL DP010I-NUMERIC-EDIT-ROUTINE                                       
C00000*  MAKING THE CALL DYNAMIC VS STATIC                                      
C00000*-----------------------------------------------------------------        
CICS  *  CHANGED 10/12/2022    BY JIM HUNTER      WR: CHG0276153                
CICS  *                                                                         
CICS  *  ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL               
CICS  *  DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.              
CICS  *-----------------------------------------------------------------        
004602                                                                          
004603 ENVIRONMENT DIVISION.                                                    
004604                                                                          
004605 DATA DIVISION.                                                           
004606                                                                          
004607 WORKING-STORAGE SECTION.                                                 
004608                                                                          
004609 01  DB2-KEYS.                                                            
004610     05  DK-STORE                       PIC S9(04)  VALUE ZERO            
004611                                                    COMP.                 
004612     05  DK-REGISTER                    PIC S9(04)  VALUE ZERO            
004613                                                    COMP.                 
004614     05  DK-TRANS                       PIC S9(04)  VALUE ZERO            
004615                                                    COMP.                 
004616     05  DK-PARTITION                   PIC S9(04)  VALUE ZERO            
004617                                                    COMP.                 
004618     05  DK-START-TIME                  PIC  X(08)  VALUE SPACE.          
004619     05  DK-SALES-CORR-SEQ-NBR          PIC S9(04)  VALUE ZERO            
004620                                                    COMP.                 
004621     05  DK-SALES-DATE                  PIC  X(10)  VALUE SPACE.          
004622                                                                          
004623 01  PC-PROGRAM-CONSTANTS.                                                
004624     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
004625                                                    'SA403A  '.           
004626     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
004627                                                    'SAKM403 '.           
004628     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
004629                                                    'SAKCS403'.           
004630     05  PC-DTL-QUEUE-SEQ               PIC  9(01)  VALUE 3.              
004631     05  PC-LIST-FORMAT                 PIC  X(01)  VALUE '1'.            
004632     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +10             
004700                                                    COMP SYNC.            
004800     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
004900                                                    COMP SYNC.            
005000     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
005100                                                    +1000000              
005200                                                    COMP-3.               
005300     05  PC-MAX-RETRIES                 PIC S9(3)    VALUE 3              
005400                                                     COMP-3.              
005500     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
005600                                                    COMP SYNC.            
005700     05  PC-RESOLVED-VOID               PIC  X(01)  VALUE 'V'.            
005800     05  PC-NO-LINE-ITEM-DISCOUNT       PIC  X(03)  VALUE 'NON'.          
005900     05  PC-EMPLOYEE-DISCOUNT           PIC  X(03)  VALUE '790'.          
005900     05  PC-BOGO                        PIC  X(03)  VALUE '776'.          
005900     05  PC-BOGO-DLR-PCT-OFF            PIC  X(03)  VALUE '778'.          
005900     05  PC-SURCHARGE                   PIC  X(03)  VALUE 'S'.            
005900     05  PC-WRAP                        PIC  X(03)  VALUE 'W'.            
006000     05  PC-GIFT-CARD-DEPT              PIC  X(03)  VALUE '983'.          
006001     05  PC-GIFT-CERT-DEPT              PIC  X(03)  VALUE '981'.          
006002                                                                          
006100 01  PC-TSYMSG-NUMBERS.                                                   
006200     05  PC-TSYMSG-00008                PIC 9(5)    VALUE 00008.          
006300     05  PC-TSYMSG-00050                PIC 9(5)    VALUE 00050.          
006400     05  PC-TSYMSG-00069                PIC 9(5)    VALUE 00069.          
006500     05  PC-TSYMSG-00070                PIC 9(5)    VALUE 00070.          
006600     05  PC-TSYMSG-00101                PIC 9(5)    VALUE 00101.          
006700     05  PC-TSYMSG-00279                PIC 9(5)    VALUE 00279.          
006800     05  PC-TSYMSG-01985                PIC 9(5)    VALUE 01985.          
006810     05  PC-TSYMSG-02419                PIC 9(5)    VALUE 02419.          
006900                                                                          
007000 01  PS-PROGRAM-SWITCHES.                                                 
007100     05  PS-ERROR-SW                    PIC X(01)    VALUE 'Y'.           
007200         88  PS-NO-ERROR                             VALUE 'Y'.           
007300         88  PS-ERROR                                VALUE 'N'.           
007400     05  PS-COMMAREA-SW                 PIC X(01)    VALUE 'N'.           
007500         88  PS-COMMAREA-VALID                       VALUE 'Y'.           
007600         88  PS-COMMAREA-NOT-VALID                   VALUE 'N'.           
007700     05  PS-LIST-END-SW                 PIC X(01)    VALUE 'N'.           
007800         88  PS-LIST-END                             VALUE 'Y'.           
007900         88  PS-NO-LIST-END                          VALUE 'N'.           
008000                                                                          
008100 01  PV-PROGRAM-VARIABLES.                                                
008200     05  PV-REMAINDER                   PIC S9(04)   VALUE +0             
008300                                                     COMP SYNC.           
008400     05  PV-READ-COUNT                  PIC S9(09)   VALUE +0             
008500                                                     COMP-3.              
008600     05  PV-TOP-ITEM                    PIC S9(09)   VALUE +0             
008700                                                     COMP-3.              
008800     05  PV-BOTTOM-ITEM                 PIC S9(09)   VALUE +0             
008900                                                     COMP-3.              
009000     05  PV-BLOCK-NUMBER                PIC S9(04)   VALUE +0             
009100                                                     COMP SYNC.           
009200     05  PV-CICS-RESP                   PIC S9(04)   VALUE +0             
009300                                                     COMP SYNC.           
009400     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)   VALUE +0             
009500                                                     COMP SYNC.           
009600     05  PV-LIST-ITEM-NUMBER            PIC S9(04)   VALUE +0             
009700                                                     COMP SYNC.           
009800     05  PV-RETRY-COUNTER               PIC S9(04)   VALUE ZERO           
009900                                                     COMP SYNC.           
010000     05  PV-HOLD-RCPT-SEQ-NBR           PIC S9(04)   VALUE ZERO           
010100                                                     COMP.                
010200     05  PV-AMT-FORMATTED               PIC ZZ,ZZ9.99-.                   
010200     05  PV-ECOM-SURCHARGE              PIC S9(03)V99 VALUE 0.            
010200     05  PV-ECOM-SHIP-CHRG              PIC S9(03)V99 VALUE 0.            
010300     05  PV-SUB                         PIC S9(04) VALUE ZERO             
010400                                                   COMP SYNC.             
010500     05  PV-TIME.                                                         
010600         10  PV-TIME-HH                 PIC  9(02) VALUE ZERO.            
010700         10  FILLER                     PIC  X(01).                       
010800         10  PV-TIME-MM                 PIC  9(02) VALUE ZERO.            
010900         10  FILLER                     PIC  X(03).                       
011000     05  PV-TIME-A-P                    PIC  X(01) VALUE SPACE.           
011100     05  PV-CUST-CHRGD-AMT              PIC S9(07)V9(02) COMP-3           
011200                                                       VALUE 0.           
011602 01  HL-HEADING-LINES.                                                    
011603     05  HL-LEFT-HEADING                PIC  X(78)   VALUE                
011604         'SKU             UPC / G.C.     UNITS  KEYED      AMOUNT         
011605-        '   EXTENDED  ACTION   '.                                        
011606     05  HL-RIGHT-HEADING               PIC  X(78)   VALUE                
011607         'SKU                  DESCRIPTION            UNITS      E        
011608-        'XTENDED               '.                                        
011700                                                                          
011800 01  DL-DETAIL-LINE-AREA.                                                 
011900     05  DL-DETAIL-LINE                 PIC  X(78)   VALUE SPACES.        
012000     05  FILLER REDEFINES DL-DETAIL-LINE.                                 
012100        10  FILLER                      PIC  X(03).                       
012200        10  DL-SKU-L                    PIC  X(08).                       
012300        10  FILLER                      PIC  X(05).                       
012400        10  DL-UPC-GC-L                 PIC  X(13).                       
012500        10  FILLER                      PIC  X(03).                       
012600        10  DL-UNITS-L                  PIC  X(02).                       
012700        10  FILLER                      PIC  X(06).                       
012800        10  DL-KEYED-IND-L              PIC  X(01).                       
012900        10  FILLER                      PIC  X(04).                       
013000        10  DL-AMOUNT-L                 PIC  X(10).                       
013100        10  FILLER                      PIC  X(02).                       
013200        10  DL-EXT-AMOUNT-L             PIC  X(10).                       
013300        10  FILLER                      PIC  X(04).                       
013400        10  DL-ACTION-CDE-L             PIC  X(01).                       
013500        10  FILLER                      PIC  X(01).                       
013600        10  DL-ACTION-TEXT-L            PIC  X(05).                       
013700     05  FILLER REDEFINES DL-DETAIL-LINE.                                 
013800        10  FILLER                      PIC  X(03).                       
013900        10  DL-SKU-R                    PIC  X(08).                       
014000        10  FILLER                      PIC  X(10).                       
014100        10  DL-MDSE-DESCRIPTION-R       PIC  X(17).                       
014200        10  FILLER                      PIC  X(07).                       
014300        10  DL-UNITS-R                  PIC  X(02).                       
014400        10  FILLER                      PIC  X(07).                       
014500        10  DL-EXT-AMOUNT-R             PIC  X(10).                       
014600        10  FILLER                      PIC  X(14).                       
014601                                                                          
014602*---------------------------------------------------------------*         
CICS  *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
014604*---------------------------------------------------------------*         
014605                                                                          
014606     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
014607                                                                          
014608*---------------------------------------------------------------*         
014609*    STANDARD COMMAREA.                                         *         
014610*---------------------------------------------------------------*         
014700                                                                          
014800     COPY DPWS020.                                                        
014900     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
015000                                                                          
015100*---------------------------------------------------------------*         
015200*    INTER-APPLICATION COMMAREA.                                *         
015300*---------------------------------------------------------------*         
015400                                                                          
015500         10  INTER-APPL-COMM-AREA       PIC X(200).                       
015600     COPY SAWS004.                                                        
015700                                                                          
015800*---------------------------------------------------------------*         
015900*    SPECIFIC COMMMAREA FOR SAKCS403.                           *         
016000*---------------------------------------------------------------*         
016100         10  ASC-SPECIFIC-COMMAREA.                                       
016200             15  ASC-LIST-KEYS.                                           
016300                 20  ASC-KEY-PARTITION               PIC S9(04)           
016400                                                           COMP.          
016500                 20  ASC-KEY-SALES-DATE-ISO          PIC  X(10).          
016600                 20  ASC-KEY-STORE                   PIC  9(05).          
016700                 20  ASC-KEY-REGISTER                PIC  9(04).          
016800                 20  ASC-KEY-TRANSACTION             PIC  9(04).          
016810                 20  ASC-KEY-ORDER-NBR               PIC  X(40).          
016900                 20  ASC-KEY-START-TIME              PIC  X(08).          
017000                 20  ASC-KEY-ID                      PIC  X(06).          
017100                 20  ASC-KEY-TYPE                    PIC  X(02).          
017200                     88 ASC-KOHLS-PAYMENT-TYPE       VALUE '30'.          
017300                     88 ASC-INVALID-TYPE             VALUE '42',          
017400                                                     '43', '44',          
017401                                                     '54', '64'.          
017402                 20  ASC-KEY-SALES-CORR-SEQ-NBR      PIC S9(04)           
017403                                                           COMP.          
028802                 20  FILLER                          PIC  X(01).          
017500             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
017600                 20  ASC-ITEM-ARRAY.                                      
017700                                                                          
017800                     25  ASC-ITEM-ENTRIES OCCURS 10 TIMES.                
017900                         30  ASC-LIST-ITEM-NUMBER                         
018000                                                     PIC S9(09)           
018100                                                     COMP-3.              
018200                         30  ASC-DEPT                PIC  X(03).          
018200                         30  ASC-CLASS               PIC  X(02).          
061900                         30  ASC-SKU                 PIC  X(08).          
061900                         30  ASC-SKU-N REDEFINES ASC-SKU                  
062000                                                     PIC  9(08).          
018800                         30  ASC-UPC-GIFT-CARD-NBR   PIC  X(13).          
018900                         30  ASC-UPC-GIFT-CARD-NBR-N REDEFINES            
019000                                 ASC-UPC-GIFT-CARD-NBR                    
019100                                                     PIC  9(13).          
019200                         30  ASC-SOLD-QUANTITY       PIC  X(02).          
019300                         30  ASC-SOLD-QUANTITY-N     REDEFINES            
019400                                 ASC-SOLD-QUANTITY   PIC  9(02).          
019500                         30  ASC-KEYED-IND           PIC  X(01).          
019501                             88  ASC-ITEM-IS-KEYED   VALUE 'Y'.           
019502                         30  ASC-CUST-CHRGD-AMT      PIC  X(10).          
019600                         30  ASC-CUST-CHRGD-AMT-N                         
019700                                                  PIC S9(5)V9(2)          
019800                                                      COMP-3.             
019900                         30  ASC-EXT-CHRGD-AMT       PIC  X(10).          
020000                         30  ASC-EXT-CHRGD-AMT-N  PIC S9(05)V9(02)        
020100                                                      COMP-3.             
020200                         30  ASC-ACTION              PIC  X(01).          
020300                             88 ASC-ACTION-TAX       VALUE 'X'.           
020400                             88 ASC-ACTION-TLD-D     VALUE 'D'.           
020400                             88 ASC-ACTION-TLD-P     VALUE 'C'.           
020500                             88 ASC-ACTION-TIERED    VALUE 'T'.           
020600                             88 ASC-ACTION-EMP-DISC  VALUE 'E'.           
020700                             88 ASC-ACTION-LID       VALUE 'L'.           
020800                             88 ASC-ACTION-SCD       VALUE 'S'.           
020900                             88 ASC-ACTION-ITM-COR   VALUE 'I'.           
020900                             88 ASC-ACTION-SURCHG    VALUE 'H'.           
020900                             88 ASC-ACTION-WRAP      VALUE 'W'.           
020900                             88 ASC-ACTION-SHIP      VALUE 'P'.           
021000                             88 ASC-NO-ACTION        VALUE ' '.           
021100                         30  ASC-ACTION-TEXT         PIC  X(05).          
021200                             88 ASC-ACT-TEXT-TAX     VALUE 'TAX'.         
021300                             88 ASC-ACT-TEXT-TLD     VALUE 'TLD'.         
021400                             88 ASC-ACT-TEXT-TIERED  VALUE 'TIER'.        
021500                             88 ASC-ACT-TEXT-EMP     VALUE 'EMP'.         
021600                             88 ASC-ACT-TEXT-LID     VALUE 'LID'.         
021700                             88 ASC-ACT-TEXT-SCD     VALUE 'SCD'.         
021800                             88 ASC-ACT-TEXT-ITM-COR VALUE 'COR'.         
021800                             88 ASC-ACT-TEXT-SURCHG  VALUE                
021800                                                     'SURCH'.             
021800                             88 ASC-ACT-TEXT-WRAP    VALUE 'WRAP'.        
021800                             88 ASC-ACT-TEXT-SHIP    VALUE 'SHIP'.        
021810                         30  ASC-MDSE-DESCRIPTION    PIC  X(17).          
                               30  ASC-BOGO-IND            PIC  X(01).          
                                   88  ASC-BOGO-Y          VALUE 'Y'.           
                                   88  ASC-BOGO-N          VALUE 'N'.           
021900                                                                          
022000             15  ASC-BLK-SUB                         PIC S9(04)           
022100                                                         COMP             
022200                                                         SYNC.            
022300             15  ASC-ITEM-SUB                        PIC S9(04)           
022400                                                         COMP             
022500                                                         SYNC.            
022600             15  ASC-SEL-SUB                         PIC S9(04)           
022700                                                         COMP.            
022800             15  ASC-SEL-ITEM                        PIC S9(04)           
022900                                                         COMP.            
023000             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
023100                                                         COMP-3.          
023200             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
023300                                                         COMP-3.          
023400             15  ASC-NUMBER-OF-TS-ITEMS              PIC S9(05)           
023500                                                         COMP-3.          
023600             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
023700                                                         COMP-3.          
023800             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
023900                                                         COMP-3.          
024000             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
024100                                                         COMP-3.          
024200             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
024300                                                         COMP             
024400                                                         SYNC.            
024500             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
024600                                                         COMP-3.          
024700             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
024800                                                         COMP-3.          
024900             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
025000                 88  ASC-ITEMS-LEFT-ON-DB              VALUE 'Y'.         
025100                 88  ASC-NO-ITEMS-LEFT-ON-DB           VALUE 'N'.         
025200             15  ASC-USE-SELECTION-QUEUE-IND         PIC  X(01).          
025300                 88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'.         
025400             15  ASC-LIST-QUEUE-NAME.                                     
025500                 20  ASC-LIST-TERM-ID                PIC  X(04).          
025600                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
025700                                                         COMP-3.          
025800                 20  ASC-LIST-SEQ                    PIC  9(01).          
025900             15  ASC-SEL-QUEUE-NAME.                                      
026000                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
026100                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
026200                                                          COMP-3.         
026300                 20  ASC-SEL-SEQ                     PIC  9(01).          
026400             15  ASC-DTL-QUEUE-NAME.                                      
026500                 20  ASC-DTL-TERM-ID                 PIC  X(04).          
026600                 20  ASC-DTL-TASK-NUMBER             PIC S9(05)           
026700                                                         COMP-3.          
026800                 20  ASC-DTL-SEQ                     PIC  9(01).          
026900                                                                          
027000             15  ASC-START-ITEM-REQUEST.                                  
027100                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
027200             15  ASC-TRAN-SEL-SUB                    PIC  9(02).          
027300             15  ASC-NBR-OF-TRAN-SEL-ITEMS           PIC S9(09)           
027400                                                     COMP-3.              
027500             15  ASC-NBR-OF-TRAN-TS-ITEMS            PIC S9(05)           
027600                                                     COMP-3.              
027700             15  ASC-TRAN-SEL-QUEUE-NAME             PIC  X(08).          
027800             15  ASC-TRAN-SEL-ITEM                   PIC S9(04)           
027900                                                     COMP SYNC.           
028000             15  ASC-SALES-DATE-DISPLAY              PIC  X(10).          
028100             15  ASC-TRAN-TOT-AMT                    PIC  X(10).          
028200             15  ASC-TRAN-TOT-AMT-N                 PIC S9(5)V9(2)        
028300                                                     COMP-3.              
028400             15  ASC-TIME-HH                         PIC  X(02).          
028500             15  ASC-TIME-MM                         PIC  X(02).          
028600             15  ASC-TIME-A-P                        PIC  X(02).          
028700             15  ASC-VIEW-IND                        PIC  X(01).          
028800                 88  ASC-LEFT-VIEW                   VALUE 'L'.           
028801                 88  ASC-RIGHT-VIEW                  VALUE 'R'.           
028802         10  FILLER                                  PIC  X(931).         
028803                                                                          
028900                                                                          
029000     EJECT                                                                
029100                                                                          
029200*----------------------------------------------------------------*        
029300*  RECORD LAYOUT FOR THE TRAN SELECTION TEMP STORAGE QUEUE       *        
029400*----------------------------------------------------------------*        
029500                                                                          
029600     COPY SAWS001.                                                        
029700                                                                          
029800*---------------------------------------------------------------*         
029900*    ABEND PROCESSING COPYBOOK.                                 *         
030000*---------------------------------------------------------------*         
030100                                                                          
030200     COPY DPWS013.                                                        
030300                                                                          
030400     EJECT                                                                
030500*---------------------------------------------------------------*         
030600*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
030700*---------------------------------------------------------------*         
030800                                                                          
030900     COPY DPWS015.                                                        
031000                                                                          
031100*---------------------------------------------------------------*         
031200*    FUNCTION KEYS COPYBOOK.                                    *         
031300*---------------------------------------------------------------*         
031400                                                                          
031500     COPY DPWS016.                                                        
031600                                                                          
031700*---------------------------------------------------------------*         
031800*    NUMERIC EDIT LAYOUT.                                       *         
031900*---------------------------------------------------------------*         
032000                                                                          
032100     COPY DPWS010I.                                                       
032200                                                                          
032300*---------------------------------------------------------------*         
032400*    MAP LAYOUT                                                 *         
032500*---------------------------------------------------------------*         
032600                                                                          
032700     COPY SAKM403.                                                        
032800                                                                          
032900 01  MR-OCCURS-LAYOUT          REDEFINES  SA403AO.                        
033000     05  FILLER                PIC X(262).                                
033100     05  MR-SCROLL-AREA OCCURS 10 TIMES.                                  
033200                                                                          
033300         10  MR-SCROLLABLE-AREAL        PIC S9(04) COMP.                  
033400         10  MR-SCROLLABLE-AREAA        PIC  X(01).                       
033500         10  MR-SCROLLABLE-AREAC        PIC  X(01).                       
033600         10  MR-SCROLLABLE-AREAH        PIC  X(01).                       
033700         10  MR-SCROLLABLE-AREA         PIC  X(78).                       
033800                                                                          
038100     05  FILLER                         PIC  X(15).                       
038200                                                                          
038500*---------------------------------------------------------------*         
038600*    DB2 AREA FOR TEPITM (ITEM TABLE)                           *         
038700*---------------------------------------------------------------*         
038800     EXEC SQL                                                             
038900          INCLUDE TEPITM                                                  
039000     END-EXEC.                                                            
039100                                                                          
039200*---------------------------------------------------------------*         
039300*    DB2 AREA FOR TETRN (TRANSACTION TABLE)                     *         
039400*---------------------------------------------------------------*         
039500     EXEC SQL                                                             
039600          INCLUDE TEPTRN                                                  
039700     END-EXEC.                                                            
039800                                                                          
039900*---------------------------------------------------------------*         
040000*    DB2 AREA FOR LID (LINE ITEM DISCOUNT TABLE)                *         
040100*---------------------------------------------------------------*         
040200     EXEC SQL                                                             
040300         INCLUDE TEPLID                                                   
040400     END-EXEC.                                                            
040500                                                                          
040600*---------------------------------------------------------------*         
040700*    DB2 AREA FOR TLD (TRANSACTION LEVEL DISCOUNT TABLE)        *         
040800*---------------------------------------------------------------*         
040900     EXEC SQL                                                             
041000         INCLUDE TEPTLD                                                   
041100     END-EXEC.                                                            
041200                                                                          
040600*---------------------------------------------------------------*         
040700*    DB2 AREA FOR TEPSHP (SHIPPING CHARGES TABLE)                         
040800*---------------------------------------------------------------*         
040900     EXEC SQL                                                             
041000         INCLUDE TEPSHP                                                   
041100     END-EXEC.                                                            
041200                                                                          
041208*---------------------------------------------------------------*         
041209*    DB2 AREA FOR TEPPART (PARTITION TABLE)                     *         
041210*---------------------------------------------------------------*         
041211     EXEC SQL                                                             
041212         INCLUDE TEPPART                                                  
041213     END-EXEC.                                                            
041214                                                                          
041256*---------------------------------------------------------------*         
041257*    DB2 AREA FOR ORDER TABLE                                   *         
041258*---------------------------------------------------------------*         
041259     EXEC SQL                                                             
041260         INCLUDE TEPORD                                                   
041270     END-EXEC.                                                            
041280                                                                          
041306*---------------------------------------------------------------*         
070300*  SQL AND COBOL DECLARATIONS FOR XST_SUB_CL TABLE              *         
070500*---------------------------------------------------------------*         
070700     EXEC SQL                                                             
070800         INCLUDE XST00014                                                 
070900     END-EXEC.                                                            
071000                                                                          
041306*---------------------------------------------------------------*         
041400*    DB2 AREA FOR COMMUNICATIONS                                *         
041500*---------------------------------------------------------------*         
041600     EXEC SQL                                                             
041700          INCLUDE SQLCA                                                   
041800     END-EXEC.                                                            
041900                                                                          
042000*---------------------------------------------------------------*         
042100*    THIS CURSOR WILL RETRIEVE ITEMS AND APPLICABLE ITEM        *         
042200*    DISCOUNTS FROM THE OPERATIONAL TABLES.                     *         
042300*---------------------------------------------------------------*         
042400                                                                          
042500     EXEC SQL                                                             
042600          DECLARE ITEMS CURSOR                                            
042700          FOR SELECT                                                      
042800                DEPT_NBR                                                  
042900              , SUB_CL_NBR                                                
042910              , MAJ_CL_NBR                                                
043000              , SKU_NBR                                                   
043100              , SLD_QTY                                                   
043200              , UPC_NBR                                                   
043200              , MDSE_ID                                                   
043210              , SRC_TYP_CDE                                               
043300              , CUST_CHRGD_AMT                                            
043400              , VT1.LNE_ITM_SEQ_NBR                                       
043500              , LIV_RSLU_CDE                                              
043600              , COALESCE(LID_RSN_CDE,'NON')                               
043700              , COALESCE(LID_AMT,0.00)                                    
043700              , VT1.RCPT_SEQ_NBR                                          
043800          FROM (SELECT PARTN_NBR                                          
043900                     , STR_NBR                                            
044000                     , RGST_ID                                            
044100                     , TRN_NBR                                            
044200                     , STRT_TM                                            
044300                     , SLS_CORR_SEQ_NBR                                   
044400                     , LNE_ITM_SEQ_NBR                                    
044500                     , LIV_RSLU_CDE                                       
044600                     , DEPT_NBR                                           
044700                     , SUB_CL_NBR                                         
044710                     , MAJ_CL_NBR                                         
044800                     , SKU_NBR                                            
044900                     , SLD_QTY                                            
045000                     , UPC_NBR                                            
045000                     , MDSE_ID                                            
045010                     , SRC_TYP_CDE                                        
045100                     , CUST_CHRGD_AMT                                     
045100                     , RCPT_SEQ_NBR                                       
045200                FROM TEPITM                                               
045300                WHERE  PARTN_NBR        = :DK-PARTITION                   
045400                  AND  STR_NBR          = :DK-STORE                       
045500                  AND  RGST_ID          = :DK-REGISTER                    
045600                  AND  TRN_NBR          = :DK-TRANS                       
045700                  AND  STRT_TM          = :DK-START-TIME                  
045800                  AND  SLS_CORR_SEQ_NBR = :DK-SALES-CORR-SEQ-NBR)         
045900                AS VT1                                                    
046000          LEFT OUTER JOIN                                                 
046100               (SELECT PARTN_NBR                                          
046200                     , STR_NBR                                            
046300                     , RGST_ID                                            
046400                     , TRN_NBR                                            
046500                     , STRT_TM                                            
046600                     , SLS_CORR_SEQ_NBR                                   
046700                     , LNE_ITM_SEQ_NBR                                    
046800                     , LID_RSN_CDE                                        
046900                     , LID_AMT                                            
047000                     , LID_TYP_CDE                                        
047100                FROM TEPLID                                               
047200                WHERE  PARTN_NBR        = :DK-PARTITION                   
047300                  AND  STR_NBR          = :DK-STORE                       
047400                  AND  RGST_ID          = :DK-REGISTER                    
047500                  AND  TRN_NBR          = :DK-TRANS                       
047600                  AND  STRT_TM          = :DK-START-TIME                  
047700                  AND  SLS_CORR_SEQ_NBR = :DK-SALES-CORR-SEQ-NBR          
047800                  AND  LID_TYP_CDE    IN ('P','C','D','E')) AS VT2        
047900          ON  VT2.PARTN_NBR             = VT1.PARTN_NBR                   
048000          AND VT2.STR_NBR               = VT1.STR_NBR                     
048100          AND VT2.RGST_ID               = VT1.RGST_ID                     
048200          AND VT2.TRN_NBR               = VT1.TRN_NBR                     
048300          AND VT2.STRT_TM               = VT1.STRT_TM                     
048400          AND VT2.SLS_CORR_SEQ_NBR      = VT1.SLS_CORR_SEQ_NBR            
048500          AND VT2.LNE_ITM_SEQ_NBR       = VT1.LNE_ITM_SEQ_NBR             
048600                                                                          
048601          UNION ALL                                                       
048602                                                                          
048603          SELECT                                                          
048604                DEPT_NBR                                                  
048605              , SUB_CL_NBR                                                
048606              , MAJ_CL_NBR                                                
048606              , SKU_NBR                                                   
048607              , SLD_QTY                                                   
048608              , UPC_NBR                                                   
048608              , MDSE_ID                                                   
048609              , SRC_TYP_CDE                                               
048610              , CUST_CHRGD_AMT                                            
048611              , VT3.LNE_ITM_SEQ_NBR                                       
048612              , LIV_RSLU_CDE                                              
048613              , COALESCE(RSN_CDE,'NON')                                   
048614              , COALESCE(EMP_DISC_AMT,0.00)                               
048614              , VT3.RCPT_SEQ_NBR                                          
048615          FROM (SELECT PARTN_NBR                                          
048616                     , STR_NBR                                            
048617                     , RGST_ID                                            
048618                     , TRN_NBR                                            
048619                     , STRT_TM                                            
048620                     , SLS_CORR_SEQ_NBR                                   
048621                     , LNE_ITM_SEQ_NBR                                    
048622                     , LIV_RSLU_CDE                                       
048623                     , DEPT_NBR                                           
048624                     , SUB_CL_NBR                                         
048625                     , MAJ_CL_NBR                                         
048625                     , SKU_NBR                                            
048626                     , SLD_QTY                                            
048627                     , UPC_NBR                                            
048627                     , MDSE_ID                                            
048628                     , SRC_TYP_CDE                                        
048629                     , CUST_CHRGD_AMT                                     
048629                     , RCPT_SEQ_NBR                                       
048630                FROM TEPITM                                               
048631                WHERE  PARTN_NBR        = :DK-PARTITION                   
048632                  AND  STR_NBR          = :DK-STORE                       
048633                  AND  RGST_ID          = :DK-REGISTER                    
048634                  AND  TRN_NBR          = :DK-TRANS                       
048635                  AND  STRT_TM          = :DK-START-TIME                  
048636                  AND  SLS_CORR_SEQ_NBR = :DK-SALES-CORR-SEQ-NBR)         
048637                AS VT3                                                    
048638          LEFT OUTER JOIN                                                 
048639               (SELECT PARTN_NBR                                          
048640                     , STR_NBR                                            
048641                     , RGST_ID                                            
048642                     , TRN_NBR                                            
048643                     , STRT_TM                                            
048644                     , SLS_CORR_SEQ_NBR                                   
048645                     , LNE_ITM_SEQ_NBR                                    
048646                     , '790' RSN_CDE                                      
048647                     , EMP_DISC_AMT                                       
048648                FROM TEPEID                                               
048649                WHERE  PARTN_NBR        = :DK-PARTITION                   
048650                  AND  STR_NBR          = :DK-STORE                       
048651                  AND  RGST_ID          = :DK-REGISTER                    
048652                  AND  TRN_NBR          = :DK-TRANS                       
048653                  AND  STRT_TM          = :DK-START-TIME                  
048654                  AND  SLS_CORR_SEQ_NBR = :DK-SALES-CORR-SEQ-NBR          
048655                  AND  EMP_DISC_CTG_CDE = 'C')                            
048656                  AS VT4                                                  
048657          ON  VT4.PARTN_NBR             = VT3.PARTN_NBR                   
048658          AND VT4.STR_NBR               = VT3.STR_NBR                     
048659          AND VT4.RGST_ID               = VT3.RGST_ID                     
048660          AND VT4.TRN_NBR               = VT3.TRN_NBR                     
048661          AND VT4.STRT_TM               = VT3.STRT_TM                     
048662          AND VT4.SLS_CORR_SEQ_NBR      = VT3.SLS_CORR_SEQ_NBR            
048663          AND VT4.LNE_ITM_SEQ_NBR       = VT3.LNE_ITM_SEQ_NBR             
048664                                                                          
048601          UNION ALL                                                       
048602                                                                          
048603          SELECT                                                          
048604                DEPT_NBR                                                  
048605              , SUB_CL_NBR                                                
048605              , MAJ_CL_NBR                                                
048606              , SKU_NBR                                                   
048607              , SLD_QTY                                                   
048608              , UPC_NBR                                                   
048608              , MDSE_ID                                                   
048609              , SRC_TYP_CDE                                               
048610              , CUST_CHRGD_AMT                                            
048611              , VT5.LNE_ITM_SEQ_NBR                                       
048612              , LIV_RSLU_CDE                                              
048613              , COALESCE(SRVC_TYP_CDE,'NON')                              
048614              , COALESCE(SRVC_AMT,0.00)                                   
048614              , VT5.RCPT_SEQ_NBR                                          
048615          FROM (SELECT PARTN_NBR                                          
048616                     , STR_NBR                                            
048617                     , RGST_ID                                            
048618                     , TRN_NBR                                            
048619                     , STRT_TM                                            
048620                     , SLS_CORR_SEQ_NBR                                   
048621                     , LNE_ITM_SEQ_NBR                                    
048622                     , LIV_RSLU_CDE                                       
048623                     , DEPT_NBR                                           
048624                     , SUB_CL_NBR                                         
048625                     , MAJ_CL_NBR                                         
048625                     , SKU_NBR                                            
048626                     , SLD_QTY                                            
048627                     , UPC_NBR                                            
048627                     , MDSE_ID                                            
048628                     , SRC_TYP_CDE                                        
048629                     , CUST_CHRGD_AMT                                     
048629                     , RCPT_SEQ_NBR                                       
048630                FROM TEPITM                                               
048631                WHERE  PARTN_NBR        = :DK-PARTITION                   
048632                  AND  STR_NBR          = :DK-STORE                       
048633                  AND  RGST_ID          = :DK-REGISTER                    
048634                  AND  TRN_NBR          = :DK-TRANS                       
048635                  AND  STRT_TM          = :DK-START-TIME                  
048636                  AND  SLS_CORR_SEQ_NBR = :DK-SALES-CORR-SEQ-NBR)         
048637                AS VT5                                                    
048638          LEFT OUTER JOIN                                                 
048639               (SELECT PARTN_NBR                                          
048640                     , STR_NBR                                            
048641                     , RGST_ID                                            
048642                     , TRN_NBR                                            
048643                     , STRT_TM                                            
048644                     , SLS_CORR_SEQ_NBR                                   
048645                     , LNE_ITM_SEQ_NBR                                    
048646                     , SRVC_TYP_CDE                                       
048647                     , SRVC_AMT                                           
048648                FROM TEPLIS                                               
048649                WHERE  PARTN_NBR        = :DK-PARTITION                   
048650                  AND  STR_NBR          = :DK-STORE                       
048651                  AND  RGST_ID          = :DK-REGISTER                    
048652                  AND  TRN_NBR          = :DK-TRANS                       
048653                  AND  STRT_TM          = :DK-START-TIME                  
048654                  AND  SLS_CORR_SEQ_NBR = :DK-SALES-CORR-SEQ-NBR)         
048656                  AS VT6                                                  
048657          ON  VT6.PARTN_NBR             = VT5.PARTN_NBR                   
048658          AND VT6.STR_NBR               = VT5.STR_NBR                     
048659          AND VT6.RGST_ID               = VT5.RGST_ID                     
048660          AND VT6.TRN_NBR               = VT5.TRN_NBR                     
048661          AND VT6.STRT_TM               = VT5.STRT_TM                     
048662          AND VT6.SLS_CORR_SEQ_NBR      = VT5.SLS_CORR_SEQ_NBR            
048663          AND VT6.LNE_ITM_SEQ_NBR       = VT5.LNE_ITM_SEQ_NBR             
048664                                                                          
048665          ORDER BY 14                                                     
048700          WITH UR                                                         
048800     END-EXEC.                                                            
048900                                                                          
049047*----------------------------------------------------------------*        
049100*     THIS CURSOR WILL RETRIEVE TRANSACTION LEVEL DISCOUNTS      *        
049110*     FROM THE OPERATIONAL TABLES.                               *        
049200*----------------------------------------------------------------*        
049300                                                                          
049400     EXEC SQL                                                             
049500         DECLARE TRAN-DISCOUNT CURSOR FOR                                 
049600             SELECT  TLD_TYP_CDE                                          
049700                   , TLD_AMT                                              
049800                   , TLD_SEQ_NBR                                          
049900             FROM    TEPTLD                                               
050000             WHERE   PARTN_NBR        = :DK-PARTITION                     
050100               AND   STR_NBR          = :DK-STORE                         
050200               AND   RGST_ID          = :DK-REGISTER                      
050300               AND   TRN_NBR          = :DK-TRANS                         
050400               AND   STRT_TM          = :DK-START-TIME                    
050500               AND   SLS_CORR_SEQ_NBR = :DK-SALES-CORR-SEQ-NBR            
050600             ORDER BY TLD_SEQ_NBR                                         
050700             WITH UR                                                      
050800     END-EXEC.                                                            
050922                                                                          
051000 LINKAGE SECTION.                                                         
051100                                                                          
051200 01  DFHCOMMAREA.                                                         
051300     05  FILLER                         OCCURS  1 TO 4072 TIMES           
051400                                        DEPENDING ON EIBCALEN.            
051500         10  FILLER                     PIC X.                            
051600                                                                          
051700 EJECT                                                                    
051800 PROCEDURE DIVISION.                                                      
051900                                                                          
052000 0000-MAIN-MODULE.                                                        
052100                                                                          
052200     INITIALIZE DP030-CICS-API-FIELDS.                                    
052300     MOVE +1                      TO DP030-NUMBER-OF-MAPS.                
052400     MOVE PC-CURRENT-MAPSET-NAME  TO DP030-MAPSET-NAME.                   
052500     MOVE PC-CURRENT-MAP-NAME     TO DP030-MAP-NAME (1).                  
052600     SET DP030-RECEIVE-APPL-MAP   TO TRUE.                                
052700     MOVE LENGTH OF SA403AI       TO DP030-MAP-LENGTH (1).                
052800     MOVE 'PROGRAM ENTRY CALL'    TO DP013-MESSAGE-TEXT (1).              
052900     PERFORM 0001-CALL-CICS-ARCH-API.                                     
053000                                                                          
053100     PERFORM 1000-CONTROL-PROCESSING.                                     
053200                                                                          
053300     MOVE 'PROGRAM EXIT CALL'     TO DP013-MESSAGE-TEXT (1).              
053400     PERFORM 0001-CALL-CICS-ARCH-API.                                     
053500                                                                          
053600*----------------------------------------------------------------*        
053700*   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *        
053800*   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *        
053900*----------------------------------------------------------------*        
054000                                                                          
054100 0001-CALL-CICS-ARCH-API.                                                 
054200                                                                          
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
054400                                    DFHCOMMAREA                           
054500                                    DP030-CICS-API-FIELDS                 
054600                                    DP020-STANDARD-COMMAREA               
054700                                    SA403AI.                              
054800                                                                          
054900     IF  DP030-RC-CALL-SUCCESSFUL                                         
055000         CONTINUE                                                         
055100     ELSE                                                                 
055200         SET DP013-NO-ROLLBACK TO TRUE                                    
055300         SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
055400         SET DP013-CICS-ABEND TO TRUE                                     
055500         MOVE 'BEFORE 0000-MAIN-MODULE' TO DP013-PARAGRAPH                
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
055800         MOVE DP030-RETURN-CODE                                           
055900                               TO DP013-MESSAGE-TEXT (3)                  
056000         PERFORM DP013-0000-PROCESS-ABEND                                 
056100     END-IF.                                                              
056200                                                                          
056300*----------------------------------------------------------------*        
056400* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
056500* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
056600*                                                                *        
056700* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
056800*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
056900*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
057000*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
057100*----------------------------------------------------------------*        
057200                                                                          
057300 1000-CONTROL-PROCESSING.                                                 
057400                                                                          
057500     EVALUATE TRUE                                                        
057600         WHEN DP020-NEXT-ACT-INITIAL                                      
057700             INITIALIZE ASC-SPECIFIC-COMMAREA                             
057710             SET ASC-LEFT-VIEW      TO TRUE                               
057800             MOVE EIBTRMID          TO ASC-DTL-TERM-ID                    
057900             MOVE DP020-SRC-TASK-NUMBER                                   
058000                                    TO ASC-DTL-TASK-NUMBER                
058100             MOVE PC-DTL-QUEUE-SEQ  TO ASC-DTL-SEQ                        
058200             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
058300***-----$TEMPLATE NOTE$-------------------------------------------        
058400*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
058500*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
058600*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
058700***---------------------------------------------------------------        
058800             EVALUATE TRUE                                                
058900                 WHEN ASC-KOHLS-PAYMENT-TYPE                              
059000                     MOVE PC-TSYMSG-01985                                 
059100                             TO DP020-MSG-NUMBER                          
059200                     SET DP020-MSG-FATAL                                  
059300                             TO TRUE                                      
059400                     SET DP020-NEXT-ACT-APPL-ERROR                        
059500                               TO TRUE                                    
059600                     SET SA001-ERR (ASC-TRAN-SEL-SUB)                     
059700                               TO TRUE                                    
059800                     PERFORM 9200-REWRITE-SEL-QUEUE                       
059900                                                                          
060000                WHEN ASC-INVALID-TYPE                                     
060100                     MOVE PC-TSYMSG-02419                                 
060200                             TO DP020-MSG-NUMBER                          
060300                     SET DP020-MSG-FATAL                                  
060400                             TO TRUE                                      
060500                     SET DP020-NEXT-ACT-APPL-ERROR                        
060600                               TO TRUE                                    
060700                     SET SA001-ERR (ASC-TRAN-SEL-SUB)                     
060800                               TO TRUE                                    
060900                     PERFORM 9200-REWRITE-SEL-QUEUE                       
061000                                                                          
061100                WHEN OTHER                                                
061101                                                                          
061102* SET THE INQUIRY FLAG FOR THE SELECTED TRANSACTION.                      
061103                                                                          
061104                    SET SA001-INQ (ASC-TRAN-SEL-SUB)                      
061105                               TO TRUE                                    
061106                    PERFORM 9200-REWRITE-SEL-QUEUE                        
061107                    PERFORM 4000-BUILD-INITIAL-PANEL                      
061108             END-EVALUATE                                                 
061109                                                                          
061110         WHEN DP020-NEXT-ACT-READ-MAP                                     
061111             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
061112                               TO PV-TOP-ITEM                             
061113             PERFORM 2000-PROCESS-PANEL                                   
061200                                                                          
061300         WHEN DP020-NEXT-ACT-RETURN                                       
061400             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
061500                               TO PV-TOP-ITEM                             
061600             MOVE ZERO         TO ASC-ITEM-AT-TOP-OF-PANEL                
139444             IF ASC-KEY-STORE   = 873                                     
139569                CONTINUE                                                  
139570             ELSE                                                         
139580                MOVE SPACE     TO ADESCO                                  
139581             END-IF                                                       
061700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
061800                                                                          
061900         WHEN OTHER                                                       
062000             SET DP013-LOGIC-ABEND TO TRUE                                
062100             SET DP013-NO-ROLLBACK TO TRUE                                
062200             MOVE '1000-CONTROL-PROCESSING'                               
062300                               TO  DP013-PARAGRAPH                        
062400             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
062500                               TO  DP013-MESSAGE-TEXT(1)                  
062600             PERFORM DP013-0000-PROCESS-ABEND                             
062700     END-EVALUATE.                                                        
062800                                                                          
062900 EJECT                                                                    
063000*----------------------------------------------------------------*        
063100* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
063200* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
063300*----------------------------------------------------------------*        
063400                                                                          
063500 1100-PROCESS-INTER-APPL-COMM.                                            
063600                                                                          
063700     SET PS-COMMAREA-VALID     TO TRUE.                                   
063800     MOVE SPACE                TO ASC-START-ITEM-REQUEST.                 
063900     EVALUATE TRUE                                                        
064000         WHEN DP020-INT-APPL-FORMAT-IND                                   
064100                                   = PC-LIST-FORMAT                       
064200             MOVE SA004-NUMBER-OF-SELECTED-ITEMS                          
064300                                   TO ASC-NBR-OF-TRAN-SEL-ITEMS           
064400             MOVE SA004-NUMBER-OF-TS-ITEMS                                
064500                                   TO ASC-NBR-OF-TRAN-TS-ITEMS            
064600             MOVE SA004-SEL-QUEUE-NAME                                    
064700                                   TO ASC-TRAN-SEL-QUEUE-NAME             
064800             MOVE SA004-SALES-DATE-ISO                                    
064900                                   TO ASC-KEY-SALES-DATE-ISO              
064910                                      DK-SALES-DATE                       
065000             MOVE SA004-SALES-DATE-DISPLAY                                
065100                                   TO ASC-SALES-DATE-DISPLAY              
065200             PERFORM 1120-PREPARE-LIST-ENTRY                              
065300                                                                          
065400         WHEN OTHER                                                       
065500             SET DP013-LOGIC-ABEND TO TRUE                                
065600             SET DP013-NO-ROLLBACK TO TRUE                                
065700             MOVE '1100-PROCESS-INTER-APPL-COMM'                          
065800                                   TO DP013-PARAGRAPH                     
065900             MOVE 'INVALID INTER APPL FORMAT INDICATOR'                   
066000                                   TO DP013-MESSAGE-TEXT(1)               
066100             PERFORM DP013-0000-PROCESS-ABEND                             
066200                                                                          
066300     END-EVALUATE.                                                        
066400                                                                          
066500*----------------------------------------------------------------*        
066600*    GET READY TO START AT THE FIRST ITEM ON THE LIST.           *        
066700*----------------------------------------------------------------*        
066800                                                                          
066900 1120-PREPARE-LIST-ENTRY.                                                 
067000                                                                          
067100     MOVE SA001-MAX-ITEMS TO ASC-TRAN-SEL-SUB.                            
067200     MOVE ZERO            TO ASC-TRAN-SEL-ITEM.                           
067300     PERFORM 2140-GET-NEXT-MESSAGE.                                       
067400 EJECT                                                                    
067500*----------------------------------------------------------------*        
067600*    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED *        
067700*    ON WHAT FUNCTION KEY WAS SELECTED.                          *        
067800*----------------------------------------------------------------*        
067900                                                                          
068000 2000-PROCESS-PANEL.                                                      
068100                                                                          
068200     EVALUATE TRUE                                                        
068300         WHEN DP020-SRC-AID = DP016-CLEAR                                 
068400             MOVE ZERO TO ASC-ITEM-AT-TOP-OF-PANEL                        
068500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
068600             PERFORM 3200-RESET-ATTRIBUTES                                
068700                                                                          
068800         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
068810             SET ASC-LEFT-VIEW     TO TRUE                                
068900             MOVE ASC-KEY-PARTITION                                       
069000                                   TO DK-PARTITION                        
069100             MOVE ASC-KEY-STORE                                           
069200                                   TO DK-STORE                            
069300             MOVE ASC-KEY-REGISTER                                        
069400                                   TO DK-REGISTER                         
069500             MOVE ASC-KEY-TRANSACTION                                     
069600                                   TO DK-TRANS                            
069700             MOVE ASC-KEY-START-TIME                                      
069800                                   TO DK-START-TIME                       
069900             MOVE ASC-KEY-SALES-CORR-SEQ-NBR                              
070000                                   TO DK-SALES-CORR-SEQ-NBR               
070100             MOVE ASC-KEY-SALES-DATE-ISO                                  
070101                                   TO DK-SALES-DATE                       
070102             PERFORM 2050-DELETE-DETAIL-QUEUE                             
070200             PERFORM 4000-BUILD-INITIAL-PANEL                             
070300             PERFORM 3200-RESET-ATTRIBUTES                                
070400                                                                          
070500         WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                     
070600             IF  ASC-USE-SELECTION-QUEUE                                  
070700                 CONTINUE                                                 
070800                 PERFORM 2140-GET-NEXT-MESSAGE                            
070900                 IF PS-LIST-END                                           
071000                     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
071100                     SET PS-NO-LIST-END TO TRUE                           
071200                 ELSE                                                     
071300                     PERFORM 2050-DELETE-DETAIL-QUEUE                     
071400                     PERFORM 4000-BUILD-INITIAL-PANEL                     
071500                 END-IF                                                   
071600                 PERFORM 3200-RESET-ATTRIBUTES                            
071700             ELSE                                                         
071800                 MOVE PC-TSYMSG-00070                                     
071900                                  TO DP020-MSG-NUMBER                     
072000                 SET DP020-MSG-INFORMATIONAL TO TRUE                      
072100             END-IF                                                       
072200                                                                          
072300         WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)                     
072400             IF  ASC-USE-SELECTION-QUEUE                                  
072500                 PERFORM 2150-GET-PREV-MESSAGE                            
072600                 IF PS-LIST-END                                           
072700                     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
072800                     SET PS-NO-LIST-END TO TRUE                           
072900                 ELSE                                                     
073000                     PERFORM 2050-DELETE-DETAIL-QUEUE                     
073100                     PERFORM 4000-BUILD-INITIAL-PANEL                     
073200                 END-IF                                                   
073300                 PERFORM 3200-RESET-ATTRIBUTES                            
073400             ELSE                                                         
073500                 MOVE PC-TSYMSG-00069                                     
073600                                  TO DP020-MSG-NUMBER                     
073700                 SET DP020-MSG-INFORMATIONAL TO TRUE                      
073800             END-IF                                                       
073900                                                                          
074000         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
074100             MOVE +1 TO PV-TOP-ITEM                                       
074200             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
074300             PERFORM 3200-RESET-ATTRIBUTES                                
074400                                                                          
074500         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
074600             PERFORM 2110-BROWSE-BACKWARD                                 
074700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
074800             PERFORM 3200-RESET-ATTRIBUTES                                
074900                                                                          
075000         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
075100             PERFORM 2120-BROWSE-FORWARD                                  
075200             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
075300             PERFORM 3200-RESET-ATTRIBUTES                                
075400                                                                          
075500         WHEN DP020-SRC-AID = DP016-ENTER                                 
075600             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
075700             IF  PS-ERROR                                                 
075800                 CONTINUE                                                 
075900             ELSE                                                         
076000                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
076100                 PERFORM 3200-RESET-ATTRIBUTES                            
076200             END-IF                                                       
076300                                                                          
076400         WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                      
076500             SET DP030-CONTINUE-GO TO TRUE                                
076600                                                                          
138400         WHEN DP020-FK-LEFT (DP020-SRC-AID)                               
138500             SET ASC-LEFT-VIEW  TO TRUE                                   
138600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
138700                                                                          
138800         WHEN DP020-FK-RIGHT (DP020-SRC-AID)                              
138900             SET ASC-RIGHT-VIEW TO TRUE                                   
139000             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
139100                                                                          
139101         WHEN OTHER                                                       
139102             SET DP013-NO-ROLLBACK TO TRUE                                
139103             SET DP013-XCTL-DISPLAY-RESTART TO TRUE                       
139104             SET DP013-CICS-ABEND TO TRUE                                 
139105             MOVE '2100-CHECK-FUNCTION-KEY' TO DP013-PARAGRAPH            
139106             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
139107                                   TO DP013-MESSAGE-TEXT (1)              
139108             PERFORM DP013-0000-PROCESS-ABEND                             
139109                                                                          
139110     END-EVALUATE.                                                        
139111 EJECT                                                                    
139112*----------------------------------------------------------------*        
139113*    DELETE THE TEMPORARY STORAGE QUEUE.                         *        
139114*----------------------------------------------------------------*        
139115                                                                          
139116 2050-DELETE-DETAIL-QUEUE.                                                
139117                                                                          
139118     EXEC CICS                                                            
139119         DELETEQ TS                                                       
139120             QUEUE (ASC-DTL-QUEUE-NAME)                                   
139121             RESP  (PV-CICS-RESP)                                         
139122     END-EXEC.                                                            
139123                                                                          
139124     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
139125        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
139126         CONTINUE                                                         
139127     ELSE                                                                 
139128         SET DP013-CICS-ABEND TO TRUE                                     
139129         SET DP013-NO-ROLLBACK TO TRUE                                    
139130                                                                          
139131         MOVE '2050-DELETE-DETAIL-QUEUE' TO DP013-PARAGRAPH               
139132         MOVE 'ERROR TRYING TO DELETE DETAIL TEMP STORAGE QUEUE'          
139133                        TO DP013-MESSAGE-TEXT (1)                         
139134         PERFORM DP013-0000-PROCESS-ABEND                                 
139135     END-IF.                                                              
139136                                                                          
139137     MOVE +0 TO ASC-HIGHEST-BLOCK-WRITTEN.                                
139138 EJECT                                                                    
139139*----------------------------------------------------------------*        
139140*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
139141*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
139142*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
139143*    MESSAGE.                                                    *        
139144*----------------------------------------------------------------*        
139145                                                                          
139146 2110-BROWSE-BACKWARD.                                                    
139147                                                                          
139148     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
139149         GIVING PV-TOP-ITEM.                                              
139150                                                                          
139151     IF  PV-TOP-ITEM < +1                                                 
139152         MOVE +1              TO PV-TOP-ITEM                              
139153*        -- TOP OF LIST --                                                
139154         MOVE PC-TSYMSG-00069 TO DP020-MSG-NUMBER                         
139155         SET DP020-MSG-INFORMATIONAL TO TRUE                              
139156     END-IF.                                                              
139157                                                                          
139158 EJECT                                                                    
139159                                                                          
139160*----------------------------------------------------------------*        
139161*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
139162*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
139163*    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
139164*----------------------------------------------------------------*        
139165                                                                          
139166 2120-BROWSE-FORWARD.                                                     
139167                                                                          
139168     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
139169         GIVING PV-TOP-ITEM.                                              
139170     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
139171         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
139172                                   TO PV-TOP-ITEM                         
139173*        -- BOTTOM OF LIST --                                             
139174         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
139175         SET DP020-MSG-INFORMATIONAL                                      
139176                                   TO TRUE                                
139177     ELSE                                                                 
139178         COMPUTE PV-BOTTOM-ITEM =                                         
139179             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
139180         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
139181*            -- BOTTOM OF LIST --                                         
139182             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
139183             SET DP020-MSG-INFORMATIONAL                                  
139184                                   TO TRUE                                
139185         END-IF                                                           
139186     END-IF.                                                              
139187 EJECT                                                                    
139188*----------------------------------------------------------------*        
139189*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
139190*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
139191*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
139192*    RATHER THAN HITTING PF8 TO PAGE FORWARD TO THAT POINT.      *        
139193*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
139194*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
139195*----------------------------------------------------------------*        
139196                                                                          
139197 2130-CHECK-FOR-JUMP-BROWSE.                                              
139198                                                                          
139199     IF ASTRTITL > ZERO                                                   
139200         MOVE ASTRTITI             TO ASC-START-ITEM-REQUEST              
139201     ELSE                                                                 
139202         IF ASTRTITF = DP015-ERASE-EOF                                    
139203             INITIALIZE ASC-START-ITEM-REQUEST                            
139204         END-IF                                                           
139205     END-IF.                                                              
139206                                                                          
139207     SET PS-NO-ERROR               TO TRUE.                               
139208     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
139209         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
139210                                   TO PV-TOP-ITEM                         
139211     ELSE                                                                 
139212         MOVE ASC-START-ITEM-REQUEST                                      
139213                                   TO DP010I-UNEDITED-FIELD               
139214         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
139215         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
139216         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
139217                                   TO TRUE                                
139218         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
139218                         USING DP010I-NUMERIC-EDIT-AREA                   
139219                                                                          
139220         IF  NOT DP010I-ERROR-DETECTED                                    
139221             MOVE DP010I-NUMERIC-FIELD                                    
139222                                   TO  ASC-START-ITEM-REQUEST-N           
139223             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
139224                 IF  ASC-START-ITEM-REQUEST-N >                           
139225                                          ASC-NUMBER-OF-ITEMS-READ        
139226                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
139227                     COMPUTE PV-TOP-ITEM =                                
139228                             ASC-NUMBER-OF-ITEMS-READ                     
139229                             - PC-ITEMS-PER-PANEL + 1                     
139230                     IF  PV-TOP-ITEM < +1                                 
139231                         MOVE +1   TO PV-TOP-ITEM                         
139232                         MOVE PC-TSYMSG-00070                             
139233                                   TO DP020-MSG-NUMBER                    
139234                         SET DP020-MSG-INFORMATIONAL                      
139235                                   TO TRUE                                
139236                     END-IF                                               
139237                 ELSE                                                     
139238                     MOVE ASC-START-ITEM-REQUEST-N                        
139239                                   TO PV-TOP-ITEM                         
139240                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
139241                     COMPUTE PV-BOTTOM-ITEM =                             
139242                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
139243                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
139244*                        -- BOTTOM OF LIST --                             
139245                         MOVE PC-TSYMSG-00070                             
139246                                   TO DP020-MSG-NUMBER                    
139247                         SET DP020-MSG-INFORMATIONAL                      
139248                                   TO TRUE                                
139249                     END-IF                                               
139250                 END-IF                                                   
139251             ELSE                                                         
139252*                --- VALUE OUT OF RANGE ----                              
139253                 MOVE PC-TSYMSG-00101                                     
139254                                   TO DP020-MSG-NUMBER                    
139255                 MOVE -1           TO ASTRTITL                            
139256                 SET DP030-SET-CURSOR-APPL-1                              
139257                                   TO TRUE                                
139258                 MOVE DP015-REVERSE                                       
139259                                   TO ASTRTITH                            
139260                 MOVE DP015-RED    TO ASTRTITC                            
139261                 MOVE DP015-UNP-NUM-BRT-MDT                               
139262                                   TO ASTRTITA                            
139263                 SET DP020-MSG-FATAL                                      
139264                                   TO TRUE                                
139265                 SET PS-ERROR      TO TRUE                                
139266             END-IF                                                       
139267                                                                          
139268         ELSE                                                             
139269             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
139270             MOVE -1               TO ASTRTITL                            
139271             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
139272             MOVE DP015-REVERSE    TO ASTRTITH                            
139273             MOVE DP015-RED        TO ASTRTITC                            
139274             MOVE DP015-UNP-NUM-BRT-MDT                                   
139275                                   TO ASTRTITA                            
139276             SET DP020-MSG-FATAL   TO TRUE                                
139277             SET PS-ERROR          TO TRUE                                
139278         END-IF                                                           
139279     END-IF.                                                              
139280 EJECT                                                                    
139281                                                                          
139282*----------------------------------------------------------------*        
139283*    GET THE NEXT MESSAGE IF A LIST OF MESSAGES IS BEING         *        
139284*    PROCESSED.                                                  *        
139285*----------------------------------------------------------------*        
139286                                                                          
139287 2140-GET-NEXT-MESSAGE.                                                   
139288                                                                          
139289     ADD +1                       TO ASC-TRAN-SEL-SUB.                    
139290     IF  ASC-TRAN-SEL-SUB         > SA001-MAX-ITEMS                       
139291         ADD +1                   TO ASC-TRAN-SEL-ITEM                    
139292         MOVE +1                  TO ASC-TRAN-SEL-SUB                     
139293     END-IF.                                                              
139294     IF  ASC-TRAN-SEL-ITEM NOT > ASC-NBR-OF-TRAN-TS-ITEMS                 
139295         PERFORM 9100-READ-TRAN-SEL-QUEUE                                 
139296         IF  SA001-STORE (ASC-TRAN-SEL-SUB) > ZERO                        
139297             MOVE SA001-PARTITION (ASC-TRAN-SEL-SUB)                      
139298                                   TO DK-PARTITION                        
139299                                      ASC-KEY-PARTITION                   
139300             MOVE SA001-STORE (ASC-TRAN-SEL-SUB)                          
139301                                   TO ASC-KEY-STORE                       
139302                                      DK-STORE                            
139303             MOVE SA001-REGISTER (ASC-TRAN-SEL-SUB)                       
139304                                   TO ASC-KEY-REGISTER                    
139305                                      DK-REGISTER                         
139306             MOVE SA001-TRANS (ASC-TRAN-SEL-SUB)                          
139307                                   TO ASC-KEY-TRANSACTION                 
139308                                      DK-TRANS                            
139309             MOVE SA001-ID (ASC-TRAN-SEL-SUB)                             
139310                                   TO ASC-KEY-ID                          
139311             MOVE SA001-START-TIME (ASC-TRAN-SEL-SUB)                     
139312                                   TO ASC-KEY-START-TIME                  
139313                                      DK-START-TIME                       
139314             MOVE SA001-SALES-CORR-SEQ-NBR (ASC-TRAN-SEL-SUB)             
139315                                   TO ASC-KEY-SALES-CORR-SEQ-NBR          
139316                                      DK-SALES-CORR-SEQ-NBR               
139318             PERFORM 9000-SET-TRAN-TIME-DISPLAY                           
139319             MOVE SA001-TYPE (ASC-TRAN-SEL-SUB)                           
139320                                   TO ASC-KEY-TYPE                        
139321             SET SA001-INQ (ASC-TRAN-SEL-SUB)                             
139322                                   TO TRUE                                
139323             PERFORM 9200-REWRITE-SEL-QUEUE                               
139324         ELSE                                                             
139325             SUBTRACT +1 FROM ASC-TRAN-SEL-SUB                            
139326             MOVE PC-TSYMSG-00070 TO DP020-MSG-NUMBER                     
139327             SET DP020-MSG-INFORMATIONAL TO TRUE                          
139328             SET PS-LIST-END             TO TRUE                          
139329         END-IF                                                           
139330     ELSE                                                                 
139331         SUBTRACT +1 FROM ASC-TRAN-SEL-ITEM                               
139332         MOVE SA001-MAX-ITEMS     TO ASC-TRAN-SEL-SUB                     
139333         MOVE PC-TSYMSG-00070     TO DP020-MSG-NUMBER                     
139334         SET DP020-MSG-INFORMATIONAL TO TRUE                              
139335         SET PS-LIST-END             TO TRUE                              
139336     END-IF.                                                              
139337                                                                          
139338*----------------------------------------------------------------*        
139339*    GET THE PREVIOUS MESSAGE IF A LIST OF MESSAGES IS BEING     *        
139340*    PROCESSED.                                                  *        
139341*----------------------------------------------------------------*        
139342                                                                          
139343 2150-GET-PREV-MESSAGE.                                                   
139344                                                                          
139345     SUBTRACT +1 FROM ASC-TRAN-SEL-SUB.                                   
139346     IF  ASC-TRAN-SEL-SUB < +1                                            
139347         SUBTRACT +1 FROM ASC-TRAN-SEL-ITEM                               
139348         MOVE SA001-MAX-ITEMS     TO ASC-TRAN-SEL-SUB                     
139349     END-IF.                                                              
139350                                                                          
139351     IF  ASC-TRAN-SEL-ITEM > ZERO                                         
139352         PERFORM 9100-READ-TRAN-SEL-QUEUE                                 
139353         MOVE SA001-PARTITION (ASC-TRAN-SEL-SUB)                          
139354                                   TO DK-PARTITION                        
139355                                      ASC-KEY-PARTITION                   
139356         MOVE SA001-STORE (ASC-TRAN-SEL-SUB)                              
139357                                   TO ASC-KEY-STORE                       
139358                                      DK-STORE                            
139359         MOVE SA001-REGISTER (ASC-TRAN-SEL-SUB)                           
139360                                   TO ASC-KEY-REGISTER                    
139361                                      DK-REGISTER                         
139362         MOVE SA001-TRANS (ASC-TRAN-SEL-SUB)                              
139363                                   TO ASC-KEY-TRANSACTION                 
139364                                      DK-TRANS                            
139365         MOVE SA001-ID (ASC-TRAN-SEL-SUB)                                 
139366                                   TO ASC-KEY-ID                          
139367         MOVE SA001-START-TIME (ASC-TRAN-SEL-SUB)                         
139368                                   TO ASC-KEY-START-TIME                  
139369                                      DK-START-TIME                       
139370         MOVE SA001-SALES-CORR-SEQ-NBR (ASC-TRAN-SEL-SUB)                 
139371                                   TO ASC-KEY-SALES-CORR-SEQ-NBR          
139372                                      DK-SALES-CORR-SEQ-NBR               
139373     ELSE                                                                 
139374         MOVE +1                  TO ASC-TRAN-SEL-ITEM                    
139375                                     ASC-TRAN-SEL-SUB                     
139376         MOVE PC-TSYMSG-00069     TO DP020-MSG-NUMBER                     
139377         SET DP020-MSG-INFORMATIONAL TO TRUE                              
139378         SET PS-LIST-END             TO TRUE                              
139379     END-IF.                                                              
139380                                                                          
139381 EJECT                                                                    
139382*----------------------------------------------------------------*        
139383*    RESET ATTRIBUTES.                                           *        
139384*----------------------------------------------------------------*        
139385                                                                          
139386 3200-RESET-ATTRIBUTES.                                                   
139387                                                                          
139388     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
139389     MOVE DP015-GREEN              TO ASTRTITC.                           
139390     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
139391     MOVE -1                       TO ASTRTITL.                           
139392     SET DP030-SET-CURSOR-APPL-1   TO TRUE.                               
139393                                                                          
139394 EJECT                                                                    
139395                                                                          
139396*----------------------------------------------------------------*        
139397*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
139398*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
139399*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
139400*    DISPLAY THE PANEL TO USER.                                  *        
139401*----------------------------------------------------------------*        
139402                                                                          
139403 4000-BUILD-INITIAL-PANEL.                                                
139404                                                                          
139405     INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
139406                                 ASC-BLOCK-ENTRIES (2).                   
139407     MOVE +1                 TO  ASC-BLK-SUB.                             
139408     SET ASC-ITEMS-LEFT-ON-DB                                             
139409                             TO  TRUE.                                    
139410                                                                          
139411     MOVE +0                 TO  ASC-ITEM-SUB                             
139412                                 ASC-NUMBER-OF-ITEMS-READ                 
139413                                 ASC-NUMBER-OF-SELECTED-ITEMS             
139414                                 ASC-HIGHEST-BLOCK-WRITTEN                
139415                                 ASC-START-OF-SELECTED-RANGE              
139416                                 ASC-END-OF-SELECTED-RANGE                
139417                                 ASC-ITEM-AT-TOP-OF-PANEL.                
139418                                                                          
139419     PERFORM 4005-SELECT-ORDER-NBR.                                       
139420                                                                          
139421     PERFORM 4010-OPEN-CURSOR.                                            
139422                                                                          
139423     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
139424                                                                          
139425     PERFORM 4020-CLOSE-CURSOR.                                           
139426                                                                          
139427     MOVE +1                 TO  PV-TOP-ITEM.                             
139428     IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
139429         SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
139430         SET DP020-MSG-FATAL   TO  TRUE                                   
139431         MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
139432     ELSE                                                                 
139433         MOVE -1             TO ASTRTITL                                  
139434         SET DP030-SET-CURSOR-APPL-1 TO TRUE                              
139435         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
139436     END-IF.                                                              
139437 EJECT                                                                    
139438*----------------------------------------------------------------*        
139439* SELECT ORDER NUMBER FORM TEPORD.                               *        
139440*----------------------------------------------------------------*        
139441                                                                          
139442 4005-SELECT-ORDER-NBR.                                                   
139443                                                                          
139444     IF DK-STORE                    = 873                                 
139446        EXEC SQL                                                          
139447           SELECT     ORD_NBR                                             
139448             INTO :ASC-KEY-ORDER-NBR                                      
139449             FROM     TEPORD                                              
139450            WHERE     PARTN_NBR        = :DK-PARTITION                    
139460              AND     STR_NBR          = :DK-STORE                        
139470              AND     RGST_ID          = :DK-REGISTER                     
139480              AND     TRN_NBR          = :DK-TRANS                        
139490              AND     STRT_TM          = :DK-START-TIME                   
139500              AND     SLS_CORR_SEQ_NBR = :DK-SALES-CORR-SEQ-NBR           
139501                   WITH UR                                                
139502        END-EXEC                                                          
139554                                                                          
139555        EVALUATE TRUE                                                     
139556           WHEN SQLCODE = ZERO                                            
139557           WHEN SQLCODE = +100                                            
139558              CONTINUE                                                    
139559           WHEN OTHER                                                     
139560              MOVE '4005-SELECT-ORDER-NBR'                                
139561                                   TO DP013-PARAGRAPH                     
139562              MOVE 'SELECT ORDER NUMBER '                                 
139563                                   TO DP013-MESSAGE-TEXT (1)              
139564              MOVE SQLCA           TO DP013-SQLCA                         
139565              MOVE 'TSYMSG  '      TO DP013-DB2-TABLE-NAME (1)            
139566              SET DP013-DB2-ABEND                                         
139567                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
139568              PERFORM DP013-0000-PROCESS-ABEND                            
139569        END-EVALUATE                                                      
139570     ELSE                                                                 
139580        MOVE SPACE                 TO ADESCO                              
139581     END-IF.                                                              
139582                                                                          
139583*                                                                         
139584*----------------------------------------------------------------*        
139585* OPEN THE CURSOR.                                               *        
139586*----------------------------------------------------------------*        
139587                                                                          
139588 4010-OPEN-CURSOR.                                                        
139589                                                                          
139590     PERFORM                                                              
139591         WITH TEST AFTER                                                  
139592         VARYING PV-RETRY-COUNTER                                         
139593         FROM 1 BY 1                                                      
139594         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
139595                   OR (SQLCODE = ZERO))                                   
139596                                                                          
139597                 MOVE 'CURSOR:  ITEMS'                                    
139598                                   TO DP013-MESSAGE-TEXT (2)              
139600                 EXEC SQL                                                 
139700                    SELECT      ORD_NBR                                   
139800                      INTO :ASC-KEY-ORDER-NBR                             
139900                      FROM TEPORD                                         
140000                     WHERE PARTN_NBR = :DK-PARTITION                      
140100                       AND STR_NBR       = :DK-STORE                      
140200                       AND RGST_ID       = :DK-REGISTER                   
140300                       AND TRN_NBR       = :DK-TRANS                      
140400                       AND STRT_TM       = :DK-START-TIME                 
140500                       AND SLS_CORR_SEQ_NBR =                             
140501                                       :DK-SALES-CORR-SEQ-NBR             
140502                 END-EXEC                                                 
140503                 EXEC SQL                                                 
140504                      OPEN ITEMS                                          
140505                 END-EXEC                                                 
140511                                                                          
140512*        END-EVALUATE                                                     
140513                                                                          
140514         EVALUATE TRUE                                                    
140515             WHEN SQLCODE = ZERO                                          
140516             WHEN SQLCODE = -904                                          
140517             WHEN SQLCODE = -913                                          
140518                 CONTINUE                                                 
140519             WHEN SQLWARN0 NOT = SPACES                                   
140520             WHEN SQLCODE < ZERO                                          
140521             WHEN SQLCODE > ZERO                                          
140522                 MOVE '4010-OPEN-CURSOR'                                  
140523                                   TO DP013-PARAGRAPH                     
140524                 MOVE 'OPEN ITEMS CURSOR - '                              
140525                                   TO DP013-MESSAGE-TEXT (1)              
140526                 MOVE SQLCA        TO DP013-SQLCA                         
140527                 MOVE 'TSYMSG  '   TO DP013-DB2-TABLE-NAME (1)            
140528                 SET DP013-DB2-ABEND                                      
140529                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
140530                 PERFORM DP013-0000-PROCESS-ABEND                         
140531         END-EVALUATE                                                     
140532     END-PERFORM.                                                         
140533*                                                                         
140534     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
140535         MOVE '4010-OPEN-CURSOR'   TO DP013-PARAGRAPH                     
140536         MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'           
140537                                   TO DP013-MESSAGE-TEXT (1)              
140538         MOVE SQLCA                TO DP013-SQLCA                         
140539         MOVE 'TEPITM TEPLID '     TO DP013-DB2-TABLE-NAME (1)            
140540         SET DP013-DB2-ABEND                                              
140541             DP013-XCTL-DISPLAY-RESTART                                   
140542                                   TO TRUE                                
140543         PERFORM DP013-0000-PROCESS-ABEND                                 
140544     END-IF.                                                              
140545                                                                          
140546*----------------------------------------------------------------*        
140547* CLOSE THE CURSOR.                                              *        
140548*----------------------------------------------------------------*        
140549                                                                          
140550 4020-CLOSE-CURSOR.                                                       
140551                                                                          
140552     MOVE 'CURSOR:  ITEMS'         TO DP013-MESSAGE-TEXT (2).             
140554     EXEC SQL                                                             
140555         CLOSE ITEMS                                                      
140556     END-EXEC.                                                            
140562                                                                          
140563     EVALUATE TRUE                                                        
140564         WHEN SQLCODE = ZERO                                              
140565             CONTINUE                                                     
140566         WHEN SQLWARN0 NOT = SPACES                                       
140567         WHEN SQLCODE < ZERO                                              
140568         WHEN SQLCODE > ZERO                                              
140569             MOVE '4020-CLOSE-CURSOR'                                     
140570                               TO DP013-PARAGRAPH                         
140571             MOVE 'CLOSE ITEMS CURSOR '                                   
140572                               TO DP013-MESSAGE-TEXT (1)                  
140573             MOVE SQLCA        TO DP013-SQLCA                             
140574             MOVE 'TEPITM TEPLID ' TO DP013-DB2-TABLE-NAME (1)            
140575             SET DP013-DB2-ABEND                                          
140576                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
140577             PERFORM DP013-0000-PROCESS-ABEND                             
140578     END-EVALUATE.                                                        
140579                                                                          
140580*----------------------------------------------------------------*        
140581*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
140582*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
140583*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
140584*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
140585*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
140586*                                                                *        
140587*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
140588*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
140589*    UPON INITIALIZATION.                                        *        
140590*----------------------------------------------------------------*        
140591                                                                          
140592 4300-READ-ITEMS-FROM-DB.                                                 
140593                                                                          
140594     MOVE -99                      TO PV-HOLD-RCPT-SEQ-NBR                
140595     PERFORM                                                              
140596         VARYING PV-READ-COUNT                                            
140597         FROM 1 BY 1                                                      
140598         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
140599            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
140600                                                                          
140601         PERFORM 4310-FETCH                                               
140602                                                                          
140603         IF  ASC-ITEMS-LEFT-ON-DB AND (PV-HOLD-RCPT-SEQ-NBR               
140604                 NOT EQUAL EPITM-RCPT-SEQ-NBR)                            
140605             MOVE EPITM-RCPT-SEQ-NBR                                      
140606                                   TO PV-HOLD-RCPT-SEQ-NBR                
140607             PERFORM 4320-SET-BLK-ITEM-INFORMATION                        
140608             MOVE ASC-NUMBER-OF-ITEMS-READ                                
140609                                   TO ASC-LIST-ITEM-NUMBER                
140610                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
140615             MOVE EPITM-DEPT-NBR                                          
140616                                   TO ASC-DEPT                            
140617                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140615             MOVE EPITM-SUB-CL-NBR                                        
140616                                   TO ASC-CLASS                           
140617                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140615             MOVE EPITM-SKU-NBR                                           
140616                                   TO ASC-SKU                             
140617                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140617             IF EPITM-DEPT-NBR      = PC-GIFT-CARD-DEPT OR                
140617                                      PC-GIFT-CERT-DEPT                   
140617                 MOVE EPITM-MDSE-ID TO ASC-UPC-GIFT-CARD-NBR-N            
140617                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
140617             ELSE                                                         
140618                 MOVE EPITM-UPC-NBR    TO ASC-UPC-GIFT-CARD-NBR-N         
140619                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140619             END-IF                                                       
140620             MOVE EPITM-SLD-QTY                                           
140621                                   TO ASC-SOLD-QUANTITY-N                 
140622                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140623                                                                          
140624             EVALUATE EPITM-SRC-TYP-CDE                                   
140625                 WHEN 'K'                                                 
140626                 WHEN 'I'                                                 
140627                 WHEN '0'                                                 
140628                     SET ASC-ITEM-IS-KEYED                                
140629                           (ASC-BLK-SUB ASC-ITEM-SUB)                     
140630                                   TO TRUE                                
140640             END-EVALUATE                                                 
140641                                                                          
140642             IF EPITM-SLD-QTY   > +1                                      
140643                 DIVIDE EPITM-CUST-CHRGD-AMT                              
140644                   BY EPITM-SLD-QTY GIVING PV-CUST-CHRGD-AMT              
140645                 MOVE PV-CUST-CHRGD-AMT                                   
140646                                TO EPITM-CUST-CHRGD-AMT                   
140647             END-IF                                                       
140648             MOVE EPITM-CUST-CHRGD-AMT                                    
140649                                   TO ASC-CUST-CHRGD-AMT-N                
140650                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140651                                      PV-AMT-FORMATTED                    
140652             MOVE PV-AMT-FORMATTED TO ASC-CUST-CHRGD-AMT                  
140653                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140654             COMPUTE ASC-EXT-CHRGD-AMT-N                                  
140655                                   (ASC-BLK-SUB ASC-ITEM-SUB)             
140656                     = EPITM-CUST-CHRGD-AMT * EPITM-SLD-QTY               
140657             MOVE ASC-EXT-CHRGD-AMT-N (ASC-BLK-SUB ASC-ITEM-SUB)          
140658                                   TO PV-AMT-FORMATTED                    
140659             MOVE PV-AMT-FORMATTED TO ASC-EXT-CHRGD-AMT                   
140660                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140661                                                                          
140662             IF EPITM-LIV-RSLU-CDE = PC-RESOLVED-VOID                     
140663                 SET ASC-ACTION-ITM-COR (ASC-BLK-SUB ASC-ITEM-SUB)        
140664                     ASC-ACT-TEXT-ITM-COR                                 
140665                      (ASC-BLK-SUB ASC-ITEM-SUB)                          
140666                                   TO TRUE                                
140667             END-IF                                                       
140668                                                                          
140669             PERFORM 6000-GET-MDSE-DESCRIPTION                            
140670             MOVE XST00014-POS-MDSE-DESC TO ASC-MDSE-DESCRIPTION          
140671                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
140672                                                                          
140673         END-IF                                                           
140674                                                                          
140675* CHECK FOR APPLICABLE LINE ITEM DISCOUNTS                                
140676                                                                          
140677         IF ASC-ITEMS-LEFT-ON-DB AND (EPLID-LID-RSN-CDE NOT EQUAL         
140678             PC-NO-LINE-ITEM-DISCOUNT)                                    
140679             PERFORM 4320-SET-BLK-ITEM-INFORMATION                        
140680             MOVE ASC-NUMBER-OF-ITEMS-READ                                
140681                                   TO ASC-LIST-ITEM-NUMBER                
140682                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
140683             MOVE EPITM-DEPT-NBR                                          
140684                                   TO ASC-DEPT                            
140685                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140686             MOVE EPITM-SUB-CL-NBR                                        
140687                                   TO ASC-CLASS                           
140688                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140687             MOVE EPITM-SKU-NBR                                           
140688                                   TO ASC-SKU                             
140689                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140689             IF EPITM-DEPT-NBR      = PC-GIFT-CARD-DEPT OR                
140689                                      PC-GIFT-CERT-DEPT                   
140689                 MOVE EPITM-MDSE-ID TO ASC-UPC-GIFT-CARD-NBR-N            
140689                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
140689             ELSE                                                         
140690                 MOVE EPITM-UPC-NBR    TO ASC-UPC-GIFT-CARD-NBR-N         
140691                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140691             END-IF                                                       
140692             MOVE EPLID-LID-AMT    TO ASC-CUST-CHRGD-AMT-N                
140693                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140694                                      ASC-EXT-CHRGD-AMT-N                 
140695                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140696             MOVE ASC-CUST-CHRGD-AMT-N (ASC-BLK-SUB ASC-ITEM-SUB)         
140697                                   TO PV-AMT-FORMATTED                    
140698             MOVE PV-AMT-FORMATTED TO ASC-CUST-CHRGD-AMT                  
140699                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140700                                      ASC-EXT-CHRGD-AMT                   
140701                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
140702                                                                          
140703             EVALUATE EPLID-LID-RSN-CDE                                   
140703                WHEN PC-EMPLOYEE-DISCOUNT                                 
140704                   SET ASC-ACTION-EMP-DISC                                
140705                        (ASC-BLK-SUB ASC-ITEM-SUB)                        
140706                       ASC-ACT-TEXT-EMP                                   
140707                        (ASC-BLK-SUB ASC-ITEM-SUB)                        
140708                                   TO TRUE                                
140709                WHEN PC-SURCHARGE                                         
140710                   SET ASC-ACTION-SURCHG                                  
140711                        (ASC-BLK-SUB ASC-ITEM-SUB)                        
140712                       ASC-ACT-TEXT-SURCHG                                
140713                        (ASC-BLK-SUB ASC-ITEM-SUB)                        
140714                                   TO TRUE                                
140714                   ADD EPLID-LID-AMT TO PV-ECOM-SURCHARGE                 
140709                WHEN PC-WRAP                                              
140710                   SET ASC-ACTION-WRAP                                    
140711                        (ASC-BLK-SUB ASC-ITEM-SUB)                        
140712                       ASC-ACT-TEXT-WRAP                                  
140713                        (ASC-BLK-SUB ASC-ITEM-SUB)                        
140714                                   TO TRUE                                
                      WHEN PC-BOGO                                              
                      WHEN PC-BOGO-DLR-PCT-OFF                                  
                         SET ASC-ACTION-LID                                     
                              (ASC-BLK-SUB ASC-ITEM-SUB)                        
                             ASC-ACT-TEXT-LID                                   
                              (ASC-BLK-SUB ASC-ITEM-SUB)                        
                                         TO TRUE                                
                         SET ASC-BOGO-Y  (ASC-BLK-SUB ASC-ITEM-SUB)             
                                         TO TRUE                                
140709                WHEN OTHER                                                
140710                   SET ASC-ACTION-LID                                     
140711                        (ASC-BLK-SUB ASC-ITEM-SUB)                        
140712                       ASC-ACT-TEXT-LID                                   
140713                        (ASC-BLK-SUB ASC-ITEM-SUB)                        
140714                                   TO TRUE                                
                         SET ASC-BOGO-N  (ASC-BLK-SUB ASC-ITEM-SUB)             
                                         TO TRUE                                
140709             END-EVALUATE                                                 
140716         END-IF                                                           
140717     END-PERFORM.                                                         
140718                                                                          
140719     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
140720         PERFORM 4330-OPEN-TRAN-DISC-CURSOR                               
140721         PERFORM 4340-GET-TRAN-DISC-DATA                                  
140722         PERFORM 4350-CLOSE-TRAN-DISC-CURSOR                              
140723         PERFORM 4380-GET-SHIPPING-CHARGE                                 
140723         PERFORM 4360-GET-TRAN-TOTAL-AND-TAX                              
140724         MOVE +1               TO ASC-BLK-SUB                             
140725         PERFORM 5100-WRITE-TEMP-STORAGE                                  
140726         ADD +1                TO ASC-BLK-SUB                             
140727         PERFORM 5100-WRITE-TEMP-STORAGE                                  
140728                                                                          
140729         MOVE 1                TO ASC-BLK-SUB                             
140730                                  PV-BLOCK-NUMBER                         
140731         PERFORM 4410-READ-TEMP-STORAGE                                   
140732         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
140733             MOVE 2            TO ASC-BLK-SUB                             
140734                                  PV-BLOCK-NUMBER                         
140735             PERFORM 4410-READ-TEMP-STORAGE                               
140736         END-IF                                                           
140737     END-IF.                                                              
140738*----------------------------------------------------------------*        
140739*    FETCH THE TEXT CURSOR.                                      *        
140740*----------------------------------------------------------------*        
140741                                                                          
140742 4310-FETCH.                                                              
140743                                                                          
140745     EXEC SQL                                                             
140746          FETCH ITEMS                                                     
140747          INTO                                                            
140748                :EPITM-DEPT-NBR                                           
140749              , :EPITM-SUB-CL-NBR                                         
140750              , :EPITM-MAJ-CL-NBR                                         
140750              , :EPITM-SKU-NBR                                            
140751              , :EPITM-SLD-QTY                                            
140752              , :EPITM-UPC-NBR                                            
140752              , :EPITM-MDSE-ID                                            
140753              , :EPITM-SRC-TYP-CDE                                        
140754              , :EPITM-CUST-CHRGD-AMT                                     
140755              , :EPITM-LNE-ITM-SEQ-NBR                                    
140756              , :EPITM-LIV-RSLU-CDE                                       
140757              , :EPLID-LID-RSN-CDE                                        
140758              , :EPLID-LID-AMT                                            
140758              , :EPITM-RCPT-SEQ-NBR                                       
140759     END-EXEC.                                                            
140777                                                                          
140778     EVALUATE TRUE                                                        
140779         WHEN SQLCODE = ZERO                                              
140780         WHEN SQLCODE = +100                                              
140781              CONTINUE                                                    
140782         WHEN SQLWARN0 NOT = SPACE                                        
140783         WHEN SQLCODE NOT = ZERO                                          
140784              MOVE '4310-FETCH-CURSOR'                                    
140785                               TO DP013-PARAGRAPH                         
140786              MOVE 'FETCH TRANS CURSOR'                                   
140787                               TO DP013-MESSAGE-TEXT (1)                  
140788              MOVE SQLCA       TO DP013-SQLCA                             
140789              SET DP013-DB2-ABEND                                         
140790                               TO TRUE                                    
140791              PERFORM DP013-0000-PROCESS-ABEND                            
140792     END-EVALUATE.                                                        
140793                                                                          
140794     IF  SQLCODE EQUAL +100                                               
140795         SET ASC-NO-ITEMS-LEFT-ON-DB                                      
140796                                   TO TRUE                                
140797     END-IF.                                                              
140798                                                                          
140799*----------------------------------------------------------------*        
140800* SET THE SUBSCRIPTS WHEN BUIDLIGN ITEMS THAT WILL BE PUT INTO   *        
140801* THE ASC COMMAREA.  NOTE: IN A NORMAL LIST PROGRAM THIS         *        
140802* FUNCTIONALITY IS NORMALLY IN THE 4300 PARAGRAPH.  FOR THIS     *        
140803* UPDATEABLE LIST THIS FUNCTIONALIY NEEDED TO BE USED IN SEVERAL *        
140804* PLACES SO IT WAS BROKEN OUT INTO ITS OWN PARAGRAPH.            *        
140805*----------------------------------------------------------------*        
140806                                                                          
140807 4320-SET-BLK-ITEM-INFORMATION.                                           
140808                                                                          
140809     IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                                
140810         ADD +1                    TO ASC-ITEM-SUB                        
140811     ELSE                                                                 
140812         ADD +1                    TO ASC-BLK-SUB                         
140813         MOVE +1                   TO ASC-ITEM-SUB                        
140814                                                                          
140815         IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                         
140816             MOVE +1               TO ASC-BLK-SUB                         
140817             PERFORM 5100-WRITE-TEMP-STORAGE                              
140818             MOVE ASC-BLOCK-ENTRIES                                       
140819                                   (PC-MAX-BLOCKS-IN-ARRAY)               
140820                                 TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)        
140821             ADD +1                TO ASC-BLK-SUB                         
140822             INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                   
140823         END-IF                                                           
140824     END-IF.                                                              
140825                                                                          
140826     ADD +1                        TO ASC-NUMBER-OF-ITEMS-READ.           
140827                                                                          
140828*----------------------------------------------------------------*        
140829* OPEN THE CURSOR FOR TRANSACTION LEVEL DISCOUNTS                *        
140830*----------------------------------------------------------------*        
140831                                                                          
140832 4330-OPEN-TRAN-DISC-CURSOR.                                              
140833                                                                          
140834     PERFORM                                                              
140835         WITH TEST AFTER                                                  
140836         VARYING PV-RETRY-COUNTER                                         
140837         FROM 1 BY 1                                                      
140838         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
140839                   OR (SQLCODE = ZERO))                                   
140840                                                                          
140841                 MOVE 'CURSOR:  TRAN-DISCOUNT'                            
140842                                   TO DP013-MESSAGE-TEXT (2)              
140844                 EXEC SQL                                                 
140845                      OPEN TRAN-DISCOUNT                                  
140846                 END-EXEC                                                 
140852                                                                          
140853         EVALUATE TRUE                                                    
140854             WHEN SQLCODE = ZERO                                          
140900             WHEN SQLCODE = -904                                          
141000             WHEN SQLCODE = -913                                          
141100                 CONTINUE                                                 
141200             WHEN SQLWARN0 NOT = SPACES                                   
141300             WHEN SQLCODE < ZERO                                          
141400             WHEN SQLCODE > ZERO                                          
141500                 MOVE '4330-OPEN-TRAN-DISC-CURSOR'                        
141600                                   TO DP013-PARAGRAPH                     
141700                 MOVE 'OPEN TRAN DISC CURSOR - '                          
141800                                   TO DP013-MESSAGE-TEXT (1)              
141900                 MOVE SQLCA        TO DP013-SQLCA                         
142000                 MOVE 'TEPTLD  '   TO DP013-DB2-TABLE-NAME (1)            
142100                 SET DP013-DB2-ABEND                                      
142200                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
142300                 PERFORM DP013-0000-PROCESS-ABEND                         
142400         END-EVALUATE                                                     
142500     END-PERFORM.                                                         
142600*                                                                         
142700     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
142800         MOVE '4330-OPEN-TRAN-DISC-CURSOR'                                
142900                                   TO DP013-PARAGRAPH                     
143000         MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'           
143100                                   TO DP013-MESSAGE-TEXT (1)              
143200         MOVE SQLCA                TO DP013-SQLCA                         
143300         MOVE 'TEPTLD  '           TO DP013-DB2-TABLE-NAME (1)            
143400         SET DP013-DB2-ABEND                                              
143500             DP013-XCTL-DISPLAY-RESTART                                   
143600                                   TO TRUE                                
143700         PERFORM DP013-0000-PROCESS-ABEND                                 
143800     END-IF.                                                              
143900                                                                          
144000*----------------------------------------------------------------*        
144100*  READ THE TRANSACTION LEVEL DISCOUNT TABLE FOR DISCOUNTS       *        
144200*----------------------------------------------------------------*        
144300                                                                          
144400 4340-GET-TRAN-DISC-DATA.                                                 
144500                                                                          
144600     PERFORM                                                              
144700         WITH TEST AFTER                                                  
144800         VARYING PV-RETRY-COUNTER                                         
144900         FROM 1 BY 1                                                      
145000         UNTIL SQLCODE = +100                                             
145100                                                                          
145200         EXEC SQL                                                         
145300             FETCH TRAN-DISCOUNT                                          
145400             INTO                                                         
145500                   :EPTLD-TLD-TYP-CDE                                     
145600                 , :EPTLD-TLD-AMT                                         
145700                 , :EPTLD-TLD-SEQ-NBR                                     
145800         END-EXEC                                                         
145909                                                                          
146000     EVALUATE TRUE                                                        
146100         WHEN SQLCODE = ZERO                                              
146200             PERFORM 4320-SET-BLK-ITEM-INFORMATION                        
146300             MOVE ASC-NUMBER-OF-ITEMS-READ                                
146400                                   TO ASC-LIST-ITEM-NUMBER                
146500                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
146600             MOVE EPTLD-TLD-AMT    TO ASC-CUST-CHRGD-AMT-N                
146700                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
146800                                      ASC-EXT-CHRGD-AMT-N                 
146900                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
147000                                      PV-AMT-FORMATTED                    
147100             MOVE PV-AMT-FORMATTED TO ASC-CUST-CHRGD-AMT                  
147200                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
147300                                      ASC-EXT-CHRGD-AMT                   
147400                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
147500             EVALUATE TRUE                                                
147600             WHEN EPTLD-TLD-TYP-CDE  = 'D'                                
147700                 SET ASC-ACTION-TLD-D (ASC-BLK-SUB ASC-ITEM-SUB)          
147800                     ASC-ACT-TEXT-TLD (ASC-BLK-SUB ASC-ITEM-SUB)          
147900                                   TO TRUE                                
147600             WHEN EPTLD-TLD-TYP-CDE  = 'P'                                
147700                 SET ASC-ACTION-TLD-P (ASC-BLK-SUB ASC-ITEM-SUB)          
147800                     ASC-ACT-TEXT-TLD (ASC-BLK-SUB ASC-ITEM-SUB)          
147900                                   TO TRUE                                
148000             WHEN EPTLD-TLD-TYP-CDE  = 'T'                                
148100                 SET ASC-ACTION-TIERED (ASC-BLK-SUB ASC-ITEM-SUB)         
148200                     ASC-ACT-TEXT-TIERED                                  
148300                      (ASC-BLK-SUB ASC-ITEM-SUB)                          
148400                                   TO TRUE                                
148500             WHEN EPTLD-TLD-TYP-CDE  = 'S'                                
148600                 SET ASC-ACTION-SCD (ASC-BLK-SUB ASC-ITEM-SUB)            
148700                     ASC-ACT-TEXT-SCD (ASC-BLK-SUB ASC-ITEM-SUB)          
148800                                   TO TRUE                                
148900             END-EVALUATE                                                 
149000                                                                          
149100         WHEN SQLCODE = +100                                              
149200              CONTINUE                                                    
149300         WHEN SQLWARN0 NOT = SPACE                                        
149400         WHEN SQLCODE NOT = ZERO                                          
149500              MOVE '4310-FETCH-CURSOR'                                    
149600                               TO DP013-PARAGRAPH                         
149700              MOVE 'FETCH TRANS CURSOR'                                   
149800                               TO DP013-MESSAGE-TEXT (1)                  
149900              MOVE SQLCA       TO DP013-SQLCA                             
150000              SET DP013-DB2-ABEND                                         
150100                               TO TRUE                                    
150200              PERFORM DP013-0000-PROCESS-ABEND                            
150300     END-EVALUATE                                                         
150400     END-PERFORM.                                                         
150500                                                                          
150600*----------------------------------------------------------------*        
150700* CLOSE THE TRANSACTION DISCOUNT CURSOR                          *        
150800*----------------------------------------------------------------*        
150900                                                                          
151000 4350-CLOSE-TRAN-DISC-CURSOR.                                             
151100                                                                          
151200     MOVE 'CURSOR:  ITEMS'         TO DP013-MESSAGE-TEXT (2)              
151300                                                                          
151400     EXEC SQL                                                             
151500         CLOSE TRAN-DISCOUNT                                              
151600     END-EXEC.                                                            
151705                                                                          
151800     EVALUATE TRUE                                                        
151900         WHEN SQLCODE = ZERO                                              
152000             CONTINUE                                                     
152100         WHEN SQLWARN0 NOT = SPACES                                       
152200         WHEN SQLCODE < ZERO                                              
152300         WHEN SQLCODE > ZERO                                              
152400             MOVE '4350-CLOSE-TRAN-DISC-CURSOR'                           
152500                               TO DP013-PARAGRAPH                         
152600             MOVE 'CLOSE TRAN DISCOUNT CURSOR '                           
152700                               TO DP013-MESSAGE-TEXT (1)                  
152800             MOVE SQLCA        TO DP013-SQLCA                             
152900             MOVE 'TEPTLD  '   TO DP013-DB2-TABLE-NAME (1)                
153000             SET DP013-DB2-ABEND                                          
153100                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
153200             PERFORM DP013-0000-PROCESS-ABEND                             
153300     END-EVALUATE.                                                        
153400                                                                          
153500*----------------------------------------------------------------*        
153600* GET THE TOTAL AMOUNT OF THE TRANSACTION FROM THE TRANSACTION   *        
153700* TABLE AS WELL AS THE TAX                                       *        
153800*----------------------------------------------------------------*        
153900                                                                          
154000 4360-GET-TRAN-TOTAL-AND-TAX.                                             
154100                                                                          
154200     EXEC SQL                                                             
154300         SELECT TRN_TOT_AMT                                               
154400              , SLS_TX_AMT                                                
154500              , QUAL_CDE                                                  
154600         INTO :EPTRN-TRN-TOT-AMT                                          
154700            , :EPTRN-SLS-TX-AMT                                           
154800            , :EPTRN-QUAL-CDE                                             
154900         FROM TEPTRN                                                      
155000         WHERE PARTN_NBR           = :DK-PARTITION                        
155100           AND STR_NBR             = :DK-STORE                            
155200           AND RGST_ID             = :DK-REGISTER                         
155300           AND TRN_NBR             = :DK-TRANS                            
155400           AND STRT_TM             = :DK-START-TIME                       
155500           AND SLS_CORR_SEQ_NBR                                           
155510                               = :DK-SALES-CORR-SEQ-NBR                   
155510         WITH UR                                                          
155600     END-EXEC.                                                            
155719                                                                          
155800     EVALUATE TRUE                                                        
155900         WHEN SQLCODE = ZERO                                              
156000             MOVE EPTRN-TRN-TOT-AMT                                       
156100                                   TO ASC-TRAN-TOT-AMT-N                  
156200                                      PV-AMT-FORMATTED                    
156300             MOVE PV-AMT-FORMATTED TO ASC-TRAN-TOT-AMT                    
156400                                                                          
156500             IF EPTRN-SLS-TX-AMT   NOT EQUAL ZERO                         
156600                 PERFORM 4320-SET-BLK-ITEM-INFORMATION                    
156700                 MOVE ASC-NUMBER-OF-ITEMS-READ                            
156800                                   TO ASC-LIST-ITEM-NUMBER                
156900                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
157000                 MOVE EPTRN-SLS-TX-AMT                                    
157100                                   TO ASC-CUST-CHRGD-AMT-N                
157200                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
157300                                      ASC-EXT-CHRGD-AMT-N                 
157400                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
157500                                      PV-AMT-FORMATTED                    
157600                 MOVE PV-AMT-FORMATTED                                    
157700                                   TO ASC-CUST-CHRGD-AMT                  
157800                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
157900                                      ASC-EXT-CHRGD-AMT                   
158000                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
158100                 SET ASC-ACTION-TAX (ASC-BLK-SUB ASC-ITEM-SUB)            
158200                     ASC-ACT-TEXT-TAX (ASC-BLK-SUB ASC-ITEM-SUB)          
158300                                   TO TRUE                                
158400             END-IF                                                       
158500                                                                          
158600         WHEN SQLWARN0 NOT = SPACE                                        
158700         WHEN SQLCODE NOT = ZERO                                          
158800              MOVE '4330-GET-TRAN-TOTAL AND TAX'                          
158900                               TO DP013-PARAGRAPH                         
159000              MOVE 'GET TRAN TOTAL AND TAX'                               
159100                               TO DP013-MESSAGE-TEXT (1)                  
159200              MOVE SQLCA       TO DP013-SQLCA                             
159300              SET DP013-DB2-ABEND                                         
159400                               TO TRUE                                    
159500              PERFORM DP013-0000-PROCESS-ABEND                            
159600     END-EVALUATE.                                                        
159700                                                                          
153500*----------------------------------------------------------------*        
153600* GET AND CALCULATE TRANSACTON LEVEL SHIPPING CHARGES.                    
153800*----------------------------------------------------------------*        
153900                                                                          
154000 4380-GET-SHIPPING-CHARGE.                                                
154100                                                                          
154200     EXEC SQL                                                             
154300         SELECT SUM(SHIP_CHRG_AMT)                                        
154600           INTO :EPSHP-SHIP-CHRG-AMT                                      
154900         FROM TEPSHP                                                      
155000         WHERE PARTN_NBR           = :DK-PARTITION                        
155100           AND STR_NBR             = :DK-STORE                            
155200           AND RGST_ID             = :DK-REGISTER                         
155300           AND TRN_NBR             = :DK-TRANS                            
155400           AND STRT_TM             = :DK-START-TIME                       
155500           AND SLS_CORR_SEQ_NBR                                           
155510                               = :DK-SALES-CORR-SEQ-NBR                   
155510          WITH UR                                                         
155600     END-EXEC.                                                            
155719                                                                          
155800     EVALUATE TRUE                                                        
155800        WHEN SQLCODE = ZERO                                               
156000*****    SUBTRACT ITEM SURCHARGE FROM SHIPPING CHARGE    *****            
156000           COMPUTE PV-ECOM-SHIP-CHRG = EPSHP-SHIP-CHRG-AMT -              
156100                                       PV-ECOM-SURCHARGE                  
156400                                                                          
156600           PERFORM 4320-SET-BLK-ITEM-INFORMATION                          
156700           MOVE ASC-NUMBER-OF-ITEMS-READ                                  
156800                                   TO ASC-LIST-ITEM-NUMBER                
156900                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
157000           MOVE PV-ECOM-SHIP-CHRG                                         
157100                                   TO ASC-CUST-CHRGD-AMT-N                
157200                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
157300                                      ASC-EXT-CHRGD-AMT-N                 
157400                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
157500                                      PV-AMT-FORMATTED                    
157600           MOVE PV-AMT-FORMATTED                                          
157700                                   TO ASC-CUST-CHRGD-AMT                  
157800                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
157900                                      ASC-EXT-CHRGD-AMT                   
158000                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
158100           SET ASC-ACTION-SHIP (ASC-BLK-SUB ASC-ITEM-SUB)                 
158200               ASC-ACT-TEXT-SHIP (ASC-BLK-SUB ASC-ITEM-SUB)               
158300                                   TO TRUE                                
155800        WHEN SQLCODE = +100                                               
155800        WHEN SQLCODE = -305                                               
155800           CONTINUE                                                       
155800        WHEN OTHER                                                        
158800           MOVE '4380-GET-SHIPPING-CHARGE'                                
158900                               TO DP013-PARAGRAPH                         
159000           MOVE 'GET SHIPPING CHARGE'                                     
159100                               TO DP013-MESSAGE-TEXT (1)                  
159200           MOVE SQLCA            TO DP013-SQLCA                           
159300           SET DP013-DB2-ABEND   TO TRUE                                  
159500           PERFORM DP013-0000-PROCESS-ABEND                               
159600     END-EVALUATE.                                                        
159700                                                                          
159800*----------------------------------------------------------------*        
159900*    FORMAT THE MAP AND DETERMINE THE CURSOR POSITION.           *        
160000*----------------------------------------------------------------*        
160100                                                                          
160200 4400-MOVE-COMMAREA-TO-SCREEN.                                            
160300                                                                          
160400     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
160500         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
160600         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
160700             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
160800                 GIVING PV-BLOCK-NUMBER                                   
160900                 REMAINDER PV-REMAINDER                                   
161000             IF  PV-REMAINDER > ZERO                                      
161100                 ADD +1 TO PV-BLOCK-NUMBER                                
161200             END-IF                                                       
161300             MOVE +1    TO ASC-BLK-SUB                                    
161400             PERFORM 4410-READ-TEMP-STORAGE                               
161500             ADD +1     TO ASC-BLK-SUB                                    
161600                           PV-BLOCK-NUMBER                                
161700             PERFORM 4410-READ-TEMP-STORAGE                               
161800     END-IF.                                                              
161900     MOVE +1                       TO ASC-BLK-SUB.                        
162000     COMPUTE ASC-ITEM-SUB =                                               
162100         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
162200         + PC-ITEMS-PER-PANEL).                                           
162300                                                                          
162400     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
162500         ADD +1   TO ASC-BLK-SUB                                          
162600         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
162700     END-IF.                                                              
162800                                                                          
162900     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
163000         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
163100     PERFORM 4420-FORMAT-LIST-LINES                                       
163200         VARYING PV-SUB                                                   
163300         FROM PC-ITEMS-PER-PANEL BY -1                                    
163400         UNTIL PV-SUB < +1.                                               
163500                                                                          
163600     COMPUTE ASC-ITEMS-DISPLAYED =                                        
163700         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
163800     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
163900                                      ASTRTITO.                           
164000     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
164100     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
164200     MOVE ASC-SALES-DATE-DISPLAY   TO ASLSDTEO.                           
164300     MOVE ASC-KEY-STORE            TO ASTOREO.                            
164310     MOVE ASC-KEY-ORDER-NBR        TO AORDERO.                            
164400     MOVE ASC-KEY-REGISTER         TO AREGO.                              
164500     MOVE ASC-TIME-HH              TO ATIMEHHO.                           
164600     MOVE ASC-TIME-MM              TO ATIMEMMO.                           
164700     MOVE ASC-TIME-A-P             TO ATIMEAPO.                           
164800     MOVE ASC-KEY-TRANSACTION      TO ATRANSO.                            
164900     MOVE ASC-TRAN-TOT-AMT         TO ATOTALO.                            
165000                                                                          
165100     IF ASC-LEFT-VIEW                                                     
165200         MOVE HL-LEFT-HEADING      TO AHEADO                              
165300     ELSE                                                                 
165400         MOVE HL-RIGHT-HEADING     TO AHEADO                              
165500     END-IF.                                                              
165501                                                                          
165502     IF  DP020-MSG-NUMBER-X = SPACE                                       
165503         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
165504*            --- MORE ITEMS TO DISPLAY ----                               
165505             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
165506             SET DP020-MSG-INFORMATIONAL TO TRUE                          
165600         END-IF                                                           
165700     END-IF.                                                              
165800                                                                          
165900*----------------------------------------------------------------*        
166000*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
166100*----------------------------------------------------------------*        
166200                                                                          
166300 4410-READ-TEMP-STORAGE.                                                  
166400                                                                          
166500     EXEC CICS                                                            
166600         READQ TS                                                         
166700             QUEUE (ASC-DTL-QUEUE-NAME)                                   
166800             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
166900             ITEM  (PV-BLOCK-NUMBER)                                      
167000             RESP  (PV-CICS-RESP)                                         
167100     END-EXEC.                                                            
167200                                                                          
167300     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
167400       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
167500         CONTINUE                                                         
167600     ELSE                                                                 
167700         SET DP013-CICS-ABEND TO  TRUE                                    
167800         SET DP013-NO-ROLLBACK TO  TRUE                                   
167900         MOVE '4410-READ-TEMP-STORAGE'                                    
168000                               TO  DP013-PARAGRAPH                        
168100         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
168200                               TO  DP013-MESSAGE-TEXT(1)                  
168300         PERFORM DP013-0000-PROCESS-ABEND                                 
168400     END-IF.                                                              
168500                                                                          
168600     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
168700         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
168800     END-IF.                                                              
168900 EJECT                                                                    
169000*----------------------------------------------------------------*        
169100*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
169200*----------------------------------------------------------------*        
169300                                                                          
169400 4420-FORMAT-LIST-LINES.                                                  
169500                                                                          
169600     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
169700         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
169800         MOVE SPACE              TO MR-SCROLLABLE-AREA (PV-SUB)           
169900     ELSE                                                                 
170000                                                                          
170100*** THE IF STATEMENT BELOW IS COMMENTED OUT SO THAT THE SCREEN            
170200*** ALWAYS GETS REBUILT.  THIS ALLOWS THE LEFT AND RIGHT                  
170300*** FUNCTION KEYS TO OPERATE CORRECTLY.                                   
170301                                                                          
170302*        IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
170303           MOVE SPACES TO DL-DETAIL-LINE                                  
170304           IF ASC-LEFT-VIEW                                               
170305             MOVE ASC-SKU (ASC-BLK-SUB ASC-ITEM-SUB)                      
170306                                   TO DL-SKU-L                            
170307             MOVE ASC-UPC-GIFT-CARD-NBR (ASC-BLK-SUB ASC-ITEM-SUB)        
170400                                   TO DL-UPC-GC-L                         
170500             MOVE ASC-SOLD-QUANTITY (ASC-BLK-SUB ASC-ITEM-SUB)            
170600                                   TO DL-UNITS-L                          
170700             MOVE ASC-KEYED-IND (ASC-BLK-SUB ASC-ITEM-SUB)                
170701                                   TO DL-KEYED-IND-L                      
170702             MOVE ASC-CUST-CHRGD-AMT                                      
170800                   (ASC-BLK-SUB ASC-ITEM-SUB)                             
170900                                   TO DL-AMOUNT-L                         
171000             MOVE ASC-EXT-CHRGD-AMT (ASC-BLK-SUB ASC-ITEM-SUB)            
171100                                   TO DL-EXT-AMOUNT-L                     
                   IF ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB) = 'C'               
                      MOVE 'D'           TO DL-ACTION-CDE-L                     
                   ELSE                                                         
171200                MOVE ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB)                
171300                                   TO DL-ACTION-CDE-L                     
                   END-IF                                                       
171400             MOVE ASC-ACTION-TEXT (ASC-BLK-SUB ASC-ITEM-SUB)              
171500                                   TO DL-ACTION-TEXT-L                    
171600           ELSE                                                           
171700                                                                          
171800             IF ASC-MDSE-DESCRIPTION (ASC-BLK-SUB ASC-ITEM-SUB)           
171900                                   NOT EQUAL SPACES                       
172000                 MOVE ASC-MDSE-DESCRIPTION                                
172100                   (ASC-BLK-SUB ASC-ITEM-SUB)                             
172200                                   TO DL-MDSE-DESCRIPTION-R               
172300             ELSE                                                         
172400                 EVALUATE TRUE                                            
172500                     WHEN ASC-ACTION-SURCHG                               
172600                       (ASC-BLK-SUB ASC-ITEM-SUB)                         
172700                         MOVE 'SHPING SURCHARGE'                          
172800                                   TO DL-MDSE-DESCRIPTION-R               
172500                     WHEN ASC-ACTION-WRAP                                 
172600                       (ASC-BLK-SUB ASC-ITEM-SUB)                         
172700                         MOVE 'GIFT WRAP CHARGE'                          
172800                                   TO DL-MDSE-DESCRIPTION-R               
172500                     WHEN ASC-ACTION-SHIP                                 
172600                       (ASC-BLK-SUB ASC-ITEM-SUB)                         
172700                         MOVE 'TRAN LVL SHP CHRG'                         
172800                                   TO DL-MDSE-DESCRIPTION-R               
172500                     WHEN ASC-ACTION-TAX                                  
172600                       (ASC-BLK-SUB ASC-ITEM-SUB)                         
172700                         MOVE 'TAX'                                       
172800                                   TO DL-MDSE-DESCRIPTION-R               
172900                     WHEN ASC-ACTION-TLD-D                                
173000                            (ASC-BLK-SUB ASC-ITEM-SUB)                    
173100                              MOVE '$ OFF TLD'                            
173200                                   TO DL-MDSE-DESCRIPTION-R               
172900                     WHEN ASC-ACTION-TLD-P                                
173000                            (ASC-BLK-SUB ASC-ITEM-SUB)                    
173100                              MOVE '% OFF TLD'                            
173200                                   TO DL-MDSE-DESCRIPTION-R               
173300                     WHEN ASC-ACTION-TIERED                               
173400                            (ASC-BLK-SUB ASC-ITEM-SUB)                    
173500                              MOVE 'TIERED DISCOUNT'                      
173600                                   TO DL-MDSE-DESCRIPTION-R               
173700                     WHEN ASC-ACTION-EMP-DISC                             
173800                            (ASC-BLK-SUB ASC-ITEM-SUB)                    
173900                              MOVE 'EMPLOYEE LID'                         
174000                                   TO DL-MDSE-DESCRIPTION-R               
174100                     WHEN ASC-ACTION-LID                                  
174200                            (ASC-BLK-SUB ASC-ITEM-SUB)                    
                                    IF ASC-BOGO-Y                               
174200                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
                                       MOVE 'BOGO - LID'                        
                                         TO DL-MDSE-DESCRIPTION-R               
                                    ELSE                                        
174300                                 MOVE 'LID'                               
174400                                   TO DL-MDSE-DESCRIPTION-R               
                                    END-IF                                      
174500                     WHEN ASC-ACTION-SCD                                  
174600                            (ASC-BLK-SUB ASC-ITEM-SUB)                    
174700                              MOVE 'SENIOR DISCOUNT'                      
174800                                   TO DL-MDSE-DESCRIPTION-R               
174900                     WHEN ASC-ACTION-ITM-COR                              
175000                            (ASC-BLK-SUB ASC-ITEM-SUB)                    
175100                              MOVE 'ITEM CORRECT'                         
175200                                   TO DL-MDSE-DESCRIPTION-R               
175300                     WHEN OTHER                                           
175400                         IF ASC-DEPT (ASC-BLK-SUB ASC-ITEM-SUB)           
175500                                   = PC-GIFT-CARD-DEPT                    
175600                             MOVE 'GIFT CARD'                             
175700                                   TO DL-MDSE-DESCRIPTION-R               
175800                         ELSE                                             
175900                             IF ASC-DEPT                                  
176000                                 (ASC-BLK-SUB ASC-ITEM-SUB)               
176100                                   = PC-GIFT-CERT-DEPT                    
176200                                 MOVE 'GIFT CERTIFICATE'                  
176300                                   TO DL-MDSE-DESCRIPTION-R               
176400                             ELSE                                         
176500                                 MOVE 'MERCHANDISE'                       
176600                                   TO DL-MDSE-DESCRIPTION-R               
176700                             END-IF                                       
176800                         END-IF                                           
176900                 END-EVALUATE                                             
177000             END-IF                                                       
177100                                                                          
177200             MOVE ASC-SKU (ASC-BLK-SUB ASC-ITEM-SUB)                      
177300                                   TO DL-SKU-R                            
177400             MOVE ASC-SOLD-QUANTITY (ASC-BLK-SUB ASC-ITEM-SUB)            
177500                                   TO DL-UNITS-R                          
177600             MOVE ASC-EXT-CHRGD-AMT (ASC-BLK-SUB ASC-ITEM-SUB)            
177700                                   TO DL-EXT-AMOUNT-R                     
177800           END-IF                                                         
177801           MOVE DL-DETAIL-LINE   TO MR-SCROLLABLE-AREA (PV-SUB)           
177802*        END-IF                                                           
177803     END-IF.                                                              
177804     SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
177805     IF  ASC-ITEM-SUB < + 1                                               
177806         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
177807         MOVE +1                 TO ASC-BLK-SUB                           
177808     END-IF.                                                              
177809                                                                          
177810*----------------------------------------------------------------*        
177811*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
177812*----------------------------------------------------------------*        
177813                                                                          
177814 5100-WRITE-TEMP-STORAGE.                                                 
177815                                                                          
177816     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
177817                               TO  PV-LIST-ITEM-NUMBER.                   
177818     COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                                   
177819         ASC-HIGHEST-BLOCK-WRITTEN + 1.                                   
177820                                                                          
177821     EXEC CICS                                                            
177822         WRITEQ TS                                                        
177823             QUEUE (ASC-DTL-QUEUE-NAME)                                   
177824             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
177825             ITEM  (PV-HIGHEST-BLOCK-WRITTEN)                             
177826             RESP  (PV-CICS-RESP)                                         
177827     END-EXEC.                                                            
177828                                                                          
177829     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
177830         SET DP013-CICS-ABEND                                             
177831                       TO  TRUE                                           
177832         SET DP013-NO-ROLLBACK                                            
177833                       TO  TRUE                                           
177834         MOVE '5100-WRITE-TEMP-STORAGE'                                   
177835                       TO  DP013-PARAGRAPH                                
177836         MOVE 'ATTEMPTING TO WRITE TEMP STORAGE LIST BLOCK'               
177837                       TO  DP013-MESSAGE-TEXT(1)                          
177838         PERFORM DP013-0000-PROCESS-ABEND                                 
177839     END-IF.                                                              
177840     ADD +1            TO  ASC-HIGHEST-BLOCK-WRITTEN.                     
177841                                                                          
177842 EJECT                                                                    
177843                                                                          
177844*----------------------------------------------------------------*        
177845*  GET THE MERCHANDISE DESCRIPTION THAT APPEARED ON THE RECIPT   *        
177846*----------------------------------------------------------------*        
177847 6000-GET-MDSE-DESCRIPTION.                                               
177848                                                                          
177849     MOVE EPITM-DEPT-NBR           TO XST00014-DEPT-NBR.                  
177850     MOVE EPITM-SUB-CL-NBR         TO XST00014-SUB-CL-NBR.                
177860     MOVE EPITM-MAJ-CL-NBR         TO XST00014-MAJ-CL-NBR.                
177870                                                                          
177880     EXEC SQL                                                             
177890         SELECT POS_MDSE_DESC                                             
177900         INTO   :XST00014-POS-MDSE-DESC                                   
178000         FROM   XST_SUB_CL                                                
178300         WHERE  DEPT_NBR           = :XST00014-DEPT-NBR                   
178350           AND  MAJ_CL_NBR         = :XST00014-MAJ-CL-NBR                 
178400           AND  SUB_CL_NBR         = :XST00014-SUB-CL-NBR                 
178800         WITH UR                                                          
178900     END-EXEC.                                                            
178901                                                                          
178902     EVALUATE TRUE                                                        
178903         WHEN SQLCODE = ZERO                                              
178904             CONTINUE                                                     
178905         WHEN SQLCODE = +100                                              
178906             MOVE SPACES           TO XST00014-POS-MDSE-DESC              
178907         WHEN SQLWARN0 NOT = SPACES                                       
178908         WHEN SQLCODE < ZERO                                              
178909         WHEN SQLCODE > ZERO                                              
178910             MOVE '6000-GET-MDSE-DESCRIPTION'                             
178911                               TO DP013-PARAGRAPH                         
178912             MOVE 'SELECT MDSE DESCRIPTION'                               
178913                               TO DP013-MESSAGE-TEXT (1)                  
178914             MOVE SQLCA        TO DP013-SQLCA                             
178915             MOVE 'XST_SUB_CL ' TO DP013-DB2-TABLE-NAME (1)               
178917             SET DP013-DB2-ABEND                                          
178918                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
178919             PERFORM DP013-0000-PROCESS-ABEND                             
178920     END-EVALUATE.                                                        
178930                                                                          
179521*----------------------------------------------------------------*        
179522*  THE TIME THAT IS DISPLAYED ON THE SCREEN SHOULD BE IN THE     *        
179523*  FOLLOWING FORMAT, HH : MM A/P.  THIS DETERMINES THE DISPLAY   *        
179524*----------------------------------------------------------------*        
179525                                                                          
179526 9000-SET-TRAN-TIME-DISPLAY.                                              
179527                                                                          
179528     MOVE ASC-KEY-START-TIME       TO PV-TIME.                            
179529     IF PV-TIME-HH                 >= 12                                  
179530         MOVE 'P'                  TO PV-TIME-A-P                         
179531     ELSE                                                                 
179532         MOVE 'A'                  TO PV-TIME-A-P                         
179533     END-IF.                                                              
179534                                                                          
179535     IF PV-TIME-HH                 >  12                                  
179536         SUBTRACT 12 FROM PV-TIME-HH                                      
179537     END-IF.                                                              
179538                                                                          
179539     MOVE PV-TIME-HH               TO ASC-TIME-HH.                        
179540     MOVE PV-TIME-MM               TO ASC-TIME-MM.                        
179541     MOVE PV-TIME-A-P              TO ASC-TIME-A-P.                       
179542                                                                          
179543*----------------------------------------------------------------*        
179544* READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
179545*----------------------------------------------------------------*        
179546                                                                          
179547 9100-READ-TRAN-SEL-QUEUE.                                                
179548                                                                          
179549     MOVE +1                       TO ASC-TRAN-SEL-ITEM.                  
179550     MOVE +1                       TO ASC-TRAN-SEL-SUB.                   
179551                                                                          
179552     EXEC CICS                                                            
179553         READQ TS                                                         
179554             QUEUE (ASC-TRAN-SEL-QUEUE-NAME)                              
179555             INTO  (SA001-TRANSACTION-SEL-REC)                            
179556             ITEM  (ASC-TRAN-SEL-ITEM)                                    
179557             RESP  (PV-CICS-RESP)                                         
179600     END-EXEC.                                                            
179700                                                                          
179800     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
179900         CONTINUE                                                         
180000     ELSE                                                                 
180100         SET DP013-LOGIC-ABEND                                            
180200             DP013-NO-ROLLBACK     TO TRUE                                
180300         MOVE '9100-READ-SEL-QUEUE'                                       
180400                                   TO DP013-PARAGRAPH                     
180500         MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'           
180600                                   TO DP013-MESSAGE-TEXT (1)              
180700         PERFORM DP013-0000-PROCESS-ABEND                                 
180800     END-IF.                                                              
180900*----------------------------------------------------------------*        
181000* UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
181100*----------------------------------------------------------------*        
181200 9200-REWRITE-SEL-QUEUE.                                                  
181300     EXEC CICS                                                            
181400          WRITEQ TS                                                       
181500              QUEUE  (ASC-TRAN-SEL-QUEUE-NAME)                            
181600              FROM   (SA001-TRANSACTION-SEL-REC)                          
181700              ITEM   (ASC-TRAN-SEL-ITEM)                                  
181800              RESP   (PV-CICS-RESP)                                       
181900              REWRITE                                                     
182000     END-EXEC.                                                            
182100                                                                          
182200     IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                           
182300         SET DP013-CICS-ABEND                                             
182400             DP013-NO-ROLLBACK     TO TRUE                                
182500         MOVE '9200-REWRITE-SEL-QUEUE'                                    
182600                                   TO DP013-PARAGRAPH                     
182700         MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'           
182800                                   TO DP013-MESSAGE-TEXT (1)              
182900         PERFORM DP013-0000-PROCESS-ABEND                                 
183000     END-IF.                                                              
183100                                                                          
183200 EJECT                                                                    
183300*----------------------------------------------------------------*        
183400*    ABEND PROCESSOR MODULE                                      *        
183500*----------------------------------------------------------------*        
183600                                                                          
183700     COPY DPPD013.                                                        
