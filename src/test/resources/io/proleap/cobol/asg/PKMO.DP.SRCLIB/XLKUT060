000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400 PROGRAM-ID.    XLKUT060.                                                 
000500 AUTHOR.        DEANNA BRANTNER.                                          
000600 INSTALLATION.  KOHLS.                                                    
000700 DATE-WRITTEN   APRIL, 2003.                                              
000800 DATE-COMPILED.                                                           
000900*****************************************************************         
001000*  THIS PROGRAM WILL REFORMAT THE LEGACY TABLE:  TPOAGMT        *         
001100*  TO PO DOMAIN TEMPLATES:  XXT_PO_DISC                                   
      * ****************************************************************        
      * CHANGE DESCRIPTION                                                      
      * ****************************************************************        
      * DATE  05/12/2010                                                        
      *  CHANGE MADE: ADDED A NEW IR INDICATOR(WHSE_RPLNT_IND) IN               
      *  TPURCHS TABLE UNDER IMPORT REPLENISHMENT PROJECT. CHANGES MADE         
      *  ACCORDINGLY FOR THE SAME.                                              
      * MADE BY: MONIKA SACHDEVA         TKID: TKMAW78                          
      * CHANGES TAGGED AS MS 05/12/2010                                         
001400*****************************************************************         
001500 ENVIRONMENT DIVISION.                                                    
001600 INPUT-OUTPUT SECTION.                                                    
001700                                                                          
001800 FILE-CONTROL.                                                            
001900                                                                          
002000     SELECT TPURCHS-FILE                                                  
002010         ASSIGN TO INP01.                                                 
002100                                                                          
002000     SELECT TPOAGMT-FILE                                                  
002010         ASSIGN TO INP02.                                                 
002100                                                                          
002110     SELECT OUTPUT-FILE                                                   
002120         ASSIGN TO OUT01.                                                 
002130                                                                          
002200*****************************************************************         
002300 DATA DIVISION.                                                           
002400*****************************************************************         
002500 FILE SECTION.                                                            
002600                                                                          
002700 FD  TPURCHS-FILE                                                         
002800     RECORDING MODE IS F                                                  
002810     BLOCK CONTAINS 0 RECORDS                                             
002900     LABEL RECORDS ARE STANDARD.                                          
003000                                                                          
      ** CHANGE THE RECORD LENGTH TO ACCOMODATE THE NEW INDICATOR **            
      ** MS 05/12/2010 BEG                                                      
003300*01  TPURCHS-RECORD               PIC X(481).                             
       01  TPURCHS-RECORD               PIC X(482).                             
      ** MS 05/12/2010 END                                                      
003700                                                                          
002700 FD  TPOAGMT-FILE                                                         
002800     RECORDING MODE IS F                                                  
002810     BLOCK CONTAINS 0 RECORDS                                             
002900     LABEL RECORDS ARE STANDARD.                                          
003000                                                                          
003300 01  TPOAGMT-RECORD               PIC X(099).                             
003700                                                                          
003710 FD  OUTPUT-FILE                                                          
003720     RECORDING MODE IS F                                                  
003730     BLOCK CONTAINS 0 RECORDS                                             
003740     LABEL RECORDS ARE STANDARD.                                          
003750                                                                          
003760 01  OUTPUT-RECORD                PIC X(40).                              
003770                                                                          
003900*****************************************************************         
004000 WORKING-STORAGE SECTION.                                                 
004100                                                                          
004200 01  PS-PROGRAM-SWITCHES.                                                 
004300     05  PS-EOF-TPURCHS-SW            PIC X(01)  VALUE 'N'.               
004400         88  PS-EOF-TPURCHS-YES                  VALUE 'Y'.               
004500         88  PS-EOF-TPURCHS-NO                   VALUE 'N'.               
004300     05  PS-EOF-TPOAGMT-SW            PIC X(01)  VALUE 'N'.               
004400         88  PS-EOF-TPOAGMT-YES                  VALUE 'Y'.               
004500         88  PS-EOF-TPOAGMT-NO                   VALUE 'N'.               
004300     05  PS-TPOAGMT-FOUND-SW          PIC X(01)  VALUE 'N'.               
004400         88  PS-TPOAGMT-FOUND                    VALUE 'Y'.               
004500         88  PS-TPOAGMT-NOT-FOUND                VALUE 'N'.               
004600                                                                          
004700 01  PC-PROGRAM-CONSTANTS.                                                
004800     05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'XLKUT060'.        
005000                                                                          
005100 01  PV-PROGRAM-VARIABLES.                                                
005900     05  PV-RECS-READ-TPURCHS         PIC  9(09) VALUE  0.                
005900     05  PV-RECS-READ-TPOAGMT         PIC  9(09) VALUE  0.                
006000     05  PV-RECORDS-WRITTEN           PIC  9(09) VALUE  0.                
007000                                                                          
007100 01  PV-ABEND-FIELDS.                                                     
007400     05  PV-ABEND-PROGRAM             PIC  X(08) VALUE 'XLKUT060'.        
007700     05  FILLER                       PIC  X(01) VALUE SPACES.            
007700     05  PV-ABEND-TABLE               PIC  X(12) VALUE SPACES.            
007700     05  FILLER                       PIC  X(01) VALUE SPACES.            
007500     05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.            
007700     05  FILLER                       PIC  X(01) VALUE SPACES.            
007600     05  PV-ABEND-OPERATION           PIC  X(50) VALUE SPACES.            
008200                                                                          
008200                                                                          
008400***********************************************************               
008500*  COPYBOOK USED FOR THE TPURCHS INPUT FILE               *               
008600***********************************************************               
002500     EXEC SQL                                                             
002600         INCLUDE TPURCHS                                                  
002700     END-EXEC.                                                            
002800                                                                          
008400***********************************************************               
008500*  COPYBOOK USED FOR THE TPOAGMT INPUT FILE               *               
008600***********************************************************               
002500     EXEC SQL                                                             
002600         INCLUDE TPOAGMT                                                  
002700     END-EXEC.                                                            
002800                                                                          
008900***********************************************************               
009000*  COPYBOOK USED FOR THE LOGISTIC SKU OUTPUT FILE         *               
009100***********************************************************               
       01  PC-OUTPUT-RECORD.                                                    
009200     COPY XLRD017.                                                        
009300                                                                          
010800*****************************************************************         
010900 PROCEDURE DIVISION.                                                      
011000*****************************************************************         
011100     DISPLAY '**** BEGINNING XLKUT060 ****'.                              
011200     DISPLAY ' '.                                                         
011300                                                                          
011400     OPEN INPUT  TPURCHS-FILE                                             
                       TPOAGMT-FILE                                             
011410          OUTPUT OUTPUT-FILE.                                             
011500                                                                          
012100     PERFORM 1000-PROCESS-INPUT-FILE                                      
012200       UNTIL PS-EOF-TPURCHS-YES.                                          
012300                                                                          
012310     CLOSE TPURCHS-FILE                                                   
                 TPOAGMT-FILE                                                   
012320           OUTPUT-FILE.                                                   
012500                                                                          
012600     DISPLAY '   INPUT TPURCHS RECS    = ' PV-RECS-READ-TPURCHS.          
012600     DISPLAY '   INPUT TPOAGMT RECS    = ' PV-RECS-READ-TPOAGMT.          
012700     DISPLAY '  OUTPUT XLRD017 RECS    = ' PV-RECORDS-WRITTEN.            
012800     DISPLAY ' '.                                                         
012900     DISPLAY '****  END OF  XLKUT060  ****'.                              
013000                                                                          
013100     GOBACK.                                                              
013200                                                                          
015200*****************************************************************         
015300*  PROCESS INPUT FILE                                           *         
015400*****************************************************************         
       1000-PROCESS-INPUT-FILE.                                                 
                                                                                
           MOVE '1000-PROCESS-INPUT-FILE ' TO PV-ABEND-PARAGRAPH.               
                                                                                
           PERFORM 2000-READ-TPURCHS-INPUT-FILE.                                
                                                                                
           IF PS-EOF-TPURCHS-YES                                                
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 2100-READ-OTHER-FILES                                    
           END-IF.                                                              
                                                                                
013400*****************************************************************         
013500*  READ A TPURCHS RECORD                                                  
013600*****************************************************************         
       2000-READ-TPURCHS-INPUT-FILE.                                            
                                                                                
           MOVE '2000-READ-TPURCHS-INPUT-FILE' TO PV-ABEND-PARAGRAPH.           
                                                                                
           INITIALIZE XL017-DATA-AREA.                                          
           INITIALIZE DCLTPURCHS.                                               
                                                                                
           READ TPURCHS-FILE                                                    
                INTO DCLTPURCHS                                                 
                  AT END                                                        
                     SET PS-EOF-TPURCHS-YES TO TRUE                             
           END-READ.                                                            
                                                                                
           IF PS-EOF-TPURCHS-YES                                                
              CONTINUE                                                          
           ELSE                                                                 
              MOVE PURCHS-PO-NBR  TO XL017-PO-NBR                               
              ADD +1 TO PV-RECS-READ-TPURCHS                                    
           END-IF.                                                              
                                                                                
013400*****************************************************************         
013500*  READ OTHER FILES                                                       
013600*****************************************************************         
       2100-READ-OTHER-FILES.                                                   
                                                                                
           IF PURCHS-PO-NBR > POAGMT-PO-NBR                                     
               PERFORM 3000-READ-TPOAGMT-INPUT-FILE                             
                         UNTIL PS-TPOAGMT-FOUND                                 
           ELSE                                                                 
               IF PURCHS-PO-NBR = POAGMT-PO-NBR                                 
                   PERFORM 3100-MOVE-TPOAGMT-INFO                               
                   PERFORM 3000-READ-TPOAGMT-INPUT-FILE                         
                         UNTIL PS-TPOAGMT-FOUND                                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE 'N'  TO PS-TPOAGMT-FOUND-SW.                                    
                                                                                
      *****************************************************************         
      *  READ A TPOAGMT RECORD                                                  
      *****************************************************************         
       3000-READ-TPOAGMT-INPUT-FILE.                                            
                                                                                
           MOVE '3000-READ-TPOAGMT-INPUT-FILE' TO PV-ABEND-PARAGRAPH.           
                                                                                
           IF PS-EOF-TPOAGMT-YES                                                
               SET PS-TPOAGMT-FOUND TO TRUE                                     
           ELSE                                                                 
               INITIALIZE DCLTPOAGMT                                            
               READ TPOAGMT-FILE                                                
                    INTO DCLTPOAGMT                                             
                      AT END                                                    
                         SET PS-EOF-TPOAGMT-YES TO TRUE                         
               END-READ                                                         
               ADD +1 TO PV-RECS-READ-TPOAGMT                                   
               IF PURCHS-PO-NBR  =  POAGMT-PO-NBR                               
                   PERFORM 3100-MOVE-TPOAGMT-INFO                               
               ELSE                                                             
                   IF PURCHS-PO-NBR  <  POAGMT-PO-NBR                           
                       SET PS-TPOAGMT-FOUND TO TRUE                             
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      *  MOVE TPOAGMT INFO TO EXTRACT FILE                            *         
      *****************************************************************         
       3100-MOVE-TPOAGMT-INFO.                                                  
                                                                                
           MOVE '3100-MOVE-TPOAGMT-INFO ' TO PV-ABEND-PARAGRAPH.                
                                                                                
           MOVE POAGMT-PO-NBR  TO  XL017-PO-NBR.                                
           EVALUATE POAGMT-AGRMT-TYP-ID                                         
               WHEN '5'                                                         
                   EVALUATE POAGMT-TYP-CDE                                      
                       WHEN 7                                                   
                           MOVE 'MOS'           TO XL017-DISC-TYPE-CDE          
                           IF POAGMT-DISC-PCT > 0                               
                               COMPUTE XL017-DISC-PCT =                         
                                               POAGMT-DISC-PCT * 100            
                           ELSE                                                 
                               MOVE 0 TO  XL017-DISC-PCT                        
                           END-IF                                               
                           PERFORM 6000-WRITE-OUTPUT                            
                       WHEN 8                                                   
                           MOVE 'TRADE'         TO XL017-DISC-TYPE-CDE          
                           IF POAGMT-DISC-PCT > 0                               
                               COMPUTE XL017-DISC-PCT =                         
                                               POAGMT-DISC-PCT * 100            
                           ELSE                                                 
                               MOVE 0 TO  XL017-DISC-PCT                        
                           END-IF                                               
                           PERFORM 6000-WRITE-OUTPUT                            
                       WHEN 9                                                   
                           MOVE 'ADVRT'         TO XL017-DISC-TYPE-CDE          
                           IF POAGMT-DISC-PCT > 0                               
                               COMPUTE XL017-DISC-PCT =                         
                                               POAGMT-DISC-PCT * 100            
                           ELSE                                                 
                               MOVE 0 TO  XL017-DISC-PCT                        
                           END-IF                                               
                           PERFORM 6000-WRITE-OUTPUT                            
                       WHEN 10                                                  
                           MOVE 'ADCOOP'        TO XL017-DISC-TYPE-CDE          
                           IF POAGMT-DISC-PCT > 0                               
                               COMPUTE XL017-DISC-PCT =                         
                                               POAGMT-DISC-PCT * 100            
                           ELSE                                                 
                               MOVE 0 TO  XL017-DISC-PCT                        
                           END-IF                                               
                           PERFORM 6000-WRITE-OUTPUT                            
                       WHEN 11                                                  
                           MOVE 'PROAST'        TO XL017-DISC-TYPE-CDE          
                           IF POAGMT-DISC-PCT > 0                               
                               COMPUTE XL017-DISC-PCT =                         
                                               POAGMT-DISC-PCT * 100            
                           ELSE                                                 
                               MOVE 0 TO  XL017-DISC-PCT                        
                           END-IF                                               
                           PERFORM 6000-WRITE-OUTPUT                            
                       WHEN 12                                                  
                           MOVE 'TANDT '        TO XL017-DISC-TYPE-CDE          
                           MOVE POAGMT-DISC-PAY-DAY-QTY TO                      
                                                   XL017-EXTD-DAYS-QTY          
                           IF POAGMT-DISC-PCT > 0                               
                               COMPUTE XL017-DISC-PCT =                         
                                               POAGMT-DISC-PCT * 100            
                           ELSE                                                 
                               MOVE 0 TO  XL017-DISC-PCT                        
                           END-IF                                               
                           PERFORM 6000-WRITE-OUTPUT                            
                       WHEN 13                                                  
                           MOVE 'BRIDAL'        TO XL017-DISC-TYPE-CDE          
                           IF POAGMT-DISC-PCT > 0                               
                               COMPUTE XL017-DISC-PCT =                         
                                               POAGMT-DISC-PCT * 100            
                           ELSE                                                 
                               MOVE 0 TO  XL017-DISC-PCT                        
                           END-IF                                               
                           PERFORM 6000-WRITE-OUTPUT                            
                       WHEN 1                                                   
                           MOVE 'VNDADD'        TO XL017-DISC-TYPE-CDE          
                           MOVE POAGMT-DISC-PAY-DAY-QTY TO                      
                                             XL017-EXTD-DAYS-QTY                
                           IF POAGMT-DISC-PCT > 0                               
                               COMPUTE XL017-DISC-PCT =                         
                                               POAGMT-DISC-PCT * 100            
                           ELSE                                                 
                               MOVE 0 TO  XL017-DISC-PCT                        
                           END-IF                                               
                           PERFORM 6000-WRITE-OUTPUT                            
                   END-EVALUATE                                                 
               WHEN '2'                                                         
                   EVALUATE POAGMT-TYP-CDE                                      
                       WHEN 0                                                   
                           MOVE 'NWSTR'   TO XL017-DISC-TYPE-CDE                
                           MOVE POAGMT-DISC-PAY-DAY-QTY TO                      
                                             XL017-EXTD-DAYS-QTY                
                           IF POAGMT-DISC-PCT > 0                               
                               COMPUTE XL017-DISC-PCT =                         
                                               POAGMT-DISC-PCT * 100            
                           ELSE                                                 
                               MOVE 0 TO  XL017-DISC-PCT                        
                           END-IF                                               
                           PERFORM 6000-WRITE-OUTPUT                            
                   END-EVALUATE                                                 
           END-EVALUATE.                                                        
                                                                                
           INITIALIZE XL017-DATA-AREA.                                          
                                                                                
021600*****************************************************************         
021700*  WRITE A RECORDS TO THE LOGISTICS SKU OUTPUT FILE             *         
021800*****************************************************************         
021900 6000-WRITE-OUTPUT.                                                       
022000                                                                          
022010     WRITE OUTPUT-RECORD FROM                                             
022020         XL017-RECORD                                                     
022030     END-WRITE.                                                           
024400     ADD +1                 TO PV-RECORDS-WRITTEN.                        
027200                                                                          
