000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.             SUKRF710.                                        
000400 AUTHOR.                 JIM ARENDT.                                      
000500 INSTALLATION.           KOHLS.                                           
000600 DATE-WRITTEN            OCTOBER, 2013.                                   
000700 DATE-COMPILED.                                                           
000800                                                                          
001600******************************************************************        
000800                                                                          
000900* THIS PROGRAM DISPLAYS THE UPC INFORMATION SCREEN THAT WILL BE           
001000* CHECKED WHEN DOING AN RFID AUDIT.                                       
001100                                                                          
001600******************************************************************        
001600* 10/10/13 - JIM ARENDT   - CHG0068598                           *        
001600* - INITIAL IMPLEMENTATION.                                      *        
      * 04/14/14 RLK BEG                                                        
001600* 04/14/14 - RICH KELLEN  - CHG0079586                           *        
001600* - CHANGED LOGIC SO A NEW TSUAUPC ROW IS CREATED                *        
001600* - WHEN A UPC MISPACK, TYPE 38, IS CREATED. THIS NEW ROW WILL   *        
001600* - HAVE SCANNED AND TAGGED QTYS WILL HAVE 0 AND THE 38 TSUEXCP  *        
001600* - ROW WILL HAVE THE SAME KEY AS NEW TSUAUPC ROW. THE           *        
001600* - SU006-SCN-TMST WILL NOT BE UPDATED WITH THE VALUE OF THE NEW *        
001600* - TSUAUPC ROW AS THE ORIGINAL ROW CREATED IN THE PROGRAM WILL  *        
001600* - HAVE CORRECT SCANNED AND TAGGED QTYS AND THAT IS WHAT LATER  *        
001600* - PROGRAMS WILL UPDATE AS AUDIT PROGRESSES. MOST OF THE CHANGES*        
001600* - ARE IN PARA 1500-CHECK-EXCEPTIONS, BUT, SOME ARE IN THE      *        
001600* - THE INITIALIZATION LOGIC IN 0000-PROGRAM-MAINLINE.           *        
      * 04/14/14 RLK END                                                        
001600* 02/14/23 - JEAN KURK    - CHG0281369                           *        
001600* - REMOVED COPYBOOK FOR UPC CHECK DIGIT ROUTINE                 *        
001600******************************************************************        
002100                                                                          
002200 ENVIRONMENT DIVISION.                                                    
002300 DATA DIVISION.                                                           
002400 WORKING-STORAGE SECTION.                                                 
002500                                                                          
002600 01  CA-COMMAREA.                                                         
002700     COPY SUWS006.                                                        
002800* * * * * * * * * * * *  PLEASE NOTE * * * * * * * * * * * *              
002900* THE COMMAREA FROM HERE....                                              
003000    05  CA-PROGRAM-COMMAREA.                                              
003100      07  CA-LOOKUP-COMMAREA.                                             
003200        10 CA-SCANNED-UPC                PIC  X(15).                      
003300        10 CA-RETURN-MSG                 PIC  X(19).                      
003400        10 CA-RETURN-ERROR-IND           PIC  X(01) VALUE 'N'.            
003500           88  CA-RETURN-ERROR                      VALUE 'Y'.            
003600           88  CA-NO-RETURN-ERROR                   VALUE 'N'.            
003700* ...TO HERE IS COPIED TO SUKRF518/521/524...                             
003800      07  CA-DETAIL-COMMAREA.                                             
003900        10 CA-RETAIL-X                   PIC  X(08).                      
004000        10 CA-RETAIL REDEFINES CA-RETAIL-X                                
004100                                         PIC  ZZZZ9.99.                   
004200        10 CA-SKU-DESC                   PIC  X(19).                      
004300        10 CA-COLOR                      PIC  X(10).                      
004400        10 CA-SIZE                       PIC  X(10).                      
004500        10 CA-VENDOR-STYLE               PIC  X(19).                      
004600* ...AND TO HERE IS COPIED TO SUKRF711/522...                             
004700      07  CA-VS-COMMAREA.                                                 
004800        10 CA-VENDOR-ENT-ID              PIC S9(09) COMP.                 
004900        10 CA-VENDOR-VS-NBR              PIC  X(20).                      
005000        10 CA-VENDOR-VS-ID               PIC S9(09) COMP.                 
005100        10 CA-INTR-UPC-ID                PIC S9(09) COMP.                 
005200        10 CA-USE-UPC-IND                PIC  X(01).                      
005300        10 FILLER                        PIC  X(01) VALUE 'N'.            
005700* ...AND TO HERE IS COPIED TO SUKRF514/532/533...                         
005800* IF YOU CHANGE IT (THIS COMMAREA) PLEASE VERIFY THAT THE COMMAREA        
005900* PASSED TO THOSE PROGRAMS MATCHES                                        
006000      07  CA-SCREEN-COMMAREA.                                             
006100        10 CA-INTERNAL-UPC-X             PIC  X(15).                      
006200        10 CA-INTERNAL-UPC  REDEFINES CA-INTERNAL-UPC-X                   
006300                                         PIC  9(15).                      
006400        10 CA-VENDOR-NAME                PIC  X(11).                      
006500        10 CA-ITEM-LOOKUP-IND            PIC  X(01) VALUE ' '.            
006600           88 CA-UPC-FOUND                          VALUE '1'.            
006700           88 CA-UPC-NOT-FOUND                      VALUE '2'.            
006800           88 CA-SKU-FOUND                          VALUE '3'.            
006900           88 CA-SKU-NOT-FOUND                      VALUE '4'.            
007000        10 CA-F4-IND                     PIC  X(01) VALUE 'N'.            
007100           88 CA-F4-NOT-PRESSED-YET                 VALUE 'N'.            
007200           88 CA-F4-PRESSED                         VALUE 'Y'.            
007300         10 CA-PO-NBR                    PIC S9(09) COMP.                 
007400         10 CA-PO-LNE-NBR                PIC S9(09) COMP.                 
007500         10 CA-HANG-FOLD                 PIC  X(01).                      
007600         10 CA-GOH-IND                   PIC  X(01).                      
007700                                                                          
007800 01  DK-DB2-KEYS.                                                         
007900     05  DK-UPC-NUMBER                   PIC S9(15)  COMP-3.              
008100     05  DK-DEST-NBR                     PIC S9(04)  COMP.                
008900                                                                          
009000 01  PC-PROGRAM-CONSTANTS.                                                
009100     05  PC-MAX-RETRIES                  PIC S9(04)  VALUE 3              
009200                                                     COMP.                
009300     05  PC-CARTON-AUDIT-GROUP-CODE      PIC  X(03)  VALUE 'VA'.          
009400     05  PC-DMGD-MDSE-VILN-SEQ           PIC S9(04)  VALUE 20             
009500                                                     COMP.                
009900     05  PC-P                            PIC  X(01)  VALUE 'P'.           
010000     05  PC-O                            PIC  X(01)  VALUE 'O'.           
010100     05  PC-S                            PIC  X(01)  VALUE 'S'.           
010200     05  PC-CURRENT-PROGRAM-NAME         PIC  X(08)  VALUE                
010300                                                     'SUKRF710'.          
010400     05  PC-MESSAGE-SCREEN-PROGRAM-NAME  PIC  X(08)  VALUE                
010500                                                     'SUKRF514'.          
010600     05  PC-DATA-INFO-PROGRAM-NAME       PIC  X(08)  VALUE                
010700                                                     'SUKRF519'.          
010800     05  PC-UPC-SCAN-PROGRAM-NAME        PIC  X(08)  VALUE                
010900                                                     'SUKRF518'.          
011000     05  PC-SKU-SCAN-PROGRAM-NAME        PIC  X(08)  VALUE                
011100                                                     'SUKRF521'.          
011400     05  PC-LOCATION-MODULE              PIC  X(08)  VALUE                
011500                                                     'XLKUT071'.          
012000     05  PC-TIX-NO-MTCH-PROGRAM-NAME     PIC  X(08)  VALUE                
012100                                                     'SUKRF711'.          
012200     05  PC-UPC-KEY-QTY-PROGRAM          PIC  X(08)  VALUE                
012300                                                     'SUKRF537'.          
012400     05  PC-CURRENT-TRANID               PIC  X(04)  VALUE 'S710'.        
012500     05  PC-INITIAL-KEYS-MSG             PIC  X(19)  VALUE                
012800         'F2-OK  F9-NO MATCH'.                                            
012700     05  PC-AFTER-DAMAGE-KEYS-MSG        PIC  X(19)  VALUE                
012800         'F2-OK  F9-NO MATCH'.                                            
012900     05  PC-TRANS-UNAVAILABLE-MSG        PIC  X(19)  VALUE                
013000         'TRANS NOT AVAILABLE'.                                           
013100                                                                          
013200 01  PS-PROGRAM-SWITCHES.                                                 
013300     05  PS-DATABASE-CONTENTION-SW       PIC  X(01)  VALUE 'N'.           
013400         88  PS-DATABASE-CONTENTION-NO               VALUE 'N'.           
013500         88  PS-DATABASE-CONTENTION-YES              VALUE 'Y'.           
014200     05  PS-DEST-TYPE-IND                PIC  X(01)  VALUE ' '.           
014300         88  PS-DEST-IS-A-DC                         VALUE 'D'.           
014400         88  PS-DEST-IS-A-STR                        VALUE 'S'.           
014500     05  PS-EOC-PO-LINE-CURSOR-IND       PIC  X(01)  VALUE 'N'.           
014600         88  PS-NOT-EOC-PO-LINE-CURSOR               VALUE 'N'.           
014700         88  PS-EOC-PO-LINE-CURSOR                   VALUE 'Y'.           
014800     05  PS-DONE-IND                     PIC  X(01)  VALUE 'N'.           
014900         88  PS-NOT-DONE                             VALUE 'N'.           
015000         88  PS-DONE                                 VALUE 'Y'.           
017500     05  PS-BLACKOUT-SW                  PIC  X(01)  VALUE 'N'.           
017600         88  PS-BLACKOUT-YES                         VALUE 'Y'.           
017700         88  PS-BLACKOUT-NO                          VALUE 'N'.           
017800                                                                          
017900 01  PV-PROGRAM-VARIABLES.                                                
018000     05  PV-DISPLAY-KEYS.                                                 
018100         10 FILLER                       PIC  X(12)  VALUE                
018200            'KEY VALUES: '.                                               
018300         10 PV-DISP-AUD-CNTL-NBR         PIC  9(09).                      
018400         10 FILLER                       PIC  X(03)  VALUE ' - '.         
018600         10 PV-DISP-SHIPT-UNT-ID         PIC  9(18).                      
018700         10 FILLER                       PIC  X(03)  VALUE ' - '.         
018900         10 PV-DISP-UPC-NBR              PIC  9(15).                      
019200     05  PV-DISP-SCN-TMST                PIC  X(26).                      
019300     05  PV-DISP-SKU-SQL-CDE             PIC  9(09).                      
019400     05  PV-DISP-VS-SQL-CDE              PIC  9(09).                      
019500     05  PV-DISP-SKU-STR-SQL-CDE         PIC  9(09).                      
019700     05  PV-DISP-UPC-SQL-CDE             PIC  9(09).                      
018900     05  PV-DISP-VALUE                   PIC  9(15).                      
012300     05  PV-SCND-UNT-QTY                 PIC S9(09)  VALUE ZERO           
012400                                                     COMP.                
020000     05  PV-RETRY-COUNTER                PIC S9(04)  VALUE ZERO.          
020100     05  PV-COMMAREA-LENGTH              PIC S9(04)  VALUE ZERO           
020200                                                     COMP SYNC.           
020500     05  PV-CICS-RESPONSE                PIC S9(08)  VALUE ZEROS          
020600                                                     COMP SYNC.           
020700     05  PV-TRANSFER-PROGRAM             PIC  X(08)  VALUE SPACE.         
022100     05  PV-CICS-MSG-AREA2               PIC  X(100) VALUE SPACE.         
022200     05  PV-DB2-COUNT                    PIC S9(09)  VALUE +0             
022300                                                     COMP-3.              
022400                                                                          
022600*    (LOGISTICS DOMAIN ITEM ACCESS MODULE)                                
022700     COPY XLWS001.                                                        
022800                                                                          
022900*    (LOCATION ACCESS MODULE)                                             
023000     COPY XLWS071.                                                        
023100                                                                          
023200*  KOHL'S STANDARD ATTENTION IDENTIFIER LIST                              
023300     COPY DFHAID.                                                         
023400                                                                          
023800*  LAYOUT FOR THE ABEND MODULE COMMUNICATIONS AREA                        
023900     COPY DPWS013.                                                        
024000                                                                          
024100*  SENDING/RECEIVING AREA FOR THE NUMERIC EDIT ROUTINE (DPKUT100)         
024200     COPY DPWS010I.                                                       
024300                                                                          
024400*  GENERATED DSECT FOR THE SUKM710 MAPSET                                 
024500     COPY SUKM710.                                                        
024600                                                                          
024700*  KOHL'S STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST           
024800     COPY DPWS015.                                                        
024900                                                                          
025000*  COPY BOOK CONTAINING MESSAGES USED IN THE MESSAGE SCREEN               
025100*  (SUKRF514)                                                             
025200     COPY SUWS007.                                                        
025300                                                                          
025400*  COPY BOOK CONTAINING ERROR/WARNING/INFORMATIONAL MESSAGES              
025500*  TO BE DISPLAYED ON THE LRT                                             
025600     COPY SUWS005.                                                        
025700                                                                          
025800* HELP SCREEN COMMAREA                                                    
025900     COPY PCWS192.                                                        
026000                                                                          
026100* HELP SCREEN WORK AREA                                                   
026200     COPY PCWS188.                                                        
026300                                                                          
026400*  SHIPPING UNIT UPC TABLE                                                
026500     EXEC SQL                                                             
026600         INCLUDE TSHUUPC                                                  
026700     END-EXEC.                                                            
026800                                                                          
026900*  SHIPPING UNIT AUDIT  TABLE                                             
027000     EXEC SQL                                                             
027100         INCLUDE TSUATRN                                                  
027200     END-EXEC.                                                            
027300                                                                          
027400*  PURCHASE ORDER TABLE (TPURCHS)                                         
027500     EXEC SQL                                                             
027600         INCLUDE TPURCHS                                                  
027700     END-EXEC.                                                            
027800                                                                          
028400*  PURCHASE ORDER ITEM DETAIL TABLE                                       
028500     EXEC SQL                                                             
028600         INCLUDE TPURITM                                                  
028700     END-EXEC.                                                            
028800                                                                          
028900*  SHIPPING UNIT                                                          
029000*  UPC AUDIT TABLE                                                        
029100     EXEC SQL                                                             
029200         INCLUDE TSUAUPC                                                  
029300     END-EXEC.                                                            
029400                                                                          
029500*  SHIPPING UNIT                                                          
029600*  CONTENT AUDIT TABLE                                                    
029700     EXEC SQL                                                             
029800         INCLUDE TSUEXCP                                                  
029900     END-EXEC.                                                            
029400                                                                          
030000*  DCLGEN FOR THE XLV_SKU VIEW                                            
030100     EXEC SQL                                                             
030200         INCLUDE XLV00000                                                 
030300     END-EXEC.                                                            
030400                                                                          
030500*  DCLGEN FOR THE XLV_UPC VIEW                                            
030600     EXEC SQL                                                             
030700         INCLUDE XLV00003                                                 
030800     END-EXEC.                                                            
030900                                                                          
031000*  DCLGEN FOR THE XLT_UPC TABLE                                           
031100     EXEC SQL                                                             
031200         INCLUDE XLT00006                                                 
031300     END-EXEC.                                                            
031400                                                                          
032500*  SKU/UPC TABLE                                                          
032600*  (TUPCSKU)                                                              
032700     EXEC SQL                                                             
032800         INCLUDE TUPCSKU                                                  
032900     END-EXEC.                                                            
033000                                                                          
033100*  UPC TABLE                                                              
033200     EXEC SQL                                                             
033300         INCLUDE TUPC                                                     
033400     END-EXEC.                                                            
033500                                                                          
033600*  ALLOCIATION PLAN TABLE                                                 
033700     EXEC SQL                                                             
033800         INCLUDE TALLPLN                                                  
033900     END-EXEC.                                                            
034000                                                                          
034100*  MERCHANDISE STORE DISTRIBUTION TABLE                                   
034200     EXEC SQL                                                             
034300         INCLUDE TMESTDS                                                  
034400     END-EXEC.                                                            
034500                                                                          
034600*  MESSAGE TABLE                                                          
035100                                                                          
035800*  PO AGREEMENT TABLE                                                     
035900     EXEC SQL                                                             
036000         INCLUDE TPOAGMT                                                  
036100     END-EXEC.                                                            
036200                                                                          
036300*  WST_INTGRTN_TYP                                                        
036400     EXEC SQL                                                             
036500         INCLUDE WST00008                                                 
036600     END-EXEC.                                                            
036700                                                                          
036800*  WST_ATTR_DEFN                                                          
036900     EXEC SQL                                                             
037000         INCLUDE WST00009                                                 
037100     END-EXEC.                                                            
037200                                                                          
037300*  WST_ATTR_ELEM                                                          
037400     EXEC SQL                                                             
037500         INCLUDE WST00010                                                 
037600     END-EXEC.                                                            
037700                                                                          
037800     EXEC SQL                                                             
037900         INCLUDE SQLCA                                                    
038000     END-EXEC.                                                            
038100                                                                          
038200     EXEC SQL                                                             
038300         DECLARE PO-LINE-CURSOR CURSOR FOR                                
038400           SELECT  B.PO_LNE_NBR                                           
038600             FROM  TPURCHS A                                              
038800                 , TPURITM B                                              
038900            WHERE A.PO_NBR         = :SU006-PO                            
039100              AND A.PO_NBR         = B.PO_NBR                             
039200              AND B.ITM_NBR        = :XL001-ITM-NBR                       
039300              AND A.VER_STATUS_CDE = 'A'                                  
039400              AND B.VER_STATUS_CDE = 'A'                                  
039500              AND A.STATUS_CDE IN ('AC', 'AP', 'CM', 'CA', 'CB')          
039700              AND B.STATUS_CDE IN ('AC', 'AP', 'CM'                       
039800                                  ,'CA', 'CB', 'RV')                      
039900     END-EXEC.                                                            
040000                                                                          
043200 LINKAGE SECTION.                                                         
043300 01  DFHCOMMAREA.                                                         
043400     05  FILLER                         OCCURS  1 TO 4072 TIMES           
043500                                        DEPENDING ON EIBCALEN.            
043600         10  FILLER                     PIC X.                            
043700                                                                          
195200******************************************************************        
043900 PROCEDURE DIVISION.                                                      
240900                                                                          
044000 0000-PROGRAM-MAINLINE.                                                   
240900                                                                          
044100     IF EIBTRNID = 'Z278'                                                 
044200         MOVE DFHCOMMAREA            TO PC192-COMMAREA                    
044300         MOVE PC192-HOST-SAVE-AREA   TO CA-COMMAREA                       
044400     ELSE                                                                 
044500         MOVE DFHCOMMAREA            TO CA-COMMAREA                       
044600     END-IF.                                                              
044700     MOVE LOW-VALUES   TO SU710AI.                                        
044800                                                                          
044900     EVALUATE TRUE                                                        
044100         WHEN EIBTRNID = 'Z278'                                           
059800            MOVE SU006-UPC-X         TO AUPCO                             
061200            MOVE CA-VENDOR-STYLE     TO ASTYLEO                           
061000            MOVE CA-SIZE             TO ASIZEO                            
060600            MOVE CA-COLOR            TO ACOLORO                           
062000            MOVE PC-INITIAL-KEYS-MSG TO AMSGLNO                           
060700                                                                          
045000         WHEN EIBCALEN = ZERO                                             
045100         WHEN EIBAID = DFHCLEAR                                           
045200         WHEN SU006-INITIALIZE-SCREEN                                     
046700             INITIALIZE CA-PROGRAM-COMMAREA                               
046800             SET SU006-PROCESS-SCREEN                                     
046900                 SU006-UPC-CERTIFIED   TO TRUE                            
047100             PERFORM 1000-SETUP-SCREEN                                    
047200                                                                          
047300*          PERFORM CHECK OF THE PO TABLES TO VALIDATE THAT THE            
047400*          ITEM IS ON THE PO AND THE ALLOCATION TO BE SURE IT             
047500*          SHOULD BE COMING THROUGH HERE AND NOW.                         
047600             PERFORM 3000-CHECK-PO                                        
047700                                                                          
047800*          PERFORM CHECK OF THE SHIPPING TABLES TO VERIFY THAT            
047900*          THE ITEM SHOULD ACTUALLY BE IN THIS CARTON (ONLY DONE          
048000*          IF THE ITEM WAS ENTERED BY UPC)                                
048100             IF SU006-UPC-X > SPACES                                      
048200                 PERFORM 5000-CHECK-SHIPPING-TABLES                       
048300             END-IF                                                       
048400                                                                          
048500*  AT THIS POINT ALL THE LOOKUPS HAVE BEEN DONE AND ALL THE               
048600*  POSSIBLE EXCEPTIONS HAVE BEEN CREATED.  IF THE                         
048700*  SU006-PGM-MSG-NBR VARIABLE IS GREATER THAN 0, IT MEANS IT HAS          
048800*  ALREADY GONE THROUGH THE INITIALIZATION PART OF THIS PROGRAM,          
048900*  AND FOUND EXCEPTIONS...IT TRANSFERRED TO THE MESSAGE SCREEN TO         
049000*  DISPLAY THEM TO THE USER, AND IS NOW RETURNING TO THIS PROGRAM         
049100*  FOR FURTHER PROCESSING.  THE PROGRAM WILL REDO THE                     
049200*  INITIALIZATION IN ORDER TO RE-FILL THE COMMAREA WITH THE               
049300*  CORRECT INFO, BUT WE WANT TO SKIP THE CREATION OF THE AUDIT AND        
049400*  EXCEPTIONS THIS TIME THROUGH                                           
049500                                                                          
049700             IF SU006-PGM-MSG-NBR > +0                                    
049800                 MOVE +0       TO SU006-PGM-MSG-NBR                       
049900             ELSE                                                         
050000*              CREATE AN AUDIT ROW AND CREATE EXCEPTIONS IF ANY           
050100*              WERE FOUND                                                 
050200                 MOVE 'N' TO SUAUPC-UPC-CERTD-IND                         
RFAUDT                 IF (SU006-QC-AND-RFID-AUD                                
RFAUDT                 AND SU006-MANUAL-GENERATED-AUDIT)                        
050300                    CONTINUE                                              
050300                 ELSE                                                     
      * 04/14/14 RLK BEG                                                        
RFAUDT                    MOVE 1          TO SUAUPC-SCND-UNT-QTY                
094300                    MOVE 1          TO SUAUPC-RFID-TGD-QTY                
      * 04/14/14 RLK END                                                        
050300                    PERFORM 1400-CREATE-AUDIT-ROW                         
      * 04/14/14 RLK BEG                                                        
                          MOVE SUAUPC-SCN-TMST   TO SU006-SCN-TMST              
      * 04/14/14 RLK END                                                        
050400                    PERFORM 1500-CHECK-EXCEPTIONS                         
050300                 END-IF                                                   
050500             END-IF                                                       
050600                                                                          
051700             MOVE -1                   TO  AUPCL                          
051800             PERFORM 8000-SEND-MAP                                        
051900             PERFORM 8200-RETURN-TRANID                                   
052000                                                                          
052200*      THE OK KEY WAS PRESSED....UPDATE THE AUDIT ROWS CREATED            
052300*      IN SUKRF518 WITH THE DETAIL INFORMATION, THEN RETURN TO            
052400*      THE UPC SCAN SCREEN                                                
052500         WHEN EIBAID = DFHENTER                                           
052600         WHEN EIBAID = DFHPF2                                             
052700*          IF THE UPC HAS NOT BEEN CERTIFIED, CERTIFY IT AND              
052800*          SET THE FLAG ON THE AUDIT ROW TO INDICATE THAT IT              
052900*          WAS THE AUDIT PROCESS THAT CERTIFIED IT                        
053000             IF SU006-UPC-NOT-CERTIFIED                                   
053100                                                                          
053200*     DO NOT EVEN ATTEMPT TO CERTIFY A TUPC                               
053300*     DURING THE TIME THAT DB MAINTENANCE IS BEING DONE.                  
053400                PERFORM 7300-GET-BLACKOUT                                 
053500                IF PS-BLACKOUT-NO                                         
053600                    MOVE 'Y'         TO SUAUPC-UPC-CERTD-IND              
053700                    PERFORM 1700-CERTIFY-UPC                              
053800                    PERFORM 1410-UPDATE-AUDIT-ROW                         
053900                END-IF                                                    
054000                                                                          
054100             END-IF                                                       
      *** IF RFID ONLY AND NOT BULK SET SCANNED AND TAGGED QTY TO 1             
      *** AND XFER TO SUKRF518, ELSE XFER TO SUKRF537 AS USERS MUST             
      *** ENTER SCANNED AND TAGGED QTYS.                                        
      *** IF COMBO AUDIT JUST SET TAGGED QTY TO SCANNED QTY FROM                
      *** QC AUDIT AND XFER TO SUKRF518.                                        
                   IF SU006-RFID-AUD-ONLY                                       
                     IF SU006-BULK-CSPK-PKGNG-CDE                               
      *** XFER TO SUKRF537                                                      
                       MOVE 0 TO SU006-WRONG-RFID-CTR                           
                       MOVE PC-UPC-KEY-QTY-PROGRAM                              
                                             TO  PV-TRANSFER-PROGRAM            
                       PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                    
                     ELSE                                                       
                       MOVE 1 TO SUAUPC-RFID-TGD-QTY                            
                                 SUAUPC-SCND-UNT-QTY                            
                       MOVE SU006-AUDIT-CNTL-NBR                                
                                             TO SUAUPC-AUD-CNTL-NBR             
                       PERFORM 1470-UPDATE-TGD-SCND-QTY                         
      *** XFER TO SUKRF518                                                      
                       MOVE PC-UPC-SCAN-PROGRAM-NAME                            
                                             TO  PV-TRANSFER-PROGRAM            
                       PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                    
                     END-IF                                                     
                   ELSE                                                         
                     PERFORM 1460-SELECT-SCND-QTY                               
                     MOVE PV-SCND-UNT-QTY TO SUAUPC-RFID-TGD-QTY                
                                             SUAUPC-SCND-UNT-QTY                
                     PERFORM 1470-UPDATE-TGD-SCND-QTY                           
      *** XFER TO SUKRF518                                                      
                     MOVE PC-UPC-SCAN-PROGRAM-NAME                              
054700                                     TO  PV-TRANSFER-PROGRAM              
054900               PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                      
                   END-IF                                                       
055000                                                                          
055100         WHEN EIBAID = DFHPF1                                             
055200              PERFORM 1300-HELP-SCREEN                                    
056500                                                                          
056600*        THE "WRONG" KEY WAS PRESSED -- CONTROL IS PASSED TO THE          
056700*        TICKET NO MATCH SCREEN                                           
056800         WHEN EIBAID = DFHPF9                                             
                   IF SU006-BULK-CSPK-PKGNG-CDE                                 
                     MOVE 0 TO SU006-WRONG-RFID-CTR                             
                     MOVE PC-UPC-KEY-QTY-PROGRAM                                
057000                                        TO PV-TRANSFER-PROGRAM            
057100               PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                      
                   ELSE                                                         
                     MOVE 1 TO SU006-WRONG-RFID-CTR                             
                     IF SU006-MANUAL-GENERATED-AUDIT                            
                       PERFORM 1480-UPDATE-TGD-2-SCND-QTY                       
                     END-IF                                                     
                     MOVE PC-TIX-NO-MTCH-PROGRAM-NAME                           
057000                                        TO PV-TRANSFER-PROGRAM            
                     PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                      
                   END-IF                                                       
057200                                                                          
057300         WHEN OTHER                                                       
057400             MOVE PM-UNRECOGNIZED-PF-KEY-MSG  TO AMSGLNO                  
057500                                                                          
057600     END-EVALUATE.                                                        
057700                                                                          
044100     IF EIBTRNID = 'Z278'                                                 
238900        PERFORM 8000-SEND-MAP                                             
044100     ELSE                                                                 
057800        PERFORM 8100-SEND-DATA-ONLY                                       
044100     END-IF.                                                              
058000                                                                          
057900     PERFORM 8200-RETURN-TRANID.                                          
058000                                                                          
058100     GOBACK.                                                              
240900                                                                          
195200******************************************************************        
058300 1000-SETUP-SCREEN.                                                       
058400                                                                          
058500*  DETERMINE IF THE ITEM EXISTS                                           
058600     PERFORM 1010-LOOKUP-ITEM.                                            
058700                                                                          
059700*  MOVE ITEM INFORMATION TO THE SCREEN                                    
059800     MOVE SU006-UPC-X         TO AUPCO.                                   
060300                                                                          
061200     MOVE CA-VENDOR-STYLE     TO ASTYLEO.                                 
061300                                                                          
061000     MOVE CA-SIZE             TO ASIZEO.                                  
061100                                                                          
060600     MOVE CA-COLOR            TO ACOLORO.                                 
060700                                                                          
061900*  SET-UP KEYS MESSAGE LINE                                               
062000     MOVE PC-INITIAL-KEYS-MSG TO AMSGLNO.                                 
240900                                                                          
195200******************************************************************        
062200 1010-LOOKUP-ITEM.                                                        
062300                                                                          
062400*  IF COMING FROM THE SKU SCAN SCREEN, DO A LOOKUP BY SKU                 
062500                                                                          
062600     IF SU006-LAST-PROGRAM = PC-SKU-SCAN-PROGRAM-NAME                     
062700         PERFORM 1120-GET-SKU-INFO                                        
062800     ELSE                                                                 
062900         PERFORM 1100-SELECT-ITEM-BY-UPC-NBR                              
063000     END-IF.                                                              
063100                                                                          
063200     PERFORM 7200-GET-GARMENT-ON-HANGER.                                  
063300                                                                          
063400     MOVE XL001-CERTD-IND             TO SU006-UPC-CERTIFIED-IND.         
063500     MOVE XL001-HNG-FOLD-CDE          TO CA-HANG-FOLD.                    
063300                                                                          
058800*  MOVE THE SIZE                                                          
076500     MOVE XL001-SIZE-DESC             TO CA-SIZE.                         
059000                                                                          
059100*  MOVE THE VENDOR STYLE                                                  
077000     MOVE XL001-VS-NBR                TO CA-VENDOR-STYLE                  
077100                                         CA-VENDOR-VS-NBR.                
077200     MOVE XL001-ENT-ID                TO CA-VENDOR-ENT-ID.                
077300     MOVE XL001-INTR-UPC-ID           TO CA-INTR-UPC-ID.                  
077400                                                                          
195200******************************************************************        
063800 1100-SELECT-ITEM-BY-UPC-NBR.                                             
063900                                                                          
064000     INITIALIZE XL001-ITEM-COMMAREA.                                      
064100     SET XL001-SELECT-SKU                                                 
064100         XL001-SELECT-SKU-STR                                             
064200         XL001-SELECT-VND-STYL                                            
064500         XL001-SELECT-UPC                                                 
064600         XL001-BY-UPC-NBR             TO TRUE.                            
064700                                                                          
064800     MOVE SU006-UPC                   TO XL001-KEY-UPC-NBR                
018900                                         PV-DISP-VALUE.                   
064900                                                                          
065000     PERFORM 8600-CALL-XLKUT001.                                          
065100                                                                          
065200     EVALUATE TRUE                                                        
065300         WHEN XL001-SUCCESSFUL                                            
065400             SET CA-UPC-FOUND        TO TRUE                              
065500*       ONLY VALIDATE THE UPC, DO NOT OVERLAY THE VENDOR UPC WITH         
065600*       THE INTERNAL CODE 4 UPC                                           
065700             MOVE XL001-CSTM-CLOR-DESC TO CA-COLOR                        
065800             MOVE XL001-SKU-NBR       TO SU006-NUMERIC-SKU                
065900             MOVE XL001-DEPT-NBR      TO SU006-SKU-DPT                    
066000             MOVE XL001-MAJ-CL-NBR-2CH TO SU006-SKU-MAJ-CLASS             
066100             MOVE XL001-SUB-CL-NBR-2CH TO SU006-SKU-SUB-CLASS             
066200             MOVE XL001-SKU-DESC      TO CA-SKU-DESC                      
066300             MOVE XL001-UNT-RTL-AMT   TO CA-RETAIL                        
066500             MOVE XL001-HNG-FOLD-CDE  TO CA-HANG-FOLD                     
068100                                                                          
066600         WHEN XL001-UPC-NOT-FOUND                                         
066700             SET CA-UPC-NOT-FOUND     TO TRUE                             
066800             IF SU006-NO-UPC-LOOKUP-ERROR                                 
066900                 SET SU006-UPC-NOT-FOUND-ERROR                            
067000                                      TO TRUE                             
067100             END-IF                                                       
067200                                                                          
067300*          SET UP MESSAGE SCREEN                                          
067400             SET PM-UPC-NOT-FOUND      TO TRUE                            
067500             MOVE PM-PGM-MSG-NBR       TO SU006-PGM-MSG-NBR               
067600             MOVE PC-MESSAGE-SCREEN-PROGRAM-NAME                          
067700                                       TO PV-TRANSFER-PROGRAM             
067800             MOVE PC-SKU-SCAN-PROGRAM-NAME TO SU006-LAST-PROGRAM          
067900                                                                          
068000             PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                        
068100             MOVE SPACES               TO CA-COLOR                        
068100                                                                          
068200         WHEN OTHER                                                       
068300             MOVE '1100-SELECT-ITEM-BY-UPC-NBR'                           
068400                                       TO DP013-PARAGRAPH                 
068500             MOVE 'CALL TO DPKUT800'   TO DP013-MESSAGE-TEXT (1)          
068600             MOVE SU006-UPC-X          TO DP013-MESSAGE-TEXT (2)          
068700             STRING ' ERROR PARAGRAPH = '                                 
068800                        XL001-ERROR-PARAGRAPH-NBR                         
068900                 DELIMITED BY SIZE INTO DP013-MESSAGE-TEXT (3)            
069000             MOVE XL001-SQLCA          TO DP013-SQLCA                     
069100             MOVE 'XKUT001'            TO DP013-DB2-TABLE-NAME (1)        
069200             SET DP013-DB2-ABEND         TO TRUE                          
069300             SET DP013-NO-ROLLBACK       TO TRUE                          
069400             PERFORM 9999-WRITE-CICS-LOG-MSG                              
069500             MOVE PM-DB2-FATAL-ERROR-MSG TO CA-RETURN-MSG                 
069600             SET CA-RETURN-ERROR         TO TRUE                          
069700                                                                          
069800*          RETURN TO THE PROGRAM THAT CALLED IT                           
069900             MOVE SU006-LAST-PROGRAM     TO PV-TRANSFER-PROGRAM           
070000             PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                        
070100     END-EVALUATE.                                                        
240900                                                                          
195200******************************************************************        
070300 1120-GET-SKU-INFO.                                                       
070400                                                                          
070500     INITIALIZE XL001-ITEM-COMMAREA.                                      
070600                                                                          
070700     SET XL001-SELECT-SKU                                                 
070800         XL001-SELECT-VND-STYL                                            
071100         XL001-SELECT-UPC                                                 
071200         XL001-BY-NUMERIC-SKU          TO TRUE.                           
071300                                                                          
071400     MOVE SU006-NUMERIC-SKU            TO XL001-KEY-SKU                   
018900                                         PV-DISP-VALUE.                   
071500                                                                          
071600     PERFORM 8600-CALL-XLKUT001.                                          
071700                                                                          
071800     EVALUATE TRUE                                                        
071900         WHEN XL001-SUCCESSFUL                                            
072000             SET CA-SKU-FOUND          TO TRUE                            
072100             MOVE XL001-UPC-NBR        TO SU006-UPC                       
072200             MOVE XL001-CSTM-CLOR-DESC TO CA-COLOR                        
072300             MOVE XL001-UPC-ID         TO CA-INTERNAL-UPC                 
072400             MOVE XL001-DEPT-NBR       TO SU006-SKU-DPT                   
072500             MOVE XL001-MAJ-CL-NBR     TO SU006-SKU-MAJ-CLASS             
072600             MOVE XL001-SUB-CL-NBR     TO SU006-SKU-SUB-CLASS             
072700             MOVE XL001-SKU-DESC       TO CA-SKU-DESC                     
072800             MOVE XL001-UNT-RTL-AMT    TO CA-RETAIL                       
072900             MOVE XL001-HNG-FOLD-CDE   TO CA-HANG-FOLD                    
072900                                                                          
073000         WHEN XL001-SKU-NOT-FOUND                                         
073100         WHEN XL001-VND-STYL-NOT-FOUND                                    
073200         WHEN XL001-SKU-STR-NOT-FOUND                                     
073300             SET CA-SKU-NOT-FOUND                                         
073400                 SU006-SKU-NOT-FOUND-ERROR                                
073500                 SU006-STYLE-SUB-ERROR TO TRUE                            
073600                                                                          
073700*          SET UP MESSAGE                                                 
073800             SET PM-SKU-NOT-FOUND      TO TRUE                            
073900             MOVE PM-PGM-MSG-NBR       TO SU006-PGM-MSG-NBR               
074000             MOVE PC-MESSAGE-SCREEN-PROGRAM-NAME                          
074100                                       TO PV-TRANSFER-PROGRAM             
074200             MOVE PC-DATA-INFO-PROGRAM-NAME                               
074300                                       TO SU006-LAST-PROGRAM              
074400             PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                        
072900                                                                          
074500         WHEN OTHER                                                       
074600             MOVE '1120-GET-SKU-INFO'  TO DP013-PARAGRAPH                 
074800             MOVE 'CALL TO XLKUT001'   TO DP013-MESSAGE-TEXT (1)          
074900             MOVE SU006-SKU-NBR        TO DP013-MESSAGE-TEXT (2)          
075000             STRING ' ERROR PARA = ' XL001-ERROR-PARAGRAPH-NBR            
075100                 DELIMITED BY SIZE INTO DP013-MESSAGE-TEXT (3)            
075200             MOVE XL001-SQLCA          TO DP013-SQLCA                     
075300             SET DP013-DB2-ABEND       TO TRUE                            
075400             SET DP013-NO-ROLLBACK     TO TRUE                            
075500             PERFORM 9999-WRITE-CICS-LOG-MSG                              
075600             MOVE PM-DB2-FATAL-ERROR-MSG TO CA-RETURN-MSG                 
075700             SET CA-RETURN-ERROR         TO TRUE                          
075800                                                                          
075900*          RETURN TO THE PROGRAM THAT CALLED IT                           
076000             MOVE SU006-LAST-PROGRAM   TO PV-TRANSFER-PROGRAM             
076100             PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                        
076200     END-EVALUATE.                                                        
076300                                                                          
195200******************************************************************        
087000 1300-HELP-SCREEN.                                                        
077400                                                                          
087100     MOVE 'DPKRF278'                  TO PV-TRANSFER-PROGRAM.             
087200     MOVE 'SUKRF710'                  TO PC192-PROGRAM-NAME.              
087300     COPY PCPD188.                                                        
087400     MOVE PC188-DISPLAY-DATE          TO PC192-DISPLAY-DATE.              
087500     PERFORM 2200-XCTL-TO-HELP.                                           
087700                                                                          
195200******************************************************************        
087900*   1400-CREATE-AUDIT-ROW                                        *        
088000* CREATE AN AUDIT ROW FOR THE SCANNED/ENTERED UPC (OR ZEROS IF   *        
088100* THERE IS NO UPC ENTERED                                        *        
195200******************************************************************        
088300 1400-CREATE-AUDIT-ROW.                                                   
088400                                                                          
RFAUDT     IF (SU006-QC-AND-RFID-AUD                                            
RFAUDT     AND SU006-MANUAL-GENERATED-AUDIT)                                    
RFAUDT      OR SU006-QC-AUD-ONLY                                                
RFAUDT      OR SU006-RFID-AUD-ONLY                                              
RFAUDT**  FOR A MANUAL GENERATED AUDIT ONLY 1 ROW EXISTS AND WE WILL USE        
RFAUDT**  THE AUDIT CONTROL NUMBER FROM THAT                                    
RFAUDT        MOVE SU006-AUDIT-CNTL-NBR       TO SUAUPC-AUD-CNTL-NBR            
RFAUDT     ELSE                                                                 
RFAUDT        MOVE SU006-RFID-AUDIT-CNTL-NBR  TO SUAUPC-AUD-CNTL-NBR            
RFAUDT     END-IF.                                                              
RFAUDT**   MOVE SU006-AUDIT-CNTL-NBR          TO SUAUPC-AUD-CNTL-NBR.           
088600     MOVE SU006-SHIP-UNIT-NBR           TO SUAUPC-SHIPT-UNT-ID.           
088700     MOVE SU006-UPC                     TO SUAUPC-UPC-NBR.                
088800     MOVE SU006-USERID                  TO SUAUPC-AUD-USR-ID.             
088900     MOVE CA-RETAIL                     TO SUAUPC-UPC-RTL-AMT.            
089000     MOVE SU006-NUMERIC-SKU             TO SUAUPC-SKU-NBR.                
089100     MOVE CA-SKU-DESC                   TO SUAUPC-ITM-DESC.               
089200     MOVE CA-VENDOR-STYLE               TO SUAUPC-VND-STYL-DESC.          
089300     MOVE CA-COLOR                      TO SUAUPC-UPC-CLOR-DESC.          
089400     MOVE CA-SIZE                       TO SUAUPC-UPC-SIZE-DESC.          
089500     MOVE XL001-DEPT-NBR                TO SUAUPC-DEPT-NBR.               
089600     MOVE XL001-MAJ-CL-NBR              TO SUAUPC-MAJ-CL-NBR.             
089700     MOVE XL001-SUB-CL-NBR              TO SUAUPC-SUB-CL-NBR.             
089800** THE SCANNED TYPE IS 'P' FOR PREPACKS, 'O' FOR ODD UNITS                
089900** OTHERWISE 'S' FOR THE DEFAULT OF SCANNED.                              
090000** IF THE PREPACK IS CLOSED BY THE ASSOCIATE, ANY OTHER ITEMS             
090100** SCANNED ARE ODD UNITS                                                  
090200     IF SU006-PREPK-ITYPE-PKGNG-CDE                                       
090300        IF SU006-PREPACK-CLOSED                                           
090400           MOVE PC-O        TO SUAUPC-SCN-TYP-CDE                         
090500        ELSE                                                              
090600           MOVE PC-P        TO SUAUPC-SCN-TYP-CDE                         
090700        END-IF                                                            
090800     ELSE                                                                 
090900        MOVE PC-S           TO SUAUPC-SCN-TYP-CDE                         
091000     END-IF.                                                              
091200                                                                          
091300*  CONTINUE TO TRY INSERTING UNTIL SUCCESSFUL...IF -803 SQLCODE,          
091400*  RETRY WITH THE NEW TIMESTAMP (PART OF THE KEY)                         
091500     PERFORM                                                              
091600         WITH TEST AFTER                                                  
091700         VARYING PV-RETRY-COUNTER                                         
091800         FROM 1 BY 1                                                      
091900         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
092000                   OR (SQLCODE = ZERO))                                   
092100                                                                          
092200         EXEC SQL                                                         
092300             SET :SUAUPC-SCN-TMST = CURRENT TIMESTAMP                     
092400         END-EXEC                                                         
092500                                                                          
092600         EXEC SQL                                                         
092700             INSERT INTO TSUAUPC                                          
092800                 ( AUD_CNTL_NBR                                           
092900                  ,SHIPT_UNT_ID                                           
093000                  ,UPC_NBR                                                
093100                  ,SCN_TMST                                               
093200                  ,AUD_USR_ID                                             
093300                  ,DEPT_NBR                                               
093400                  ,MAJ_CL_NBR                                             
093500                  ,SUB_CL_NBR                                             
093600                  ,ITM_DESC                                               
093700                  ,VND_STYL_DESC                                          
093800                  ,UPC_CLOR_DESC                                          
093900                  ,UPC_SIZE_DESC                                          
094000                  ,UPC_RTL_AMT                                            
094100                  ,UPC_CERTD_IND                                          
094200                  ,SCN_TYP_CDE                                            
094300                  ,SKU_NBR                                                
094300                  ,RFID_TGD_QTY                                           
RFAUDT                  ,SCND_UNT_QTY                                           
094400                 )                                                        
094500             VALUES                                                       
094600                 (                                                        
094700                    :SUAUPC-AUD-CNTL-NBR                                  
094800                  , :SUAUPC-SHIPT-UNT-ID                                  
094900                  , :SUAUPC-UPC-NBR                                       
095000                  , :SUAUPC-SCN-TMST                                      
095100                  , :SUAUPC-AUD-USR-ID                                    
095200                  , :SUAUPC-DEPT-NBR                                      
095300                  , :SUAUPC-MAJ-CL-NBR                                    
095400                  , :SUAUPC-SUB-CL-NBR                                    
095500                  , :SUAUPC-ITM-DESC                                      
095600                  , :SUAUPC-VND-STYL-DESC                                 
095700                  , :SUAUPC-UPC-CLOR-DESC                                 
095800                  , :SUAUPC-UPC-SIZE-DESC                                 
095900                  , :SUAUPC-UPC-RTL-AMT                                   
096000                  , :SUAUPC-UPC-CERTD-IND                                 
096100                  , :SUAUPC-SCN-TYP-CDE                                   
096200                  , :SUAUPC-SKU-NBR                                       
094300                  , :SUAUPC-RFID-TGD-QTY                                  
RFAUDT                  , :SUAUPC-SCND-UNT-QTY                                  
096300                 )                                                        
096400         END-EXEC                                                         
096500                                                                          
096600         EVALUATE TRUE                                                    
096700             WHEN SQLCODE = ZERO                                          
097000                 CONTINUE                                                 
096800                                                                          
096900             WHEN SQLCODE = -803                                          
097000                 CONTINUE                                                 
096800                                                                          
097100             WHEN SQLWARN0 NOT = SPACES                                   
097200             WHEN SQLCODE < ZERO                                          
097300             WHEN SQLCODE > ZERO                                          
097400                 MOVE '1400-CREATE-AUDIT-ROW'                             
097500                                        TO DP013-PARAGRAPH                
097600                 MOVE 'INSERT TSUAUPC'  TO DP013-MESSAGE-TEXT(1)          
097700                 MOVE SUAUPC-AUD-CNTL-NBR                                 
097800                                        TO PV-DISP-AUD-CNTL-NBR           
097900                 MOVE SUAUPC-SHIPT-UNT-ID                                 
098000                                        TO PV-DISP-SHIPT-UNT-ID           
098100                 MOVE SUAUPC-UPC-NBR    TO PV-DISP-UPC-NBR                
098300                 MOVE PV-DISPLAY-KEYS   TO DP013-MESSAGE-TEXT(2)          
098200                 MOVE SUAUPC-SCN-TMST   TO PV-DISP-SCN-TMST               
098300                 MOVE PV-DISP-SCN-TMST  TO DP013-MESSAGE-TEXT(3)          
098400                 MOVE SQLCA             TO DP013-SQLCA                    
098500                 MOVE 'TSUAUPC'         TO DP013-DB2-TABLE-NAME(1)        
098600                 SET DP013-DB2-ABEND         TO TRUE                      
098700                 SET DP013-NO-ROLLBACK       TO TRUE                      
098800                 SET SU006-INITIALIZE-SCREEN TO TRUE                      
098900                 PERFORM 9999-WRITE-CICS-LOG-MSG                          
099000                 MOVE PM-DB2-FATAL-ERROR-MSG TO AMSGLNO                   
099100                 PERFORM 8100-SEND-DATA-ONLY                              
099200                 PERFORM 8200-RETURN-TRANID                               
099300         END-EVALUATE                                                     
099400     END-PERFORM.                                                         
099500                                                                          
195200******************************************************************        
099800*   1410-UPDATE-AUDIT-ROW                                        *        
099900* THE UPC WAS CERTIFIED, UPDATE THE CERTIFICATION INIDICATOR ON  *        
100000* TSUAUPC.                                                       *        
195200******************************************************************        
100200 1410-UPDATE-AUDIT-ROW.                                                   
100300                                                                          
RFAUDT      IF (SU006-QC-AND-RFID-AUD                                           
RFAUDT      AND SU006-MANUAL-GENERATED-AUDIT)                                   
RFAUDT       OR SU006-QC-AUD-ONLY                                               
RFAUDT       OR SU006-RFID-AUD-ONLY                                             
RFAUDT**  FOR A MANUAL GENERATED AUDIT ONLY 1 ROW EXISTS AND WE WILL USE        
RFAUDT**  THE AUDIT CONTROL NUMBER FROM THAT                                    
RFAUDT         MOVE SU006-AUDIT-CNTL-NBR                                        
RFAUDT                               TO SUAUPC-AUD-CNTL-NBR                     
RFAUDT      ELSE                                                                
RFAUDT         MOVE SU006-RFID-AUDIT-CNTL-NBR                                   
RFAUDT                               TO SUAUPC-AUD-CNTL-NBR                     
RFAUDT      END-IF.                                                             
RFAUDT**   MOVE SU006-AUDIT-CNTL-NBR TO SUAUPC-AUD-CNTL-NBR.                    
100500     MOVE SU006-SHIP-UNIT-NBR  TO SUAUPC-SHIPT-UNT-ID.                    
100600     MOVE SU006-UPC            TO SUAUPC-UPC-NBR.                         
100700                                                                          
100800     EXEC SQL                                                             
100900         UPDATE TSUAUPC                                                   
101000            SET UPC_CERTD_IND  = :SUAUPC-UPC-CERTD-IND                    
101100          WHERE AUD_CNTL_NBR = :SUAUPC-AUD-CNTL-NBR                       
101200            AND SHIPT_UNT_ID = :SUAUPC-SHIPT-UNT-ID                       
101300            AND UPC_NBR      = :SUAUPC-UPC-NBR                            
101400     END-EXEC.                                                            
101500                                                                          
101600     EVALUATE TRUE                                                        
101700         WHEN SQLCODE = ZERO                                              
101800              CONTINUE                                                    
096800                                                                          
101900         WHEN SQLWARN0 NOT = SPACES                                       
102000         WHEN SQLCODE < ZERO                                              
102100         WHEN SQLCODE > ZERO                                              
102200             MOVE '1410-UPDATE-AUDIT-ROW'                                 
102300                                        TO DP013-PARAGRAPH                
102400             MOVE 'UPDATE TSUAUPC'      TO DP013-MESSAGE-TEXT(1)          
102450             MOVE SUAUPC-AUD-CNTL-NBR   TO PV-DISP-AUD-CNTL-NBR           
102700             MOVE SU006-SHIP-UNIT-NBR   TO PV-DISP-SHIPT-UNT-ID           
102900             MOVE SU006-UPC             TO PV-DISP-UPC-NBR                
103100             MOVE PV-DISPLAY-KEYS       TO DP013-MESSAGE-TEXT(2)          
103200             MOVE SQLCA                 TO DP013-SQLCA                    
103300             MOVE 'TSUAUPC'             TO DP013-DB2-TABLE-NAME(1)        
103400             SET DP013-DB2-ABEND        TO TRUE                           
103500             SET DP013-NO-ROLLBACK      TO TRUE                           
103600             PERFORM 9999-WRITE-CICS-LOG-MSG                              
103700             MOVE PM-DB2-FATAL-ERROR-MSG TO AMSGLNO                       
103800             PERFORM 8100-SEND-DATA-ONLY                                  
103900             PERFORM 8200-RETURN-TRANID                                   
104000     END-EVALUATE.                                                        
104100                                                                          
S95200******************************************************************        
 99800*   1460-SELECT-SCND-QTY                                       *          
099900* THE UPC WAS CERTIFIED, UPDATE THE CERTIFICATION INIDICATOR ON  *        
100000* TSUAUPC.                                                       *        
195200******************************************************************        
100200 1460-SELECT-SCND-QTY.                                                    
100300                                                                          
RFAUDT     MOVE SU006-AUDIT-CNTL-NBR TO SUAUPC-AUD-CNTL-NBR.                    
100500     MOVE SU006-SHIP-UNIT-NBR  TO SUAUPC-SHIPT-UNT-ID.                    
100700                                                                          
047200     EXEC SQL                                                             
047300         SELECT SCND_UNT_QTY                                              
047500           INTO :PV-SCND-UNT-QTY                                          
047400           FROM TSUAUPC                                                   
047600         WHERE AUD_CNTL_NBR    =  :SUAUPC-AUD-CNTL-NBR                    
047800           AND SHIPT_UNT_ID    =  :SUAUPC-SHIPT-UNT-ID                    
047900           AND SCN_TMST        =                                          
                (SELECT MAX(SCN_TMST)                                           
                   FROM TSUAUPC B                                               
                  WHERE B.AUD_CNTL_NBR  = :SUAUPC-AUD-CNTL-NBR                  
                    AND B.SHIPT_UNT_ID  = :SUAUPC-SHIPT-UNT-ID)                 
048000     END-EXEC.                                                            
100700                                                                          
101600     EVALUATE TRUE                                                        
101700         WHEN SQLCODE = ZERO                                              
101800              CONTINUE                                                    
096800                                                                          
101900         WHEN SQLWARN0 NOT = SPACES                                       
102000         WHEN SQLCODE < ZERO                                              
102100         WHEN SQLCODE > ZERO                                              
102200             MOVE '1460-SELECT-SCND-QTY'                                  
102300                                        TO DP013-PARAGRAPH                
102400             MOVE 'SELECT TSUAUPC'      TO DP013-MESSAGE-TEXT(1)          
102450             MOVE SUAUPC-AUD-CNTL-NBR   TO PV-DISP-AUD-CNTL-NBR           
102700             MOVE SUAUPC-SHIPT-UNT-ID   TO PV-DISP-SHIPT-UNT-ID           
103100             MOVE PV-DISPLAY-KEYS       TO DP013-MESSAGE-TEXT(2)          
103200             MOVE SQLCA                 TO DP013-SQLCA                    
103300             MOVE 'TSUAUPC'             TO DP013-DB2-TABLE-NAME(1)        
103400             SET DP013-DB2-ABEND        TO TRUE                           
103500             SET DP013-NO-ROLLBACK      TO TRUE                           
103600             PERFORM 9999-WRITE-CICS-LOG-MSG                              
103700             MOVE PM-DB2-FATAL-ERROR-MSG TO AMSGLNO                       
103800             PERFORM 8100-SEND-DATA-ONLY                                  
103900             PERFORM 8200-RETURN-TRANID                                   
104000     END-EVALUATE.                                                        
104100                                                                          
195200******************************************************************        
099800*   1470-UPDATE-TGD-SCND-QTY                                     *        
099900* UPDATE BOTH SCANNED AND TAGGED QTY                             *        
195200******************************************************************        
100200 1470-UPDATE-TGD-SCND-QTY.                                                
100300                                                                          
RFAUDT      IF (SU006-QC-AND-RFID-AUD                                           
RFAUDT      AND SU006-MANUAL-GENERATED-AUDIT)                                   
RFAUDT       OR SU006-QC-AUD-ONLY                                               
RFAUDT       OR SU006-RFID-AUD-ONLY                                             
RFAUDT**  FOR A MANUAL GENERATED AUDIT ONLY 1 ROW EXISTS AND WE WILL USE        
RFAUDT**  THE AUDIT CONTROL NUMBER FROM THAT                                    
RFAUDT         MOVE SU006-AUDIT-CNTL-NBR                                        
RFAUDT                               TO SUAUPC-AUD-CNTL-NBR                     
RFAUDT      ELSE                                                                
RFAUDT         MOVE SU006-RFID-AUDIT-CNTL-NBR                                   
RFAUDT                               TO SUAUPC-AUD-CNTL-NBR                     
RFAUDT      END-IF.                                                             
100500     MOVE SU006-SHIP-UNIT-NBR  TO SUAUPC-SHIPT-UNT-ID.                    
100600     MOVE SU006-UPC            TO SUAUPC-UPC-NBR.                         
100600     MOVE SU006-SCN-TMST       TO SUAUPC-SCN-TMST.                        
100700                                                                          
100800     EXEC SQL                                                             
100900         UPDATE TSUAUPC                                                   
101000            SET RFID_TGD_QTY   = :SUAUPC-RFID-TGD-QTY                     
101000               ,SCND_UNT_QTY   = :SUAUPC-SCND-UNT-QTY                     
101100          WHERE AUD_CNTL_NBR = :SUAUPC-AUD-CNTL-NBR                       
101200            AND SHIPT_UNT_ID = :SUAUPC-SHIPT-UNT-ID                       
101300            AND UPC_NBR      = :SUAUPC-UPC-NBR                            
100600            AND SCN_TMST     = :SUAUPC-SCN-TMST                           
101400     END-EXEC.                                                            
101500                                                                          
101600     EVALUATE TRUE                                                        
101700         WHEN SQLCODE = ZERO                                              
101800              CONTINUE                                                    
096800                                                                          
101900         WHEN SQLWARN0 NOT = SPACES                                       
102000         WHEN SQLCODE < ZERO                                              
102100         WHEN SQLCODE > ZERO                                              
102200             MOVE '1470-UPDATE-TGD-SCND-QTY'                              
102300                                        TO DP013-PARAGRAPH                
102400             MOVE 'UPDATE TSUAUPC'      TO DP013-MESSAGE-TEXT(1)          
102450             MOVE SUAUPC-AUD-CNTL-NBR   TO PV-DISP-AUD-CNTL-NBR           
102700             MOVE SU006-SHIP-UNIT-NBR   TO PV-DISP-SHIPT-UNT-ID           
102900             MOVE SU006-UPC             TO PV-DISP-UPC-NBR                
103100             MOVE PV-DISPLAY-KEYS       TO DP013-MESSAGE-TEXT(2)          
098200             MOVE SUAUPC-SCN-TMST       TO PV-DISP-SCN-TMST               
098300             MOVE PV-DISP-SCN-TMST      TO DP013-MESSAGE-TEXT(3)          
103200             MOVE SQLCA                 TO DP013-SQLCA                    
103300             MOVE 'TSUAUPC'             TO DP013-DB2-TABLE-NAME(1)        
103400             SET DP013-DB2-ABEND        TO TRUE                           
103500             SET DP013-NO-ROLLBACK      TO TRUE                           
103600             PERFORM 9999-WRITE-CICS-LOG-MSG                              
103700             MOVE PM-DB2-FATAL-ERROR-MSG TO AMSGLNO                       
103800             PERFORM 8100-SEND-DATA-ONLY                                  
103900             PERFORM 8200-RETURN-TRANID                                   
104000     END-EVALUATE.                                                        
104000                                                                          
195200******************************************************************        
099800*   1480-UPDATE-TGD-2-SCND-QTY                                   *        
099900* UPDATE TAGGED TO SCANNED QTY                                   *        
195200******************************************************************        
100200 1480-UPDATE-TGD-2-SCND-QTY.                                              
100300                                                                          
RFAUDT     MOVE SU006-AUDIT-CNTL-NBR TO SUAUPC-AUD-CNTL-NBR.                    
100500     MOVE SU006-SHIP-UNIT-NBR  TO SUAUPC-SHIPT-UNT-ID.                    
100600     MOVE SU006-UPC            TO SUAUPC-UPC-NBR.                         
100600     MOVE SU006-SCN-TMST       TO SUAUPC-SCN-TMST.                        
100700                                                                          
100800     EXEC SQL                                                             
100900         UPDATE TSUAUPC                                                   
101000            SET RFID_TGD_QTY   = SCND_UNT_QTY                             
101100          WHERE AUD_CNTL_NBR = :SUAUPC-AUD-CNTL-NBR                       
101200            AND SHIPT_UNT_ID = :SUAUPC-SHIPT-UNT-ID                       
101300            AND UPC_NBR      = :SUAUPC-UPC-NBR                            
100600            AND SCN_TMST     = :SUAUPC-SCN-TMST                           
101400     END-EXEC.                                                            
101500                                                                          
101600     EVALUATE TRUE                                                        
101700         WHEN SQLCODE = ZERO                                              
101800              CONTINUE                                                    
096800                                                                          
101900         WHEN SQLWARN0 NOT = SPACES                                       
102000         WHEN SQLCODE < ZERO                                              
102100         WHEN SQLCODE > ZERO                                              
102200             MOVE '1480-UPDATE-TGD-2-SCND-QTY'                            
102300                                        TO DP013-PARAGRAPH                
102400             MOVE 'UPDATE TSUAUPC'      TO DP013-MESSAGE-TEXT(1)          
102450             MOVE SUAUPC-AUD-CNTL-NBR   TO PV-DISP-AUD-CNTL-NBR           
102700             MOVE SU006-SHIP-UNIT-NBR   TO PV-DISP-SHIPT-UNT-ID           
102900             MOVE SU006-UPC             TO PV-DISP-UPC-NBR                
103100             MOVE PV-DISPLAY-KEYS       TO DP013-MESSAGE-TEXT(2)          
098200             MOVE SUAUPC-SCN-TMST       TO PV-DISP-SCN-TMST               
098300             MOVE PV-DISP-SCN-TMST      TO DP013-MESSAGE-TEXT(3)          
103200             MOVE SQLCA                 TO DP013-SQLCA                    
103300             MOVE 'TSUAUPC'             TO DP013-DB2-TABLE-NAME(1)        
103400             SET DP013-DB2-ABEND        TO TRUE                           
103500             SET DP013-NO-ROLLBACK      TO TRUE                           
103600             PERFORM 9999-WRITE-CICS-LOG-MSG                              
103700             MOVE PM-DB2-FATAL-ERROR-MSG TO AMSGLNO                       
103800             PERFORM 8100-SEND-DATA-ONLY                                  
103900             PERFORM 8200-RETURN-TRANID                                   
104000     END-EVALUATE.                                                        
104000                                                                          
195200******************************************************************        
104300*   1500-CHECK-EXCEPTIONS                                        *        
104400* CHECK FLAGS TO DETERMINE WHICH EXCEPTIONS WERE FOUND DURING    *        
104500* THE LOOKUP PROCESS                                             *        
195200******************************************************************        
104700 1500-CHECK-EXCEPTIONS.                                                   
104800                                                                          
104900     MOVE PC-CARTON-AUDIT-GROUP-CODE  TO SUEXCP-VILN-GP-CDE.              
105000                                                                          
106200** IF A UPC-LOOKUP PROBLEM EXISTS FROM THE UPC SCAN SCREEN                
106300** CREATE AN EXCEPTION                                                    
106400     IF SU006-NO-UPC-LOOKUP-ERROR                                         
106500        CONTINUE                                                          
106600     ELSE                                                                 
106700        MOVE SU006-UPC-LOOKUP-IND     TO SUEXCP-VILN-GP-SEQ-NBR           
106800        SET SU006-NO-UPC-LOOKUP-ERROR TO TRUE                             
106900        PERFORM 1501-CREATE-EXCEPTION                                     
107000     END-IF.                                                              
107100                                                                          
107200*  WHEN A PO ALLOCATION PROBLEM OR A CARTONIZATION PROBLEM                
107300*  EXISTS, CREATE THE EXCEPTION(S) AND XCTL TO THE MESSAGE                
107400*  SCREEN.  WE ARE ONLY GOING TO GO TO THE MESSAGE SCREEN                 
107500*  ONCE PER "LOOKUP", SO BOTH ERRORS HAVE TO BE CHECKED AND               
107600*  THE APPROPRIATE MESSAGE PARMS SET-UP FOR ALL COMBINATIONS              
107700     MOVE PC-MESSAGE-SCREEN-PROGRAM-NAME                                  
107800                                      TO PV-TRANSFER-PROGRAM.             
107900     MOVE PC-CURRENT-PROGRAM-NAME     TO SU006-LAST-PROGRAM.              
108000     MOVE PC-CARTON-AUDIT-GROUP-CODE  TO SUEXCP-VILN-GP-CDE.              
108100                                                                          
108200** GB 03/21/14 BEG                                                        
108200** COMMENT OF WHEN STMT AS SU006-NO-PO-ALLOC-ERROR WILL                   
      ** NEVER BE TRUE WHEN SU006-STYLE-SUB-ERROR  IS SET                       
108200**   EVALUATE TRUE                                                        
108300**   WHEN SU006-NO-PO-ALLOC-ERROR                                         
108400         IF SU006-RF-UPC-NOT-IN-CRTN-ERROR                                
               OR SU006-STYLE-SUB-ERROR                                         
108700*         CARTONIZATION EXCEPTION                                         
                 IF SU006-RFID-AUD-ONLY                                         
108400             IF SU006-RF-UPC-NOT-IN-CRTN-ERROR                            
109400               SET PM-UPC-NOT-ON-CRTN   TO TRUE                           
                   ELSE                                                         
110600               SET PM-STYLE-SUB         TO TRUE                           
                   END-IF                                                       
109000**           SET SU006-RF-UPC-NOT-IN-CRTN-ERROR                           
109100**                                    TO TRUE                             
108800             MOVE SU006-UPC-CARTON-IND                                    
108900                                      TO SUEXCP-VILN-GP-SEQ-NBR           
      * 04/14/14 RLK BEG                                                        
      *** WHEN A UPC MISPACK EXCEPTION IS CREATED A SEPARATE TSUAUPC            
      *** IS CREATED FOR IT WITH A ZERO SCANNED AND TAGGED QTY. THE             
      *** SU006-SCN-TMST IS NOT UPDATED AS THE FOLLOWING PROGRAMS WILL          
      *** UPDATE THE ORIGINAL TSUAUPC ROW WITH ADJUSTMENTS TO                   
      *** TAGGED QTY.                                                           
RFAUDT             MOVE 0          TO SUAUPC-SCND-UNT-QTY                       
094300             MOVE 0          TO SUAUPC-RFID-TGD-QTY                       
050300             PERFORM 1400-CREATE-AUDIT-ROW                                
      * 04/14/14 RLK END                                                        
109200             PERFORM 1501-CREATE-EXCEPTION                                
109300**         XCTL TO MESSAGE SCREEN                                         
      *** FOR RFID AUDITS THE UPC NOT IN CARTON EXCEPTION WILL BE USED          
      *** FOR BOTH THE UPC NOT IN CARTON AND STYLE SUB ERRORS.                  
109400***          SET PM-UPC-NOT-ON-CRTN   TO TRUE                             
109500             MOVE PM-PGM-MSG-NBR      TO SU006-PGM-MSG-NBR                
109600             PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                        
109700           END-IF                                                         
109700         END-IF.                                                          
109800                                                                          
117900**   END-EVALUATE.                                                        
118000                                                                          
195200******************************************************************        
118200*   1501-CREATE-EXCEPTION                                        *        
195200******************************************************************        
118500 1501-CREATE-EXCEPTION.                                                   
RFAUDT                                                                          
RFAUDT     IF (SU006-QC-AND-RFID-AUD                                            
RFAUDT     AND SU006-MANUAL-GENERATED-AUDIT)                                    
RFAUDT      OR SU006-QC-AUD-ONLY                                                
RFAUDT      OR SU006-RFID-AUD-ONLY                                              
RFAUDT**  FOR A MANUAL GENERATED AUDIT ONLY 1 ROW EXISTS AND WE WILL USE        
RFAUDT**  THE AUDIT CONTROL NUMBER FROM THAT                                    
RFAUDT        MOVE SU006-AUDIT-CNTL-NBR    TO SUEXCP-AUD-CNTL-NBR               
RFAUDT     ELSE                                                                 
RFAUDT        MOVE SU006-RFID-AUDIT-CNTL-NBR                                    
RFAUDT                                     TO SUEXCP-AUD-CNTL-NBR               
RFAUDT     END-IF.                                                              
118700     MOVE SU006-SHIP-UNIT-NBR        TO SUEXCP-SHIPT-UNT-ID.              
      * 04/14/14 RLK BEG                                                        
118800     MOVE SUAUPC-SCN-TMST            TO SUEXCP-SCN-TMST.                  
      * 04/14/14 RLK END                                                        
118900     MOVE SU006-UPC                  TO SUEXCP-UPC-NBR.                   
119000     MOVE SPACES                     TO SUEXCP-OVRRID-SYS-DAT-TXT.        
119100                                                                          
119200     EXEC SQL                                                             
119300         INSERT  INTO  TSUEXCP                                            
119400                (AUD_CNTL_NBR                                             
119500               , SHIPT_UNT_ID                                             
119600               , UPC_NBR                                                  
119700               , SCN_TMST                                                 
119800               , VILN_GP_CDE                                              
119900               , VILN_GP_SEQ_NBR                                          
120000               , OVRRID_SYS_DAT_TXT)                                      
120100         VALUES                                                           
120200             (  :SUEXCP-AUD-CNTL-NBR                                      
120400              , :SUEXCP-SHIPT-UNT-ID                                      
120500              , :SUEXCP-UPC-NBR                                           
120600              , :SUEXCP-SCN-TMST                                          
120700              , :SUEXCP-VILN-GP-CDE                                       
120800              , :SUEXCP-VILN-GP-SEQ-NBR                                   
120900              , :SUEXCP-OVRRID-SYS-DAT-TXT)                               
121000     END-EXEC.                                                            
121100                                                                          
121200     EVALUATE TRUE                                                        
121300         WHEN SQLCODE = ZERO                                              
121400             CONTINUE                                                     
121500                                                                          
121600         WHEN SQLWARN0 NOT = SPACES                                       
121700         WHEN SQLCODE < ZERO                                              
121800         WHEN SQLCODE > ZERO                                              
118500             MOVE '1501-CREATE-EXCEPTION'                                 
121900                                         TO DP013-PARAGRAPH               
122100             MOVE 'INSERT TO TSUEXCP TABLE'                               
122200                                         TO DP013-MESSAGE-TEXT(1)         
122250             MOVE SUEXCP-AUD-CNTL-NBR    TO PV-DISP-AUD-CNTL-NBR          
122300             MOVE PV-DISP-AUD-CNTL-NBR   TO DP013-MESSAGE-TEXT(2)         
122300             MOVE SU006-SHIP-UNIT-NBR-X  TO DP013-MESSAGE-TEXT(3)         
122400             MOVE SQLCA                  TO DP013-SQLCA                   
122500             MOVE 'TSUEXCP'              TO                               
122500                                         DP013-DB2-TABLE-NAME(1)          
122600             SET DP013-DB2-ABEND         TO TRUE                          
122700             SET DP013-NO-ROLLBACK       TO TRUE                          
122800             PERFORM 9999-WRITE-CICS-LOG-MSG                              
122900             MOVE PM-DB2-FATAL-ERROR-MSG TO CA-RETURN-MSG                 
123000             SET CA-RETURN-ERROR         TO TRUE                          
123100                                                                          
123200*          RETURN TO THE PROGRAM THAT CALLED IT                           
123300             MOVE SU006-LAST-PROGRAM     TO PV-TRANSFER-PROGRAM           
123400             PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                        
123500     END-EVALUATE.                                                        
123600                                                                          
123700*  IF THE CURRENT SHIPPING UNIT AUDIT RECORD DOES NOT HAVE ANY            
123800*  EXCEPTIONS CREATED YET, SET THE INDICATOR TO SAY THAT IT               
123900*  DOES NOW                                                               
124000     IF SU006-NO-AUD-ERR                                                  
124100         PERFORM 1600-UPDATE-SHIP-UNIT                                    
124200     END-IF.                                                              
124300                                                                          
195200******************************************************************        
135300*   1600-UPDATE-SHIP-UNIT                                        *        
135400* UPDATE THE AUDIT ERROR INDICATOR TO SAY THERE ARE EXCEPTIONS   *        
135500* FOR THIS SHIPPING UNIT AUDIT                                   *        
195200******************************************************************        
135700 1600-UPDATE-SHIP-UNIT.                                                   
135800                                                                          
RFAUDT     IF (SU006-QC-AND-RFID-AUD                                            
RFAUDT     AND SU006-MANUAL-GENERATED-AUDIT)                                    
RFAUDT      OR SU006-QC-AUD-ONLY                                                
RFAUDT      OR SU006-RFID-AUD-ONLY                                              
RFAUDT**  FOR A MANUAL GENERATED AUDIT ONLY 1 ROW EXISTS AND WE WILL USE        
RFAUDT**  THE AUDIT CONTROL NUMBER FROM THAT                                    
RFAUDT        MOVE SU006-AUDIT-CNTL-NBR                                         
RFAUDT                               TO SUATRN-AUD-CNTL-NBR                     
RFAUDT     ELSE                                                                 
RFAUDT        MOVE SU006-RFID-AUDIT-CNTL-NBR                                    
RFAUDT                               TO SUATRN-AUD-CNTL-NBR                     
RFAUDT     END-IF.                                                              
136000     MOVE SU006-SHIP-UNIT-NBR  TO SUATRN-SHIPT-UNT-ID.                    
136100                                                                          
136200     EXEC SQL                                                             
136300         UPDATE TSUATRN                                                   
136400            SET UNT_AUD_ERR_IND = 'Y'                                     
136500          WHERE AUD_CNTL_NBR = :SUATRN-AUD-CNTL-NBR                       
136600            AND SHIPT_UNT_ID = :SUATRN-SHIPT-UNT-ID                       
136700     END-EXEC.                                                            
136800                                                                          
136900     EVALUATE TRUE                                                        
137000         WHEN SQLCODE = ZERO                                              
137100             SET SU006-AUD-ERR TO TRUE                                    
137200                                                                          
137300         WHEN SQLWARN0 NOT = SPACES                                       
137400         WHEN SQLCODE < ZERO                                              
137500         WHEN SQLCODE > ZERO                                              
137600             MOVE '1600-UPDATE-SHIP-UNIT'                                 
137700                                         TO DP013-PARAGRAPH               
137800             MOVE 'UPDATE THE AUDIT ERROR CODE'                           
137900                                         TO DP013-MESSAGE-TEXT(1)         
127800             MOVE SUATRN-AUD-CNTL-NBR    TO PV-DISP-AUD-CNTL-NBR          
127800             MOVE PV-DISP-AUD-CNTL-NBR   TO DP013-MESSAGE-TEXT(2)         
138100             MOVE SU006-SHIP-UNIT-NBR-X  TO DP013-MESSAGE-TEXT(3)         
138200             MOVE SQLCA                  TO DP013-SQLCA                   
138300             MOVE 'TSUATRN'              TO                               
138300                                         DP013-DB2-TABLE-NAME(1)          
138400             SET DP013-DB2-ABEND         TO TRUE                          
138500             SET DP013-NO-ROLLBACK       TO TRUE                          
138600             PERFORM 9999-WRITE-CICS-LOG-MSG                              
138700             MOVE PM-DB2-FATAL-ERROR-MSG TO CA-RETURN-MSG                 
138800             SET CA-RETURN-ERROR         TO TRUE                          
138900                                                                          
139000*          RETURN TO THE PROGRAM THAT CALLED IT                           
139100             MOVE SU006-LAST-PROGRAM     TO PV-TRANSFER-PROGRAM           
139200             PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                        
139300     END-EVALUATE.                                                        
139500                                                                          
195200******************************************************************        
139700*   1700-CERTIFY-UPC                                             *        
139800* UPDATE THE UPC CERTIFICATION FLAG TO 'Y' ON BOTH TUPC AND      *        
139900* TUPCSKU TABLES                                                 *        
195200******************************************************************        
140100 1700-CERTIFY-UPC.                                                        
140200                                                                          
140300     MOVE SU006-UPC                 TO DK-UPC-NUMBER.                     
140400     SET PS-DATABASE-CONTENTION-NO  TO TRUE.                              
140500                                                                          
140600     EXEC SQL                                                             
140700         UPDATE TUPC                                                      
140800            SET CERTD_IND           = 'Y'                                 
140900              , CERTD_DTE           =  CURRENT DATE                       
141000              , LAST_UPD_BY_USR_ID  = :SU006-USERID                       
141100              , LAST_UPD_TMST       =  CURRENT TIMESTAMP                  
141200          WHERE UPC_NBR = :DK-UPC-NUMBER                                  
141300     END-EXEC.                                                            
141400                                                                          
141500     EVALUATE TRUE                                                        
141600         WHEN SQLCODE = ZERO                                              
141700             PERFORM 1701-UPDATE-XLT-UPC                                  
141800                                                                          
141900         WHEN SQLCODE = -904                                              
142000         WHEN SQLCODE = -913                                              
142100              SET PS-DATABASE-CONTENTION-YES  TO  TRUE                    
142200                                                                          
142300         WHEN SQLWARN0 NOT = SPACES                                       
142400         WHEN SQLCODE < ZERO                                              
142500         WHEN SQLCODE > ZERO                                              
142600             MOVE '1700-CERTIFY-UPC'     TO DP013-PARAGRAPH               
142800             MOVE SU006-UPC-X            TO DP013-MESSAGE-TEXT(1)         
142900             MOVE SQLCA                  TO DP013-SQLCA                   
143000             MOVE 'TUPC   '              TO                               
143110                                         DP013-DB2-TABLE-NAME(1)          
143100             SET DP013-DB2-ABEND         TO TRUE                          
143200             SET DP013-NO-ROLLBACK       TO TRUE                          
143300             PERFORM 9999-WRITE-CICS-LOG-MSG                              
143400             MOVE PM-DB2-FATAL-ERROR-MSG TO CA-RETURN-MSG                 
143500             SET CA-RETURN-ERROR         TO TRUE                          
143600                                                                          
143700*          RETURN TO THE PROGRAM THAT CALLED IT                           
143800             MOVE SU006-LAST-PROGRAM     TO PV-TRANSFER-PROGRAM           
143900             PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                        
144000     END-EVALUATE.                                                        
144100                                                                          
144200     IF PS-DATABASE-CONTENTION-NO                                         
144300         EXEC SQL                                                         
144400             UPDATE TUPCSKU                                               
144500                SET CERT_IND  = 'Y'                                       
144600                  , CERT_DTE  = CURRENT DATE                              
144700              WHERE UPC_NBR   = :DK-UPC-NUMBER                            
144800         END-EXEC                                                         
144900                                                                          
145000         EVALUATE TRUE                                                    
145100             WHEN SQLCODE = ZERO                                          
145200                 CONTINUE                                                 
145300                                                                          
145400             WHEN SQLWARN0 NOT = SPACES                                   
145500             WHEN SQLCODE < ZERO                                          
145600             WHEN SQLCODE > ZERO                                          
145700                 MOVE '1700-CERTIFY-UPC' TO DP013-PARAGRAPH               
145900                 MOVE SU006-UPC-X        TO DP013-MESSAGE-TEXT(1)         
146000                 MOVE SQLCA              TO DP013-SQLCA                   
146100                 MOVE 'TUPCSKU'          TO                               
146110                                         DP013-DB2-TABLE-NAME(1)          
146200                 SET DP013-DB2-ABEND     TO TRUE                          
146300                 SET DP013-NO-ROLLBACK   TO TRUE                          
146400                 PERFORM 9999-WRITE-CICS-LOG-MSG                          
146500                 MOVE PM-DB2-FATAL-ERROR-MSG TO CA-RETURN-MSG             
146600                 SET CA-RETURN-ERROR     TO TRUE                          
146700                                                                          
146800*              RETURN TO THE PROGRAM THAT CALLED IT                       
146900                 MOVE SU006-LAST-PROGRAM TO PV-TRANSFER-PROGRAM           
147000                 PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                    
147100         END-EVALUATE                                                     
147200     END-IF.                                                              
240900                                                                          
195200******************************************************************        
147400 1701-UPDATE-XLT-UPC.                                                     
147500                                                                          
147600     EXEC SQL                                                             
147700         UPDATE XLT_UPC                                                   
147800            SET CERTD_IND  = 'Y'                                          
147900              , CERTD_DTE  =  CURRENT DATE                                
148000          WHERE  UPC_NBR   = :DK-UPC-NUMBER                               
148100     END-EXEC.                                                            
148200                                                                          
148300     EVALUATE TRUE                                                        
148400         WHEN SQLCODE = ZERO                                              
148500             CONTINUE                                                     
148600                                                                          
148700         WHEN SQLWARN0 NOT = SPACES                                       
148800         WHEN SQLCODE < ZERO                                              
148900         WHEN SQLCODE > ZERO                                              
149000             MOVE '1701-UPDATE-XLT-UPC'  TO DP013-PARAGRAPH               
149200             MOVE SU006-UPC-X            TO DP013-MESSAGE-TEXT(1)         
149300             MOVE SQLCA                  TO DP013-SQLCA                   
149400             MOVE 'XLT_UPC'              TO                               
149410                                         DP013-DB2-TABLE-NAME(1)          
149500             SET DP013-DB2-ABEND         TO TRUE                          
149600             SET DP013-NO-ROLLBACK       TO TRUE                          
149700             PERFORM 9999-WRITE-CICS-LOG-MSG                              
149800             MOVE PM-DB2-FATAL-ERROR-MSG TO CA-RETURN-MSG                 
149900             SET CA-RETURN-ERROR         TO TRUE                          
150000                                                                          
150100*          RETURN TO THE PROGRAM THAT CALLED IT                           
150200             MOVE SU006-LAST-PROGRAM     TO PV-TRANSFER-PROGRAM           
150300             PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                        
150400     END-EVALUATE.                                                        
240900                                                                          
195200******************************************************************        
150700 2100-XCTL-TO-SELECTED-PROGRAM.                                           
240900                                                                          
150800     SET SU006-INITIALIZE-SCREEN   TO TRUE.                               
150900     MOVE LENGTH OF CA-COMMAREA    TO PV-COMMAREA-LENGTH.                 
151000                                                                          
151100     EXEC CICS                                                            
151200         XCTL                                                             
151300             PROGRAM   (PV-TRANSFER-PROGRAM)                              
151400             COMMAREA  (CA-COMMAREA)                                      
151500             LENGTH    (PV-COMMAREA-LENGTH)                               
151600             RESP      (PV-CICS-RESPONSE)                                 
151700     END-EXEC.                                                            
151800                                                                          
151900     EVALUATE TRUE                                                        
152000         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
152100              CONTINUE                                                    
190200                                                                          
152200         WHEN OTHER                                                       
152300              MOVE '2100-XCTL-TO-SELECTED-PROGRAM'                        
152400                                     TO DP013-PARAGRAPH                   
152500              MOVE 'UNABLE TO XCTL THE FOLLOWING PROGRAM: '               
152600                                     TO DP013-MESSAGE-TEXT (1)            
152700              MOVE PV-TRANSFER-PROGRAM                                    
152800                                     TO DP013-MESSAGE-TEXT (2)            
152900              SET DP013-CICS-ABEND   TO TRUE                              
153000              SET DP013-NO-ROLLBACK  TO TRUE                              
153100              PERFORM 9999-WRITE-CICS-LOG-MSG                             
153200              MOVE PM-TRANS-NOT-AVAILABLE-MSG                             
153300                                     TO AMSGLNO                           
153400              SET SU006-PROCESS-SCREEN TO TRUE                            
153500              PERFORM 8100-SEND-DATA-ONLY                                 
153600              PERFORM 8200-RETURN-TRANID                                  
153700     END-EVALUATE.                                                        
240900                                                                          
195200******************************************************************        
154100 2200-XCTL-TO-HELP.                                                       
154200                                                                          
154300     MOVE LENGTH OF PC192-COMMAREA    TO PV-COMMAREA-LENGTH.              
154400     SET SU006-INITIALIZE-SCREEN      TO TRUE.                            
154500     MOVE CA-COMMAREA                 TO PC192-HOST-SAVE-AREA.            
154600                                                                          
154700     EXEC CICS                                                            
154800         XCTL                                                             
154900             PROGRAM   (PV-TRANSFER-PROGRAM)                              
155000             COMMAREA  (PC192-COMMAREA)                                   
155100             LENGTH    (PV-COMMAREA-LENGTH)                               
155200             RESP      (PV-CICS-RESPONSE)                                 
155300     END-EXEC.                                                            
155400                                                                          
155500     EVALUATE TRUE                                                        
155600         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
155700              CONTINUE                                                    
068100                                                                          
155800         WHEN OTHER                                                       
155900              MOVE '2200-XCTL-TO-HELP'                                    
156000                                     TO DP013-PARAGRAPH                   
156100              MOVE 'UNABLE TO XCTL THE FOLLOWING PROGRAM: '               
156200                                     TO DP013-MESSAGE-TEXT (1)            
156300              MOVE PV-TRANSFER-PROGRAM                                    
156400                                     TO DP013-MESSAGE-TEXT (2)            
156500              SET DP013-CICS-ABEND   TO TRUE                              
156600              SET DP013-NO-ROLLBACK  TO TRUE                              
156700              PERFORM 9999-WRITE-CICS-LOG-MSG                             
156800              MOVE PC-TRANS-UNAVAILABLE-MSG                               
156900                                     TO AMSGLNO                           
157000              SET SU006-PROCESS-SCREEN TO TRUE                            
157100              PERFORM 8100-SEND-DATA-ONLY                                 
157200              PERFORM 8200-RETURN-TRANID                                  
157300     END-EVALUATE.                                                        
157400                                                                          
157500******************************************************************        
157600* 3000-CHECK-PO                                                  *        
157700*  CHECK TO MAKE SURE THE UPC/SKU IS ON THE PO THAT THIS CARTON  *        
157800* IS ASSOCIATED TO -- IF IT IS, IT GETS THE LINE NUMBERS TO      *        
157900* VERIFY THAT THE LINE IS ASSOCIATED TO THE STORE/DC BEING       *        
158000* PROCESSED.                                                     *        
158100******************************************************************        
158200 3000-CHECK-PO.                                                           
158300                                                                          
158400     PERFORM 3001-OPEN-PO-LINE-CURSOR.                                    
158500     PERFORM 3002-FETCH-PO-LINE-CURSOR.                                   
158600                                                                          
158700*  IF THERE ARE NO LINES FOR THIS SKU ON THIS PO, IT IS STYLE SUB         
158900     IF PS-EOC-PO-LINE-CURSOR                                             
159000         SET SU006-STYLE-SUB-ERROR TO TRUE                                
159100     ELSE                                                                 
159200*    DETERMINE WHAT TYPE OF DESTINATION IS ASSOCIATED TO THIS             
159300*    CARTON (IE. GOING TO A DC OR A STORE?) ,,,,,,                        
159400         IF SU006-STORE = 0                                               
159500             SET PS-DEST-IS-A-STR TO TRUE                                 
159600         ELSE                                                             
159700             PERFORM 3100-GET-DEST-TYPE                                   
159800         END-IF                                                           
159900     END-IF.                                                              
160000                                                                          
160100*  ....THEN FOR EACH LINE THIS ITEM IS ON, CHECK THE DISTRO               
160200*  DEPENDING ON THE TYPE OF THE DESTINATION (VERIFY THAT THE              
160300*  PO/LINE IS SUPPOSE TO BE GOING THROUGH THIS DC.......                  
160400     PERFORM UNTIL PS-EOC-PO-LINE-CURSOR                                  
160500                OR PS-DONE                                                
160600                                                                          
160700*      IT WAS DECIDED NOT TO CHECK FOR STORE ALLOCATIONS AS OF            
160800*      5/17/04 PER CATHY FAIRWEATHER                                      
160900         IF PS-DEST-IS-A-DC                                               
161000             PERFORM 3200-CHECK-DC-DISTRO                                 
161100         END-IF                                                           
161200                                                                          
161300         PERFORM 3002-FETCH-PO-LINE-CURSOR                                
161400     END-PERFORM.                                                         
161500                                                                          
161600*  CLOSE THE PO LINE CURSOR                                               
161700     PERFORM 3003-CLOSE-PO-LINE-CURSOR.                                   
161800                                                                          
161900******************************************************************        
162000* 3001-OPEN-PO-LINE-CURSOR                                       *        
162200******************************************************************        
162300 3001-OPEN-PO-LINE-CURSOR.                                                
162400                                                                          
162500     EXEC SQL                                                             
162600         OPEN PO-LINE-CURSOR                                              
162700     END-EXEC.                                                            
162800                                                                          
162900     EVALUATE TRUE                                                        
163000         WHEN SQLCODE = ZERO                                              
163100             SET PS-NOT-EOC-PO-LINE-CURSOR TO TRUE                        
163200                                                                          
163300         WHEN SQLWARN0 NOT = SPACES                                       
163400         WHEN SQLCODE < ZERO                                              
163500         WHEN SQLCODE > ZERO                                              
163600             MOVE '3001-OPEN-PO-LINE-CURSOR'                              
163700                                   TO DP013-PARAGRAPH                     
163800             MOVE 'OPEN PO-LINE-CURSOR'                                   
163900                                   TO DP013-MESSAGE-TEXT (1)              
164000             MOVE SU006-PO         TO DP013-MESSAGE-TEXT (2)              
164100             MOVE SU006-SKU-NBR    TO DP013-MESSAGE-TEXT (3)              
164200             MOVE SQLCA            TO DP013-SQLCA                         
164300             MOVE 'TPURCHS'        TO DP013-DB2-TABLE-NAME (1)            
164400             MOVE 'TPURITM'        TO DP013-DB2-TABLE-NAME (2)            
164500             SET DP013-DB2-ABEND   TO TRUE                                
164600             SET DP013-NO-ROLLBACK TO TRUE                                
164700             PERFORM 9999-WRITE-CICS-LOG-MSG                              
164800             MOVE PM-DB2-FATAL-ERROR-MSG TO AMSGLNO                       
164900             PERFORM 8100-SEND-DATA-ONLY                                  
165000             PERFORM 8200-RETURN-TRANID                                   
165100     END-EVALUATE.                                                        
165200                                                                          
165300******************************************************************        
165400* 3002-FETCH-PO-LINE-CURSOR                                      *        
165500*  FETCH THE NEXT OCCURANCE OF THE PO-LINE-CURSOR                *        
165600******************************************************************        
165700 3002-FETCH-PO-LINE-CURSOR.                                               
165800                                                                          
165900     EXEC SQL                                                             
166000         FETCH PO-LINE-CURSOR                                             
166100          INTO :PURITM-PO-LNE-NBR                                         
166200     END-EXEC.                                                            
166300                                                                          
166400     EVALUATE TRUE                                                        
166500         WHEN SQLCODE = ZERO                                              
166600             CONTINUE                                                     
166700                                                                          
166800         WHEN SQLCODE = +100                                              
166900             SET PS-EOC-PO-LINE-CURSOR       TO TRUE                      
167000                                                                          
167100         WHEN SQLWARN0 NOT = SPACES                                       
167200         WHEN SQLCODE < ZERO                                              
167300         WHEN SQLCODE > ZERO                                              
167400             MOVE '3002-FETCH-PO-LINE-CURSOR'                             
167500                                   TO DP013-PARAGRAPH                     
167600             MOVE 'FETCH OF PO-LINE-CUROSR'                               
167700                                   TO DP013-MESSAGE-TEXT (1)              
167800             MOVE SU006-PO         TO DP013-MESSAGE-TEXT (2)              
167900             MOVE SU006-SKU-NBR    TO DP013-MESSAGE-TEXT (3)              
168000             MOVE SQLCA            TO DP013-SQLCA                         
168100             MOVE 'TPURCHS'        TO DP013-DB2-TABLE-NAME (1)            
168200             MOVE 'TPURITM'        TO DP013-DB2-TABLE-NAME (2)            
168300             SET DP013-DB2-ABEND   TO TRUE                                
168400             SET DP013-NO-ROLLBACK TO TRUE                                
168500             PERFORM 9999-WRITE-CICS-LOG-MSG                              
168600             MOVE PM-DB2-FATAL-ERROR-MSG TO AMSGLNO                       
168700             PERFORM 8100-SEND-DATA-ONLY                                  
168800             PERFORM 8200-RETURN-TRANID                                   
168900     END-EVALUATE.                                                        
169000                                                                          
169100******************************************************************        
169200* 3003-CLOSE-PO-LINE-CURSOR                                      *        
169300*  CLOSE THE PO LINE CURSOR                                      *        
169400******************************************************************        
169500 3003-CLOSE-PO-LINE-CURSOR.                                               
169600                                                                          
169700     EXEC SQL                                                             
169800         CLOSE PO-LINE-CURSOR                                             
169900     END-EXEC.                                                            
170000                                                                          
170100     EVALUATE TRUE                                                        
170200         WHEN SQLCODE = ZERO                                              
170300             CONTINUE                                                     
170400                                                                          
170500         WHEN SQLWARN0 NOT = SPACES                                       
170600         WHEN SQLCODE < ZERO                                              
170700         WHEN SQLCODE > ZERO                                              
170800             MOVE '3003-CLOSE-PO-LINE-CURSOR'                             
170900                                   TO DP013-PARAGRAPH                     
171000             MOVE 'CLOSE OF PO-LINE-CURSOR'                               
171100                                   TO DP013-MESSAGE-TEXT (1)              
171200             MOVE SU006-PO         TO DP013-MESSAGE-TEXT (2)              
171300             MOVE SU006-SKU-NBR    TO DP013-MESSAGE-TEXT (3)              
171400             MOVE SQLCA            TO DP013-SQLCA                         
171500             MOVE 'TPURCHS'        TO DP013-DB2-TABLE-NAME (1)            
171600             MOVE 'TPURITM'        TO DP013-DB2-TABLE-NAME (2)            
171700             SET DP013-DB2-ABEND   TO TRUE                                
171800             SET DP013-NO-ROLLBACK TO TRUE                                
171900             PERFORM 9999-WRITE-CICS-LOG-MSG                              
172000             MOVE PM-DB2-FATAL-ERROR-MSG TO AMSGLNO                       
172100             PERFORM 8100-SEND-DATA-ONLY                                  
172200             PERFORM 8200-RETURN-TRANID                                   
172300     END-EVALUATE.                                                        
172400                                                                          
195200******************************************************************        
172600  3100-GET-DEST-TYPE.                                                     
172700                                                                          
172800     INITIALIZE XL071-COMMAREA-REC.                                       
172900     MOVE SU006-STORE TO XL071-LOCATION-NUMBER.                           
173100                                                                          
173200     CALL PC-LOCATION-MODULE  USING DFHEIBLK                              
173300                                    XL071-COMMAREA                        
173400                                    XL071-COMMAREA-REC.                   
173500                                                                          
173600     IF XL071-NO-ERRORS                                                   
173700          IF XL071-LOC-TYP-CDE EQUAL 'DC'                                 
173800              SET PS-DEST-IS-A-DC    TO TRUE                              
173900          ELSE                                                            
174000              SET PS-DEST-IS-A-STR   TO TRUE                              
174100          END-IF                                                          
174200     ELSE                                                                 
174300         MOVE '3100-GET-DEST-TYPE'   TO DP013-PARAGRAPH                   
174400         MOVE 'SELECT OF XLT_LOC TABLE'                                   
174400                                     TO DP013-MESSAGE-TEXT (1)            
174500         MOVE SU006-STORE            TO DP013-MESSAGE-TEXT (2)            
174600         MOVE SQLCA                  TO DP013-SQLCA                       
174700         MOVE 'XLT_LOC'              TO DP013-DB2-TABLE-NAME (1)          
174800         SET DP013-DB2-ABEND         TO TRUE                              
174900         SET DP013-NO-ROLLBACK       TO TRUE                              
175000         PERFORM 9999-WRITE-CICS-LOG-MSG                                  
175100         MOVE PM-DB2-FATAL-ERROR-MSG TO AMSGLNO                           
175200         PERFORM 8100-SEND-DATA-ONLY                                      
175300         PERFORM 8200-RETURN-TRANID                                       
175400     END-IF.                                                              
175500                                                                          
195200******************************************************************        
178700* 3200-CHECK-DC-DISTRO                                           *        
178800*      CHECK THE DISTRO OF THE PO TO MAKE SURE THERE IS A SPLIT  *        
178900*  FOR THE DC BEING PROCESSED - IE, SHOULD THIS STUFF BE HERE?   *        
195200******************************************************************        
179100 3200-CHECK-DC-DISTRO.                                                    
179200                                                                          
179300     MOVE SU006-OPER-UNT-ID        TO DK-DEST-NBR.                        
179400                                                                          
179500     EXEC SQL                                                             
179600         SELECT PLAN_NBR                                                  
179700           INTO :ALLPLN-PLAN-NBR                                          
179800           FROM TALLPLN                                                   
179900          WHERE PO_NBR     = :SU006-PO                                    
180000            AND PO_LNE_NBR = :PURITM-PO-LNE-NBR                           
180100            AND DEST_NBR   = :DK-DEST-NBR                                 
180100            WITH UR                                                       
180200     END-EXEC.                                                            
180300                                                                          
180400     EVALUATE TRUE                                                        
180500*      IF AN ALLOCATION WAS FOUND, SET FLAG TO GET OUT OF LOOP            
180600*      AND RESET ANY EXCEPTION THAT WAS CREATED FOR ANY OTHER             
180700*      LINE THIS ITEM WAS ON                                              
180800         WHEN SQLCODE = ZERO                                              
180900             SET PS-DONE                                                  
181000                 SU006-NO-PO-ALLOC-ERROR TO TRUE                          
181100                                                                          
181200*      IF NO ALLOCATION WAS FOUND FOR THIS LINE, SET BULK                 
181300*      MISDIRECT FLAG -- THIS FLAG COULD BE RE-SET IF AN                  
181400*      ALLOCATION FOR THIS ITEM IS FOUND ON ANOTHER LINE                  
181500         WHEN SQLCODE = +100                                              
181600             SET SU006-BULK-MISDIRECT-ERROR TO TRUE                       
181700                                                                          
181800         WHEN SQLWARN0 NOT = SPACES                                       
181900         WHEN SQLCODE < ZERO                                              
182000         WHEN SQLCODE > ZERO                                              
182100             MOVE '3200-CHECK-DC-DISTRO'                                  
182200                                        TO DP013-PARAGRAPH                
182300             MOVE 'SELECT OF ALLOCATION'                                  
182400                                        TO DP013-MESSAGE-TEXT (1)         
182500             MOVE SQLCA                 TO DP013-SQLCA                    
182600             MOVE 'TALLPLN'             TO                                
182600                                        DP013-DB2-TABLE-NAME (1)          
182700             SET DP013-DB2-ABEND        TO TRUE                           
182800             SET DP013-NO-ROLLBACK      TO TRUE                           
182900             PERFORM 9999-WRITE-CICS-LOG-MSG                              
183000             MOVE PM-DB2-FATAL-ERROR-MSG TO AMSGLNO                       
183100             PERFORM 8100-SEND-DATA-ONLY                                  
183200             PERFORM 8200-RETURN-TRANID                                   
183300     END-EVALUATE.                                                        
183500                                                                          
195200******************************************************************        
183700* 3201-CHECK-STR-DISTRO                                          *        
183800*      CHECK THE DISTRO OF THE PO TO MAKE SURE THERE IS          *        
183900*  ALLOATION FOR THE STORE ASSOCIATED TO THIS CARTON             *        
195200******************************************************************        
184100 3201-CHECK-STR-DISTRO.                                                   
184200                                                                          
184300     MOVE SU006-OPER-UNT-ID          TO DK-DEST-NBR.                      
184400                                                                          
184500     EXEC SQL                                                             
184600         SELECT  A.PLAN_NBR                                               
184700           INTO  :ALLPLN-PLAN-NBR                                         
184800           FROM  TALLPLN A                                                
184900              ,  TMESTDS B                                                
185000          WHERE A.PO_NBR      = :SU006-PO                                 
185100            AND A.PO_NBR      = B.PO_NBR                                  
185200            AND A.PO_LNE_NBR  = :PURITM-PO-LNE-NBR                        
185300            AND A.PO_LNE_NBR  = B.PO_LNE_NBR                              
185400            AND A.PLAN_NBR    = B.PLAN_NBR                                
185500            AND B.STR_NBR     = :SU006-STORE                              
185500           WITH UR                                                        
185600     END-EXEC.                                                            
185700                                                                          
185800     EVALUATE TRUE                                                        
185900*      IF AN ALLOCATION WAS FOUND, SET FLAG TO GET OUT OF LOOP            
186000*      AND RESET ANY EXCEPTION THAT WAS CREATED FOR ANY OTHER             
186100*      LINE THIS ITEM WAS ON                                              
186200         WHEN SQLCODE = ZERO                                              
186300             SET PS-DONE                                                  
186400                 SU006-NO-PO-ALLOC-ERROR TO TRUE                          
096800                                                                          
186500         WHEN SQLCODE = -811                                              
186600             SET PS-DONE                                                  
186700                 SU006-NO-PO-ALLOC-ERROR TO TRUE                          
186800                                                                          
186900*      IF NO ALLOCATION WAS FOUND FOR THIS LINE, SET NO ALLOC             
187000*      FLAG -- THIS FLAG COULD BE RE-SET IF AN ALLOCATION FOR             
187100*      THIS ITEM IS FOUND ON ANOTHER LINE                                 
187200         WHEN SQLCODE = +100                                              
187300             SET SU006-PBS-NO-ALLOC-ERROR TO TRUE                         
187400                                                                          
187500         WHEN SQLWARN0 NOT = SPACES                                       
187600         WHEN SQLCODE < ZERO                                              
187700         WHEN SQLCODE > ZERO                                              
187800             MOVE '3201-CHECK-STR-DISTRO'                                 
187900                                        TO DP013-PARAGRAPH                
188000             MOVE 'SELECT OF ALLOCATION'                                  
188100                                        TO DP013-MESSAGE-TEXT (1)         
193500             MOVE SU006-PO              TO DP013-MESSAGE-TEXT (2)         
193500             MOVE PURITM-PO-LNE-NBR     TO DP013-MESSAGE-TEXT (3)         
193500             MOVE SU006-STORE           TO DP013-MESSAGE-TEXT (4)         
188200             MOVE SQLCA                 TO DP013-SQLCA                    
188300             MOVE 'TALLPLN'             TO                                
188300                                        DP013-DB2-TABLE-NAME (1)          
188400             MOVE 'TMESTDS'             TO                                
188400                                        DP013-DB2-TABLE-NAME (2)          
188500             SET DP013-DB2-ABEND        TO TRUE                           
188600             SET DP013-NO-ROLLBACK      TO TRUE                           
188700             PERFORM 9999-WRITE-CICS-LOG-MSG                              
188800             MOVE PM-DB2-FATAL-ERROR-MSG TO AMSGLNO                       
188900             PERFORM 8100-SEND-DATA-ONLY                                  
189000             PERFORM 8200-RETURN-TRANID                                   
189100     END-EVALUATE.                                                        
189300                                                                          
189400******************************************************************        
189500*  5000-CHECK-SHIPPING-TABLES                                    *        
189600*  CHECK THE AUDIT CARTON TABLES TO SEE IF THIS UPC SHOULD BE    *        
189700*  IN THIS CARTON                                                *        
189800******************************************************************        
189900 5000-CHECK-SHIPPING-TABLES.                                              
190000                                                                          
190200     MOVE SU006-SHIP-UNIT-NBR      TO SHUUPC-SHIPT-UNT-ID.                
190300     MOVE SU006-UPC                TO SHUUPC-UPC-NBR.                     
190400     MOVE SU006-PO                 TO SHUUPC-TRN-NBR.                     
190500                                                                          
190700     EXEC SQL                                                             
190800         SELECT UPC_NBR                                                   
191000              , TRN_LNE_NBR                                               
191100           INTO :SHUUPC-UPC-NBR                                           
191300              , :SHUUPC-TRN-LNE-NBR                                       
191400           FROM TSHUUPC                                                   
191600          WHERE SHIPT_UNT_ID = :SHUUPC-SHIPT-UNT-ID                       
191800            AND TRN_NBR      = :SHUUPC-TRN-NBR                            
191900            AND UPC_NBR      = :SHUUPC-UPC-NBR                            
185500           WITH UR                                                        
192000     END-EXEC.                                                            
192100                                                                          
192200     EVALUATE TRUE                                                        
192300         WHEN SQLCODE = ZERO                                              
192400         WHEN SQLCODE = -811                                              
192500             CONTINUE                                                     
192600                                                                          
192700         WHEN SQLCODE = +100                                              
192800             PERFORM 5100-CHECK-SHIP-TABLE-SKU                            
192900                                                                          
193000         WHEN SQLWARN0 NOT = SPACES                                       
193100         WHEN SQLCODE < ZERO                                              
193200         WHEN SQLCODE > ZERO                                              
193300             MOVE '5000-CHECK-SHIPPING-TABLES'                            
193400                                   TO DP013-PARAGRAPH                     
193500             MOVE SHUUPC-SHIPT-UNT-ID                                     
193600                                   TO DP013-MESSAGE-TEXT (1)              
193700             MOVE SHUUPC-TRN-NBR   TO DP013-MESSAGE-TEXT (2)              
193900             MOVE SHUUPC-UPC-NBR   TO DP013-MESSAGE-TEXT (3)              
194100             MOVE SQLCA            TO DP013-SQLCA                         
194200             MOVE 'TSHUUPC'        TO DP013-DB2-TABLE-NAME (1)            
194300             SET DP013-DB2-ABEND   TO TRUE                                
194400             SET DP013-NO-ROLLBACK TO TRUE                                
194500             PERFORM 9999-WRITE-CICS-LOG-MSG                              
194600             MOVE PM-DB2-FATAL-ERROR-MSG TO AMSGLNO                       
194700             PERFORM 8100-SEND-DATA-ONLY                                  
194800             PERFORM 8200-RETURN-TRANID                                   
194900     END-EVALUATE.                                                        
195100                                                                          
195200******************************************************************        
195300*  5100-CHECK-SHIP-TABLE-SKU                                     *        
195400*  CHECK THE AUDIT CARTON TABLES TO SEE IF THIS UPC SHOULD BE    *        
195500*  IN THIS CARTON                                                *        
195600******************************************************************        
195700 5100-CHECK-SHIP-TABLE-SKU.                                               
195800                                                                          
196000     MOVE SU006-SHIP-UNIT-NBR      TO SHUUPC-SHIPT-UNT-ID.                
196100     MOVE SU006-PO                 TO SHUUPC-TRN-NBR.                     
196200                                                                          
196400     EXEC SQL                                                             
196500         SELECT XUPC.UPC_NBR                                              
196700           INTO :SHUUPC-UPC-NBR                                           
196900           FROM  TSHUUPC SUPC                                             
197100                ,XLV_UPC XUPC                                             
197200                ,XLV_SKU XSKU                                             
197300          WHERE SUPC.SHIPT_UNT_ID   = :SHUUPC-SHIPT-UNT-ID                
197500            AND SUPC.TRN_NBR        = :SHUUPC-TRN-NBR                     
197700            AND SUPC.UPC_NBR        = XUPC.UPC_NBR                        
197800            AND XUPC.INTR_UPC_ID    = XSKU.INTR_UPC_ID                    
198000            AND XSKU.ITM_NBR        = :XL001-ITM-NBR                      
198100            AND XSKU.SKU_NBR        = :XL001-SKU-NBR                      
185500           WITH UR                                                        
198200     END-EXEC.                                                            
198300                                                                          
198400     EVALUATE TRUE                                                        
198500         WHEN SQLCODE = ZERO                                              
198600         WHEN SQLCODE = -811                                              
198700             CONTINUE                                                     
198800                                                                          
198900         WHEN SQLCODE = +100                                              
                   IF SU006-RFID-AUD-ONLY                                       
199000               SET SU006-RF-UPC-NOT-IN-CRTN-ERROR                         
199100                                      TO TRUE                             
                   ELSE                                                         
      *** THIS PROGRAM PROCESSES ONLY RF AUDITS, SO, IF NOT AN RF               
      *** AUDIT ERROR NO EXCEPTION WILL BE CREATED.                             
                     CONTINUE                                                   
                   END-IF                                                       
199200                                                                          
199300         WHEN SQLWARN0 NOT = SPACES                                       
199400         WHEN SQLCODE < ZERO                                              
199500         WHEN SQLCODE > ZERO                                              
199600             MOVE '5100-CHECK-SHIP-TABLE-SKU'                             
199700                                   TO DP013-PARAGRAPH                     
199800             MOVE SHUUPC-SHIPT-UNT-ID                                     
199900                                   TO DP013-MESSAGE-TEXT (1)              
200000             MOVE SHUUPC-TRN-NBR   TO DP013-MESSAGE-TEXT (2)              
200200             MOVE SHUUPC-UPC-NBR   TO DP013-MESSAGE-TEXT (3)              
200400             MOVE SQLCA            TO DP013-SQLCA                         
200500             MOVE 'TSHUUPC'        TO DP013-DB2-TABLE-NAME (1)            
200600             SET DP013-DB2-ABEND   TO TRUE                                
200700             SET DP013-NO-ROLLBACK TO TRUE                                
200800             PERFORM 9999-WRITE-CICS-LOG-MSG                              
200900             MOVE PM-DB2-FATAL-ERROR-MSG TO AMSGLNO                       
201000             PERFORM 8100-SEND-DATA-ONLY                                  
201100             PERFORM 8200-RETURN-TRANID                                   
201200     END-EVALUATE.                                                        
201300                                                                          
195200******************************************************************        
230600 7200-GET-GARMENT-ON-HANGER.                                              
230700                                                                          
230800     INITIALIZE PV-DB2-COUNT.                                             
230700                                                                          
230900     EXEC SQL                                                             
231000         SELECT COUNT(*)                                                  
231100           INTO :PV-DB2-COUNT                                             
231200           FROM TPOAGMT                                                   
231300          WHERE PO_NBR         = :SU006-PO                                
231400            AND AGRMT_TYP_ID   = '4'                                      
231500            AND VER_STATUS_CDE = 'A'                                      
231600            AND TYP_CDE        = 2                                        
185500           WITH UR                                                        
231700     END-EXEC.                                                            
231800                                                                          
231900     EVALUATE TRUE                                                        
232000         WHEN SQLCODE = ZERO                                              
232100         WHEN SQLCODE = +100                                              
232200             IF PV-DB2-COUNT > +0                                         
232300                 MOVE 'Y'          TO CA-GOH-IND                          
232400             ELSE                                                         
232500                 MOVE 'N'          TO CA-GOH-IND                          
232600             END-IF                                                       
232700                                                                          
232800         WHEN SQLWARN0 NOT = SPACES                                       
232900         WHEN SQLCODE < ZERO                                              
233000         WHEN SQLCODE > ZERO                                              
233100             MOVE '7200-GET-GARMENT-ON-HANGER'                            
233200                                   TO DP013-PARAGRAPH                     
233300             MOVE 'SELECT TPOAGMT' TO DP013-MESSAGE-TEXT (1)              
200000             MOVE SU006-PO         TO DP013-MESSAGE-TEXT (2)              
233500             MOVE SQLCA            TO DP013-SQLCA                         
233600             MOVE 'TPOAGMT'        TO DP013-DB2-TABLE-NAME (1)            
233700             SET DP013-DB2-ABEND   TO TRUE                                
233800             SET DP013-NO-ROLLBACK TO TRUE                                
233900             PERFORM 9999-WRITE-CICS-LOG-MSG                              
234000     END-EVALUATE.                                                        
234100                                                                          
195200******************************************************************        
234200 7300-GET-BLACKOUT.                                                       
234300                                                                          
234400     SET PS-BLACKOUT-NO               TO TRUE.                            
234500                                                                          
234600     EXEC SQL                                                             
234700         SELECT 'Y'                                                       
234800         INTO :PS-BLACKOUT-SW                                             
235000          FROM WST_INTGRTN_TYP A                                          
235100             , WST_ATTR_DEFN   B                                          
235200             , WST_ATTR_ELEM   C                                          
235300             , WST_ATTR_ELEM   D                                          
235400         WHERE A.INTGRTN_TYP_CDE    =  'TUPCNOUPD'                        
235500           AND A.INTGRTN_TYP_CDE    =  B.INTGRTN_TYP_CDE                  
235600           AND B.ATTR_DEFN_SEQ_NBR  =  1                                  
235700           AND B.INTGRTN_TYP_CDE    =  C.INTGRTN_TYP_CDE                  
235800           AND C.ATTR_DEFN_SEQ_NBR  =  1                                  
235900           AND B.INTGRTN_TYP_CDE    =  D.INTGRTN_TYP_CDE                  
236000           AND D.ATTR_DEFN_SEQ_NBR  =  2                                  
236100           AND D.ATTR_ELEM_SEQ_NBR  =  C.ATTR_ELEM_SEQ_NBR                
236200           AND CURRENT TIME        >= TIME(C.ATTR_ELEM_TXT)               
236300           AND CURRENT TIME        <= TIME(D.ATTR_ELEM_TXT)               
236400         WITH UR                                                          
236600     END-EXEC.                                                            
236700                                                                          
236800     EVALUATE TRUE                                                        
236900         WHEN SQLCODE = ZERO                                              
237000         WHEN SQLCODE = +100                                              
237100             CONTINUE                                                     
237200                                                                          
237300         WHEN SQLWARN0 NOT = SPACES                                       
237400         WHEN SQLCODE < ZERO                                              
237500         WHEN SQLCODE > ZERO                                              
237600             MOVE '7300-GET-BLACKOUT'                                     
237700                                    TO DP013-PARAGRAPH                    
237800             MOVE 'SELECT FROM INTEGRATION TBLS'                          
237900                                    TO DP013-MESSAGE-TEXT (1)             
238000             MOVE SQLCA             TO DP013-SQLCA                        
238100             MOVE 'WST_INTGRTN_TYP' TO DP013-DB2-TABLE-NAME (1)           
238200             MOVE 'WST_ATTR_DEFN'   TO DP013-DB2-TABLE-NAME (2)           
238300             MOVE 'WST_ATTR_ELEM'   TO DP013-DB2-TABLE-NAME (3)           
238400             SET DP013-DB2-ABEND    TO TRUE                               
238500             SET DP013-NO-ROLLBACK  TO TRUE                               
238600             PERFORM 9999-WRITE-CICS-LOG-MSG                              
238700     END-EVALUATE.                                                        
238800                                                                          
195200******************************************************************        
238900 8000-SEND-MAP.                                                           
240900                                                                          
239000     EXEC CICS                                                            
239100         SEND                                                             
239200             MAP    ('SU710A')                                            
239300             MAPSET ('SUKM710')                                           
239400             ERASE                                                        
239500             RESP   (PV-CICS-RESPONSE)                                    
239600     END-EXEC.                                                            
239700                                                                          
239800     EVALUATE TRUE                                                        
239900         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
240000             CONTINUE                                                     
068100                                                                          
240100         WHEN OTHER                                                       
240200             MOVE '8000-SEND-MAP'   TO DP013-PARAGRAPH                    
240300             MOVE 'UNABLE TO SEND MAP'                                    
240400                                    TO DP013-MESSAGE-TEXT (1)             
240500             SET DP013-CICS-ABEND   TO TRUE                               
240600             SET DP013-NO-ROLLBACK  TO TRUE                               
240700             PERFORM 9999-WRITE-CICS-LOG-MSG                              
240800     END-EVALUATE.                                                        
240900                                                                          
195200******************************************************************        
241000 8100-SEND-DATA-ONLY.                                                     
241100                                                                          
241200     EXEC CICS                                                            
241300         SEND                                                             
241400             MAP      ('SU710A')                                          
241500             MAPSET   ('SUKM710')                                         
241600             CURSOR                                                       
241700             DATAONLY                                                     
241800             RESP     (PV-CICS-RESPONSE)                                  
241900     END-EXEC.                                                            
242000                                                                          
242100     EVALUATE TRUE                                                        
242200         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
242300             CONTINUE                                                     
068100                                                                          
242400         WHEN OTHER                                                       
242500             MOVE '8100-SEND-DATA-ONLY'                                   
242600                                    TO DP013-PARAGRAPH                    
242700             MOVE 'UNABLE TO SEND DATA'                                   
242800                                    TO DP013-MESSAGE-TEXT (1)             
242900             SET DP013-CICS-ABEND   TO TRUE                               
243000             SET DP013-NO-ROLLBACK  TO TRUE                               
243100             PERFORM 9999-WRITE-CICS-LOG-MSG                              
243200     END-EVALUATE.                                                        
240900                                                                          
195200******************************************************************        
243400 8200-RETURN-TRANID.                                                      
240900                                                                          
243500     MOVE LENGTH OF CA-COMMAREA  TO  PV-COMMAREA-LENGTH.                  
243600                                                                          
243700     EXEC CICS                                                            
243800         RETURN                                                           
243900             TRANSID   (PC-CURRENT-TRANID)                                
244000             COMMAREA  (CA-COMMAREA)                                      
244100             LENGTH    (PV-COMMAREA-LENGTH)                               
244200             RESP      (PV-CICS-RESPONSE)                                 
244300     END-EXEC.                                                            
244400                                                                          
244500     EVALUATE TRUE                                                        
244600         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
244700             CONTINUE                                                     
068100                                                                          
244800         WHEN OTHER                                                       
244900             MOVE '8200-RETURN-TRANID'                                    
245000                                    TO DP013-PARAGRAPH                    
245100             MOVE 'UNABLE TO RETURN TO TRANID'                            
245200                                    TO DP013-MESSAGE-TEXT (1)             
245300             SET DP013-CICS-ABEND   TO TRUE                               
245400             SET DP013-NO-ROLLBACK  TO TRUE                               
245500             PERFORM 9999-WRITE-CICS-LOG-MSG                              
245600     END-EVALUATE.                                                        
240900                                                                          
195200******************************************************************        
245800 8600-CALL-XLKUT001.                                                      
245900                                                                          
246000     CALL XL001-VSM-PGM USING DFHEIBLK                                    
246100                              XL001-COMMAREA                              
246200                              XL001-ITEM-COMMAREA.                        
246300                                                                          
246400     EVALUATE TRUE                                                        
246500         WHEN XL001-SUCCESSFUL                                            
246600         WHEN XL001-SKU-NOT-FOUND                                         
246700         WHEN XL001-UPC-NOT-FOUND                                         
246800             CONTINUE                                                     
246800                                                                          
246900         WHEN XL001-SQL-ERROR                                             
247000         WHEN XL001-INVALID-ACCESS                                        
247100            MOVE XL001-SKU-SQLCODE     TO PV-DISP-SKU-SQL-CDE             
247200            MOVE XL001-VND-STYL-SQLCODE                                   
247300                                       TO PV-DISP-VS-SQL-CDE              
247400            MOVE XL001-SKU-STR-SQLCODE TO PV-DISP-SKU-STR-SQL-CDE         
247800            MOVE XL001-UPC-SQLCODE     TO PV-DISP-UPC-SQL-CDE             
247900            MOVE '8600-CALL-XLKUT001'  TO DP013-PARAGRAPH                 
248000            STRING  'SUKRF710 - DB2 ERROR IN PROGRAM: '                   
248100                     XL001-RETURN-CODE                                    
                          '  UPC/SKU = '       PV-DISP-VALUE                    
248200                        DELIMITED BY SIZE                                 
248300                                    INTO DP013-MESSAGE-TEXT (1)           
248400            STRING 'ERROR PARA = '       XL001-ERROR-PARAGRAPH-NBR        
248600                    'CONT PARA = '   XL001-CONTENTION-ERR-PARA-NBR        
248800                        DELIMITED BY SIZE                                 
248900                                    INTO DP013-MESSAGE-TEXT (2)           
249000            STRING 'SKU SQL = '          PV-DISP-SKU-SQL-CDE              
249200                   ' VND-STYL SQL = '    PV-DISP-VS-SQL-CDE               
249400                        DELIMITED BY SIZE                                 
249500                                    INTO DP013-MESSAGE-TEXT (3)           
249600            STRING ' SKU-STOR SQL = '    PV-DISP-SKU-STR-SQL-CDE          
250000                   ' UPC SQL = '         PV-DISP-UPC-SQL-CDE              
250200                        DELIMITED BY SIZE                                 
250300                                    INTO DP013-MESSAGE-TEXT (4)           
250400             SET DP013-CICS-ABEND                                         
250500                 DP013-ROLLBACK       TO TRUE                             
250600             PERFORM 9999-WRITE-CICS-LOG-MSG                              
250700             MOVE PM-DB2-FATAL-ERROR-MSG TO AMSGLNO                       
250800             PERFORM 8100-SEND-DATA-ONLY                                  
250900             PERFORM 8200-RETURN-TRANID                                   
246800                                                                          
251000         WHEN OTHER                                                       
251100             MOVE '8600-CALL-XLKUT001' TO DP013-PARAGRAPH                 
251200             MOVE 'SUKRF710 - UNDETERMINED ERROR IN PROGRAM: '            
251300                                      TO DP013-MESSAGE-TEXT (1)           
251400             MOVE 'XLKUT001'          TO DP013-MESSAGE-TEXT (2)           
251500             SET DP013-CICS-ABEND                                         
251600                 DP013-ROLLBACK       TO TRUE                             
251700             PERFORM 9999-WRITE-CICS-LOG-MSG                              
251800             MOVE PM-DB2-FATAL-ERROR-MSG TO AMSGLNO                       
251900             PERFORM 8100-SEND-DATA-ONLY                                  
252000             PERFORM 8200-RETURN-TRANID                                   
252100     END-EVALUATE.                                                        
240900                                                                          
195200******************************************************************        
252300 9999-WRITE-CICS-LOG-MSG.                                                 
240900                                                                          
252400     MOVE PC-CURRENT-PROGRAM-NAME    TO DP013-PROGRAM.                    
252500     MOVE DFHEIBLK                   TO DP013-DFHEIBLK.                   
252600     INITIALIZE DP013-SRC-TASK-NUMBER                                     
252700                DP013-STACK.                                              
252800     SET DP013-LINK-RETURN           TO TRUE.                             
252900                                                                          
253000     EXEC CICS                                                            
253100         LINK                                                             
253200             PROGRAM   (DP013-NAME)                                       
253300             COMMAREA  (DP013-PARAMETERS)                                 
253400             LENGTH    (DP013-LENGTH)                                     
253500     END-EXEC.                                                            
253600                                                                          
253700*----------------------------------------------------------------*        
253800*    ABEND PROCESSOR MODULE                                      *        
253900*----------------------------------------------------------------*        
254100     COPY DPPD014.                                                        
254200                                                                          
