       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.             DAKUT301.                                        
       AUTHOR.                 BOB MCASEY.                                      
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           JUNE 2000.                                       
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *  PROGRAM NARRATIVE:                                           *         
      *  T H I S   I S   N O T   A   D B 2   P R O G R A M            *         
      *  C O M P I L E   I T   L I K E   A   R E G U L A R            *         
      *  P R O G R A M !!!!!!                                         *         
      *                                                               *         
      *  THIS PROGRAM READS AN EXTRACT FROM THE DEBIT ALLOWANCE ORACLE*         
      *  ONLINE APPLICATION AND CREATES AP TRANSACTION RECORDS.       *         
      *  IT CALLS A SUBROUTINE TO OBTAIN THE NECESSARY DB2 INFO       *         
      *****************************************************************         
RB0201*---------------------------------------------------------------*         
RB0201*   CHANGES:      SEE LINES WITH RB0201.                        *         
RB0201*                                                               *         
RB0201*   DATE:         02/19/01                                      *         
RB0201*                                                               *         
RB0201*   DESCRIPTION:  INCLUDE THE FISCAL WEEK FOR THE MONTH INSTEAD *         
RB0201*                 OF THE FISCAL WEEK FOR THE YEAR.              *         
RB0201*                                                               *         
RB0201*   MADE BY: RONNA BUTZ                 IR #:                   *         
RB0201*                                                               *         
0301RM*                                                               *         
  |   *   DATE: 03/06/2001     CHANGE ID: 394831     WHO: BOB MCASEY  *         
  |   * DESCRIPTION OF CHANGE:                                        *         
  |   * ADDED LOGIC TO LOAD THE DATE THE TRANSACTIONS ARE PROCESSED ON*         
  |   * THE APRD045 (AND THEN SOME) RECORD. THIS IS NEEDED FOR REPORT *         
  |   * DAKRP102 TO SHOW THE DAY THE TXNS WERE POSTED.                *         
  |   * CHANGE THE FISCAL WEEK NUMBER BEING PASSED TO A/P TO A 2 DIGIT*         
0301RM* NUMBER.  A/P REQUIRES THE FIELD TO BE D\2 DIGITS.             *         
0308DM*                                                               *         
  |   *   DATE: 03/08/2001     CHANGE ID: WR20419    WHO: DAVE METZGER*         
  |   * DESCRIPTION OF CHANGE:                                        *         
  |   * ADDED NEW INPUT FILE (WEEKLY DATE FILE) SO THAT THE OPEN FSCL *         
  |   * WEEK DATE (DA104-OPN-FSCL-WK-DTE) COULD BE MOVED TO THE A/P   *         
  |   * INVENTROY CUTOFF DATE (AP045-INV-CUTOFF-DTE).                 *         
  |   * ALSO CHANGED CODE TO MOVE                                     *         
  |   * CURRENT DATE (CC-CONTROL-DTE) TO A/P DOC DTE (AP045-DOC-DTE)  *         
  |   * CURRENT DATE (CC-CONTROL-DTE) TO A/P ENTR DTE(AP045-ENTR-DTE) *         
0413DM*                                                               *         
  |   *   DATE: 04/13/2001     CHANGE ID: WR20419    WHO: DAVE METZGER*         
  |   * DESCRIPTION OF CHANGE:                                        *         
  |   * ADDED LOGIC TO SET STORE NUMBER TO 090 FOR INTERNAL LOADS FOR *         
  |   * A/P TRAN CODE 69 ONLY.  THE OTHER PORTION OF THIS TRANSACTION *         
  |   * A/P TRAN CODE 19 WILL STILL HAVE STORE NUMBER 400.            *         
0701RM*                                                               *         
  |   *   DATE: 07/20/2001     CHANGE ID: WR22467    WHO: BOB MCASEY  *         
  |   * DESCRIPTION OF CHANGE:                                        *         
  |   * CHANGED RECORD LENGTH OF AP TRANS FILE AND ALL PROCESSING     *         
  |   * ASSOCIATED WITH IT. THE RECORD LENGTH WENT FROM 400 TO 500 AND*         
  |   * THIS PROGRAM ADDED 50 BYTES TO THAT ALSO.                     *         
      *                                                               *         
      *  WR/PITS#     DATE        DESCRIPTION                                   
      *  --------   ----------   ---------------------------------              
      * WR23724     05/15/2002   CHANGED TO INITIALIZE AP045-SKU-NBR            
      *                          IN APRD045 COPYBOOK                            
22405 * 22405  05-31-2001 R JANSSEN  DAII - ASSET RECOVERY ENHANCEMENT          
      * 26601  04-20-2004 S JACKSON  STORE NUMBER EXPANSION                     
      *W30021  02-02-2006 J BLAZICH  VENDOR NOTES TO CONTAIN TYPE/REASON        
S30021*S30021  03-20-2006 S JACKSON  UPDATE REASON CODES TO MATCH TDACDE        
P30021*P30021  05-03-2006 P KEBER    VENDOR CONTRACTS CHANGES TO PROCESS        
P30021*                              NEW OFF INVOICE DEBIT TYPES                
J30021*J30021  07-12-2006 J LIN      SPACES OUT DEPT NBR FOR 7C TRANS           
I30021*I30021  10-11-2006 J LIN      INCREASE TTRANCDE-TABLE SIZE               
265366*265366 01-08-2022 B SCHULTZ   REMOVE MOVING SPACES TO DEPT NBR           
265366*                              AND CL-NBR FOR 7C AND 7D TRANS             
265366*                              THIS WAS DONE SO AP CAN GET DEPT           
265366*                              AND CL-NBR UPDATED ON AP045 RECORDS        
      *---------------------------------------------------------------*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.                        IBM-3090.                        
       OBJECT-COMPUTER.                        IBM-3090.                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  IN-DA-FILE        ASSIGN TO INP01.                           
                                                                                
           SELECT  CONTROL-CARD-FILE ASSIGN TO INP02.                           
                                                                                
0308DM     SELECT  WEEKLY-DATE-FILE  ASSIGN TO INP03.                           
                                                                                
           SELECT  REPORT-FILE       ASSIGN TO RPT01.                           
                                                                                
           SELECT  AP-TRANS-FILE     ASSIGN TO OUT01.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  IN-DA-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  IN-DA-RECORD                  PIC X(350).                            
                                                                                
       FD  CONTROL-CARD-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  CONTROL-CARD-RECORD           PIC X(80).                             
                                                                                
0308DM FD  WEEKLY-DATE-FILE                                                     
0308DM     RECORDING MODE IS F                                                  
0308DM     LABEL RECORDS ARE STANDARD                                           
0308DM     BLOCK CONTAINS 0 RECORDS.                                            
0308DM                                                                          
0308DM 01  WEEKLY-DATE-RECORD            PIC X(50).                             
                                                                                
       FD  REPORT-FILE                                                          
           RECORDING MODE F                                                     
           LABEL RECORDS OMITTED                                                
           BLOCK CONTAINS  0 RECORDS.                                           
       01  REPORT-RECORD.                                                       
           05  REPORT-LINE               PIC X(150).                            
      *                                                                         
       FD  AP-TRANS-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
0701RM*01  AP-TRANS-RECORD               PIC X(450).                            
0701RM 01  AP-TRANS-RECORD               PIC X(550).                            
      *                                                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-LINE-COUNT           PIC S9(03)    COMP-3 VALUE 0.            
           05  PA-LINE-NO              PIC S9(03)    COMP-3 VALUE 0.            
           05  PA-PAGE-COUNT           PIC S9(05)    COMP-3 VALUE 0.            
           05  PA-IN-COUNT             PIC S9(09)    COMP-3 VALUE 0.            
           05  PA-TOTAL-READ           PIC S9(09)    COMP-3 VALUE 0.            
           05  PA-AP-TOTAL-WRITTEN     PIC S9(09)    COMP-3 VALUE 0.            
           05  PA-TOTAL-CREDIT-AMT     PIC S9(09)V99 COMP-3 VALUE 0.            
           05  PA-TOTAL-RTL-AMT        PIC S9(09)V99 COMP-3 VALUE 0.            
                                                                                
       01  PC-CONSTANTS.                                                        
           05  PC-ORIGIN-CDE-A         PIC  X(01) VALUE 'A'.                    
           05  PC-REC-TYPE-CDE-2       PIC  X(01) VALUE '2'.                    
           05  PC-REC-TYPE-CDE-9       PIC  X(01) VALUE '9'.                    
           05  PC-ADV-ALLOW-LIT        PIC  X(02) VALUE 'AA'.                   
           05  PC-FIX-ALLOW-LIT        PIC  X(02) VALUE 'FS'.                   
           05  PC-INTERNL-LOAD-LIT     PIC  X(02) VALUE 'IL'.                   
22405      05  PC-ASST-RECOVER-LIT     PIC  X(02) VALUE 'AR'.                   
           05  PC-MKDN-ALLOW-LIT       PIC  X(02) VALUE 'MA'.                   
           05  PC-MKDN-ALLOW-COST      PIC  X(02) VALUE 'MC'.                   
           05  PC-MRK-OUT-STCK-LIT     PIC  X(02) VALUE 'MS'.                   
P30021     05  PC-MRK-ALW-PR-ASST-RTL-LIT                                       
P30021                                 PIC  X(02) VALUE 'PR'.                   
P30021     05  PC-MRK-ALW-PR-ASST-COST-LIT                                      
P30021                                 PIC  X(02) VALUE 'PC'.                   
P30021     05  PC-AD-COOP-VNDR-FUNDING-LIT                                      
P30021                                 PIC  X(02) VALUE 'AC'.                   
           05  PC-F2                   PIC  X(02) VALUE 'F2'.                   
           05  PC-CR                   PIC  X(02) VALUE 'CR'.                   
           05  PC-MDS                  PIC  X(03) VALUE 'MDS'.                  
           05  PC-DAK001-JOB           PIC  X(06) VALUE 'DAK001'.               
           05  PC-DPKUT500-PGM         PIC  X(08) VALUE 'DPKUT500'.             
           05  PC-DAKUT301-PGM         PIC  X(08) VALUE 'DAKUT301'.             
           05  PC-DAKUT304-PGM         PIC  X(08) VALUE 'DAKUT304'.             
           05  PC-DPKUP151-PGM         PIC  X(08) VALUE 'DPKUP151'.             
           05  PC-ENTER-TIME           PIC  X(16) VALUE                         
                                                '-22.00.00.000000'.             
           05  PC-NO-ERROR-MSG         PIC  X(80) VALUE                         
                                                '* * * NO ERRORS * * *'.        
           05  PC-MAX-LINES            PIC  9(03) VALUE 60.                     
W26601*    05  PC-STR-400              PIC  9(03) VALUE 400.                    
W26601     05  PC-STR-400              PIC  9(05) VALUE 00400.                  
I30021     05  PC-TABLE-MAX            PIC  9(03) VALUE 500.                    
I30021     05  PC-TABLE-WARN-SIZE      PIC  9(03) VALUE 450.                    
0701RM     05  PC-00090                PIC  9(05) VALUE 00090.                  
           05  PC-ET-IDX-MAX           PIC  9(01) VALUE 5.                      
                                                                                
       01  PS-SWITCHES.                                                         
           05  PS-EOF-EXTRACT-SWITCH   PIC  X(01) VALUE 'N'.                    
               88  PS-EOF-EXTRACT                 VALUE 'Y'.                    
           05  PS-TTRNCDE-FOUND-SW     PIC  X(01) VALUE 'N'.                    
               88  PS-TTRNCDE-NOT-FOUND           VALUE 'Y'.                    
W30021     05  PS-REASON-FOUND-SW      PIC  X(01) VALUE 'N'.                    
W30021         88  PS-REASON-FOUND                VALUE 'Y'.                    
W30021         88  PS-REASON-NOT-FOUND            VALUE 'N'.                    
W30021     05  PS-DEBIT-TYPE-FOUND-SW  PIC  X(01) VALUE 'N'.                    
W30021         88  PS-DEBIT-TYPE-FOUND            VALUE 'Y'.                    
W30021         88  PS-DEBIT-TYPE-FOUND-NOT        VALUE 'N'.                    
W30021     05  PS-CHAR-FOUND-SW        PIC  X(01) VALUE 'N'.                    
W30021         88  PS-CHAR-FOUND                VALUE 'Y'.                      
W30021         88  PS-CHAR-NOT-FOUND            VALUE 'N'.                      
                                                                                
       01  ABEND-CODE                  PIC S9(04) VALUE ZERO                    
                                                       COMP SYNC.               
                                                                                
       01  PV-VARIABLES.                                                        
           05  PV-BTCH-SRC-CDE         PIC X(03)     VALUE SPACES.              
           05  PV-RUN-TIME             PIC X(08)     VALUE SPACES.              
           05  PV-RUN-DATE             PIC X(10)     VALUE SPACES.              
           05  PV-PARAGRAPH            PIC X(30)     VALUE SPACES.              
           05  PV-SUB                  PIC 9(03)     VALUE ZERO.                
           05  PV-BATCH-NBR.                                                    
               10  FILLER              PIC X(01)     VALUE '4'.                 
               10  PV-BATCH-NBR-DAYS   PIC X(02).                               
           05  PV-BATCH-NBR-RDF REDEFINES PV-BATCH-NBR                          
                                       PIC 9(03).                               
           05  PV-RETURN-CODE          PIC 9(04)     VALUE ZERO.                
           05  PV-PO-NBR               PIC 9(10)     VALUE ZERO.                
           05  PV-PO-NBR-RDF REDEFINES PV-PO-NBR.                               
               10  PV-PO-NBR1          PIC X(01).                               
               10  PV-PO-NBR2-10       PIC X(09).                               
           05  PV-TOTAL-CHARGES        PIC S9(09)V99 COMP-3 VALUE 0.            
           05  PV-TOTAL-CREDITS        PIC S9(09)V99 COMP-3 VALUE 0.            
           05  PV-TOTAL-CREDIT-AMT     PIC S9(09)V99 COMP-3 VALUE 0.            
           05  PV-TOTAL-RTL            PIC S9(09)V99 COMP-3 VALUE 0.            
           05  PV-NET-AMT              PIC S9(09)V99 COMP-3 VALUE 0.            
           05  PV-TOTAL-RTL-AMT        PIC S9(09)V99 COMP-3 VALUE 0.            
           05  PV-STMT-DTL-SEQ-NBR     PIC S9(05)    COMP-3 VALUE 0.            
           05  PV-DISP-NUMBER          PIC Z,ZZZ,ZZ9.                           
           05  PV-EFF-DTE-TIME.                                                 
               10  PV-EFF-DTE          PIC X(10).                               
               10  PV-EFF-TIME         PIC X(09).                               
           05  PV-PREV-ENT-ID          PIC S9(10)          VALUE 0.             
           05  PV-ENT-ID               PIC S9(10)          VALUE 0.             
           05  PV-ENT-ID-COMP          PIC S9(10)    COMP  VALUE 0.             
0301RM     05  PV-PSTG-MONTHLY-FSCL-WK-NBR.                                     
0301RM         10  FILLER              PIC 9(01)           VALUE 0.             
0301RM         10  PV-PSTG-FWK-NBR     PIC 9(01)           VALUE 0.             
W30021     05  PV-CHARS                PIC S9(05) COMP-3 VALUE +0.              
W30021     05  PV-NOTE.                                                         
W30021         10 PV-NOTE-CHAR         PIC X                                    
W30021                                 OCCURS 50 TIMES.                         
                                                                                
       01  PV-APRD045-AND-THEN-SOME.                                            
0701RM*    05  PV-APRD045-REC            PIC X(400).                            
0701RM     05  PV-APRD045-REC            PIC X(500).                            
           05  PV-APRD045-EXTRA-FIELDS.                                         
               10 PV-APRD045-BATCH-NBR   PIC X(03).                             
               10 PV-APRD045-CENPPC-NBR  PIC S9(07)    COMP-3.                  
               10 PV-APRD045-RTL-AMT     PIC S9(09)V99 COMP-3.                  
               10 PV-APRD045-NET-CST-AMT PIC S9(09)V99 COMP-3.                  
               10 PV-APRD045-EFF-DTE     PIC X(19).                             
0301RM         10 PV-APRD045-PROC-DTE    PIC X(10).                             
0301RM*        10 FILLER                 PIC X(12).                             
0301RM         10 FILLER                 PIC X(02).                             
       01  TTRANCDE-TABLE.                                                      
           05 TTRNCDE-ENTRIES OCCURS 500 TIMES                                  
                   INDEXED BY TT-IDX.                                           
               10 TT-DA-TRN-CDE        PIC X(02).                               
               10 TT-AP-TRN-CDE        PIC X(02).                               
               10 TT-PSTG-TYP-CDE      PIC X(02).                               
               10 TT-TRN-TYP-CDE       PIC X(03).                               
               10 TT-GL-ACCT-NBR       PIC X(06).                               
               10 TT-BTCH-SRC-CDE      PIC X(03).                               
                                                                                
       01  ERROR-TABLE.                                                         
           05  ERROR-ENTRIES OCCURS 5 TIMES                                     
               INDEXED BY ET-IDX.                                               
               10  ET-ERROR-MESSAGE    PIC X(79).                               
                                                                                
W30021*****************************************************************         
W30021*  TYPE TABLE IS DELIMITED BY NULLS.                            *         
W30021*****************************************************************         
W30021 01  WS-TYP-CDES.                                                         
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE 'AAADVERTISING ALLOWANCE^'.                                
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE 'ARASSET RECOVERY^'.                                       
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE 'FSFIXTURE ALLOWANCE^'.                                    
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE 'ILINTERNAL LOAD^'.                                        
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE 'MAMARKDOWN ALLOWANCE^'.                                   
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE 'MCMKDN ALW COST ONLY^'.                                   
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE 'MSMARK OUT OF STOCK^'.                                    
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE 'PRMKDN ALW PROFIT ASST - CST/RTL^'.                       
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE 'PCMKDN ALW PROFIT ASST - COST^'.                          
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE 'ACAD COOP FROM VENDOR FUNDING^'.                          
W30021                                                                          
W30021 01  FILLER                       REDEFINES WS-TYP-CDES.                  
W30021     05  FILLER                   OCCURS 10 TIMES                         
W30021                                  INDEXED BY XTYP, HOLD-XTYP.             
W30021         10  WS-TYP-CDE           PIC XX.                                 
W30021         10  WS-TYP-DESC          PIC X(35).                              
W30021                                                                          
W30021*****************************************************************         
W30021*  REASON TABLE IS DELIMITED BY NULLS.                          *         
W30021*****************************************************************         
W30021 01  WS-REASONS.                                                          
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE '00NO REASON SPECIFIED^'.                                  
W30021     05  FILLER                   PIC X(37)                               
W30021*        VALUE '05PROFITABILITY ALLOWANCE^'.                              
S30021         VALUE '05MARGIN SUPPORT^'.                                       
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE '10EARLY/LATE SHIP ALLOWANCE^'.                            
W30021     05  FILLER                   PIC X(37)                               
W30021*        VALUE '15REBATE ALLOWANCE^'.                                     
S30021         VALUE '15VOLUME REBATE^'.                                        
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE '20DAMAGE ALLOWANCE^'.                                     
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE '25OFF PRICE ALLOWANCE^'.                                  
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE '30SHORT SHIP DISCOUNT ALLOWANCE^'.                        
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE '35NEW STORE ALLOWANCE^'.                                  
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE '40NEW ITEM ALLOWANCE^'.                                   
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE '45DISPLAY ALLOWANCE^'.                                    
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE '50MARKET DEVELOPMENT FUNDS^'.                             
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE '55IN STORE MERCHANDISING ALLOW^'.                         
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE '60SHIPPED CLEARANCE MERCHANDISE^'.                        
W30021     05  FILLER                   PIC X(37)                               
W30021*        VALUE '65PROMOTIONAL ALLOWANCE^'.                                
S30021         VALUE '65ADVERTISING ALLOWANCE^'.                                
W30021     05  FILLER                   PIC X(37)                               
W30021         VALUE '99ALL REASON CODES^'.                                     
W30021 01  FILLER                       REDEFINES WS-REASONS.                   
W30021     05  FILLER                   OCCURS 15 TIMES                         
W30021                                  INDEXED BY XRSN, HOLD-XRSN.             
W30021         10  WS-RSN-CDE           PIC XX.                                 
W30021         10  WS-RSN-DESC          PIC X(35).                              
      *****************************************************************         
      *  WORK AREA TO BE PASSED TO/FROM SUBROUTINE DAKUT304           *         
      *****************************************************************         
           COPY DAWS304.                                                        
                                                                                
      *****************************************************************         
      *  WORK AREA TO BE PASSED TO/FROM SUBROUTINE DPKUP151           *         
      *****************************************************************         
           COPY APWS151.                                                        
                                                                                
      *****************************************************************         
      *  WORK AREA PASSED TO/FROM DPKUT500                            *         
      *****************************************************************         
           COPY DPWS500.                                                        
                                                                                
      *****************************************************************         
      *  KOHL'S STANDARD FIRST HEADING LINE                                     
      *****************************************************************         
           COPY DPWS150O.                                                       
      *                                                                         
       01  H2-HEADING2.                                                         
           05  FILLER                PIC X(62)     VALUE SPACES.                
           05  FILLER                PIC X(33)     VALUE                        
           'DEBIT ALLOWANCE ERROR REPORT'.                                      
           05  FILLER                PIC X(28)     VALUE SPACES.                
           05  FILLER                PIC X(05)     VALUE 'JOB: '.               
           05  H2-JOB-NAME           PIC X(06)     VALUE SPACES.                
           05  FILLER                PIC X(16)     VALUE SPACES.                
                                                                                
       01  H3-HEADING3.                                                         
           05  FILLER                PIC X(05)     VALUE SPACES.                
           05  FILLER                PIC X(04)     VALUE 'DEPT'.                
           05  FILLER                PIC X(05)     VALUE SPACES.                
           05  FILLER                PIC X(05)     VALUE 'CLASS'.               
           05  FILLER                PIC X(05)     VALUE SPACES.                
           05  FILLER                PIC X(05)     VALUE 'STORE'.               
           05  FILLER                PIC X(05)     VALUE SPACES.                
           05  FILLER                PIC X(09)     VALUE 'ENT ID'.              
           05  FILLER                PIC X(05)     VALUE SPACES.                
           05  FILLER                PIC X(16)     VALUE 'D/A NBR'.             
           05  FILLER                PIC X(05)     VALUE SPACES.                
           05  FILLER                PIC X(05)     VALUE 'ERROR'.               
           05  FILLER                PIC X(76)     VALUE SPACES.                
                                                                                
       01  DL1-DETAIL-LINE-1.                                                   
           05  FILLER                PIC X(05)     VALUE SPACE.                 
           05  DL1-DEPT-NBR          PIC X(03)     VALUE SPACES.                
           05  FILLER                PIC X(07)     VALUE SPACE.                 
           05  DL1-SUB-CL-NBR        PIC X(02)     VALUE SPACES.                
W26601*    05  FILLER                PIC X(08)     VALUE SPACE.                 
W26601     05  FILLER                PIC X(06)     VALUE SPACE.                 
W26601*    05  DL1-STR-NBR           PIC X(03)     VALUE SPACES.                
W26601     05  DL1-STR-NBR           PIC X(05)     VALUE SPACES.                
           05  FILLER                PIC X(04)     VALUE SPACE.                 
           05  DL1-ENT-ID            PIC X(09)     VALUE SPACES.                
           05  FILLER                PIC X(07)     VALUE SPACES.                
           05  DL1-DT-ALW-NBR        PIC X(16)     VALUE SPACES.                
           05  FILLER                PIC X(05)     VALUE SPACES.                
           05  DL1-ERROR-MSG         PIC X(81)     VALUE SPACES.                
                                                                                
      *****************************************************************         
      *  INPUT/OUTPUT DEBIT ALLOWANCE LAYOUT RECORD                   *         
      *****************************************************************         
           COPY DARD100.                                                        
                                                                                
      ****************************************************************          
      * RECORD LAYOUT FOR CONTROL CARD FILE.                                    
      ****************************************************************          
       01  CC-CONTROL-CARD.                                                     
           05  CC-CTL-DTE                   PIC X(10).                          
           05  CC-CTL-FILLER                PIC X(70).                          
                                                                                
0308DM*****************************************************************         
0308DM*  WEEKLY OPEN FISCAL WEEK/MONTH DATES RECORD LAYOUT            *         
0308DM*****************************************************************         
0308DM     COPY DARD104.                                                        
                                                                                
      *****************************************************************         
      *  ADJUSTMENT JOURNAL TRANSACTION RECORD LAYOUT                 *         
      *****************************************************************         
           COPY APRD045.                                                        
                                                                                
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-DAKUT301.                                                           
                                                                                
           PERFORM 1000-INITIALIZE                                              
                                                                                
           PERFORM 2000-PROCESS-EXTRACT                                         
               UNTIL PS-EOF-EXTRACT.                                            
                                                                                
           PERFORM 9000-EOJ.                                                    
                                                                                
           GOBACK.                                                              
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  IN-DA-FILE                                               
                       CONTROL-CARD-FILE                                        
0308DM                 WEEKLY-DATE-FILE.                                        
           OPEN OUTPUT REPORT-FILE                                              
                OUTPUT AP-TRANS-FILE.                                           
                                                                                
           PERFORM 8000-READ-CONTROL-CARD-FILE.                                 
           PERFORM 8010-READ-WEEKLY-DATE-FILE.                                  
           PERFORM 8020-READ-EXTRACT.                                           
                                                                                
           DISPLAY '**------ BEGIN DAKUT301 ------**'.                          
                                                                                
           MOVE FUNCTION CURRENT-DATE (3:2)  TO DP150O-RUN-YEAR.                
           MOVE FUNCTION CURRENT-DATE (5:2)  TO DP150O-RUN-MONTH.               
           MOVE FUNCTION CURRENT-DATE (7:2)  TO DP150O-RUN-DAY.                 
           MOVE FUNCTION CURRENT-DATE (9:2)  TO DP150O-RUN-HOUR.                
           MOVE FUNCTION CURRENT-DATE (11:2) TO DP150O-RUN-MINUTE.              
           MOVE PC-DAKUT301-PGM              TO DP150O-PROGRAM-NAME.            
           MOVE 1                            TO DP150O-REPORT-NUMBER.           
                                                                                
           IF PS-EOF-EXTRACT                                                    
               DISPLAY 'NO DA INPUT -- OUT01 WILL BE EMPTY'                     
           ELSE                                                                 
               PERFORM 4000-WRITE-REPORT-HEADING                                
           END-IF.                                                              
                                                                                
       2000-PROCESS-EXTRACT.                                                    
                                                                                
      *****************************************************************         
      ***====>>> CALL THE SUBROUTINE TO DO THE DB2 STUFF                        
      ***====>>> CALLING SUBROUTINE BECAUSE EVENTUALLY THIS PROGRAM             
      ***====>>> WILL BE ACCESSING ORACLE DATABASES                             
      *****************************************************************         
           IF DA100-ENT-ID = PV-PREV-ENT-ID                                     
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 7000-CALL-DAKUT304                                       
               MOVE DA100-ENT-ID TO PV-PREV-ENT-ID                              
           END-IF.                                                              
                                                                                
           IF DA304-GOOD-CALL                                                   
               PERFORM 2050-BUILD-COMMON-FIELDS                                 
               PERFORM 2200-BUILD-DETAIL-FIELDS                                 
           ELSE                                                                 
               DISPLAY 'BAD RETURN FROM DAKUT304 ' DA304-PARAMATERS             
               MOVE 4095 TO PV-RETURN-CODE                                      
               PERFORM 5000-PREPARE-ERROR-REPORT                                
           END-IF.                                                              
                                                                                
           PERFORM 8020-READ-EXTRACT.                                           
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH CONTAINS FIELDS THAT ARE COMMON TO DEBITS &              
      *     CREDITS. IT IS PERFORMED FOR BOTH.                                  
      ******************************************************************        
       2050-BUILD-COMMON-FIELDS.                                                
                                                                                
W30021     MOVE SPACES                      TO AP045-TRANSACTION-FILE           
           INITIALIZE AP045-KEY-FIELDS                                          
W30021*               AP045-HEADER-FIELDS                                       
                      AP045-COMMON-FIELDS                                       
                      AP045-DETAIL-FIELDS                                       
                      PV-APRD045-EXTRA-FIELDS.                                  
                                                                                
           MOVE DA304-DUN-NBR               TO AP045-VND-NBR.                   
                                                                                
                                                                                
           MOVE DA100-EFF-DTE               TO PV-EFF-DTE-TIME.                 
0301DM*    MOVE PV-EFF-DTE                  TO AP045-DOC-DTE                    
0301DM*                                        AP045-ENTR-DTE                   
0301DM*                                        AP045-INV-CUTOFF-DTE.            
0301DM*                                                                         
0301DM     MOVE CC-CTL-DTE                  TO AP045-DOC-DTE                    
0301DM                                         AP045-ENTR-DTE.                  
0301DM     MOVE DA104-OPN-FSCL-WK-DTE       TO AP045-INV-CUTOFF-DTE.            
                                                                                
           MOVE PC-REC-TYPE-CDE-2           TO AP045-RECD-TYP-CDE.              
           MOVE PC-ENTER-TIME               TO AP045-ENTR-TME.                  
           MOVE PC-DAKUT301-PGM             TO AP045-ENTR-ID.                   
           MOVE PC-ORIGIN-CDE-A             TO AP045-ORGN-CDE.                  
           MOVE DA100-STR-NBR               TO AP045-STR-NBR.                   
23724      MOVE ZEROES                      TO AP045-SKU-NBR.                   
                                                                                
      ******************************************************************        
      * CREATE THE DETAIL TRANSACTION RECORD FOR CREDITS (REVERSALS)            
      ******************************************************************        
       2200-BUILD-DETAIL-FIELDS.                                                
                                                                                
           MOVE DA100-DR-ALW-NBR             TO AP045-DOC-NBR.                  
           MOVE DA100-PO-NBR                 TO PV-PO-NBR.                      
           MOVE PV-PO-NBR2-10                TO AP045-PO-NBR.                   
                                                                                
           MOVE 'N' TO PS-TTRNCDE-FOUND-SW.                                     
           SET TT-IDX TO 1.                                                     
           SEARCH TTRNCDE-ENTRIES                                               
               AT END                                                           
                   SET PS-TTRNCDE-NOT-FOUND TO TRUE                             
               WHEN TT-DA-TRN-CDE (TT-IDX) = DA100-TYP-CDE                      
                   MOVE TT-AP-TRN-CDE (TT-IDX)   TO AP045-AP-TRN-CDE            
                   MOVE TT-PSTG-TYP-CDE (TT-IDX) TO AP045-PSTG-TYP-CDE          
                   MOVE TT-TRN-TYP-CDE (TT-IDX)  TO AP045-TRN-TYP-CDE           
                   MOVE TT-GL-ACCT-NBR (TT-IDX)  TO AP045-GL-ACCT-NBR           
                   MOVE TT-BTCH-SRC-CDE (TT-IDX) TO PV-BTCH-SRC-CDE             
           END-SEARCH.                                                          
                                                                                
           IF PS-TTRNCDE-NOT-FOUND                                              
               DISPLAY '* * * * NO MATCHING TRANCODE * * * * *'                 
               DISPLAY 'INPUT TYPE CODE ' DA100-TYP-CDE                         
               DISPLAY 'INPUT RECORD ' DARD100-RECORD                           
           END-IF.                                                              
                                                                                
           MOVE DA100-DEPT-NBR              TO AP045-DEPT-NBR.                  
                                                                                
0308DM*    MOVE PV-EFF-DTE                  TO AP045-ENTR-DTE                   
0308DM*                                        AP045-INV-CUTOFF-DTE.            
0301DM     MOVE CC-CTL-DTE                  TO AP045-DOC-DTE                    
0301DM                                         AP045-ENTR-DTE.                  
                                                                                
           STRING AP045-ENTR-DTE                                                
                  PV-BTCH-SRC-CDE                                               
                  '00'                                                          
                   DELIMITED BY SIZE                                            
                                            INTO AP045-PS-BTCH-NBR.             
                                                                                
           MOVE DA100-ENT-ID                TO AP045-ENT-ID.                    
           MOVE DA304-VENDOR-NAME           TO AP045-ENT-NME-DESC.              
W30021     IF DA100-TYP-CDE-AR                                                  
W30021         MOVE DA100-NOTE-LINE1        TO AP045-RSN-1ST-LNE-TXT            
W30021     ELSE                                                                 
W30021         PERFORM 3000-NON-AR-DEB-TYPES                                    
W30021     END-IF.                                                              
           MOVE DA100-NOTE-LINE2            TO AP045-RSN-2ND-LNE-TXT.           
           MOVE DA304-DTATTR-FSCL-YR-NBR    TO AP045-PSTG-FYR-NBR.              
           MOVE DA304-MLCNTL-MX-FSCL-MN-NBR TO AP045-PSTG-FMN-NBR.              
RB0201*    MOVE DA304-MLCNTL-MX-FSCL-WK-NBR TO AP045-PSTG-FWK-NBR.              
0301RM*    MOVE DA304-DTATTR-WK-IN-MN-NBR   TO AP045-PSTG-FWK-NBR.              
0301RM     MOVE DA304-DTATTR-WK-IN-MN-NBR   TO PV-PSTG-FWK-NBR.                 
0301RM     MOVE PV-PSTG-MONTHLY-FSCL-WK-NBR TO AP045-PSTG-FWK-NBR.              
                                                                                
      *-- DETAIL FIELDS --*                                                     
           MOVE DA100-SUB-CL-NBR            TO AP045-CL-NBR.                    
                                                                                
P30021     IF AP045-AP-TRN-CDE = '24' OR '17' OR '2F'                           
               MOVE DA100-RTL-AMT           TO AP045-RETL-AMT                   
           END-IF.                                                              
                                                                                
           MOVE DA100-NET-CST-AMT           TO AP045-GRO-COST-AMT               
                                               AP045-NET-COST-AMT.              
                                                                                
           IF AP045-PSTG-TYP-CDE = 'CR'                                         
               COMPUTE AP045-GRO-COST-AMT = AP045-GRO-COST-AMT * -1             
               COMPUTE AP045-NET-COST-AMT  = AP045-NET-COST-AMT * -1            
               COMPUTE AP045-RETL-AMT      = AP045-RETL-AMT * -1                
           END-IF.                                                              
                                                                                
           ADD AP045-RETL-AMT               TO PA-TOTAL-RTL-AMT.                
           ADD AP045-NET-COST-AMT           TO PA-TOTAL-CREDIT-AMT.             
                                                                                
      *-- CREATE PAYMENT DUE DATE                                               
           INITIALIZE DPG51                                                     
                      DPG52                                                     
                      DPG53                                                     
                      DPG54                                                     
                      DPG55                                                     
                      DPG56.                                                    
                                                                                
           SET DPG51-INCREMENT-DATE   TO TRUE.                                  
                                                                                
           MOVE PV-EFF-DTE            TO DPG52-LK-DATE-INPUT.                   
           SET DPG51-INCREMENT-DATE   TO TRUE.                                  
           MOVE DA100-CLLCTN-DAYS-NBR TO DPG51-INCR-DECR-DAYS-9.                
                                                                                
           PERFORM 7100-CALL-DATE-ROUTINE.                                      
                                                                                
           MOVE DPG55-DB2-ISO-DATE-FMT TO AP045-PYMT-DUE-DTE.                   
                                                                                
           PERFORM 8100-WRITE-AP-TRANS-REC.                                     
                                                                                
      *-- ON INTERNAL LOAD TRANSACTIONS (IL), TWO RECORDS ARE TO BE             
      *-- GENERATED A CHARGE (19) AND A CREDIT (69)                             
           IF AP045-AP-TRN-CDE = '19'                                           
               PERFORM 2400-CREATE-CREDIT-REC                                   
22405      ELSE                                                                 
22405 *--      FOR AN ASSET RECOVERY (AR), TWO RECORDS ARE TO BE                
22405 *--      GENERATED A CHARGE (17) AND A CREDIT (87)                        
22405          IF AP045-AP-TRN-CDE = '17'                                       
22405              PERFORM 2500-CREATE-CREDIT-REC                               
P30021         ELSE                                                             
P30021*--          FOR OFF INVOICE DEBIT TYPES, TWO RECORDS ARE TO BE           
P30021*--          GENERATED: A CHARGE AND A CREDIT (7D)                        
P30021             IF AP045-AP-TRN-CDE = '2F' OR '2G' OR '7C'                   
P30021                 PERFORM 2600-CREATE-OFF-INV-CREDIT-REC                   
P30021             END-IF                                                       
P30021         END-IF                                                           
           END-IF.                                                              
                                                                                
      *************************************************************             
      *-- THIS MODULE CREATES THE OTHER HALF OF THE INTERNAL LOAD *             
      *-- RECORD GENERATED PREVIOUSLY                             *             
      *************************************************************             
       2400-CREATE-CREDIT-REC.                                                  
                                                                                
           MOVE SPACES TO AP045-DEPT-NBR                                        
                          AP045-CL-NBR.                                         
23724      MOVE ZEROES TO AP045-SKU-NBR.                                        
                                                                                
           PERFORM 7200-GET-NEXT-DOC-NO.                                        
           MOVE WS151-NEXT-DOC-NBR           TO AP045-DOC-NBR                   
                                                                                
           COMPUTE AP045-RETL-AMT      = AP045-RETL-AMT * -1.                   
           COMPUTE AP045-GRO-COST-AMT  = AP045-GRO-COST-AMT * -1.               
           COMPUTE AP045-NET-COST-AMT  = AP045-NET-COST-AMT * -1.               
           ADD AP045-RETL-AMT               TO PA-TOTAL-RTL-AMT.                
           ADD AP045-NET-COST-AMT           TO PA-TOTAL-CREDIT-AMT.             
                                                                                
           SET TT-IDX TO 1.                                                     
           SEARCH TTRNCDE-ENTRIES                                               
               AT END                                                           
                   SET PS-TTRNCDE-NOT-FOUND TO TRUE                             
               WHEN TT-AP-TRN-CDE (TT-IDX) = '69'                               
0413DM*          MOVE 00090                    TO AP045-STR-NBR                 
0701RM           MOVE PC-00090                 TO AP045-STR-NBR                 
                 MOVE TT-AP-TRN-CDE (TT-IDX)   TO AP045-AP-TRN-CDE              
                 MOVE TT-PSTG-TYP-CDE (TT-IDX) TO AP045-PSTG-TYP-CDE            
                 MOVE TT-TRN-TYP-CDE (TT-IDX)  TO AP045-TRN-TYP-CDE             
                 MOVE TT-GL-ACCT-NBR (TT-IDX)  TO AP045-GL-ACCT-NBR             
                 MOVE TT-BTCH-SRC-CDE (TT-IDX) TO PV-BTCH-SRC-CDE               
           END-SEARCH.                                                          
                                                                                
           STRING AP045-ENTR-DTE                                                
                  PV-BTCH-SRC-CDE                                               
                  '00'                                                          
                   DELIMITED BY SIZE                                            
                                            INTO AP045-PS-BTCH-NBR.             
                                                                                
           PERFORM 8100-WRITE-AP-TRANS-REC.                                     
                                                                                
22405 *************************************************************             
  |   *-- THIS MODULE CREATES THE OTHER HALF OF THE ASST RECOVERY *             
  |   *-- RECORD GENERATED PREVIOUSLY                             *             
  |   *************************************************************             
  |    2500-CREATE-CREDIT-REC.                                                  
  |                                                                             
  |        MOVE SPACES TO AP045-DEPT-NBR                                        
  |                       AP045-CL-NBR.                                         
  |        MOVE ZEROES TO AP045-SKU-NBR.                                        
  |                                                                             
  |        PERFORM 7200-GET-NEXT-DOC-NO.                                        
  |        MOVE WS151-NEXT-DOC-NBR           TO AP045-DOC-NBR                   
  |                                                                             
  |        MOVE ZEROS                        TO AP045-RETL-AMT.                 
  |        COMPUTE AP045-GRO-COST-AMT  = AP045-GRO-COST-AMT * -1.               
  |        COMPUTE AP045-NET-COST-AMT  = AP045-NET-COST-AMT * -1.               
  |        ADD AP045-RETL-AMT               TO PA-TOTAL-RTL-AMT.                
  |        ADD AP045-NET-COST-AMT           TO PA-TOTAL-CREDIT-AMT.             
  |                                                                             
  |        SET TT-IDX TO 1.                                                     
  |        SEARCH TTRNCDE-ENTRIES                                               
  |            AT END                                                           
  |                SET PS-TTRNCDE-NOT-FOUND TO TRUE                             
  |            WHEN TT-AP-TRN-CDE (TT-IDX) = '87'                               
  |              MOVE PC-00090                 TO AP045-STR-NBR                 
  |              MOVE TT-AP-TRN-CDE (TT-IDX)   TO AP045-AP-TRN-CDE              
  |              MOVE TT-PSTG-TYP-CDE (TT-IDX) TO AP045-PSTG-TYP-CDE            
  |              MOVE TT-TRN-TYP-CDE (TT-IDX)  TO AP045-TRN-TYP-CDE             
  |              MOVE TT-GL-ACCT-NBR (TT-IDX)  TO AP045-GL-ACCT-NBR             
  |              MOVE TT-BTCH-SRC-CDE (TT-IDX) TO PV-BTCH-SRC-CDE               
  |        END-SEARCH.                                                          
  |                                                                             
  |        STRING AP045-ENTR-DTE                                                
  |               PV-BTCH-SRC-CDE                                               
  |               '00'                                                          
  |                DELIMITED BY SIZE                                            
  |                                         INTO AP045-PS-BTCH-NBR.             
  |                                                                             
  |        PERFORM 8100-WRITE-AP-TRANS-REC.                                     
22405                                                                           
P30021*************************************************************             
  |   *-- THIS MODULE CREATES THE OTHER HALF OF THE OFF INVOICE   *             
  |   *-- DEBIT TYPE RECORD GENERATED PREVIOUSLY                  *             
  |   *************************************************************             
  |    2600-CREATE-OFF-INV-CREDIT-REC.                                          
  |                                                                             
265366*    COMMENTED OUT THE FOLLOWING ON 01/08/2022.                           
265366*    MOVE SPACES TO AP045-DEPT-NBR                                        
265366*                   AP045-CL-NBR.                                         
  |        MOVE ZEROES TO AP045-SKU-NBR.                                        
  |                                                                             
  |        PERFORM 7200-GET-NEXT-DOC-NO.                                        
  |        MOVE WS151-NEXT-DOC-NBR          TO AP045-DOC-NBR                    
  |                                                                             
  |        IF AP045-AP-TRN-CDE = '2F'                                           
  |            COMPUTE AP045-RETL-AMT  = AP045-RETL-AMT * -1                    
  |        ELSE                                                                 
  |            MOVE ZEROS                   TO AP045-RETL-AMT                   
  |        END-IF.                                                              
  |        COMPUTE AP045-GRO-COST-AMT  = AP045-GRO-COST-AMT * -1.               
  |        COMPUTE AP045-NET-COST-AMT  = AP045-NET-COST-AMT * -1.               
  |        ADD AP045-RETL-AMT               TO PA-TOTAL-RTL-AMT.                
  |        ADD AP045-NET-COST-AMT           TO PA-TOTAL-CREDIT-AMT.             
  |                                                                             
  |        SET TT-IDX TO 1.                                                     
  |        SEARCH TTRNCDE-ENTRIES                                               
  |            AT END                                                           
  |                SET PS-TTRNCDE-NOT-FOUND TO TRUE                             
  |            WHEN TT-AP-TRN-CDE (TT-IDX) = '7D'                               
  |              MOVE PC-00090                 TO AP045-STR-NBR                 
  |              MOVE TT-AP-TRN-CDE (TT-IDX)   TO AP045-AP-TRN-CDE              
  |              MOVE TT-PSTG-TYP-CDE (TT-IDX) TO AP045-PSTG-TYP-CDE            
  |              MOVE TT-TRN-TYP-CDE (TT-IDX)  TO AP045-TRN-TYP-CDE             
  |              MOVE TT-GL-ACCT-NBR (TT-IDX)  TO AP045-GL-ACCT-NBR             
  |              MOVE TT-BTCH-SRC-CDE (TT-IDX) TO PV-BTCH-SRC-CDE               
  |        END-SEARCH.                                                          
  |                                                                             
  |        STRING AP045-ENTR-DTE                                                
  |               PV-BTCH-SRC-CDE                                               
  |               '00'                                                          
  |                DELIMITED BY SIZE                                            
  |                                         INTO AP045-PS-BTCH-NBR.             
  |                                                                             
P30021     PERFORM 8100-WRITE-AP-TRANS-REC.                                     
                                                                                
W30021******************************************************************        
W30021*  FOR DEBIT TYPES OTHER THAN 'AR'                                        
W30021******************************************************************        
W30021 3000-NON-AR-DEB-TYPES.                                                   
W30021     MOVE DA100-NOTE-LINE1  TO PV-NOTE.                                   
W30021     MOVE 50                TO PV-CHARS.                                  
W30021     SET PS-CHAR-NOT-FOUND  TO TRUE.                                      
W30021     IF NOT DA100-TYP-CDE-AR                                              
W30021       PERFORM 3005-FIND-END-OF-STRING VARYING PV-CHARS                   
W30021          FROM 50 BY -1                                                   
W30021         UNTIL                                                            
W30021               PV-CHARS = 0                                               
W30021            OR PS-CHAR-FOUND.                                             
W30021                                                                          
W30021     IF PS-CHAR-FOUND                                                     
W30021         IF PV-CHARS <= 10                                                
W30021             MOVE DA100-NOTE-LINE1 TO AP045-AUTH-NBR                      
W30021             MOVE 50             TO PV-CHARS                              
W30021         END-IF.                                                          
W30021                                                                          
W30021     SET PS-DEBIT-TYPE-FOUND-NOT TO TRUE                                  
W30021     PERFORM 3200-MATCH-TYPE-CDE                                          
W30021       VARYING XTYP FROM 1 BY 1                                           
W30021         UNTIL XTYP > 10                                                  
W30021            OR PS-DEBIT-TYPE-FOUND                                        
W30021                                                                          
W30021     IF PS-DEBIT-TYPE-FOUND                                               
W30021         SET PS-REASON-NOT-FOUND TO TRUE                                  
W30021         PERFORM 3300-MATCH-RSN-CDE                                       
W30021           VARYING XRSN FROM 1 BY 1                                       
W30021             UNTIL XRSN > 15                                              
W30021                OR PS-REASON-FOUND                                        
W30021     ELSE                                                                 
W30021         DISPLAY 'UNKNOWN DEBIT TYPE, RSN-1ST-LINE NOT FORMATTED.         
W30021-                'TYPE = ' DA100-TYP-CDE                                  
W30021     END-IF.                                                              
W30021     IF PS-REASON-FOUND                                                   
W30021         STRING WS-TYP-DESC (HOLD-XTYP) DELIMITED BY '^'                  
W30021                ' - '            DELIMITED BY SIZE                        
W30021                WS-RSN-DESC (HOLD-XRSN) DELIMITED BY '^'                  
W30021                               INTO AP045-RSN-1ST-LNE-TXT                 
W30021         MOVE SPACES             TO AP045-RSN-2ND-LNE-TXT                 
W30021     END-IF.                                                              
W30021*************************************************************             
W30021*  DUMMY PARAGRAPH                                                        
W30021*************************************************************             
W30021 3005-FIND-END-OF-STRING.                                                 
W30021     IF PV-NOTE-CHAR(PV-CHARS) EQUAL SPACE OR LOW-VALUES                  
W30021         NEXT SENTENCE                                                    
W30021     ELSE                                                                 
W30021        SET PS-CHAR-FOUND      TO TRUE.                                   
W30021                                                                          
W30021*************************************************************             
W30021*  CREATE A CONCATENATION OF THE TYPE CODE AND REASON CODE                
W30021*  DESCRIPTIONS FOR VENDER NOTES.                                         
W30021*************************************************************             
W30021 3200-MATCH-TYPE-CDE.                                                     
W30021                                                                          
W30021     IF DA100-TYP-CDE = WS-TYP-CDE (XTYP)                                 
W30021         SET HOLD-XTYP    TO XTYP                                         
W30021         SET PS-DEBIT-TYPE-FOUND TO TRUE                                  
W30021     END-IF.                                                              
W30021                                                                          
W30021 3300-MATCH-RSN-CDE.                                                      
W30021                                                                          
W30021     IF DA100-RSN-CDE = WS-RSN-CDE (XRSN)                                 
W30021         SET HOLD-XRSN    TO XRSN                                         
W30021         SET PS-REASON-FOUND TO TRUE                                      
W30021     END-IF.                                                              
W30021                                                                          
       4000-WRITE-REPORT-HEADING.                                               
      ******************************************************************        
      *   4000-WRITE-REPORT-HEADING                                    *        
      *      PRINT HEADING LINES                                       *        
      ******************************************************************        
                                                                                
           MOVE 0                              TO PA-LINE-COUNT                 
           ADD 1                               TO PA-PAGE-COUNT.                
           MOVE PA-PAGE-COUNT                  TO DP150O-PAGE-NUMBER.           
           MOVE DP150O-STANDRD-HEADER-150-COLS TO REPORT-RECORD.                
           WRITE REPORT-RECORD                                                  
                   AFTER ADVANCING PAGE.                                        
                                                                                
           MOVE PC-DAK001-JOB                  TO H2-JOB-NAME.                  
           MOVE H2-HEADING2                    TO REPORT-RECORD.                
           MOVE 1                              TO PA-LINE-NO.                   
           WRITE REPORT-RECORD                                                  
                   AFTER ADVANCING 1 LINE.                                      
                                                                                
           MOVE H3-HEADING3                    TO REPORT-RECORD.                
           WRITE REPORT-RECORD                                                  
                   AFTER ADVANCING 1 LINE.                                      
                                                                                
           MOVE SPACES                         TO REPORT-RECORD.                
           WRITE REPORT-RECORD                                                  
                   AFTER ADVANCING 1 LINE.                                      
                                                                                
       5000-PREPARE-ERROR-REPORT.                                               
                                                                                
           SET ET-IDX TO 1.                                                     
           IF DA304-TAPCNTL-BAD-CALL                                            
               MOVE DA304-TAPCNTL-ERROR-MESSAGE TO                              
                    ET-ERROR-MESSAGE (ET-IDX)                                   
               SET ET-IDX UP BY 1                                               
           END-IF.                                                              
           IF DA304-TTRNCDE-BAD-CALL                                            
               MOVE DA304-TTRNCDE-ERROR-MESSAGE TO                              
                    ET-ERROR-MESSAGE (ET-IDX)                                   
               SET ET-IDX UP BY 1                                               
           END-IF.                                                              
           IF DA304-VENDOR-BAD-CALL                                             
               MOVE DA304-VENDOR-ERROR-MESSAGE TO                               
                    ET-ERROR-MESSAGE (ET-IDX)                                   
               SET ET-IDX UP BY 1                                               
           END-IF.                                                              
           IF DA304-DTATTR-BAD-CALL                                             
               MOVE DA304-DTATTR-ERROR-MESSAGE TO                               
                    ET-ERROR-MESSAGE (ET-IDX)                                   
               SET ET-IDX UP BY 1                                               
           END-IF.                                                              
           IF DA304-MLCNTL-BAD-CALL                                             
               MOVE DA304-MLCNTL-ERROR-MESSAGE TO                               
                    ET-ERROR-MESSAGE (ET-IDX)                                   
           END-IF.                                                              
                                                                                
           MOVE DA100-DEPT-NBR   TO DL1-DEPT-NBR.                               
           MOVE DA100-SUB-CL-NBR TO DL1-SUB-CL-NBR.                             
           MOVE DA100-STR-NBR    TO DL1-STR-NBR.                                
           MOVE DA100-ENT-ID     TO DL1-ENT-ID.                                 
           MOVE DA100-DR-ALW-NBR TO DL1-DT-ALW-NBR.                             
                                                                                
           PERFORM                                                              
              VARYING ET-IDX FROM 1 BY 1                                        
                UNTIL (ET-IDX > PC-ET-IDX-MAX)                                  
                   OR (ET-ERROR-MESSAGE (ET-IDX) = SPACES OR LOW-VALUE)         
                 MOVE ET-ERROR-MESSAGE (ET-IDX) TO DL1-ERROR-MSG                
                 PERFORM 5100-WRITE-REPORT-LINE                                 
                 INITIALIZE DL1-DETAIL-LINE-1                                   
                 ADD 1                           TO PA-LINE-NO                  
           END-PERFORM.                                                         
                                                                                
       5100-WRITE-REPORT-LINE.                                                  
                                                                                
           IF PA-LINE-NO > PC-MAX-LINES                                         
               PERFORM 4000-WRITE-REPORT-HEADING                                
           END-IF.                                                              
                                                                                
           WRITE REPORT-RECORD FROM DL1-DETAIL-LINE-1                           
                  AFTER ADVANCING 1 LINES.                                      
                                                                                
       7000-CALL-DAKUT304.                                                      
           IF PA-TOTAL-READ = 1                                                 
               SET DA304-INIT-SELECT-FOR-DAKUT301 TO TRUE                       
           ELSE                                                                 
               SET DA304-REG-SELECT-FOR-DAKUT301  TO TRUE                       
           END-IF.                                                              
                                                                                
           MOVE DA100-ENT-ID       TO PV-ENT-ID.                                
           MOVE PV-ENT-ID          TO PV-ENT-ID-COMP.                           
           MOVE PV-ENT-ID-COMP     TO DA304-ENT-ID.                             
           CALL PC-DAKUT304-PGM    USING DA304-PARAMATERS.                      
                                                                                
           IF DA304-INIT-SELECT-FOR-DAKUT301                                    
               IF DA304-TTRNCDE-GOOD-CALL                                       
                   MOVE 1 TO PV-SUB                                             
                   PERFORM 7010-PROCESS-DA304-TABLE                             
                        UNTIL PV-SUB > PC-TABLE-MAX                             
                           OR (DA304-AP-TRN-CDE (PV-SUB) = SPACES OR            
                                                           LOW-VALUES)          
               END-IF                                                           
           END-IF.                                                              
                                                                                
       7010-PROCESS-DA304-TABLE.                                                
           MOVE DA304-AP-TRN-CDE (PV-SUB)   TO TT-AP-TRN-CDE (PV-SUB).          
           MOVE DA304-PSTG-TYP-CDE (PV-SUB) TO TT-PSTG-TYP-CDE (PV-SUB).        
           MOVE DA304-TRN-TYP-CDE (PV-SUB)  TO TT-TRN-TYP-CDE (PV-SUB).         
           MOVE DA304-GL-ACCT-NBR (PV-SUB)  TO TT-GL-ACCT-NBR (PV-SUB).         
           MOVE DA304-BTCH-SRC-CDE (PV-SUB) TO TT-BTCH-SRC-CDE (PV-SUB).        
                                                                                
           EVALUATE TT-AP-TRN-CDE (PV-SUB)                                      
22405        WHEN '17'                                                          
22405            MOVE PC-ASST-RECOVER-LIT TO TT-DA-TRN-CDE (PV-SUB)             
             WHEN '18'                                                          
                 MOVE PC-MRK-OUT-STCK-LIT TO TT-DA-TRN-CDE (PV-SUB)             
             WHEN '19'                                                          
                 MOVE PC-INTERNL-LOAD-LIT TO TT-DA-TRN-CDE (PV-SUB)             
             WHEN '24'                                                          
                 MOVE PC-MKDN-ALLOW-LIT   TO TT-DA-TRN-CDE (PV-SUB)             
             WHEN '25'                                                          
                 MOVE PC-ADV-ALLOW-LIT    TO TT-DA-TRN-CDE (PV-SUB)             
             WHEN '26'                                                          
                 MOVE PC-MKDN-ALLOW-COST  TO TT-DA-TRN-CDE (PV-SUB)             
             WHEN '27'                                                          
                 MOVE PC-FIX-ALLOW-LIT    TO TT-DA-TRN-CDE (PV-SUB)             
P30021       WHEN '2F'                                                          
P30021           MOVE PC-MRK-ALW-PR-ASST-RTL-LIT                                
P30021                                    TO TT-DA-TRN-CDE (PV-SUB)             
P30021       WHEN '2G'                                                          
P30021           MOVE PC-MRK-ALW-PR-ASST-COST-LIT                               
P30021                                    TO TT-DA-TRN-CDE (PV-SUB)             
             WHEN '69'                                                          
                 MOVE PC-INTERNL-LOAD-LIT TO TT-DA-TRN-CDE (PV-SUB)             
P30021       WHEN '7C'                                                          
P30021           MOVE PC-AD-COOP-VNDR-FUNDING-LIT                               
P30021                                    TO TT-DA-TRN-CDE (PV-SUB)             
P30021       WHEN '7D'                                                          
P30021           MOVE SPACES              TO TT-DA-TRN-CDE (PV-SUB)             
22405        WHEN '87'                                                          
22405            MOVE PC-ASST-RECOVER-LIT TO TT-DA-TRN-CDE (PV-SUB)             
           END-EVALUATE.                                                        
                                                                                
           ADD 1 TO PV-SUB.                                                     
                                                                                
       7100-CALL-DATE-ROUTINE.                                                  
                                                                                
           MOVE PC-F2                 TO DPG51-CALENDAR-TYPE                    
           SET  DPG52-LK-DTE-ISO-FMT  TO TRUE.                                  
                                                                                
           CALL PC-DPKUT500-PGM                                                 
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
                                                                                
           IF (DPG54-DATE-INVALID OR                                            
               DPG54-SEVERE-ERROR)                                              
               DISPLAY 'DPKUT500 ERROR = ' DPG54-ERROR-MESSAGE                  
               DISPLAY '** PROGRAM ABEND **'                                    
               DISPLAY 'DAKUT301  - INCREMENT DATE: '                           
                   DA100-CLLCTN-DAYS-NBR                                        
               MOVE +4003              TO ABEND-CODE                            
               PERFORM 9999-ERROR                                               
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * THIS PARAGRAPH WILL CALL A MODULE TO OBTAIN THE NEXT AVAILABLE *        
      * DOCUMENT NUMBER.                                               *        
      *----------------------------------------------------------------*        
       7200-GET-NEXT-DOC-NO.                                                    
                                                                                
           CALL PC-DPKUP151-PGM    USING WS151-DOC-NBR-PARAMETERS.              
                                                                                
           IF WS151-ERRORS-FOUND                                                
               DISPLAY 'DPKUP151 ERROR = ' DPG54-ERROR-MESSAGE                  
               DISPLAY '** PROGRAM ABEND **'                                    
               DISPLAY 'DAKUT301  - NEXT AVAILABLE DOC NUMBER ERROR'            
               MOVE +4003              TO ABEND-CODE                            
               PERFORM 9999-ERROR                                               
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *  READ CONTROL CARD FILE                                                 
      *----------------------------------------------------------------*        
       8000-READ-CONTROL-CARD-FILE.                                             
                                                                                
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD.                         
                                                                                
      *----------------------------------------------------------------*        
      *  READ WEEKLY DATE FILE                                                  
      *----------------------------------------------------------------*        
       8010-READ-WEEKLY-DATE-FILE.                                              
                                                                                
           READ WEEKLY-DATE-FILE INTO DARD104-RECORD.                           
                                                                                
      *----------------------------------------------------------------*        
      *  READ DA EXTRACT FILE                                                   
      *----------------------------------------------------------------*        
       8020-READ-EXTRACT.                                                       
           READ IN-DA-FILE INTO DARD100-RECORD                                  
           AT END                                                               
               SET PS-EOF-EXTRACT           TO TRUE                             
           NOT AT END                                                           
               ADD 1                        TO PA-TOTAL-READ                    
           END-READ.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *  WRITE THE A/P TRANACTION                                               
      *----------------------------------------------------------------*        
       8100-WRITE-AP-TRANS-REC.                                                 
                                                                                
      *-- ADDING FIELDS TO END OF RECORD TO BE USED TO CREATE                   
      *-- M/L PRICE CHANGE FILE AND REPORTS                                     
           MOVE DA100-CENPPC-NBR       TO PV-APRD045-CENPPC-NBR                 
           MOVE DA100-RTL-AMT          TO PV-APRD045-RTL-AMT.                   
           MOVE DA100-NET-CST-AMT      TO PV-APRD045-NET-CST-AMT.               
           MOVE DA100-EFF-DTE          TO PV-APRD045-EFF-DTE.                   
                                                                                
0301RM*- CTL CARD DTE REFLECTS SYSTEM PROCESS DTE.                              
0301RM     MOVE CC-CTL-DTE             TO PV-APRD045-PROC-DTE.                  
                                                                                
      *-- CREATE BATCH NUMBER                                                   
           MOVE CC-CTL-DTE (9:2)       TO PV-BATCH-NBR-DAYS.                    
           MOVE PV-BATCH-NBR           TO PV-APRD045-BATCH-NBR.                 
                                                                                
265366* COMMENTED OUT MOVING SPACES TO DEPT-NBR AND CL-NBR FOR '7C'             
265366* AP045-AP-TRN-CDE BELOW ON 01/08/2022.                                   
265366*    IF AP045-AP-TRN-CDE = '7C'                                           
265366*         MOVE SPACES TO AP045-DEPT-NBR                                   
265366*                        AP045-CL-NBR                                     
265366*    END-IF.                                                              
           MOVE AP045-TRANSACTION-FILE TO PV-APRD045-REC.                       
           WRITE AP-TRANS-RECORD                                                
               FROM PV-APRD045-AND-THEN-SOME.                                   
           ADD 1                  TO PA-AP-TOTAL-WRITTEN.                       
                                                                                
      *************************************************************             
      *  9000-EOJ IS PERFORMED FROM 0000-DAKUT301.  IT PRODUCES   *             
      *  A 'NO ERROR MESSAGE' ON THE REPORT IF NONE WERE FOUND    *             
      *  IF ERRORS WERE FOUND, THE RETURN CODE IS SET TO 4095     *             
      *  WHICH IS USED TO ALERT THE ML TEAM VIA AN EMAIL THAT     *             
      *  ERRORS OCCURED.  RECORD COUNTS ARE ALSO DISPLAYED.       *             
      *************************************************************             
       9000-EOJ.                                                                
                                                                                
      *-- SEND A RETURN CODE OF 4095 WHEN ANY ERRORS OCCUR ON THE               
      *-- INPUT FILE (THAT RECORD IS NOT WRITTEN TO THE OUTPUT FILE             
      *-- THUS THE INPUT "DA" RECORD COUNT WILL NOT BALANCE WITH                
      *-- THE OUTPUT "DA" RECORD COUNT.                                         
                                                                                
           IF PA-TOTAL-READ > 0                                                 
               PERFORM 9010-CREATE-CONTROL-RECORD                               
           END-IF.                                                              
                                                                                
           DISPLAY 'PV-SUB ' PV-SUB.                                            
           IF PV-RETURN-CODE = 0                                                
               WRITE REPORT-RECORD FROM PC-NO-ERROR-MSG                         
                    AFTER ADVANCING 5 LINES                                     
               IF PV-SUB > PC-TABLE-WARN-SIZE                                   
                  MOVE 4094           TO RETURN-CODE                            
               END-IF                                                           
           ELSE                                                                 
               IF PV-SUB > PC-TABLE-WARN-SIZE                                   
                  MOVE 4091           TO RETURN-CODE                            
               ELSE                                                             
                  MOVE PV-RETURN-CODE TO RETURN-CODE                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
           CLOSE IN-DA-FILE                                                     
                 REPORT-FILE                                                    
                 CONTROL-CARD-FILE                                              
0308DM           WEEKLY-DATE-FILE                                               
                 AP-TRANS-FILE.                                                 
                                                                                
           DISPLAY SPACES.                                                      
           MOVE PA-TOTAL-READ        TO PV-DISP-NUMBER.                         
           DISPLAY 'RECS READ    =    ' PV-DISP-NUMBER.                         
           MOVE PA-AP-TOTAL-WRITTEN  TO PV-DISP-NUMBER.                         
           DISPLAY 'AP RECS WRITTEN = ' PV-DISP-NUMBER.                         
           DISPLAY '**------ END DAKUT301 ------**'.                            
                                                                                
       9010-CREATE-CONTROL-RECORD.                                              
                                                                                
W30021     MOVE SPACES                      TO AP045-TRANSACTION-FILE           
           INITIALIZE AP045-KEY-FIELDS                                          
W30021*               AP045-HEADER-FIELDS                                       
                      AP045-COMMON-FIELDS                                       
                      AP045-CONTROL-FIELDS                                      
                      PV-APRD045-EXTRA-FIELDS.                                  
                                                                                
           MOVE PC-REC-TYPE-CDE-9           TO AP045-RECD-TYP-CDE.              
           MOVE PA-AP-TOTAL-WRITTEN         TO AP045-CTL-RECORD-COUNT.          
           MOVE PA-TOTAL-RTL-AMT            TO AP045-CTL-RETL-AMT.              
           MOVE PA-TOTAL-CREDIT-AMT         TO AP045-CTL-GRO-COST-AMT           
                                               AP045-CTL-NET-COST-AMT.          
                                                                                
           PERFORM 8100-WRITE-AP-TRANS-REC.                                     
                                                                                
       9999-ERROR.                                                              
      ******************************************************************        
      *   9999-ERROR.                                                  *        
      *      ERROR ABEND ROUTINE                                       *        
      ******************************************************************        
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
