000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400*                                                                         
000500*                QUICK CREDIT/ACCOUNT LOOKUP PROGRAM.                     
000600*                                                                         
000700******************************************************************        
000800 PROGRAM-ID.             EPKUT013.                                        
000900 AUTHOR.                 CHRIS CLARK  .                                   
001000 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
001100 DATE-WRITTEN.           07-26-2001.                                      
001200 DATE-COMPILED.                                                           
001300******************************************************************        
001400*  CREDIT FORMATTING PROGRAM FOR QC/ALU                                   
001500*                                                                         
001600*  THIS MQ PROGRAM WILL UTILIZE MQ, READ FROM A QUEUE THAT HAS            
001700*  BEEN POPULATED DURING THE BUSINESS DAY, EVALUATE THE MESSAGE           
001800*  TYPE, AND WRITE EITHER THE QUICK CREDIT OFFLINE APPLICATION            
001900*  FILE OR THE ADDRESS CHANGE FILE.  PROCESSING IS COMPLETE WHEN          
002000*  THERE ARE NO MORE MESSAGES LEFT TO RETREIVE.                           
002100******************************************************************        
001800* CHANGE LOG:   WR26840                                                   
001900* DATE:  06/03/04                                                         
002000* PRGMR: CHRIS CLARK                                                      
002100* WHY:   CHANGED TO USE 4 DIGIT STORE INSTEAD OF 3 DIGIT STORE            
002200*        NUMBER.                                                          
002600******************************************************************        
N2I206* DATE: 01/17/2008                                                        
N2I206* WHO: DAN PAYNTER (REFER TO 'N2I206' FOR ALL CHANGES)                    
N2I206* WHY: CHANGES FOR NEW QC/ACLU FIELDS                                     
N2I206******************************************************************        
N2I301* DATE: 06/05/2008                                               *        
N2I301* WHO: DAN PAYNTER (REFER TO 'N2I301' FOR ALL CHANGES)           *        
N2I301* WHY: REPLACE PRODUCTION STORE SSN'S WITH KNOWN TEST-ONLY       *        
N2I301*      SSN'S.                                                    *        
N2I301******************************************************************        
SP0909******************************************************************        
SP0909* DATE: 09/18/2008                                               *        
SP0909* WHO: LYNN MCADAMS (REFER TO 'SP0909' FOR ALL CHANGES)          *        
SP0909* WHY: CHANGE LENGTH OF CREDIT RECORD 'INITIALS' FIELD.          *        
SP0909*                                                                *        
SP0909*      THE FOLLOWING FIELDS WILL BE OBSOLETE WHEN THE            *        
SP0909*      ROLLOUT TO THE SPRING 2009 RELEASE HAS BEEN               *        
SP0909*      COMPLETED.  THEY CANNOT BE REMOVED UNTIL THE              *        
SP0909*      CREDIT TEAM HAS REMOVED THEM FROM THEIR LAYOUTS:          *        
SP0909*         CO-APPLICANT HOUSE ADDRESS                             *        
SP0909*         CO-APPLICANT ZIP CODE                                  *        
SP0909*         CO-APPLICANT ISO COUNTRY CODE                          *        
SP0909*         CO-APPLICANT QUICK CREDIT ADDRESS 1                    *        
SP0909*         CO-APPLICANT QUICK CREDIT ADDRESS 2                    *        
SP0909*         CO-APPLICANT QUICK CREDIT CITY                         *        
SP0909*         CO-APPLICANT QUICK CREDIT STATE                        *        
SP0909*         CO-APPLICANT QUICK CREDIT ZIP CODE                     *        
SP0909*                                                                *        
SP0909******************************************************************        
FB1001* DATE: 01/04/2010                                                        
FB1001* WHO: BRYAN BUKOWSKI                                                     
FB1001* WHY: REMOVE OLD CO-APPLICANT CODE AND ADD QUICK CREDIT                  
FB1001*      INCOME CAPTURE PROCESSING.                                         
FB1001******************************************************************        
FL1002* DATE: 07/26/2010                                                        
FL1002* WHO: BRYAN BUKOWSKI/KAREN GIVENS                                        
FL1002*                           (REFER TO 'FL1002' FOR ALL CHANGES)           
FL1002* WHY: NEW FIELDS FOR QUICK CREDIT OFFLINE PROCESS.                       
FL1002******************************************************************        
KFGFIX* DATE: 05/01/2012                                                        
KFGFIX* WHO: KAREN GIVENS (REFER TO 'KFGFIX' FOR ALL CHANGES)                   
KFGFIX* WHY: NEW FIELD FOR QUICK CREDIT OFFLINE PROCESS.                        
JNS000******************************************************************        
JNS000* DATE  03/02/2018                                                        
JNS000* CHANGE MADE:  FIX TO HANDLED EXPIRY DATE IF IT IS IN THE WRONG          
JNS000* FORMAT. IF THIS IS THE CASE, MOVE CURRENT DATE TO THE EXPIRY            
JNS000* DATE FIELD. CHANGES TAGGED WITH JNS000.                                 
JNS000* MADE BY: SRINATH(COGNIZANT)      WR/IR #: CHG0191827                    
KFGFIX******************************************************************        
PCZOOT* DATE  03/02/2019                                                        
PCZOOT* CHANGE MADE:  PC ZOOT FILE LAYOUT CHANGES                               
PCZOOT* MADE BY: COGNIZANT               WR/IR #: CHG0219083                    
PCZOOT******************************************************************        
002700                                                                          
002700 ENVIRONMENT DIVISION.                                                    
002700                                                                          
002900 CONFIGURATION SECTION.                                                   
002700                                                                          
003000 SOURCE-COMPUTER.        IBM-3090.                                        
003100 OBJECT-COMPUTER.        IBM-3090.                                        
002700                                                                          
003200 INPUT-OUTPUT SECTION.                                                    
003300 FILE-CONTROL.                                                            
002700                                                                          
003400     SELECT CONTROL-CARD-1                                                
003500            ASSIGN TO INP01.                                              
003600     SELECT CONTROL-CARD-2                                                
003700            ASSIGN TO INP02.                                              
003800     SELECT QUICK-CREDIT-FILE                                             
003900            ASSIGN TO OUT01.                                              
004000     SELECT ADDRESS-CHNG-FILE                                             
004100            ASSIGN TO OUT02.                                              
004200                                                                          
004300******************************************************************        
004400 DATA DIVISION.                                                           
004500******************************************************************        
004600 FILE SECTION.                                                            
004200                                                                          
004700 FD  CONTROL-CARD-1                                                       
004800     RECORDING MODE F                                                     
004900     BLOCK CONTAINS 0 RECORDS                                             
005000     LABEL RECORDS ARE OMITTED.                                           
005100 01  CONTROL-CARD-1-LINE              PIC  X(80).                         
005200                                                                          
005300 FD  CONTROL-CARD-2                                                       
005400     RECORDING MODE F                                                     
005500     BLOCK CONTAINS 0 RECORDS                                             
005600     LABEL RECORDS ARE OMITTED.                                           
005700 01  CONTROL-CARD-2-LINE              PIC  X(80).                         
005800                                                                          
005900 FD  QUICK-CREDIT-FILE                                                    
006000     RECORDING MODE F                                                     
006100     BLOCK CONTAINS 0 RECORDS                                             
006200     LABEL RECORDS ARE OMITTED.                                           
N2I206 01  QUICK-CREDIT-RECORD              PIC  X(3500).                       
006400                                                                          
006500 FD  ADDRESS-CHNG-FILE                                                    
006600     RECORDING MODE F                                                     
006700     BLOCK CONTAINS 0 RECORDS                                             
006800     LABEL RECORDS ARE OMITTED.                                           
N2I206 01  ADDRESS-CHNG-RECORD              PIC  X(192).                        
007000                                                                          
007100 WORKING-STORAGE SECTION.                                                 
007200 01  MQM-OBJECT-DESCRIPTOR.                                               
007300     COPY CMQODV.                                                         
007400 01  MQM-MESSAGE-DESCRIPTOR.                                              
007500     COPY CMQMDV.                                                         
007600 01  MQM-GET-MESSAGE-OPTIONS.                                             
007700     COPY CMQGMOV.                                                        
007800                                                                          
007900 01  MQM-CONSTANTS.                                                       
008000     COPY CMQV.                                                           
008100                                                                          
N2I206******************************************************************        
N2I206*  QUICK CREDIT/ACCOUNT LOOKUP APPLICATION FILE COPYBOOKS                 
N2I206******************************************************************        
N2I206     COPY EPWS020.                                                        
N2I206     COPY EPWS022.                                                        
N2I206                                                                          
N2I206******************************************************************        
N2I206*  QUICK CREDIT/ACCOUNT LOOKUP ADDRESS CHANGE FILE COPYBOOKS              
N2I206******************************************************************        
N2I206     COPY EPWS021.                                                        
N2I206     COPY EPWS024.                                                        
N2I206                                                                          
FL1002******************************************************************        
FL1002* DATE CALCULATION ROUTINE RECORD LAYOUT                                  
FL1002******************************************************************        
FL1002     COPY DPWS500.                                                        
FL1002                                                                          
008200 01  PS-PROGRAM-SWITCHES-GLOBAL.                                          
008300     05  PS-CONTROL-CARD-1-SW      PIC X(01)  VALUE 'N'.                  
008400         88  PS-CONTROL-CARD-1-END            VALUE 'Y'.                  
008500         88  PS-CONTROL-CARD-1-START          VALUE 'N'.                  
008600     05  PS-CONTROL-CARD-2-SW      PIC X(01)  VALUE 'N'.                  
008700         88  PS-CONTROL-CARD-2-END            VALUE 'Y'.                  
008800         88  PS-CONTROL-CARD-2-START          VALUE 'N'.                  
008900     05  PS-MORE-MESSAGES          PIC X(01)  VALUE 'Y'.                  
009000         88  PS-MORE-MSG                      VALUE 'Y'.                  
009100         88  PS-NO-MORE-MSG                   VALUE 'N'.                  
INC921     05  PS-ISSUE-DATE-ZERO-SW     PIC X(8)   VALUE SPACES.               
INC921         88  PS-ISSUE-DATE-ZERO-YES           VALUE '00000000'            
INC921                                                    ' 0000000'            
INC921                                                    '  000000'            
INC921                                                    '   00000'            
INC921                                                    '    0000'            
INC921                                                    '     000'            
INC921                                                    '      00'            
INC921                                                    '       0'.           
PCZOOT     05  PS-EXPIRE-DATE-ZERO-SW    PIC X(8)   VALUE SPACES.               
PCZOOT         88  PS-EXPIRE-DATE-ZERO-YES          VALUE '00000000'            
PCZOOT                                                    ' 0000000'            
PCZOOT                                                    '  000000'            
PCZOOT                                                    '   00000'            
PCZOOT                                                    '    0000'            
PCZOOT                                                    '     000'            
PCZOOT                                                    '      00'            
PCZOOT                                                    '       0'.           
PCZOOT     05  PS-DOB-DATE-ZERO-SW       PIC X(8)   VALUE SPACES.               
PCZOOT         88  PS-DOB-DATE-ZERO-YES             VALUE '00000000'            
PCZOOT                                                    ' 0000000'            
PCZOOT                                                    '  000000'            
PCZOOT                                                    '   00000'            
PCZOOT                                                    '    0000'            
PCZOOT                                                    '     000'            
PCZOOT                                                    '      00'            
PCZOOT                                                    '       0'.           
009200                                                                          
009300 01  ABEND-CODE                  PIC S9(4) COMP SYNC VALUE ZERO.          
009400     88  AC-CONTROL-CARD-ERROR                  VALUE +4003.              
009500     88  AC-MQ-ERROR                            VALUE +4020.              
009600                                                                          
009700 01  MQ-WORKING-STORAGE.                                                  
009800     05  WS-MQCONN               PIC X(8)  VALUE 'CSQBCONN'.              
009900     05  WS-MQOPEN               PIC X(8)  VALUE 'CSQBOPEN'.              
010000     05  WS-MQGET                PIC X(8)  VALUE 'CSQBGET'.               
010100     05  WS-MQCLOSE              PIC X(8)  VALUE 'CSQBCLOS'.              
010200     05  WS-MQDISC               PIC X(8)  VALUE 'CSQBDISC'.              
010300     05  WS-MQCHECK              PIC X(8)  VALUE 'MQR2C'.                 
010300     05  WS-MQCMIT               PIC X(8)  VALUE 'CSQBCOMM'.              
010300     05  WS-MQBACK               PIC X(8)  VALUE 'CSQBBACK'.              
010400                                                                          
010500 01  CC-CONTROL-CARD-1.                                                   
010600     05  CC-QMGR-NAME              PIC X(48).                             
010700     05  FILLER                    PIC X(32).                             
010800 01  CC-CONTROL-CARD-2.                                                   
010900     05  CC-QCQLU-NAME             PIC X(48).                             
011000     05  FILLER                    PIC X(32).                             
011100                                                                          
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-TOT-RECS-READ-FROM-Q      PIC S9(9) VALUE 0                   
                                                      COMP-3.                   
CHGXXX     05  PA-TOTAL-RECS-READ-FROM-Q    PIC S9(9) VALUE 0                   
                                                      COMP-3.                   
           05  PA-TOT-RECS-WRITTEN-QC       PIC S9(9) VALUE 0                   
                                                      COMP-3.                   
           05  PA-TOT-RECS-WRITTEN-QCADR    PIC S9(9) VALUE 0                   
                                                      COMP-3.                   
011200 01 PV-CONSTANTS.                                                         
011300     05  PC-PGM-NAME             PIC X(8)  VALUE 'EPKUT013'.              
011400     05  PC-FIFTY-STARS          PIC X(50)    VALUE ALL '*'.              
011500                                                                          
011600 01 PV-MQ-PROGRAM-VARIABLES.                                              
011700     05  PV-QM-NAME              PIC X(48).                               
011800     05  PV-HCONN                PIC S9(9) BINARY VALUE 0.                
011900     05  PV-SAVE-REASON          PIC S9(9) BINARY.                        
012000     05  PV-SAVE-COMP-CODE       PIC S9(9) BINARY.                        
012100     05  PV-COMPLETION-CODE      PIC S9(9) BINARY.                        
012200     05  PV-REASON               PIC S9(9) BINARY.                        
012300     05  PV-CON-REASON           PIC S9(9) BINARY.                        
012400     05  PV-PARAGRAPH            PIC X(48) VALUE SPACES.                  
012500     05  PV-ERROR-MESSAGE        PIC X(48) VALUE SPACES.                  
012600     05  PV-OPEN-OPTIONS         PIC S9(9) BINARY.                        
012700     05  PV-GQ-HANDLE            PIC S9(9) BINARY VALUE 0.                
012800     05  PV-ECOM-OPEN-IO-HNDL    PIC S9(9) BINARY VALUE 0.                
012900     05  PV-MSGBUFFER            PIC X(100000) VALUE SPACES.              
013000     05  PV-MSGLENGTH            PIC S9(9) BINARY VALUE +100000.          
013100     05  PV-DATA-LENGTH          PIC S9(9) BINARY.                        
013200     05  PV-MQ-REASON-TEXT       PIC X(30) VALUE SPACES.                  
014200                                                                          
N2I301     05  PV-STORE-NBR            PIC  X(4)   VALUE SPACES.                
FL1002     05  PV-CONTROL-DATE.                                                 
FL1002         10  PV-CNTL-CC            PIC  X(02)    VALUE '20'.              
FL1002         10  PV-CNTL-YY            PIC  X(02)    VALUE SPACES.            
FL1002         10  PV-CNTL-MM            PIC  X(02)    VALUE SPACES.            
FL1002         10  PV-CNTL-DD            PIC  X(02)    VALUE SPACES.            
FL1002     05  PV-PARAGRAPH-NAME         PIC  X(30)    VALUE SPACES.            
FL1002     05  PV-OPERATION-MSG-1        PIC  X(40)    VALUE SPACES.            
PCZOOT     05  PV-ISSUE-DATE.                                                   
PCZOOT         10  PV-ISSUE-DT-YYYY      PIC  X(04)    VALUE SPACES.            
PCZOOT         10  PV-ISSUE-FILLER1      PIC  X(01)    VALUE '-'.               
PCZOOT         10  PV-ISSUE-DT-MM        PIC  X(02)    VALUE SPACES.            
PCZOOT         10  PV-ISSUE-FILLER2      PIC  X(01)    VALUE '-'.               
PCZOOT         10  PV-ISSUE-DT-DD        PIC  X(02)    VALUE SPACES.            
PCZOOT     05  PV-EXPIRE-DATE.                                                  
PCZOOT         10  PV-EXPIRE-DT-YYYY     PIC  X(04)    VALUE SPACES.            
PCZOOT         10  PV-EXPIRE-FILLER1     PIC  X(01)    VALUE '-'.               
PCZOOT         10  PV-EXPIRE-DT-MM       PIC  X(02)    VALUE SPACES.            
PCZOOT         10  PV-EXPIRE-FILLER2     PIC  X(01)    VALUE '-'.               
PCZOOT         10  PV-EXPIRE-DT-DD       PIC  X(02)    VALUE SPACES.            
PCZOOT                                                                          
014300 01 WS-PROGRAM-VARIABLES.                                                 
014700     05  WS-ADDRCHG.                                                      
014800        10 WS-ADDRCHG-TYPE-CODE        PIC X(01).                         
014900        10 WS-ADDRCHG-MESSAGE          PIC X(159).                        
N2I206        10 WS-ADDRCHG-STORE            PIC 9(04).                         
N2I206        10 WS-ADDRCHG-RGST-ID          PIC 9(04).                         
N2I206        10 WS-ADDRCHG-TRN-NBR          PIC 9(04).                         
N2I206                                                                          
015000     05  WS-TEMP-MESSAGE.                                                 
015100        10 WS-TEMP-TYPE-CODE           PIC X(01).                         
FB1001        10 WS-TEMP-MESSAGE-DATA        PIC X(171).                        
FB1001                                                                          
015300******************************************************************        
015400 PROCEDURE DIVISION.                                                      
015500******************************************************************        
015600 0000-MAINLINE.                                                           
015600                                                                          
015700     PERFORM 1000-OPEN-FILES.                                             
015800     PERFORM 1500-MQSETUP.                                                
015900     PERFORM 8000-CLOSE-FILES.                                            
016000     GOBACK.                                                              
016000                                                                          
016100 EJECT                                                                    
016200***********************************************************               
016300* 1000-OPEN-FILES.                                        *               
016400*     - OPENS FILES FOR INPUT AND OUTPUT                  *               
016500***********************************************************               
016600 1000-OPEN-FILES.                                                         
016700                                                                          
016800     OPEN OUTPUT QUICK-CREDIT-FILE.                                       
016900     OPEN OUTPUT ADDRESS-CHNG-FILE.                                       
017000                                                                          
017100                                                                          
017200*1000-EXIT.                                                               
017300 1500-MQSETUP.                                                            
017400* ------------------------------------------------------------- *         
017500*  READ CONTROL CARD TO GET QUEUE MANAGER NAME                  *         
017600* ------------------------------------------------------------- *         
017700      SET PS-CONTROL-CARD-1-START TO TRUE.                                
020300      INITIALIZE CC-CONTROL-CARD-1.                                       
017800      OPEN INPUT CONTROL-CARD-1.                                          
017900      READ CONTROL-CARD-1 INTO CC-CONTROL-CARD-1                          
018000          AT END                                                          
018100              SET PS-CONTROL-CARD-1-END                                   
018200                                  TO TRUE.                                
018300                                                                          
018400      IF PS-CONTROL-CARD-1-END                                            
018500          DISPLAY PC-PGM-NAME                                             
018600              'CONTROL CARD MISSING - FOR QMGR NAME'                      
018700          SET AC-CONTROL-CARD-ERROR TO TRUE                               
018800          CALL 'ILBOABN0' USING ABEND-CODE                                
018900      ELSE                                                                
019000          DISPLAY PC-PGM-NAME                                             
019100                  '- '                                                    
019200                  PC-FIFTY-STARS                                          
019300          DISPLAY PC-PGM-NAME                                             
019400                  '- CONTROL CARD: '                                      
019500                  CC-CONTROL-CARD-1                                       
019600      END-IF.                                                             
019700                                                                          
019800      CLOSE CONTROL-CARD-1.                                               
019900                                                                          
020000* ------------------------------------------------------------- *         
020100*  READ CONTROL CARD TO GET QUICK CREDIT/ACCT LOOKUP QUEUE NAME *         
020200* ------------------------------------------------------------- *         
020300      SET PS-CONTROL-CARD-2-START TO TRUE.                                
020300      INITIALIZE CC-CONTROL-CARD-2.                                       
020400      OPEN INPUT CONTROL-CARD-2.                                          
020500      READ CONTROL-CARD-2 INTO CC-CONTROL-CARD-2                          
020600          AT END                                                          
020700              SET PS-CONTROL-CARD-2-END                                   
020800                                  TO TRUE.                                
020900                                                                          
021000      IF PS-CONTROL-CARD-2-END                                            
021100          DISPLAY PC-PGM-NAME                                             
021200              'CONTROL CARD MISSING - FOR QCALU NAME'                     
021300          SET AC-CONTROL-CARD-ERROR TO TRUE                               
021400          CALL 'ILBOABN0' USING ABEND-CODE                                
021500      ELSE                                                                
021600          DISPLAY PC-PGM-NAME                                             
021700                  '- '                                                    
021800                  PC-FIFTY-STARS                                          
021900          DISPLAY PC-PGM-NAME                                             
022000                  '- CONTROL CARD: '                                      
022100                  CC-CONTROL-CARD-2                                       
022200      END-IF.                                                             
022300                                                                          
022400      CLOSE CONTROL-CARD-2.                                               
022500                                                                          
022600* ------------------------------------------------------------- *         
022700*    CONNECT TO THE QUEUE MANAGER                                         
022800* ------------------------------------------------------------- *         
022900* SUPPLY QM-NAME, GET BACK HCONN, COMPLETION CODE AND REASON              
023000                                                                          
023100     MOVE CC-QMGR-NAME TO PV-QM-NAME.                                     
023200     CALL WS-MQCONN USING PV-QM-NAME                                      
023300                          PV-HCONN                                        
023400                          PV-COMPLETION-CODE                              
023500                          PV-REASON.                                      
023600                                                                          
023700*    IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE                      
023800*    AND EXIT                                                             
023900                                                                          
024000     MOVE PV-REASON TO PV-CON-REASON.                                     
024100     IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
024200        MOVE 'MQCONN ERROR'   TO PV-ERROR-MESSAGE                         
024300        MOVE '1500-MQ-SETUP' TO PV-PARAGRAPH                              
024400        MOVE PV-REASON            TO PV-SAVE-REASON                       
024500        MOVE PV-COMPLETION-CODE   TO PV-SAVE-COMP-CODE                    
024600        PERFORM 9200-DISPLAY-MQ-ERROR                                     
024700     END-IF.                                                              
024800     DISPLAY 'MQCONN SUCCESSFUL TO ', PV-QM-NAME.                         
024900                                                                          
025000* ------------------------------------------------------------- *         
025100*    OPEN THE QUICK CREDIT/ACCT LOOKUP QUEUE FOR INPUT                    
025200* ------------------------------------------------------------- *         
025300                                                                          
025400     COMPUTE PV-OPEN-OPTIONS = MQOO-INPUT-SHARED +                        
025500                               MQOO-OUTPUT +                              
025600                               MQOO-FAIL-IF-QUIESCING +                   
025700                               MQOO-SAVE-ALL-CONTEXT.                     
025800     MOVE CC-QCQLU-NAME            TO MQOD-OBJECTNAME.                    
025900     MOVE PV-QM-NAME               TO MQOD-OBJECTQMGRNAME.                
026000                                                                          
026100     CALL WS-MQOPEN USING PV-HCONN                                        
026200                          MQM-OBJECT-DESCRIPTOR                           
026300                          PV-OPEN-OPTIONS                                 
026400                          PV-GQ-HANDLE                                    
026500                          PV-COMPLETION-CODE                              
026600                          PV-REASON.                                      
026700                                                                          
026800*    IF OPEN FAILED THEN DISPLAY ERROR MESSAGE                            
026900*    AND EXIT.                                                            
027000*                                                                         
027100     IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
027200        MOVE 'MQOPEN ERROR'       TO PV-ERROR-MESSAGE                     
027300        MOVE 'ERROR ON OPEN OF ECOM Q' TO PV-PARAGRAPH                    
027400        MOVE PV-REASON            TO PV-SAVE-REASON                       
027500        MOVE PV-COMPLETION-CODE   TO PV-SAVE-COMP-CODE                    
027600        PERFORM 9400-DISCONNECT-QMGR                                      
027700        PERFORM 9200-DISPLAY-MQ-ERROR                                     
027800     END-IF.                                                              
027900     MOVE PV-GQ-HANDLE TO PV-ECOM-OPEN-IO-HNDL.                           
028000     DISPLAY 'MQOPEN OF UNPACKED-INPUT DC QUEUE SUCCESSFUL'.              
028100                                                                          
028200     PERFORM 9500-GET-MQ-MESSAGE                                          
                   UNTIL PS-NO-MORE-MSG.                                        
      ***TEST ONLY                                                              
      ***          UNTIL PA-TOT-RECS-READ-FROM-Q = 100.                         
                                                                                
028400*1500-EXIT.                                                               
028500****************************************************************          
028600* 8000-CLOSE-FILES.                                                       
028700*    THIS ROUTINE CLOSES THE INPUT/OUTPUT FILES.               *          
028800****************************************************************          
028900 8000-CLOSE-FILES.                                                        
029000                                                                          
029100     CLOSE QUICK-CREDIT-FILE.                                             
029200     CLOSE ADDRESS-CHNG-FILE.                                             
                                                                                
           DISPLAY '*********************************************'.             
           DISPLAY '    EPKUT013 SUMMARY STATISTICS              '              
           DISPLAY '*********************************************'.             
CHGXXX     DISPLAY 'RECS READ FROM Q...: ' PA-TOTAL-RECS-READ-FROM-Q            
           DISPLAY 'QUICK CR RECS WRITTEN: ' PA-TOT-RECS-WRITTEN-QC             
           DISPLAY 'ADD CHG RECS WRITTEN: '                                     
                    PA-TOT-RECS-WRITTEN-QCADR                                   
           DISPLAY '*********************************************'.             
                                                                                
      ************************************************************              
      *       E N D    O F    P R O G R A M                      *              
      ************************************************************              
029300                                                                          
029400* -------------------------------------------------------------           
029500* DISPLAY THE VALUES OF A FAILED MQ API COMMAND, AND ABEND PGM            
029600* -------------------------------------------------------------           
029700 9200-DISPLAY-MQ-ERROR.                                                   
029800                                                                          
029900     CALL WS-MQCHECK USING PV-SAVE-REASON                                 
030000                           PV-MQ-REASON-TEXT.                             
030100                                                                          
030200     DISPLAY '**** ABEND *************************************'.          
030300     DISPLAY '** MQSERIES ERROR **'.                                      
030400     DISPLAY '** PROGRAM:    ', PC-PGM-NAME.                              
030500     DISPLAY '** PARAGRAPH:  ', PV-PARAGRAPH.                             
030600     DISPLAY '** MESSAGE:    ', PV-ERROR-MESSAGE.                         
030700     DISPLAY '** COMP CODE:  ', PV-SAVE-COMP-CODE.                        
030800     DISPLAY '** RSN CODE:   ', PV-SAVE-REASON.                           
030900     DISPLAY '** RSN TEXT:   ', PV-MQ-REASON-TEXT.                        
031000     DISPLAY '************************************************'.          
031100     SET AC-MQ-ERROR TO TRUE.                                             
031200     CALL 'ILBOABN0' USING ABEND-CODE.                                    
031300                                                                          
031400*9200-DISPLAY-MQ-ERROR.                                                   
031500                                                                          
031600*****************************************************************         
031700 9400-DISCONNECT-QMGR.                                                    
031800*****************************************************************         
031900                                                                          
032000*    DISCONNECT FROM THE QUEUE MANAGER                                    
032100                                                                          
032200     IF PV-CON-REASON IS NOT EQUAL TO MQRC-ALREADY-CONNECTED              
032300        CALL WS-MQDISC USING PV-HCONN                                     
032400                             PV-COMPLETION-CODE                           
032500                             PV-REASON                                    
032600        IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                        
032700           MOVE 'MQDISC ERROR'         TO PV-ERROR-MESSAGE                
032800           MOVE '9400-DISCONNECT-QMGR' TO PV-PARAGRAPH                    
032900           MOVE PV-REASON              TO PV-SAVE-REASON                  
033000           MOVE PV-COMPLETION-CODE     TO PV-SAVE-COMP-CODE               
033100           PERFORM 9200-DISPLAY-MQ-ERROR                                  
033200        ELSE                                                              
033300           DISPLAY 'MQDISC SUCCESSFUL'                                    
033400        END-IF                                                            
033500     END-IF.                                                              
033600                                                                          
033700*9400-EXIT.                                                               
033800 9500-GET-MQ-MESSAGE.                                                     
033900******************************************************************        
034000*  PERFORM AN MQGET TO RETRIEVE A MESSAGE FROM THE MQ QUEUE.     *        
034100*  THE OPTIONS ARE SET SO THAT IT WILL WAIT UNTIL A MESSAGE               
034200*  APPEARS ON THE QUEUE, AND THAT SYNCPOINT IS SET FOR COMMITING *        
034300*  AND ROLLING BACK MESSAGES. THE MESSAGE IS EXAMINED, IF IT IS  *        
034400*  AN END OF PGM MESSAGE, THE PROGRAM WILL END. *                         
034500******************************************************************        
034600      INITIALIZE PV-MSGBUFFER.                                            
034700      MOVE +100000               TO PV-MSGLENGTH.                         
034800      MOVE MQMI-NONE             TO MQMD-MSGID.                           
034900      MOVE MQCI-NONE             TO MQMD-CORRELID.                        
035000      MOVE CC-QCQLU-NAME         TO MQOD-OBJECTNAME.                      
035001                                                                          
035010      COMPUTE MQGMO-OPTIONS  = MQGMO-SYNCPOINT.                           
035100                                                                          
035200      CALL WS-MQGET USING PV-HCONN                                        
035300                          PV-ECOM-OPEN-IO-HNDL                            
035400                          MQM-MESSAGE-DESCRIPTOR                          
035500                          MQM-GET-MESSAGE-OPTIONS                         
035600                          PV-MSGLENGTH                                    
035700                          PV-MSGBUFFER                                    
035800                          PV-DATA-LENGTH                                  
035900                          PV-COMPLETION-CODE                              
036000                          PV-REASON.                                      
036100                                                                          
SP0909      IF (PV-QM-NAME NOT = 'QKSP')                                        
SP0909          DISPLAY ' '                                                     
SP0909          DISPLAY 'QC-MSGBUFFER ' PV-MSGBUFFER (1:1000)                   
SP0909      END-IF.                                                             
                                                                                
036200*     IF GET FAILED THEN DISPLAY ERROR MESSAGE                            
036300*     AND BREAK OUT OF LOOP                                               
036400                                                                          
036500     IF (PV-COMPLETION-CODE NOT = MQCC-OK)                                
036600        SET PS-NO-MORE-MSG  TO  TRUE                                      
036700     ELSE                                                                 
036800        EVALUATE TRUE                                                     
036900           WHEN  PV-MSGBUFFER NOT = SPACES                                
                    ADD +1 TO PA-TOT-RECS-READ-FROM-Q                           
CHGXXX              ADD +1 TO PA-TOTAL-RECS-READ-FROM-Q                         
CHGAAA              IF PA-TOT-RECS-READ-FROM-Q > 2000                           
CHGAAA                 DISPLAY 'REC COUNT: ' PA-TOT-RECS-READ-FROM-Q            
CHGAAA                 PERFORM 9800-COMMIT-MQ                                   
CHGAAA                 INITIALIZE PA-TOT-RECS-READ-FROM-Q                       
CHGAAA                 DISPLAY 'REC COUNT: ' PA-TOT-RECS-READ-FROM-Q            
CHGAAA              END-IF                                                      
037000              MOVE PV-MSGBUFFER TO WS-TEMP-MESSAGE                        
037100              IF WS-TEMP-TYPE-CODE = 'O' THEN                             
N2I206                 MOVE PV-MSGBUFFER TO APPLBAT                             
N2I206                 PERFORM 9600-POPULATE-APPLICATION                        
N2I206                 WRITE QUICK-CREDIT-RECORD                                
N2I206                       FROM QUICK-CREDIT-APPLICATION                      
                    ADD +1 TO PA-TOT-RECS-WRITTEN-QC                            
037400              END-IF                                                      
037500              IF WS-TEMP-TYPE-CODE = 'A' THEN                             
N2I206                 MOVE PV-MSGBUFFER TO ADDRESS-CHANGE-FILE                 
N2I206                 PERFORM 9700-POPULATE-ADDRESS-CHANGE                     
                    ADD +1 TO PA-TOT-RECS-WRITTEN-QCADR                         
037800              END-IF                                                      
037900        END-EVALUATE                                                      
038000     END-IF.                                                              
038100                                                                          
038200*9500-EXIT.                                                               
038300                                                                          
N2I206 9600-POPULATE-APPLICATION.                                               
N2I206******************************************************************        
N2I206*  POPULATE THE QC APPLICATION FILE USING THE FIELDS FROM THE             
N2I206*  INPUT MQ QUEUE FORMAT.                                                 
FB1001*  THE FOLLOWING CO-APPLICANT FIELDS ARE NO LONGER USED, BUT              
FB1001*  REMAIN BECAUSE CREDIT STILL HAS THOSE VALUES ON THEIR END.             
FB1001*  THEY CAN BE DELETED ONCE THE CREDIT TEAMS REMOVES THEM                 
FB1001*  FROM THEIR END.                                                        
FB1001*                                                                         
N2I206******************************************************************        
N2I206     INITIALIZE QUICK-CREDIT-APPLICATION.                                 
N2I206                                                                          
N2I206     MOVE APB-STORE                TO QCA-STORE.                          
N2I206     MOVE APB-ASSOC                TO QCA-ASSOC.                          
N2I206     MOVE APB-FNAME-1              TO QCA-FNAME-1.                        
N2I206     MOVE APB-INITIAL-1            TO QCA-INITIAL-1.                      
N2I206     MOVE APB-LNAME-1              TO QCA-LNAME-1.                        
N2I206     MOVE APB-PHONE-NO             TO QCA-PHONE-NO.                       
N2I206     MOVE APB-PRIMARY-SSN          TO QCA-PRIMARY-SSN.                    
N2I301                                                                          
N2I301     IF CC-QMGR-NAME = 'QKST' OR                                          
N2I301        CC-QMGR-NAME = 'QKSQ'                                             
N2I301         MOVE APB-STORE            TO PV-STORE-NBR                        
N2I301         EVALUATE TRUE                                                    
N2I301           WHEN PV-STORE-NBR >= '9900'                                    
N2I301                CONTINUE                                                  
N2I301           WHEN OTHER                                                     
N2I301                IF QCA-PRIMARY-SSN NOT = ' '                              
N2I301                   MOVE '111111111' TO QCA-PRIMARY-SSN                    
FL1002                   MOVE '111111111' TO QCA-DL-PASSPORT                    
N2I301                END-IF                                                    
N2I301         END-EVALUATE                                                     
N2I301     END-IF.                                                              
N2I301                                                                          
PCZOOT     MOVE APB-DOB                  TO  PS-DOB-DATE-ZERO-SW.               
PCZOOT     IF NOT PS-DOB-DATE-ZERO-YES                                          
PCZOOT        AND APB-DOB           NOT = SPACES                                
PCZOOT        MOVE APB-DOB               TO QCA-DOB                             
PCZOOT     ELSE                                                                 
PCZOOT       MOVE SPACES                 TO QCA-DOB                             
PCZOOT     END-IF.                                                              
PCZOOT                                                                          
N2I206     MOVE APB-HOUSE-NUMBER         TO QCA-HOUSE-NUMBER                    
PCZOOT     MOVE APB-ADDRESS1-ID          TO QCA-ADDRESS1-ID.                    
N2I206     MOVE APB-ZIP-5-CDE            TO QCA-POSTAL-CDE.                     
PCZOOT     MOVE APB-POSTCODE-ID          TO QCA-POSTCODE-ID.                    
N2I206                                                                          
N2I206     IF APB-ZIP-9-CDE NOT = LOW-VALUES AND                                
N2I206        APB-ZIP-9-CDE NOT = SPACES                                        
N2I206         MOVE APB-ZIP-9-CDE        TO QCA-POSTAL-CDE                      
N2I206     END-IF.                                                              
N2I206                                                                          
FL1002     MOVE APB-ISO-COUNTRY-CDE      TO QCA-ISO-COUNTRY-CDE.                
N2I206                                                                          
FB1001     MOVE APB-INCOME-TYPE          TO QCA-INCOME-TYPE.                    
PCZOOT     MOVE APB-INCOME-VALUE-DEC     TO QCA-INCOME-VALUE.                   
FL1002     MOVE APB-SSN-SIN-TIN-ID-VIEW  TO QCA-SSN-SIN-TIN-ID-VIEW.            
FL1002     MOVE APB-IDENTIFICATION-TYPE  TO QCA-IDENTIFICATION-TYPE.            
PCZOOT     MOVE APB-ID-TYP-ID            TO QCA-ID-TYP-ID.                      
FL1002     MOVE APB-DL-PASSPORT          TO QCA-DL-PASSPORT.                    
PCZOOT     MOVE APB-ID-NBR-ID            TO QCA-ID-NBR-ID.                      
FL1002     MOVE APB-STATE-PROV           TO QCA-STATE-PROV.                     
PCZOOT     MOVE APB-ISSUE-STATE-ID       TO QCA-ISSUE-STATE-ID.                 
FL1002     MOVE APB-ISO-COUNTRY-PASSPORT TO QCA-ISO-COUNTRY-PASSPORT.           
FL1002     MOVE APB-COUNTRY-TEXT-PASSPORT TO QCA-COUNTRY-TEXT-PASSPORT.         
FL1002                                                                          
FL1002*    DURING THE FALL ROLLOUT THERE WILL BE BOTH RECORDS WITH              
FL1002*     VALUES IN THE DATE FIELD AND EMPTY FIELDS.  WE NEED TO              
FL1002*     BE ABLE TO HANDLE BOTH.  IT CAN BE DELETED ONCE ROLLOUT             
FL1002*     IS COMPLETE.                                                        
INC921     MOVE APB-ID-ISSUE-DATE TO  PS-ISSUE-DATE-ZERO-SW.                    
INC921     IF NOT PS-ISSUE-DATE-ZERO-YES                                        
FL1002        AND APB-ID-ISSUE-DATE NOT = SPACES                                
FL1002       PERFORM 9650-CONVERT-ISSUE-DATE                                    
PCZOOT       MOVE DPG55-DB2-ISO-DATE-FMT   TO QCA-ID-ISSUE-DATE                 
PCZOOT     ELSE                                                                 
PCZOOT       MOVE SPACES                   TO QCA-ID-ISSUE-DATE                 
PCZOOT     END-IF.                                                              
FL1002                                                                          
INC921     MOVE APB-ID-EXPIRE-DATE TO  PS-ISSUE-DATE-ZERO-SW.                   
INC921     IF NOT PS-ISSUE-DATE-ZERO-YES                                        
FL1002        AND APB-ID-EXPIRE-DATE NOT = SPACES                               
FL1002       PERFORM 9660-CONVERT-EXPIRE-DATE                                   
FL1002*      MOVE DPG55-DB2-ISO-DATE-FMT   TO QCA-ID-EXPIRE-DATE.               
PCZOOT     ELSE                                                                 
PCZOOT       MOVE SPACES                   TO QCA-ID-EXPIRE-DATE                
PCZOOT     END-IF.                                                              
N2I206                                                                          
PCZOOT     MOVE APB-IDNT-CONSENT-IND       TO QCA-ID-SCAN-CONSENT.              
PCZOOT     MOVE APB-IDNT-PROMT-IND         TO QCA-ID-SCAN-IND.                  
PCZOOT     MOVE APB-IDNT-CONSENT-PROMT-IND TO QCA-ID-SCAN-RESP.                 
PCZOOT     MOVE APB-LOYALTY-TYPE           TO QCA-LOYALTY-TYPE.                 
PCZOOT     MOVE APB-EMAIL-ADDR             TO QCA-EMAIL-ADDR.                   
PCZOOT     MOVE APB-PAPERLESS-IND          TO QCA-PAPERLESS-IND.                
PCZOOT     MOVE APB-PAPERLESS-TEXT-MSG-IND TO QCA-PAPERLESS-TEXT-IND.           
PCZOOT     MOVE APB-PAPERLESS-MOBILE       TO QCA-PAPERLESS-MOBILE.             
PCZOOT     MOVE APB-CI-ADD-CT-NM           TO QCA-CITY.                         
PCZOOT     MOVE APB-CFC-ADD-ST-CD          TO QCA-STATE.                        
PCZOOT     MOVE APB-RGST-ID                TO QCA-REGISTER.                     
PCZOOT     MOVE APB-FNAME-ID               TO QCA-FNAME-ID.                     
PCZOOT     MOVE APB-INITIAL-ID             TO QCA-INITIAL-ID.                   
PCZOOT     MOVE APB-LNAME-ID               TO QCA-LNAME-ID.                     
PCZOOT                                                                          
PCZOOT* CHANGE THE ISSUE DATE FORMAT TO YYYY-MM-DD FROM MMDDYYYY.               
PCZOOT                                                                          
PCZOOT     MOVE APB-ISSUE-DATE-ID-SCAN     TO  PS-ISSUE-DATE-ZERO-SW.           
PCZOOT     IF NOT PS-ISSUE-DATE-ZERO-YES                                        
PCZOOT        AND APB-ISSUE-DATE-ID-SCAN NOT = SPACES                           
PCZOOT        MOVE APB-ISSUE-DATE-YYYY-ID  TO PV-ISSUE-DT-YYYY                  
PCZOOT        MOVE APB-ISSUE-DATE-MM-ID    TO PV-ISSUE-DT-MM                    
PCZOOT        MOVE APB-ISSUE-DATE-DD-ID    TO PV-ISSUE-DT-DD                    
PCZOOT        MOVE PV-ISSUE-DATE           TO QCA-ISSUE-DTE-ID                  
PCZOOT     ELSE                                                                 
PCZOOT        MOVE SPACES                  TO QCA-ISSUE-DTE-ID                  
PCZOOT     END-IF.                                                              
PCZOOT                                                                          
PCZOOT                                                                          
PCZOOT* CHANGE THE EXPIRE DATE FORMAT TO YYYY-MM-DD FROM MMDDYYYY.              
PCZOOT     MOVE APB-EXPIRE-DATE-ID-SCAN    TO  PS-EXPIRE-DATE-ZERO-SW.          
PCZOOT     IF NOT PS-EXPIRE-DATE-ZERO-YES                                       
PCZOOT        AND APB-EXPIRE-DATE-ID-SCAN NOT = SPACES                          
PCZOOT        MOVE APB-EXPIRE-DATE-YYYY-ID TO PV-EXPIRE-DT-YYYY                 
PCZOOT        MOVE APB-EXPIRE-DATE-MM-ID   TO PV-EXPIRE-DT-MM                   
PCZOOT        MOVE APB-EXPIRE-DATE-DD-ID   TO PV-EXPIRE-DT-DD                   
PCZOOT        MOVE PV-EXPIRE-DATE          TO QCA-EXPIRE-DTE-ID                 
PCZOOT     ELSE                                                                 
PCZOOT        MOVE SPACES                  TO QCA-EXPIRE-DTE-ID                 
PCZOOT     END-IF.                                                              
PCZOOT                                                                          
PCZOOT     MOVE APB-DOB-ID                 TO PS-DOB-DATE-ZERO-SW.              
PCZOOT     IF NOT PS-DOB-DATE-ZERO-YES                                          
PCZOOT        AND APB-DOB-ID        NOT = SPACES                                
PCZOOT        MOVE APB-DOB-ID              TO QCA-DOB-ID                        
PCZOOT     ELSE                                                                 
PCZOOT       MOVE SPACES                   TO QCA-DOB-ID                        
PCZOOT     END-IF.                                                              
PCZOOT                                                                          
N2I206*9600-EXIT.                                                               
FL1002******************************************************************        
FL1002*  CONVERT ISSUE DATE TO DB2 DATE FORMAT                                  
FL1002*                                                                         
FL1002******************************************************************        
FL1002 9650-CONVERT-ISSUE-DATE.                                                 
FL1002                                                                          
FL1002     INITIALIZE DPG51 DPG52 DPG53                                         
FL1002                DPG54 DPG55 DPG56.                                        
FL1002                                                                          
FL1002     SET DPG51-ACTUAL-CALENDAR-ONLY  TO TRUE                              
FL1002                                                                          
FL1002     MOVE APB-ID-ISSUE-DATE    TO DPG52-GREG-CC-DATE.                     
FL1002     SET  DPG52-LK-DTE-GREG-CC      TO TRUE.                              
FL1002                                                                          
FL1002     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
FL1002                                             DPG54 DPG55 DPG56.           
FL1002                                                                          
FL1002     IF DPG54-SEVERE-ERROR OR DPG54-DATE-INVALID                          
FL1002         DISPLAY '           '                                            
FL1002         DISPLAY '** ABEND **'                                            
FL1002         DISPLAY '   DATE = ' PV-CONTROL-DATE                             
FL1002         DISPLAY '   ERR  = ' DPG54-ERROR-MESSAGE                         
FL1002                                                                          
FL1002         MOVE '9650-CONVERT-ISSUE-DATE' TO PV-PARAGRAPH-NAME              
FL1002         MOVE 'INVALID DATE ' TO PV-OPERATION-MSG-1                       
FL1002                                                                          
FL1002         PERFORM 9900-ABEND                                               
FL1002     END-IF.                                                              
FL1002                                                                          
FL1002******************************************************************        
FL1002*  CONVERT EXPIRE DATE TO DB2 DATE FORMAT                                 
FL1002*                                                                         
FL1002******************************************************************        
FL1002 9660-CONVERT-EXPIRE-DATE.                                                
FL1002                                                                          
FL1002     INITIALIZE DPG51 DPG52 DPG53                                         
FL1002                DPG54 DPG55 DPG56.                                        
FL1002                                                                          
FL1002     SET DPG51-ACTUAL-CALENDAR-ONLY  TO TRUE                              
FL1002     SET DPG51-DO-NOT-INCR-DECR-DATE TO TRUE                              
FL1002     MOVE APB-ID-EXPIRE-DATE    TO DPG52-GREG-CC-DATE.                    
FL1002*    MOVE APB-ID-EXPIRE-DATE    TO DPG52-LK-DATE-INPUT.                   
FL1002     SET  DPG52-LK-DTE-GREG-CC      TO TRUE.                              
FL1002                                                                          
FL1002     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
FL1002                                             DPG54 DPG55 DPG56.           
FL1002                                                                          
FL1002     IF DPG54-SEVERE-ERROR OR DPG54-DATE-INVALID                          
JNS000      MOVE FUNCTION CURRENT-DATE(5:2) TO APB-ID-EXPIRE-DATE(1:2)          
JNS000      MOVE FUNCTION CURRENT-DATE(7:2) TO APB-ID-EXPIRE-DATE(3:2)          
JNS000      MOVE FUNCTION CURRENT-DATE(1:4) TO APB-ID-EXPIRE-DATE(5:4)          
FL1002         DISPLAY '           '                                            
FL1002         DISPLAY '** ABEND **'                                            
FL1002         DISPLAY '   DATE = ' PV-CONTROL-DATE                             
FL1002         DISPLAY '   ERR  = ' DPG54-ERROR-MESSAGE                         
FL1002                                                                          
FL1002         MOVE '9660-CONVERT-EXPIRE-DATE' TO PV-PARAGRAPH-NAME             
FL1002         MOVE 'INVALID DATE ' TO PV-OPERATION-MSG-1                       
JNS000     ELSE                                                                 
JNS000         MOVE DPG55-DB2-ISO-DATE-FMT   TO QCA-ID-EXPIRE-DATE              
JNS000*        PERFORM 9900-ABEND                                               
FL1002     END-IF.                                                              
FL1002                                                                          
N2I206 9700-POPULATE-ADDRESS-CHANGE.                                            
N2I206******************************************************************        
N2I206*  POPULATE THE ADDRESS CHANGE FILE AND WRITE A RECORD TO THE             
N2I206*  FILE.  IF THERE ARE CO-APPLICANT FIELDS, THEN WRITE OUT A              
N2I206*  SECOND RECORD FOR THE CO-APPLICANT.                                    
N2I206******************************************************************        
N2I206                                                                          
N2I206     INITIALIZE ADDRESS-CHANGE-REC.                                       
N2I206     MOVE ADDRESS-CHANGE-FILE      TO WS-ADDRCHG.                         
N2I206     MOVE WS-ADDRCHG-MESSAGE       TO ADDRESS-CHANGE-REC.                 
N2I206     MOVE WS-ADDRCHG-STORE         TO ACCOUNT-STORE.                      
N2I206     MOVE WS-ADDRCHG-RGST-ID       TO ACCOUNT-RGST-ID.                    
N2I206     MOVE WS-ADDRCHG-TRN-NBR       TO ACCOUNT-TRN-NBR.                    
FB1001     MOVE 'P'                      TO ACCOUNT-ROLE-CDE.                   
FB1001     WRITE ADDRESS-CHNG-RECORD FROM ADDRESS-CHANGE-REC.                   
N2I206                                                                          
N2I206                                                                          
N2I206*9700-EXIT.                                                               
CHGAAA******************************************************************        
CHGAAA*  COMMIT ONCE AFTER EVERY 2000 MESSAGES READ FROM THE QUEUE              
CHGAAA******************************************************************        
CHGAAA 9800-COMMIT-MQ.                                                          
CHGAAA     DISPLAY '9800-COMMIT-MQ PARA'                                        
CHGAAA     CALL WS-MQCMIT USING PV-HCONN                                        
CHGAAA                          PV-COMPLETION-CODE                              
CHGAAA                          PV-REASON.                                      
CHGAAA                                                                          
CHGAAA     IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                           
CHGAAA        MOVE '9800-COMMIT-MQ'   TO PV-PARAGRAPH                           
CHGAAA        MOVE 'MQCMIT ERROR'     TO PV-ERROR-MESSAGE                       
CHGAAA        MOVE PV-COMPLETION-CODE TO PV-SAVE-COMP-CODE                      
CHGAAA        MOVE PV-REASON          TO PV-SAVE-REASON                         
CHGAAA                                                                          
CHGAAA        PERFORM 9900-ABEND                                                
CHGAAA     END-IF.                                                              
303900                                                                          
304000                                                                          
FL1002******************************************************************        
FL1002*  ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                             
FL1002******************************************************************        
FL1002 9900-ABEND.                                                              
FL1002                                                                          
FL1002     DISPLAY '** ABEND           **'.                                     
FL1002     DISPLAY '** PROGRAM         ** = ' PC-PGM-NAME.                      
FL1002     DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
FL1002     DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.               
FL1002                                                                          
FL1002     CALL 'ILBOABN0' USING ABEND-CODE.                                    
FL1002                                                                          
