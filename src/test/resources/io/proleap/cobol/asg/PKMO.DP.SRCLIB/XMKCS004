      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.       XMKCS004.                                      00050006
       AUTHOR.           MARIA KLAMIK.                                  00060000
       INSTALLATION.     KOHLS.                                         00070000
       DATE-WRITTEN      JULY, 2004.                                    00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ******************************************************************00110000
      *        PROGRAM REQUEST FOR MERCHANDISING INFORMATION           *00120000
      ******************************************************************00120100
      *                                                                *00121000
      *  THIS PROGRAM WILL RETURN TSK DATA WHEN CALLED.                *00130000
      *                                                                *00131000
      *  THIS PROGRAM WILL REQUIRE THE CALLING PROGRAM TO PROVIDE      *00140000
      *  A SKU NUMBER.  FORMAT THE CALL USING XMWS002.                 *00150004
      *                                                                *00150100
      *   INPUT FILES:    SKU                                          *00151000
      *                                                                *00152000
      *   OUTPUT FILES:   SETUP-DATE                                   *00153000
      *                   ORIG-UNT-RTL-AMT                             *00154100
      *                   LOWST-UNT-RTL-AMT                            *00154200
      *                                                                *00154300
      *   I/O:   TSKXREF            DB2 TABLE   SELECT                 *00155000
      *          TSK                DB2 TABLE   SELECT                 *00158000
      *                                                                *00158100
      ******************************************************************00159100
      *  CHANGE LOG:                                                    00159200
      *                                                                 00159300
      *  07/21/04 - MARIA KLAMIK                                        00159400
      *             (WR #26354) NEW SUBROUTINE FOR MERCHANDISING        00159507
      *             INFORMATION.                                        00159600
      ******************************************************************00160000
                                                                        00170000
      ******************************************************************00180000
       ENVIRONMENT DIVISION.                                            00190000
      ******************************************************************00200000
                                                                        00210000
      ******************************************************************00220000
       DATA DIVISION.                                                   00230000
      ******************************************************************00240000
                                                                        00250000
       WORKING-STORAGE SECTION.                                         00260000
                                                                        00270000
       01  PC-PROGRAM-CONSTANTS.                                        00330000
           05  PC-PROGRAM-NAME        PIC  X(08)  VALUE 'XMKCS004'.     00340006
                                                                        00380000
       01  PV-PROGRAM-VARIABLES.                                        00390000
           05  PV-COMMAREA-LENGTH     PIC S9(04)  VALUE +0              00400000
                                                  COMP SYNC.            00410000
           05  PV-CICS-RESPONSE       PIC S9(08)  VALUE ZEROS           00420000
                                                  COMP SYNC.            00430000
           05  PV-SETUP-DATE           PIC  S9(7)V COMP-3.              00481001
           05  PV-SETUP-DATE-YMD       PIC 9(6)     VALUE ZERO.         00482001
           05  FILLER                  REDEFINES PV-SETUP-DATE-YMD.     00483001
               10  PV-SETUP-YY         PIC 9(2).                        00484001
               10  PV-SETUP-MM         PIC 9(2).                        00486001
               10  PV-SETUP-DD         PIC 9(2).                        00487001
           05  PV-SETUP-DATE-DB2.                                       00488001
               10  PV-SETUP-CC-DB2     PIC X(2).                        00489001
               10  PV-SETUP-YY-DB2     PIC X(2).                        00489101
               10  FILLER              PIC X(1)  VALUE '-'.             00489201
               10  PV-SETUP-MM-DB2     PIC X(2).                        00489301
               10  FILLER              PIC X(1)  VALUE '-'.             00489401
               10  PV-SETUP-DD-DB2     PIC X(2).                        00489501
                                                                        00490000
      *---------------------------------------------------------------  00581000
      *  DCLGEN LAYOUT AND COBOL DECLARATION FOR THE NI-SKU XREF TABLE  00582000
      *---------------------------------------------------------------  00583000
           EXEC SQL                                                     00584000
               INCLUDE TSKXREF                                          00585000
           END-EXEC.                                                    00586000
      *---------------------------------------------------------------  00590000
      *  DCLGEN LAYOUT AND COBOL DECLARATION FOR THE TSK TABLE          00600001
      *---------------------------------------------------------------  00610000
           EXEC SQL                                                     00620000
               INCLUDE TSK                                              00630001
           END-EXEC.                                                    00640000
      *----------------------------------------------------------------*00750000
      * COMMAREA USED WHEN LINKING FROM A CALLING PROGRAM               00760000
      *----------------------------------------------------------------*00770000
           COPY XMWS002.                                                00780004
      *---------------------------------------------------------------  00810000
      *  COMMUNICATIONS AREA FOR DB2                                    00820000
      *---------------------------------------------------------------  00830000
           EXEC SQL                                                     00840000
               INCLUDE SQLCA                                            00850000
           END-EXEC.                                                    00860000
                                                                        00870000
       LINKAGE SECTION.                                                 00890000
       01  DFHCOMMAREA.                                                 00900000
           05  FILLER                         OCCURS  1 TO 4072 TIMES   00910000
                                            DEPENDING ON EIBCALEN.      00920000
               10  FILLER                   PIC X.                      00930000
                                                                        00940001
      ******************************************************************00960000
       PROCEDURE DIVISION.                                              00970000
      ******************************************************************00980000
                                                                        00990000
           INITIALIZE                       XM002-ITEM-COMMAREA.        01000005
           MOVE DFHCOMMAREA             TO  XM002-ITEM-COMMAREA.        01010005
           SET XM002-SUCCESSFUL         TO TRUE.                        01020005
                                                                        01200000
           PERFORM 1000-SELECT-MERCH-INFO.                              01310001
                                                                        01311001
           MOVE XM002-ITEM-COMMAREA     TO DFHCOMMAREA.                 01390005
                                                                        01400000
           EXEC CICS                                                    01410000
                RETURN                                                  01420000
           END-EXEC.                                                    01430000
                                                                        01440001
      *---------------------------------------------------------------  01460901
      *  DB2 CALL TO RETRIEVE MERCHANDISING INFORMATION                 01461001
      *---------------------------------------------------------------  01462001
       1000-SELECT-MERCH-INFO.                                          01820101
                                                                        01820201
           INITIALIZE DCLTSKXREF                                        01820301
                      DCLTSK.                                           01820401
                                                                        01820501
           EXEC SQL                                                     01821000
               SELECT  A.ORIG_UNIT_RTL_AMT                              01821101
                      ,A.SETUP_DATE                                     01822001
                      ,A.LOWST_UNIT_RTL_AMT                             01824001
                 INTO  :TSK-ORIG-UNIT-RTL-AMT                           01825001
                      ,:TSK-SETUP-DATE                                  01826001
                      ,:TSK-LOWST-UNIT-RTL-AMT                          01827001
                 FROM  TSK A                                            01828001
                      ,TSKXREF B                                        01828101
                WHERE  A.ITEM_NMBR = B.ITM_NBR                          01829001
                  AND  B.SKU = :XM002-SKU                               01829105
           END-EXEC.                                                    01829201
                                                                        01829301
           EVALUATE TRUE                                                01830000
               WHEN SQLCODE = ZERO                                      01840000
                   SET XM002-SUCCESSFUL   TO TRUE                       01850005
                   PERFORM 2000-FORMAT-SETUP-DATE                       01851003
                   PERFORM 3000-MOVE-RETURN-FIELDS                      01852003
               WHEN SQLCODE = +100                                      01860000
                   SET XM002-NOT-SUCCESSFUL TO TRUE                     01870005
                   MOVE SQLCODE TO XM002-RETURN-SQLCODE                 01871005
               WHEN SQLWARN0 NOT = SPACES                               01880000
               WHEN SQLCODE < ZERO                                      01890000
               WHEN SQLCODE > ZERO                                      01900000
                   SET XM002-NOT-SUCCESSFUL TO TRUE                     01910005
                   MOVE SQLCODE TO XM002-RETURN-SQLCODE                 01911005
           END-EVALUATE.                                                01920000
                                                                        01930000
      *---------------------------------------------------------------  01930101
      *  FORMAT SETUP DATE TO DB2 FORMAT CCYY-MM-DD.                    01930203
      *---------------------------------------------------------------  01930301
       2000-FORMAT-SETUP-DATE.                                          01931003
                                                                        01932001
           MOVE TSK-SETUP-DATE     TO PV-SETUP-DATE-YMD.                01933501
                                                                        01933601
           IF PV-SETUP-YY > 80                                          01933701
               MOVE 19             TO PV-SETUP-CC-DB2                   01933801
           ELSE                                                         01933901
               MOVE 20             TO PV-SETUP-CC-DB2                   01934001
           END-IF.                                                      01934101
                                                                        01934201
           MOVE PV-SETUP-YY        TO PV-SETUP-YY-DB2.                  01934301
           MOVE PV-SETUP-MM        TO PV-SETUP-MM-DB2.                  01934401
           MOVE PV-SETUP-DD        TO PV-SETUP-DD-DB2.                  01934501
                                                                        01934601
           MOVE PV-SETUP-DATE-DB2 TO XM002-SETUP-DATE.                  01934705
                                                                        01934801
      *---------------------------------------------------------------  01934903
      *  MOVE THE MERCHANDISING FIELDS TO THE COMMAREA.                 01935003
      *---------------------------------------------------------------  01936003
       3000-MOVE-RETURN-FIELDS.                                         01937003
                                                                        01937103
           MOVE TSK-ORIG-UNIT-RTL-AMT  TO XM002-ORIG-UNT-RTL-AMT.       01937305
           MOVE TSK-LOWST-UNIT-RTL-AMT TO XM002-LOWST-UNT-RTL-AMT.      01937505
                                                                        01938003
