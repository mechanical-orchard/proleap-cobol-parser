       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         IMKRP012.                                            
       AUTHOR.             D. THEEL.                                            
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       JUNE 2008.                                           
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  REGULAR BRAND STYLE CREATE REPORT                                      
      *  THIS PROGRAM WILL CREATE A COMMA DELIMITED FILE CONTAINING ALL         
      *  STYLES CREATED DURING THE WEEK WITH A REGULAR BRAND.  A TITLE          
      *  AND COLUMN HEADER RECORD ARE ALSO WRITTEN. IF ANY SKUS IN THE          
      *  STYLE HAVE A DIFFERENT CORPORATE COST AND/OR RETAIL, THIS IS           
      *  INDICATED WITH A *.                                                    
      ******************************************************************        
      ******************************************************************        
      *  CHANGES:                                                               
      *                                                                         
      *  06/26/2008 D. THEEL   WR34981                                          
      *     INITIAL PROGRAM                                                     
      *                                                                         
      *  07/31/2008 D. THEEL   WR35088                                          
      *     ADDED LOGIC TO DROP STYLE IF ANY OF THE FOLLOWING ARE TRUE:         
      *     - STYLE IS NO LONGER ACTIVE                                         
      *     - STYLE CONTAINS ONE OR MORE WEB EXCLUSIVE SKUS                     
      *     - STYLE IS NO LONGER A BRAND TYPE OF REGULAR                        
      *     - STYLE HAS A MAX RETAIL LESS THAN OR EQUAL TO 5 CENTS              
      *                                                                         
W39691*  12/26/2011 COGNIZANT  WR39691                                          
W39691*     REMOVED LOGIC TO DROP STYLE IF ANY SKU IS WEB EXCLUSIVE.            
W39691*     REMOVED THE MAJOR CLASS FIELD IN EXTRACT FILE.                      
W39691*     FOLLOWING NEW FIELDS ADDED TO THE EXTRACT FILE.                     
W39691*       - DIVISION NUMBER                                                 
W39691*       - SKU                                                             
W39691*       - PERM PRICE CHANGE NUMBER                                        
W39691*       - PERM DATE                                                       
W39691*       - BRAND                                                           
W39691*       - STYLE                                                           
W39691*       - WEB EXCLUSIVE IND                                               
W39691*     PRODUCTION BUG FIX - TO REMOVE ',' INSIDE THE RETAIL AND            
W39691*     COST FIELD OF EXTRACT FILE RECORD STRUCTURE.                        
216771*                                                                         
216771*  01/28/2019 J. HUNTER  CHG0216771                                       
216771*     MAINFRAME REFACTORING - DB2 LUW HANDLES CALLS WITH                  
216771*     CHAR FORMATTING DIFFERENTLY THAN THE MAINFRAME VERSION              
216771*     OF DB2.  REPLACING THE CHAR FORMATTING WITH DIGITS.                 
216771*     SINCE CHAR RETURNS A LEADING SPACE AND TRAILING DECIMAL             
216771*     POINT AND DIGITS DOESN'T THE POSITIONAL PARMS IN THE CALL           
216771*     MUST BE SHIFTED ONE TO THE LEFT.                                    
216771*     SEE THE SELECT IN PARA D900-GET-MIN-FIRST-RCVD.                     
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT NEW-STYLE-FILE            ASSIGN TO INP01.                    
                                                                                
           SELECT REPORT-FILE               ASSIGN TO RPT01.                    
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  NEW-STYLE-FILE                                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 15 CHARACTERS                                        
           DATA RECORD IS NEW-STYLE-RECORD.                                     
       01  NEW-STYLE-RECORD            PIC  X(15).                              
                                                                                
       FD  REPORT-FILE                                                          
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
                                                                                
W39691*01  REPORT-RECORD                    PIC X(109).                         
      *CHG0202950-START                                                         
W39691 01  REPORT-RECORD                    PIC X(201).                         
      *CHG0202950-END                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE +0 COMP SYNC.          
                                                                                
      ******************************************************************        
      *    PC-PROGRAM CONSTANTS                                        *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'IMKRP012'.        
           05  PC-BUYER-NOT-FOUND           PIC X(18)  VALUE                    
                   'BUYER NOT FOUND   '.                                        
010700     05  PC-MAXIMUM-RETRY-COUNT       PIC S9(04)  VALUE +2                
010800                                                  COMP SYNC.              
                                                                                
      ******************************************************************        
      *       PROGRAM VARIABLES                                        *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME            PIC X(30)  VALUE SPACES.            
           05  PV-DB2-OPERATION-ATTEMPTED   PIC X(30)  VALUE SPACES.            
           05  PV-CURRENT-DATE              PIC X(10)  VALUE SPACES.            
           05  PV-MIN-FIRST-RCVD            PIC X(10)  VALUE SPACES.            
           05  PV-MIN-DATE.                                                     
               10  PV-MIN-CCYY              PIC X(04)  VALUE SPACES.            
               10  PV-MIN-FILLER1           PIC X(01)  VALUE SPACES.            
               10  PV-MIN-MM                PIC X(02)  VALUE SPACES.            
               10  PV-MIN-FILLER2           PIC X(01)  VALUE SPACES.            
               10  PV-MIN-DD                PIC X(02)  VALUE SPACES.            
           05  PV-DATE.                                                         
               10  PV-DATE-MM               PIC X(02)  VALUE SPACES.            
               10  PV-DATE-FILLER1          PIC X(01)  VALUE '/'.               
               10  PV-DATE-DD               PIC X(02)  VALUE SPACES.            
               10  PV-DATE-FILLER2          PIC X(01)  VALUE '/'.               
               10  PV-DATE-CCYY             PIC X(04)  VALUE SPACES.            
W39691     05  PV-WEB-COUNT                 PIC S9(07) VALUE ZERO               
W39691                                                 COMP-3.                  
007000     05  PV-RETRY-COUNT               PIC S9(04) VALUE ZERO               
007100                                                 COMP SYNC.               
           05  PV-PREV-DEPT                 PIC S9(4)V COMP-3                   
                                                       VALUE ZERO.              
           05  PV-DEPT-NBR                  PIC 9(03)  VALUE ZERO.              
W39691*    05  PV-MCL-NBR                   PIC 9(03)  VALUE ZERO.              
           05  PV-SCL-NBR                   PIC 9(03)  VALUE ZERO.              
W39691     05  PV-SKU-NBR                   PIC 9(08)  VALUE ZERO.              
24521      05  PV-DB2-NUM-COSTS             PIC S9(04) VALUE ZEROES             
24521                                       COMP.                               
24521      05  PV-DB2-NUM-RTLS              PIC S9(04) VALUE ZEROES             
24521                                       COMP.                               
W39691     05  PV-DB2-NUM-SKUS              PIC S9(04) VALUE ZEROES             
W39691                                      COMP.                               
W39691     05  PV-DB2-NUM-WEB-SKUS          PIC S9(04) VALUE ZEROES             
W39691                                      COMP.                               
      *CHG0202950-START                                                         
           05  PV-PRD-GP-NBR                PIC 9(10)  VALUE ZEROES.            
      *CHG0202950-END                                                           
       01  SA-REC-IN.                                                           
           05  SA-RMS-STYL-ID               PIC S9(8)V COMP-3.                  
           05  SA-STYLE-CREATE-DATE         PIC X(10)  VALUE SPACES.            
                                                                                
       01  SA-REC-OUT.                                                          
W39691     05  SA-DIVISN-NBR                PIC X(02)  VALUE SPACES.            
W39691     05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-BUYER-NBR                 PIC X(03)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-BUYER-LAST-NAME           PIC X(18)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-DEPT-NBR                  PIC X(03)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
W39691*    05  SA-MCL-NBR                   PIC X(03)  VALUE SPACES.            
W39691*    05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-SCL-NBR                   PIC X(03)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-VS-NBR                    PIC X(20)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
W39691     05  SA-SKU-NBR                   PIC X(08)  VALUE SPACES.            
W39691     05  FILLER                       PIC X(01)  VALUE ','.               
W39691*    05  SA-CREATE-DATE               PIC X(10)  VALUE SPACES.            
W39691*    05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-ASTERISK-RETAIL           PIC X(01)  VALUE SPACE.             
W39691*    05  SA-RETAIL                    PIC -Z,ZZZ,ZZ9.99                   
W39691     05  SA-RETAIL                    PIC -ZZZZZZ9.99                     
                                                       VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-ASTERISK-COST             PIC X(01)  VALUE SPACE.             
W39691*    05  SA-COST                      PIC -Z,ZZZ,ZZ9.999                  
W39691     05  SA-COST                      PIC -ZZZZZZ9.999                    
                                                       VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
           05  SA-FIRST-RCVD-DATE           PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE ','.               
W39691     05  SA-CREATE-DATE               PIC X(10)  VALUE SPACES.            
W39691     05  FILLER                       PIC X(01)  VALUE ','.               
W39691     05  SA-PERM-DOC-NBR              PIC X(10)                           
W39691                                                 VALUE SPACES.            
W39691     05  FILLER                       PIC X(01)  VALUE ','.               
W39691     05  SA-PERM-EFF-DTE              PIC X(10)  VALUE SPACES.            
W39691     05  FILLER                       PIC X(01)  VALUE ','.               
W39691     05  SA-BRAND                     PIC X(20)  VALUE SPACES.            
W39691     05  FILLER                       PIC X(01)  VALUE ','.               
W39691     05  SA-VS-DESC                   PIC X(30)  VALUE SPACES.            
W39691     05  FILLER                       PIC X(01)  VALUE ','.               
W39691     05  SA-WEB-XCLUSIVE              PIC X(01)  VALUE SPACES.            
W39691     05  FILLER                       PIC X(01)  VALUE ','.               
      *CHG0202950-START                                                         
W39691     05  SA-PRD-GP-NBR                PIC X(10)  VALUE SPACES.            
W39691     05  FILLER                       PIC X(01)  VALUE ','.               
      *CHG0202950-END                                                           
                                                                                
      ******************************************************************        
      *    PS-PROGRAM SWITCHES                                         *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW            PIC  X(01)   VALUE 'N'.             
               88  PS-END-OF-FILE                        VALUE 'Y'.             
                                                                                
      ******************************************************************        
      *    PA-PROGRAM ACCUMULATORS                                     *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-REPORT-RECORDS-WRITTEN    PIC S9(9) COMP-3                    
                                                       VALUE ZEROES.            
           05  PA-BYR-SELECT-CNT            PIC S9(9) COMP-3                    
                                                       VALUE ZEROES.            
           05  PA-RECS-READ-CNT             PIC S9(9) COMP-3                    
                                                       VALUE ZEROES.            
           05  PA-MIN-FIRST-RCVD-CNT        PIC S9(9) COMP-3                    
                                                       VALUE ZEROES.            
           05  PA-COST-RTL-SELECT-CNT       PIC S9(9) COMP-3                    
                                                       VALUE ZEROES.            
           05  PA-STYLE-SELECT-CNT          PIC S9(9) COMP-3                    
                                                       VALUE ZEROES.            
W39691     05  PA-SKU-SELECT-CNT            PIC S9(9) COMP-3                    
W39691                                                 VALUE ZEROES.            
                                                                                
W39691*01  REPORT-TITLE-LINE-CSV.                                               
W39691*    05  FILLER                       PIC X(24)  VALUE ',,,,'.            
W39691*    05  REPORT-TITLE-LINE1-CSV.                                          
W39691*        10  REPORT-TITLE-LINEA-CSV   PIC X(35)  VALUE                    
W39691*       ', WEEKLY NEW STYLE CREATION REPORT '.                            
W39691*    05  FILLER                       PIC X(10)  VALUE ',,,,'.            
W39691*    05  FILLER                       PIC X(07)  VALUE                    
W39691*                                   'DATE:  '.                            
W39691*    05  TITLE-DATE                   PIC X(10)  VALUE SPACES.            
W39691*    05  FILLER                       PIC X(23)  VALUE ',,,,'.            
                                                                                
W39691*01  REPORT-HEADING-LINE-CSV.                                             
W39691*    05  FILLER                       PIC X(07)  VALUE                    
W39691*        'BUYER #'.                                                       
W39691*    05  FILLER                       PIC X(02)  VALUE ', '.              
W39691*    05  FILLER                       PIC X(10)  VALUE                    
W39691*        'BUYER NAME'.                                                    
W39691*    05  FILLER                       PIC X(02)  VALUE ', '.              
W39691*    05  FILLER                       PIC X(04)  VALUE                    
W39691*        'DEPT'.                                                          
W39691*    05  FILLER                       PIC X(02)  VALUE ', '.              
W39691*    05  FILLER                       PIC X(11)  VALUE                    
W39691*        'MAJOR CLASS'.                                                   
W39691*    05  FILLER                       PIC X(02)  VALUE ', '.              
W39691*    05  FILLER                       PIC X(09)  VALUE                    
W39691*        'SUB CLASS'.                                                     
W39691*    05  FILLER                       PIC X(02)  VALUE ', '.              
W39691*    05  FILLER                       PIC X(07)  VALUE                    
W39691*        'STYLE #'.                                                       
W39691*    05  FILLER                       PIC X(02)  VALUE ', '.              
W39691*    05  FILLER                       PIC X(11)  VALUE                    
W39691*        'CREATE DATE'.                                                   
W39691*    05  FILLER                       PIC X(02)  VALUE ', '.              
W39691*    05  FILLER                       PIC X(06)  VALUE                    
W39691*        'RETAIL'.                                                        
W39691*    05  FILLER                       PIC X(02)  VALUE ', '.              
W39691*    05  FILLER                       PIC X(04)  VALUE                    
W39691*        'COST'.                                                          
W39691*    05  FILLER                       PIC X(02)  VALUE ', '.              
W39691*    05  FILLER                       PIC X(13)  VALUE                    
W39691*        '1ST RCVD DATE'.                                                 
W39691*    05  FILLER                       PIC X(09)  VALUE ',,'.              
                                                                                
      ******************************************************************        
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *    MB-MERCHANDISE AREA TABLE                                   *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
      ******************************************************************        
      *    MB-MERCHANDISE AREA TABLE                                   *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TRSPBAR                                                  
           END-EXEC.                                                            
      ******************************************************************        
      *    SKU STORE                                                   *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TSKST                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    TSKXREF  GET SKU FROM ITEM NMB                              *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    TUPC                                                        *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TUPC                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    TUPCPLS                                                     *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TUPCPLS                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    TVNDSTL                                                     *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TVNDSTL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    TMDARVS                                                     *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMDARVS                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    TWEBITM                                                     *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TWEBITM                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    IMT_LBL                                                     *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE IMT00000                                                 
           END-EXEC.                                                            
                                                                                
W39691******************************************************************        
W39691*    TVSCHVL                                                     *        
W39691******************************************************************        
W39691     EXEC SQL                                                             
W39691         INCLUDE TVSCHVL                                                  
W39691     END-EXEC.                                                            
W39691                                                                          
      ******************************************************************        
      *    STANDARD LAYOUT FOR THE SQL COMMUNCATIONS AREA              *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAINLINE.                                                           
                                                                                
           PERFORM B100-INITIALIZE.                                             
                                                                                
           PERFORM B200-PROCESS-REPORT                                          
               UNTIL PS-END-OF-FILE.                                            
                                                                                
           PERFORM B300-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *     B100-INITIALIZE                                            *        
      * ==> PROCESSES:                                                 *        
      *    - READS THE FIRST RECORD FROM STYLE CREATE INPUT FILE       *        
      * ==> PERFORMED FROM:  A100-MAINLINE                             *        
      ******************************************************************        
       B100-INITIALIZE.                                                         
           OPEN INPUT NEW-STYLE-FILE                                            
                OUTPUT REPORT-FILE.                                             
                                                                                
           PERFORM C200-READ-NEW-STYLE.                                         
           IF PS-END-OF-FILE                                                    
               DISPLAY ' '                                                      
               DISPLAY '***********************************'                    
               DISPLAY ' NO INPUT RECS TO PROCESS.'                             
               DISPLAY '***********************************'                    
               DISPLAY ' '                                                      
W39691*    ELSE                                                                 
W39691*        PERFORM C100-GET-CURRENT-DTE                                     
W39691*        MOVE PV-CURRENT-DATE TO TITLE-DATE                               
W39691*        WRITE REPORT-RECORD FROM REPORT-TITLE-LINE-CSV AFTER 1           
W39691*        WRITE REPORT-RECORD FROM REPORT-HEADING-LINE-CSV                 
W39691*            AFTER 1                                                      
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *     B200-PROCESS-REPORT                                        *        
      * ==> PROCESSES:                                                 *        
      * ==> PERFORMED FROM:  A100-MAINLINE                             *        
      ******************************************************************        
       B200-PROCESS-REPORT.                                                     
           PERFORM D100-GET-STYLE-INFO.                                         
      *** DROP STYLE IF NO LONGER ACTIVE, NO LONGER REGULAR TYPE BRAND.         
           IF SQLCODE = +0                                                      
               IF MDARVS-DEPT-NBR NOT EQUAL PV-PREV-DEPT                        
                   PERFORM D300-SELECT-BUYER-NAME                               
                   MOVE MDARVS-DEPT-NBR TO PV-PREV-DEPT                         
               END-IF                                                           
W39691         PERFORM D400-GET-SKU                                             
W39691         IF PV-DB2-NUM-SKUS = +1                                          
W39691            MOVE SKXREF-SKU TO PV-SKU-NBR                                 
W39691            MOVE PV-SKU-NBR TO SA-SKU-NBR                                 
W39691         ELSE                                                             
W39691            MOVE SPACES     TO SA-SKU-NBR                                 
W39691         END-IF                                                           
               PERFORM D500-GET-COST-RTL                                        
      *** DROP STYLE IF RETAIL <= 5 CENTS   *****                               
               IF UPCPLS-UNT-RTL-AMT > +.05                                     
                   IF PV-DB2-NUM-COSTS > +1                                     
                       MOVE '*' TO SA-ASTERISK-COST                             
                   ELSE                                                         
                       MOVE ' ' TO SA-ASTERISK-COST                             
                   END-IF                                                       
                   IF PV-DB2-NUM-RTLS > +1                                      
                       MOVE '*' TO SA-ASTERISK-RETAIL                           
                   ELSE                                                         
                       MOVE ' ' TO SA-ASTERISK-RETAIL                           
                   END-IF                                                       
                   MOVE UPCPLS-UNT-COST-AMT TO SA-COST                          
                   MOVE UPCPLS-UNT-RTL-AMT  TO SA-RETAIL                        
W39691             PERFORM D600-WEB-ELGBLE-CNT                                  
W39691* CHECK IF ALL SKUS ARE WEB ELIGIBLE                                      
W39691             IF PV-DB2-NUM-WEB-SKUS = PV-DB2-NUM-SKUS                     
W39691                PERFORM D700-GET-WEB-XLUSIVE                              
W39691             ELSE                                                         
W39691                MOVE 'N'    TO SA-WEB-XCLUSIVE                            
W39691             END-IF                                                       
                   PERFORM D900-GET-MIN-FIRST-RCVD                              
      **CHG0202950-START                                                        
                   IF SA-SKU-NBR = SPACES                                       
                      MOVE SPACES                 TO SA-PRD-GP-NBR              
                   ELSE                                                         
                      PERFORM D800-GET-CP-ID                                    
                   END-IF                                                       
      *CHG0202950-END                                                           
                   PERFORM F100-PRINT-DETAIL                                    
               END-IF                                                           
           END-IF.                                                              
           PERFORM C200-READ-NEW-STYLE.                                         
                                                                                
      ******************************************************************        
      *     B300-EOJ-ROUTINE                                           *        
      * ==> PROCESSES:                                                 *        
      *     - CLOSES THE FILES                                         *        
      *     - DISPLAYS THE NUMBER OF RECORDS READ AND WRITTEN          *        
      * ==> PERFORMED FROM:  A100-MAINLINE                             *        
      ******************************************************************        
       B300-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE NEW-STYLE-FILE                                                 
                 REPORT-FILE.                                                   
                                                                                
           DISPLAY '*******'.                                                   
           DISPLAY 'MESSAGES FOR PROGRAM   '  PC-PROGRAM-NAME.                  
           DISPLAY 'RECS READ      :       '  PA-RECS-READ-CNT.                 
           DISPLAY 'BUYER SELECTS  :       '  PA-BYR-SELECT-CNT.                
           DISPLAY 'STYLE SELECTS  :       '  PA-STYLE-SELECT-CNT.              
           DISPLAY 'COST/RTL SELECTS:      '  PA-COST-RTL-SELECT-CNT.           
           DISPLAY '1ST RCVD SELECTS:      '  PA-MIN-FIRST-RCVD-CNT.            
           DISPLAY 'RECS WRITTEN    :      '  PA-REPORT-RECORDS-WRITTEN.        
           DISPLAY '*******'.                                                   
                                                                                
                                                                                
W39691*C100-GET-CURRENT-DTE.                                                    
W39691*                                                                         
W39691*    PERFORM  WITH TEST AFTER                                             
W39691*            UNTIL SQLCODE = +0                                           
W39691*               OR SQLCODE = +100                                         
W39691*               OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
W39691*        EXEC SQL                                                         
W39691*            SELECT CURRENT DATE                                          
W39691*            INTO :PV-CURRENT-DATE                                        
W39691*            FROM SYSIBM.SYSDUMMY1                                        
W39691*            WITH UR                                                      
W39691*        END-EXEC                                                         
W39691*        EVALUATE TRUE                                                    
W39691*            WHEN SQLCODE = ZERO                                          
W39691*                CONTINUE                                                 
W39691*            WHEN SQLCODE = -911                                          
W39691*            WHEN SQLCODE = -913                                          
W39691*                 ADD 1          TO PV-RETRY-COUNT                        
W39691*            WHEN OTHER                                                   
W39691*                MOVE 'C100-GET-CURRENT-DTE'                              
W39691*                                  TO PV-PARAGRAPH-NAME                   
W39691*                MOVE 'GET CURRENT DATE'                                  
W39691*                                  TO PV-DB2-OPERATION-ATTEMPTED          
W39691*                PERFORM Z900-SQL-ABEND                                   
W39691*        END-EVALUATE                                                     
W39691*    END-PERFORM.                                                         
W39691*                                                                         
W39691*    IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
W39691*        MOVE 'C100-GET-CURRENT-DTE'                                      
W39691*                                TO PV-PARAGRAPH-NAME                CL**1
W39691*        MOVE 'GET CURRENT DATE'                                          
W39691*                                TO PV-DB2-OPERATION-ATTEMPTED       CL**1
W39691*        PERFORM Z900-SQL-ABEND                                           
W39691*    END-IF.                                                              
W39691*                                                                         
      ******************************************************************        
      *     C200-READ-NEW-STYLE                                        *        
      ******************************************************************        
       C200-READ-NEW-STYLE.                                                     
           MOVE 'C200-READ-NEW-STYLE' TO PV-PARAGRAPH-NAME.                     
           READ NEW-STYLE-FILE                                                  
               INTO SA-REC-IN                                                   
                   AT END                                                       
                       MOVE 'Y'     TO PS-END-OF-FILE-SW                        
                   NOT AT END                                                   
                       ADD +1       TO PA-RECS-READ-CNT                         
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * D100-GET-STYLE-INFO.                                           *        
      ******************************************************************        
       D100-GET-STYLE-INFO.                                                     
           PERFORM  WITH TEST AFTER                                             
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                   SELECT A.SUB_CL_MDSEAR_ID                                    
                        , A.VS_ID                                               
                        , A.DEPT_NBR                                            
W39691*                 , A.MAJ_CL_NBR                                          
                        , A.SUB_CL_NBR                                          
W39691                  , A.VS_DESC                                             
                        , B.VS_NBR                                              
W39691                  , D.CHARTC_VAL_DESC                                     
                     INTO :MDARVS-SUB-CL-MDSEAR-ID                              
                        , :MDARVS-VS-ID                                         
                        , :MDARVS-DEPT-NBR                                      
W39691*                 , :MDARVS-MAJ-CL-NBR                                    
                        , :MDARVS-SUB-CL-NBR                                    
W39691                  , :MDARVS-VS-DESC                                       
                        , :VNDSTL-VS-NBR                                        
W39691                  , :VSCHVL-CHARTC-VAL-DESC                               
                   FROM TMDARVS A                                               
                       ,TVNDSTL B                                               
                       ,IMT_LBL C                                               
W39691                 ,TVSCHVL D                                               
                   WHERE A.RMS_STYL_ID = :SA-RMS-STYL-ID                        
                     AND A.ACTV_IND    = 'Y'                                    
                     AND A.VS_ID       = B.VS_ID                                
                     AND A.LBL_SEQ_NBR = C.LBL_SEQ_NBR                          
                     AND C.LBL_TYP_CDE = 'R'                                    
W39691               AND D.CHARTC_CDE  = 3                                      
W39691               AND D.CHARTC_SEQ_NBR = A.LBL_SEQ_NBR                       
W39691*            AND NOT EXISTS                                               
W39691*            (SELECT 1                                                    
W39691*             FROM TUPC D                                                 
W39691*                 ,TWEBITM E                                              
W39691*                 ,TMDARVS F                                              
W39691*             WHERE F.RMS_STYL_ID = :SA-RMS-STYL-ID                       
W39691*               AND D.VS_ID = F.VS_ID                                     
W39691*               AND D.SUB_CL_MDSEAR_ID = F.SUB_CL_MDSEAR_ID               
W39691*               AND D.UPC_TYP_CDE      = 'I'                              
W39691*               AND D.EFF_END_DTE IS NULL                                 
W39691*               AND D.UPC_ID           = E.INTR_UPC_ID                    
W39691*               AND E.WEB_EXCLV_IND    = 'Y'                              
W39691*               AND E.EFF_BEG_TMST <= CURRENT TIMESTAMP                   
W39691*               AND E.EFF_END_TMST IS NULL)                               
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD +1 TO PA-STYLE-SELECT-CNT                            
                       MOVE MDARVS-DEPT-NBR TO PV-DEPT-NBR                      
                       MOVE PV-DEPT-NBR     TO SA-DEPT-NBR                      
W39691*                MOVE MDARVS-MAJ-CL-NBR TO PV-MCL-NBR                     
W39691*                MOVE PV-MCL-NBR        TO SA-MCL-NBR                     
                       MOVE MDARVS-SUB-CL-NBR TO PV-SCL-NBR                     
                       MOVE PV-SCL-NBR        TO SA-SCL-NBR                     
                       MOVE VNDSTL-VS-NBR TO SA-VS-NBR                          
W39691                 MOVE MDARVS-VS-DESC    TO SA-VS-DESC                     
W39691                 MOVE VSCHVL-CHARTC-VAL-DESC TO SA-BRAND                  
                   WHEN SQLCODE = +100                                          
33885                  CONTINUE                                                 
045200             WHEN SQLCODE = -911                                          
045300             WHEN SQLCODE = -913                                          
045400                  ADD 1          TO PV-RETRY-COUNT                        
                   WHEN OTHER                                                   
                       MOVE 'D100-GET-STYLE-INFO'                               
                                         TO PV-PARAGRAPH-NAME                   
                       MOVE 'SELECT STYLE INFO'                                 
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
046200     END-PERFORM.                                                         
046300*                                                                         
046400     IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE 'D100-GET-STYLE-INFO'                                       
046600                                 TO PV-PARAGRAPH-NAME                CL**1
               MOVE 'SELECT STYLE INFO'                                         
046800                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
               PERFORM Z900-SQL-ABEND                                           
047000     END-IF.                                                              
      ******************************************************************        
      *     D300-SELECT-BUYER-NAME                                     *        
      ******************************************************************        
       D300-SELECT-BUYER-NAME.                                                  
           MOVE MDARVS-DEPT-NBR TO PV-DEPT-NBR.                                 
           MOVE PV-DEPT-NBR TO MDSEAR-DEPT-NBR.                                 
           PERFORM  WITH TEST AFTER                                             
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                   SELECT C.WORKER_LAST_NAME                                    
                        , A.BYR_NBR                                             
W39691                  , A.DMA_NBR                                             
                     INTO :RSPBAR-WORKER-LAST-NAME                              
                        , :MDSEAR-BYR-NBR                                       
W39691                  , :MDSEAR-DMA-NBR                                       
                     FROM TMDSEAR A                                             
                        , TRSPBAR C                                             
                    WHERE A.OPERCO_NBR  = '03'                                  
                      AND A.DEPT_NBR    = :MDSEAR-DEPT-NBR                      
                      AND A.MDSELV_CDE  = 'DPT'                                 
                      AND A.MDSEAR_DKEY = C.MDSEAR_DKEY                         
                      AND C.RSPLVL_CDE  = 'BYR'                                 
                      AND (CURRENT DATE BETWEEN A.STRT_DTE                      
                                            AND A.END_DTE)                      
                      AND (CURRENT DATE BETWEEN C.WORKER_STRT_DTE               
                                            AND C.WORKER_END_DTE)               
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       MOVE MDSEAR-BYR-NBR TO SA-BUYER-NBR                      
                       MOVE RSPBAR-WORKER-LAST-NAME TO                          
                                   SA-BUYER-LAST-NAME                           
W39691                 MOVE MDSEAR-DMA-NBR TO SA-DIVISN-NBR                     
                       ADD +1       TO PA-BYR-SELECT-CNT                        
                   WHEN SQLCODE = +100                                          
33885                  MOVE SPACES           TO SA-BUYER-NBR                    
                       MOVE PC-BUYER-NOT-FOUND                                  
                                             TO SA-BUYER-LAST-NAME              
W39691                 MOVE SPACES           TO SA-DIVISN-NBR                   
045200             WHEN SQLCODE = -911                                          
045300             WHEN SQLCODE = -913                                          
045400                  ADD 1          TO PV-RETRY-COUNT                        
                   WHEN OTHER                                                   
                       MOVE 'D300-SELECT-BUYER-NAME'                            
                                         TO PV-PARAGRAPH-NAME                   
                       MOVE 'SELECT BUYER NAME'                                 
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
046200     END-PERFORM.                                                         
046300*                                                                         
046400     IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE 'D300-SELECT-BUYER-NAME'                                    
046600                                 TO PV-PARAGRAPH-NAME                CL**1
               MOVE 'SELECT BUYER NAME'                                         
046800                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
               PERFORM Z900-SQL-ABEND                                           
047000     END-IF.                                                              
                                                                                
W39691******************************************************************        
W39691*     D400-GET-SKU                                               *        
W39691*     FIND OUT IF MULTIPLE SKU EXISTS FOR THIS STYLE AND POPULATE*        
W39691*     SPACE FOR SKU. IF SINGLE SKU, POPULATE SKU VALUE.          *        
W39691******************************************************************        
W39691 D400-GET-SKU.                                                            
W39691                                                                          
W39691     PERFORM  WITH TEST AFTER                                             
W39691             UNTIL SQLCODE = +0                                           
W39691                OR SQLCODE = +100                                         
W39691                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
W39691         EXEC SQL                                                         
W39691             SELECT COUNT (DISTINCT B.SKU)                                
W39691                   ,MAX(B.SKU)                                            
W39691             INTO   :PV-DB2-NUM-SKUS                                      
W39691                   ,:SKXREF-SKU                                           
W39691             FROM  TUPC A                                                 
W39691                  ,TSKXREF B                                              
W39691             WHERE A.VS_ID            = :MDARVS-VS-ID                     
W39691               AND A.SUB_CL_MDSEAR_ID = :MDARVS-SUB-CL-MDSEAR-ID          
W39691               AND A.UPC_TYP_CDE      = 'I'                               
W39691               AND A.EFF_END_DTE IS NULL                                  
W39691               AND B.INTR_UPC_ID      = A.UPC_ID                          
W39691         END-EXEC                                                         
W39691                                                                          
W39691         EVALUATE TRUE                                                    
W39691             WHEN SQLCODE = ZERO                                          
W39691                 ADD +1 TO PA-SKU-SELECT-CNT                              
W39691             WHEN SQLCODE = -911                                          
W39691             WHEN SQLCODE = -913                                          
W39691                  ADD 1          TO PV-RETRY-COUNT                        
W39691             WHEN OTHER                                                   
W39691                 MOVE 'D400-GET-SKU'                                      
W39691                                   TO PV-PARAGRAPH-NAME                   
W39691                 MOVE 'SELECT SKU' TO PV-DB2-OPERATION-ATTEMPTED          
W39691                 PERFORM Z900-SQL-ABEND                                   
W39691         END-EVALUATE                                                     
W39691     END-PERFORM.                                                         
W39691                                                                          
W39691     IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
W39691         MOVE 'D400-GET-SKU'     TO PV-PARAGRAPH-NAME                     
W39691         MOVE 'SELECT SKU'       TO PV-DB2-OPERATION-ATTEMPTED            
W39691         PERFORM Z900-SQL-ABEND                                           
W39691     END-IF.                                                              
                                                                                
      ******************************************************************        
      *     D500-GET-COST-RTL                                          *        
      *     FIND OUT IF MULTIPLE COST AND/OR RETAIL EXISTS FOR THIS    *        
      *     STYLE AND GET MAX COST AND RETAIL.                         *        
      ******************************************************************        
       D500-GET-COST-RTL.                                                       
                                                                                
           PERFORM  WITH TEST AFTER                                             
                   UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
                      OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                   SELECT COUNT (DISTINCT B.UNT_COST_AMT)                       
                         ,COUNT (DISTINCT B.UNT_RTL_AMT)                        
                         ,MAX(B.UNT_COST_AMT)                                   
                         ,MAX(B.UNT_RTL_AMT)                                    
                   INTO   :PV-DB2-NUM-COSTS                                     
                         ,:PV-DB2-NUM-RTLS                                      
                         ,:UPCPLS-UNT-COST-AMT                                  
                         ,:UPCPLS-UNT-RTL-AMT                                   
                   FROM  TUPC A                                                 
                        ,TUPCPLS B                                              
                   WHERE A.VS_ID = :MDARVS-VS-ID                                
                     AND A.SUB_CL_MDSEAR_ID = :MDARVS-SUB-CL-MDSEAR-ID          
                     AND A.UPC_TYP_CDE = 'I'                                    
                     AND A.EFF_END_DTE IS NULL                                  
                     AND A.UPC_ID = B.UPC_ID                                    
                     AND B.STR_NBR = SMALLINT(0)                                
                     AND B.EFF_BEG_TMST <= CURRENT TIMESTAMP                    
                     AND (B.EFF_END_TMST IS NULL                                
                          OR B.EFF_END_TMST >= CURRENT TIMESTAMP)               
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       ADD +1 TO PA-COST-RTL-SELECT-CNT                         
045200             WHEN SQLCODE = -911                                          
045300             WHEN SQLCODE = -913                                          
045400                  ADD 1          TO PV-RETRY-COUNT                        
                   WHEN OTHER                                                   
                       MOVE 'D500-GET-COST-RTL'                                 
                                         TO PV-PARAGRAPH-NAME                   
                       MOVE 'SELECT COST/RETAIL'                                
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
046200     END-PERFORM.                                                         
046300*                                                                         
046400     IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE 'D500-GET-COST-RTL'                                         
046600                                 TO PV-PARAGRAPH-NAME                CL**1
               MOVE 'SELECT COST/RETAIL'                                        
046800                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
               PERFORM Z900-SQL-ABEND                                           
047000     END-IF.                                                              
W39691                                                                          
W39691******************************************************************        
W39691*     D600-WEB-ELGBLE-CNT                                        *        
W39691*     RETRIEVE THE COUNT OF WEB ELIGIBLE SKUS UNDER A STYLE      *        
W39691******************************************************************        
W39691 D600-WEB-ELGBLE-CNT.                                                     
W39691                                                                          
W39691     PERFORM  WITH TEST AFTER                                             
W39691             UNTIL SQLCODE = +0                                           
W39691                OR SQLCODE = +100                                         
W39691                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
W39691         EXEC SQL                                                         
W39691             SELECT  COUNT (DISTINCT B.INTR_UPC_ID)                       
W39691             INTO   :PV-DB2-NUM-WEB-SKUS                                  
W39691             FROM  TUPC A                                                 
W39691                  ,TWEBITM B                                              
W39691             WHERE A.VS_ID            = :MDARVS-VS-ID                     
W39691               AND A.SUB_CL_MDSEAR_ID = :MDARVS-SUB-CL-MDSEAR-ID          
W39691               AND A.UPC_TYP_CDE      = 'I'                               
W39691               AND A.EFF_END_DTE IS NULL                                  
W39691               AND B.INTR_UPC_ID      = A.UPC_ID                          
W39691               AND B.EFF_BEG_TMST <= CURRENT TIMESTAMP                    
W39691               AND B.EFF_END_TMST IS NULL                                 
W39691         END-EXEC                                                         
W39691                                                                          
W39691         EVALUATE TRUE                                                    
W39691             WHEN SQLCODE = ZERO                                          
W39691                 CONTINUE                                                 
W39691             WHEN SQLCODE = -911                                          
W39691             WHEN SQLCODE = -913                                          
W39691                  ADD 1          TO PV-RETRY-COUNT                        
W39691             WHEN OTHER                                                   
W39691                 MOVE 'D600-WEB-ELGBLE-CNT'                               
W39691                                   TO PV-PARAGRAPH-NAME                   
W39691                 MOVE 'SELECT WEB CNT'                                    
W39691                                   TO PV-DB2-OPERATION-ATTEMPTED          
W39691                 PERFORM Z900-SQL-ABEND                                   
W39691         END-EVALUATE                                                     
W39691     END-PERFORM.                                                         
W39691                                                                          
W39691     IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
W39691         MOVE 'D600-WEB-ELGBLE-CNT'                                       
W39691                                 TO PV-PARAGRAPH-NAME                     
W39691         MOVE 'SELECT WEB CNT'   TO PV-DB2-OPERATION-ATTEMPTED            
W39691         PERFORM Z900-SQL-ABEND                                           
W39691     END-IF.                                                              
                                                                                
W39691******************************************************************        
W39691*     D700-GET-WEB-XLUSIVE                                       *        
W39691*     RETRIEVE THE WEB EXCLUSIVE INDICATIOR. POPULATE 'Y' IF ALL *        
W39691*     SKUS IN A STYLE ARE WEB EXCLUSIVE ELSE 'N'.                *        
W39691******************************************************************        
W39691 D700-GET-WEB-XLUSIVE.                                                    
W39691                                                                          
W39691     MOVE ZERO     TO PV-WEB-COUNT                                        
W39691     PERFORM  WITH TEST AFTER                                             
W39691             UNTIL SQLCODE = +0                                           
W39691                OR SQLCODE = +100                                         
W39691                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
W39691         EXEC SQL                                                         
W39691             SELECT 1                                                     
W39691             INTO   :PV-WEB-COUNT                                         
W39691             FROM  TUPC A                                                 
W39691                  ,TWEBITM B                                              
W39691             WHERE A.VS_ID            = :MDARVS-VS-ID                     
W39691               AND A.SUB_CL_MDSEAR_ID = :MDARVS-SUB-CL-MDSEAR-ID          
W39691               AND A.UPC_TYP_CDE      = 'I'                               
W39691               AND A.EFF_END_DTE IS NULL                                  
W39691               AND B.INTR_UPC_ID      = A.UPC_ID                          
W39691               AND B.WEB_EXCLV_IND    = 'N'                               
W39691               AND B.EFF_BEG_TMST <= CURRENT TIMESTAMP                    
W39691               AND B.EFF_END_TMST IS NULL                                 
W39691             FETCH FIRST 1 ROWS ONLY                                      
W39691         END-EXEC                                                         
W39691                                                                          
W39691         EVALUATE TRUE                                                    
W39691             WHEN SQLCODE = 0 AND PV-WEB-COUNT = 1                        
W39691                 MOVE 'N' TO SA-WEB-XCLUSIVE                              
W39691             WHEN SQLCODE = 0 AND PV-WEB-COUNT = 0                        
W39691             WHEN SQLCODE = 100                                           
W39691                 MOVE 'Y' TO SA-WEB-XCLUSIVE                              
W39691             WHEN SQLCODE = -911                                          
W39691             WHEN SQLCODE = -913                                          
W39691                  ADD 1          TO PV-RETRY-COUNT                        
W39691             WHEN OTHER                                                   
W39691                 MOVE 'D700-GET-WEB-XLUSIVE'                              
W39691                                   TO PV-PARAGRAPH-NAME                   
W39691                 MOVE 'SELECT WEB EXCLUSIVE'                              
W39691                                   TO PV-DB2-OPERATION-ATTEMPTED          
W39691                 PERFORM Z900-SQL-ABEND                                   
W39691         END-EVALUATE                                                     
W39691     END-PERFORM.                                                         
W39691                                                                          
W39691     IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
W39691         MOVE 'D700-GET-WEB-XLUSIVE'                                      
W39691                                 TO PV-PARAGRAPH-NAME                     
W39691         MOVE 'SELECT WEB EXCLUSIVE'                                      
W39691                                 TO PV-DB2-OPERATION-ATTEMPTED            
W39691         PERFORM Z900-SQL-ABEND                                           
W39691     END-IF.                                                              
                                                                                
      *CHG0202950-START                                                         
      ******************************************************************        
      *     D800-GET-CP-ID.                                                     
      * ==> PROCESSES:                                                 *        
      *     -FIND THE MIN FIRST RECEIVED DATE FOR ALL SKUS IN STYLE    *        
      ******************************************************************        
       D800-GET-CP-ID.                                                          
00558          EXEC SQL                                                         
                    SELECT A.PRD_GP_NBR                                         
                      INTO :WEBITM-PRD-GP-NBR                                   
                    FROM TWEBITM A                                              
                        ,TSKXREF B                                              
                    WHERE A.INTR_UPC_ID = B.INTR_UPC_ID                         
                      AND B.SKU = :SA-SKU-NBR                                   
00564               ORDER BY A.INTR_UPC_ID,A.EFF_BEG_TMST DESC                  
044800              FETCH FIRST ROW ONLY                                        
                   WITH UR                                                      
               END-EXEC                                                         
                                                                                
044900         EVALUATE TRUE                                                    
045000             WHEN SQLCODE = +0                                            
                        MOVE WEBITM-PRD-GP-NBR TO PV-PRD-GP-NBR                 
                        MOVE PV-PRD-GP-NBR     TO SA-PRD-GP-NBR                 
045000             WHEN SQLCODE = +100                                          
                        MOVE SPACES TO SA-PRD-GP-NBR                            
045500             WHEN OTHER                                                   
045600                  MOVE 'D800-GET-CP-ID'                                  C
045700                                 TO PV-PARAGRAPH-NAME                CL**1
045800                  MOVE 'GET PRDGRP NBR'                              CL*11
045900                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
                        PERFORM Z900-SQL-ABEND                                  
046100         END-EVALUATE.                                                    
      *CHG0202950-END                                                           
      ******************************************************************        
      *     D900-GET-MIN-FIRST-RCVD.                                   *        
      * ==> PROCESSES:                                                 *        
      *     -FIND THE MIN FIRST RECEIVED DATE FOR ALL SKUS IN STYLE    *        
      ******************************************************************        
       D900-GET-MIN-FIRST-RCVD.                                                 
051000     PERFORM  WITH TEST AFTER                                             
051100             UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
051200                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
00558          EXEC SQL                                                         
                SELECT MIN(VT1.FIRST_RCVD_DATE)                                 
                INTO :PV-MIN-FIRST-RCVD                                         
                FROM                                                            
                (SELECT                                                         
                   CASE                                                         
216771             WHEN (INTEGER(SUBSTR(DIGITS(D.FIRST_RCVD_DATE),2,2))         
                                              < 30                              
216771              AND INTEGER(SUBSTR(DIGITS(D.FIRST_RCVD_DATE),6,2))          
                                              > 0) THEN                         
216771                 DATE('20'||SUBSTR(DIGITS(D.FIRST_RCVD_DATE),2,2)         
                           ||'-'||                                              
216771                 SUBSTR(DIGITS(D.FIRST_RCVD_DATE),4,2)                    
                           ||'-'||                                              
216771                 SUBSTR(DIGITS(D.FIRST_RCVD_DATE),6,2))                   
                   WHEN D.FIRST_RCVD_DATE = 0 THEN                              
                        '9999-09-09'                                            
                   ELSE                                                         
216771                 DATE('19'||SUBSTR(DIGITS(D.FIRST_RCVD_DATE),2,2)         
                           ||'-'||                                              
216771                 SUBSTR(DIGITS(D.FIRST_RCVD_DATE),4,2)                    
                           ||'-'||                                              
216771                 SUBSTR(DIGITS(D.FIRST_RCVD_DATE),6,2))                   
                   END AS FIRST_RCVD_DATE                                       
                   FROM TUPC B                                                  
                       ,TSKXREF C                                               
                       ,TSKST D                                                 
                   WHERE B.VS_ID = :MDARVS-VS-ID                                
                    AND B.SUB_CL_MDSEAR_ID = :MDARVS-SUB-CL-MDSEAR-ID           
                     AND B.UPC_TYP_CDE = 'I'                                    
                     AND B.EFF_END_DTE IS NULL                                  
                     AND B.UPC_ID    = C.INTR_UPC_ID                            
                     AND C.ITM_NBR   = D.ITEM_NMBR                              
                     AND D.LOC_NBR   = SMALLINT(0)) AS VT1                      
00564          END-EXEC                                                         
044800                                                                          
044900         EVALUATE TRUE                                                    
045000             WHEN SQLCODE = +0                                            
                        ADD +1         TO PA-MIN-FIRST-RCVD-CNT                 
045000             WHEN SQLCODE = +100                                          
                        CONTINUE                                                
045200             WHEN SQLCODE = -911                                          
045300             WHEN SQLCODE = -913                                          
045400                  ADD 1          TO PV-RETRY-COUNT                        
045500             WHEN OTHER                                                   
045600                  MOVE 'D900-GET-MIN-FIRST-RCVD'                         C
045700                                 TO PV-PARAGRAPH-NAME                CL**1
045800                  MOVE 'GET MIN FIRST RCVD'                          CL*11
045900                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
                        PERFORM Z900-SQL-ABEND                                  
046100         END-EVALUATE                                                     
046200     END-PERFORM.                                                         
046300*                                                                         
046400     IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
045600         MOVE 'D900-GET-MIN-FIRST-RCVD'                                  C
046600                                 TO PV-PARAGRAPH-NAME                CL**1
045800         MOVE 'GET MIN FIRST RCVD'                                   CL*11
046800                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
               PERFORM Z900-SQL-ABEND                                           
047000     END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *     F100-PRINT-DETAIL                                          *        
      * ==> PROCESSES:                                                 *        
      *     - WRITES THE DETAIL LINES FOR THE REPORT                   *        
      * ==> PERFORMED FROM:  B200-WRITE-REPORT                         *        
      ******************************************************************        
       F100-PRINT-DETAIL.                                                       
           MOVE SA-STYLE-CREATE-DATE       TO SA-CREATE-DATE.                   
           IF PV-MIN-FIRST-RCVD = '9999-09-09'                                  
W39691*        MOVE 'NONE' TO SA-FIRST-RCVD-DATE                                
W39691         MOVE SPACES TO SA-FIRST-RCVD-DATE                                
           ELSE                                                                 
               MOVE PV-MIN-FIRST-RCVD TO PV-MIN-DATE                            
               MOVE PV-MIN-CCYY    TO PV-DATE-CCYY                              
               MOVE PV-MIN-MM      TO PV-DATE-MM                                
               MOVE PV-MIN-DD      TO PV-DATE-DD                                
               MOVE PV-DATE TO SA-FIRST-RCVD-DATE                               
           END-IF.                                                              
W39691     MOVE SPACES                     TO SA-PERM-DOC-NBR.                  
W39691     MOVE SPACES                     TO SA-PERM-EFF-DTE.                  
           WRITE REPORT-RECORD FROM SA-REC-OUT AFTER 1.                         
           ADD 1                         TO PA-REPORT-RECORDS-WRITTEN.          
                                                                                
      ******************************************************************        
      *     Z900-SQL-ABEND                                             *        
      * ==> PROCESSES:                                                 *        
      *     -PERFORMS ABEND PROCESSING FOR SQL ERRORS.                 *        
      * ==> PERFORMED FROM:                                            *        
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           DISPLAY 'Z900-SQL-ABEND'.                                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   ** = ' PC-PROGRAM-NAME.                       
           DISPLAY '**  PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                     
           DISPLAY '**  OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.            
           DISPLAY 'RECS READ      :       '  PA-RECS-READ-CNT.                 
           DISPLAY 'BUYER SELECTS  :       '  PA-BYR-SELECT-CNT.                
           DISPLAY 'STYLE SELECTS  :       '  PA-STYLE-SELECT-CNT.              
           DISPLAY 'COST/RTL SELECTS:      '  PA-COST-RTL-SELECT-CNT.           
           DISPLAY '1ST RCVD SELECTS:      '  PA-MIN-FIRST-RCVD-CNT.            
           DISPLAY 'RECS WRITTEN    :      '  PA-REPORT-RECORDS-WRITTEN.        
           DISPLAY '*******'.                                                   
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
      * FORMAT.                                                        *        
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013                    TO ABEND-CODE.                         
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       9999-ABEND.                                                      07730094
      ******************************************************************07740094
      * ABEND ROUTINE MISC ERRORS                                      *07750094
      ******************************************************************07760094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07770094
           DISPLAY '** PARAGRAPH **      = ' PV-PARAGRAPH-NAME.         07780094
           CALL 'ILBOABN0' USING ABEND-CODE.                            07800094
