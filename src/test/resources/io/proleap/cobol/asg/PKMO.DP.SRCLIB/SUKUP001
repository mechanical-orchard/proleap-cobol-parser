      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
       PROGRAM-ID.             SUKUP001.                                        
       AUTHOR.                 V.SUDHAN.                                        
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           SEPTEMBER, 2011.                                 
       DATE-COMPILED.                                                           
      *****************************************************************         
      *                                                               *         
      *  THIS PROGRAM WILL READ THE EXTRACT FILE OF MASTER CONTAINER  *         
      *  IDS THAT NEED TO BE DELETED. TEH EXTRACT FILE IS CREATED     *         
      *  BY PROGRAM SUKUT059. THIS PROGRAM WILL DELETE THE RECORDS    *         
      *  FROM THE SUT_MSTR_SHIPT_UNT & SUT_MSTR_SHIPT_CNT TABLES FOR  *         
      *  THE EXTRACTED MASTER CONTAINER IDS.                          *         
      *                                                               *         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT MSTR-CRTN-PURGE-FILE                                          
                  ASSIGN TO INP01.                                              
      *                                                                         
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  MSTR-CRTN-PURGE-FILE                                                 
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0  RECORDS.                                           
       01  MSTR-PURGE-EXT-REC              PIC  X(030).                         
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  ABEND-CODE                      PIC S9(04)    VALUE +0               
                                                         COMP SYNC.             
      *                                                                         
       01  MST-CNTR-ID                     PIC  S9(18)V COMP-3.                 
      *                                                                         
       01  PROGRAM-SWITCHES.                                                    
           05  PS-EOF-MSTR-FILE-SW         PIC  X(01)    VALUE 'N'.             
               88  PS-EOF-MSTR-FILE                      VALUE 'Y'.             
               88  PS-NOT-EOF-MSTR-FILE                  VALUE 'N'.             
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
                                                         'SUKUP001'.            
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DATE-COMPILED            PIC  X(16)  VALUE SPACES.            
           05  PV-PARAGRAPH-NAME           PIC  X(30)  VALUE SPACES.            
           05  PV-DB2-OPERATION            PIC  X(30)  VALUE SPACES.            
           05  PV-MSTR-CONT-CNT            PIC  9(09)  VALUE ZEROS.             
           05  PV-MSTR-DATA-CNT            PIC  9(09)  VALUE ZEROS.             
           05  PV-CHILD-DATA-CNT           PIC  9(09)  VALUE ZEROS.             
           05  PV-REC-READ-COUNT           PIC  9(09)  VALUE ZEROS.             
           05  PV-COMMIT-COUNTER           PIC  9(09)  VALUE ZEROS.             
           05  PV-NUMBER-OF-COMMITS        PIC  9(09)  VALUE ZEROS.             
           05  PV-SQLERRD3                 PIC S9(09)  VALUE ZERO               
                                                       COMP-3.                  
      *                                                                         
       01  PV-DISP-KEY-MSG.                                                     
           05  FILLER                      PIC X(18) VALUE                      
               'MASTER CONTAINER ='.                                            
           05  PV-MSTR-CART                PIC 9(18) VALUE ZEROES.              
      ******************************************************************        
      *  DB2 FORMATTED MESSAGE AREA                                             
      ******************************************************************        
           COPY DPWS004.                                                        
      ******************************************************************        
      *  INPUT FILE COPY BOOK                                                   
      ******************************************************************        
           COPY SUWS074.                                                        
      ******************************************************************        
      *  DB2 COMMUNICATION AREA                                                 
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      ******************************************************************        
      *  DB2 COMMUNICATION AREA2                                                
      ******************************************************************        
           COPY SQLCA2.                                                         
      ******************************************************************        
      *  COPY BOOK FOR THE SUT_MSTR_SHIPT_UNT TABLE                             
      ******************************************************************        
      *                                                                         
           EXEC SQL                                                             
                INCLUDE SUT00098                                                
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      *  COPY BOOK FOR THE SUT_MSTR_SHIPT_CNT TABLE                             
      ******************************************************************        
      *                                                                         
           EXEC SQL                                                             
                INCLUDE SUT00099                                                
           END-EXEC.                                                            
      *                                                                         
       PROCEDURE DIVISION.                                                      
      *                                                                         
      ******************************************************************        
      **  THIS IS THE MAIN DRIVER FOR THE PROGRAM.                    **        
      ******************************************************************        
      *                                                                         
       0000-MAINLINE-PROCESSING.                                                
      *                                                                         
           PERFORM 1000-INITIALIZATION.                                         
      *                                                                         
           PERFORM 2000-PROCESS-MSTR-EXT-DATA UNTIL                             
                   PS-EOF-MSTR-FILE.                                            
      *                                                                         
           DISPLAY '                                                '.          
           DISPLAY '** SUKUP001-TOTAL NO OF RECORDS READ          : '           
                   PV-REC-READ-COUNT                                            
           DISPLAY '** SUKUP001-TOTAL NO OF MASTER RECS DELETED   : '           
                   PV-MSTR-DATA-CNT                                             
           DISPLAY '** SUKUP001-TOTAL NO OF MASTERS NOT DEL(-532) : '           
                   PV-MSTR-CONT-CNT                                             
           DISPLAY '** SUKUP001-TOTAL NO OF CHILD  RECS DELETED   : '           
                   PV-CHILD-DATA-CNT                                            
           DISPLAY '** SUKUP001-NUMBER OF COMMITS TAKEN           : '           
                   PV-NUMBER-OF-COMMITS                                         
           DISPLAY '                                                '.          
           DISPLAY '** PROGRAM SUKUP001 EXECUTION ENDED   **        '.          
      *                                                                         
           CLOSE MSTR-CRTN-PURGE-FILE.                                          
      *                                                                         
           GOBACK.                                                              
      *                                                                         
      ******************************************************************        
      ** PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.              **        
      ******************************************************************        
      *                                                                         
       1000-INITIALIZATION.                                                     
      *                                                                         
           MOVE WHEN-COMPILED           TO  PV-DATE-COMPILED                    
           DISPLAY '** PROGRAM SUKUP001 EXECUTION STARTED **'.                  
           DISPLAY '** PROGRAM SUKUP001 LAST COMPILED ON  **'                   
           PV-DATE-COMPILED(1:8) ' - ' PV-DATE-COMPILED(9:8)                    
      *                                                                         
           MOVE ZEROES                   TO PV-COMMIT-COUNTER                   
                                            PV-NUMBER-OF-COMMITS                
           OPEN INPUT  MSTR-CRTN-PURGE-FILE.                                    
      *                                                                         
           SET  PS-NOT-EOF-MSTR-FILE     TO TRUE.                               
           PERFORM 8000-READ-MSTR-DATA-FILE.                                    
           IF PS-EOF-MSTR-FILE                                                  
              DISPLAY '                                       '                 
              DISPLAY '** SUKUP001-MASTER CONTAINER FILE IS EMPTY'              
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      **  THIS PARAGRAPH READS THE RECORDS FROM PO ORD NBR PURGE CSR  **        
      **  MSTR-DATA-CSR  AND WRITE A RECORD TO THE OUTPUT FILE.       **        
      ******************************************************************        
      *                                                                         
       2000-PROCESS-MSTR-EXT-DATA.                                              
      *                                                                         
            IF PV-COMMIT-COUNTER = 100                                          
               PERFORM 8200-COMMIT-DB2                                          
               DISPLAY 'COMMIT:'                                                
                       '   RECORDS READ-' PV-REC-READ-COUNT                     
                       '   RECORDS DELETED-' PV-MSTR-DATA-CNT                   
               MOVE ZEROES      TO  PV-COMMIT-COUNTER                           
            END-IF.                                                             
            INITIALIZE PV-SQLERRD3                                              
            PERFORM 8050-DELETE-CHILD-DATA                                      
            PERFORM 8200-COMMIT-DB2                                             
            PERFORM 8100-DELETE-MSTR-DATA                                       
      /     PERFORM 8200-COMMIT-DB2                                             
            PERFORM 8000-READ-MSTR-DATA-FILE.                                   
      *                                                                         
      ******************************************************************        
      ** THIS PARAGRAPH WILL SELECT THE DATE RANGE TO FETCH THE       **        
      ** RECORDS FROM THE RECEIVER TABLES (TRECEIV AND TRECITM)       **        
      ******************************************************************        
      *                                                                         
       8000-READ-MSTR-DATA-FILE.                                                
      *                                                                         
           MOVE '8000-READ-MSTR-DATA-FILE' TO  PV-PARAGRAPH-NAME.               
      *                                                                         
           READ MSTR-CRTN-PURGE-FILE                                            
               INTO                                                             
                   SU074-MSR-CTN-DATA-RECORD                                    
               AT END                                                           
                   SET PS-EOF-MSTR-FILE      TO TRUE                            
               NOT AT END                                                       
                   MOVE SU074-MST-CNTR-ID    TO MST-CNTR-ID                     
                   ADD 1                     TO PV-REC-READ-COUNT               
           END-READ.                                                            
      *                                                                         
      ******************************************************************        
      **  THIS PARAGRAPH OPENS THE CURSOR WHICH WILL CONTAIN THE NO   **        
      **  OF PO ORD THAT CAN BEOLD ENOUGH AND CAN BE PURGED.          **        
      ******************************************************************        
      *                                                                         
       8050-DELETE-CHILD-DATA.                                                  
      *                                                                         
           EXEC SQL                                                             
                DELETE FROM SUT_MSTR_SHIPT_CNT                                  
                       WHERE  MSTR_SHIPT_UNT_ID = :MST-CNTR-ID                  
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                    MOVE SQLERRD(3) TO PV-SQLERRD3                              
                    ADD PV-SQLERRD3 TO PV-CHILD-DATA-CNT                        
               WHEN SQLCODE = +100                                              
                   MOVE MST-CNTR-ID         TO PV-MSTR-CART                     
      /            DISPLAY PV-DISP-KEY-MSG                                      
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                   CONTINUE                                                     
               WHEN SQLCODE < ZERO                                              
                   MOVE MST-CNTR-ID         TO PV-MSTR-CART                     
                   DISPLAY '**************************************'             
                   DISPLAY 'ERROR ISSUED'                                       
                   DISPLAY 'DELETE SUT_MSTR_SHIPT_CNT             '             
                   DISPLAY PV-DISP-KEY-MSG                                      
                   DISPLAY '**************************************'             
                   MOVE '8050-DELETE-CHILD-DATA'                                
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE 'DELETE FROM SUT_MSTR_SHIPT_CNT'                        
                                   TO PV-DB2-OPERATION                          
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      **  THIS PARAGRAPH OPENS THE CURSOR WHICH WILL CONTAIN THE NO   **        
      **  OF PO ORD THAT CAN BEOLD ENOUGH AND CAN BE PURGED.          **        
      ******************************************************************        
      *                                                                         
       8100-DELETE-MSTR-DATA.                                                   
      *                                                                         
           EXEC SQL                                                             
                DELETE FROM SUT_MSTR_SHIPT_UNT                                  
                       WHERE  MSTR_SHIPT_UNT_ID = :MST-CNTR-ID                  
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                    ADD +1 TO PV-MSTR-DATA-CNT                                  
                              PV-COMMIT-COUNTER                                 
               WHEN SQLCODE = +100                                              
                    MOVE MST-CNTR-ID TO PV-MSTR-CART                            
      /             DISPLAY PV-DISP-KEY-MSG                                     
               WHEN SQLCODE = -532                                              
                    ADD +1 TO PV-MSTR-CONT-CNT                                  
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                    CONTINUE                                                    
               WHEN SQLCODE < ZERO                                              
                   MOVE MST-CNTR-ID         TO PV-MSTR-CART                     
                   DISPLAY '**************************************'             
                   DISPLAY 'ERROR ISSUED'                                       
                   DISPLAY 'DELETE SUT_MSTR_SHIPT_UNT             '             
                   DISPLAY PV-DISP-KEY-MSG                                      
                   DISPLAY '**************************************'             
                   MOVE '8100-DELETE-MSTR-DATA'                                 
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE 'DELETE FROM SUT_MSTR_SHIPT_UNT'                        
                                   TO PV-DB2-OPERATION                          
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * DB2 COMMIT                                                              
      ******************************************************************        
       8200-COMMIT-DB2.                                                         
                                                                                
           EXEC SQL                                                             
              COMMIT                                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN ZERO                                                        
                   ADD +1 TO PV-NUMBER-OF-COMMITS                               
               WHEN OTHER                                                       
                   MOVE '8200-COMMIT-DB2'   TO  PV-PARAGRAPH-NAME               
                   MOVE 'SUKUP001 ABENDING ON COMMIT'                           
                                            TO  PV-DB2-OPERATION                
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE.                                                        
      ******************************************************************        
      *  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL   *        
      *  ERROR OR WARNING CODE OCCURS.                                 *        
      ******************************************************************        
      *                                                                         
       9000-SQL-ABEND.                                                          
      *                                                                         
           DISPLAY '9000-SQL-ABEND'.                                            
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.                       
      *                                                                         
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
      * FORMAT.                                                        *        
      ******************************************************************        
      *                                                                         
           COPY DPPD004.                                                        
      *                                                                         
           MOVE +4013         TO ABEND-CODE.                                    
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
      *                                                                         
