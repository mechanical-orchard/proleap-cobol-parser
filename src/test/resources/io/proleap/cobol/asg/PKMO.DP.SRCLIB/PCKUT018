000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.         PCKUT018.                                            
000400 AUTHOR.             DENNIS STEFFEN.                                      
000500 INSTALLATION.       KOHLS DEPARTMENT STORES.                             
000600 DATE-WRITTEN.       AUTUMN 2001.                                         
000700 DATE-COMPILED.                                                           
004000************************************************************              
004000* THIS PROGRAM PREPS THE INTRANSIT FILE SO THAT IT CAN USE                
004000* CHECKPOINT/RESTART PROCESSING FUNCTIONS WHEN UPDATING THE               
004000* INTRANSIT TABLES IN PROGRAM PCKUP005.                                   
004000************************************************************              
003900 ENVIRONMENT DIVISION.                                                    
004100 CONFIGURATION SECTION.                                                   
004200                                                                          
004300 SOURCE-COMPUTER.    IBM-3090.                                            
004400 OBJECT-COMPUTER.    IBM-3090.                                            
004500                                                                          
004600 INPUT-OUTPUT SECTION.                                                    
004800 FILE-CONTROL.                                                            
004900                                                                          
005000     SELECT IN-TRANSIT-FILE              ASSIGN TO INP01.                 
005100                                                                          
005300 DATA DIVISION.                                                           
005500 FILE SECTION.                                                            
005600                                                                          
005700 FD  IN-TRANSIT-FILE                                                      
005800     LABEL RECORDS ARE STANDARD                                           
005900     RECORDING MODE IS F                                                  
006000     BLOCK CONTAINS 0 RECORDS                                             
006100     RECORD CONTAINS 100 CHARACTERS                                       
006200     DATA RECORD IS IN-TRANSIT-RECORD.                                    
006300 01  IN-TRANSIT-RECORD    PIC  X(100).                                    
006400                                                                          
006700*                                                                         
006800 WORKING-STORAGE SECTION.                                                 
006900                                                                          
007000 01  ABEND-CODE                  PIC S9(04)     VALUE +0  COMP.           
007100                                                                          
007200 01  PROGRAM-CONSTANTS.                                                   
007300     05  PC-PROGRAM-NAME         PIC X(08)      VALUE 'PCKUT018'.         
007400     05  PC-TRAN-PREP-FUNC-CODES.                                         
007500         10  PC-TRAN-PREP-FUNC-OPEN                                       
007600                                 PIC X(05)      VALUE 'OPEN '.            
007700         10  PC-TRAN-PREP-FUNC-WRITE                                      
007800                                 PIC X(05)      VALUE 'WRITE'.            
007900         10  PC-TRAN-PREP-FUNC-CLOSE                                      
008000                                 PIC X(05)      VALUE 'CLOSE'.            
008100     05  PC-TRANS-RECORD-LENGTH  PIC S9(04)     VALUE +100 COMP.          
008200     05  PC-JOB-NAME             PIC X(08)      VALUE 'PCK026  '.         
008300     05  PC-PLAN-NAME            PIC X(08)      VALUE 'PCKUP005'.         
008500                                                                          
008600 01  PROGRAM-VARIABLES.                                                   
008700     05  PV-CHECKPOINT-TYPE      PIC X(01)      VALUE SPACE.              
008800         88  CONTROL-BREAK                      VALUE 'C'.                
008900         88  LOGICAL-TRANSACTION                VALUE 'L'.                
009000         88  TIME-INTERVAL                      VALUE 'T'.                
009100         88  REQUESTED-BY-PROGRAM               VALUE 'R'.                
009200     05  PS-TRANS-EOF-SW     PIC X(01)          VALUE 'N'.                
009300         88  PS-TRANS-EOF                       VALUE 'Y'.                
009600*    05  PV-SKIP-CLOSE-SW        PIC X(01)      VALUE 'N'.                
009700*        88  SKIP-CLOSE                         VALUE 'Y'.                
009800     05  PV-CURRENT-DATE         PIC X(10).                               
C09900                                                                          
010100*     IN-TRANSIT RECORD                                                   
010300     COPY PCRD010.                                                        
010500*                                                                         
010600*     TRANSACTION PREPARATION MODULE LINKAGE SECTION.                     
010700*                                                                         
260107     COPY DPWS990.                                                        
010900                                                                          
010800     COPY DPLS000.                                                        
010900                                                                          
011300     EXEC SQL                                                             
011400         INCLUDE SQLCA                                                    
011500     END-EXEC.                                                            
011600     COPY SQLCA2.                                                         
011900                                                                          
011800 PROCEDURE DIVISION.                                                      
012100******************************************************************        
012200* 0000-MAINLINE-PROCESSING                                       *        
012300*      CONTROLS FLOW OF THE PROGRAM.                             *        
012400******************************************************************        
012600 0000-MAINLINE-PROCESSING.                                                
012700                                                                          
012710     PERFORM 1000-INITIALIZE.                                             
014800     PERFORM 2000-PROCESS UNTIL PS-TRANS-EOF                              
014900     PERFORM 9000-EOJ.                                                    
016900                                                                          
017000     GOBACK.                                                              
017100                                                                          
017110 1000-INITIALIZE.                                                         
017111                                                                          
017120     OPEN INPUT IN-TRANSIT-FILE.                                          
017130                                                                          
017140     ACCEPT PC-JOB-NAME FROM SYSIN.                                       
017150                                                                          
017160     EXEC SQL                                                             
017170          SET :PV-CURRENT-DATE = CURRENT DATE                             
017180     END-EXEC.                                                            
017193                                                                          
017194     PERFORM 8000-READ-ASN-TRANS-FILE.                                    
017195                                                                          
017196*    IF PS-TRANS-EOF                                                      
017197*       SET SKIP-CLOSE                TO TRUE                             
017198*       MOVE +1                       TO RETURN-CODE                      
017199*    ELSE                                                                 
017200        PERFORM 1100-ISSUE-OPEN-REQUEST.                                  
017201*    END-IF.                                                              
017202                                                                          
018000 1100-ISSUE-OPEN-REQUEST.                                                 
018100                                                                          
018200     MOVE PC-TRAN-PREP-FUNC-OPEN      TO DP000-FUNC-CODE.                 
018300     MOVE PC-JOB-NAME                 TO DP000-JOB-NAME.                  
018400     MOVE PC-PLAN-NAME                TO DP000-PLAN-NAME.                 
018600     PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
018800     MOVE DP000-CKPT-TYPE-CODE        TO PV-CHECKPOINT-TYPE.              
018801                                                                          
018810 2000-PROCESS.                                                            
018850     PERFORM 2100-MOVE-TRANS-DATA                                         
018860                                                                          
018870     PERFORM 8000-READ-ASN-TRANS-FILE.                                    
018895                                                                          
018896 2100-MOVE-TRANS-DATA.                                                    
018897                                                                          
018898*    IF RC001-PO-LINE-NBR-HDR                                             
018899*        MOVE 'Y' TO DP000-CHECKPOINT-IND                                 
018900*    ELSE                                                                 
018910*        MOVE ' ' TO DP000-CHECKPOINT-IND                                 
018920*    END-IF.                                                              
018930                                                                          
018940     MOVE LENGTH OF PC010-PCT-INTRANSIT-REC                               
018950                                      TO DP000-TRANS-REC-LENGTH.          
018960                                                                          
018970     MOVE PC010-PCT-INTRANSIT-REC     TO DP000-TRANS-REC.                 
018980                                                                          
018990     MOVE PC-TRAN-PREP-FUNC-WRITE     TO DP000-FUNC-CODE.                 
018991     MOVE PC-JOB-NAME                 TO DP000-JOB-NAME.                  
018992     MOVE PC-PLAN-NAME                TO DP000-PLAN-NAME.                 
018993                                                                          
018994     PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
019000                                                                          
020100 8000-READ-ASN-TRANS-FILE.                                                
020200                                                                          
020300     READ IN-TRANSIT-FILE INTO PC010-PCT-INTRANSIT-REC                    
020400         AT END                                                           
020500           SET PS-TRANS-EOF           TO TRUE                             
020510     END-READ.                                                            
020700                                                                          
025310 9000-EOJ.                                                                
025311     CLOSE IN-TRANSIT-FILE.                                               
025312                                                                          
025313*    IF SKIP-CLOSE                                                        
025314*       NEXT SENTENCE                                                     
025315*    ELSE                                                                 
025316        PERFORM 9100-ISSUE-CLOSE-REQUEST.                                 
025317*    END-IF.                                                              
020700                                                                          
025318 9100-ISSUE-CLOSE-REQUEST.                                                
025319                                                                          
025320     MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.                     
025321     MOVE PC-JOB-NAME TO DP000-JOB-NAME.                                  
025322     MOVE PC-PLAN-NAME TO DP000-PLAN-NAME.                                
025323                                                                          
025324     PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
025330                                                                          
025400     COPY DPPD000.                                                        
025500*                                                                         
025600*     TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES.             
025700*                                                                         
