000100*-----------------------*                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*-----------------------*                                                 
000400 PROGRAM-ID.    IQKCS406.                                                 
000500 AUTHOR.        GREG SHEFF.                                               
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000700 DATE-WRITTEN.  02-19-03.                                                 
000800 DATE-COMPILED.                                                           
000900                                                                          
001000*---------------------------------------------------------------*         
001100*    I406 - COLOR LIST                                          *         
001200*                                                               *         
001300*    YOU CAN GET TO THIS SCREEN FROM SEVERAL DIFFERENT ONES.    *         
001400*                                                               *         
001500*    F20 - IQLDEPT  - IQKCS404 - DEPARTMENT LIST                *         
001600*          FORMAT CODE IS SPACE - PC-DEPT-FORMAT-IND.           *         
001700*                                                               *         
001800*    F19 - IQLCLASS - IQKCS405 - MAJOR AND SUB CLASS LIST       *         
001900*          FORMAT CODE IS '1' - PC-CLASS-FORMAT-IND             *         
002000*                                                               *         
002100*    F20 - IQLCLASS - IQKCS405 - MAJOR AND SUB CLASS LIST       *         
002200*          FORMAT CODE IS SPACE - PC-DEPT-FORMAT-IND.           *         
002300*          THIS IS TREATED JUST LIKE F20 FROM IQLDEPT.          *         
002310*                                                               *         
002400*    F19 - IQLSTYLE - IQKCS409 - STYLE LIST                     *         
002500*          FORMAT CODE IS '2' - PC-STYLE-FORMAT-IND             *         
002600*                                                               *         
002700*                                                               *         
002800*    SCREEN CAN DISPLAY LISTS OF COLORS USING SEVERAL TYPES     *         
002900*    OF CRITERIA DEPENDING ON HOW THEY GET TO THIS SCREEN.      *         
003000*    THEY ARE AS FOLLOWS:                                       *         
003100*                                                               *         
003200*       VENDOR-CLASS-COLOR                                      *         
003300*       VENDOR/LABEL NAME (IQKCS402), BUSINESS GROUP (IQKCS403),*         
003400*           DEPARTMENT (IQKCS404), AND MAJOR CLASS / SUB CLASS  *         
003500*           (IQKCS405).                                         *         
003600*                                                               *         
003700*       NOVND-CLASS-COLOR (VENDOR SCREEN SKIPPED)               *         
003800*       BUSINESS GROUP (IQLBUSGP - IQKCS403), DEPARTMENT        *         
003900*           (IQLDEPT - IQLCS404), AND MAJOR CLASS / SUB CLASS   *         
004000*           (IQLCLASS - IQLCS405).                              *         
004100*                                                               *         
004200*       VENDOR-NOCLS-COLOR (CLASS SCREEN SKIPPED)               *         
004300*       VENDOR/LABEL NAME (IQKCS402), BUSINESS GROUP (IQKCS403),*         
004400*           AND DEPARTMENT (IQKCS404)                           *         
004500*                                                               *         
004600*       NOVND-NOCLS-COLOR (VENDOR AND CLASS SCREENS SKIPPED)    *         
004700*       BUSINESS GROUP (IQLBUSGP - IQKCS403), AND DEPARTMENT    *         
004800*           (IQLDEPT - IQLCS404).                               *         
004900*                                                               *         
005000*    DISPLAYS A LIST OF COLORS ASSOCIATED WITH A PARTICULAR     *         
005100*    VENDOR LABEL/VENDOR NAME, BUSINESS GROUP, DEPARTMENT AND   *         
005200*    POSSIBLY 'MAJOR AND SUB CLASS' THAT WAS PREVIOUSLY         *         
005300*    SELECTED.                                                  *         
005400*                                                               *         
005500*    ALLOWS THE USER TO SELECT ONE COLOR AND PASS IT, ALONG     *         
005600*    WITH THE VENDOR NAME (IF SUPPLIED), BUSINESS GROUP,        *         
005610*    DEPARTMENT, AND POSSIBLY "MAJOR AND SUB CLASS" TO          *         
005700*    ONE OF THE FOLLOWING PROGRAMS.                             *         
005800*                                                               *         
005801*        F17 - IQLSKU   - IQKCS408 - FORMAT CODE IS '2'         *         
005802*        F19 - IQLSIZE  - IQLCS407 - FORMAT CODE IS SPACE.      *         
005810*                                                               *         
005900*---------------------------------------------------------------*         
005910*---------------------------------------------------------------*         
005920* DATE:        09/14/2021                                                 
005930* WR/IR#:      CHG0261678                                                 
005940* PROGRAMMER:  GERMAN BANDA                                               
005950* DESCRIPTION: CHANGE CALL OF DPKUT100 TO                                 
005960*              CALL DP010I-NUMERIC-EDIT-ROUTINE                           
005970*              MAKING THE CALL DYNAMIC VS STATIC                          
005980*---------------------------------------------------------------*         
CICS  * CHANGED 11/01/2022    BY JIM HUNTER      WR: CHG0277565                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  ******************************************************************        
006000                                                                          
006100 ENVIRONMENT DIVISION.                                                    
006200                                                                          
006300 DATA DIVISION.                                                           
006400                                                                          
006500 WORKING-STORAGE SECTION.                                                 
006600 01  DK-DB2-KEYS.                                                         
006700     15  DK-DEPT-NBR                    PIC S9(04)  COMP-3                
006800                                                    VALUE ZERO.           
006900     15  DK-MAJ-CL                      PIC S9(04)  COMP-3                
007000                                                    VALUE ZERO.           
007100     15  DK-SUB-CL                      PIC S9(04)  COMP-3                
007200                                                    VALUE ZERO.           
007300     15  DK-VS-NBR                      PIC  X(20)  VALUE SPACES.         
007400     15  DK-LBL-NM                      PIC  X(30)  VALUE SPACES.         
007500 01  PC-PROGRAM-CONSTANTS.                                                
007600     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
007700                                                    'IQ406A  '.           
007800     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
007900                                                    'IQKM406 '.           
008000     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
008100                                                    'IQKCS406'.           
008200     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
008300     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
008400*    CONTROL PASSED FROM IQKCS404 (DEPT)                                  
008500     05  PC-DEPT-FORMAT-IND             PIC  X(01)  VALUE ' '.            
008600*    CONTROL PASSED FROM IQKCS405 (CLASS)                                 
008700     05  PC-CLASS-FORMAT-IND            PIC  X(01)  VALUE '1'.            
008800*    CONTROL PASSED FROM IQKCS409 (STYLE)                                 
008900     05  PC-STYLE-FORMAT-IND            PIC  X(01)  VALUE '2'.            
009000     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +10             
009100                                                    COMP SYNC.            
009200     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
009300                                                    COMP SYNC.            
009400     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
009500                                                    +1000000              
009600                                                    COMP-3.               
009700     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
009800                                                    COMP SYNC.            
009900     05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
010000                                                    COMP-3.               
010100*                                                                         
010200     05  PC-TSYMSG-NUMBERS.                                               
010300         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
010400         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
010500         10  PC-TSYMSG-00047            PIC  9(05)  VALUE 00047.          
010600         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
010700         10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
010800         10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
010900         10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
011000         10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
011100         10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
011200         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
011300         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
011400         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
011500         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
011600         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
011700*                                                                         
011800 01  PS-PROGRAM-SWITCHES.                                                 
011900     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
012000         88  PS-NO-ERROR                            VALUE 'Y'.            
012100         88  PS-ERROR                               VALUE 'N'.            
012200     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
012300         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
012400         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
012500     05  PS-DATA-CURSOR-SW              PIC  X(01)  VALUE ' '.            
012600         88  PS-CSR-VENDOR-CLASS-COLOR              VALUE 'A'.            
012700         88  PS-CSR-NOVND-CLASS-COLOR               VALUE 'B'.            
012800         88  PS-CSR-VENDOR-NOCLS-COLOR              VALUE 'C'.            
012900         88  PS-CSR-NOVND-NOCLS-COLOR               VALUE 'D'.            
013000         88  PS-CSR-VENDOR-STYLE-COLOR              VALUE 'E'.            
013100         88  PS-CSR-NOVND-STYLE-COLOR               VALUE 'F'.            
013200     05  PS-COUNT-CURSOR-SW             PIC  X(01)  VALUE ' '.            
013300         88  PS-CNT-VENDOR-DEPT-CLASS               VALUE 'A'.            
013400         88  PS-CNT-VENDOR-DEPT-NOCLASS             VALUE 'B'.            
013500         88  PS-CNT-VENDOR-DEPT-STYLE               VALUE 'C'.            
013600         88  PS-CNT-LABEL-DEPT-CLASS                VALUE 'D'.            
013700         88  PS-CNT-LABEL-DEPT-NOCLASS              VALUE 'E'.            
013800         88  PS-CNT-LABEL-DEPT-STYLE                VALUE 'F'.            
013900         88  PS-CNT-DEPT-CLASS                      VALUE 'G'.            
014000         88  PS-CNT-DEPT-NOCLASS                    VALUE 'H'.            
014100         88  PS-CNT-DEPT-STYLE                      VALUE 'I'.            
014200                                                                          
014300 01  PV-PROGRAM-VARIABLES.                                                
014400     05  PV-CURSOR-NAME                 PIC X(18)   VALUE SPACES.         
014500     05  PV-SUB                         PIC S9(04)  VALUE +0              
014600                                                    COMP SYNC.            
014700     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
014800                                                    COMP SYNC.            
014900     05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
015000                                                    COMP-3.               
015100     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
015200                                                    COMP-3.               
015300     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
015400                                                    COMP-3.               
015500     05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
015600                                                    COMP-3.               
015700     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
015800                                                    COMP-3.               
015900     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
016000                                                    COMP-3.               
016100     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
016200                                                    COMP SYNC.            
016300     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
016400                                                    COMP SYNC.            
016500     05  PV-ITEM                        PIC S9(04)  VALUE +0              
016600                                                    COMP SYNC.            
016700     05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
016800                                                    COMP SYNC.            
016900     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
017000                                                    COMP SYNC.            
017100     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
017200                                                    COMP SYNC.            
017300     05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
017400                                                    COMP SYNC.            
017500     05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
017600                                                    COMP SYNC.            
017700     05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
017800                                                    COMP SYNC.            
017900     05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
018000                                                    COMP SYNC.            
018100     05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
018200                                                    COMP SYNC.            
018300     05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
018400                                                    COMP SYNC.            
018500     05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
018600                                                    COMP SYNC.            
018700     05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
018800                                                    COMP SYNC.            
018900     05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
019000                                                    COMP SYNC.            
019100     05  PV-SKU-CNT                     PIC S9(09)  VALUE +0              
019200                                                    COMP-3.               
019300******************************************************************        
019400**  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
019500**  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **        
019600**  APPLICATION-SPECIFIC COMMAREA.                              **        
019700******************************************************************        
019800 01  LQW-LIST-QUEUE-WORK-AREA.                                            
019900     05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
020000     05  LQW-ITEM-ENTRIES               OCCURS 10 TIMES                   
020100                                        INDEXED BY LQW-IDX.               
020200         10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
020300         10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
020400         10  LQW-SELECTED-SW            PIC  X(01).                       
020500             88  LQW-SELECT                         VALUE 'S'.            
020600             88  LQW-DESELECT                       VALUE ' '.            
020700         10  LQW-CLOR-DESC              PIC  X(10).                       
020800                                                                          
020900     EJECT                                                                
021000*---------------------------------------------------------------*         
021100*  RECORD LAYOUT FOR THE SELECTION TEMP STORAGE QUEUE.                    
021200*---------------------------------------------------------------*         
021300     COPY IQWS014.                                                        
021400                                                                          
021500     EJECT                                                                
021600*---------------------------------------------------------------*         
021700*    MAP LAYOUT                                                 *         
021800*---------------------------------------------------------------*         
021900                                                                          
022000     COPY IQKM406.                                                        
022100*                                                                         
022200 01  MR-OCCURS-LAYOUT                   REDEFINES  IQ406AO.               
022300     05  FILLER                         PIC X(204).                       
022400     05  MR-SCROLL-AREA                 OCCURS 10 TIMES.                  
022500         10  MR-SELECTL                 PIC S9(04) COMP.                  
022600         10  MR-SELECTA                 PIC  X(01).                       
022700         10  MR-SELECTC                 PIC  X(01).                       
022800         10  MR-SELECTH                 PIC  X(01).                       
022900         10  MR-SELECTI                 PIC  X(01).                       
023000                                                                          
023100         10  MR-CLOR-DESCL              PIC S9(04) COMP.                  
023200         10  MR-CLOR-DESCA              PIC  X(01).                       
023300         10  MR-CLOR-DESCC              PIC  X(01).                       
023400         10  MR-CLOR-DESCH              PIC  X(01).                       
023500         10  MR-CLOR-DESC               PIC  X(10).                       
023600     EJECT                                                                
023700*---------------------------------------------------------------*         
023800*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
023900*---------------------------------------------------------------*         
024000                                                                          
024100     COPY DPWS015.                                                        
024200                                                                          
024300*---------------------------------------------------------------*         
024400*    FUNCTION KEYS COPYBOOK.                                    *         
024500*---------------------------------------------------------------*         
024600                                                                          
024700     COPY DPWS016.                                                        
024800                                                                          
024900*---------------------------------------------------------------*         
025000*    NUMERIC EDIT LAYOUT.                                       *         
025100*---------------------------------------------------------------*         
025200                                                                          
025300     COPY DPWS010I.                                                       
025400 EJECT                                                                    
025500*---------------------------------------------------------------*         
025600*    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
025700*---------------------------------------------------------------*         
025800                                                                          
025900     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
026000 EJECT                                                                    
026100*---------------------------------------------------------------*         
026200*    STANDARD COMMAREA.                                         *         
026300*---------------------------------------------------------------*         
026400                                                                          
026500     COPY DPWS020.                                                        
026600     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
026700*---------------------------------------------------------------*         
026800* INTER-APPLICATION COMMAREA.                                   *         
026900*   IQWS016 - IS THE INPUT LAYOUT WHEN COMING FROM THE PREVIOUS *         
027000*             PROGARM.                                          *         
027100*   IQWS016 - IS THE OUTPUT LAYOUT THAT WILL BE PASSED FROM     *         
027200*             THIS LIST PROGRAM.                                *         
027300*---------------------------------------------------------------*         
027400                                                                          
027500         10  INTER-APPL-COMM-AREA       PIC X(200).                       
027600     COPY IQWS016.                                                        
027700*---------------------------------------------------------------*         
027800*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
027900*        CURRENT LENGTH IS  600 FOR IQLCOLOR MNEMONIC.                    
028000*        YOU MUST CHECK THE COMMAREA LENGTH FOR THE MNEMONIC              
028100*            IF YOU ADD ANY MORE FIELDS.                                  
028200*---------------------------------------------------------------*         
028300         10  ASC-SPECIFIC-COMMAREA.                                       
028400             15  ASC-LIST-KEYS.                                           
028500                 20  ASC-LBL-NM                      PIC  X(30).          
028600                 20  ASC-GP-NBR                      PIC  9(03).          
028700                 20  ASC-GP-DESC                     PIC  X(25).          
028800                 20  ASC-DEPT-NBR                    PIC  9(04).          
028900                 20  ASC-DEPT-DESC                   PIC  X(20).          
029000                 20  ASC-MAJ-CL                      PIC  9(04).          
029100                 20  ASC-SUB-CL                      PIC  9(04).          
029200                 20  ASC-SUB-CL-NM                   PIC  X(20).          
029300                 20  ASC-SKU-CNT                     PIC  9(09).          
029400                 20  ASC-VS-NBR                      PIC  X(20).          
029500             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
029600                 20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
029700                     88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
029800                     88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
029900                                                                          
030000                 20  ASC-ITEM-ARRAY.                                      
030100                     25  ASC-ITEM-ENTRIES OCCURS 10 TIMES.                
030200                         30  ASC-LIST-ITEM-NUMBER                         
030300                                                     PIC S9(09)           
030400                                                     COMP-3.              
030500                         30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
030600                             88  ASC-VALID-SELECT    VALUE ' '            
030700                                                           'S'            
030800                                                           'B'            
030900                                                           'E'.           
031000                             88  ASC-TEMP-NO-SELECT  VALUE ' '.           
031100                             88  ASC-TEMP-SELECT     VALUE 'S'.           
031200                             88  ASC-TEMP-BEGIN      VALUE 'B'.           
031300                             88  ASC-TEMP-END        VALUE 'E'.           
031400                         30  ASC-SELECTED-SW         PIC  X(01).          
031500                             88  ASC-NO-SELECT       VALUE ' '.           
031600                             88  ASC-SELECT          VALUE 'S'.           
031700                             88  ASC-BEGIN           VALUE 'B'.           
031800                             88  ASC-END             VALUE 'E'.           
031900                         30  ASC-CLOR-DESC           PIC  X(10).          
032000                                                                          
032100             15  ASC-BLK-SUB                         PIC S9(04)           
032200                                                          COMP            
032300                                                          SYNC.           
032400             15  ASC-ITEM-SUB                        PIC S9(04)           
032500                                                          COMP            
032600                                                          SYNC.           
032700             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
032800                                                          COMP-3.         
032900             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
033000                                                          COMP-3.         
033100             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
033200                                                          COMP-3.         
033300             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
033400                                                          COMP-3.         
033500             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
033600                                                          COMP-3.         
033700             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
033800                                                          COMP            
033900                                                          SYNC.           
034000             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
034100                                                          COMP-3.         
034200             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
034300                                                          COMP-3.         
034400             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
034500                 88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
034600                 88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
034700             15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
034800                 88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
034900                 88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
035000             15  ASC-LIST-QUEUE-NAME.                                     
035100                 20  ASC-LIST-TERM-ID                PIC  X(04).          
035200                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
035300                                                          COMP-3.         
035400                 20  ASC-LIST-SEQ                    PIC  9(01).          
035500             15  ASC-SEL-QUEUE-NAME.                                      
035600                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
035700                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
035800                                                          COMP-3.         
035900                 20  ASC-SEL-SEQ                     PIC  9(01).          
036000             15  ASC-START-ITEM-REQUEST.                                  
036100                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
036200             15  ASC-FILLER                          PIC  X(58).          
036300         10  FILLER                                  PIC  X(2265).        
036400     EJECT                                                                
036500*---------------------------------------------------------------*         
036600*    ABEND PROCESSING COPYBOOK.                                 *         
036700*---------------------------------------------------------------*         
036800                                                                          
036900     COPY DPWS013.                                                        
037000                                                                          
037100 EJECT                                                                    
037200*---------------------------------------------------------------*         
037300*    DB2 AREA FOR XST_SKU                                                 
037400*---------------------------------------------------------------*         
037500     EXEC SQL                                                             
037600          INCLUDE XST00010                                                
037700     END-EXEC.                                                            
037800*---------------------------------------------------------------*         
037900*    DB2 AREA FOR XST_VND_STYL                                  *         
038000*---------------------------------------------------------------*         
038100     EXEC SQL                                                             
038200          INCLUDE XST00013                                                
038300     END-EXEC.                                                            
038400*---------------------------------------------------------------*         
038500*    DB2 AREA FOR XST_VND                                       *         
038600*---------------------------------------------------------------*         
038700     EXEC SQL                                                             
038800          INCLUDE XST00018                                                
038900     END-EXEC.                                                            
039000*---------------------------------------------------------------*         
039100*    DB2 AREA FOR COMMUNICATIONS                                *         
039200*---------------------------------------------------------------*         
039300     EXEC SQL                                                             
039400          INCLUDE SQLCA                                                   
039500     END-EXEC.                                                            
039600*---------------------------------------------------------------*         
039700*    FETCH COLOR                                                          
039800*---------------------------------------------------------------*         
039900                                                                          
040000     EXEC SQL                                                             
040100          DECLARE VENDOR-CLASS-COLOR CURSOR FOR                           
040200            SELECT  DISTINCT                                              
040300                      F.NRF_CLOR_DESC                                     
040400             FROM  XST_VND_STYL  D,                                       
040500                   XST_VND       E,                                       
040600                   XST_SKU       F                                        
040700              WHERE  D.DEPT_NBR     = :DK-DEPT-NBR                        
040800                AND  E.ENT_NM_DESC  = :DK-LBL-NM                          
040900                AND  D.MAJ_CL_NBR   = :DK-MAJ-CL                          
041000                AND  D.SUB_CL_NBR   = :DK-SUB-CL                          
041100                AND  D.STYL_ID      = F.STYL_ID                           
041200                AND  D.ENT_ID       = E.ENT_ID                            
041300            UNION                                                         
041400            SELECT  DISTINCT                                              
041500                      F.NRF_CLOR_DESC                                     
041600             FROM  XST_VND_STYL  D,                                       
041700                   XST_SKU       F                                        
041800              WHERE  D.DEPT_NBR   = :DK-DEPT-NBR                          
041900                AND  D.LBL_NM     = :DK-LBL-NM                            
042000                AND  D.MAJ_CL_NBR = :DK-MAJ-CL                            
042100                AND  D.SUB_CL_NBR = :DK-SUB-CL                            
042200                AND  D.STYL_ID    =  F.STYL_ID                            
042300                 ORDER BY 1                                               
042400     END-EXEC.                                                            
042500                                                                          
042600     EXEC SQL                                                             
042700          DECLARE NOVND-CLASS-COLOR CURSOR FOR                            
042800            SELECT  DISTINCT                                              
042900                      F.NRF_CLOR_DESC                                     
043000             FROM  XST_VND_STYL  D,                                       
043100                   XST_SKU       F                                        
043200              WHERE  D.DEPT_NBR     = :DK-DEPT-NBR                        
043300                AND  D.MAJ_CL_NBR   = :DK-MAJ-CL                          
043400                AND  D.SUB_CL_NBR   = :DK-SUB-CL                          
043500                AND  D.STYL_ID      =  F.STYL_ID                          
043600     END-EXEC.                                                            
043700                                                                          
043800*---------------------------------------------------------------*         
043900*    FETCH COLOR (SKIPPED CLASS SCREEN)                                   
044000*---------------------------------------------------------------*         
044100                                                                          
044200     EXEC SQL                                                             
044300          DECLARE VENDOR-NOCLS-COLOR CURSOR FOR                           
044400            SELECT  DISTINCT                                              
044500                      F.NRF_CLOR_DESC                                     
044600             FROM  XST_VND_STYL  D,                                       
044700                   XST_VND       E,                                       
044800                   XST_SKU       F                                        
044900              WHERE  D.DEPT_NBR     = :DK-DEPT-NBR                        
045000                AND  E.ENT_NM_DESC  = :DK-LBL-NM                          
045100                AND  D.STYL_ID      = F.STYL_ID                           
045200                AND  D.ENT_ID       = E.ENT_ID                            
045300            UNION                                                         
045400            SELECT  DISTINCT                                              
045500                      F.NRF_CLOR_DESC                                     
045600             FROM  XST_VND_STYL  D,                                       
045700                   XST_SKU       F                                        
045800              WHERE  D.DEPT_NBR   = :DK-DEPT-NBR                          
045900                AND  D.LBL_NM     = :DK-LBL-NM                            
046000                AND  D.STYL_ID    =  F.STYL_ID                            
046100                 ORDER BY 1                                               
046200     END-EXEC.                                                            
046300                                                                          
046400     EXEC SQL                                                             
046500          DECLARE NOVND-NOCLS-COLOR CURSOR FOR                            
046600            SELECT  DISTINCT                                              
046700                      F.NRF_CLOR_DESC                                     
046800             FROM  XST_VND_STYL  D,                                       
046900                   XST_SKU       F                                        
047000              WHERE  D.DEPT_NBR     = :DK-DEPT-NBR                        
047100                AND  D.STYL_ID      =  F.STYL_ID                          
047200     END-EXEC.                                                            
047300                                                                          
047400*---------------------------------------------------------------*         
047500*    FETCH COLOR (WENT THROUGH STYLE SCREEN)                              
047600*---------------------------------------------------------------*         
047700                                                                          
047800     EXEC SQL                                                             
047900          DECLARE VENDOR-STYLE-COLOR CURSOR FOR                           
048000            SELECT  DISTINCT                                              
048100                      F.NRF_CLOR_DESC                                     
048200             FROM  XST_VND_STYL  D,                                       
048300                   XST_VND       E,                                       
048400                   XST_SKU       F                                        
048500              WHERE  D.DEPT_NBR     = :DK-DEPT-NBR                        
048600                AND  D.VS_NBR       = :DK-VS-NBR                          
048700                AND  E.ENT_NM_DESC  = :DK-LBL-NM                          
048800                AND  D.STYL_ID      =  F.STYL_ID                          
048900                AND  D.ENT_ID       =  E.ENT_ID                           
049000            UNION                                                         
049100            SELECT  DISTINCT                                              
049200                      F.NRF_CLOR_DESC                                     
049300             FROM  XST_VND_STYL  D,                                       
049400                   XST_SKU       F                                        
049500              WHERE  D.DEPT_NBR   = :DK-DEPT-NBR                          
049600                AND  D.VS_NBR     = :DK-VS-NBR                            
049700                AND  D.LBL_NM     = :DK-LBL-NM                            
049800                AND  D.STYL_ID    =  F.STYL_ID                            
049900                 ORDER BY 1                                               
050000     END-EXEC.                                                            
050100                                                                          
050200     EXEC SQL                                                             
050300          DECLARE NOVND-STYLE-COLOR CURSOR FOR                            
050400            SELECT  DISTINCT                                              
050500                      F.NRF_CLOR_DESC                                     
050600             FROM  XST_VND_STYL  D,                                       
050700                   XST_SKU       F                                        
050800              WHERE  D.DEPT_NBR     = :DK-DEPT-NBR                        
050900                AND  D.VS_NBR       = :DK-VS-NBR                          
051000                AND  D.STYL_ID      =  F.STYL_ID                          
051100     END-EXEC.                                                            
051200                                                                          
051300*---------------------------------------------------------------*         
051400*    COUNT ROWS                                                           
051500*---------------------------------------------------------------*         
051600                                                                          
051700*    COUNT NEXT AVAILABLE SKU'S (VENDOR NAME)                             
051800     EXEC SQL                                                             
051900          DECLARE CNT-VND-DEPT-CLS CURSOR FOR                             
052000          SELECT  COUNT(*)                                                
052100           FROM  XST_VND_STYL  D,                                         
052200                 XST_VND       E,                                         
052300                 XST_SKU       F                                          
052400            WHERE  D.DEPT_NBR     = :DK-DEPT-NBR                          
052500              AND  E.ENT_NM_DESC  = :DK-LBL-NM                            
052600              AND  D.MAJ_CL_NBR   = :DK-MAJ-CL                            
052700              AND  D.SUB_CL_NBR   = :DK-SUB-CL                            
052800              AND  D.STYL_ID      =  F.STYL_ID                            
052900              AND  D.ENT_ID       = E.ENT_ID                              
053000     END-EXEC.                                                            
053100                                                                          
053200*    COUNT NEXT AVAILABLE SKU'S (VENDOR LABEL)                            
053300     EXEC SQL                                                             
053400          DECLARE CNT-LBL-DEPT-CLS CURSOR FOR                             
053500          SELECT  COUNT(*)                                                
053600           FROM  XST_VND_STYL  D,                                         
053700                 XST_SKU       F                                          
053800            WHERE  D.DEPT_NBR   = :DK-DEPT-NBR                            
053900              AND  D.LBL_NM     = :DK-LBL-NM                              
054000              AND  D.MAJ_CL_NBR = :DK-MAJ-CL                              
054100              AND  D.SUB_CL_NBR = :DK-SUB-CL                              
054200              AND  D.STYL_ID    =  F.STYL_ID                              
054300     END-EXEC.                                                            
054400                                                                          
054500*    COUNT NEXT AVAILABLE SKU'S (VENDOR NAME, MISSING CLASS)              
054600     EXEC SQL                                                             
054700          DECLARE CNT-VND-DEPT-NOCLS CURSOR FOR                           
054800          SELECT  COUNT(*)                                                
054900           FROM  XST_VND_STYL  D,                                         
055000                 XST_VND       E,                                         
055100                 XST_SKU       F                                          
055200            WHERE  D.DEPT_NBR      = :DK-DEPT-NBR                         
055300              AND  E.ENT_NM_DESC   = :DK-LBL-NM                           
055400              AND  D.STYL_ID       =  F.STYL_ID                           
055500              AND  D.ENT_ID        = E.ENT_ID                             
055600     END-EXEC.                                                            
055700                                                                          
055800*                                                                         
055900*    COUNT NEXT AVAILABLE SKU'S (VENDOR LABEL, MISSING CLASS)             
056000     EXEC SQL                                                             
056100          DECLARE CNT-LBL-DEPT-NOCLS CURSOR FOR                           
056200          SELECT  COUNT(*)                                                
056300           FROM  XST_VND_STYL  D,                                         
056400                 XST_SKU       F                                          
056500            WHERE  D.DEPT_NBR   = :DK-DEPT-NBR                            
056600              AND  D.LBL_NM     = :DK-LBL-NM                              
056700              AND  D.STYL_ID    =  F.STYL_ID                              
056800     END-EXEC.                                                            
056900                                                                          
057000***************************************************************           
057100*    COUNT NEXT AVAILABLE SKU'S (VENDOR NAME, STYLE)                      
057200***************************************************************           
057300     EXEC SQL                                                             
057400          DECLARE CNT-VND-DEPT-STYLE CURSOR FOR                           
057500          SELECT  COUNT(*)                                                
057600           FROM  XST_VND_STYL  D,                                         
057700                 XST_VND       E,                                         
057800                 XST_SKU       F                                          
057900            WHERE  D.DEPT_NBR      = :DK-DEPT-NBR                         
058000              AND  D.VS_NBR        = :DK-VS-NBR                           
058100              AND  E.ENT_NM_DESC   = :DK-LBL-NM                           
058200              AND  D.STYL_ID       =  F.STYL_ID                           
058300              AND  D.ENT_ID        = E.ENT_ID                             
058400     END-EXEC.                                                            
058500                                                                          
058600                                                                          
058700***************************************************************           
058800*    COUNT NEXT AVAILABLE SKU'S (VENDOR LABEL, STYLE)                     
058900***************************************************************           
059000     EXEC SQL                                                             
059100          DECLARE CNT-LBL-DEPT-STYLE CURSOR FOR                           
059200          SELECT  COUNT(*)                                                
059300           FROM  XST_VND_STYL  D,                                         
059400                 XST_SKU       F                                          
059500            WHERE  D.DEPT_NBR   = :DK-DEPT-NBR                            
059600              AND  D.VS_NBR     = :DK-VS-NBR                              
059700              AND  D.LBL_NM     = :DK-LBL-NM                              
059800              AND  D.STYL_ID    =  F.STYL_ID                              
059900     END-EXEC.                                                            
060000                                                                          
060100***************************************************************           
060200*    NO VENDOR OR LABEL NAME                                              
060300***************************************************************           
060400     EXEC SQL                                                             
060500          DECLARE CNT-DEPT-STYLE CURSOR FOR                               
060600          SELECT  COUNT(*)                                                
060700           FROM  XST_VND_STYL  D,                                         
060800                 XST_SKU       F                                          
060900            WHERE  D.DEPT_NBR   = :DK-DEPT-NBR                            
061000              AND  D.VS_NBR     = :DK-VS-NBR                              
061200              AND  D.STYL_ID    =  F.STYL_ID                              
061300     END-EXEC.                                                            
061400                                                                          
061500     EXEC SQL                                                             
061600          DECLARE CNT-DEPT-NOCLS CURSOR FOR                               
061700          SELECT  COUNT(*)                                                
061800           FROM  XST_VND_STYL  D,                                         
061900                 XST_SKU       F                                          
062000            WHERE  D.DEPT_NBR   = :DK-DEPT-NBR                            
062100              AND  D.STYL_ID    =  F.STYL_ID                              
062200     END-EXEC.                                                            
062300                                                                          
062400     EXEC SQL                                                             
062500          DECLARE CNT-DEPT-CLS CURSOR FOR                                 
062600          SELECT  COUNT(*)                                                
062700           FROM  XST_VND_STYL  D,                                         
062800                 XST_SKU       F                                          
062900            WHERE  D.DEPT_NBR   = :DK-DEPT-NBR                            
063000              AND  D.MAJ_CL_NBR = :DK-MAJ-CL                              
063100              AND  D.SUB_CL_NBR = :DK-SUB-CL                              
063200              AND  D.STYL_ID    =  F.STYL_ID                              
063300     END-EXEC.                                                            
063400                                                                          
063500 LINKAGE SECTION.                                                         
063600                                                                          
063700 01  DFHCOMMAREA.                                                         
063800     05  FILLER                         OCCURS  1 TO 4072 TIMES           
063900                                        DEPENDING ON EIBCALEN.            
064000         10  FILLER                     PIC X.                            
064100                                                                          
064200                                                                          
064300 PROCEDURE DIVISION.                                                      
064400******************************************************************        
064500**  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
064600**  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
064700**  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
064800******************************************************************        
064900 0000-MAIN-MODULE.                                                        
065000     INITIALIZE DP030-CICS-API-FIELDS.                                    
065100     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
065200     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
065300     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
065400     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
065500                                                                          
065600     MOVE LENGTH OF IQ406AI        TO DP030-MAP-LENGTH (1).               
065700     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
065800     PERFORM 0001-CALL-CICS-ARCH-API.                                     
065900*                                                                         
066000     PERFORM 1000-CONTROL-PROCESSING                                      
066100*                                                                         
066200     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
066300     PERFORM 0001-CALL-CICS-ARCH-API.                                     
066400                                                                          
066500******************************************************************        
066600**  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
066700**  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
066800******************************************************************        
066900 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
067100                                    DFHCOMMAREA                           
067200                                    DP030-CICS-API-FIELDS                 
067300                                    DP020-STANDARD-COMMAREA               
067400                                    IQ406AI.                              
067500*                                                                         
067600     IF  DP030-RC-CALL-SUCCESSFUL                                         
067700         CONTINUE                                                         
067800     ELSE                                                                 
067900         SET DP013-NO-ROLLBACK                                            
068000             DP013-XCTL-DISPLAY-RESTART                                   
068100             DP013-CICS-ABEND      TO TRUE                                
068200         MOVE '0001-CALL-CICS-ARCH-API'                                   
068300                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
068600         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
068700         PERFORM DP013-0000-PROCESS-ABEND                                 
068800     END-IF.                                                              
068900*----------------------------------------------------------------*        
069000* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
069100* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
069200*                                                                *        
069300* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
069400*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
069500*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
069600*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
069700*----------------------------------------------------------------*        
069800                                                                          
069900 1000-CONTROL-PROCESSING.                                                 
070000                                                                          
070100     EVALUATE TRUE                                                        
070200         WHEN DP020-NEXT-ACT-INITIAL                                      
070300             INITIALIZE ASC-SPECIFIC-COMMAREA                             
070400             MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
070500                                      ASC-SEL-TERM-ID                     
070600             MOVE DP020-SRC-TASK-NUMBER                                   
070700                                   TO ASC-LIST-TASK-NUMBER                
070800                                      ASC-SEL-TASK-NUMBER                 
070900             MOVE PC-LIST-QUEUE-SEQ                                       
071000                                   TO ASC-LIST-SEQ                        
071100             MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
071200             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
071300***-----$TEMPLATE NOTE$-------------------------------------------        
071400*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
071500*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
071600*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
071700***---------------------------------------------------------------        
071800             IF  PS-COMMAREA-NOT-VALID                                    
071900                 SET DP020-NEXT-ACT-APPL-ERROR                            
072000                                   TO TRUE                                
072100             ELSE                                                         
072200                 PERFORM 4000-BUILD-INITIAL-PANEL                         
072300             END-IF                                                       
072400                                                                          
072500         WHEN DP020-NEXT-ACT-READ-MAP                                     
072600             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
072700                                   TO PV-TOP-ITEM                         
072800             PERFORM 2000-PROCESS-PANEL                                   
072900                                                                          
073000         WHEN DP020-NEXT-ACT-RETURN                                       
073100             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
073200                                   TO PV-TOP-ITEM                         
073300             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
073400             PERFORM 6500-REINIT-ASC-FLDS                                 
073500             PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
073700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
073800                                                                          
073900         WHEN OTHER                                                       
074000             SET DP013-LOGIC-ABEND TO TRUE                                
074100             SET DP013-NO-ROLLBACK TO TRUE                                
074200             MOVE '1000-CONTROL-PROCESSING'                               
074300                                   TO DP013-PARAGRAPH                     
074400             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
074500                                   TO DP013-MESSAGE-TEXT(1)               
074600             PERFORM DP013-0000-PROCESS-ABEND                             
074700     END-EVALUATE.                                                        
074800                                                                          
074900                                                                          
075000*----------------------------------------------------------------*        
075100* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
075200* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
075300*----------------------------------------------------------------*        
075400                                                                          
075500 1100-PROCESS-INTER-APPL-COMM.                                            
075600     SET PS-COMMAREA-VALID         TO TRUE.                               
075700     MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
075800                                                                          
075900     EVALUATE TRUE                                                        
076000         WHEN DP020-INT-APPL-FORMAT-IND = PC-DEPT-FORMAT-IND              
076100*             COMING FROM IQLDEPT - DEPARTMENT LIST                       
076200              MOVE ZEROES                   TO IQ016-MAJ-CL               
076300              MOVE ZEROES                   TO IQ016-SUB-CL               
076400              MOVE 'CLASS SCREEN BYPASSD'   TO IQ016-SUB-CL-NM            
076500              PERFORM 1200-MOVE-DATA                                      
076600                                                                          
076700         WHEN DP020-INT-APPL-FORMAT-IND = PC-CLASS-FORMAT-IND             
076800*             COMING FROM IQLCLASS - CLASS LIST                           
076900              IF IQ016-MAJ-CL = 0 AND                                     
077000                 IQ016-SUB-CL = 0                                         
077100                 MOVE 'CLASS SCREEN BYPASSD' TO IQ016-SUB-CL-NM           
077200              END-IF                                                      
077300                                                                          
077400              PERFORM 1200-MOVE-DATA                                      
077500                                                                          
077600         WHEN DP020-INT-APPL-FORMAT-IND = PC-STYLE-FORMAT-IND             
077700*             COMING FROM IQLSTYLE - STYLE LIST                           
077800              MOVE ZEROES                   TO IQ016-MAJ-CL               
077900              MOVE ZEROES                   TO IQ016-SUB-CL               
078000              MOVE 'STYLE SCREEN USED   '   TO IQ016-SUB-CL-NM            
078100              PERFORM 1200-MOVE-DATA                                      
078200                                                                          
078300         WHEN OTHER                                                       
078400              IF DP020-NAV-KEY-DATA > SPACE                               
078500                 MOVE DP020-NAV-KEY-DATA TO                               
078600                                         ASC-LBL-NM                       
078700                                         DK-LBL-NM                        
078800                                         ASC-DEPT-NBR                     
078900                                         DK-DEPT-NBR                      
079000                                         ASC-MAJ-CL                       
079100                                         DK-MAJ-CL                        
079200                                         ASC-SUB-CL                       
079300                                         DK-SUB-CL                        
079400              END-IF                                                      
079500                                                                          
079600     END-EVALUATE.                                                        
079700                                                                          
079800*---------------------------------------------------------------*         
079900*                                                               *         
080000*---------------------------------------------------------------*         
080100                                                                          
080200 1200-MOVE-DATA.                                                          
080300                                                                          
080400     MOVE IQ016-LBL-NM       TO ASC-LBL-NM                                
080500                                DK-LBL-NM.                                
080600     MOVE IQ016-GP-NBR       TO ASC-GP-NBR.                               
080700     MOVE IQ016-GP-DESC      TO ASC-GP-DESC.                              
080800     MOVE IQ016-DEPT-NBR     TO ASC-DEPT-NBR                              
080900                                DK-DEPT-NBR.                              
081000     MOVE IQ016-DEPT-DESC    TO ASC-DEPT-DESC.                            
081100     MOVE IQ016-MAJ-CL       TO ASC-MAJ-CL                                
081200                                DK-MAJ-CL.                                
081300     MOVE IQ016-SUB-CL       TO ASC-SUB-CL                                
081400                                DK-SUB-CL.                                
081500     MOVE IQ016-VS-NBR       TO ASC-VS-NBR                                
081600                                DK-VS-NBR.                                
081700     MOVE IQ016-SUB-CL-NM    TO ASC-SUB-CL-NM.                            
081800                                                                          
081900*----------------------------------------------------------------*        
082000* DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
082100* ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
082200* USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
082300* OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
082400* FUNCTION KEYS CAN GET THIS FAR.                                *        
082500*----------------------------------------------------------------*        
082600                                                                          
082700 2000-PROCESS-PANEL.                                                      
082800     EVALUATE TRUE                                                        
082900         WHEN DP020-SRC-AID = DP016-CLEAR                                 
083000             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
083100             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
083200             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
083300                                                                          
083400         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
083500             MOVE ASC-LBL-NM   TO DK-LBL-NM                               
083600             MOVE ASC-DEPT-NBR TO DK-DEPT-NBR                             
083700             MOVE ASC-MAJ-CL   TO DK-MAJ-CL                               
083800             MOVE ASC-SUB-CL   TO DK-SUB-CL                               
083900             MOVE ASC-VS-NBR   TO DK-VS-NBR                               
084000                                                                          
084100             PERFORM 2050-DELETE-LIST-QUEUE                               
084200             PERFORM 4000-BUILD-INITIAL-PANEL                             
084300                                                                          
084400         WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
084500             PERFORM 2140-DESELECT-ALL-ITEMS                              
084600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
084700                                                                          
084800         WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
084900             PERFORM 2150-SELECT-ALL-ITEMS                                
085000             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
085100                                                                          
085200         WHEN OTHER                                                       
085300             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
085400             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
085500             IF  PS-NO-ERROR                                              
085600                 SET DP030-CONTINUE-GO                                    
085700                                 TO TRUE                                  
085800                 PERFORM 2100-CHECK-FUNCTION-KEY                          
085900             ELSE                                                         
086000                 SET DP030-OVERRIDE-GO                                    
086100                                 TO TRUE                                  
086200             END-IF                                                       
086300     END-EVALUATE.                                                        
086400 EJECT                                                                    
086500*----------------------------------------------------------------*        
086600*  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
086700*----------------------------------------------------------------*        
086800 2050-DELETE-LIST-QUEUE.                                                  
086900                                                                          
087000     EXEC CICS                                                            
087100         DELETEQ TS                                                       
087200             QUEUE (ASC-LIST-QUEUE-NAME)                                  
087300             RESP  (PV-CICS-RESP)                                         
087400     END-EXEC.                                                            
087500                                                                          
087600     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
087700        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
087800         CONTINUE                                                         
087900     ELSE                                                                 
088000         SET DP013-CICS-ABEND      TO TRUE                                
088100         SET DP013-NO-ROLLBACK     TO TRUE                                
088200                                                                          
088300         MOVE '2050-DELETE-LIST-QUEUE'                                    
088400                                   TO DP013-PARAGRAPH                     
088500         MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
088600                                   TO DP013-MESSAGE-TEXT (1)              
088700         PERFORM DP013-0000-PROCESS-ABEND                                 
088800     END-IF.                                                              
088900                                                                          
089000     MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
089100 EJECT                                                                    
089200*----------------------------------------------------------------*        
089300* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
089400* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
089500* CICS ARCHITECTURE API.                                         *        
089600*----------------------------------------------------------------*        
089700 2100-CHECK-FUNCTION-KEY.                                                 
089800     EVALUATE TRUE                                                        
089900*        LOCAL-01   SKU LIST   IQLSKU                                     
090000*        LOCAL-02   SIZE LST   IQLSIZE                                    
090100         WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
090200         WHEN DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID)                       
090300             IF  ASC-NUMBER-OF-SELECTED-ITEMS = ZERO                      
090400*                --  A LIST ITEM MUST BE SELECTED --                      
090500                 MOVE PC-TSYMSG-00057                                     
090600                                   TO DP020-MSG-NUMBER                    
090700                 SET DP020-MSG-FATAL                                      
090800                     DP030-OVERRIDE-GO                                    
090900                                   TO TRUE                                
091000             ELSE                                                         
091100               IF  ASC-NUMBER-OF-SELECTED-ITEMS > 1                       
091200*                --  ONLY ONE LIST ITEM MAY BE SELECTED --                
091300                   MOVE PC-TSYMSG-00047                                   
091400                                   TO DP020-MSG-NUMBER                    
091500                   SET DP020-MSG-FATAL                                    
091600                       DP030-OVERRIDE-GO                                  
091700                                   TO TRUE                                
091800               ELSE                                                       
091900                 MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
092000                                TO IQ016-NUMBER-OF-SELECTED-ITEMS         
092100                 PERFORM 5000-PREPARE-SEL-QUEUE                           
092200               END-IF                                                     
092300             END-IF                                                       
092400                                                                          
092500         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
092600             MOVE +1               TO PV-TOP-ITEM                         
092700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
092800                                                                          
092900         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
093000             PERFORM 2110-BROWSE-BACKWARD                                 
093100             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
093200                                                                          
093300         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
093400             PERFORM 2120-BROWSE-FORWARD                                  
093500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
093600                                                                          
093700         WHEN DP020-SRC-AID = DP016-ENTER                                 
093800             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
093900             IF  PS-ERROR                                                 
094000                 CONTINUE                                                 
094100             ELSE                                                         
094200                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
094300             END-IF                                                       
094400                                                                          
094500         WHEN OTHER                                                       
094600             SET DP013-NO-ROLLBACK                                        
094700                 DP013-XCTL-DISPLAY-RESTART                               
094800                 DP013-CICS-ABEND  TO TRUE                                
094900             MOVE '2100-CHECK-FUNCTION-KEY'                               
095000                                   TO DP013-PARAGRAPH                     
095100             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
095200                                   TO DP013-MESSAGE-TEXT (1)              
095300             PERFORM DP013-0000-PROCESS-ABEND                             
095400     END-EVALUATE.                                                        
095500*                                                                         
095600*----------------------------------------------------------------*        
095700*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
095800*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
095900*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
096000*----------------------------------------------------------------*        
096100                                                                          
096200 2110-BROWSE-BACKWARD.                                                    
096300     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
096400         GIVING PV-TOP-ITEM.                                              
096500*                                                                         
096600     IF  ((PV-TOP-ITEM < +1)                                              
096700       OR (PV-TOP-ITEM = +1))                                             
096800         MOVE +1                   TO PV-TOP-ITEM                         
096900*        -- TOP OF LIST --                                                
097000         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
097100         SET DP020-MSG-INFORMATIONAL TO TRUE                              
097200     END-IF.                                                              
097300                                                                          
097400 EJECT                                                                    
097500*----------------------------------------------------------------*        
097600*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
097700*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
097800*    PAST THE END, ISSUE THE BOTTOM OF LIST                      *        
097900*----------------------------------------------------------------*        
098000                                                                          
098100 2120-BROWSE-FORWARD.                                                     
098200                                                                          
098300     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
098400         GIVING PV-TOP-ITEM.                                              
098500     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
098600         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
098700                                   TO PV-TOP-ITEM                         
098800*        -- BOTTOM OF LIST --                                             
098900         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
099000         SET DP020-MSG-INFORMATIONAL                                      
099100                                   TO TRUE                                
099200     ELSE                                                                 
099300         COMPUTE PV-BOTTOM-ITEM =                                         
099400             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
099500         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
099600*            -- BOTTOM OF LIST --                                         
099700             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
099800             SET DP020-MSG-INFORMATIONAL                                  
099900                                   TO TRUE                                
100000         END-IF                                                           
100100     END-IF.                                                              
100200 EJECT                                                                    
100300*----------------------------------------------------------------*        
100400*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
100500*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
100600*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
100700*    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
100800*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
100900*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
101000*----------------------------------------------------------------*        
101100                                                                          
101200 2130-CHECK-FOR-JUMP-BROWSE.                                              
101300     SET PS-NO-ERROR               TO TRUE.                               
101400     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
101500         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
101600                                   TO PV-TOP-ITEM                         
101700     ELSE                                                                 
101800         MOVE ASC-START-ITEM-REQUEST                                      
101900                                   TO DP010I-UNEDITED-FIELD               
102000         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
102100         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
102200         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
102300                                   TO TRUE                                
102410         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
102420                         USING DP010I-NUMERIC-EDIT-AREA                   
102500                                                                          
102600         IF  NOT DP010I-ERROR-DETECTED                                    
102700             MOVE DP010I-NUMERIC-FIELD                                    
102800                                   TO  ASC-START-ITEM-REQUEST-N           
102900             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
103000                 IF  ASC-START-ITEM-REQUEST-N >                           
103100                                          ASC-NUMBER-OF-ITEMS-READ        
103200                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
103300                     COMPUTE PV-TOP-ITEM =                                
103400                             ASC-NUMBER-OF-ITEMS-READ                     
103500                             - PC-ITEMS-PER-PANEL + 1                     
103600                     IF  PV-TOP-ITEM < +1                                 
103700                         MOVE +1   TO PV-TOP-ITEM                         
103800                         MOVE PC-TSYMSG-00070                             
103900                                   TO DP020-MSG-NUMBER                    
104000                         SET DP020-MSG-INFORMATIONAL                      
104100                                   TO TRUE                                
104200                     END-IF                                               
104300                 ELSE                                                     
104400                     MOVE ASC-START-ITEM-REQUEST-N                        
104500                                   TO PV-TOP-ITEM                         
104600                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
104700                     COMPUTE PV-BOTTOM-ITEM =                             
104800                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
104900                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
105000*                        -- BOTTOM OF LIST --                             
105100                         MOVE PC-TSYMSG-00070                             
105200                                   TO DP020-MSG-NUMBER                    
105300                         SET DP020-MSG-INFORMATIONAL                      
105400                                   TO TRUE                                
105500                     END-IF                                               
105600                 END-IF                                                   
105700             ELSE                                                         
105800*                --- VALUE OUT OF RANGE ----                              
105900                 MOVE PC-TSYMSG-00101                                     
106000                                   TO DP020-MSG-NUMBER                    
106100                 MOVE -1           TO ASTRTITL                            
106200                 SET DP030-SET-CURSOR-APPL-1                              
106300                                   TO TRUE                                
106400                 MOVE DP015-REVERSE                                       
106500                                   TO ASTRTITH                            
106600                 MOVE DP015-RED    TO ASTRTITC                            
106700                 MOVE DP015-UNP-NUM-BRT-MDT                               
106800                                   TO ASTRTITA                            
106900                 SET DP020-MSG-FATAL                                      
107000                                   TO TRUE                                
107100                 SET PS-ERROR      TO TRUE                                
107200             END-IF                                                       
107300                                                                          
107400         ELSE                                                             
107500             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
107600             MOVE -1               TO ASTRTITL                            
107700             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
107800             MOVE DP015-REVERSE    TO ASTRTITH                            
107900             MOVE DP015-RED        TO ASTRTITC                            
108000             MOVE DP015-UNP-NUM-BRT-MDT                                   
108100                                   TO ASTRTITA                            
108200             SET DP020-MSG-FATAL   TO TRUE                                
108300             SET PS-ERROR          TO TRUE                                
108400         END-IF                                                           
108500     END-IF.                                                              
108600 EJECT                                                                    
108700*----------------------------------------------------------------*        
108800*    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
108900*    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
109000*----------------------------------------------------------------*        
109100 2140-DESELECT-ALL-ITEMS.                                                 
109200     MOVE +1                       TO ASC-BLK-SUB.                        
109300                                                                          
109400     PERFORM 2141-CLEAR-SELECTED-INDS                                     
109500         VARYING ASC-ITEM-SUB                                             
109600         FROM 1 BY 1                                                      
109700         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
109800                                                                          
109900     ADD +1                        TO ASC-BLK-SUB.                        
110000                                                                          
110100     PERFORM 2141-CLEAR-SELECTED-INDS                                     
110200         VARYING ASC-ITEM-SUB                                             
110300         FROM 1 BY 1                                                      
110400         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
110500                                                                          
110600     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
110700                                   TO PV-LIST-ITEM-NUMBER.                
110800                                                                          
110900     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
111000         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
111100                                                                          
111200     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
111300        (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
111400         MOVE +1                   TO ASC-BLK-SUB                         
111500                                                                          
111600         PERFORM 5100-WRITE-TEMP-STORAGE                                  
111700                                                                          
111800         MOVE +1                   TO PV-BLOCK-NUMBER                     
111900                                                                          
112000         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
112100             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
112200                                                                          
112300         PERFORM                                                          
112400             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
112500                                                                          
112600             PERFORM 4410-READ-TEMP-STORAGE                               
112700                                                                          
112800             PERFORM 2141-CLEAR-SELECTED-INDS                             
112900                 VARYING ASC-ITEM-SUB FROM 1 BY 1                         
113000                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
113100                                                                          
113200             PERFORM 5100-WRITE-TEMP-STORAGE                              
113300                                                                          
113400             ADD +1                TO PV-BLOCK-NUMBER                     
113500                                                                          
113600         END-PERFORM                                                      
113700                                                                          
113800         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
113900             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
114000                                                                          
114100         PERFORM                                                          
114200             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
114300                   ASC-HIGHEST-BLOCK-WRITTEN                              
114400                                                                          
114500             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
114600                                   TO PV-BLOCK-NUMBER                     
114700                                                                          
114800             PERFORM 4410-READ-TEMP-STORAGE                               
114900                                                                          
115000             PERFORM 2141-CLEAR-SELECTED-INDS                             
115100                 VARYING ASC-ITEM-SUB                                     
115200                 FROM 1 BY 1                                              
115300                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
115400                                                                          
115500             PERFORM 5100-WRITE-TEMP-STORAGE                              
115600                                                                          
115700             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
115800                                                                          
115900         END-PERFORM                                                      
116000                                                                          
116100         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
116200                                   TO PV-BLOCK-NUMBER                     
116300                                                                          
116400         PERFORM 4410-READ-TEMP-STORAGE                                   
116500     END-IF.                                                              
116600                                                                          
116700                                                                          
116800     MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS.         
116900     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.        
117000     MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.          
117100                                                                          
117200     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
117300                                                                          
117400 EJECT                                                                    
117500*----------------------------------------------------------------*        
117600*    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
117700*----------------------------------------------------------------*        
117800                                                                          
117900 2141-CLEAR-SELECTED-INDS.                                                
118000                                                                          
118100     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
118200             NOT EQUAL ZERO                                               
118300         IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
118400             SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
118500                 ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
118600                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
118700                                   TO TRUE                                
118800         END-IF                                                           
118900     END-IF.                                                              
119000                                                                          
119100 EJECT                                                                    
119200*----------------------------------------------------------------*        
119300*  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *        
119400*  FOR ALL ITEMS.                                                *        
119500*----------------------------------------------------------------*        
119600                                                                          
119700 2150-SELECT-ALL-ITEMS.                                                   
119800                                                                          
119900     MOVE +1                       TO ASC-BLK-SUB.                        
120000                                                                          
120100     PERFORM 2151-SET-SELECTED-INDS                                       
120200         VARYING ASC-ITEM-SUB                                             
120300         FROM 1 BY 1                                                      
120400         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
120500                                                                          
120600     ADD +1                        TO ASC-BLK-SUB.                        
120700                                                                          
120800     PERFORM 2151-SET-SELECTED-INDS                                       
120900         VARYING ASC-ITEM-SUB                                             
121000         FROM 1 BY 1                                                      
121100         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
121200                                                                          
121300     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
121400                                   TO PV-LIST-ITEM-NUMBER.                
121500                                                                          
121600     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
121700         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
121800                                                                          
121900     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
122000        (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
122100         MOVE +1                   TO ASC-BLK-SUB                         
122200                                                                          
122300         PERFORM 5100-WRITE-TEMP-STORAGE                                  
122400                                                                          
122500         MOVE +1                   TO PV-BLOCK-NUMBER                     
122600                                                                          
122700         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
122800             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
122900                                                                          
123000         PERFORM                                                          
123100             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
123200                                                                          
123300             PERFORM 4410-READ-TEMP-STORAGE                               
123400                                                                          
123500             PERFORM 2151-SET-SELECTED-INDS                               
123600                 VARYING ASC-ITEM-SUB                                     
123700                 FROM 1 BY 1                                              
123800                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
123900                                                                          
124000             PERFORM 5100-WRITE-TEMP-STORAGE                              
124100                                                                          
124200             ADD +1                TO PV-BLOCK-NUMBER                     
124300                                                                          
124400         END-PERFORM                                                      
124500                                                                          
124600         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
124700             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
124800                                                                          
124900         PERFORM                                                          
125000             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
125100                   ASC-HIGHEST-BLOCK-WRITTEN                              
125200                                                                          
125300             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
125400                                   TO PV-BLOCK-NUMBER                     
125500                                                                          
125600             PERFORM 4410-READ-TEMP-STORAGE                               
125700                                                                          
125800             PERFORM 2151-SET-SELECTED-INDS                               
125900                 VARYING ASC-ITEM-SUB                                     
126000                 FROM 1 BY 1                                              
126100                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
126200                                                                          
126300             PERFORM 5100-WRITE-TEMP-STORAGE                              
126400                                                                          
126500             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
126600                                                                          
126700         END-PERFORM                                                      
126800                                                                          
126900         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
127000                                   TO PV-BLOCK-NUMBER                     
127100                                                                          
127200         PERFORM 4410-READ-TEMP-STORAGE                                   
127300     END-IF.                                                              
127400                                                                          
127500                                                                          
127600     MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
127700                       ASC-NUMBER-OF-SELECTED-ITEMS.                      
127800                                                                          
127900     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
128000                                                                          
128100 EJECT                                                                    
128200*----------------------------------------------------------------*        
128300*    MARK EACH ITEM AS BEING SELECTED.                           *        
128400*----------------------------------------------------------------*        
128500                                                                          
128600 2151-SET-SELECTED-INDS.                                                  
128700                                                                          
128800     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
128900             NOT EQUAL ZERO                                               
129000         IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
129100             SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
129200                 ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
129300                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
129400                               TO  TRUE                                   
129500         END-IF                                                           
129600     END-IF.                                                              
129700******************************************************************        
129800** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
129900** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
130000******************************************************************        
130100 2200-MOVE-SCREEN-TO-COMMAREA.                                            
130200                                                                          
130300     MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
130400     MOVE +1                       TO PV-SUB.                             
130500*                                                                         
130600     PERFORM 3100-SET-SUBSCRIPTS.                                         
130700     IF  ASTRTITL > ZERO                                                  
130800         MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
130900     ELSE                                                                 
131000         IF  ASTRTITF = DP015-ERASE-EOF                                   
131100             INITIALIZE ASC-START-ITEM-REQUEST                            
131200         END-IF                                                           
131300     END-IF.                                                              
131400     PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
131500         VARYING PV-SUB                                                   
131600         FROM 1 BY 1                                                      
131700         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
131800******************************************************************        
131900** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
132000******************************************************************        
132100 2210-MOVE-LINES-TO-COMMAREA.                                             
132200     IF  MR-SELECTL (PV-SUB) > ZERO                                       
132300         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
132400         MOVE MR-SELECTI (PV-SUB)                                         
132500                                   TO ASC-TEMP-SELECTED-SW                
132600                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
132700     ELSE                                                                 
132800         IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
132900                 SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
133000                 MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
133100                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
133200         END-IF                                                           
133300     END-IF.                                                              
133400     ADD +1                        TO ASC-ITEM-SUB.                       
133500     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
133600         ADD +1                    TO ASC-BLK-SUB                         
133700         MOVE +1                   TO ASC-ITEM-SUB                        
133800     END-IF.                                                              
133900******************************************************************        
134000** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
134100** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
134200******************************************************************        
134300 3000-EDIT-DATA-IN-COMMAREA.                                              
134400     SET PS-NO-ERROR               TO TRUE.                               
134500     PERFORM 3200-RESET-ATTRIBUTES.                                       
134600     MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
134700**                                                                        
134800* CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
134900* EDITTING IS DONE FROM THE BOTTOM UP.                                    
135000**                                                                        
135100     COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
135200         ASC-ITEMS-DISPLAYED - 1.                                         
135300     PERFORM 3100-SET-SUBSCRIPTS.                                         
135400     PERFORM 3300-EDIT-SELECTIONS                                         
135500         VARYING PV-SUB                                                   
135600         FROM PV-SUB BY -1                                                
135700         UNTIL PV-SUB < +1                                                
135800*                                                                         
135900     IF  PS-NO-ERROR                                                      
136000         PERFORM 3400-CHECK-RANGE-SEQUENCE                                
136100     END-IF.                                                              
136200*                                                                         
136300     IF  ((PS-NO-ERROR)                                                   
136400       AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
136500       AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
136600             PERFORM 3500-MARK-RANGE-AS-SELECTED                          
136700     END-IF.                                                              
136800 EJECT                                                                    
136900*----------------------------------------------------------------*        
137000*    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
137100*    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
137200*    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
137300*                                                                *        
137400*    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
137500*    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
137600*    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
137700*    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
137800*----------------------------------------------------------------*        
137900                                                                          
138000 3100-SET-SUBSCRIPTS.                                                     
138100                                                                          
138200     COMPUTE ASC-ITEM-SUB =                                               
138300         (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
138400                                                                          
138500     IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
138600         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
138700                                                                          
138800         MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
138900     ELSE                                                                 
139000         MOVE +1                     TO ASC-BLK-SUB                       
139100     END-IF.                                                              
139200******************************************************************        
139300** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
139400******************************************************************        
139500                                                                          
139600 3200-RESET-ATTRIBUTES.                                                   
139700     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
139800     MOVE DP015-GREEN              TO ASTRTITC.                           
139900     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
140000*                                                                         
140100     PERFORM                                                              
140200         VARYING PV-SUB                                                   
140300         FROM 1 BY 1                                                      
140400         UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
140500         MOVE DP015-UNP-ALP-NOR-OFF                                       
140600                                   TO MR-SELECTA (PV-SUB)                 
140700         MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
140800         MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
140900     END-PERFORM.                                                         
141000******************************************************************        
141100** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **        
141200** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **        
141300** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **        
141400** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **        
141500******************************************************************        
141600 3300-EDIT-SELECTIONS.                                                    
141700     EVALUATE TRUE                                                        
141800         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
141900             = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
142000                 CONTINUE                                                 
142100         WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
142200             IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
142300                 ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
142400                 MOVE ASC-TEMP-SELECTED-SW                                
142500                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
142600                                TO ASC-SELECTED-SW                        
142700                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
142800             END-IF                                                       
142900         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
143000                 = SPACE                                                  
143100             EVALUATE TRUE                                                
143200                 WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
143300                     SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
143400                     MOVE SPACE TO ASC-SELECTED-SW                        
143500                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
143600                 WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
143700                     MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
143800                     MOVE SPACE TO ASC-SELECTED-SW                        
143900                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
144000                 WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
144100                     MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
144200                     MOVE SPACE TO ASC-SELECTED-SW                        
144300                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
144400             END-EVALUATE                                                 
144500         WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
144600             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
144700                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
144800             END-IF                                                       
144900             PERFORM 3310-EDIT-RANGE                                      
145000         WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
145100             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
145200                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
145300             END-IF                                                       
145400             PERFORM 3310-EDIT-RANGE                                      
145500         WHEN OTHER                                                       
145600             MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
145700             SET DP020-MSG-FATAL    TO TRUE                               
145800             MOVE -1                TO MR-SELECTL (PV-SUB)                
145900             SET DP030-SET-CURSOR-APPL-1                                  
146000                                    TO TRUE                               
146100             MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
146200             MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
146300             SET PS-ERROR TO TRUE                                         
146400             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
146500                 SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
146600                 MOVE SPACE         TO ASC-SELECTED-SW                    
146700                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
146800             END-IF                                                       
146900     END-EVALUATE.                                                        
147000                                                                          
147100     SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
147200     SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
147300     IF  ASC-ITEM-SUB < 1                                                 
147400         SUBTRACT +1 FROM ASC-BLK-SUB                                     
147500         MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
147600     END-IF.                                                              
147700******************************************************************        
147800** IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A      **        
147900** RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1    **        
148000** ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN)**        
148100** THE ENDING.                                                  **        
148200******************************************************************        
148300 3310-EDIT-RANGE.                                                         
148400     IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
148500         IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
148600             MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
148700                                   TO ASC-SELECTED-SW                     
148800                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
148900             MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
149000         ELSE                                                             
149100             IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
149200                 CONTINUE                                                 
149300             ELSE                                                         
149400*                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
149500                 MOVE PC-TSYMSG-00054                                     
149600                                   TO DP020-MSG-NUMBER                    
149700                 SET DP020-MSG-FATAL                                      
149800                                   TO TRUE                                
149900                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
150000                 SET DP030-SET-CURSOR-APPL-1                              
150100                                   TO TRUE                                
150200                 MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
150300                 MOVE DP015-UNDERLINE                                     
150400                                   TO MR-SELECTH (PV-SUB)                 
150500                 SET PS-ERROR      TO TRUE                                
150600             END-IF                                                       
150700         END-IF                                                           
150800     ELSE                                                                 
150900         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
151000             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
151100                                   TO ASC-SELECTED-SW                     
151200                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
151300             MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
151400         ELSE                                                             
151500             IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
151600                 CONTINUE                                                 
151700             ELSE                                                         
151800*                -- ONLY ONE END-OF-RANGE ALLOWED --                      
151900                 MOVE PC-TSYMSG-00055                                     
152000                                   TO DP020-MSG-NUMBER                    
152100                 SET DP020-MSG-FATAL                                      
152200                                   TO TRUE                                
152300                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
152400                 SET DP030-SET-CURSOR-APPL-1                              
152500                                   TO TRUE                                
152600                 SET PS-ERROR      TO TRUE                                
152700             END-IF                                                       
152800         END-IF                                                           
152900     END-IF.                                                              
153000*                                                                         
153100******************************************************************        
153200** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **        
153300** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **        
153400******************************************************************        
153500 3400-CHECK-RANGE-SEQUENCE.                                               
153600     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
153700       AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
153800       AND  ASC-START-OF-SELECTED-RANGE >                                 
153900                                         ASC-END-OF-SELECTED-RANGE        
154000*        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
154100         MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
154200         SET DP020-MSG-FATAL TO TRUE                                      
154300         SET PS-ERROR TO TRUE                                             
154400         PERFORM 3410-POSITION-AT-ERROR                                   
154500     END-IF.                                                              
154600******************************************************************        
154700** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **        
154800** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **        
154900** SHOULD BE POSITIONED ON A RANGE ERROR.                       **        
155000******************************************************************        
155100 3410-POSITION-AT-ERROR.                                                  
155200     SET DP030-SET-CURSOR-APPL-1                                          
155300                                   TO TRUE.                               
155400     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
155500                    - PV-TOP-ITEM + 1.                                    
155600     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
155700         CONTINUE                                                         
155800     ELSE                                                                 
155900         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
156000         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
156100         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
156200     END-IF.                                                              
156300*                                                                         
156400     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
156500                    - PV-TOP-ITEM + 1.                                    
156600     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
156700         CONTINUE                                                         
156800     ELSE                                                                 
156900         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
157000         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
157100         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
157200     END-IF.                                                              
157300 EJECT                                                                    
157400*----------------------------------------------------------------*        
157500*    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
157600*    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
157700*    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
157800*----------------------------------------------------------------*        
157900                                                                          
158000 3500-MARK-RANGE-AS-SELECTED.                                             
158100     MOVE +1                       TO  ASC-BLK-SUB.                       
158200     PERFORM 3510-MARK-SELECTED-INDS                                      
158300         VARYING ASC-ITEM-SUB                                             
158400         FROM 1 BY 1                                                      
158500         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
158600                                                                          
158700     ADD +1                        TO ASC-BLK-SUB.                        
158800     PERFORM 3510-MARK-SELECTED-INDS                                      
158900         VARYING ASC-ITEM-SUB                                             
159000         FROM 1 BY 1                                                      
159100         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
159200                                                                          
159300     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
159400                                   TO  PV-LIST-ITEM-NUMBER.               
159500     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
159600         1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
159700               PC-ITEMS-PER-PANEL).                                       
159800                                                                          
159900     MOVE ASC-START-OF-SELECTED-RANGE                                     
160000                                   TO PV-START-OF-SELECTED-RANGE.         
160100     COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
160200         1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
160300               PC-ITEMS-PER-PANEL).                                       
160400                                                                          
160500     MOVE ASC-END-OF-SELECTED-RANGE                                       
160600                                   TO  PV-END-OF-SELECTED-RANGE.          
160700     COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
160800         1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
160900               PC-ITEMS-PER-PANEL).                                       
161000     COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
161100         PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
161200                                                                          
161300     IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
161400        (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
161500         MOVE +1                   TO ASC-BLK-SUB                         
161600         PERFORM 5100-WRITE-TEMP-STORAGE                                  
161700         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
161800             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
161900                                                                          
162000         PERFORM                                                          
162100             UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
162200                   PV-PREV-BLOCK-IN-ARRAY                                 
162300             MOVE PV-FIRST-BLOCK-IN-RANGE                                 
162400                                   TO PV-BLOCK-NUMBER                     
162500             PERFORM 4410-READ-TEMP-STORAGE                               
162600             PERFORM 3510-MARK-SELECTED-INDS                              
162700                 VARYING ASC-ITEM-SUB                                     
162800                 FROM 1 BY 1                                              
162900                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
163000             PERFORM 5100-WRITE-TEMP-STORAGE                              
163100             ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
163200         END-PERFORM                                                      
163300                                                                          
163400         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
163500             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
163600         PERFORM                                                          
163700             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
163800                   PV-LAST-BLOCK-IN-RANGE                                 
163900             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
164000                                   TO PV-BLOCK-NUMBER                     
164100             PERFORM 4410-READ-TEMP-STORAGE                               
164200             PERFORM 3510-MARK-SELECTED-INDS                              
164300                 VARYING ASC-ITEM-SUB                                     
164400                 FROM 1 BY 1                                              
164500                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
164600             PERFORM 5100-WRITE-TEMP-STORAGE                              
164700             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
164800         END-PERFORM                                                      
164900                                                                          
165000         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
165100                                   TO PV-BLOCK-NUMBER                     
165200         PERFORM 4410-READ-TEMP-STORAGE                                   
165300     END-IF.                                                              
165400                                                                          
165500     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
165600                                      ASC-END-OF-SELECTED-RANGE.          
165700                                                                          
165800 EJECT                                                                    
165900*----------------------------------------------------------------*        
166000* SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
166100*----------------------------------------------------------------*        
166200                                                                          
166300 3510-MARK-SELECTED-INDS.                                                 
166400                                                                          
166500     IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
166600                                ASC-ITEM-SUB) >                           
166700         ASC-START-OF-SELECTED-RANGE)                                     
166800     OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
166900                                ASC-ITEM-SUB) =                           
167000         ASC-START-OF-SELECTED-RANGE)                                     
167100                                                                          
167200         IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
167300                                    ASC-ITEM-SUB) <                       
167400             ASC-END-OF-SELECTED-RANGE)                                   
167500         OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
167600                                    ASC-ITEM-SUB) =                       
167700             ASC-END-OF-SELECTED-RANGE)                                   
167800                                                                          
167900             IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
168000                 SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
168100                     ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
168200                               TO TRUE                                    
168300                 SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      
168400                               TO TRUE                                    
168500                 ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS            
168600             END-IF                                                       
168700         END-IF                                                           
168800     END-IF.                                                              
168900                                                                          
169000 EJECT                                                                    
169100*----------------------------------------------------------------*        
169200*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
169300*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
169400*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
169500*    DISPLAY THE PANEL TO USER.                                  *        
169600*----------------------------------------------------------------*        
169700                                                                          
169800 4000-BUILD-INITIAL-PANEL.                                                
169900                                                                          
170000     INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
170100                                 ASC-BLOCK-ENTRIES (2).                   
170200     MOVE +1                 TO ASC-BLK-SUB.                              
170300     SET ASC-ITEMS-LEFT-ON-DB                                             
170400                             TO TRUE.                                     
170500                                                                          
170600     MOVE +0                 TO ASC-ITEM-SUB                              
170700                                ASC-NUMBER-OF-ITEMS-READ                  
170800                                ASC-NUMBER-OF-SELECTED-ITEMS              
170900                                ASC-HIGHEST-BLOCK-WRITTEN                 
171000                                ASC-START-OF-SELECTED-RANGE               
171100                                ASC-END-OF-SELECTED-RANGE                 
171200                                ASC-ITEM-AT-TOP-OF-PANEL.                 
171300                                                                          
171400*--------------------------------------------------------------*          
171500*    FORMAT INDICATORS ARE SET AS FOLLOWS:                     *          
171600*                                                              *          
171700*    I404 - IQKCS404 - IQLDEPT  - SPACE      F20               *          
171800*    I405 - IQLCS405 - IQLCLASS - 1     BOTH F19 AND F20       *          
171900*    I406 - IQKCS406 - IQLSTYLE - 2          F19               *          
172000*                                                              *          
172100*    THERE ARE TWO SETS OF CURSORS.  THE CURSORS THAT PROVIDE  *          
172200*    THE AVAILABLE UPC COUNT ON THE SCREEN AND THE ACTUAL DATA *          
172300*    CURSORS USED TO RETRIEVE THE DATA FOR THE SCREEN.         *          
172400*                                                              *          
172500*    ALL OF THE COUNTS FOR EACH CONDITION IS DONE HERE BEFORE  *          
172600*    THE ACTUAL DATA TO DISPLAYED IS RETRIEVED IN PARAGRAPH    *          
172700*    4300-READ-ITEMS-FROM-DB.                                  *          
172800*                                                              *          
172900*                                                              *          
173000*--------------------------------------------------------------*          
173100                                                                          
173200     EVALUATE DP020-INT-APPL-FORMAT-IND                                   
173300         WHEN PC-CLASS-FORMAT-IND                                         
173400              IF DK-LBL-NM = SPACES                                       
173700                 SET PS-CNT-DEPT-CLASS  TO TRUE                           
173720                 MOVE 'CNT-DEPT-CLS      ' TO PV-CURSOR-NAME              
173800                 PERFORM 4006-OPEN-COUNT-CURSORS                          
173900                 PERFORM 4340-FETCH-COUNTS                                
174000                 PERFORM 4026-CLOSE-COUNT-CURSORS                         
174100                                                                          
174200                 MOVE PV-SKU-CNT TO ASC-SKU-CNT                           
174300                 MOVE ZEROES     TO PV-SKU-CNT                            
174320                                                                          
174330                 SET PS-CSR-NOVND-CLASS-COLOR TO TRUE                     
174360                 MOVE 'NOVND-CLASS-COLOR ' TO PV-CURSOR-NAME              
174400              ELSE                                                        
174700                 SET PS-CNT-VENDOR-DEPT-CLASS TO TRUE                     
174730                 MOVE 'CNT-VND-DEPT-CLS  ' TO PV-CURSOR-NAME              
174740                                                                          
174800                 PERFORM 4006-OPEN-COUNT-CURSORS                          
174900                 PERFORM 4340-FETCH-COUNTS                                
175000                 PERFORM 4026-CLOSE-COUNT-CURSORS                         
175100                                                                          
175200                 MOVE PV-SKU-CNT TO ASC-SKU-CNT                           
175300                                                                          
175400                 SET PS-CNT-LABEL-DEPT-CLASS TO TRUE                      
175430                 MOVE 'CNT-LBL-DEPT-CLS  ' TO PV-CURSOR-NAME              
175440                                                                          
175500                 PERFORM 4006-OPEN-COUNT-CURSORS                          
175600                 PERFORM 4340-FETCH-COUNTS                                
175700                 PERFORM 4026-CLOSE-COUNT-CURSORS                         
175900                 ADD  PV-SKU-CNT TO ASC-SKU-CNT                           
176000                 MOVE ZEROES     TO PV-SKU-CNT                            
176020                                                                          
176030                 SET PS-CSR-VENDOR-CLASS-COLOR TO TRUE                    
176060                 MOVE 'VENDOR-CLASS-COLOR' TO PV-CURSOR-NAME              
176100              END-IF                                                      
176200                                                                          
176300         WHEN PC-DEPT-FORMAT-IND                                          
176400              IF DK-LBL-NM = SPACES                                       
176700                 SET PS-CNT-DEPT-NOCLASS TO TRUE                          
176730                 MOVE 'CNT-DEPT-NOCLS    ' TO PV-CURSOR-NAME              
176740                                                                          
176800                 PERFORM 4006-OPEN-COUNT-CURSORS                          
176900                 PERFORM 4340-FETCH-COUNTS                                
177000                 PERFORM 4026-CLOSE-COUNT-CURSORS                         
177200                 MOVE PV-SKU-CNT TO ASC-SKU-CNT                           
177300                 MOVE ZEROES     TO PV-SKU-CNT                            
177320                                                                          
177330                 SET PS-CSR-NOVND-NOCLS-COLOR TO TRUE                     
177360                 MOVE 'NOVND-NOCLS-COLOR ' TO PV-CURSOR-NAME              
177400              ELSE                                                        
177700                 SET PS-CNT-VENDOR-DEPT-NOCLASS TO TRUE                   
177703                 MOVE 'CNT-VND-DEPT-NOCLS' TO PV-CURSOR-NAME              
177710                                                                          
177800                 PERFORM 4006-OPEN-COUNT-CURSORS                          
177900                 PERFORM 4340-FETCH-COUNTS                                
178000                 PERFORM 4026-CLOSE-COUNT-CURSORS                         
178200                 MOVE PV-SKU-CNT TO ASC-SKU-CNT                           
178300                                                                          
178400                 SET PS-CNT-LABEL-DEPT-NOCLASS TO TRUE                    
178403                 MOVE 'CNT-LBL-DEPT-NOCLS' TO PV-CURSOR-NAME              
178410                                                                          
178500                 PERFORM 4006-OPEN-COUNT-CURSORS                          
178600                 PERFORM 4340-FETCH-COUNTS                                
178700                 PERFORM 4026-CLOSE-COUNT-CURSORS                         
178900                 ADD  PV-SKU-CNT TO ASC-SKU-CNT                           
179000                 MOVE ZEROES     TO PV-SKU-CNT                            
179020                                                                          
179030                 SET PS-CSR-VENDOR-NOCLS-COLOR TO TRUE                    
179060                 MOVE 'VENDOR-NOCLS-COLOR ' TO PV-CURSOR-NAME             
179100              END-IF                                                      
179200                                                                          
179300         WHEN PC-STYLE-FORMAT-IND                                         
179400              IF DK-LBL-NM = SPACES                                       
179700                 SET PS-CNT-DEPT-STYLE TO TRUE                            
179703                 MOVE 'CNT-DEPT-STYLE    ' TO PV-CURSOR-NAME              
179710                                                                          
179800                 PERFORM 4006-OPEN-COUNT-CURSORS                          
179900                 PERFORM 4340-FETCH-COUNTS                                
180000                 PERFORM 4026-CLOSE-COUNT-CURSORS                         
180200                 MOVE PV-SKU-CNT TO ASC-SKU-CNT                           
180300                 MOVE ZEROES     TO PV-SKU-CNT                            
180320                                                                          
180330                 SET PS-CSR-NOVND-STYLE-COLOR TO TRUE                     
180360                 MOVE 'NOVND-STYLE-COLOR ' TO PV-CURSOR-NAME              
180400              ELSE                                                        
180700                 SET PS-CNT-VENDOR-DEPT-STYLE TO TRUE                     
180703                 MOVE 'CNT-VND-DEPT-STYLE' TO PV-CURSOR-NAME              
180710                                                                          
180800                 PERFORM 4006-OPEN-COUNT-CURSORS                          
180900                 PERFORM 4340-FETCH-COUNTS                                
181000                 PERFORM 4026-CLOSE-COUNT-CURSORS                         
181200                 MOVE PV-SKU-CNT TO ASC-SKU-CNT                           
181300                                                                          
181400                 SET PS-CNT-LABEL-DEPT-STYLE TO TRUE                      
181430                 MOVE 'CNT-LBL-DEPT-STYLE' TO PV-CURSOR-NAME              
181440                                                                          
181500                 PERFORM 4006-OPEN-COUNT-CURSORS                          
181600                 PERFORM 4340-FETCH-COUNTS                                
181700                 PERFORM 4026-CLOSE-COUNT-CURSORS                         
181900                 ADD  PV-SKU-CNT TO ASC-SKU-CNT                           
182000                 MOVE ZEROES     TO PV-SKU-CNT                            
182020                                                                          
182030                 SET PS-CSR-VENDOR-STYLE-COLOR TO TRUE                    
182060                 MOVE 'VENDOR-STYLE-COLOR' TO PV-CURSOR-NAME              
182100              END-IF                                                      
182200                                                                          
182300     END-EVALUATE.                                                        
182400                                                                          
182500     PERFORM 4005-OPEN-DATA-CURSORS.                                      
182600                                                                          
182700     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
182800                                                                          
182900     PERFORM 4022-CLOSE-DATA-CURSORS.                                     
183000                                                                          
183100     MOVE +1                   TO  PV-TOP-ITEM.                           
183200     IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
183300         SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
183400         SET DP020-MSG-FATAL   TO  TRUE                                   
183500         MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
183600     ELSE                                                                 
183700         MOVE -1               TO MR-SELECTL (1)                          
183800         SET DP030-SET-CURSOR-APPL-1                                      
183900                               TO TRUE                                    
184000         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
184100     END-IF.                                                              
184200                                                                          
184300*---------------------------------------------------------------*         
184400*    OPEN ANY OF THE DATA CURSORS.                              *         
184500*                                                               *         
184600*---------------------------------------------------------------*         
184700                                                                          
184800 4005-OPEN-DATA-CURSORS.                                                  
184900                                                                          
185000     MOVE '4005-OPEN-DATA-CURSORS' TO DP013-PARAGRAPH.                    
185100                                                                          
185200     PERFORM                                                              
185300         WITH TEST AFTER                                                  
185400         VARYING PV-RETRY-COUNTER                                         
185500         FROM 1 BY 1                                                      
185600         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
185700                   OR (SQLCODE = ZERO))                                   
185800                                                                          
185900         EVALUATE TRUE                                                    
186000             WHEN PS-CSR-VENDOR-CLASS-COLOR                               
186200                  EXEC SQL                                                
186300                       OPEN VENDOR-CLASS-COLOR                            
186400                  END-EXEC                                                
186500                                                                          
186600             WHEN PS-CSR-VENDOR-NOCLS-COLOR                               
186800                  EXEC SQL                                                
186900                       OPEN VENDOR-NOCLS-COLOR                            
187000                  END-EXEC                                                
187100                                                                          
187200             WHEN PS-CSR-NOVND-CLASS-COLOR                                
187400                  EXEC SQL                                                
187500                       OPEN NOVND-CLASS-COLOR                             
187600                  END-EXEC                                                
187700                                                                          
187800             WHEN PS-CSR-NOVND-NOCLS-COLOR                                
188000                  EXEC SQL                                                
188100                       OPEN NOVND-NOCLS-COLOR                             
188200                  END-EXEC                                                
188300                                                                          
188400             WHEN PS-CSR-VENDOR-STYLE-COLOR                               
188600                  EXEC SQL                                                
188700                       OPEN VENDOR-STYLE-COLOR                            
188800                  END-EXEC                                                
188900                                                                          
189000             WHEN PS-CSR-NOVND-STYLE-COLOR                                
189200                  EXEC SQL                                                
189300                       OPEN NOVND-STYLE-COLOR                             
189400                  END-EXEC                                                
189500                                                                          
189600         END-EVALUATE                                                     
189700                                                                          
189800         EVALUATE TRUE                                                    
189900             WHEN SQLCODE = ZERO                                          
190000             WHEN SQLCODE = -904                                          
190100             WHEN SQLCODE = -913                                          
190200                 CONTINUE                                                 
190300             WHEN SQLWARN0 NOT = SPACES                                   
190400             WHEN SQLCODE < ZERO                                          
190500             WHEN SQLCODE > ZERO                                          
190600                 STRING 'OPEN  '                                          
190700                         PV-CURSOR-NAME                                   
190800                        ' CURSOR'                                         
190900                         DELIMITED BY SIZE                                
191000                         INTO DP013-MESSAGE-TEXT (1)                      
191100                 MOVE SQLCA        TO DP013-SQLCA                         
191200                 MOVE 'SKU, VND' TO DP013-DB2-TABLE-NAME (1)              
191300                 MOVE 'VND_STYL' TO DP013-DB2-TABLE-NAME (2)              
191400                 SET DP013-DB2-ABEND                                      
191500                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
191600                 PERFORM DP013-0000-PROCESS-ABEND                         
191700         END-EVALUATE                                                     
191800     END-PERFORM.                                                         
191900                                                                          
192000     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
192210         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN '            
192220                 PV-CURSOR-NAME                                           
192230                ' CURSOR'                                                 
192240                 DELIMITED BY SIZE                                        
192250                 INTO DP013-MESSAGE-TEXT (1)                              
192300         MOVE SQLCA                TO DP013-SQLCA                         
192400         MOVE 'SKU, VND'           TO DP013-DB2-TABLE-NAME (1)            
192500         MOVE 'VND_STYL'           TO DP013-DB2-TABLE-NAME (2)            
192600         SET DP013-DB2-ABEND                                              
192700             DP013-XCTL-DISPLAY-RESTART                                   
192800                                   TO TRUE                                
192900         PERFORM DP013-0000-PROCESS-ABEND                                 
193000     END-IF.                                                              
193100                                                                          
193200*---------------------------------------------------------------*         
193300*    OPEN ANY OF THE COUNT CURSORS.                             *         
193400*                                                               *         
193500*---------------------------------------------------------------*         
193600                                                                          
193700 4006-OPEN-COUNT-CURSORS.                                                 
193800                                                                          
193900     MOVE '4006-OPEN-COUNT-CURSORS' TO DP013-PARAGRAPH.                   
194000                                                                          
194100     PERFORM                                                              
194200         WITH TEST AFTER                                                  
194300         VARYING PV-RETRY-COUNTER                                         
194400         FROM 1 BY 1                                                      
194500         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
194600                   OR (SQLCODE = ZERO))                                   
194700                                                                          
194800         EVALUATE TRUE                                                    
194900             WHEN PS-CNT-VENDOR-DEPT-CLASS                                
195100                  EXEC SQL                                                
195200                       OPEN CNT-VND-DEPT-CLS                              
195300                  END-EXEC                                                
195400                                                                          
195500             WHEN PS-CNT-VENDOR-DEPT-NOCLASS                              
195700                  EXEC SQL                                                
195800                       OPEN CNT-VND-DEPT-NOCLS                            
195900                  END-EXEC                                                
196000                                                                          
196100             WHEN PS-CNT-VENDOR-DEPT-STYLE                                
196300                  EXEC SQL                                                
196400                       OPEN CNT-VND-DEPT-STYLE                            
196500                  END-EXEC                                                
196600                                                                          
196700             WHEN PS-CNT-LABEL-DEPT-CLASS                                 
196900                  EXEC SQL                                                
197000                       OPEN CNT-LBL-DEPT-CLS                              
197100                  END-EXEC                                                
197200                                                                          
197300             WHEN PS-CNT-LABEL-DEPT-NOCLASS                               
197500                  EXEC SQL                                                
197600                       OPEN CNT-LBL-DEPT-NOCLS                            
197700                  END-EXEC                                                
197800                                                                          
197900             WHEN PS-CNT-LABEL-DEPT-STYLE                                 
198100                  EXEC SQL                                                
198200                       OPEN CNT-LBL-DEPT-STYLE                            
198300                  END-EXEC                                                
198400                                                                          
198500             WHEN PS-CNT-DEPT-CLASS                                       
198700                  EXEC SQL                                                
198800                       OPEN CNT-DEPT-CLS                                  
198900                  END-EXEC                                                
199000                                                                          
199100             WHEN PS-CNT-DEPT-NOCLASS                                     
199300                  EXEC SQL                                                
199400                       OPEN CNT-DEPT-NOCLS                                
199500                  END-EXEC                                                
199600                                                                          
199700             WHEN PS-CNT-DEPT-STYLE                                       
199900                  EXEC SQL                                                
200000                       OPEN CNT-DEPT-STYLE                                
200100                  END-EXEC                                                
200200                                                                          
200300             WHEN OTHER                                                   
200400                  CONTINUE                                                
200500                                                                          
200600         END-EVALUATE                                                     
200700                                                                          
200800         EVALUATE TRUE                                                    
200900             WHEN SQLCODE = ZERO                                          
201000             WHEN SQLCODE = -904                                          
201100             WHEN SQLCODE = -913                                          
201200                 CONTINUE                                                 
201300             WHEN SQLWARN0 NOT = SPACES                                   
201400             WHEN SQLCODE < ZERO                                          
201500             WHEN SQLCODE > ZERO                                          
201600                 STRING 'OPEN  '                                          
201700                         PV-CURSOR-NAME                                   
201800                        ' CURSOR'                                         
201900                         DELIMITED BY SIZE                                
202000                         INTO DP013-MESSAGE-TEXT (1)                      
202100                 MOVE SQLCA        TO DP013-SQLCA                         
202200                 MOVE 'SKU, VND' TO DP013-DB2-TABLE-NAME (1)              
202300                 MOVE 'VND_STYL' TO DP013-DB2-TABLE-NAME (2)              
202400                 SET DP013-DB2-ABEND                                      
202500                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
202600                 PERFORM DP013-0000-PROCESS-ABEND                         
202700         END-EVALUATE                                                     
202800     END-PERFORM.                                                         
202900                                                                          
203000     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
203210         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN '            
203220                 PV-CURSOR-NAME                                           
203230                ' CURSOR'                                                 
203240                 DELIMITED BY SIZE                                        
203250                 INTO DP013-MESSAGE-TEXT (1)                              
203300         MOVE SQLCA                TO DP013-SQLCA                         
203400         MOVE 'SKU, VND'           TO DP013-DB2-TABLE-NAME (1)            
203500         MOVE 'VND_STYL'           TO DP013-DB2-TABLE-NAME (2)            
203600         SET DP013-DB2-ABEND                                              
203700             DP013-XCTL-DISPLAY-RESTART                                   
203800                                   TO TRUE                                
203900         PERFORM DP013-0000-PROCESS-ABEND                                 
204000     END-IF.                                                              
204100                                                                          
204200                                                                          
204300 4022-CLOSE-DATA-CURSORS.                                                 
204400                                                                          
204500     MOVE '4022-CLOSE-DATA-CURSORS' TO DP013-PARAGRAPH.                   
204600                                                                          
204700     EVALUATE TRUE                                                        
204800         WHEN PS-CSR-VENDOR-CLASS-COLOR                                   
205000              EXEC SQL                                                    
205100                   CLOSE VENDOR-CLASS-COLOR                               
205200              END-EXEC                                                    
205300                                                                          
205400         WHEN PS-CSR-VENDOR-NOCLS-COLOR                                   
205600              EXEC SQL                                                    
205700                   CLOSE VENDOR-NOCLS-COLOR                               
205800              END-EXEC                                                    
205900                                                                          
206000         WHEN PS-CSR-NOVND-CLASS-COLOR                                    
206200              EXEC SQL                                                    
206300                   CLOSE NOVND-CLASS-COLOR                                
206400              END-EXEC                                                    
206500                                                                          
206600         WHEN PS-CSR-NOVND-NOCLS-COLOR                                    
206800              EXEC SQL                                                    
206900                   CLOSE NOVND-NOCLS-COLOR                                
207000              END-EXEC                                                    
207100                                                                          
207200         WHEN PS-CSR-VENDOR-STYLE-COLOR                                   
207400              EXEC SQL                                                    
207500                   CLOSE VENDOR-STYLE-COLOR                               
207600              END-EXEC                                                    
207700                                                                          
207800         WHEN PS-CSR-NOVND-STYLE-COLOR                                    
208000              EXEC SQL                                                    
208100                   CLOSE NOVND-STYLE-COLOR                                
208200              END-EXEC                                                    
208300                                                                          
208400     END-EVALUATE.                                                        
208500                                                                          
208600     EVALUATE TRUE                                                        
208700         WHEN SQLCODE = ZERO                                              
208800             CONTINUE                                                     
208900         WHEN SQLWARN0 NOT = SPACES                                       
209000         WHEN SQLCODE < ZERO                                              
209100         WHEN SQLCODE > ZERO                                              
209200              STRING 'CLOSE '                                             
209300                      PV-CURSOR-NAME                                      
209400                     ' CURSOR'                                            
209500                      DELIMITED BY SIZE                                   
209600                      INTO DP013-MESSAGE-TEXT (1)                         
209700              MOVE SQLCA        TO DP013-SQLCA                            
209800              MOVE 'SKU, VND' TO DP013-DB2-TABLE-NAME (1)                 
209900              MOVE 'VND_STYL' TO DP013-DB2-TABLE-NAME (2)                 
210000              SET DP013-DB2-ABEND                                         
210100                  DP013-XCTL-DISPLAY-RESTART TO TRUE                      
210200              PERFORM DP013-0000-PROCESS-ABEND                            
210300                                                                          
210400     END-EVALUATE.                                                        
210500                                                                          
210600*---------------------------------------------------------------*         
210700*    CLOSE ANY OF THE COUNT CURSORS.                            *         
210800*                                                               *         
210900*---------------------------------------------------------------*         
211000                                                                          
211100 4026-CLOSE-COUNT-CURSORS.                                                
211200                                                                          
211300     MOVE '4026-CLOSE-COUNT-CURSORS' TO DP013-PARAGRAPH.                  
211400                                                                          
211500     PERFORM                                                              
211600         WITH TEST AFTER                                                  
211700         VARYING PV-RETRY-COUNTER                                         
211800         FROM 1 BY 1                                                      
211900         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
212000                   OR (SQLCODE = ZERO))                                   
212100                                                                          
212200         EVALUATE TRUE                                                    
212300             WHEN PS-CNT-VENDOR-DEPT-CLASS                                
212500                  EXEC SQL                                                
212600                       CLOSE CNT-VND-DEPT-CLS                             
212700                  END-EXEC                                                
212800                                                                          
212900             WHEN PS-CNT-VENDOR-DEPT-NOCLASS                              
213100                  EXEC SQL                                                
213200                       CLOSE CNT-VND-DEPT-NOCLS                           
213300                  END-EXEC                                                
213400                                                                          
213500             WHEN PS-CNT-VENDOR-DEPT-STYLE                                
213700                  EXEC SQL                                                
213800                       CLOSE CNT-VND-DEPT-STYLE                           
213900                  END-EXEC                                                
214000                                                                          
214100             WHEN PS-CNT-LABEL-DEPT-CLASS                                 
214300                  EXEC SQL                                                
214400                       CLOSE CNT-LBL-DEPT-CLS                             
214500                  END-EXEC                                                
214600                                                                          
214700             WHEN PS-CNT-LABEL-DEPT-NOCLASS                               
214900                  EXEC SQL                                                
215000                       CLOSE CNT-LBL-DEPT-NOCLS                           
215100                  END-EXEC                                                
215200                                                                          
215300             WHEN PS-CNT-LABEL-DEPT-STYLE                                 
215500                  EXEC SQL                                                
215600                       CLOSE CNT-LBL-DEPT-STYLE                           
215700                  END-EXEC                                                
215800                                                                          
215900             WHEN PS-CNT-DEPT-CLASS                                       
216100                  EXEC SQL                                                
216200                       CLOSE CNT-DEPT-CLS                                 
216300                  END-EXEC                                                
216400                                                                          
216500             WHEN PS-CNT-DEPT-NOCLASS                                     
216700                  EXEC SQL                                                
216800                       CLOSE CNT-DEPT-NOCLS                               
216900                  END-EXEC                                                
217000                                                                          
217100             WHEN PS-CNT-DEPT-STYLE                                       
217300                  EXEC SQL                                                
217400                       CLOSE CNT-DEPT-STYLE                               
217500                  END-EXEC                                                
217600                                                                          
217700             WHEN OTHER                                                   
217800                  CONTINUE                                                
217900                                                                          
218000         END-EVALUATE                                                     
218100                                                                          
218200         EVALUATE TRUE                                                    
218300             WHEN SQLCODE = ZERO                                          
218400             WHEN SQLCODE = -904                                          
218500             WHEN SQLCODE = -913                                          
218600                 CONTINUE                                                 
218700             WHEN SQLWARN0 NOT = SPACES                                   
218800             WHEN SQLCODE < ZERO                                          
218900             WHEN SQLCODE > ZERO                                          
219000                  STRING 'CLOSE '                                         
219100                          PV-CURSOR-NAME                                  
219200                         ' CURSOR'                                        
219300                          DELIMITED BY SIZE                               
219400                          INTO DP013-MESSAGE-TEXT (1)                     
219500                  MOVE SQLCA        TO DP013-SQLCA                        
219600                  MOVE 'SKU, VND' TO DP013-DB2-TABLE-NAME (1)             
219700                  MOVE 'VND_STYL' TO DP013-DB2-TABLE-NAME (2)             
219800                  SET DP013-DB2-ABEND                                     
219900                      DP013-XCTL-DISPLAY-RESTART TO TRUE                  
220000                  PERFORM DP013-0000-PROCESS-ABEND                        
220100         END-EVALUATE                                                     
220200     END-PERFORM.                                                         
220300                                                                          
220400     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
220610         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO CLOSE '           
220620                 PV-CURSOR-NAME                                           
220630                ' CURSOR'                                                 
220640                 DELIMITED BY SIZE                                        
220650                 INTO DP013-MESSAGE-TEXT (1)                              
220700         MOVE SQLCA                TO DP013-SQLCA                         
220800         MOVE 'SKU, VND'           TO DP013-DB2-TABLE-NAME (1)            
220900         MOVE 'VND_STYL'           TO DP013-DB2-TABLE-NAME (2)            
221000         SET DP013-DB2-ABEND                                              
221100             DP013-XCTL-DISPLAY-RESTART                                   
221200                                   TO TRUE                                
221300         PERFORM DP013-0000-PROCESS-ABEND                                 
221400     END-IF.                                                              
221500                                                                          
221600*----------------------------------------------------------------*        
221700*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
221800*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
221900*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
222000*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
222100*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
222200*                                                                *        
222300*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
222400*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
222500*    UPON INITIALIZATION.                                        *        
222600*----------------------------------------------------------------*        
222700                                                                          
222800 4300-READ-ITEMS-FROM-DB.                                                 
222900                                                                          
223000     PERFORM                                                              
223100         VARYING PV-READ-COUNT                                            
223200         FROM 1 BY 1                                                      
223300         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
223400            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
223500                                                                          
223600         PERFORM 4310-FETCH-COLORS                                        
223700                                                                          
223800         IF  ASC-ITEMS-LEFT-ON-DB                                         
223900             IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
224000                 ADD +1            TO ASC-ITEM-SUB                        
224100             ELSE                                                         
224200                 ADD +1            TO ASC-BLK-SUB                         
224300                 MOVE +1           TO ASC-ITEM-SUB                        
224400                                                                          
224500                 IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
224600                     MOVE +1       TO ASC-BLK-SUB                         
224700                     SET ASC-BLOCK-MODIFIED (1)                           
224800                                   TO TRUE                                
224900                     PERFORM 5100-WRITE-TEMP-STORAGE                      
225000                     MOVE ASC-BLOCK-ENTRIES                               
225100                               (PC-MAX-BLOCKS-IN-ARRAY)                   
225200                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
225300                     ADD +1        TO ASC-BLK-SUB                         
225400                     INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
225500                     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
225600                                   TO TRUE                                
225700                 END-IF                                                   
225800             END-IF                                                       
225900             ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
226000             MOVE ASC-NUMBER-OF-ITEMS-READ                                
226100                                   TO ASC-LIST-ITEM-NUMBER                
226200                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
226300             MOVE XST00010-NRF-CLOR-DESC   TO ASC-CLOR-DESC               
226400                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
226500         END-IF                                                           
226600     END-PERFORM.                                                         
226700                                                                          
226800     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
226900         MOVE +1               TO ASC-BLK-SUB                             
227000         PERFORM 5100-WRITE-TEMP-STORAGE                                  
227100         ADD +1                TO ASC-BLK-SUB                             
227200         PERFORM 5100-WRITE-TEMP-STORAGE                                  
227300                                                                          
227400         MOVE 1                TO ASC-BLK-SUB                             
227500                                  PV-BLOCK-NUMBER                         
227600         PERFORM 4410-READ-TEMP-STORAGE                                   
227700         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
227800             MOVE 2            TO ASC-BLK-SUB                             
227900                                  PV-BLOCK-NUMBER                         
228000             PERFORM 4410-READ-TEMP-STORAGE                               
228100         END-IF                                                           
228200     END-IF.                                                              
228300                                                                          
228400*----------------------------------------------------------------*        
228500*    FETCH THE CURSOR                                                     
228600*----------------------------------------------------------------*        
228700                                                                          
228800 4310-FETCH-COLORS.                                                       
228900                                                                          
229000     MOVE '4310-FETCH-COLORS'  TO DP013-PARAGRAPH.                        
229100                                                                          
229110     PERFORM                                                              
229120         WITH TEST AFTER                                                  
229130         VARYING PV-RETRY-COUNTER                                         
229140         FROM 1 BY 1                                                      
229150         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
229160                   OR  SQLCODE = ZERO                                     
229170                   OR  SQLCODE = +100)                                    
229200         EVALUATE TRUE                                                    
229300             WHEN PS-CSR-VENDOR-CLASS-COLOR                               
229500                  EXEC SQL                                                
229600                    FETCH VENDOR-CLASS-COLOR                              
229700                         INTO                                             
229800                          :XST00010-NRF-CLOR-DESC                         
229900                  END-EXEC                                                
230000                                                                          
230100             WHEN PS-CSR-VENDOR-NOCLS-COLOR                               
230300                  EXEC SQL                                                
230400                    FETCH VENDOR-NOCLS-COLOR                              
230500                         INTO                                             
230600                          :XST00010-NRF-CLOR-DESC                         
230700                  END-EXEC                                                
230800                                                                          
230900             WHEN PS-CSR-NOVND-CLASS-COLOR                                
231100                  EXEC SQL                                                
231200                    FETCH NOVND-CLASS-COLOR                               
231300                         INTO                                             
231400                          :XST00010-NRF-CLOR-DESC                         
231500                  END-EXEC                                                
231600                                                                          
231700             WHEN PS-CSR-NOVND-NOCLS-COLOR                                
231900                  EXEC SQL                                                
232000                    FETCH NOVND-NOCLS-COLOR                               
232100                         INTO                                             
232200                          :XST00010-NRF-CLOR-DESC                         
232300                  END-EXEC                                                
232400                                                                          
232500             WHEN PS-CSR-VENDOR-STYLE-COLOR                               
232700                  EXEC SQL                                                
232800                    FETCH VENDOR-STYLE-COLOR                              
232900                         INTO                                             
233000                          :XST00010-NRF-CLOR-DESC                         
233100                  END-EXEC                                                
233200                                                                          
233300             WHEN PS-CSR-NOVND-STYLE-COLOR                                
233500                  EXEC SQL                                                
233600                    FETCH NOVND-STYLE-COLOR                               
233700                         INTO                                             
233800                          :XST00010-NRF-CLOR-DESC                         
233900                  END-EXEC                                                
234000                                                                          
234100         END-EVALUATE                                                     
234200                                                                          
234300                                                                          
234400         EVALUATE TRUE                                                    
234500           WHEN SQLCODE = ZERO                                            
234600           WHEN SQLCODE = +100                                            
234700                CONTINUE                                                  
234800           WHEN SQLWARN0 NOT = SPACE                                      
234900           WHEN SQLCODE NOT = ZERO                                        
235000                STRING 'FETCH '                                           
235100                        PV-CURSOR-NAME                                    
235200                       ' CURSOR'                                          
235300                        DELIMITED BY SIZE                                 
235400                        INTO DP013-MESSAGE-TEXT (1)                       
235500                MOVE SQLCA       TO DP013-SQLCA                           
235600                SET DP013-DB2-ABEND                                       
235700                                 TO TRUE                                  
235800                PERFORM DP013-0000-PROCESS-ABEND                          
235900         END-EVALUATE                                                     
235910                                                                          
235920     END-PERFORM.                                                         
235930                                                                          
235940     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
235971         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO FETCH '           
235972                 PV-CURSOR-NAME                                           
235973                ' CURSOR'                                                 
235974                 DELIMITED BY SIZE                                        
235975                 INTO DP013-MESSAGE-TEXT (1)                              
235980         MOVE SQLCA                TO DP013-SQLCA                         
235991         SET DP013-DB2-ABEND                                              
235992             DP013-XCTL-DISPLAY-RESTART                                   
235993                                   TO TRUE                                
235994         PERFORM DP013-0000-PROCESS-ABEND                                 
235995     END-IF.                                                              
236000                                                                          
236100     IF  SQLCODE EQUAL +100                                               
236200         SET ASC-NO-ITEMS-LEFT-ON-DB  TO TRUE                             
236300     END-IF.                                                              
236400                                                                          
236500*----------------------------------------------------------------*        
236600*    FETCH THE COUNTS                                                     
236700*----------------------------------------------------------------*        
236800                                                                          
236900 4340-FETCH-COUNTS.                                                       
237000                                                                          
237100     MOVE '4340-FETCH-COUNTS'  TO DP013-PARAGRAPH.                        
237200                                                                          
237210     PERFORM                                                              
237220         WITH TEST AFTER                                                  
237230         VARYING PV-RETRY-COUNTER                                         
237240         FROM 1 BY 1                                                      
237250         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
237260                   OR  SQLCODE = ZERO                                     
237270                   OR  SQLCODE = +100)                                    
237280                                                                          
237300         EVALUATE TRUE                                                    
237400             WHEN PS-CNT-VENDOR-DEPT-CLASS                                
237600                  EXEC SQL                                                
237700                      FETCH CNT-VND-DEPT-CLS                              
237800                             INTO                                         
237900                              :PV-SKU-CNT                                 
238000                  END-EXEC                                                
238100                                                                          
238200             WHEN PS-CNT-VENDOR-DEPT-NOCLASS                              
238400                  EXEC SQL                                                
238500                      FETCH CNT-VND-DEPT-NOCLS                            
238600                             INTO                                         
238700                              :PV-SKU-CNT                                 
238800                  END-EXEC                                                
238900                                                                          
239000             WHEN PS-CNT-VENDOR-DEPT-STYLE                                
239200                  EXEC SQL                                                
239300                      FETCH CNT-VND-DEPT-STYLE                            
239400                             INTO                                         
239500                              :PV-SKU-CNT                                 
239600                  END-EXEC                                                
239700                                                                          
239800             WHEN PS-CNT-LABEL-DEPT-CLASS                                 
240000                  EXEC SQL                                                
240100                      FETCH CNT-LBL-DEPT-CLS                              
240200                             INTO                                         
240300                              :PV-SKU-CNT                                 
240400                  END-EXEC                                                
240500                                                                          
240600             WHEN PS-CNT-LABEL-DEPT-NOCLASS                               
240800                  EXEC SQL                                                
240900                      FETCH CNT-LBL-DEPT-NOCLS                            
241000                             INTO                                         
241100                              :PV-SKU-CNT                                 
241200                  END-EXEC                                                
241300                                                                          
241400             WHEN PS-CNT-LABEL-DEPT-STYLE                                 
241600                  EXEC SQL                                                
241700                      FETCH CNT-LBL-DEPT-STYLE                            
241800                             INTO                                         
241900                              :PV-SKU-CNT                                 
242000                  END-EXEC                                                
242100                                                                          
242200             WHEN PS-CNT-DEPT-CLASS                                       
242400                  EXEC SQL                                                
242500                      FETCH CNT-DEPT-CLS                                  
242600                             INTO                                         
242700                              :PV-SKU-CNT                                 
242800                  END-EXEC                                                
242900                                                                          
243000             WHEN PS-CNT-DEPT-NOCLASS                                     
243200                  EXEC SQL                                                
243300                      FETCH CNT-DEPT-NOCLS                                
243400                             INTO                                         
243500                              :PV-SKU-CNT                                 
243600                  END-EXEC                                                
243700                                                                          
243800             WHEN PS-CNT-DEPT-STYLE                                       
244000                  EXEC SQL                                                
244100                      FETCH CNT-DEPT-STYLE                                
244200                             INTO                                         
244300                              :PV-SKU-CNT                                 
244400                  END-EXEC                                                
244500                                                                          
244600             WHEN OTHER                                                   
244700                  CONTINUE                                                
244800                                                                          
244900         END-EVALUATE                                                     
245000                                                                          
245100         EVALUATE TRUE                                                    
245200           WHEN SQLCODE = ZERO                                            
245300           WHEN SQLCODE = +100                                            
245400                CONTINUE                                                  
245500           WHEN SQLWARN0 NOT = SPACE                                      
245600           WHEN SQLCODE NOT = ZERO                                        
245700                STRING 'FETCH '                                           
245800                        PV-CURSOR-NAME                                    
245900                       ' CURSOR'                                          
246000                        DELIMITED BY SIZE                                 
246100                        INTO DP013-MESSAGE-TEXT (1)                       
246200                MOVE SQLCA       TO DP013-SQLCA                           
246300                SET DP013-DB2-ABEND                                       
246400                                 TO TRUE                                  
246500                PERFORM DP013-0000-PROCESS-ABEND                          
246600         END-EVALUATE                                                     
246610                                                                          
246620     END-PERFORM.                                                         
246630                                                                          
246640     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
246671         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO FETCH '           
246672                 PV-CURSOR-NAME                                           
246673                ' CURSOR'                                                 
246674                 DELIMITED BY SIZE                                        
246675                 INTO DP013-MESSAGE-TEXT (1)                              
246680         MOVE SQLCA                TO DP013-SQLCA                         
246691         SET DP013-DB2-ABEND                                              
246692             DP013-XCTL-DISPLAY-RESTART                                   
246693                                   TO TRUE                                
246694         PERFORM DP013-0000-PROCESS-ABEND                                 
246695     END-IF.                                                              
246696                                                                          
246700                                                                          
246800*----------------------------------------------------------------*        
246900* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
247000*----------------------------------------------------------------*        
247100                                                                          
247200 4400-MOVE-COMMAREA-TO-SCREEN.                                            
247300                                                                          
247400     IF ASC-LBL-NM = SPACES                                               
247500        MOVE 'ALL' TO AVENDNMO                                            
247600     ELSE                                                                 
247700        MOVE ASC-LBL-NM TO AVENDNMO                                       
247800     END-IF.                                                              
247900                                                                          
248000     MOVE ASC-GP-NBR          TO ABUSGRPO.                                
248100     MOVE ASC-GP-DESC         TO ABUSDESO.                                
248200     MOVE ASC-SKU-CNT         TO ASKUO.                                   
248300     MOVE ASC-DEPT-NBR        TO ADEPTNOO.                                
248400     MOVE ASC-DEPT-DESC       TO ADPTDESO.                                
248500     MOVE ASC-MAJ-CL          TO AMAJORO.                                 
248600     MOVE ASC-SUB-CL          TO ASUBO.                                   
248700     MOVE ASC-SUB-CL-NM       TO ASUBDESO.                                
248800                                                                          
248900     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
249000         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
249100         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
249200             MOVE +1    TO ASC-BLK-SUB                                    
249300             PERFORM 5100-WRITE-TEMP-STORAGE                              
249400             ADD +1     TO ASC-BLK-SUB                                    
249500             PERFORM 5100-WRITE-TEMP-STORAGE                              
249600             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
249700                 GIVING PV-BLOCK-NUMBER                                   
249800                 REMAINDER PV-REMAINDER                                   
249900             IF  PV-REMAINDER > ZERO                                      
250000                 ADD +1 TO PV-BLOCK-NUMBER                                
250100             END-IF                                                       
250200             MOVE +1    TO ASC-BLK-SUB                                    
250300             PERFORM 4410-READ-TEMP-STORAGE                               
250400             ADD +1     TO ASC-BLK-SUB                                    
250500                           PV-BLOCK-NUMBER                                
250600             PERFORM 4410-READ-TEMP-STORAGE                               
250700     END-IF.                                                              
250800     MOVE +1            TO ASC-BLK-SUB.                                   
250900     COMPUTE ASC-ITEM-SUB =                                               
251000         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
251100         + PC-ITEMS-PER-PANEL).                                           
251200*                                                                         
251300     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
251400         ADD +1         TO ASC-BLK-SUB                                    
251500         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
251600     END-IF.                                                              
251700*                                                                         
251800     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
251900         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
252000     PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
252100     PERFORM 4420-FORMAT-LIST-LINES                                       
252200         VARYING PV-SUB                                                   
252300         FROM PC-ITEMS-PER-PANEL BY -1                                    
252400         UNTIL PV-SUB < +1.                                               
252500*                                                                         
252600     COMPUTE ASC-ITEMS-DISPLAYED =                                        
252700         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
252800     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
252900                                      ASTRTITO.                           
253000     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
253100     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
253200*                                                                         
253300     IF  DP020-MSG-NUMBER-X = SPACE                                       
253400         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
253500*            --- MORE ITEMS TO DISPLAY ----                               
253600             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
253700             SET DP020-MSG-INFORMATIONAL TO TRUE                          
253800         END-IF                                                           
253900     END-IF.                                                              
254000*----------------------------------------------------------------*        
254100*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
254200*----------------------------------------------------------------*        
254300                                                                          
254400 4410-READ-TEMP-STORAGE.                                                  
254500                                                                          
254600     EXEC CICS                                                            
254700         READQ TS                                                         
254800             QUEUE (ASC-LIST-QUEUE-NAME)                                  
254900             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
255000             ITEM  (PV-BLOCK-NUMBER)                                      
255100             RESP  (PV-CICS-RESP)                                         
255200     END-EXEC.                                                            
255300                                                                          
255400     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
255500       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
255600         CONTINUE                                                         
255700     ELSE                                                                 
255800         SET DP013-CICS-ABEND     TO TRUE                                 
255900         SET DP013-NO-ROLLBACK    TO TRUE                                 
256000         MOVE '4410-READ-TEMP-STORAGE'                                    
256100                                  TO DP013-PARAGRAPH                      
256200         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
256300                                  TO DP013-MESSAGE-TEXT(1)                
256400         PERFORM DP013-0000-PROCESS-ABEND                                 
256500     END-IF.                                                              
256600                                                                          
256700     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
256800*                                                                         
256900     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
257000         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
257100     END-IF.                                                              
257200 EJECT                                                                    
257300*----------------------------------------------------------------*        
257400*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
257500*----------------------------------------------------------------*        
257600                                                                          
257700 4420-FORMAT-LIST-LINES.                                                  
257800     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
257900         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
258000         MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
258100         MOVE DP015-HL-OFF       TO MR-SELECTH (PV-SUB)                   
258200         MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
258300                                    MR-CLOR-DESC (PV-SUB)                 
258400     ELSE                                                                 
258500         IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
258600                                                   > SPACE                
258700             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
258800                                 TO MR-SELECTI (PV-SUB)                   
258900         ELSE                                                             
259000             MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
259100                                 TO MR-SELECTI (PV-SUB)                   
259200         END-IF                                                           
259300*                                                                         
259400         IF  DP030-SET-CURSOR-TRAILER                                     
259500             IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
259600                 > SPACE)                                                 
259700               OR (PV-SUB = 1))                                           
259800                SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
259900                MOVE -1          TO MR-SELECTL (PV-SUB)                   
260000             END-IF                                                       
260100         END-IF                                                           
260200*                                                                         
260300         IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
260400                                                                          
260500             MOVE ASC-CLOR-DESC (ASC-BLK-SUB ASC-ITEM-SUB)                
260600                                 TO MR-CLOR-DESC (PV-SUB)                 
260700         END-IF                                                           
260800     END-IF.                                                              
260900     SUBTRACT +1 FROM ASC-ITEM-SUB                                        
261000     IF  ASC-ITEM-SUB < + 1                                               
261100         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
261200         MOVE +1                 TO ASC-BLK-SUB                           
261300     END-IF.                                                              
261400******************************************************************        
261500** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
261600** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
261700** ISSUE A WARNING.                                             **        
261800******************************************************************        
261900 4430-CHECK-FOR-BEGIN-AND-END.                                            
262000     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
262100         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
262200*            -- NO END-OF-RANGE SPECFIED ---                              
262300             MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
262400             SET DP020-MSG-WARNING TO TRUE                                
262500             PERFORM 4431-POSITION-AT-WARNING                             
262600         END-IF                                                           
262700     ELSE                                                                 
262800         IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
262900*            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
263000             MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
263100             SET DP020-MSG-WARNING TO TRUE                                
263200             PERFORM 4431-POSITION-AT-WARNING                             
263300         END-IF                                                           
263400     END-IF.                                                              
263500******************************************************************        
263600** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
263700** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
263800** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
263900** POSITION THE CURSOR THERE.                                   **        
264000******************************************************************        
264100 4431-POSITION-AT-WARNING.                                                
264200     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
264300                    - PV-TOP-ITEM + 1.                                    
264400     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
264500         CONTINUE                                                         
264600     ELSE                                                                 
264700         SET DP030-SET-CURSOR-APPL-1                                      
264800                                   TO TRUE                                
264900         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
265000         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
265100         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
265200     END-IF.                                                              
265300*                                                                         
265400     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
265500                    - PV-TOP-ITEM + 1.                                    
265600     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
265700         CONTINUE                                                         
265800     ELSE                                                                 
265900         SET DP030-SET-CURSOR-APPL-1                                      
266000                                   TO TRUE                                
266100         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
266200         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
266300         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
266400     END-IF.                                                              
266500 EJECT                                                                    
266600*----------------------------------------------------------------*        
266700* PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
266800* ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
266900* FOR THE SELECTED ITEMS QUEUE.                                  *        
267000*                                                                *        
267100* FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
267200* QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
267300* THE ARRAY IS FULL.                                             *        
267400*----------------------------------------------------------------*        
267500                                                                          
267600 5000-PREPARE-SEL-QUEUE.                                                  
267700                                                                          
267800     MOVE +1                   TO  ASC-BLK-SUB.                           
267900     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
268000     ADD +1                    TO  ASC-BLK-SUB.                           
268100     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
268200                                                                          
268300     MOVE ASC-SEL-QUEUE-NAME   TO  IQ016-SEL-QUEUE-NAME.                  
268400     PERFORM 6010-DELETE-SEL-QUEUE.                                       
268500                                                                          
268600     SET ASC-UPDATE-TSQ        TO  TRUE.                                  
268700                                                                          
268800     SET IQ014-IDX             TO  1.                                     
268900     MOVE ZERO                 TO IQ016-NUMBER-OF-TS-ITEMS.               
269000     INITIALIZE IQ014-SELECTED-ITEM-ARRAY.                                
269100                                                                          
269200     PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
269300             VARYING PV-ITEM                                              
269400             FROM 1 BY 1                                                  
269500             UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
269600                                                                          
269700     IF  IQ014-IDX > 1                                                    
269800         PERFORM 5205-WRITE-SEL-QUEUE                                     
269900         ADD +1                TO IQ016-NUMBER-OF-TS-ITEMS                
270000     END-IF.                                                              
270100 EJECT                                                                    
270200*----------------------------------------------------------------*        
270300*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
270400*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
270500*----------------------------------------------------------------*        
270600                                                                          
270700 5100-WRITE-TEMP-STORAGE.                                                 
270800     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
270900                                   TO PV-LIST-ITEM-NUMBER.                
271000     COMPUTE PV-BLOCK-NUMBER =                                            
271100         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
271200                                                                          
271300     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
271400     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
271500         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
271600             PERFORM 5105-REWRITE-ASC-LISTQ                               
271700         END-IF                                                           
271800     ELSE                                                                 
271900         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
272000         ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
272100         PERFORM 5110-WRITE-ASC-LISTQ                                     
272200         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
272300     END-IF.                                                              
272400                                                                          
272500     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
272600******************************************************************        
272700** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
272800** SPECIFIC COMMAREA.                                           **        
272900******************************************************************        
273000 5105-REWRITE-ASC-LISTQ.                                                  
273100     EXEC CICS                                                            
273200         WRITEQ TS                                                        
273300             QUEUE (ASC-LIST-QUEUE-NAME)                                  
273400             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
273500             ITEM  (PV-BLOCK-NUMBER)                                      
273600             REWRITE                                                      
273700             RESP  (PV-CICS-RESP)                                         
273800     END-EXEC.                                                            
273900                                                                          
274000     IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
274100         CONTINUE                                                         
274200     ELSE                                                                 
274300         SET DP013-CICS-ABEND            TO TRUE                          
274400         SET DP013-NO-ROLLBACK           TO TRUE                          
274500         MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
274600         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
274700                                         TO DP013-MESSAGE-TEXT(1)         
274800         PERFORM DP013-0000-PROCESS-ABEND                                 
274900     END-IF.                                                              
275000                                                                          
275100******************************************************************        
275200** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
275300** SPECIFIC COMMAREA.                                           **        
275400******************************************************************        
275500 5110-WRITE-ASC-LISTQ.                                                    
275600     EXEC CICS                                                            
275700         WRITEQ TS                                                        
275800             QUEUE (ASC-LIST-QUEUE-NAME)                                  
275900             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
276000             ITEM  (PV-BLOCK-NUMBER)                                      
276100             RESP  (PV-CICS-RESP)                                         
276200     END-EXEC.                                                            
276300     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
276400         SET DP013-CICS-ABEND      TO TRUE                                
276500         SET DP013-NO-ROLLBACK     TO TRUE                                
276600         MOVE '5110-WRITE-ASC-LISTQ'                                      
276700                                   TO DP013-PARAGRAPH                     
276800         MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
276900                                   TO DP013-MESSAGE-TEXT(1)               
277000         PERFORM DP013-0000-PROCESS-ABEND                                 
277100     END-IF.                                                              
277200 EJECT                                                                    
277300*----------------------------------------------------------------*        
277400* READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
277500* TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
277600*----------------------------------------------------------------*        
277700                                                                          
277800 5200-FILL-SEL-QUEUE-RECORD.                                              
277900                                                                          
278000     PERFORM 6112-READ-LIST-QUEUE.                                        
278100                                                                          
278200     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
278300         PERFORM                                                          
278400         VARYING LQW-IDX                                                  
278500         FROM 1 BY 1                                                      
278600         UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
278700           OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
278800                                                                          
278900             IF  LQW-SELECT (LQW-IDX)                                     
279000                 MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
279100                                   TO IQ014-ITEM (IQ014-IDX)              
279200                 MOVE LQW-CLOR-DESC (LQW-IDX)                             
279300                             TO IQ014-CLOR-DESC (IQ014-IDX)               
279400                                IQ016-CLOR-DESC                           
279500                 SET IQ014-IDX UP BY 1                                    
279600                 IF  IQ014-IDX > IQ014-MAX-ITEMS                          
279700                     PERFORM 5205-WRITE-SEL-QUEUE                         
279800                     ADD +1        TO IQ016-NUMBER-OF-TS-ITEMS            
279900                     INITIALIZE IQ014-SELECTED-ITEM-ARRAY                 
280000                     SET IQ014-IDX TO  1                                  
280100                 END-IF                                                   
280200             END-IF                                                       
280300         END-PERFORM                                                      
280400     END-IF.                                                              
280500                                                                          
280600*----------------------------------------------------------------*        
280700*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
280800*----------------------------------------------------------------*        
280900                                                                          
281000 5205-WRITE-SEL-QUEUE.                                                    
281100                                                                          
281200     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
281300                                   TO IQ014-SELECTED-ITEMS.               
281400                                                                          
281500     EXEC CICS                                                            
281600          WRITEQ TS                                                       
281700              QUEUE (ASC-SEL-QUEUE-NAME)                                  
281800              FROM  (IQ014-MESSAGE-SEL-REC)                               
281900              ITEM  (PV-SEL-QUEUE-ITEM)                                   
282000              RESP  (PV-CICS-RESP)                                        
282100     END-EXEC.                                                            
282200                                                                          
282300     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
282400         CONTINUE                                                         
282500     ELSE                                                                 
282600         SET DP013-CICS-ABEND      TO TRUE                                
282700         SET DP013-NO-ROLLBACK     TO TRUE                                
282800         MOVE '5205-WRITE-SEL-QUEUE'                                      
282900                                   TO DP013-PARAGRAPH                     
283000         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
283100                                   TO DP013-MESSAGE-TEXT(1)               
283200         PERFORM DP013-0000-PROCESS-ABEND                                 
283300     END-IF.                                                              
283400 EJECT                                                                    
283500******************************************************************        
283600** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **        
283700** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **        
283800** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **        
283900** THE USER.                                                    **        
284000******************************************************************        
284100 6000-REFRESH-PANEL-FROM-TEMP.                                            
284200     IF  ASC-UPDATE-TSQ                                                   
284300         SET ASC-NO-TSQ-UPDATE TO TRUE                                    
284400         MOVE ZERO                 TO PV-SAVE-ITEM                        
284500                                      PV-SEL-QUEUE-ITEM                   
284600         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
284700                                   TO PV-HOLD-SELECTED-ITEMS              
284800         MOVE +1                   TO PV-ITEMS-PROCESSED                  
284900         PERFORM 6100-UPDATE-TSQ                                          
285000             UNTIL PV-ITEMS-PROCESSED >                                   
285100                                     PV-HOLD-SELECTED-ITEMS               
285200         PERFORM 6010-DELETE-SEL-QUEUE                                    
285300         INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
285400                    ASC-BLOCK-ENTRIES (2)                                 
285500     END-IF.                                                              
285600                                                                          
285700*----------------------------------------------------------------*        
285800*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
285900*----------------------------------------------------------------*        
286000 6010-DELETE-SEL-QUEUE.                                                   
286100                                                                          
286200     EXEC CICS                                                            
286300         DELETEQ TS                                                       
286400             QUEUE (ASC-SEL-QUEUE-NAME)                                   
286500             RESP  (PV-CICS-RESP)                                         
286600     END-EXEC.                                                            
286700*                                                                         
286800     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
286900        (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
287000         CONTINUE                                                         
287100     ELSE                                                                 
287200         SET DP013-CICS-ABEND TO TRUE                                     
287300         SET DP013-NO-ROLLBACK TO TRUE                                    
287400         MOVE '6010-DELETE-SEL-QUEUE'                                     
287500                        TO DP013-PARAGRAPH                                
287600         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
287700                        TO DP013-MESSAGE-TEXT (1)                         
287800         PERFORM DP013-0000-PROCESS-ABEND                                 
287900     END-IF.                                                              
288000                                                                          
288100*----------------------------------------------------------------*        
288200*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
288300*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
288400*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
288500*    THE TEMP QUEUE.                                             *        
288600*----------------------------------------------------------------*        
288700                                                                          
288800 6100-UPDATE-TSQ.                                                         
288900                                                                          
289000     ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
289100     PERFORM 6105-READ-SEL-QUEUE.                                         
289200                                                                          
289300     PERFORM                                                              
289400         VARYING IQ014-IDX                                                
289500         FROM 1 BY 1                                                      
289600         UNTIL ((PV-ITEMS-PROCESSED >                                     
289700                              PV-HOLD-SELECTED-ITEMS)                     
289800         OR (IQ014-IDX GREATER THAN IQ014-MAX-ITEMS))                     
289900         PERFORM 6110-PROCESS-ITEMS                                       
290000         ADD +1                     TO PV-ITEMS-PROCESSED                 
290100     END-PERFORM.                                                         
290200     PERFORM 6111-WRITE-LIST-QUEUE.                                       
290300*----------------------------------------------------------------*        
290400*    READ THE LIST QUEUE RECORD.                                 *        
290500*----------------------------------------------------------------*        
290600                                                                          
290700 6105-READ-SEL-QUEUE.                                                     
290800                                                                          
290900     EXEC CICS                                                            
291000          READQ TS                                                        
291100              QUEUE (ASC-SEL-QUEUE-NAME)                                  
291200              INTO  (IQ014-MESSAGE-SEL-REC)                               
291300              ITEM  (PV-SEL-QUEUE-ITEM)                                   
291400              RESP  (PV-CICS-RESP)                                        
291500     END-EXEC.                                                            
291600*                                                                         
291700     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
291800         CONTINUE                                                         
291900     ELSE                                                                 
292000         SET DP013-CICS-ABEND      TO  TRUE                               
292100         SET DP013-NO-ROLLBACK     TO  TRUE                               
292200         MOVE '6105-READ-SEL-QUEUE'                                       
292300                                   TO  DP013-PARAGRAPH                    
292400         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
292500                                   TO  DP013-MESSAGE-TEXT(1)              
292600         PERFORM DP013-0000-PROCESS-ABEND                                 
292700     END-IF.                                                              
292800 EJECT                                                                    
292900*----------------------------------------------------------------*        
293000*    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
293100*    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
293200*    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
293300*----------------------------------------------------------------*        
293400                                                                          
293500 6110-PROCESS-ITEMS.                                                      
293600                                                                          
293700     COMPUTE PV-ITEM = 1 + ((IQ014-ITEM (IQ014-IDX) - 1)                  
293800                     / PC-ITEMS-PER-PANEL).                               
293900                                                                          
294000     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
294100         IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
294200             PERFORM 6111-WRITE-LIST-QUEUE                                
294300         END-IF                                                           
294400         MOVE PV-ITEM          TO PV-SAVE-ITEM                            
294500                                                                          
294600         PERFORM 6112-READ-LIST-QUEUE                                     
294700         IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
294800             CONTINUE                                                     
294900         END-IF                                                           
295000     END-IF.                                                              
295100                                                                          
295200     COMPUTE PV-SUB = (IQ014-ITEM (IQ014-IDX)                             
295300                    - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
295400     SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
295500     MOVE LQW-SELECTED-SW (PV-SUB)                                        
295600                               TO LQW-TEMP-SELECTED-SW (PV-SUB).          
295700                                                                          
295800     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
295900                                                                          
296000*----------------------------------------------------------------*        
296100*    WRITE THE TS QUEUE RECORD JUST UPDATED                      *        
296200*----------------------------------------------------------------*        
296300                                                                          
296400 6111-WRITE-LIST-QUEUE.                                                   
296500                                                                          
296600     EXEC CICS                                                            
296700         WRITEQ TS                                                        
296800             QUEUE (ASC-LIST-QUEUE-NAME)                                  
296900             FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
297000             ITEM  (PV-SAVE-ITEM)                                         
297100             REWRITE                                                      
297200             RESP  (PV-CICS-RESP)                                         
297300     END-EXEC.                                                            
297400*                                                                         
297500     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
297600         CONTINUE                                                         
297700     ELSE                                                                 
297800         SET DP013-CICS-ABEND  TO  TRUE                                   
297900         SET DP013-NO-ROLLBACK TO  TRUE                                   
298000         MOVE '6111-WRITE-LIST-QUEUE'                                     
298100                               TO  DP013-PARAGRAPH                        
298200         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
298300                               TO  DP013-MESSAGE-TEXT(1)                  
298400         PERFORM DP013-0000-PROCESS-ABEND                                 
298500     END-IF.                                                              
298600                                                                          
298700 EJECT                                                                    
298800*----------------------------------------------------------------*        
298900*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
299000*----------------------------------------------------------------*        
299100                                                                          
299200 6112-READ-LIST-QUEUE.                                                    
299300                                                                          
299400     EXEC CICS                                                            
299500         READQ TS                                                         
299600             QUEUE (ASC-LIST-QUEUE-NAME)                                  
299700             INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
299800             ITEM  (PV-ITEM)                                              
299900             RESP  (PV-CICS-RESP)                                         
300000     END-EXEC.                                                            
300100*                                                                         
300200     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
300300         CONTINUE                                                         
300400     ELSE                                                                 
300500         SET DP013-CICS-ABEND        TO TRUE                              
300600         SET DP013-NO-ROLLBACK       TO TRUE                              
300700         MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
300800         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
300900                                     TO DP013-MESSAGE-TEXT(1)             
301000         PERFORM DP013-0000-PROCESS-ABEND                                 
301100     END-IF.                                                              
301200                                                                          
301300 EJECT                                                                    
301400*----------------------------------------------------------------*        
301500*    RE-INITIALIZE ASC- FIELDS AFTER F3 RETURN                   *        
301600*----------------------------------------------------------------*        
301700 6500-REINIT-ASC-FLDS.                                                    
301800                                                                          
301900     MOVE ASC-LBL-NM       TO   IQ016-LBL-NM.                             
302000     MOVE ASC-GP-NBR       TO   IQ016-GP-NBR.                             
302100     MOVE ASC-GP-DESC      TO   IQ016-GP-DESC.                            
302200     MOVE ASC-DEPT-NBR     TO   IQ016-DEPT-NBR.                           
302300     MOVE ASC-DEPT-DESC    TO   IQ016-DEPT-DESC.                          
302400     MOVE ASC-MAJ-CL       TO   IQ016-MAJ-CL.                             
302500     MOVE ASC-SUB-CL       TO   IQ016-SUB-CL.                             
302600     MOVE ASC-SUB-CL-NM    TO   IQ016-SUB-CL-NM.                          
302700     MOVE ASC-VS-NBR       TO   IQ016-VS-NBR.                             
302800                                                                          
302900*----------------------------------------------------------------*        
303000*    ABEND PROCESSOR MODULE                                      *        
303100*----------------------------------------------------------------*        
303200                                                                          
303300     COPY DPPD013.                                                        
