      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PEKUT110.                                        
       AUTHOR.                 KELLY WIESNER.                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           APRIL 2005.                                      
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PEKUT110 - ML BALANCING                                     *        
      *                                                                *        
      *    EXTRACT PERM ACTUALS BY PRICE CHANGE TO ALLOW               *        
      *    MERCHANDISE LEDGER TO COMPARE IF WE BALANCE.                *        
      ******************************************************************        
      * HISTORY LOG:                                                            
      *----------------------------------------------------------               
      * DATE:        10/06/2005                                                 
      * WR/IR#:      WR27018 (NEW PERM)                                         
      * PROGRAMMER:  COLLEEN CUMMINGS.                                          
      * DESCRIPTION: INITIAL IMPLEMENTATION.                                    
      *                                                                         
      * DATE:        11/21/2005                                                 
      * WR/IR#:      PM00825377                                                 
      * PROGRAMMER:  ERIC ZUNKE.                                                
      * DESCRIPTION: CHANGE QUERY RESULTS TO GO TO SKU LEVEL.                   
      *                                                                         
      * DATE:        01/17/2015                                                 
      * WR/IR#:      PRB0048890                                                 
      * PROGRAMMER:  COGNIZANT.                                                 
      * CHANGES MADE:ADDED HINT TO INCREASE THE PERFORMANCE OF SELECT           
      *              QUERY.                                                     
      *                                                                         
      * DATE:        07/30/2018                                                 
      * WR/IR#:      CHG0204126                                                 
      * PROGRAMMER:  COGNIZANT                                                  
      * DESCRIPTION: ORACLE LOGON STANDARDS PROGRAM CHANGE                      
      *----------------------------------------------------------               
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATE-RANGE-FILE                                               
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT BALANCE-FILE                                                  
               ASSIGN                       TO OUT01.                           
                                                                                
           SELECT ORACLE-LOGON-FILE                                             
                  ASSIGN                    TO ORALOGON.                        
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  DATE-RANGE-FILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  DATE-RANGE-LINE                  PIC  X(80).                         
                                                                                
                                                                                
       FD  BALANCE-FILE                                                         
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  BALANCE-LINE                     PIC X(100).                         
                                                                                
       FD  ORACLE-LOGON-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
       01  OL-ORACLE-LOGON-RECORD      PIC X(80).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  CC-DATE-RANGE-FILE.                                                  
           05  CC-START-DATE                PIC X(08).                          
           05  FILLER                       PIC X(01).                          
           05  CC-END-DATE                  PIC X(08).                          
                                                                                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
006510     05  PS-END-OF-LOGON-FILE-SW         PIC X(1) VALUE 'N'.              
006530         88  PS-END-OF-LOGON-FILE                 VALUE 'Y'.              
           05  PS-ORA-SRC-CTL-DONE-SW          PIC X(1) VALUE 'N'.              
               88  PS-ORA-SRC-EMPTY                     VALUE 'Y'.              
           05  PS-DATE-RANGE-FILE-SW           PIC X(1) VALUE 'N'.              
               88  PS-DATE-RANGE-FILE-END               VALUE 'Y'.              
               88  PS-DATE-RANGE-FILE-START             VALUE 'N'.              
           05  PS-END-OF-BALANCE-CSR-SW        PIC X(1) VALUE 'N'.              
               88  PS-END-OF-BALANCE-CSR                VALUE 'Y'.              
               88  PS-NOT-END-OF-BALANCE-CSR            VALUE 'N'.              
           05  PS-END-OF-PMDSE-NS-CSR-SW       PIC X(1) VALUE 'N'.              
               88  PS-END-OF-PMDSE-NS-CSR               VALUE 'Y'.              
               88  PS-NOT-END-OF-PMDSE-NS-CSR           VALUE 'N'.              
                                                                                
       01  CC-ORACLE-CONNECTION.                                                
           05  CC-ORACLE-USERID             PIC X(40).                          
               88 CC-AUTO-LOGON                         VALUE '/'.              
           05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC  X(08)  VALUE                   
                                                           'PEKUT110'.          
           05  PC-DASH                      PIC  X(01)  VALUE '-'.              
           05  PC-SPACE                     PIC  X(01)  VALUE ' '.              
           05  PC-BEG-TMST                  PIC  X(08)  VALUE                   
                                                          '00:00:00'.           
           05  PC-END-TMST                  PIC  X(08)  VALUE                   
                                                          '23:59:59'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-ORACLE-USERID-LEN         PIC S9(4)   VALUE ZEROS             
                                                        COMP SYNC.              
           05  PV-ORACLE-PASSWORD-LEN       PIC S9(4)   VALUE ZEROS             
                                                        COMP SYNC.              
           05  PV-SQL                       PIC S9(04)  VALUE ZEROS.            
           05  PV-TABLE-NAME                PIC  X(12)  VALUE SPACES.           
           05  PV-OPERATION-MSG             PIC  X(60)  VALUE SPACES.           
           05  PV-OPERATION-MSG-1           PIC  X(40)  VALUE SPACES.           
           05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACES.           
           05  PV-NEW-STRT-DATE             PIC  X(26)     VALUE SPACES.        
           05  PV-NEW-END-DATE              PIC  X(26)     VALUE SPACES.        
           05  PV-SELECT-COUNTER       PIC  S9(04) COMP VALUE +0.               
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRY-CNT        PIC  S9(04) COMP VALUE +10.              
                                                                                
       01  PV-SELECT-VARIABLES.                                                 
           05  PV-PRCHG-ID              PIC  9(10) VALUE ZEROES.                
           05  PV-EFF-DTE               PIC  X(10) VALUE ZEROES.                
           05  PV-PRCHG-TYP-CDE         PIC  X(03) VALUE SPACES.                
           05  PV-PRCHG-SKU-NBR         PIC  9(08) VALUE ZEROES.                
           05  PV-TOT-ACT-OH-QTY        PIC  S9(10) VALUE ZEROES.               
           05  PV-TOT-ACT-PR-CHG-AMT    PIC  S9(09)V99 VALUE ZEROES.            
                                                                                
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-WRITTEN              PIC S9(11)  COMP-3                  
                                                        VALUE ZEROS.            
           05  PA-BALANCE-CNT               PIC S9(11)  COMP-3                  
                                                        VALUE ZEROS.            
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE BALANCE FILE                                        
      ******************************************************************        
           COPY PERD110.                                                        
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           SET PS-DATE-RANGE-FILE-START TO TRUE.                                
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B200-READ-DATE-RANGE-FILE.                                   
                                                                                
      *    PERFORM PE700-A000-ORACLE-SETUP.                                     
           PERFORM B210-ORACLE-SETUP.                                           
                                                                                
           PERFORM C200-DECLARE-BALANCE-CSR.                                    
           PERFORM C300-OPEN-BALANCE-CSR.                                       
           PERFORM C400-FETCH-BALANCE-CSR.                                      
           PERFORM C500-PROCESS-BALANCE-CSR                                     
               UNTIL PS-END-OF-BALANCE-CSR.                                     
           PERFORM C600-CLOSE-BALANCE-CSR.                                      
                                                                                
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           DISPLAY 'RECORDS WRITTEN TO FILE = ' PA-RECS-WRITTEN.                
           DISPLAY 'BALANCE CSR FETCHES FROM ORACLE = '                         
                                                      PA-BALANCE-CNT.           
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES.                                              *         
      * ==> PROCESSES:                                                *         
      *  - OPEN TWO INPUT FILES AND ONE OUTPUT FILE                   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  DATE-RANGE-FILE                                          
                OUTPUT BALANCE-FILE.                                            
                                                                                
      *****************************************************************         
      * B200-READ-DATE-RANGE-FILE.                                    *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE DATE RANGE FILE                                *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B200-READ-DATE-RANGE-FILE.                                               
                                                                                
           READ DATE-RANGE-FILE INTO CC-DATE-RANGE-FILE                         
              AT END                                                            
                 SET PS-DATE-RANGE-FILE-END TO TRUE                             
           END-READ.                                                            
                                                                                
           IF PS-DATE-RANGE-FILE-END                                            
               MOVE 'NO DATE RANGE FILE TO PROCESS'                             
                                                 TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-START-DATE = ' CC-START-DATE                         
               DISPLAY 'CC-END-DATE   = ' CC-END-DATE                           
                                                                                
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B210-ORACLE-SETUP.                                            *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE TWO INPUT FILES AND ONE OUTPUT FILE              *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B210-ORACLE-SETUP.                                                       
                                                                                
           OPEN INPUT ORACLE-LOGON-FILE.                                        
                                                                                
           PERFORM B220-READ-ORACLE-LOGON.                                      
                                                                                
           CLOSE ORACLE-LOGON-FILE.                                             
                                                                                
           IF ((PS-END-OF-LOGON-FILE)                                           
                   OR (CC-ORACLE-CONNECTION = SPACES))                          
               DISPLAY '** ABEND **'                                            
               DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
                         'NOT PROVIDED'                                         
               SET PS-ORA-SRC-EMPTY TO TRUE                                     
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
           IF CC-AUTO-LOGON                                                     
               DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
               EXEC SQL                                                         
                    CONNECT :USERNAME                                           
               END-EXEC                                                         
           ELSE                                                                 
               INITIALIZE PV-ORACLE-USERID-LEN                                  
                          PV-ORACLE-PASSWORD-LEN                                
               INSPECT CC-ORACLE-USERID                                         
                   TALLYING PV-ORACLE-USERID-LEN                                
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            INSPECT CC-ORACLE-PASSWORD                                          
                TALLYING PV-ORACLE-PASSWORD-LEN                                 
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
                      USERNAME-ARR                                              
            MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
            MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
                      PASSWD-ARR                                                
            MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
            DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
                      USERNAME-ARR                                              
            EXEC SQL                                                            
              CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
            END-EXEC                                                            
            INITIALIZE CC-ORACLE-PASSWORD                                       
                       PASSWD                                                   
            END-IF.                                                             
                                                                                
020500     IF (SQLCODE NOT = 0)                                                 
020900         DISPLAY PV-PARAGRAPH-NAME                                        
020910         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
021000         PERFORM Z100-ABEND                                               
021200     END-IF.                                                              
                                                                                
                                                                                
034503****************************************************************          
034504** READ THE ORACLE LOGON CONTROL CARD.                        **          
034505****************************************************************          
034506 B220-READ-ORACLE-LOGON.                                                  
                                                                                
034510     READ ORACLE-LOGON-FILE                                               
034520         INTO CC-ORACLE-CONNECTION                                        
034530         AT END                                                           
034540             SET PS-END-OF-LOGON-FILE TO TRUE                             
034550     END-READ.                                                            
034600*                                                                         
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE TWO INPUT FILES AND ONE OUTPUT FILE              *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  DATE-RANGE-FILE                                               
                  BALANCE-FILE.                                                 
                                                                                
      *****************************************************************         
      * C200-DECLARE-BALANCE-CSR.                                     *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C200-DECLARE-BALANCE-CSR.                                                
                                                                                
           EXEC SQL                                                             
                DECLARE BALANCECSR                                              
                CURSOR FOR                                                      
                   SELECT  /*+ FULL(H) PARALLEL(H) FULL(C) */                   
                    B.PRCHG_ID                                                  
                   ,D.EFF_DTE                                                   
                   ,B.PRCHG_TYP_CDE                                             
                   ,L.SKU_NBR                                                   
                   ,NVL(SUM(H.ACTL_PRCHG_OH_UNT_QTY),0) TOT_ACT_OH_QTY          
                   ,NVL(SUM(H.TOT_ACTL_PRCHG_AMT),0) TOT_ACTL_PRCHG_AMT         
                   FROM PET_PRCHG                 B,                            
                        PET_STR_GP_PRCHG_ASSN     C,                            
                        PET_PERM_EVNT             D,                            
                        PET_STR_GP_MDSE_LNE_PRICG H,                            
                        PET_PRCHG_MDSE_SELN_LNE   L,                            
                        PET_PRCHG_MDSE_SELN       M                             
                   WHERE B.PRCHG_ID             = C.PRCHG_ID                    
                     AND C.PERM_EVNT_ID         = D.PERM_EVNT_ID                
                     AND C.STR_GP_PRCHG_ASSN_ID = H.STR_GP_PRCHG_ASSN_ID        
                     AND H.PRCHG_MDSE_SELN_LNE_ID =                             
                         L.PRCHG_MDSE_SELN_LNE_ID                               
                     AND L.PRCHG_MDSE_SELN_ID   = M.PRCHG_MDSE_SELN_ID          
                     AND M.MDSE_HIER_CDE        = 'SKU'                         
                     AND B.PRCHG_ST_CDE         = 'APR'                         
                     AND D.EFF_DTE  >=                                          
                         TO_DATE(:CC-START-DATE, 'YYYYMMDD')                    
                     AND D.EFF_DTE <=                                           
                         TO_DATE(:CC-END-DATE, 'YYYYMMDD')                      
                     AND B.STAT_CDE             = 'S'                           
                     AND C.STAT_CDE             = 'S'                           
                     AND D.STAT_CDE             = 'S'                           
                     AND H.STAT_CDE             = 'S'                           
                     AND L.STAT_CDE             = 'S'                           
                     AND M.STAT_CDE             = 'S'                           
                   GROUP BY                                                     
                      B.PRCHG_ID,                                               
                      D.EFF_DTE,                                                
                      B.PRCHG_TYP_CDE,                                          
                      L.SKU_NBR                                                 
                                                                                
                   UNION                                                        
                                                                                
                   SELECT  /*+ FULL(H) PARALLEL(H) FULL(C) */                   
                    B.PRCHG_ID                                                  
                   ,D.EFF_DTE                                                   
                   ,B.PRCHG_TYP_CDE                                             
                   ,N.SKU_NBR                                                   
                   ,NVL(SUM(P.ACTL_PRCHG_OH_UNT_QTY),0) TOT_ACT_OH_QTY          
                   ,NVL(SUM(P.TOT_ACTL_PRCHG_AMT),0) TOT_ACTL_PRCHG_AMT         
                   FROM PET_PRCHG                 B,                            
                        PET_STR_GP_PRCHG_ASSN     C,                            
                        PET_PERM_EVNT             D,                            
                        PET_STR_GP_MDSE_LNE_PRICG H,                            
                        PET_PRCHG_MDSE_SELN_LNE   L,                            
                        PET_PRCHG_MDSE_SELN       M,                            
                        PET_PRCHG_SKU_SUBLNE      N,                            
                        PET_PRCHG_SKU_STR_GP      P                             
                   WHERE B.PRCHG_ID               = C.PRCHG_ID                  
                     AND C.PERM_EVNT_ID           = D.PERM_EVNT_ID              
                     AND C.STR_GP_PRCHG_ASSN_ID   =                             
                         H.STR_GP_PRCHG_ASSN_ID                                 
                     AND H.PRCHG_MDSE_SELN_LNE_ID =                             
                         L.PRCHG_MDSE_SELN_LNE_ID                               
                     AND L.PRCHG_MDSE_SELN_ID     = M.PRCHG_MDSE_SELN_ID        
                     AND L.PRCHG_MDSE_SELN_LNE_ID =                             
                         N.PRCHG_MDSE_SELN_LNE_ID                               
                     AND N.PRCHG_SKU_SUBLNE_ID    =                             
                         P.PRCHG_SKU_SUBLNE_ID                                  
                     AND P.STR_GP_MDSE_LNE_PRICG_ID =                           
                         H.STR_GP_MDSE_LNE_PRICG_ID                             
                     AND M.MDSE_HIER_CDE        = 'STY'                         
                     AND B.PRCHG_ST_CDE         = 'APR'                         
                     AND D.EFF_DTE  >=                                          
                         TO_DATE(:CC-START-DATE, 'YYYYMMDD')                    
                     AND D.EFF_DTE <=                                           
                         TO_DATE(:CC-END-DATE, 'YYYYMMDD')                      
                     AND B.STAT_CDE             = 'S'                           
                     AND C.STAT_CDE             = 'S'                           
                     AND D.STAT_CDE             = 'S'                           
                     AND H.STAT_CDE             = 'S'                           
                     AND L.STAT_CDE             = 'S'                           
                     AND M.STAT_CDE             = 'S'                           
                     AND N.STAT_CDE             = 'S'                           
                     AND P.STAT_CDE             = 'S'                           
                   GROUP BY                                                     
                      B.PRCHG_ID,                                               
                      D.EFF_DTE,                                                
                      B.PRCHG_TYP_CDE,                                          
                      N.SKU_NBR                                                 
                   ORDER BY 1, 4                                                
                 END-EXEC.                                                      
      ***************************************************************           
      * C300-OPEN-BALANCE-CSR.                                      *           
      * ==> PROCESSES:                                              *           
      *  - OPEN BALANCECSR                                          *           
      * ==> SQLCODE:                                                *           
      *  - 0 = SUCCESSFUL OPEN                                      *           
      * ==> PERFORMED FROM: A100-MAINLINE                           *           
      ***************************************************************           
       C300-OPEN-BALANCE-CSR.                                                   
                                                                                
           EXEC SQL                                                             
                 OPEN BALANCECSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C300-OPEN-BALANCE-CSR      '                           
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON BALANCECSR'                              
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C400-FETCH-BALANCE-CSR.                                       *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      *  - THEN MOVE TO THE COPYBOOK                                  *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C500-PROCESS-BALANCE-CSR *           
      *****************************************************************         
       C400-FETCH-BALANCE-CSR.                                                  
                                                                                
           EXEC SQL                                                             
               FETCH BALANCECSR                                                 
                INTO  :PV-PRCHG-ID,                                             
                      :PV-EFF-DTE,                                              
                      :PV-PRCHG-TYP-CDE,                                        
                      :PV-PRCHG-SKU-NBR,                                        
                      :PV-TOT-ACT-OH-QTY,                                       
                      :PV-TOT-ACT-PR-CHG-AMT                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-BALANCE-CNT                                      
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'BALANCE COUNT FROM FETCH = '                        
                                                  PA-BALANCE-CNT                
                   DISPLAY 'END OF BALANCE CSR'                                 
                   SET PS-END-OF-BALANCE-CSR TO TRUE                            
                                                                                
               WHEN OTHER                                                       
                   MOVE 'C400-FETCH-BALANCE-CSR        '                        
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON BALANCECSR  '                           
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C500-PROCESS-BALANCE-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DATA TO THE FILE                                 *         
      *  - REINITALIZE THE VARIABLES TO START FROM SCRATCH            *         
      *  - FETCH THE NEXT ROW IN THE SELECT STATMENT                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C500-PROCESS-BALANCE-CSR.                                                
                                                                                
           PERFORM F100-MOVE-TO-BALANCE-CPYBK.                                  
           PERFORM W100-WRITE-DETAIL.                                           
           INITIALIZE PV-SELECT-VARIABLES.                                      
           PERFORM C400-FETCH-BALANCE-CSR.                                      
                                                                                
      *****************************************************************         
      * C600-CLOSE-BALANCE-CSR.                                       *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE BALANCECSR                                       *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = CLOSE WAS SUCCESSFUL                                *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C600-CLOSE-BALANCE-CSR.                                                  
                                                                                
           EXEC SQL                                                             
                CLOSE BALANCECSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C600-CLOSE-BALANCE-CSR      '                           
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON BALANCECSR    '                          
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * F100-MOVE-TO-BALANCE-CPYBK                                    *         
      * ==> PROCESSES:                                                *         
      *  - MOVE THE VARIABLES TO THE COPYBOOK                         *         
      * ==> PERFORMED FROM: C400-FETCH-BALANCE-CSR                    *         
      *****************************************************************         
       F100-MOVE-TO-BALANCE-CPYBK.                                              
                                                                                
           INITIALIZE PE110-RECORD.                                             
           MOVE PV-PRCHG-ID           TO  PE110-PRCHG-ID.                       
           MOVE PV-EFF-DTE            TO  PE110-PRCHG-EFF-DTE.                  
           MOVE PV-PRCHG-TYP-CDE      TO  PE110-PRCHG-TYP-CDE.                  
           MOVE PV-PRCHG-SKU-NBR      TO  PE110-PRCHG-SKU-NBR.                  
           MOVE PV-TOT-ACT-OH-QTY     TO  PE110-TOT-ACT-OH-QTY.                 
           IF PV-PRCHG-TYP-CDE = 'CTA' OR 'MUA'                                 
               COMPUTE PV-TOT-ACT-PR-CHG-AMT =                                  
                       PV-TOT-ACT-PR-CHG-AMT * -1                               
           END-IF.                                                              
           MOVE PV-TOT-ACT-PR-CHG-AMT TO  PE110-TOT-ACT-PR-CHG-AMT.             
                                                                                
      *****************************************************************         
      * W100-WRITE-DETAIL.                                            *         
      * ==> PROCESSES:                                                *         
      *  - WRITE THE DETAIL LINE TO THE BALANCE OUTPUT FILE           *         
      * ==> PERFORMED FROM: C500-PROCESS-BALANCE-CSR                  *         
      *****************************************************************         
       W100-WRITE-DETAIL.                                                       
                                                                                
           WRITE BALANCE-LINE FROM PE110-RECORD.                                
                                                                                
           ADD +1                    TO PA-RECS-WRITTEN.                        
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
      *    COPY PEPD700.                                                        
                                                                                
      *****************************************************************         
      * Z100-ABEND                                                    *         
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE            *         
      *****************************************************************         
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                           
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
