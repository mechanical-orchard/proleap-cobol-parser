       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.      XMKRP010.                                               
       AUTHOR.          R. MCDONALD                                             
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    10/13/2004.                                             
       DATE-COMPILED.                                                           
      ******************************************************************        
      **XM DOMAIN TABLE INTEGRITY CHECK - FOR TABLE XMT_PRCHG                   
      ******************************************************************        
      **THIS PROGRAM VALIDATES THE DATA IN THE XM DOMAIN IS THE SAME            
      **AS THE DATA IN THE SOR                                                  
      **   CREATED FOR NEW PERM SYSTEM WORK WR27472                             
      ******************************************************************        
      * 02/21/2005  MADE BY: PAUL KEBER          WR# 27472                      
      * CHANGE: CORRECTED LOGIC FOR A NULL DEPARTMENT NUMBER.                   
      ******************************************************************        
      * DATE:        07/26/2010                                                 
      * WR/IR#:      WR36734                                                    
      * PROGRAMMER:  DAVE METZGER                                               
      * DESCRIPTION: REMOVED TMST VALIDATION CHECK AS PART OF THE               
      *              MDO SPRING 2010 PROJECT DUE TO MESSAGING                   
      *              CHANGES FROM PERM TO XM.                                   
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SOR-PRCHG-EXTRACT-FILE                                        
                  ASSIGN TO INP01.                                              
           SELECT XM-PRCHG-EXTRACT-FILE                                         
                  ASSIGN TO INP02.                                              
           SELECT XM-PRCHG-INTEG-RPT-FILE                                       
                  ASSIGN TO RPT01.                                              
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  SOR-PRCHG-EXTRACT-FILE                                               
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS SOR-PRCHG-EXTRACT-RECORD.                             
       01  SOR-PRCHG-EXTRACT-RECORD        PIC X(200).                          
                                                                                
       FD  XM-PRCHG-EXTRACT-FILE                                                
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS XM-PRCHG-EXTRACT-RECORD.                              
       01  XM-PRCHG-EXTRACT-RECORD         PIC X(174).                          
                                                                                
       FD  XM-PRCHG-INTEG-RPT-FILE                                              
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0  RECORDS.                                           
       01  XM-PRCHG-INTEG-RPT-RECORD       PIC  X(200).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                              PIC X(41)                        
           VALUE 'WORKING STORAGE STARTS HERE FOR XMKRP010'.                    
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-SOR-PRCHG-SW          PIC X(01) VALUE 'N'.             
               88  PS-END-OF-SOR-PRCHG                   VALUE 'Y'.             
               88  PS-NOT-END-OF-SOR-PRCHG               VALUE 'N'.             
           05  PS-END-OF-XM-PRCHG-SW           PIC X(01) VALUE 'N'.             
               88  PS-END-OF-XM-PRCHG                    VALUE 'Y'.             
               88  PS-NOT-END-OF-XM-PRCHG                VALUE 'N'.             
           05  PS-ERROR-FOUND-SW               PIC X(01) VALUE 'N'.             
               88  PS-ERROR-FOUND                        VALUE 'Y'.             
               88  PS-NO-ERROR-FOUND                     VALUE 'N'.             
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)  VALUE                 
                                                          'XMKRP010'.           
           05  PC-REPORT-NBR-1                 PIC  9(01) VALUE 1.              
           05  PC-ONE                          PIC  9(01) VALUE 1.              
           05  PC-NULL                         PIC S9(04) VALUE -1.             
           05  PC-MAX-REPORT-LINES             PIC S9(05) VALUE +74             
                                                          COMP-3.               
                                                                                
           05  PC-NO-ERROR-MESSAGE             PIC  X(200) VALUE                
               'NO ERRORS'.                                                     
                                                                                
           05  PC-MISSING-FROM-SOR-PRCHG       PIC  X(50) VALUE                 
               'RECORD NOT FOUND ON SOR PRCHG EXTRACT.            '.            
                                                                                
           05  PC-MISSING-FROM-XM-PRCHG        PIC  X(50) VALUE                 
               'RECORD NOT FOUND ON XM PRCHG EXTRACT.             '.            
                                                                                
           05  PC-EXTRA-FOUND-SOR-PRCHG        PIC  X(50) VALUE                 
               'ADDITIONAL DATA FOUND FOR SOR PRCHG EXTRACT.      '.            
                                                                                
           05  PC-EXTRA-FOUND-XM-PRCHG        PIC  X(50) VALUE                  
               'ADDITIONAL DATA FOUND ON XM PRCHG EXTRACT.        '.            
                                                                                
           05  PC-FIELD-DOES-NOT-MATCH        PIC  X(25) VALUE                  
               ' - FIELD DOES NOT MATCH. '.                                     
                                                                                
                                                                                
       01  PV-MISCELANEOUS.                                                     
           05  PV-TEMP-PRCHG-ID                PIC S9(11) COMP                  
                                                          VALUE ZEROS.          
           05  PV-PRINT-RECORD                 PIC X(200) VALUE SPACE.          
           05  PV-SAVE-PRINT-RECORD            PIC X(200) VALUE SPACE.          
           05  PV-LINE-SPACING                 PIC S9(04) VALUE ZEROES          
                                                          COMP SYNC.            
           05  PV-CURR-TIME.                                                    
              10  PV-CURR-TIME-HH              PIC  9(02) VALUE ZEROES.         
              10  PV-CURR-TIME-MM              PIC  9(02) VALUE ZEROES.         
              10  PV-CURR-TIME-SSHH            PIC  9(04) VALUE ZEROES.         
                                                                                
           05  PV-CURR-DATE-CCYYMMDD.                                           
               10  PV-CURR-DATE-CCYY           PIC  9(04) VALUE ZEROES.         
               10  PV-CURR-DATE-MM             PIC  9(02) VALUE ZEROES.         
               10  PV-CURR-DATE-DD             PIC  9(02) VALUE ZEROES.         
                                                                                
       01  PV-PROGRAM-COUNTER.                                                  
           05  PV-PAGE-CNT                     PIC  9(05) VALUE ZEROS.          
           05  PV-LINE-CNT                     PIC S9(07) VALUE ZEROS.          
           05  PV-SOR-PRCHG-RECS-READ-CNT      PIC  9(09) VALUE ZEROS.          
           05  PV-XM-EXTRACT-REC-READ-CNT      PIC  9(09) VALUE ZEROS.          
           05  PV-SOR-PRCHG-RECS-UNIQUE        PIC  9(09) VALUE ZEROS.          
           05  PV-XM-EXTRACT-REC-UNIQUE        PIC  9(09) VALUE ZEROS.          
           05  PV-SOR-XM-COMMON-CNT            PIC  9(09) VALUE ZEROS.          
           05  PV-SOR-XM-TOTAL-CNT             PIC  9(09) VALUE ZEROS.          
           05  PV-ERROR-RECORD-WRITTEN-CNT     PIC  9(09) VALUE ZEROS.          
           05  PV-VALID-MATCH-CNT              PIC  9(09) VALUE ZEROS.          
           05  PV-TOTAL-ERROR-CNT              PIC  9(09) VALUE ZEROS.          
           05  PV-RECORD-ERR-CNT               PIC  9(09) VALUE ZEROS.          
                                                                                
      ******************************************************************        
      *  TOP OF PAGE HEADING LINE FOR PRINTING A 200 CHARACTER REPORT.          
      ******************************************************************        
           COPY DPWS200.                                                        
                                                                                
       01  HL2-HEADING-LINE-2.                                                  
           05  FILLER                  PIC X(85)   VALUE SPACES.                
           05  FILLER                  PIC X(31)   VALUE                        
               'XMT_PRCHG DATA INTEGRITY ERRORS'.                               
           05  FILLER                  PIC X(84)   VALUE SPACES.                
                                                                                
       01  HL3-HEADING-LINE-3.                                                  
           05  FILLER                  PIC X(02)   VALUE SPACES.                
           05  FILLER                  PIC X(50)   VALUE                        
      ********* 123456789-123456789-123456789-123456789-123456789-              
               'PRCHG ID                                          '.            
           05  FILLER                  PIC X(02)   VALUE SPACES.                
           05  FILLER                  PIC X(40)   VALUE                        
      ********* 123456789-123456789-123456789-123456789-                        
               'SOR VALUE                               '.                      
           05  FILLER                  PIC X(02)   VALUE SPACES.                
           05  FILLER                  PIC X(40)   VALUE                        
      ********* 123456789-123456789-123456789-123456789-                        
               'XM VALUE                                '.                      
           05  FILLER                  PIC X(02)   VALUE SPACES.                
           05  FILLER                  PIC X(50)   VALUE                        
      ********* 123456789-123456789-123456789-123456789-123456789-              
               'ERROR MESSAGE                                     '.            
           05  FILLER                  PIC X(12)   VALUE SPACES.                
                                                                                
       01  HL4-HEADING-LINE-4.                                                  
           05  FILLER                  PIC X(02)   VALUE SPACES.                
           05  FILLER                  PIC X(50)   VALUE                        
      ********* 123456789-123456789-123456789-123456789-123456789-              
               '--------------------------------------------------'.            
           05  FILLER                  PIC X(02)   VALUE SPACES.                
           05  FILLER                  PIC X(40)   VALUE                        
      ********* 123456789-123456789-123456789-123456789-                        
               '----------------------------------------'.                      
           05  FILLER                  PIC X(02)   VALUE SPACES.                
           05  FILLER                  PIC X(40)   VALUE                        
      ********* 123456789-123456789-123456789-123456789-                        
               '----------------------------------------'.                      
           05  FILLER                  PIC X(02)   VALUE SPACES.                
           05  FILLER                  PIC X(50)   VALUE                        
      ********* 123456789-123456789-123456789-123456789-123456789-              
               '--------------------------------------------------'.            
           05  FILLER                  PIC X(12)   VALUE SPACES.                
                                                                                
      ******************************************************************        
      *  DETAIL LINE LAYOUT FOR THE ERROR REPORT.                               
      ******************************************************************        
       01  DL1-DETAIL-LINE-1.                                                   
           05  FILLER                  PIC X(02)     VALUE SPACES.              
           05  DL1-KEY.                                                         
               10  DL1-PRCHG-ID        PIC Z(10)     VALUE ZEROES.              
               10  FILLER              PIC X(40)     VALUE SPACES.              
           05  FILLER                  PIC X(02)     VALUE SPACES.              
           05  DL1-SOR-VALUE           PIC X(40)     VALUE SPACES.              
           05  FILLER                  PIC X(02)     VALUE SPACES.              
           05  DL1-XM-VALUE            PIC X(40)     VALUE SPACES.              
           05  FILLER                  PIC X(02)     VALUE SPACES.              
           05  DL1-ERROR-MESSAGE.                                               
               10  DL1-ERROR-FIELD     PIC X(25)     VALUE SPACES.              
               10  DL1-ERROR-MSG       PIC X(25)     VALUE SPACES.              
           05  FILLER                  PIC X(12)     VALUE SPACES.              
                                                                                
                                                                                
      ******************************************************************        
      *  TRAILER LAYOUT FOR THE ERROR REPORT.                                   
      ******************************************************************        
       01  TR1-TRAILER-LINE-1.                                                  
           05  FILLER                  PIC X(25)   VALUE                        
               ' TOTAL NUMBER OF ERRORS: '.                                     
           05  TR1-TOTAL-ERROR-CNT     PIC Z(09)   VALUE ZEROS.                 
           05  FILLER                  PIC X(166)  VALUE SPACES.                
                                                                                
       01  TR2-TRAILER-LINE-2.                                                  
           05  FILLER                  PIC X(25)   VALUE                        
               ' NOT FOUND ON SOR      : '.                                     
           05  TR2-SOR-MISSING-CNT     PIC Z(09)   VALUE ZEROS.                 
           05  FILLER                  PIC X(166)  VALUE SPACES.                
                                                                                
       01  TR3-TRAILER-LINE-3.                                                  
           05  FILLER                  PIC X(25)   VALUE                        
               ' NOT FOUND ON XM       : '.                                     
           05  TR3-XM-MISSING-CNT      PIC Z(09)   VALUE ZEROS.                 
           05  FILLER                  PIC X(166)  VALUE SPACES.                
                                                                                
       01  TR4-TRAILER-LINE-4.                                                  
           05  FILLER                  PIC X(25)   VALUE                        
               ' RECORD LEVEL ERRORS   : '.                                     
           05  TR4-RECORD-ERR-CNT      PIC Z(09)   VALUE ZEROS.                 
           05  FILLER                  PIC X(166)  VALUE SPACES.                
                                                                                
      ******************************************************************        
      **COPYBOOKS                                                               
      ******************************************************************        
                                                                                
      ***SOR EXTRACT FILE LAYOUT                                                
           COPY PERD103.                                                        
                                                                                
      ***XM EXTRACT FILE LAYOUT                                                 
           COPY XMRD010.                                                        
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-RECORDS                                         
             UNTIL PS-END-OF-SOR-PRCHG                                          
                OR PS-END-OF-XM-PRCHG.                                          
                                                                                
           PERFORM 2200-PROCESS-EXTRA-RECORDS                                   
             UNTIL PS-END-OF-SOR-PRCHG                                          
               AND PS-END-OF-XM-PRCHG.                                          
                                                                                
           IF PV-ERROR-RECORD-WRITTEN-CNT = 0                                   
               MOVE PC-NO-ERROR-MESSAGE TO DL1-DETAIL-LINE-1                    
               MOVE DL1-DETAIL-LINE-1   TO PV-PRINT-RECORD                      
               MOVE +2                  TO PV-LINE-SPACING                      
               PERFORM 8900-WRITE-REPORT-LINE                                   
           ELSE                                                                 
               MOVE PV-ERROR-RECORD-WRITTEN-CNT TO                              
                      TR4-RECORD-ERR-CNT                                        
               MOVE TR4-TRAILER-LINE-4  TO PV-PRINT-RECORD                      
               MOVE +2                  TO PV-LINE-SPACING                      
               PERFORM 8900-WRITE-REPORT-LINE                                   
                                                                                
               MOVE PV-XM-EXTRACT-REC-UNIQUE   TO                               
                      TR2-SOR-MISSING-CNT                                       
               MOVE TR2-TRAILER-LINE-2  TO PV-PRINT-RECORD                      
               MOVE +1                  TO PV-LINE-SPACING                      
               PERFORM 8900-WRITE-REPORT-LINE                                   
                                                                                
               MOVE PV-SOR-PRCHG-RECS-UNIQUE    TO                              
                      TR3-XM-MISSING-CNT                                        
               MOVE TR3-TRAILER-LINE-3  TO PV-PRINT-RECORD                      
               PERFORM 8900-WRITE-REPORT-LINE                                   
                                                                                
               MOVE PV-TOTAL-ERROR-CNT          TO                              
                      TR1-TOTAL-ERROR-CNT                                       
               MOVE TR1-TRAILER-LINE-1  TO PV-PRINT-RECORD                      
               PERFORM 8900-WRITE-REPORT-LINE                                   
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 9000-END-OF-PGM.                                             
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *1000-INITIALIZATION.                                                     
      * INITIALIZATION AREA                                                     
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           INITIALIZE PV-PROGRAM-COUNTER                                        
                      PE103-RECORD                                              
                      XM010-PRCHG-UNLOAD.                                       
                                                                                
           OPEN INPUT  SOR-PRCHG-EXTRACT-FILE                                   
                       XM-PRCHG-EXTRACT-FILE.                                   
                                                                                
           OPEN OUTPUT XM-PRCHG-INTEG-RPT-FILE.                                 
                                                                                
           PERFORM 7000-READ-SOR-PRCHG-EXTRACT                                  
           PERFORM 7100-READ-XM-PRCHG-EXTRACT.                                  
                                                                                
           ACCEPT PV-CURR-TIME FROM TIME.                                       
           MOVE PV-CURR-TIME-HH          TO DP200-RUN-HOUR.                     
           MOVE PV-CURR-TIME-MM          TO DP200-RUN-MINUTE.                   
                                                                                
           MOVE FUNCTION CURRENT-DATE (1:8)                                     
                                         TO PV-CURR-DATE-CCYYMMDD.              
                                                                                
           MOVE PV-CURR-DATE-MM          TO DP200-RUN-MONTH.                    
           MOVE PV-CURR-DATE-DD          TO DP200-RUN-DAY.                      
           MOVE PV-CURR-DATE-CCYY        TO DP200-RUN-YEAR.                     
                                                                                
           MOVE PC-CURRENT-PROGRAM-NAME  TO DP200-PROGRAM-NAME.                 
                                                                                
           PERFORM 8700-REPORT-HEADER-ROUTINE.                                  
                                                                                
      ******************************************************************        
      *2000-PROCESS-RECORDS.                                                    
      * THIS SECTION COMPARES RECORDS ON THE SOR PRICE CHANGE FILE &            
      * XM DOMAIN PRCHG EXTRACT FILE. IF KEYS MATCH, PROGRAM LOOKS AT           
      * DETAIL FIELDS AND CHECKS THAT THEY MATCH, DIFFERENCES ARE               
      * REPORTED. IF THE KEY (PRCHG ID) DOES NOT APPEAR ON ONE FILE,            
      * BUT IS ON THE OTHER FILE THEN AN APPROPRIATE MESSAGE WILL BE            
      * WRITTEN TO THE ERROR FILE, ALONG WITH THE DATA THAT WAS ON              
      * THE CORRESPONDING FILES.                                                
      ******************************************************************        
       2000-PROCESS-RECORDS.                                                    
                                                                                
           INITIALIZE DL1-DETAIL-LINE-1.                                        
                                                                                
      **   DISPLAY '>>> 2000-PROCESS KEYS ARE SOR*'                             
      **           PE103-PRCHG-ID,                                              
      **              '== AND XM*'                                              
      **           XM010-PRCHG-ID, '**'                                         
           EVALUATE TRUE                                                        
              WHEN PE103-PRCHG-ID = XM010-PRCHG-ID                              
                   ADD +1 TO PV-SOR-XM-COMMON-CNT                               
                   PERFORM 2100-COMPARE-FIELDS                                  
                   PERFORM 7000-READ-SOR-PRCHG-EXTRACT                          
                   PERFORM 7100-READ-XM-PRCHG-EXTRACT                           
              WHEN PE103-PRCHG-ID < XM010-PRCHG-ID                              
      ***     DISPLAY '=== SOR LOWER *'                                         
      ***          PE103-PRCHG-ID, '-', PE103-PRCHG-ID                          
      ***             '== AND XM*'                                              
      ***          XM010-PRCHG-ID, '-', XM010-PRCHG, '**'                       
                   PERFORM 2400-MOVE-SOR-VALUES                                 
                   MOVE PC-MISSING-FROM-XM-PRCHG                                
                      TO DL1-ERROR-MESSAGE                                      
                   MOVE DL1-DETAIL-LINE-1 TO PV-PRINT-RECORD                    
                   ADD +1 TO PV-ERROR-RECORD-WRITTEN-CNT                        
                   ADD +1 TO PV-SOR-PRCHG-RECS-UNIQUE                           
                   ADD +1 TO PV-TOTAL-ERROR-CNT                                 
                   PERFORM 8900-WRITE-REPORT-LINE                               
                   PERFORM 7000-READ-SOR-PRCHG-EXTRACT                          
              WHEN PE103-PRCHG-ID > XM010-PRCHG-ID                              
                   PERFORM 2500-MOVE-XM-VALUES                                  
                   MOVE PC-MISSING-FROM-SOR-PRCHG                               
                      TO DL1-ERROR-MESSAGE                                      
                   MOVE DL1-DETAIL-LINE-1 TO PV-PRINT-RECORD                    
                   ADD +1 TO PV-ERROR-RECORD-WRITTEN-CNT                        
                   ADD +1 TO PV-XM-EXTRACT-REC-UNIQUE                           
                   ADD +1 TO PV-TOTAL-ERROR-CNT                                 
                   PERFORM 8900-WRITE-REPORT-LINE                               
                   PERFORM 7100-READ-XM-PRCHG-EXTRACT                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *2100-COMPARE-FIELDS.                                                     
      * COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST           
      ******************************************************************        
       2100-COMPARE-FIELDS.                                                     
                                                                                
           SET PS-NO-ERROR-FOUND                   TO TRUE.                     
                                                                                
           INITIALIZE DL1-DETAIL-LINE-1.                                        
                                                                                
           IF  PE103-PRCHG-DESC NOT EQUAL XM010-PRCHG-DESC                      
              IF  PE103-PRCHG-DESC-NI EQUAL PC-NULL                             
               AND XM010-PRCHG-DESC-IS-NULL                                     
                  CONTINUE                                                      
              ELSE                                                              
                  SET PS-ERROR-FOUND               TO TRUE                      
                  ADD +1                           TO PV-TOTAL-ERROR-CNT        
                  MOVE PE103-PRCHG-ID              TO DL1-PRCHG-ID              
                  MOVE PE103-PRCHG-DESC            TO DL1-SOR-VALUE             
                  MOVE XM010-PRCHG-DESC            TO DL1-XM-VALUE              
                  MOVE 'PRCH_DESC                ' TO DL1-ERROR-FIELD           
                  MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG             
                  MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD           
                  PERFORM 8900-WRITE-REPORT-LINE                                
                  INITIALIZE DL1-DETAIL-LINE-1                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF XM010-DEPT-NBR-IS-NULL                                            
               MOVE +0 TO XM010-DEPT-NBR                                        
           END-IF.                                                              
                                                                                
           IF  PE103-DEPT-NBR NOT EQUAL XM010-DEPT-NBR                          
              IF  PE103-DEPT-NBR-NI EQUAL PC-NULL                               
               AND XM010-DEPT-NBR-IS-NULL                                       
                  CONTINUE                                                      
              ELSE                                                              
                  SET PS-ERROR-FOUND  TO TRUE                                   
                  ADD +1                           TO PV-TOTAL-ERROR-CNT        
                  MOVE PE103-PRCHG-ID              TO DL1-PRCHG-ID              
                  MOVE PE103-DEPT-NBR              TO DL1-SOR-VALUE             
                  MOVE XM010-DEPT-NBR              TO DL1-XM-VALUE              
                  MOVE 'DEPT_NBR                 ' TO DL1-ERROR-FIELD           
                  MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG             
                  MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD           
                  PERFORM 8900-WRITE-REPORT-LINE                                
                  INITIALIZE DL1-DETAIL-LINE-1                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF  PE103-EVNT-CTG-CDE NOT EQUAL XM010-EVNT-CTG-CDE                  
               SET PS-ERROR-FOUND  TO TRUE                                      
               ADD +1                           TO PV-TOTAL-ERROR-CNT           
               MOVE PE103-PRCHG-ID              TO DL1-PRCHG-ID                 
               MOVE PE103-EVNT-CTG-CDE          TO DL1-SOR-VALUE                
               MOVE XM010-EVNT-CTG-CDE          TO DL1-XM-VALUE                 
               MOVE 'EVNT_CTG_CDE             ' TO DL1-ERROR-FIELD              
               MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG                
               MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD              
               PERFORM 8900-WRITE-REPORT-LINE                                   
               INITIALIZE DL1-DETAIL-LINE-1                                     
           END-IF.                                                              
                                                                                
           IF  PE103-PRCHG-TYP-CDE NOT EQUAL XM010-PRCHG-TYP-CDE                
               SET PS-ERROR-FOUND  TO TRUE                                      
               ADD +1                           TO PV-TOTAL-ERROR-CNT           
               MOVE PE103-PRCHG-ID              TO DL1-PRCHG-ID                 
               MOVE PE103-PRCHG-TYP-CDE         TO DL1-SOR-VALUE                
               MOVE XM010-PRCHG-TYP-CDE         TO DL1-XM-VALUE                 
               MOVE 'PRCHG_TYP_CDE            ' TO DL1-ERROR-FIELD              
               MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG                
               MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD              
               PERFORM 8900-WRITE-REPORT-LINE                                   
               INITIALIZE DL1-DETAIL-LINE-1                                     
           END-IF.                                                              
                                                                                
           IF  PE103-PRCHG-ST-CDE NOT EQUAL XM010-PRCHG-ST-CDE                  
               SET PS-ERROR-FOUND  TO TRUE                                      
               ADD +1                           TO PV-TOTAL-ERROR-CNT           
               MOVE PE103-PRCHG-ID              TO DL1-PRCHG-ID                 
               MOVE PE103-PRCHG-ST-CDE          TO DL1-SOR-VALUE                
               MOVE XM010-PRCHG-ST-CDE          TO DL1-XM-VALUE                 
               MOVE 'PRCHG_ST_CDE             ' TO DL1-ERROR-FIELD              
               MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG                
               MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD              
               PERFORM 8900-WRITE-REPORT-LINE                                   
               INITIALIZE DL1-DETAIL-LINE-1                                     
           END-IF.                                                              
                                                                                
           IF  PE103-ALL-RMNG-STR-IND                                           
                                  NOT EQUAL XM010-ALL-RMNG-STR-IND              
              IF  PE103-ALL-RMNG-STR-IND-NI EQUAL PC-NULL                       
               AND XM010-ALL-RMNG-STR-IND-IS-NULL                               
                  CONTINUE                                                      
              ELSE                                                              
                  SET PS-ERROR-FOUND  TO TRUE                                   
                  ADD +1                           TO PV-TOTAL-ERROR-CNT        
                  MOVE PE103-PRCHG-ID              TO DL1-PRCHG-ID              
                  MOVE PE103-ALL-RMNG-STR-IND      TO DL1-SOR-VALUE             
                  MOVE XM010-ALL-RMNG-STR-IND      TO DL1-XM-VALUE              
                  MOVE 'ALL_RMNG_STR_IND         ' TO DL1-ERROR-FIELD           
                  MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG             
                  MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD           
                  PERFORM 8900-WRITE-REPORT-LINE                                
                  INITIALIZE DL1-DETAIL-LINE-1                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF  PE103-SUBMTD-BY-USR-ID NOT EQUAL XM010-SUBMTD-BY-USR-ID          
              IF  PE103-SUBMTD-BY-USR-ID-NI EQUAL PC-NULL                       
               AND XM010-SUBMTD-BY-USR-ID-IS-NULL                               
                  CONTINUE                                                      
              ELSE                                                              
                  SET PS-ERROR-FOUND               TO TRUE                      
                  ADD +1                           TO PV-TOTAL-ERROR-CNT        
                  MOVE PE103-PRCHG-ID              TO DL1-PRCHG-ID              
                  MOVE PE103-SUBMTD-BY-USR-ID      TO DL1-SOR-VALUE             
                  MOVE XM010-SUBMTD-BY-USR-ID      TO DL1-XM-VALUE              
                  MOVE 'SUBMTD_BY_USR_ID         ' TO DL1-ERROR-FIELD           
                  MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG             
                  MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD           
                  PERFORM 8900-WRITE-REPORT-LINE                                
                  INITIALIZE DL1-DETAIL-LINE-1                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF  PE103-SUBMTD-TMST NOT EQUAL XM010-SUBMTD-TMST                    
              IF  PE103-SUBMTD-TMST-NI EQUAL PC-NULL                            
               AND XM010-SUBMTD-TMST-IS-NULL                                    
                  CONTINUE                                                      
              ELSE                                                              
                  SET PS-ERROR-FOUND               TO TRUE                      
                  ADD +1                           TO PV-TOTAL-ERROR-CNT        
                  MOVE PE103-PRCHG-ID              TO DL1-PRCHG-ID              
                  MOVE PE103-SUBMTD-TMST           TO DL1-SOR-VALUE             
                  MOVE XM010-SUBMTD-TMST           TO DL1-XM-VALUE              
                  MOVE 'SUBMTD_TMST              ' TO DL1-ERROR-FIELD           
                  MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG             
                  MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD           
                  PERFORM 8900-WRITE-REPORT-LINE                                
                  INITIALIZE DL1-DETAIL-LINE-1                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF  PE103-APP-BY-USR-ID NOT EQUAL XM010-APP-BY-USR-ID                
              IF  PE103-APP-BY-USR-ID-NI EQUAL PC-NULL                          
               AND XM010-APP-BY-USR-ID-IS-NULL                                  
                  CONTINUE                                                      
              ELSE                                                              
                  SET PS-ERROR-FOUND               TO TRUE                      
                  ADD +1                           TO PV-TOTAL-ERROR-CNT        
                  MOVE PE103-PRCHG-ID              TO DL1-PRCHG-ID              
                  MOVE PE103-APP-BY-USR-ID         TO DL1-SOR-VALUE             
                  MOVE XM010-APP-BY-USR-ID         TO DL1-XM-VALUE              
                  MOVE 'APP_BY_USR_ID            ' TO DL1-ERROR-FIELD           
                  MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG             
                  MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD           
                  PERFORM 8900-WRITE-REPORT-LINE                                
                  INITIALIZE DL1-DETAIL-LINE-1                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF  PE103-APP-TMST NOT EQUAL XM010-APP-TMST                          
              IF  PE103-APP-TMST-NI EQUAL PC-NULL                               
               AND XM010-APP-TMST-IS-NULL                                       
                  CONTINUE                                                      
              ELSE                                                              
                  SET PS-ERROR-FOUND               TO TRUE                      
                  ADD +1                           TO PV-TOTAL-ERROR-CNT        
                  MOVE PE103-PRCHG-ID              TO DL1-PRCHG-ID              
                  MOVE PE103-APP-TMST              TO DL1-SOR-VALUE             
                  MOVE XM010-APP-TMST              TO DL1-XM-VALUE              
                  MOVE 'APP_TMST                 ' TO DL1-ERROR-FIELD           
                  MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG             
                  MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD           
                  PERFORM 8900-WRITE-REPORT-LINE                                
                  INITIALIZE DL1-DETAIL-LINE-1                                  
              END-IF                                                            
           END-IF.                                                              
039200* 07/26/2010 - COMMENTED OUT AS PART OF MDO SPRING 2010 PROJECT           
      **                                                                        
      *    IF  PE103-SOR-CRE-TMST                                               
      *                       NOT EQUAL XM010-SOR-CRE-TMST                      
      *         SET PS-ERROR-FOUND  TO TRUE                                     
      *         ADD +1                           TO PV-TOTAL-ERROR-CNT          
      *         MOVE PE103-PRCHG-ID              TO DL1-PRCHG-ID                
      *         MOVE PE103-SOR-CRE-TMST          TO DL1-SOR-VALUE               
      *         MOVE XM010-SOR-CRE-TMST          TO DL1-XM-VALUE                
      *         MOVE 'SOR_CRE_TMST             ' TO DL1-ERROR-FIELD             
      *         MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG               
      *         MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD             
      *         PERFORM 8900-WRITE-REPORT-LINE                                  
      *         INITIALIZE DL1-DETAIL-LINE-1                                    
      *    END-IF.                                                              
      *                                                                         
      *    IF  PE103-SOR-LAST-UPD-TMST                                          
      *                       NOT EQUAL XM010-SOR-LAST-UPD-TMST                 
      *         SET PS-ERROR-FOUND  TO TRUE                                     
      *         ADD +1                           TO PV-TOTAL-ERROR-CNT          
      *         MOVE PE103-PRCHG-ID              TO DL1-PRCHG-ID                
      *         MOVE PE103-SOR-LAST-UPD-TMST     TO DL1-SOR-VALUE               
      *         MOVE XM010-SOR-LAST-UPD-TMST     TO DL1-XM-VALUE                
      *         MOVE 'SOR_LAST_UPD_TMST        ' TO DL1-ERROR-FIELD             
      *         MOVE PC-FIELD-DOES-NOT-MATCH     TO DL1-ERROR-MSG               
      *         MOVE DL1-DETAIL-LINE-1           TO PV-PRINT-RECORD             
      *         PERFORM 8900-WRITE-REPORT-LINE                                  
      *    END-IF.                                                              
                                                                                
           IF  PS-ERROR-FOUND                                                   
               ADD +1 TO PV-RECORD-ERR-CNT                                      
               ADD +1 TO PV-ERROR-RECORD-WRITTEN-CNT                            
           ELSE                                                                 
               ADD +1 TO PV-VALID-MATCH-CNT                                     
           END-IF.                                                              
                                                                                
      ******************************************************************        
      **  PROCESSES THRU END OF FILE FOR REMAINING RECORDS                      
      ******************************************************************        
       2200-PROCESS-EXTRA-RECORDS.                                              
                                                                                
           INITIALIZE DL1-DETAIL-LINE-1.                                        
                                                                                
           IF  PS-NOT-END-OF-SOR-PRCHG                                          
               IF PE103-PRCHG-ID NOT = XM010-PRCHG-ID                           
                  PERFORM 2400-MOVE-SOR-VALUES                                  
                  MOVE PC-EXTRA-FOUND-SOR-PRCHG                                 
                     TO DL1-ERROR-MESSAGE                                       
                  MOVE DL1-DETAIL-LINE-1   TO PV-PRINT-RECORD                   
                  ADD +1 TO PV-ERROR-RECORD-WRITTEN-CNT                         
                  ADD +1 TO PV-SOR-PRCHG-RECS-UNIQUE                            
                  ADD +1 TO PV-TOTAL-ERROR-CNT                                  
                  PERFORM 8900-WRITE-REPORT-LINE                                
               END-IF                                                           
               PERFORM 7000-READ-SOR-PRCHG-EXTRACT                              
           END-IF.                                                              
                                                                                
           IF  PS-NOT-END-OF-XM-PRCHG                                           
               IF PE103-PRCHG-ID NOT = XM010-PRCHG-ID                           
                  PERFORM 2500-MOVE-XM-VALUES                                   
                  MOVE PC-EXTRA-FOUND-XM-PRCHG                                  
                      TO DL1-ERROR-MESSAGE                                      
                  MOVE DL1-DETAIL-LINE-1   TO PV-PRINT-RECORD                   
                  ADD +1 TO PV-ERROR-RECORD-WRITTEN-CNT                         
                  ADD +1 TO PV-XM-EXTRACT-REC-UNIQUE                            
                  ADD +1 TO PV-TOTAL-ERROR-CNT                                  
                  PERFORM 8900-WRITE-REPORT-LINE                                
               END-IF                                                           
               PERFORM 7100-READ-XM-PRCHG-EXTRACT                               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2300-CHECK-IF-NEW-PG-NEEDED.                                            
      *     CHECKS TO SEE IF A NEW PAGE IS NEEDED.                              
      *****************************************************************         
       2300-CHECK-IF-NEW-PG-NEEDED.                                             
                                                                                
           IF PV-LINE-CNT > PC-MAX-REPORT-LINES                                 
              MOVE PV-PRINT-RECORD       TO  PV-SAVE-PRINT-RECORD               
              PERFORM 8700-REPORT-HEADER-ROUTINE                                
              MOVE PV-SAVE-PRINT-RECORD  TO PV-PRINT-RECORD                     
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2400-MOVE-SOR-VALUES.                                                   
      *     MOVES THE SOR ITEM PRCHG VALUES TO THE ERROR RECORD.                
      *****************************************************************         
       2400-MOVE-SOR-VALUES.                                                    
                                                                                
           MOVE PE103-PRCHG-ID     TO PV-TEMP-PRCHG-ID                          
           MOVE PV-TEMP-PRCHG-ID   TO DL1-PRCHG-ID.                             
                                                                                
      ******************************************************************        
      * 2500-MOVE-XM-VALUES.                                                    
      *     MOVES THE XM PRCHG VALUES TO THE ERROR RECORD.                      
      *****************************************************************         
       2500-MOVE-XM-VALUES.                                                     
                                                                                
           MOVE XM010-PRCHG-ID     TO PV-TEMP-PRCHG-ID.                         
           MOVE PV-TEMP-PRCHG-ID   TO DL1-PRCHG-ID.                             
                                                                                
      *****************************************************************         
      *7000-READ-SOR-PRCHG-EXTRACT.                                             
      *  READ SOR PRCHG    EXTRACT FILE                                         
      *****************************************************************         
       7000-READ-SOR-PRCHG-EXTRACT.                                             
                                                                                
           IF  PS-NOT-END-OF-SOR-PRCHG                                          
               READ SOR-PRCHG-EXTRACT-FILE                                      
                  INTO PE103-RECORD                                             
                    AT END                                                      
                       SET PS-END-OF-SOR-PRCHG TO TRUE                          
                       MOVE 999999999 TO PE103-PRCHG-ID                         
                    NOT AT END                                                  
                       ADD +1 TO PV-SOR-PRCHG-RECS-READ-CNT                     
               END-READ                                                         
           END-IF.                                                              
                                                                                
      *****************************************************************         
      *7100-READ-XM-PRCHG-EXTRACT.                                              
      * READ XM EXTRACT FILE                                                    
      *****************************************************************         
       7100-READ-XM-PRCHG-EXTRACT.                                              
                                                                                
           IF  PS-NOT-END-OF-XM-PRCHG                                           
               READ XM-PRCHG-EXTRACT-FILE                                       
                 INTO XM010-PRCHG-UNLOAD                                        
                   AT END                                                       
                       SET PS-END-OF-XM-PRCHG TO TRUE                           
                       MOVE 999999999 TO XM010-PRCHG-ID                         
                   NOT AT END                                                   
                       ADD +1 TO PV-XM-EXTRACT-REC-READ-CNT                     
               END-READ                                                         
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 8700-REPORT-HEADER-ROUTINE.                                             
      *     PRINT THE HEADINGS FOR THE REPORT.                                  
      ******************************************************************        
       8700-REPORT-HEADER-ROUTINE.                                              
                                                                                
           MOVE ZEROS                  TO PV-LINE-CNT.                          
           ADD +1                      TO PV-PAGE-CNT.                          
           MOVE PV-PAGE-CNT            TO DP200-PAGE-NUMBER.                    
                                                                                
           MOVE PC-REPORT-NBR-1        TO DP200-REPORT-NUMBER.                  
           MOVE DP200-STANDRD-HEADER-200-COLS                                   
                                       TO PV-PRINT-RECORD.                      
                                                                                
           PERFORM 8800-WRITE-RPT-TOP-OF-PAGE.                                  
                                                                                
           MOVE HL2-HEADING-LINE-2     TO PV-PRINT-RECORD.                      
           MOVE +2                     TO PV-LINE-SPACING.                      
           PERFORM 8900-WRITE-REPORT-LINE.                                      
                                                                                
           MOVE HL3-HEADING-LINE-3     TO PV-PRINT-RECORD.                      
           MOVE +3                     TO PV-LINE-SPACING.                      
           PERFORM 8900-WRITE-REPORT-LINE.                                      
                                                                                
           MOVE HL4-HEADING-LINE-4     TO PV-PRINT-RECORD.                      
           MOVE +1                     TO PV-LINE-SPACING.                      
           PERFORM 8900-WRITE-REPORT-LINE.                                      
                                                                                
      ******************************************************************        
      * 8800-WRITE-RPT-TOP-OF-PAGE                                              
      *     WRITE FIRST REPORT LINE AFTER PAGE BREAK.                           
      ******************************************************************        
       8800-WRITE-RPT-TOP-OF-PAGE.                                              
                                                                                
           WRITE XM-PRCHG-INTEG-RPT-RECORD                                      
             FROM PV-PRINT-RECORD                                               
               AFTER ADVANCING PAGE.                                            
                                                                                
           MOVE +1 TO PV-LINE-CNT.                                              
                                                                                
      ******************************************************************        
      * 8900-WRITE-REPORT-LINE                                                  
      *     WRITE REPORT LINE AFTER ADVANCING LINE SPACING.                     
      ******************************************************************        
       8900-WRITE-REPORT-LINE.                                                  
                                                                                
           PERFORM 2300-CHECK-IF-NEW-PG-NEEDED.                                 
                                                                                
           WRITE XM-PRCHG-INTEG-RPT-RECORD                                      
             FROM PV-PRINT-RECORD                                               
               AFTER ADVANCING PV-LINE-SPACING.                                 
                                                                                
           ADD PV-LINE-SPACING TO PV-LINE-CNT.                                  
                                                                                
      ****************************************************************          
      *END OF PROGRAM ROUTINE                                                   
      ****************************************************************          
       9000-END-OF-PGM.                                                         
                                                                                
           DISPLAY '******** XMKRP010 COUNTS *********'.                        
           DISPLAY ' INTEGRITY CHECK - FOR XMT_PRCHG '.                         
           DISPLAY '   SOR REC READ CNT= '                                      
                           PV-SOR-PRCHG-RECS-READ-CNT.                          
           DISPLAY 'XM PRCHG READ COUNT= ' PV-XM-EXTRACT-REC-READ-CNT.          
           DISPLAY '  =============================== '                         
           DISPLAY 'UNIQUE TO SOR ONLY  - ', PV-SOR-PRCHG-RECS-UNIQUE           
           DISPLAY 'UNIQUE TO XM  ONLY  - ', PV-XM-EXTRACT-REC-UNIQUE           
           DISPLAY 'COMMON TO BOTH FILES- ',  PV-SOR-XM-COMMON-CNT              
           COMPUTE PV-SOR-XM-TOTAL-CNT =  PV-SOR-XM-COMMON-CNT                  
            + PV-SOR-PRCHG-RECS-UNIQUE  + PV-XM-EXTRACT-REC-UNIQUE              
           DISPLAY 'TOTAL COMMON + UNIQUE=', PV-SOR-XM-TOTAL-CNT                
           DISPLAY '  =============================== '                         
           DISPLAY ' ERROR MSGS WRITTEN= ' PV-TOTAL-ERROR-CNT.                  
           DISPLAY ' TOTAL RECORDS ERROR= ' PV-ERROR-RECORD-WRITTEN-CNT.        
           DISPLAY '  VALID MATCH COUNT= ' PV-VALID-MATCH-CNT.                  
           DISPLAY '                              '.                            
           DISPLAY '**********************************'.                        
                                                                                
           IF  PV-ERROR-RECORD-WRITTEN-CNT > 0                                  
               MOVE 04               TO RETURN-CODE                             
           END-IF.                                                              
                                                                                
           CLOSE SOR-PRCHG-EXTRACT-FILE                                         
                 XM-PRCHG-EXTRACT-FILE                                          
                 XM-PRCHG-INTEG-RPT-FILE.                                       
                                                                                
