000100*****************************************************************        L
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400                                                                          
000500 PROGRAM-ID.             TMKRP012.                                        
000600 AUTHOR.                 LARRY SCHLEICHER - STRATAGEM.                    
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000800 DATE-WRITTEN            OCTOBER, 1996                                    
000900 DATE-COMPILED.                                                           
001000*****************************************************************         
001100*****************************************************************         
001200*                                                               *         
001300*  THIS PROGRAM WILL PRINT THE TOP FIFTY TROUBLE VENDORS REPORT.*         
001400*****************************************************************         
001500*  CHANGED MM/DD/YY BY XXXXXXXXXXX                              *         
001600*   WR# XXXX XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX *         
001700*                                                               *         
001800*****************************************************************         
002600* Change History                                                *         
001300*    CHANGES                                                    *         
001300*     MADE BY: PRITHIVIRAJ SUBRAMANIAN (COGNIZANT)              *         
001300*     DATE:    10/10/2014                                       *         
001300*                                                               *         
001300*      CHANGES THRU OUT THE PROGRAM TO INCREASE THE SIZE OF PO# *         
001300*      FROM 7 DIGITS TO 8 DIGITS. TAGGED WITH *PS 10/10/2014    *         
001900*****************************************************************         
002000 ENVIRONMENT DIVISION.                                                    
002100*****************************************************************         
002200                                                                          
002300 CONFIGURATION SECTION.                                                   
002400                                                                          
002500 SOURCE-COMPUTER.        IBM-3090.                                        
002600 OBJECT-COMPUTER.        IBM-3090.                                        
002700                                                                          
002800 INPUT-OUTPUT SECTION.                                                    
002900                                                                          
003000 FILE-CONTROL.                                                            
003100*                                                                         
003200     SELECT EXTRACT-FILE                                                  
003300         ASSIGN TO INP01.                                                 
003400                                                                          
003500     SELECT TM-TOP-FIFTY-TROUB-VEND-RPT                                   
003600         ASSIGN TO RPT01.                                                 
003700*                                                                         
003800*****************************************************************         
003900 DATA DIVISION.                                                           
004000*****************************************************************         
004100*                                                                         
004200 FILE SECTION.                                                            
004300*                                                                         
004400 FD  EXTRACT-FILE                                                         
004500     LABEL RECORDS ARE STANDARD                                           
004600     RECORDING MODE IS F                                                  
004700     BLOCK CONTAINS 0 RECORDS                                             
004800     RECORD CONTAINS 400 CHARACTERS.                                      
004900 01  EXTRACT-RECORD                  PIC X(400).                          
005000                                                                          
005100 FD  TM-TOP-FIFTY-TROUB-VEND-RPT                                          
005200     RECORDING MODE IS F                                                  
005300     BLOCK CONTAINS 0  RECORDS.                                           
005400 01  TM-TOP-FIFTY-RECORD             PIC  X(132).                         
005500*                                                                         
005600                                                                          
005700 WORKING-STORAGE SECTION.                                                 
005800*                                                                         
005900 01  PROGRAM-SWITCHES.                                                    
006000     05  PS-END-OF-EXTRACT-SW        PIC  X(01)    VALUE 'N'.             
006100         88  PS-END-OF-EXTRACT                     VALUE 'Y'.             
006200         88  PS-PROCESS-EXTRACT                    VALUE 'N'.             
006300*                                                                         
006400 01  PC-CONSTANTS.                                                        
006500     05  PC-1                        PIC S9(03)    VALUE +1               
006600                                                   COMP-3.                
006700     05  PC-2                        PIC S9(03)    VALUE +2               
006800                                                   COMP-3.                
006900     05  PC-3                        PIC S9(03)    VALUE +3               
007000                                                   COMP-3.                
007100     05  PC-4                        PIC S9(03)    VALUE +4               
007200                                                   COMP-3.                
007300     05  PC-6                        PIC S9(03)    VALUE +6               
007400                                                   COMP-3.                
007500     05  PC-53                       PIC S9(03)    VALUE +53              
007600                                                   COMP-3.                
007700     05  PC-60                       PIC S9(03)    VALUE +60              
007800                                                   COMP-3.                
007900     05  PC-TMKRP012                 PIC  X(08)    VALUE                  
008000         'TMKRP012'.                                                      
008100     05  PC-REPORT-NBR               PIC  9(01)    VALUE 1.               
008200     05  PC-SPECIAL-REQUEST          PIC  X(27)    VALUE                  
008300         '** SPECIAL REQUESTED RUN **'.                                   
008400*                                                                         
008500 01  PV-MISC-FIELDS.                                                      
008600     05  PV-LINE-SUB                 PIC S9(03)    VALUE ZERO             
008700                                                   COMP-3.                
008800     05  PV-LINE-CNT                 PIC S9(03)    VALUE ZERO             
008900                                                   COMP-3.                
009000     05  PV-PAGE-CNT                 PIC S9(04)    VALUE ZERO             
009100                                                   COMP-3.                
009200     05  PV-VENDOR-COUNT             PIC S9(04)    VALUE ZERO             
009300                                                   COMP-3.                
009400     05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO             
009500                                                   COMP SYNC.             
009600     05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.          
009700     05  PV-SAVE-PO                  PIC S9(09)    VALUE ZERO.            
009800     05  PV-SAVE-RECEIVER            PIC  9(03)    VALUE ZERO.            
009900     05  PV-PO-RECEIVER.                                                  
010000*PS 10/10/2014 STARTS                                                     
010000         10  PV-PO                   PIC  9(08)    VALUE ZERO.            
010000*PS 10/10/2014 ENDS                                                       
010100         10  FILLER                  PIC  X(01)    VALUE '-'.             
010200         10  PV-RECEIVER             PIC  9(04)    VALUE ZERO.            
010300         10  PV-RECEIVER-X REDEFINES PV-RECEIVER                          
010400                                     PIC  X(04).                          
010500     05  PV-TODAYS-DATE.                                                  
010600         10  PV-TODAYS-DATE-YY       PIC  9(02)    VALUE ZERO.            
010700         10  PV-TODAYS-DATE-MM       PIC  9(02)    VALUE ZERO.            
010800         10  PV-TODAYS-DATE-DD       PIC  9(02)    VALUE ZERO.            
010900     05  PV-TODAYS-TIME.                                                  
011000         10  PV-TODAYS-TIME-HH       PIC  9(02)    VALUE ZERO.            
011100         10  PV-TODAYS-TIME-MM       PIC  9(02)    VALUE ZERO.            
011200         10  PV-TODAYS-TIME-SS       PIC  9(02)    VALUE ZERO.            
011300         10  FILLER                  PIC  9(02)    VALUE ZERO.            
011400     05  PV-FORMAT-DATE.                                                  
011500         10  PV-FORMAT-DATE-MM       PIC  X(02)    VALUE SPACES.          
011600         10  FILLER                  PIC  X(01)    VALUE '/'.             
011700         10  PV-FORMAT-DATE-DD       PIC  X(02)    VALUE SPACES.          
011800         10  FILLER                  PIC  X(01)    VALUE '/'.             
011900         10  PV-FORMAT-DATE-YYYY     PIC  X(04)    VALUE SPACES.          
012000     05  PV-SAVE-ASSIGN-TOT-QTY      PIC S9(07)    VALUE ZERO             
012100                                                   COMP-3.                
012200*                                                                         
012300                                                                          
012400     COPY DPWS024.                                                        
012500                                                                          
012600     COPY TMRD001.                                                        
012700 EJECT                                                                    
012800*                                                                         
012900******************************************************************        
013000*  WORK FIELDS FOR PRINTING A 132 CHARACTER REPORT.                       
013100******************************************************************        
013200     COPY DPWS132O.                                                       
013300*                                                                         
013400*                                                                         
013500 01  HEADING-LINES.                                                       
013600     05  HEADING-LINE-1.                                                  
013700         10  HL1-SPECIAL-RUN-MSG     PIC  X(27)    VALUE SPACES.          
013800         10  FILLER                  PIC  X(26)    VALUE SPACES.          
013900         10  FILLER                  PIC  X(25)    VALUE                  
014000             'TOP FIFTY TROUBLE VENDORS'.                                 
014100         10  FILLER                  PIC  X(54)    VALUE SPACES.          
014200*                                                                         
014300     05  HEADING-LINE-2.                                                  
014400         10  FILLER                  PIC  X(54)    VALUE SPACE.           
014500         10  FILLER                  PIC  X(13)    VALUE                  
014600             'WEEK ENDING  '.                                             
014700         10  HL1-TROUBLE-DATE        PIC  X(10).                          
014800         10  FILLER                  PIC  X(55)    VALUE SPACES.          
014900*                                                                         
015000     05  HEADING-LINE-3.                                                  
015100         10  FILLER                  PIC  X(14)    VALUE                  
015200             'VENDOR NAME:  '.                                            
015300         10  HL3-VENDOR-NAME         PIC  X(30).                          
015400         10  FILLER                  PIC  X(05)    VALUE SPACES.          
015500         10  FILLER                  PIC  X(24)    VALUE                  
015600             'TOTAL TROUBLE CREATED:  '.                                  
015700         10  HL3-TOTAL-TROUBLE       PIC  ZZ,ZZZ.                         
015800         10  FILLER                  PIC  X(53)    VALUE SPACES.          
015900*                                                                         
016000     05  HEADING-LINE-4.                                                  
016100         10  FILLER                  PIC  X(28)    VALUE SPACES.          
016200         10  FILLER                  PIC  X(11)    VALUE                  
016300             'TROUBLE'.                                                   
016400         10  FILLER                  PIC  X(93)    VALUE SPACES.          
016500*                                                                         
016600     05  HEADING-LINE-5.                                                  
016700         10  FILLER                  PIC  X(06)    VALUE SPACES.          
016800         10  FILLER                  PIC  X(11)    VALUE                  
016900             'PO/RECEIVER'.                                               
017000         10  FILLER                  PIC  X(11)    VALUE SPACES.          
017100         10  FILLER                  PIC  X(06)    VALUE                  
017200             'REASON'.                                                    
017300         10  FILLER                  PIC  X(14)    VALUE SPACES.          
017400         10  FILLER                  PIC  X(19)    VALUE                  
017500             'TROUBLE DESCRIPTION'.                                       
017600         10  FILLER                  PIC  X(65)    VALUE SPACES.          
017700*                                                                         
017800*                                                                         
017900 01  MISCELLANEOUS-LINES.                                                 
018000     05  END-OF-REPORT-LINE.                                              
018100         10  FILLER                  PIC  X(44)    VALUE SPACES.          
018200         10  FILLER                  PIC  X(45)    VALUE                  
018300             '*******   E N D   O F   R E P O R T   *******'.             
018400         10  FILLER                  PIC  X(43)    VALUE SPACES.          
018500*                                                                         
018600 01  DETAIL-LINES.                                                        
018700     05  DETAIL-LINE1.                                                    
018800         10  FILLER                  PIC  X(06)    VALUE SPACES.          
018900*PS 10/10/2014 STARTS                                                     
018900         10  DL1-PO-RECEIVER         PIC  X(13)    VALUE SPACES.          
018900*PS 10/10/2014 ENDS                                                       
019000         10  FILLER                  PIC  X(10)    VALUE SPACES.          
019100         10  DL1-REASON-CODE         PIC  ZZ9      VALUE ZERO.            
019200         10  FILLER                  PIC  X(17)    VALUE SPACES.          
019300         10  DL1-TROUBLE-DESC        PIC  X(20)    VALUE SPACES.          
018900*PS 10/10/2014 STARTS                                                     
019400         10  FILLER                  PIC  X(63)    VALUE SPACES.          
018900*PS 10/10/2014 ENDS                                                       
019500                                                                          
019600                                                                          
019700*                                                                         
019800 PROCEDURE DIVISION.                                                      
019900*                                                                         
020000 0000-MAINLINE-PROCESSING.                                                
020100******************************************************************        
020200**  THIS IS THE MAIN DRIVER FOR THE PROGRAM.                              
020300******************************************************************        
020400     OPEN INPUT  EXTRACT-FILE                                             
020500          OUTPUT TM-TOP-FIFTY-TROUB-VEND-RPT.                             
020600*                                                                         
020700     PERFORM 2100-READ-EXTRACT.                                           
020800     PERFORM 1000-INITIALIZATION.                                         
020900*                                                                         
021000     PERFORM 2000-PROCESS-TM                                              
021100         UNTIL PS-END-OF-EXTRACT.                                         
021200*                                                                         
021300     WRITE TM-TOP-FIFTY-RECORD                                            
021400       FROM END-OF-REPORT-LINE                                            
021500         AFTER ADVANCING 2 LINES.                                         
021600*                                                                         
021700     CLOSE EXTRACT-FILE                                                   
021800           TM-TOP-FIFTY-TROUB-VEND-RPT.                                   
021900*                                                                         
022000     GOBACK.                                                              
022100*                                                                         
022200*                                                                         
022300 1000-INITIALIZATION.                                                     
022400******************************************************************        
022500**  PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.                       
022600******************************************************************        
022700     IF TM001-RECORD-TYPE-HEADER                                          
022800         CONTINUE                                                         
022900     ELSE                                                                 
023000         DISPLAY '** ABEND **'                                            
023100         DISPLAY '** PROGRAM   ** = ' PC-TMKRP012                         
023200         DISPLAY '** PARAGRAPH ** =  1000-INITIALIZATION'                 
023300         DISPLAY '** NO HEADER RECORD EXISTS'                             
023400         DISPLAY '** FIRST RECORD TYPE  = ' TM001-RECORD-TYPE-CDE         
023500         MOVE +4003 TO PV-ABEND-CODE                                      
023600         CALL 'ILBOABN0' USING PV-ABEND-CODE                              
023700     END-IF.                                                              
023800                                                                          
023900     ACCEPT PV-TODAYS-DATE                                                
024000       FROM DATE.                                                         
024100*                                                                         
024200     ACCEPT PV-TODAYS-TIME                                                
024300       FROM TIME.                                                         
024400*                                                                         
024500     MOVE PC-REPORT-NBR          TO DP132O-REPORT-NUMBER.                 
024600     MOVE PV-TODAYS-DATE-MM      TO DP132O-RUN-MONTH.                     
024700     MOVE PV-TODAYS-DATE-DD      TO DP132O-RUN-DAY.                       
024800     MOVE PV-TODAYS-DATE-YY      TO DP132O-RUN-YEAR.                      
024900*                                                                         
025000     MOVE PV-TODAYS-TIME-HH      TO DP132O-RUN-HOUR.                      
025100     MOVE PV-TODAYS-TIME-MM      TO DP132O-RUN-MINUTE.                    
025200     MOVE PC-TMKRP012            TO DP132O-PROGRAM-NAME.                  
025300*                                                                         
025400     MOVE TM001-END-MM         TO PV-FORMAT-DATE-MM.                      
025500     MOVE TM001-END-DD         TO PV-FORMAT-DATE-DD.                      
025600     MOVE TM001-END-YYYY       TO PV-FORMAT-DATE-YYYY.                    
025700     MOVE PV-FORMAT-DATE       TO HL1-TROUBLE-DATE.                       
025800     IF TM001-RUN-SPECIAL                                                 
025900         MOVE PC-SPECIAL-REQUEST TO HL1-SPECIAL-RUN-MSG                   
026000     END-IF.                                                              
026100     PERFORM 2100-READ-EXTRACT.                                           
026200                                                                          
026300*                                                                         
026400 2000-PROCESS-TM.                                                         
026500******************************************************************        
026600*  THIS IS THE MAIN CONTROLLING PARAGRAPH FOR PROCESSING THE     *        
026700*  EARLY SHIPMENTS AND SYTLE SUBSTITUTIONS REPORT                *        
026800******************************************************************        
026900     IF NOT TM001-RECORD-TYPE-HEADER                                      
027000         CONTINUE                                                         
027100     ELSE                                                                 
027200         DISPLAY '** ABEND **'                                            
027300         DISPLAY '** PROGRAM   ** = ' PC-TMKRP012                         
027400         DISPLAY '** PARAGRAPH ** =  1000-INITIALIZATION'                 
027500         DISPLAY '** DUPLICATE HEADER RECORDS'                            
027600         DISPLAY '** RECORD TYPE  = ' TM001-RECORD-TYPE-CDE               
027700         MOVE +4003 TO PV-ABEND-CODE                                      
027800         CALL 'ILBOABN0' USING PV-ABEND-CODE                              
027900     END-IF.                                                              
028000                                                                          
028100     IF TM001-RECORD-TYPE-VNDDTLSUM                                       
028200         ADD PC-1 TO PV-VENDOR-COUNT                                      
028300         IF PV-VENDOR-COUNT > 50 AND                                      
028400            TM001-VTL-ASSIGN-TOT-QTY < PV-SAVE-ASSIGN-TOT-QTY             
028500             SET PS-END-OF-EXTRACT TO TRUE                                
028600         ELSE                                                             
028700             MOVE TM001-VTL-ASSIGN-TOT-QTY                                
028800                                     TO PV-SAVE-ASSIGN-TOT-QTY            
028900             MOVE TM001-VTL-ENT-NAME-DESC  TO HL3-VENDOR-NAME             
029000             MOVE TM001-VTL-ASSIGN-TOT-QTY TO HL3-TOTAL-TROUBLE           
029100             PERFORM 8000-WRITE-REPORT-HEADERS                            
029200             WRITE TM-TOP-FIFTY-RECORD                                    
029300               FROM HEADING-LINE-3                                        
029400                 AFTER ADVANCING 3 LINES                                  
029500                                                                          
029600             WRITE TM-TOP-FIFTY-RECORD                                    
029700               FROM HEADING-LINE-4                                        
029800                 AFTER ADVANCING 2 LINES                                  
029900                                                                          
030000             WRITE TM-TOP-FIFTY-RECORD                                    
030100               FROM HEADING-LINE-5                                        
030200                 AFTER ADVANCING 1 LINES                                  
030300                                                                          
030400             ADD PC-6            TO PV-LINE-CNT                           
030500             MOVE PC-2           TO PV-LINE-SUB                           
030600         END-IF                                                           
030700     ELSE                                                                 
030800         MOVE TM001-PO-NBR         TO PV-PO                               
030900         MOVE TM001-REC-SEQ-NBR    TO PV-RECEIVER                         
031000         MOVE PV-PO-RECEIVER       TO DL1-PO-RECEIVER                     
031100         MOVE TM001-TRBL-RSN-CDE   TO DL1-REASON-CODE                     
031200         MOVE TM001-TRBL-RSN-CDE-DESC                                     
031300                                   TO DL1-TROUBLE-DESC                    
031400         IF  PV-LINE-CNT > PC-60                                          
031500             PERFORM 8000-WRITE-REPORT-HEADERS                            
031600             WRITE TM-TOP-FIFTY-RECORD                                    
031700               FROM HEADING-LINE-3                                        
031800                 AFTER ADVANCING 3 LINES                                  
031900                                                                          
032000             WRITE TM-TOP-FIFTY-RECORD                                    
032100               FROM HEADING-LINE-4                                        
032200                 AFTER ADVANCING 2 LINES                                  
032300                                                                          
032400             WRITE TM-TOP-FIFTY-RECORD                                    
032500               FROM HEADING-LINE-5                                        
032600                 AFTER ADVANCING 1 LINES                                  
032700             ADD PC-6            TO PV-LINE-CNT                           
032800             MOVE PC-2           TO PV-LINE-SUB                           
032900         END-IF                                                           
033000         PERFORM 8400-WRITE-DETAIL-LINES                                  
033100     END-IF.                                                              
033200     IF PS-PROCESS-EXTRACT                                                
033300         PERFORM 2100-READ-EXTRACT                                        
033400     END-IF.                                                              
033500                                                                          
033600 2100-READ-EXTRACT.                                                       
033700     READ EXTRACT-FILE INTO TM001-TROUBLE-EXT-RECORD                      
033800        AT END                                                            
033900           SET PS-END-OF-EXTRACT TO TRUE.                                 
034000                                                                          
034100*                                                                         
034200 8000-WRITE-REPORT-HEADERS.                                               
034300******************************************************************        
034400*  PRINT THE HEADING FOR THE FIRST PAGE OF AN ASN.  SEBSEQUENT   *        
034500*  PAGES FOR THE SAME ASN DO NOT REPEAT ALL OF THE HEADING INFO. *        
034600******************************************************************        
034700     PERFORM 8200-WRITE-TOP-LINE.                                         
034800*                                                                         
034900     WRITE TM-TOP-FIFTY-RECORD                                            
035000       FROM HEADING-LINE-1                                                
035100         AFTER ADVANCING 2 LINE.                                          
035200*                                                                         
035300     WRITE TM-TOP-FIFTY-RECORD                                            
035400       FROM HEADING-LINE-2                                                
035500         AFTER ADVANCING 1 LINES.                                         
035600*                                                                         
035700     MOVE PC-4            TO PV-LINE-CNT.                                 
035800     MOVE PC-2            TO PV-LINE-SUB.                                 
035900*                                                                         
036000*                                                                         
036100 8200-WRITE-TOP-LINE.                                                     
036200******************************************************************        
036300*     WRITES HEADER LINES 0 AND 1 WHEN MORE THAN ONE PAGE IS     *        
036400*     NEEDED FOR AN ASN NUMBER.                                  *        
036500******************************************************************        
036600     ADD 1                        TO PV-PAGE-CNT.                         
036700*                                                                         
036800     MOVE PV-PAGE-CNT             TO DP132O-PAGE-NUMBER.                  
036900*                                                                         
037000     WRITE TM-TOP-FIFTY-RECORD                                            
037100       FROM DP132O-STANDRD-HEADER-132-COLS                                
037200         AFTER ADVANCING PAGE.                                            
037300*                                                                         
037400*                                                                         
037500 8400-WRITE-DETAIL-LINES.                                                 
037600******************************************************************        
037700*     WRITES THE DETAIL LINE INFORMATION.                        *        
037800******************************************************************        
037900     WRITE TM-TOP-FIFTY-RECORD                                            
038000       FROM DETAIL-LINE1                                                  
038100         AFTER ADVANCING PV-LINE-SUB LINES.                               
038200*                                                                         
038300     ADD PV-LINE-SUB                 TO PV-LINE-CNT.                      
038400     MOVE PC-1                       TO PV-LINE-SUB.                      
