000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    SUKUT052.                                                 
000600 AUTHOR.        GERMAN BANDA.                                             
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   OCTOBER 2001.                                             
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM CREATES AN EXTRACT FILE OF DATA FROM THE                  
001300*  SMOOTHING WORK TABLE (SUT_SMOOTHING_PRC)                               
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
003200                                                                          
003300     SELECT SUT-SMOOTH-PRC-EXTRACT                                        
003400         ASSIGN TO OUT01.                                                 
003410                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
004000                                                                          
004800 FD  SUT-SMOOTH-PRC-EXTRACT                                               
004900     RECORDING MODE IS F                                                  
005000     LABEL RECORDS ARE STANDARD                                           
005100     BLOCK CONTAINS 0 RECORDS.                                            
005200                                                                          
005300 01  SMTH-PRC-DATA-RECORD.                                                
005400     05   SPD-SMTHG-PRC-ID              PIC  S9(9) COMP.                  
005410     05   SPD-PO-NBR                    PIC  S9(9) COMP.                  
005420     05   SPD-RCVR-SEQ-NBR              PIC  S9(9) COMP.                  
005461     05   SPD-CRE-USR-ID                PIC   X(8).                       
005470                                                                          
005500                                                                          
005600******************************************************************        
005700 WORKING-STORAGE SECTION.                                                 
005800******************************************************************        
007200*-----------------------------------------------------------------        
007500*  DB2 FORMATTED MESSAGE AREA                                             
007510*-----------------------------------------------------------------        
007700     COPY DPWS004.                                                        
007800                                                                          
007801*-----------------------------------------------------------------        
007802*  COMMUNICATIONS AREA FOR DB2                                            
007803*-----------------------------------------------------------------        
007804     EXEC SQL                                                             
007805          INCLUDE SQLCA                                                   
007806     END-EXEC.                                                            
007807                                                                          
007810*-----------------------------------------------------------------        
007820*  COBOL DCLGEN MEMBER FOR SMOOTHING PROCESS                              
007830*  WORK TABLE (SUT_SMOOTHING_PRC)                                         
007840*-----------------------------------------------------------------        
007850     EXEC SQL                                                             
007860          INCLUDE SUT00003                                                
007870     END-EXEC.                                                            
007880                                                                          
007890*-----------------------------------------------------------------        
007900*  COBOL DCLGEN MEMBER FOR COMPANY CONTROL TABLE                          
008100*-----------------------------------------------------------------        
008200     EXEC SQL                                                             
008300          INCLUDE TCOCNTL                                                 
008400     END-EXEC.                                                            
008500                                                                          
010320*-----------------------------------------------------------------        
010330*                                                                         
010340*-----------------------------------------------------------------        
011200 01  ABEND-CODE                    PIC S9(04) VALUE +0                    
011300                                              COMP SYNC.                  
011400                                                                          
011500 01  PS-PROGRAM-SWITCHES.                                                 
011600     05  PS-EOF-SMTHG-DATA-CSR-SW  PIC  X(01) VALUE 'N'.                  
011700         88  PS-EOF-SMTHG-DATA-CSR            VALUE 'Y'.                  
011800         88  PS-MORE-SMTHG-DATA-CSR           VALUE 'N'.                  
011900                                                                          
012000                                                                          
012100 01  PC-PROGRAM-CONSTANTS.                                                
012400     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08) VALUE 'SUKUT052'.           
013000     05  PC-CURRENT-TIMESTAMP      PIC  X(26) VALUE SPACES.               
013010     05  PC-DEFAULT-TIME           PIC  X(16) VALUE                       
013020                                               '-00.00.00.000000'.        
013100                                                                          
013200 01  PV-PROGRAM-VARIABLES.                                                
013400     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
013500     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
014200     05  PV-DISPLAY-COUNT            PIC S9(09)    VALUE ZERO             
014300                                                   COMP SYNC.             
014400     05  PV-WRITE-COUNTER            PIC S9(09)    VALUE ZERO             
014500                                                   COMP SYNC.             
014700     05  PV-CURRENT-TIMESTAMP        PIC  X(26) VALUE SPACES.             
014800     05  PV-CURRENT-TIMESTAMP-RDF REDEFINES PV-CURRENT-TIMESTAMP.         
014900         10  PV-CURRENT-DATE         PIC X(10).                           
015000         10  PV-CURRENT-TIME         PIC X(16).                           
015100     05  PV-EXTRACT-DATE             PIC X(10).                           
015200                                                                          
015900                                                                          
015910*-----------------------------------------------------------------        
015920*  SMOOTHING PROCESS WORK TABLE TO BE DELETED CURSOR                      
015930*-----------------------------------------------------------------        
016000     EXEC SQL                                                             
016100          DECLARE SMTH_PRC_DATA_CSR CURSOR FOR                            
016200           SELECT DISTINCT                                                
016201                  SMTHG_PRC_ID,                                           
016202                  PO_NBR,                                                 
016210                  RCVR_SEQ_NBR,                                           
016211                  CRE_USR_ID                                              
017700             FROM SUT_SMOOTHING_PRC                                       
018000            WHERE CRE_TMST  < :PC-CURRENT-TIMESTAMP                       
018200     END-EXEC.                                                            
018300                                                                          
018400                                                                          
018500******************************************************************        
018600 PROCEDURE DIVISION.                                                      
018700******************************************************************        
018800                                                                          
018801                                                                          
018810*-----------------------------------------------------------------        
018820* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
018830*      PROGRAM.                                                           
018840*-----------------------------------------------------------------        
018900 0000-PROGRAM-MAINLINE.                                                   
019400                                                                          
019500     PERFORM 1000-INITIALIZE.                                             
019600     IF  PS-MORE-SMTHG-DATA-CSR                                           
019700         PERFORM 2000-PROCESS-SMTH-PRC-DATA                               
019800           UNTIL PS-EOF-SMTHG-DATA-CSR                                    
019900     END-IF.                                                              
020000     PERFORM 9000-EOJ-ROUTINE.                                            
020100                                                                          
020200     STOP RUN.                                                            
020300                                                                          
020310                                                                          
020320*-----------------------------------------------------------------        
020330*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
020340*    -- OPEN OUTPUT FILE                                                  
020350*    -- OPEN CURSOR                                                       
020360*    -- FETCH CURSOR                                                      
020370*-----------------------------------------------------------------        
020400 1000-INITIALIZE.                                                         
021100                                                                          
021400     OPEN OUTPUT SUT-SMOOTH-PRC-EXTRACT.                                  
021600                                                                          
021700     PERFORM 1100-SELECT-TIMESTAMP.                                       
021710                                                                          
021800     PERFORM 7000-OPEN-SMTH-PRC-DATA-CSR.                                 
021900     PERFORM 7100-FETCH-SMTH-PRC-DATA-CSR.                                
022000                                                                          
022010                                                                          
025401*-----------------------------------------------------------------        
025402*  SELECT TIMESTAMP 2 DAYS PRIOR                                          
025403*-----------------------------------------------------------------        
025404 1100-SELECT-TIMESTAMP.                                                   
025405                                                                          
025406     EXEC SQL                                                             
025407          SELECT DISTINCT                                                 
025408                 DATE(BTCH_PRC_DTE - 2 DAYS)                              
025409            INTO                                                          
025410                 :PV-EXTRACT-DATE                                         
025411            FROM                                                          
025412                 TCOCNTL                                                  
025413     END-EXEC.                                                            
025414                                                                          
025415     EVALUATE TRUE                                                        
025416         WHEN SQLCODE = ZERO                                              
025417             DISPLAY ' EXTRACT DATE IS ' PV-EXTRACT-DATE                  
025418                                                                          
025419         WHEN SQLWARN0 NOT = SPACES                                       
025420         WHEN SQLCODE < ZERO                                              
025421         WHEN SQLCODE > ZERO                                              
025422             MOVE '1100-SELECT-TIMESTAMP'                                 
025423                                 TO PV-PARAGRAPH-NAME                     
025424             MOVE 'ATTEMPTING TO SELECT CURRENT TIMESTAMP                 
025425-                 'FROM TCOCNTL'                                          
025426                                 TO PV-DB2-OPERATION-ATTEMPTED            
025427             PERFORM 9100-SQL-ABEND                                       
025428     END-EVALUATE.                                                        
025429                                                                          
025432     MOVE PV-EXTRACT-DATE      TO  PV-CURRENT-DATE.                       
025433     MOVE PC-DEFAULT-TIME      TO  PV-CURRENT-TIME.                       
025439                                                                          
025440     MOVE PV-CURRENT-TIMESTAMP TO PC-CURRENT-TIMESTAMP.                   
025441                                                                          
025442     DISPLAY 'SUKUT052 TIMESTAMP -- ' PC-CURRENT-TIMESTAMP.               
025443                                                                          
025444                                                                          
025445*-----------------------------------------------------------------        
025446*  LOOP THROUGH THE RESULTS TABLE.                                        
025447*  FORMAT THE OUTPUT RECORD.  WRITE THE RECORDS THAT MEET CRITERIA.       
025448*-----------------------------------------------------------------        
025450 2000-PROCESS-SMTH-PRC-DATA.                                              
026000                                                                          
026100     PERFORM 8000-WRITE-SMTH-PRC-DATA-REC.                                
026101                                                                          
026200     PERFORM 7100-FETCH-SMTH-PRC-DATA-CSR.                                
026300                                                                          
026310                                                                          
026320*-----------------------------------------------------------------        
026330*  OPEN THE CURSOR.                                                       
026340*-----------------------------------------------------------------        
026400 7000-OPEN-SMTH-PRC-DATA-CSR.                                             
027300                                                                          
027400     EXEC SQL                                                             
027500          OPEN SMTH_PRC_DATA_CSR                                          
027600     END-EXEC                                                             
027700                                                                          
027800     EVALUATE TRUE                                                        
027810         WHEN SQLCODE = ZERO                                              
027820              CONTINUE                                                    
027900         WHEN SQLCODE = -904                                              
028000         WHEN SQLCODE = -911                                              
028100              CONTINUE                                                    
028200         WHEN SQLWARN0 NOT = SPACES                                       
028300         WHEN SQLCODE < ZERO                                              
028400         WHEN SQLCODE > ZERO                                              
028500             MOVE '7000-OPEN-SMTH-PRC-DATA-CSR    '                       
028600                                 TO PV-PARAGRAPH-NAME                     
028700             MOVE 'OPEN THE SMTH PRC DATA CSR     '                       
028800                                 TO PV-DB2-OPERATION-ATTEMPTED            
028900             PERFORM 9100-SQL-ABEND                                       
029200     END-EVALUATE.                                                        
029400                                                                          
030200                                                                          
030210*-----------------------------------------------------------------        
030220*  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.                 
030230*-----------------------------------------------------------------        
030300 7100-FETCH-SMTH-PRC-DATA-CSR.                                            
030500                                                                          
030700     EXEC SQL                                                             
030800          FETCH SMTH_PRC_DATA_CSR                                         
031000           INTO :SUT00003-SMTHG-PRC-ID                                    
031100               ,:SUT00003-PO-NBR                                          
031200               ,:SUT00003-RCVR-SEQ-NBR                                    
031210               ,:SUT00003-CRE-USR-ID                                      
032300     END-EXEC.                                                            
032400                                                                          
032500     EVALUATE TRUE                                                        
032600         WHEN SQLCODE = ZERO                                              
032700             CONTINUE                                                     
032800         WHEN SQLCODE = +100                                              
032900             SET PS-EOF-SMTHG-DATA-CSR  TO  TRUE                          
033000                                                                          
033100         WHEN SQLWARN0 NOT = SPACES                                       
033200         WHEN SQLCODE < ZERO                                              
033300         WHEN SQLCODE > ZERO                                              
033400             MOVE '7100-FETCH-SMTH-PRC-DATA-CSR  '                        
033500                                 TO PV-PARAGRAPH-NAME                     
033600             MOVE 'FETCH THE SMTH-PRC-DATA-CSR    '                       
033700                                 TO PV-DB2-OPERATION-ATTEMPTED            
033800             PERFORM 9100-SQL-ABEND                                       
033900     END-EVALUATE.                                                        
034000                                                                          
034120                                                                          
034121*-----------------------------------------------------------------        
034122*  WRITE THE SHIPMENT-UNIT-RECORD                                         
034123*-----------------------------------------------------------------        
034130 8000-WRITE-SMTH-PRC-DATA-REC.                                            
034140                                                                          
034150     INITIALIZE SMTH-PRC-DATA-RECORD.                                     
034160     MOVE SUT00003-SMTHG-PRC-ID   TO SPD-SMTHG-PRC-ID.                    
034170     MOVE SUT00003-PO-NBR         TO SPD-PO-NBR.                          
034180     MOVE SUT00003-RCVR-SEQ-NBR   TO SPD-RCVR-SEQ-NBR.                    
034190     MOVE SUT00003-CRE-USR-ID     TO SPD-CRE-USR-ID.                      
034300                                                                          
034600     WRITE SMTH-PRC-DATA-RECORD.                                          
034800     ADD +1 TO PV-WRITE-COUNTER.                                          
035690                                                                          
035700                                                                          
035710*-----------------------------------------------------------------        
035711*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
035720*-----------------------------------------------------------------        
035800 9000-EOJ-ROUTINE.                                                        
036200     CLOSE SUT-SMOOTH-PRC-EXTRACT.                                        
036400                                                                          
036500     MOVE PV-WRITE-COUNTER TO PV-DISPLAY-COUNT.                           
036600     DISPLAY 'TOTAL SMOOTH BATCH RECS WRITTEN ' PV-DISPLAY-COUNT.         
036700                                                                          
037400     EXEC SQL                                                             
037500          CLOSE SMTH_PRC_DATA_CSR                                         
037600     END-EXEC.                                                            
037700                                                                          
037800     EVALUATE TRUE                                                        
037810         WHEN SQLCODE = ZERO                                              
037820              CONTINUE                                                    
037900         WHEN SQLCODE = -904                                              
038000         WHEN SQLCODE = -911                                              
038100              CONTINUE                                                    
038200         WHEN SQLWARN0 NOT = SPACES                                       
038300         WHEN SQLCODE < ZERO                                              
038400         WHEN SQLCODE > ZERO                                              
038500              MOVE '9000-EOJ-ROUTINE               '                      
038600                                 TO PV-PARAGRAPH-NAME                     
038700              MOVE 'CLOSE THE SMTH_PRC_DATA_CSR     '                     
038800                                 TO PV-DB2-OPERATION-ATTEMPTED            
038900              PERFORM 9100-SQL-ABEND                                      
039200     END-EVALUATE.                                                        
039400                                                                          
040200                                                                          
040210*-----------------------------------------------------------------        
040220*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
040230*  ERROR OR WARNING CODE OCCURS.                                          
040240*-----------------------------------------------------------------        
040300 9100-SQL-ABEND.                                                          
040800     DISPLAY '9100-SQL-ABEND'.                                            
040900     DISPLAY '** ABEND     **'.                                           
041000     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
041100     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
041200     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
041300                                                                          
041400******************************************************************        
041500* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
041600* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
041700* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
041800* FORMAT.                                                                 
041900******************************************************************        
042000     COPY DPPD004.                                                        
042100                                                                          
042200     MOVE +4013                  TO ABEND-CODE.                           
042300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
